00001 *                                                                 12/12/95
00002  IDENTIFICATION DIVISION.                                         DPKUT010
00003 *                                                                    LV010
00004  PROGRAM-ID.    DPKUT010.                                         DPKUT010
00005 *                                                                 DPKUT010
00006  AUTHOR.        CAL BUSH.                                         DPKUT010
00007  INSTALLATION.  KOHLS DEPARTMENT STORES.                          DPKUT010
00008  DATE-WRITTEN.  09/18/95.                                         DPKUT010
00009  DATE-COMPILED.                                                   DPKUT010
00010 *                                                                 DPKUT010
00011 ***************************************************************** DPKUT010
00012 **                                                             ** DPKUT010
00013 ** THIS PROGRAM FORMATS A MESSAGE INTO PROFS FORMAT FOR THE    ** DPKUT010
00014 ** MAINFRAME E-MAIL SYSTEM.                                    ** DPKUT010
00015 **----------------------------CHANGES--------------------------**    CL**4
00016 ** 12/12/95  BY CAL BUSH                                       **    CL**4
00017 **   MODIFY OUTPUT TO BE IN 'SYSMBTCH' FORMAT BECAUSE OUTPUT   **    CL**4
00018 **   WILL BE FED INTO 'SYSMBTCH' INSTEAD OF SYSOUT CLASS 'W'.  **    CL**4
00019 ***************************************************************** DPKUT010
00020      EJECT                                                        DPKUT010
00021  ENVIRONMENT DIVISION.                                            DPKUT010
00022 *                                                                 DPKUT010
00023  CONFIGURATION SECTION.                                           DPKUT010
00024 *                                                                 DPKUT010
00025  SOURCE-COMPUTER.  IBM-3090.                                      DPKUT010
00026  OBJECT-COMPUTER.  IBM-3090.                                      DPKUT010
00027 *                                                                 DPKUT010
00028  INPUT-OUTPUT SECTION.                                            DPKUT010
00029 *                                                                 DPKUT010
00030  FILE-CONTROL.                                                    DPKUT010
00031      SELECT INPUT-EMAIL-FILE          ASSIGN TO INP01.            DPKUT010
00032      SELECT OUTPUT-EMAIL-FILE         ASSIGN TO OUT01.            DPKUT010
00033      EJECT                                                        DPKUT010
00034  DATA DIVISION.                                                   DPKUT010
00035 *                                                                 DPKUT010
00036  FILE SECTION.                                                    DPKUT010
00037 *                                                                 DPKUT010
00038  FD  INPUT-EMAIL-FILE                                             DPKUT010
00039      RECORDING MODE IS F                                          DPKUT010
00040      LABEL RECORDS ARE STANDARD                                   DPKUT010
00041      RECORD CONTAINS 80 CHARACTERS.                               DPKUT010
00042  01  INPUT-EMAIL-RECORD            PIC  X(80).                    DPKUT010
00043 *                                                                    CL**3
00044  FD  OUTPUT-EMAIL-FILE                                            DPKUT010
00045      RECORDING MODE IS F                                          DPKUT010
00046      LABEL RECORDS ARE STANDARD                                   DPKUT010
00047      RECORD CONTAINS 80 CHARACTERS.                               DPKUT010
00048  01  OUTPUT-EMAIL-RECORD           PIC  X(80).                    DPKUT010
00049 *                                                                 DPKUT010
00050      EJECT                                                        DPKUT010
00051  WORKING-STORAGE SECTION.                                         DPKUT010
00052 *                                                                 DPKUT010
00053  01  PC-PROGRAM-CONSTANTS.                                        DPKUT010
00054      05  PC-MESSAGE-SUBJECT        PIC  X(17)  VALUE              DPKUT010
00055                                    'MESSAGE FROM UNIX'.           DPKUT010
00056 *                                                                    CL**6
00057  01  PV-PROGRAM-VARIABLES.                                           CL**6
00058      05  PV-UNSTRING-COUNT         PIC S9(04)  VALUE ZERO            CL**6
00059                                                COMP.                 CL**6
00060 *                                                                 DPKUT010
00061  01  PS-PROGRAM-SWITCHES.                                         DPKUT010
00062      05  PS-EOF-SW                 PIC  X(01)  VALUE 'N'.            CL**6
00063          88  PS-EOF                            VALUE 'Y'.            CL**6
00064      05  PS-EOF2-SW                PIC  X(01)  VALUE 'N'.            CL**4
00065          88  PS-EOF2                           VALUE 'Y'.            CL**4
00066 ****************************************************************     CL**3
00067 ** THIS IS THE RECORD LAYOUT FOR THE INPUT AND OUTPUT RECORDS **     CL**3
00068 ** USED IN THIS PROGRAM.                                      **     CL**3
00069 ****************************************************************     CL**3
00070  01  ER-OUTPUT-EMAIL-RECORD.                                         CL**3
00071      05  ER1-OUTPUT-HEADER.                                          CL**3
00072          10  FILLER                PIC  X(11)  VALUE                 CL**3
00073                                                'MAIL FROM: '.        CL**2
00074          10  ER1-FROM-USER         PIC  X(08)  VALUE SPACE.       DPKUT010
00075          10  FILLER                PIC  X(61)  VALUE SPACE.          CL**2
00076      05  ER2-OUTPUT-SUBJECT.                                         CL**2
00077          10  FILLER                PIC  X(06)  VALUE                 CL**2
00078                                                'DESC: '.             CL**2
00079          10  ER2-SUBJECT           PIC  X(30)  VALUE SPACE.          CL**2
00080          10  FILLER                PIC  X(08)  VALUE SPACE.          CL**2
00081      05  ER3-OUTPUT-MAILLIST.                                        CL**2
00082          10  FILLER                PIC  X(08)  VALUE                 CL**2
00083                                                'TOLIST: '.           CL**2
00084          10  ER3-MAILLIST-ID       PIC  X(20)  VALUE SPACE.          CL**2
00085          10  FILLER                PIC  X(52)  VALUE SPACE.          CL**2
00086      05  ER4-OUTPUT-EMAIL-DATA-HDR.                                  CL**2
00087          10  FILLER                PIC  X(05)  VALUE 'DATA:'.        CL**2
00088          10  FILLER                PIC  X(75)  VALUE SPACE.          CL**2
00089      05  ER5-OUTPUT-EMAIL-5.                                         CL**4
00090          10  ER5-MESSAGE-TEXT      PIC  X(80)  VALUE SPACE.          CL**4
00091      05  ER6-OUTPUT-TRAILER.                                         CL*10
00092          10  FILLER                PIC  X(01)  VALUE '.'.            CL*10
00093          10  FILLER                PIC  X(79)  VALUE SPACE.          CL*10
00094 *                                                                 DPKUT010
00095 ****************************************************************  DPKUT010
00096 ** THIS IS THE RECORD LAYOUT FOR THE INPUT RECORD USED BY THIS**  DPKUT010
00097 ** PROGRAM.                                                   **  DPKUT010
00098 ****************************************************************  DPKUT010
00099  01  IER-INPUT-EMAIL-RECORD.                                      DPKUT010
00100      05  IER-INPUT-MESSAGE-TXT     PIC  X(80)  VALUE SPACE.          CL**6
00101 *                                                                 DPKUT010
00102  01  ABEND-CODE                    PIC S9(04)  VALUE ZERO         DPKUT010
00103                                                COMP SYNC.         DPKUT010
00104      88  AC-INVALID-PARAMETER                  VALUE +4003.       DPKUT010
00105      88  AC-INVALID-DATA                       VALUE +4008.       DPKUT010
00106 *                                                                 DPKUT010
00107  EJECT                                                            DPKUT010
00108  LINKAGE SECTION.                                                 DPKUT010
00109  01  LS-LINKAGE-SECTION-PARAMETERS.                               DPKUT010
00110      05  LS-PARAMETER-LENGTH     PIC S9(4)                        DPKUT010
00111                                  COMP SYNC.                       DPKUT010
00112      05  LS-PARAMETER-DISPLAY.                                    DPKUT010
00113          10  LS-UNIX-PLATFORM    PIC  X(10).                      DPKUT010
00114                                                                   DPKUT010
00115  EJECT                                                            DPKUT010
00116  PROCEDURE DIVISION USING LS-LINKAGE-SECTION-PARAMETERS.          DPKUT010
00117 ***************************************************************** DPKUT010
00118 **  THIS MODULE CONTROLS ALL PROCESSING DONE IN THIS PROGRAM.  ** DPKUT010
00119 ***************************************************************** DPKUT010
00120  0000-MAIN-DRIVER.                                                DPKUT010
00121      PERFORM 1000-INITIALIZATION.                                 DPKUT010
00122      PERFORM 2000-PROCESS-INPUT                                   DPKUT010
00123          UNTIL PS-EOF.                                               CL**6
00124      PERFORM 3000-TERMINATION.                                    DPKUT010
00125                                                                   DPKUT010
00126      GOBACK.                                                      DPKUT010
00127 ****************************************************************  DPKUT010
00128 ** OPEN FILES AND DO THE PRIMING READ.                        **  DPKUT010
00129 ****************************************************************  DPKUT010
00130  1000-INITIALIZATION.                                             DPKUT010
00131      IF  LS-PARAMETER-LENGTH = ZERO                               DPKUT010
00132          DISPLAY '** PROGRAM:  DPKUT010'                          DPKUT010
00133          DISPLAY '** PARAGRAPH: 1000-INITIALIZATION'              DPKUT010
00134          DISPLAY '** PLATFORM NAME MUST BE PASSED AS A PARM'      DPKUT010
00135          SET AC-INVALID-PARAMETER TO TRUE                         DPKUT010
00136          PERFORM 9000-ABEND                                       DPKUT010
00137      ELSE                                                         DPKUT010
00138          MOVE LS-UNIX-PLATFORM      TO ER1-FROM-USER              DPKUT010
00139      END-IF.                                                      DPKUT010
00140 *                                                                 DPKUT010
00141      OPEN INPUT  INPUT-EMAIL-FILE                                 DPKUT010
00142           OUTPUT OUTPUT-EMAIL-FILE.                                  CL**4
00143 *                                                                    CL**4
00144      PERFORM 4100-READ-INPUT-MESSAGE.                             DPKUT010
00145 *                                                                 DPKUT010
00146      IF  PS-EOF                                                      CL**6
00147          DISPLAY '** ABEND **'                                    DPKUT010
00148          DISPLAY '** PROGRAM:  DPKUT010'                          DPKUT010
00149          DISPLAY '** PARAGRAPH: 1000-INITIALIZATION'              DPKUT010
00150          DISPLAY '** INPUT DATASET IS EMPTY'                      DPKUT010
00151          SET AC-INVALID-DATA TO TRUE                              DPKUT010
00152          PERFORM 9000-ABEND                                       DPKUT010
00153      ELSE                                                         DPKUT010
00154          PERFORM 1100-PARSE-MESSAGE-TEXT                             CL**6
00155          PERFORM 1200-CREATE-MAIL-HEADERS                            CL**4
00156          PERFORM 4100-READ-INPUT-MESSAGE                             CL**7
00157      END-IF.                                                      DPKUT010
00158 ****************************************************************     CL**4
00159 ** READ THE MAILLIST ID FROM THE INPUT FILE.  ABEND IF THE    **     CL**4
00160 ** INPUT FILE IS EMPTY.                                       **     CL**4
00161 ****************************************************************     CL**4
00162  1100-PARSE-MESSAGE-TEXT.                                            CL**6
00163      UNSTRING IER-INPUT-MESSAGE-TXT                                  CL**6
00164          DELIMITED BY SPACE                                          CL**6
00165          INTO ER3-MAILLIST-ID                                        CL**6
00166          COUNT IN PV-UNSTRING-COUNT                                  CL**6
00167      END-UNSTRING.                                                   CL**6
00168 *                                                                    CL**6
00169      MOVE IER-INPUT-MESSAGE-TXT(PV-UNSTRING-COUNT + 1:)              CL**6
00170                                 TO ER5-MESSAGE-TEXT.                 CL**7
00171 ****************************************************************     CL**4
00172 ** CREATE THE NON-REPEATING LINES REQUIRED BY ALL MAIL        **     CL**4
00173 ** MESSAGES.                                                  **     CL**4
00174 ****************************************************************     CL**4
00175  1200-CREATE-MAIL-HEADERS.                                           CL**4
00176      MOVE ER1-OUTPUT-HEADER     TO OUTPUT-EMAIL-RECORD.              CL**4
00177      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**4
00178 *                                                                    CL**4
00179      MOVE PC-MESSAGE-SUBJECT    TO ER2-SUBJECT.                      CL**4
00180      MOVE ER2-OUTPUT-SUBJECT    TO OUTPUT-EMAIL-RECORD.              CL**4
00181      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**4
00182 *                                                                    CL**4
00183      MOVE ER3-OUTPUT-MAILLIST   TO OUTPUT-EMAIL-RECORD.              CL**4
00184      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**4
00185 *                                                                    CL**4
00186      MOVE ER4-OUTPUT-EMAIL-DATA-HDR                                  CL**4
00187                                 TO OUTPUT-EMAIL-RECORD.              CL**4
00188      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**4
00189 *                                                                    CL**8
00190      MOVE ER5-OUTPUT-EMAIL-5    TO OUTPUT-EMAIL-RECORD.              CL**8
00191      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**8
00192 *                                                                    CL**4
00193 ****************************************************************  DPKUT010
00194 **  FOR EACH LINE IN THE INPUT EMAIL TEXT FILE, GENERATE      **     CL**4
00195 **  AN OUTPUT TEXT RECORD.                                    **     CL**4
00196 ****************************************************************  DPKUT010
00197  2000-PROCESS-INPUT.                                              DPKUT010
00198      MOVE IER-INPUT-MESSAGE-TXT TO ER5-MESSAGE-TEXT.                 CL**4
00199      MOVE ER5-OUTPUT-EMAIL-5    TO OUTPUT-EMAIL-RECORD.              CL**4
00200      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL**3
00201                                                                   DPKUT010
00202      PERFORM 4100-READ-INPUT-MESSAGE.                             DPKUT010
00203                                                                   DPKUT010
00204 ****************************************************************  DPKUT010
00205 ** CLOSE FILES                                                **  DPKUT010
00206 ****************************************************************  DPKUT010
00207  3000-TERMINATION.                                                DPKUT010
00208                                                                   DPKUT010
00209      MOVE ER6-OUTPUT-TRAILER    TO OUTPUT-EMAIL-RECORD.              CL*10
00210      PERFORM 4300-WRITE-OUTPUT-LABEL.                                CL*10
00211 *                                                                    CL*10
00212      CLOSE INPUT-EMAIL-FILE                                       DPKUT010
00213            OUTPUT-EMAIL-FILE.                                     DPKUT010
00214 ****************************************************************  DPKUT010
00215 ** READ A RECORD FROM THE INPUT FILE.                         **  DPKUT010
00216 ****************************************************************  DPKUT010
00217  4100-READ-INPUT-MESSAGE.                                         DPKUT010
00218      READ INPUT-EMAIL-FILE INTO IER-INPUT-EMAIL-RECORD            DPKUT010
00219          AT END                                                   DPKUT010
00220              SET PS-EOF TO TRUE.                                     CL**6
00221 ****************************************************************  DPKUT010
00222 ** WRITE THE RECORD TO THE OUTPUT FILE.                       **  DPKUT010
00223 ****************************************************************  DPKUT010
00224  4300-WRITE-OUTPUT-LABEL.                                            CL**3
00225      WRITE OUTPUT-EMAIL-RECORD.                                   DPKUT010
00226 ****************************************************************  DPKUT010
00227 ** ABEND THE PROGRAM.                                         **  DPKUT010
00228 ****************************************************************  DPKUT010
00229  9000-ABEND.                                                      DPKUT010
00230      CALL 'ILBOABN0' USING ABEND-CODE.                            DPKUT010
