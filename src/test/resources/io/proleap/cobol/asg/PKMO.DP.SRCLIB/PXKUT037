       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    PXKUT037.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  04/02/2007.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **PX MOS ONHAND RETRIEVAL                                         00080000
      ******************************************************************00090000
      **THIS PROGRAM WILL READ IN AN MOS ITEM FILE AND LOOKUP THE       00100000
      **     ONHAND FOR THE ITEMS AT THE STORES.                        00110000
      ******************************************************************00120000
      **DATE:04/02/2007 MADE BY:DAVID WELLER INITIAL PROGRAM WRITE      00130000
      ******************************************************************00140000
       ENVIRONMENT DIVISION.                                            00150000
       CONFIGURATION SECTION.                                           00160000
       SOURCE-COMPUTER.    IBM-3090.                                    00170000
       OBJECT-COMPUTER.    IBM-3090.                                    00180000
       INPUT-OUTPUT SECTION.                                            00190000
       FILE-CONTROL.                                                    00200000
                                                                        00210000
           SELECT INPUT-FILE                                            00220000
                  ASSIGN TO INP01.                                      00230000
           SELECT OUTPUT-FILE                                           00240000
                  ASSIGN TO OUT01.                                      00250000
                                                                        00260000
       DATA DIVISION.                                                   00270000
       FILE SECTION.                                                    00280000
                                                                        00290000
       FD  INPUT-FILE                                                   00300000
           RECORDING MODE IS F                                          00310000
           LABEL RECORDS OMITTED                                        00320000
           BLOCK CONTAINS 0 RECORDS                                     00330000
           DATA RECORD IS INPUT-RECORD.                                 00340000
       01  INPUT-RECORD                        PIC X(031).              00350000
                                                                        00360000
       FD  OUTPUT-FILE                                                  00370000
           RECORDING MODE IS F                                          00380000
           LABEL RECORDS OMITTED                                        00390000
           BLOCK CONTAINS 0 RECORDS                                     00400000
           DATA RECORD IS OUTPUT-RECORD.                                00410000
       01  OUTPUT-RECORD                       PIC X(213).              00420000
                                                                        00430000
       WORKING-STORAGE SECTION.                                         00440000
      ******************************************************************00450000
      **PROGRAM SWITCHES                                                00460000
      ******************************************************************00470000
       01  PS-SWITCH-AREAS.                                             00480000
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)  VALUE 'N'.    00490000
               88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.    00500000
               88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.    00510000
      ******************************************************************00520000
      **DATE TIME FIELDS                                                00530000
      ******************************************************************00540000
       01 DF-DATE-TIME-FIELDS.                                          00550000
          05 DF-ACCEPT-DATE                    PIC X(06) VALUE SPACES.  00560000
          05 DF-ACCEPT-TIME                    PIC X(08) VALUE SPACES.  00570000
      ******************************************************************00580000
      **PROGRAM COUNTERS                                                00590000
      ******************************************************************00600000
       01  PC-PROGRAM-COUNTERS.                                         00610000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROES.  00620000
           05  PC-OUTPUT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROES.  00630000
      ****************************************************************  00640000
      **ABEND INFORMATION                                               00650000
      ****************************************************************  00660000
       01  AC-ABEND-FIELDS.                                             00670000
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00680000
                                                           COMP SYNC.   00690000
               88  AC-TABLE-ERROR                        VALUE +4004.   00700000
               88  AC-DB2-ERROR                          VALUE +4013.   00710000
       01  PV-MISC-ABEND-FIELDS.                                        00720000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00730000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00740000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00750000
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00760000
      ******************************************************************00770000
      **COPYBOOKS                                                       00780000
      ******************************************************************00790000
      ****INPUT RECORD LAYOUT                                           00800000
           COPY PXRD046.                                                00810000
      ****OUTPUT RECORD LAYOUT                                          00820000
           COPY PXRD040.                                                00830000
      ****ABEND ROUTINE INFORMATION                                     00840000
           COPY SQLCA2.                                                 00850000
      ****ABEND ROUTINE INFORMATION                                     00860000
           COPY DPWS004.                                                00870000
      ******************************************************************00880000
      **TABLE INCLUSIONS                                                00890000
      ******************************************************************00900000
      ***ONHAND TABLE                                                   00910000
           EXEC SQL                                                     00920000
                INCLUDE TSKST                                           00930000
           END-EXEC.                                                    00940000
      ***ITEM CROSS REFERENCE TABLE                                     00950000
           EXEC SQL                                                     00960000
                INCLUDE TSKXREF                                         00970000
           END-EXEC.                                                    00980000
      *****DB2 TABLE INCLUSION                                          00990000
           EXEC SQL                                                     01000000
                INCLUDE SQLCA                                           01010000
           END-EXEC.                                                    01020000
                                                                        01030000
       PROCEDURE DIVISION.                                              01040000
      ******************************************************************01050000
       000-MAINLINE-PROCESSING.                                         01060000
                                                                        01070000
           PERFORM 1000-INITIALIZATION.                                 01080000
           PERFORM 2000-PROCESS-RECORDS                                 01090000
                   UNTIL PS-END-OF-INPUT-FILE.                          01100000
           PERFORM 9999-WRAPUP.                                         01110000
           GOBACK.                                                      01120000
                                                                        01130000
       EJECT.                                                           01140000
      ******************************************************************01150000
      **INITIALIZATION ROUTINE                                          01160000
      ******************************************************************01170000
       1000-INITIALIZATION.                                             01180000
                                                                        01190000
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             01200000
                                                                        01210000
           OPEN INPUT  INPUT-FILE                                       01220000
                OUTPUT OUTPUT-FILE.                                     01230000
                                                                        01240000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             01250000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             01260000
                                                                        01270000
           PERFORM 9999-READ-INPUT-RECORD.                              01280000
                                                                        01290000
       EJECT.                                                           01300000
      ******************************************************************01310000
      **PROCESS INPUT RECORD TO CHECK ROWS                              01320000
      ******************************************************************01330000
       2000-PROCESS-RECORDS.                                            01340000
                                                                        01350000
           MOVE '2000-PROCESS-RECORDS'   TO PV-PARAGRAPH-NAME.          01360000
                                                                        01370000
           PERFORM 2100-DATA-LOOKUP.                                    01380000
                                                                        01390000
           MOVE ZEROES                 TO PXRD040-PRCHG-ID.             01400000
           MOVE ZEROES                 TO PXRD040-STR-GP-TYP-CDE.       01410000
           MOVE ZEROES                 TO PXRD040-STR-GP-ID.            01410100
           MOVE PXRD046-EFF-DATE       TO PXRD040-EFF-DATE.             01410200
           MOVE PXRD046-STR-NBR        TO PXRD040-STR-NBR.              01410300
           MOVE SPACES                 TO PXRD040-LOC-NM.               01410400
           MOVE PXRD046-SKU-NBR        TO PXRD040-SKU-NBR.              01410500
           MOVE PXRD046-UNT-RTL-AMT    TO PXRD040-UNT-RTL-AMT.          01410600
           IF PXRD046-GP-RTL-QTY-NULL-IND < 0                           01410700
               MOVE ZEROES             TO PXRD040-GP-RTL-QTY            01410800
           ELSE                                                         01410900
               MOVE PXRD046-GP-RTL-QTY                                  01411000
                                       TO PXRD040-GP-RTL-QTY            01412000
           END-IF.                                                      01413000
           MOVE PXRD046-GP-RTL-QTY-NULL-IND                             01414000
                                       TO PXRD040-GP-RTL-QTY-NULL-IND.  01415000
           IF PXRD046-GP-RTL-AMT-NULL-IND < 0                           01416000
               MOVE ZEROES             TO PXRD040-GP-RTL-AMT            01417000
           ELSE                                                         01418000
               MOVE PXRD046-GP-RTL-AMT TO PXRD040-GP-RTL-AMT            01419000
           END-IF.                                                      01419100
           MOVE PXRD046-GP-RTL-AMT-NULL-IND                             01419200
                                       TO PXRD040-GP-RTL-AMT-NULL-IND.  01419300
           MOVE ZEROES                 TO PXRD040-BUS-AREA-ID.          01419400
           MOVE SPACES                 TO PXRD040-BUS-AREA-DESC.        01419500
           MOVE ZEROES                 TO PXRD040-DEPT-NBR.             01419600
           MOVE SPACES                 TO PXRD040-VS-NBR.               01419700
           MOVE SPACES                 TO PXRD040-LBL-NM.               01419800
           MOVE SPACES                 TO PXRD040-VS-DESC.              01419900
           MOVE ZEROES                 TO PXRD040-STKR-NBR.             01420000
           MOVE ZEROES                 TO PXRD040-SUB-CL-NBR.           01421000
           MOVE ZEROES                 TO PXRD040-IN-TRNST-QTY.         01422000
           MOVE SPACES                 TO PXRD040-LAST-UPD-TMST.        01422100
           MOVE ZEROES                 TO PXRD040-LAST-NCLR-RTL-AMT.    01422200
           MOVE 'MOS'                  TO PXRD040-EVNT-CTG-CDE.         01422300
           MOVE 'MOS'                  TO PXRD040-PRCHG-TYP-CDE.        01422400
           MOVE TSKST-ONHAND-QTY       TO PXRD040-STR-EXPTED-QTY.       01422500
                                                                        01422600
           PERFORM 9999-WRITE-OUTPUT-RECORD.                            01422700
           PERFORM 9999-READ-INPUT-RECORD.                              01422800
                                                                        01422900
       EJECT.                                                           01423000
      ******************************************************************01424000
      **LOOKUP ONHAND QUANTITY FOR STORE                                01425000
      ******************************************************************01426000
       2100-DATA-LOOKUP.                                                01427000
                                                                        01428000
           MOVE '2100-DATA-LOOKUP'    TO PV-PARAGRAPH-NAME.             01429000
                                                                        01430000
           EXEC SQL                                                     01440000
                SELECT ONHAND_QTY                                       01450000
                INTO  :TSKST-ONHAND-QTY                                 01460000
                FROM   TSKST   A                                        01470000
                      ,TSKXREF B                                        01480000
                WHERE  A.ITEM_NMBR   = B.ITM_NBR                        01490000
                  AND  B.SKU         = :PXRD046-SKU-NBR                 01500000
                  AND  A.LOC_NBR     = :PXRD046-STR-NBR                 01510000
                WITH UR                                                 01520000
           END-EXEC.                                                    01530000
                                                                        01540000
           EVALUATE TRUE                                                01550000
              WHEN SQLCODE = +0                                         01560000
                   CONTINUE                                             01570000
              WHEN SQLCODE = +100                                       01580000
                   MOVE 0                TO TSKST-ONHAND-QTY            01590000
              WHEN OTHER                                                01600000
                   DISPLAY 'ERROR ON DATA LOOKUP....'                   01610000
                   DISPLAY 'SKU_NBR     ' SKXREF-SKU                    01620000
                   DISPLAY 'LOC-NBR     ' TSKST-LOC-NBR                 01630000
                   PERFORM 9999-DISPLAY-STATISTICS                      01640000
                   PERFORM 9999-SQL-ERROR                               01650000
           END-EVALUATE.                                                01660000
                                                                        01670000
       EJECT.                                                           01680000
      ******************************************************************01690000
      **READ INPUT RECORD                                               01700000
      ******************************************************************01710000
       9999-READ-INPUT-RECORD.                                          01720000
                                                                        01730000
           READ INPUT-FILE INTO PXRD046-EXTRACT-RECORD                  01740000
                AT END                                                  01750000
                SET PS-END-OF-INPUT-FILE TO TRUE                        01760000
           END-READ.                                                    01770000
                                                                        01780000
           IF  PS-NOT-END-OF-INPUT-FILE                                 01790000
               ADD +1                      TO PC-INPUT-RECORDS-READ     01800000
           END-IF.                                                      01810000
                                                                        01820000
       EJECT.                                                           01830000
      ****************************************************************  01840000
      **WRITE OUTPUT RECORD                                             01850000
      ****************************************************************  01860000
       9999-WRITE-OUTPUT-RECORD.                                        01870000
                                                                        01880000
           WRITE OUTPUT-RECORD FROM PXRD040-EXTRACT-RECORD.             01890000
                                                                        01900000
           ADD +1              TO PC-OUTPUT-RECORDS-WRITTEN.            01910000
                                                                        01920000
       EJECT.                                                           01930000
      ******************************************************************01940000
      **CLOSE INPUT FILES.                                              01950000
      ******************************************************************01960000
       9999-WRAPUP.                                                     01970000
                                                                        01980000
           CLOSE INPUT-FILE                                             01990000
                 OUTPUT-FILE.                                           02000000
                                                                        02010000
           PERFORM 9999-DISPLAY-STATISTICS.                             02020000
                                                                        02030000
       EJECT.                                                           02040000
      ******************************************************************02050000
      **DISPLAY PROGRAM STATS                                           02060000
      ******************************************************************02070000
       9999-DISPLAY-STATISTICS.                                         02080000
                                                                        02090000
           DISPLAY '*************************************************'. 02100000
           DISPLAY '* PROGRAM...........: PXKUT037'.                    02110000
           DISPLAY '* DATE..............: ' DF-ACCEPT-DATE.             02120000
           DISPLAY '* TIME..............: ' DF-ACCEPT-TIME.             02130000
           DISPLAY '* RECORDS READ......: ' PC-INPUT-RECORDS-READ.      02140000
           DISPLAY '* OUTPUT RECORDS WRITTEN...: '                      02150000
                                             PC-OUTPUT-RECORDS-WRITTEN. 02160000
           DISPLAY '*************************************************'. 02170000
                                                                        02180000
       EJECT.                                                           02190000
      ******************************************************************02200000
      * ABEND ROUTINE FOR BAD SQL CALLS                                *02210000
      ******************************************************************02220000
       9999-SQL-ERROR.                                                  02230000
                                                                        02240000
           DISPLAY '** ABEND     **'.                                   02250000
           DISPLAY '** PROGRAM   ** = PXKUT037'.                        02260000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02270000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02280000
      ***************************************************************** 02290000
      *    COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED  02300000
      *    TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.   02310000
      ***************************************************************** 02320000
           COPY DPPD004.                                                02330000
           SET AC-DB2-ERROR TO TRUE.                                    02340000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         02350000
                                                                        02360000
       EJECT.                                                           02370000
