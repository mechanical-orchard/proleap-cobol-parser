00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP009
00003  PROGRAM-ID.    SNKUP009.                                            LV005
00004                                                                   SNKUP009
00005  AUTHOR.        ANNE YEKO.                                        SNKUP009
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP009
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP009
00008  DATE-COMPILED.                                                   SNKUP009
00009                                                                   SNKUP009
00010 ******************************************************************SNKUP009
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP009
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP009
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASCRIT TABLE.          **SNKUP009
00014 ******************************************************************SNKUP009
00015 * CHANGED 04/25/2022  BY JEAN KURK                              **SNKUP009
00016 *   CHG0269122  CHANGE COMMIT FREQUENCY FROM 500 TO 250         **SNKUP009
00015 * CHANGED 05/04/2022  BY JEAN KURK                              **SNKUP009
00016 *   CHG0269578  CHANGE COMMIT FREQUENCY FROM 250 TO 50          **SNKUP009
00016 *               PER THE REQUEST OF THE DBA                      **SNKUP009
00015 * CHANGED 07/05/2022  BY JEAN KURK                              **SNKUP009
00016 *   CHG0271585  CHANGE COMMIT FREQUENCY FROM 50 TO 250          **SNKUP009
00016 *               PER THE REQUEST OF THE DBA                      **SNKUP009
00018 *                                                               **SNKUP009
COMMIT* CHANGED 08/11/2022  BY JIM HUNTER                             **SNKUP009
COMMIT*   CHG0273177  CHANGE COMMIT FREQUENCY FROM 250 TO 100         **SNKUP009
COMMIT*               PER THE REQUEST OF THE DBA                      **SNKUP009
00019 ******************************************************************SNKUP009
00020                                                                   SNKUP009
00021      EJECT                                                        SNKUP009
00022                                                                   SNKUP009
00023  ENVIRONMENT DIVISION.                                            SNKUP009
00024                                                                   SNKUP009
00025  INPUT-OUTPUT SECTION.                                            SNKUP009
00026                                                                   SNKUP009
00027  FILE-CONTROL.                                                    SNKUP009
00028                                                                   SNKUP009
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP009
00030             ASSIGN TO INP01.                                      SNKUP009
00031                                                                   SNKUP009
00032  DATA DIVISION.                                                   SNKUP009
00033                                                                   SNKUP009
00034  FILE SECTION.                                                    SNKUP009
00035                                                                   SNKUP009
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP009
00037      RECORDING MODE IS F                                          SNKUP009
00038      LABEL RECORDS ARE STANDARD                                   SNKUP009
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**5
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP009
00041                                                                   SNKUP009
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**5
00043                                                                   SNKUP009
00044      EJECT                                                        SNKUP009
00045  WORKING-STORAGE SECTION.                                         SNKUP009
00046                                                                   SNKUP009
00047  01  FILLER.                                                      SNKUP009
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP009
00049          ' ***** WORKING STORAGE PGM=SNKUP009 '.                  SNKUP009
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP009
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP009
00052          ' LOCATION='.                                            SNKUP009
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP009
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP009
00055          ' *****'.                                                SNKUP009
00056                                                                   SNKUP009
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP009
00058                                              COMP SYNC.           SNKUP009
00059                                                                   SNKUP009
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP009
00061                                                                   SNKUP009
00062      EJECT                                                        SNKUP009
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP009
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP009
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP009
00066                                                                   SNKUP009
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP009
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP009
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP009
00070                                                                   SNKUP009
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP009
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP009
00073                                              COMP-3.              SNKUP009
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP009
00075                                              COMP-3.              SNKUP009
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0                CL**2
00077                                              COMP-3.              SNKUP009
00078                                                                   SNKUP009
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP009
00080                                                                   SNKUP009
00081      COPY SNRD002I.                                               SNKUP009
00082      EJECT                                                        SNKUP009
00083 ******************************************************************SNKUP009
00084 *    DB2 DATABASE AREA                                            SNKUP009
00085 ******************************************************************SNKUP009
00086                                                                   SNKUP009
00087 *    ASN CARTON TO ITEM CROSS REFERENCE TABLE                     SNKUP009
00088                                                                   SNKUP009
00089      EXEC SQL                                                     SNKUP009
00090          INCLUDE TASCRIT                                          SNKUP009
00091      END-EXEC.                                                    SNKUP009
00092                                                                   SNKUP009
00093      EJECT                                                        SNKUP009
00094 *    SQL COMMUNICATION AREA                                       SNKUP009
00095                                                                   SNKUP009
00096      EXEC SQL                                                     SNKUP009
00097          INCLUDE SQLCA                                            SNKUP009
00098      END-EXEC.                                                    SNKUP009
00099      EJECT                                                        SNKUP009
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP009
00101                                                                   SNKUP009
00102      COPY SQLCA2.                                                 SNKUP009
00103      EJECT                                                        SNKUP009
00104                                                                   SNKUP009
00105 ***************************************************************** SNKUP009
00106  PROCEDURE DIVISION.                                              SNKUP009
00107 ***************************************************************** SNKUP009
00108                                                                   SNKUP009
00109  A000-MAIN-DRIVER.                                                SNKUP009
00110                                                                   SNKUP009
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP009
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP009
00113                                                                   SNKUP009
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP009
00115                                                                   SNKUP009
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP009
00117                                                                   SNKUP009
00118      PERFORM B100-DELETE-ROWS                                     SNKUP009
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP009
00120                                                                   SNKUP009
00121      DISPLAY 'SNKUP009 RECORDS READ:    ' PV-RECS-READ.           SNKUP009
00122      DISPLAY 'SNKUP009 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP009
00123                                                                   SNKUP009
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP009
00125                                                                   SNKUP009
00126      GOBACK.                                                      SNKUP009
00127                                                                   SNKUP009
00128      EJECT                                                        SNKUP009
00129                                                                   SNKUP009
00130 ***************************************************************** SNKUP009
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP009
00132 ***************************************************************** SNKUP009
00133  B100-DELETE-ROWS.                                                SNKUP009
00134                                                                   SNKUP009
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP009
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP009
00137                                  TO ABEND-PARAGRAPH.              SNKUP009
00138                                                                   SNKUP009
00139      PERFORM B300-DELETE-ASCRIT.                                  SNKUP009
00140***   IF PV-DELETE-COUNTER > +50                                      CL**4
00140***   IF PV-DELETE-COUNTER > +500                                     CL**4
COMMIT     IF PV-DELETE-COUNTER > +100                                     CL**4
00141          PERFORM B400-COMMIT                                      SNKUP009
00142          MOVE ZERO               TO PV-DELETE-COUNTER                CL**2
00143      END-IF.                                                      SNKUP009
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP009
00145                                                                   SNKUP009
00146      EJECT                                                        SNKUP009
00147                                                                   SNKUP009
00148 ***************************************************************** SNKUP009
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP009
00150 ***************************************************************** SNKUP009
00151  B200-READ-INPUT-FILE.                                            SNKUP009
00152                                                                   SNKUP009
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP009
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP009
00155                                  TO ABEND-PARAGRAPH.              SNKUP009
00156                                                                   SNKUP009
00157      READ ASNHDR-DELETE-FILE                                      SNKUP009
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP009
00159          AT END                                                   SNKUP009
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP009
00161                                                                   SNKUP009
00162      IF NOT PS-END-OF-INPUT                                       SNKUP009
00163          ADD 1                   TO PV-RECS-READ                  SNKUP009
00164      END-IF.                                                      SNKUP009
00165                                                                   SNKUP009
00166      EJECT                                                        SNKUP009
00167                                                                   SNKUP009
00168 ***************************************************************** SNKUP009
00169 *    THIS PARAGRAPH DELETES ALL ASCRIT ROWS THAT ARE EQUAL TO     SNKUP009
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP009
00171 ***************************************************************** SNKUP009
00172  B300-DELETE-ASCRIT.                                              SNKUP009
00173                                                                   SNKUP009
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP009
00175      MOVE 'B300-DELETE-ASCRIT        '                            SNKUP009
00176                                  TO ABEND-PARAGRAPH.              SNKUP009
00177                                                                   SNKUP009
00178      EXEC SQL                                                     SNKUP009
00179          DELETE                                                   SNKUP009
00180          FROM   TASCRIT                                           SNKUP009
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**5
00182      END-EXEC.                                                    SNKUP009
00183                                                                   SNKUP009
00184      IF SQLCODE = ZERO                                            SNKUP009
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP009
00186          ADD +1                  TO PV-DELETE-COUNTER                CL**3
00187      ELSE                                                         SNKUP009
00188          IF SQLCODE = +100                                        SNKUP009
00189              CONTINUE                                             SNKUP009
00190          ELSE                                                     SNKUP009
00191              PERFORM Z998-SQL-ERROR                               SNKUP009
00192          END-IF                                                   SNKUP009
00193      END-IF.                                                      SNKUP009
00194                                                                   SNKUP009
00195      EJECT                                                        SNKUP009
00196                                                                   SNKUP009
00197 ***************************************************************** SNKUP009
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP009
00199 ***************************************************************** SNKUP009
00200  B400-COMMIT.                                                     SNKUP009
00201                                                                   SNKUP009
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP009
00203      MOVE 'B400-COMMIT               '                            SNKUP009
00204                                  TO ABEND-PARAGRAPH.              SNKUP009
00205                                                                   SNKUP009
00206      EXEC SQL                                                     SNKUP009
00207          COMMIT                                                   SNKUP009
00208      END-EXEC.                                                    SNKUP009
00209                                                                   SNKUP009
00210      IF SQLCODE = ZERO                                            SNKUP009
00211          CONTINUE                                                 SNKUP009
00212      ELSE                                                         SNKUP009
00213          PERFORM Z998-SQL-ERROR                                   SNKUP009
00214      END-IF.                                                      SNKUP009
00215                                                                   SNKUP009
00216      EJECT                                                        SNKUP009
00217                                                                   SNKUP009
00218 ***************************************************************** SNKUP009
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP009
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP009
00221 ***************************************************************** SNKUP009
00222  Z998-SQL-ERROR.                                                  SNKUP009
00223                                                                   SNKUP009
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP009
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP009
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP009
00227      DISPLAY '** PROGRAM          ** = SNKUP009'.                 SNKUP009
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP009
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP009
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP009
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP009
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP009
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP009
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP009
