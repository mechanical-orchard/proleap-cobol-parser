       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              CWKUT032.                                       
       AUTHOR.                  MARIBETH ZAMORA                                 
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/01/08.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      * CWKUT032 - ADD ADD'L COLS FROM TUPCPLS TO RMS SKU COMMON EXTRACT        
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT SKU-FILE                                                      
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT SKU-EXPAND-FILE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
                                                                                
       FD   SKU-FILE                                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD                                          
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            DATA RECORD IS SKU-RECORD.                                          
                                                                                
       01   SKU-RECORD                        PIC X(500).                       
      *                                                                         
                                                                                
       FD   SKU-EXPAND-FILE                                                     
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD                                          
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 395 CHARACTERS                                      
            DATA RECORD IS SKU-EXPAND-RECORD.                                   
                                                                                
       01   SKU-EXPAND-RECORD                 PIC X(395).                       
      *                                                                         
      *                                                                         
       EJECT                                                                    
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW        PIC X(01)   VALUE SPACES.               
               88  PS-END-OF-FILE                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-FILE               VALUE 'N'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-NI-UNT-COST-AMT       PIC S9(04)  VALUE +0 COMP SYNC.         
           05  PV-NI-UNT-RTL-AMT        PIC S9(04)  VALUE +0 COMP SYNC.         
           05  PV-NI-MEITGP-NBR         PIC S9(04)  VALUE +0 COMP SYNC.         
           05  PV-NI-ORIG-CLR-DTE       PIC S9(04)  VALUE +0 COMP SYNC.         
           05  PV-NI-MKDN-CDE           PIC S9(04)  VALUE +0 COMP SYNC.         
           05  PV-SQL-NOT-FOUND         PIC S9(09)  VALUE +0 COMP.              
           05  PV-PARAGRAPH-NAME        PIC X(30) VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED    PIC X(30) VALUE SPACES.                 
           05  PV-TMP-UPC-ID            PIC 9(10) VALUE 0.                      
           05  PV-READ-CNT              PIC 9(12) VALUE 0.                      
           05  PV-WRITE-CNT             PIC 9(12) VALUE 0.                      
                                                                                
       01  ABEND-CODE                   PIC S9(04) VALUE +0 COMP SYNC.          
           88 ABEND-4013                                   VALUE +4013.         
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM                   PIC X(08)   VALUE                   
              'CWKUT032'.                                                       
                                                                                
      ****                                                                      
      *  INPUT SKU-RECORD                                                       
      ****                                                                      
           COPY IMRD053.                                                        
                                                                                
      ****                                                                      
      *  OUTPUT SKU-EXPAND-RECORD                                               
      ****                                                                      
           COPY CWRD032.                                                        
                                                                                
      ****                                                                      
      *  COPY BOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE                     
      ****                                                                      
                                                                                
           COPY DPWS004.                                                        
                                                                                
           COPY SQLCA2.                                                         
                                                                                
      ****                                                                      
      *  DB2 SQLCA                                                              
      ****                                                                      
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ****                                                                      
      *  TUPCPLS                                                                
      ****                                                                      
                                                                                
           EXEC SQL                                                             
              INCLUDE TUPCPLS                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * MAIN PROCESSING ROUTINE                                        *        
      ******************************************************************        
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 3000-MAIN-PROCESSING                                         
                              UNTIL PS-END-OF-FILE.                             
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  OPEN INPUT / OUTPUT FILES, INITIAL READ                       *        
      ******************************************************************        
      *                                                                         
       1000-INITIALIZATION.                                                     
                                                                                
           SET PS-NOT-END-OF-FILE TO TRUE.                                      
                                                                                
           OPEN INPUT  SKU-FILE                                                 
                OUTPUT SKU-EXPAND-FILE.                                         
                                                                                
           PERFORM 7000-READ-SKU-FILE.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING ROUTINE                                        *        
      ******************************************************************        
       3000-MAIN-PROCESSING.                                                    
                                                                                
           MOVE IM053-INTR-UPC-ID TO UPCPLS-UPC-ID.                             
                                                                                
           PERFORM 4000-GET-CORP-RTL-AMT.                                       
                                                                                
           PERFORM 5000-FORMAT-OUTPUT.                                          
           PERFORM 6000-WRITE-SKU-EXPAND-RECORD.                                
                                                                                
           IF PS-NOT-END-OF-FILE                                                
              PERFORM 7000-READ-SKU-FILE                                        
           END-IF.                                                              
                                                                                
                                                                                
      ****************************************************************          
      * GET THE CORP UNIT RETAIL AMOUNT FROM TUPCPLS USNG THE        *          
      * INTERNAL UPC ID FROM THE SKU FILE                                       
      ****************************************************************          
       4000-GET-CORP-RTL-AMT.                                                   
                                                                                
           INITIALIZE UPCPLS-EFF-BEG-TMST                                       
                      UPCPLS-UNT-COST-AMT                                       
                      UPCPLS-UNT-RTL-AMT                                        
                      UPCPLS-MEITGP-NBR                                         
                      UPCPLS-ORIG-CLR-DTE                                       
                      UPCPLS-MKDN-CDE                                           
                      PV-NI-UNT-COST-AMT                                        
                      PV-NI-UNT-RTL-AMT                                         
                      PV-NI-MEITGP-NBR                                          
                      PV-NI-ORIG-CLR-DTE                                        
                      PV-NI-MKDN-CDE.                                           
                                                                                
           EXEC SQL                                                             
                SELECT                                                          
                   EFF_BEG_TMST                                                 
                  ,UNT_COST_AMT                                                 
                  ,UNT_RTL_AMT                                                  
                  ,MEITGP_NBR                                                   
                  ,ORIG_CLR_DTE                                                 
                  ,MKDN_CDE                                                     
                INTO                                                            
                   :UPCPLS-EFF-BEG-TMST                                         
                  ,:UPCPLS-UNT-COST-AMT:PV-NI-UNT-COST-AMT                      
                  ,:UPCPLS-UNT-RTL-AMT:PV-NI-UNT-RTL-AMT                        
                  ,:UPCPLS-MEITGP-NBR:PV-NI-MEITGP-NBR                          
                  ,:UPCPLS-ORIG-CLR-DTE:PV-NI-ORIG-CLR-DTE                      
                  ,:UPCPLS-MKDN-CDE:PV-NI-MKDN-CDE                              
                  FROM   TUPCPLS                                                
                 WHERE   OPERCO_NBR  = '03'                                     
                   AND   STR_NBR     = 0                                        
                   AND   UPC_ID      = :UPCPLS-UPC-ID                           
                   AND   EFF_BEG_TMST IN                                        
                        (SELECT MAX(EFF_BEG_TMST)                               
                         FROM TUPCPLS                                           
                         WHERE OPERCO_NBR  = '03'                               
                           AND STR_NBR     = 0                                  
                           AND UPC_ID      = :UPCPLS-UPC-ID                     
                           AND (EFF_END_TMST > CURRENT TIMESTAMP OR             
                                EFF_END_TMST IS NULL))                          
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = +0                                                 
                   CONTINUE                                                     
              WHEN SQLCODE = +100                                               
                   DISPLAY 'TUPCPLS ERROR. SQLCODE=' SQLCODE                    
                   DISPLAY 'UPC-ID= '       IM053-INTR-UPC-ID                   
                   DISPLAY 'DEFAULT VALUES WILL BE USED'                        
                   DISPLAY ' '                                                  
                   ADD 1                       TO PV-SQL-NOT-FOUND              
              WHEN OTHER                                                        
                   MOVE 'SELECT FROM TUPCPLS ' TO PV-DB2-OPER-ATTEMPTED         
                   DISPLAY 'INTERNAL UPC ID = ' IM053-INTR-UPC-ID               
                   PERFORM 9999-SQL-ERROR                                       
               END-EVALUATE.                                                    
                                                                                
                                                                                
      ****************************************************************          
      * FORMAT OUTPUT RECORD BEFORE WRITING                          *          
      ****************************************************************          
       5000-FORMAT-OUTPUT.                                                      
                                                                                
           INITIALIZE CW032-RECORD.                                             
                                                                                
           MOVE IM053-INTR-UPC-ID        TO CW032-INTR-UPC-ID                   
           MOVE IM053-RMS-STYL-ID        TO CW032-RMS-STYL-ID                   
           MOVE IM053-ITM-NBR            TO CW032-ITM-NBR                       
           MOVE IM053-SKU                TO CW032-SKU                           
           MOVE IM053-INTR-UPC-DESC      TO CW032-INTR-UPC-DESC                 
           MOVE IM053-SKU-STAT-CDE       TO CW032-SKU-STAT-CDE                  
           MOVE IM053-SKU-STAT-DESC      TO CW032-SKU-STAT-DESC                 
           MOVE IM053-INNR-PK-QTY        TO CW032-INNR-PK-QTY                   
           MOVE IM053-OUTR-PK-QTY        TO CW032-OUTR-PK-QTY                   
           MOVE IM053-KOHLS-SIZE-CDE     TO CW032-KOHLS-SIZE-CDE                
           MOVE IM053-KOHLS-SIZE-DESC    TO CW032-KOHLS-SIZE-DESC               
           MOVE IM053-NRF-CLOR-CDE       TO CW032-NRF-CLOR-CDE                  
           MOVE IM053-NRF-CLOR-DESC      TO CW032-NRF-CLOR-DESC                 
           MOVE IM053-SHRT-CLOR-DESC     TO CW032-SHRT-CLOR-DESC                
           MOVE IM053-CSTM-CLOR-DESC     TO CW032-CSTM-CLOR-DESC                
           MOVE IM053-CUSTFACG-CLOR-DESC TO CW032-CUSTFACG-CLOR-DESC            
           MOVE IM053-REGN-PRIC-IND      TO CW032-REGN-PRIC-IND                 
           MOVE IM053-RPLNMT-IND         TO CW032-RPLNMT-IND                    
           MOVE IM053-KY-ITM-IND         TO CW032-KY-ITM-IND                    
           MOVE IM053-POG-IND            TO CW032-POG-IND                       
           MOVE IM053-GP-CDE             TO CW032-GP-CDE                        
           MOVE IM053-SEA-DESC           TO CW032-SEA-DESC                      
           MOVE IM053-UNT-COST-1ST-AMT   TO CW032-UNT-COST-1ST-AMT              
           MOVE IM053-TX-PRD-CDE-DESC    TO CW032-TX-PRD-CDE-DESC               
           MOVE IM053-WT-UM-CDE          TO CW032-WT-UM-CDE                     
           MOVE IM053-WT-UM-QTY          TO CW032-WT-UM-QTY                     
           MOVE IM053-SIZE-UM-CDE        TO CW032-SIZE-UM-CDE                   
           MOVE IM053-WD-UM-QTY          TO CW032-WD-UM-QTY                     
           MOVE IM053-HT-UM-QTY          TO CW032-HT-UM-QTY                     
           MOVE IM053-DPTH-UM-QTY        TO CW032-DPTH-UM-QTY                   
           MOVE IM053-E-BUS-ITM-IND      TO CW032-E-BUS-ITM-IND                 
           MOVE IM053-ASRT-BEG-DTE       TO CW032-ASRT-BEG-DTE                  
           MOVE IM053-ASRT-END-DTE       TO CW032-ASRT-END-DTE                  
           MOVE IM053-STRG-AREA-DESC     TO CW032-STRG-AREA-DESC                
           MOVE IM053-SHIP-WD-QTY        TO CW032-SHIP-WD-QTY                   
           MOVE IM053-SHIP-DPTH-QTY      TO CW032-SHIP-DPTH-QTY                 
           MOVE IM053-SHIP-HT-QTY        TO CW032-SHIP-HT-QTY                   
           MOVE IM053-SHIP-WT-QTY        TO CW032-SHIP-WT-QTY                   
           MOVE IM053-SHIP-DIM-SRC-CDE   TO CW032-SHIP-DIM-SRC-CDE              
           MOVE IM053-PKG-DESC           TO CW032-PKG-DESC                      
           MOVE IM053-SHIP-RSTRC-DESC    TO CW032-SHIP-RSTRC-DESC               
           MOVE IM053-GFT-WRAP-AVL-IND   TO CW032-GFT-WRAP-AVL-IND              
           MOVE IM053-PRD-GP-NBR         TO CW032-PRD-GP-NBR                    
           MOVE IM053-PRD-GP-IND         TO CW032-PRD-GP-IND                    
           MOVE IM053-SHIP-SRCHG-FEE-AMT TO CW032-SHIP-SRCHG-FEE-AMT            
           MOVE IM053-SHIP-SRVC-DESC     TO CW032-SHIP-SRVC-DESC                
           MOVE IM053-SHIP-GP-DESC       TO CW032-SHIP-GP-DESC                  
           MOVE IM053-TRNSTN-IND         TO CW032-TRNSTN-IND                    
           MOVE IM053-WEB-EXCLV-IND      TO CW032-WEB-EXCLV-IND                 
           MOVE IM053-EXTD-SZ-CDE        TO CW032-EXTD-SZ-CDE                   
           MOVE IM053-PO-SHRT-CYC-IND    TO CW032-PO-SHRT-CYC-IND               
           MOVE IM053-EFF-BEG-DTE        TO CW032-EFF-BEG-DTE                   
                                                                                
           MOVE UPCPLS-UNT-RTL-AMT       TO CW032-CORP-RTL-AMT                  
           MOVE UPCPLS-UNT-COST-AMT      TO CW032-CORP-COST-AMT                 
           MOVE UPCPLS-MEITGP-NBR        TO CW032-CORP-GP-PRIC-ID.              
      *    MOVE UPCPLS-ORIG-CLR-DTE      TO CW032-ORIG-CLR-DTE                  
      *    MOVE UPCPLS-MKDN-CDE          TO CW032-MKDN-CDE.                     
                                                                                
      ****************************************************************          
      * WRITES THE SKU-EXPAND-RECORD                                 *          
      ****************************************************************          
       6000-WRITE-SKU-EXPAND-RECORD.                                            
                                                                                
           WRITE SKU-EXPAND-RECORD FROM CW032-RECORD.                           
           ADD +1 TO PV-WRITE-CNT.                                              
                                                                                
                                                                                
      *****************************************************************         
       7000-READ-SKU-FILE.                                                      
      *****************************************************************         
                                                                                
           READ SKU-FILE                                                        
               INTO  IM053-ITEM-SKU-DOMAIN-REC                                  
               AT END                                                           
                   SET PS-END-OF-FILE TO TRUE                                   
           END-READ.                                                            
                                                                                
           IF PS-NOT-END-OF-FILE                                                
              ADD +1 TO PV-READ-CNT                                             
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *   CLOSE FILES, DISPLAY COUNTERS                                *        
      ******************************************************************        
       9000-END-OF-JOB.                                                         
                                                                                
           CLOSE  SKU-FILE                                                      
                  SKU-EXPAND-FILE                                               
                                                                                
           DISPLAY ' TOTAL RECORDS READ        :' PV-READ-CNT.                  
           DISPLAY ' TOTAL RECORDS WRITTEN     :' PV-WRITE-CNT.                 
           DISPLAY ' TOTAL TUPCPLS NOT FOUND   :' PV-SQL-NOT-FOUND.             
                                                                                
                                                                                
       9999-SQL-ERROR.                                                          
      ******************************************************************        
      *    SQL ABEND ROUTINE                                                    
      ******************************************************************        
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  CWKUT032'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
