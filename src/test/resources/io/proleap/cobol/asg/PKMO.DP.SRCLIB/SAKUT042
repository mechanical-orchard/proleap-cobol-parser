      ****************************************************************  00010022
       IDENTIFICATION DIVISION.                                         00020022
      ****************************************************************  00030022
                                                                        00040022
       PROGRAM-ID.             SAKUT042.                                00050022
       AUTHOR.                 DAN PAYNTER.                             00060022
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070022
       DATE-WRITTEN            OCTOBER 2013.                            00080022
       DATE-COMPILED.                                                   00090022
                                                                        00100022
      ****************************************************************  00110022
      *                                                                 00120022
      *   ***  OFFLINE ASSOCIATE DISCOUNT UPDATE PROGRAM  ***           00130022
      *                                                                 00140022
      *  RELEASE 1 OF THE POINT OF COMMERCE PROJECT HAS NO IN-STORE     00150022
      *  SOLUTION FOR CUSTOMER MDM BEING OFFLINE.  THAT MEANS THAT IT'S 00160022
      *  POSSIBLE FOR AN ASSOCIATE CUSTOMER TO FAIL TO RECEIVE THEIR    00170022
      *  ASSOCIATE DISCOUNT AT THE REGISTER.  THE SALES HUB DOESN'T     00180022
      *  EXECUTE ANY BUSINESS LOGIC FOR THE POC XML MESSAGES, THEREFORE,00190022
      *  THIS BATCH PROGRAM WILL CATCH ANY MISSED ASSOCIATE DISCOUNTS   00200022
      *  FOR POC TRANSACTIONS AND WILL UPDATE THE APPROPRIATE TEPITM    00210022
      *  COLUMNS AS WELL AS INSERTING THE REQUIRED TEPEID ENTRIES.      00220022
      *                                                                 00230022
      *   INPUT: INP01 - CONTAINS THE DATE TO BE CHECKED FOR MISSED     00240022
      *                  POC ASSOCIATE DISCOUNTS                        00250022
      *                                                                 00260022
      ****************************************************************  00270022
      *                                                                 00280022
      * CHANGE LOG:                                                     00290022
      * DATE:  2013-10-15                                               00300022
      * PRGMR: DAN PAYNTER                                              00310022
      * WHY:   NEW PROGRAM TO CATCH MISSED POC ASSOCIATE DISCOUNTS      00320022
      ******************************************************************00330022
P8612 * CHANGE LOG: CHG0092405                                          00340022
P8612 * DATE:  2014-09-01                                               00350022
P8612 * PRGMR: COGNIZANT                                                00360022
P8612 * WHY:   TO EXCLUDE NEW MARKET PLACE DEPARTMENT 070.              00370022
072815******************************************************************00380022
072815* CHANGE LOG: CHG0117216                                          00390022
072815* DATE:  2015-07-28                                               00400022
072815* PRGMR: DAN PAYNTER                                              00410022
072815* WHY:   REMOVE REGISTER LIMITATIONS & ENSURE TNDR_ID POPULATED   00420022
      ****************************************************************  00430022
P1043 * CHANGE LOG: CHG0118696                                          00440022
P1043 * DATE      : 2015-07-08                                          00450022
P1043 * PRGMR     : ACCENTURE                                           00460022
P1043 * WHY       : CHANGED TRN-CURSOR TO HANDLE ONLY 12 DIGIT TENDER ID00470022
P1043 *             AND EXCLUDE 18 DIGIT TENDER ID, ASSUMING THERE WILL 00480022
P1043 *             BE NO MATCH ON ART_EMP_DISC TABLE FOR ANY 18 DIGIT  00490022
P1043 *             TENDERS.                                            00500022
P10237******************************************************************00510022
P10237* CHANGE LOG: CHG0104429                                          00520022
P10237* DATE:  2015-03-27                                               00530022
P10237* PRGMR: COGNIZANT                                                00540022
P10237* WHY:   MODIFIED TO SPLIT THE CURSOR THAT JOINS THE TENDER ID    00550022
P10237*        WITH ART EMPLOYEE TABLE AS TOKENIZED TENDER ID WILL NOT  00560022
P10237*        WORK WITH JOIN. SPLIT THE CURSOR INTO TWO CURSORS AND    00570022
P10237*        PASS DETOKENIZED TENDER TO CHECK AGAINST ART TABLE       00580022
P10237*        TNDR_ID > '0000000000' CHECK HAS BEEN REMOVED FROM THE   00590022
P10237*        CURSOR AS THE TABLE WILL HAVE TOKENS. CLEAR VALUE WITH   00600022
P10237*        ZEROES WILL BE SKIPPED IN THE PROGRAM LOGIC              00610022
P10237******************************************************************00620022
C25054* CHANGE LOG: CHG0125054                                          00630022
C25054* DATE:  2015-08-25                                               00640022
C25054* PRGMR: COGNIZANT                                                00650022
C25054* WHY:   MODIFIED TO REMOVE THE DE-TOKENIZATION LOGIC USED AS PART00660022
C25054*        OF RELEASE 1. ART_EMP_DISC TABLE IS ALSO PROTECTED AS    00670022
C25054*        PART OF THIS RELEASE 2 EMPLOYEE DISCOUNT CHANGES AND THE 00680022
C25054*        ACCT_NBR IN THIS TABLE WILL BE IN TOKENIZED FORM. THE    00690022
C25054*        TRN-CURSOR IS MODIFIED TO REMOVE THE RETRIEVAL OF TENDER 00700022
C25054*        ID AND TENDER ID LENGTH NUMBER AND COMPARE THE FIRST 10  00710022
C25054*        DIGITS OF TENDER ID FROM TEPTND WITH ACCOUNT NUMBER FROM 00720022
C25054*        ART_EMP_DISC USING CAST FUNCTION.                        00730022
C53040******************************************************************00731044
C53040* CHANGE LOG: CHG0153040                                          00732043
C53040* DATE:  2106-10-05                                               00733043
C53040* PRGMR: COGNIZANT                                                00734043
C53040* WHY:   TO REMOVE  TOKENIZATION LOGIC FOR HARD CODED TEST ACCOUNT00735043
C53040*        NUMBER AND GIVING THE TOKENIZED TEST ACCOUNT NUMBER.     00736043
C54997******************************************************************00737047
C54997* CHANGE LOG: CHG0154997                                          00738047
C54997* DATE:  2106-10-26                                               00739047
C54997* PRGMR: COGNIZANT                                                00739147
C54997* WHY:   TO REMOVE  TOKENIZATION LOGIC COPY BOOKS                 00739247
C54997******************************************************************00739348
SEPHOR* CHANGE LOG: CHG0268843                                          00739450
SEPHOR* DATE:  2022-04-06                                               00739548
SEPHOR* PRGMR: JIM HUNTER                                               00739648
SEPHOR* WHY:   EXCLUDE NEW SEPHORA GIFT CARD DEPARTMENT 712.            00739748
SEPHOR*        REPLACED OBSOLETE WAYFAIR DEPARTMENT 070 IN CURSOR.      00740148
      ****************************************************************  00741048
       ENVIRONMENT DIVISION.                                            00750022
      ****************************************************************  00760022
                                                                        00770022
       CONFIGURATION SECTION.                                           00780022
                                                                        00790022
       SOURCE-COMPUTER.        IBM-3090.                                00800022
                                                                        00810022
       OBJECT-COMPUTER.        IBM-3090.                                00820022
                                                                        00830022
       INPUT-OUTPUT SECTION.                                            00840022
                                                                        00850022
       FILE-CONTROL.                                                    00860022
                                                                        00870022
           SELECT DATE-CARD-FILE ASSIGN TO INP01.                       00880022
           SELECT TRAN-UPDT-FILE ASSIGN TO OUT01.                       00890022
                                                                        00900022
      ***************************************************************** 00910022
       DATA DIVISION.                                                   00920022
      ***************************************************************** 00930022
                                                                        00940022
       FILE SECTION.                                                    00950022
                                                                        00960022
       FD  DATE-CARD-FILE                                               00970022
           RECORDING MODE IS F                                          00980022
           BLOCK CONTAINS 0 RECORDS                                     00990022
           LABEL RECORDS ARE STANDARD.                                  01000022
                                                                        01010022
       01  DATE-CARD-RECORD                PIC X(80).                   01020022
                                                                        01030022
       FD  TRAN-UPDT-FILE                                               01040022
           RECORDING MODE IS F                                          01050022
           LABEL RECORDS ARE STANDARD                                   01060022
           BLOCK CONTAINS 0 RECORDS                                     01070022
           RECORD CONTAINS 250 CHARACTERS                               01080022
           DATA RECORD IS TRAN-UPDT-RECORD.                             01090022
                                                                        01100022
       01  TRAN-UPDT-RECORD                PIC X(250).                  01110022
                                                                        01120022
      ***************************************************************** 01130022
       WORKING-STORAGE SECTION.                                         01140022
      ***************************************************************** 01150022
       01  FILLER                            PIC X(50)  VALUE           01160022
           ' *** WORKING STORAGE STARTS HERE FOR SAKUT042 *** '.        01170022
                                                                        01180022
      ***************************************************************** 01190022
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    01200022
      ***************************************************************** 01210022
       01  CC-CONTROL-CARD.                                             01220022
           05  CC-CONTROL-CARD-DISPLAY.                                 01230022
               10  CC-INPUT-CCYY-MM-DD PIC X(10)    VALUE SPACE.        01240022
           05  FILLER                  PIC X(70)    VALUE SPACE.        01250022
                                                                        01260022
      ***************************************************************** 01270022
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             01280022
      ***************************************************************** 01290022
           COPY DPWS500.                                                01300022
                                                                        01310022
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      01320022
                                                        COMP SYNC.      01330022
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01340022
           88  AC-DB2-ERROR                             VALUE +4013.    01350022
           88  ABEND-4019-CAL-RTN-ERROR                 VALUE +4019.    01360022
                                                                        01370022
       01  PS-PROGRAM-SWITCHES.                                         01400022
           05  PS-ZERO-EMPDIS-SW             PIC X(1)   VALUE 'N'.      01410022
               88  PS-ZERO-EMPDIS                       VALUE 'Y'.      01420022
               88  PS-EMPDIS-NOT-ZERO                   VALUE 'N'.      01430022
           05  PS-NON-DISC-ITEM-SW           PIC  X(01) VALUE 'N'.      01440022
               88  PS-NON-DISC-ITEM                     VALUE 'Y'.      01450022
               88  PS-DISCOUNTABLE-ITEM                 VALUE 'N'.      01460022
           05  PS-END-OF-CARD-FILE-SW        PIC  X(01) VALUE 'N'.      01470022
               88  PS-END-OF-CARD-FILE                  VALUE 'Y'.      01480022
           05  PS-END-OF-TRN-CSR-SW          PIC  X(01) VALUE 'N'.      01490022
               88  PS-END-OF-TRN-CSR-N                  VALUE 'N'.      01500022
               88  PS-END-OF-TRN-CSR-Y                  VALUE 'Y'.      01510022
           05  PS-END-OF-ITM-CSR-SW          PIC  X(01) VALUE 'N'.      01520022
               88  PS-END-OF-ITM-CSR-N                  VALUE 'N'.      01530022
               88  PS-END-OF-ITM-CSR-Y                  VALUE 'Y'.      01540022
                                                                        01550022
       01  PC-CONSTANTS.                                                01560022
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01570022
                                                        'SAKUT042'.     01580022
           05  PC-ROW-NOT-FOUND              PIC S9(04) VALUE +100      01590022
                                                        COMP SYNC.      01600022
           05  PC-MULTIPLE-ROWS-FOUND        PIC S9(04) VALUE -811      01610022
                                                        COMP SYNC.      01620022
                                                                        01660022
       01  PROGRAM-VARIABLES.                                           01670022
           05 PV-EMP-DISC-RATE-USED        PIC  9(03)   VALUE ZEROS.    01680022
           05 PV-DISCOUNT-PERCENT          PIC  S9(1)V9(2) COMP-3.      01690022
           05 PV-EMPLOYEE-DISCOUNT         PIC S9(7)V99 COMP-3          01700022
                                                      VALUE ZERO.       01710022
           05 PV-NET-CHARGED-AMT           PIC S9(9)V99 COMP-3          01720022
                                                      VALUE ZERO.       01730022
                                                                        01750022
       01  PV-MISC-COUNT-FIELDS            COMP-3.                      01760022
           05  PV-TRAN-CURSOR-CNT          PIC S9(07)    VALUE ZERO.    01770022
           05  PV-ITM-CURSOR-CNT           PIC S9(07)    VALUE ZERO.    01780022
           05  PV-UPDATE-TEPITM-CNT        PIC S9(07)    VALUE ZERO.    01790022
                                                                        01800022
       01  PV-DB2-VARIABLES.                                            01810022
           05  PV-DB2-PARTN-NBR            PIC S9(04)    COMP           01820022
                                                         VALUE ZERO.    01830022
           05  PV-PREV-PARTN-NBR           PIC S9(04)    COMP           01840022
                                                         VALUE ZERO.    01850022
       01  PV-ABEND-DISPLAYS.                                           01860022
           05  PV-ABEND-PARTN-NBR          PIC  9(04)    VALUE ZERO.    01870022
           05  PV-ABEND-STR-NBR            PIC  9(04)    VALUE ZERO.    01880022
           05  PV-ABEND-RGST-ID            PIC  9(03)    VALUE ZERO.    01890022
           05  PV-ABEND-TRN-NBR            PIC  9(04)    VALUE ZERO.    01900022
                                                                        01910022
       01  PV-MISC-FIELDS.                                              01920022
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01930022
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01940022
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01950022
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01960022
           05  PV-EDIT-MASK                PIC  Z,ZZZ,ZZ9.              01970022
                                                                        01980022
      ***************************************************************** 01990022
      *  WS FIELDS FOR TEPITM AND TEPEID ASSOCIATE DISCOUNT UPDATES     02000022
      ***************************************************************** 02010022
           COPY SARD045.                                                02020022
                                                                        02030022
      ***************************************************************** 02040022
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            02050022
      ***************************************************************** 02060022
           COPY DPWS004.                                                02070022
                                                                        02080022
      ***************************************************************** 02210022
      *  DB2 COMMUNICATION AREA.                                        02220022
      ***************************************************************** 02230022
           EXEC SQL                                                     02240022
                INCLUDE SQLCA                                           02250022
           END-EXEC.                                                    02260022
                                                                        02270022
      ******************************************************************02280022
      *  SQL AND COBOL DECLARATIONS FOR ART_EMP_DISC TABLE.             02290022
      ******************************************************************02300022
                                                                        02310022
           EXEC SQL                                                     02320022
               INCLUDE ART00001                                         02330022
           END-EXEC.                                                    02340022
                                                                        02350022
      ***************************************************************** 02360022
      *  DB2 TABLE TEPITM - TRANSACTION ITEM TABLE                      02370022
      ***************************************************************** 02380022
           EXEC SQL                                                     02390022
                INCLUDE TEPITM                                          02400022
           END-EXEC.                                                    02410022
                                                                        02420022
      ***************************************************************** 02430022
      *  DB2 TABLE TEPTRN - TRANSACTION TRANSACTION TABLE               02440022
      ***************************************************************** 02450022
           EXEC SQL                                                     02460022
                INCLUDE TEPTRN                                          02470022
           END-EXEC.                                                    02480022
                                                                        02490022
      ***************************************************************** 02500022
      *  DB2 TABLE TEPTND - TRANSACTION TENDER TABLE                    02510022
      ***************************************************************** 02520022
           EXEC SQL                                                     02530022
                INCLUDE TEPTND                                          02540022
           END-EXEC.                                                    02550022
                                                                        02560022
      ***************************************************************** 02570022
      *  DB2 TABLE TEPEID - TRANSACTION EMPLOYEE DISCOUNT TABLE         02580022
      ***************************************************************** 02590022
           EXEC SQL                                                     02600022
                INCLUDE TEPEID                                          02610022
           END-EXEC.                                                    02620022
                                                                        02630022
      ******************************************************************02640022
      *  DB2 TABLE TEPDKEY - CURRENT PROCESSING DAY KEYS                02650022
      ******************************************************************02660022
           EXEC SQL                                                     02670022
                INCLUDE TEPDKEY                                         02680022
           END-EXEC.                                                    02690022
                                                                        02700022
      ******************************************************************02710022
      *  SQL AND COBOL DECLARATIONS FOR THE LOCATION TABLE (XST_LOC)    02720022
      ******************************************************************02730022
           EXEC SQL                                                     02740022
               INCLUDE XST00001                                         02750022
           END-EXEC.                                                    02760022
                                                                        02770022
      ******************************************************************02780022
      *  SQL AND COBOL DECLARATIONS FOR THE DEPT  TABLE (XST_DEPT)      02790022
      ******************************************************************02800022
           EXEC SQL                                                     02810022
               INCLUDE XST00015                                         02820022
           END-EXEC.                                                    02830022
                                                                        02840022
      ***************************************************************** 02850022
      *  DB2 TABLE TEPPART - PARTITION TABLE                            02860022
      ***************************************************************** 02870022
           EXEC SQL                                                     02880022
                INCLUDE TEPPART                                         02890022
           END-EXEC.                                                    02900022
                                                                        02910022
      ***************************************************************** 02920022
      *  TRN-CURSOR -- CURSOR TO RETRIEVE ALL OFFLINE POC ASSOCIATE     02930022
      *  DISCOUNT TRANSACTIONS                                          02940022
      ***************************************************************** 02950022
           EXEC SQL                                                     02960022
             DECLARE TRN-CURSOR CURSOR FOR                              02970022
                                                                        02980022
      **> CURRENT DAY TRANSACTIONS                                      02990022
            SELECT DISTINCT                                             03000022
                   TRN.PARTN_NBR                                        03010022
                  ,TRN.STR_NBR                                          03020022
                  ,TRN.RGST_ID                                          03030022
                  ,TRN.TRN_NBR                                          03040022
                  ,TRN.STRT_TM                                          03050022
                  ,TRN.SLS_CORR_SEQ_NBR                                 03060022
                  ,TRN.SLS_DTE                                          03070022
              FROM TEPTRN  TRN                                          03080022
                  ,TEPTND  TND                                          03090022
C25054            ,ART_EMP_DISC ART                                     03100022
             WHERE TRN.PARTN_NBR         = :PV-DB2-PARTN-NBR            03110027
               AND TND.PARTN_NBR         = TRN.PARTN_NBR                03120022
               AND TND.STR_NBR           = TRN.STR_NBR                  03130022
               AND TND.RGST_ID           = TRN.RGST_ID                  03140022
               AND TND.TRN_NBR           = TRN.TRN_NBR                  03150022
               AND TND.STRT_TM           = TRN.STRT_TM                  03160022
               AND TND.SLS_CORR_SEQ_NBR  = TRN.SLS_CORR_SEQ_NBR         03170022
               AND TRN.POS_CPBL_LVL_NBR >= 200                          03180022
               AND TRN.VOID_ABRT_CDE     = 'N'                          03190022
               AND TRN.TRN_TYP_CDE       = '01'                         03200022
               AND TND.TNDR_TYP_CDE      = '04'                         03210022
P1043          AND TND.TNDR_ID_LEN_NBR  <> 18                           03220022
C53040         AND TND.TNDR_ID <> '8484560000'                          03230043
               AND TND.TNDR_ID <> ' '                                   03240022
C25054         AND CAST(SUBSTR(TND.TNDR_ID,1,10) AS DECIMAL(10,0))      03250022
C25054                                            = ART.ACCT_NBR        03260022
C25054         AND ART.DISC_STAT_CDE <> 'N'                             03270022
               AND NOT EXISTS (                                         03280022
                  SELECT 'Y'                                            03290022
                    FROM TEPEID EID                                     03300022
                   WHERE EID.PARTN_NBR        = TRN.PARTN_NBR           03310022
                     AND EID.STR_NBR          = TRN.STR_NBR             03320022
                     AND EID.RGST_ID          = TRN.RGST_ID             03330022
                     AND EID.TRN_NBR          = TRN.TRN_NBR             03340022
                     AND EID.STRT_TM          = TRN.STRT_TM             03350022
                     AND EID.SLS_CORR_SEQ_NBR = TRN.SLS_CORR_SEQ_NBR)   03360022
                                                                        03370022
             UNION                                                      03380022
                                                                        03390022
      **> LATE AND SALES CORRECTION TRANSACTIONS                        03400022
            SELECT DISTINCT                                             03410022
                   TRN.PARTN_NBR                                        03420022
                  ,TRN.STR_NBR                                          03430022
                  ,TRN.RGST_ID                                          03440022
                  ,TRN.TRN_NBR                                          03450022
                  ,TRN.STRT_TM                                          03460022
                  ,TRN.SLS_CORR_SEQ_NBR                                 03470022
                  ,TRN.SLS_DTE                                          03480022
              FROM TEPPART PRT                                          03490022
                  ,TEPTRN  TRN                                          03500022
                  ,TEPTND  TND                                          03510022
C25054            ,ART_EMP_DISC ART                                     03520022
                  ,TEPDKEY DKY                                          03530022
             WHERE DKY.PRCG_DTE          = :CC-INPUT-CCYY-MM-DD         03540022
                                                                        03550022
               AND PRT.SLS_DTE           = DKY.SLS_DTE                  03560022
               AND PRT.DB_STRUC_CDE      = 'O'                          03570022
               AND PRT.STR_GP_CDE        = 'A'                          03580022
                                                                        03590022
               AND TRN.PARTN_NBR         = PRT.PARTN_NBR                03600022
               AND TRN.STR_NBR           = DKY.STR_NBR                  03610022
               AND TRN.RGST_ID           = DKY.RGST_ID                  03620022
               AND TRN.TRN_NBR           = DKY.TRN_NBR                  03630022
               AND TRN.STRT_TM           = DKY.STRT_TM                  03640022
               AND TRN.SLS_CORR_SEQ_NBR  = DKY.SLS_CORR_SEQ_NBR         03650022
                                                                        03660022
               AND TND.PARTN_NBR         = TRN.PARTN_NBR                03670022
               AND TND.STR_NBR           = TRN.STR_NBR                  03680022
               AND TND.RGST_ID           = TRN.RGST_ID                  03690022
               AND TND.TRN_NBR           = TRN.TRN_NBR                  03700022
               AND TND.STRT_TM           = TRN.STRT_TM                  03710022
               AND TND.SLS_CORR_SEQ_NBR  = TRN.SLS_CORR_SEQ_NBR         03720022
               AND TRN.POS_CPBL_LVL_NBR >= 200                          03730022
               AND TRN.VOID_ABRT_CDE     = 'N'                          03740022
               AND TRN.TRN_TYP_CDE       = '01'                         03750022
               AND TND.TNDR_TYP_CDE      = '04'                         03760022
P1043          AND TND.TNDR_ID_LEN_NBR  <> 18                           03770022
C53040         AND TND.TNDR_ID <> '8484560000'                          03780043
               AND TND.TNDR_ID <> ' '                                   03790022
C25054         AND CAST(SUBSTR(TND.TNDR_ID,1,10) AS DECIMAL(10,0))      03800022
C25054                                            = ART.ACCT_NBR        03810022
C25054         AND ART.DISC_STAT_CDE <> 'N'                             03820022
               AND NOT EXISTS (                                         03830022
                  SELECT 'Y'                                            03840022
                    FROM TEPEID EID                                     03850022
                   WHERE EID.PARTN_NBR        = TRN.PARTN_NBR           03860022
                     AND EID.STR_NBR          = TRN.STR_NBR             03870022
                     AND EID.RGST_ID          = TRN.RGST_ID             03880022
                     AND EID.TRN_NBR          = TRN.TRN_NBR             03890022
                     AND EID.STRT_TM          = TRN.STRT_TM             03900022
                     AND EID.SLS_CORR_SEQ_NBR = TRN.SLS_CORR_SEQ_NBR)   03910022
                                                                        03920022
               ORDER BY 1                                               03930022
           END-EXEC.                                                    03940022
                                                                        03950022
      ***************************************************************** 03960022
      *  ITM-CURSOR -- CURSOR TO RETRIEVE ALL ROWS FROM THE TEPITM      03970022
      *  TABLE WITHIN THE PARTITION SPECIFIED.                          03980022
      ***************************************************************** 03990022
           EXEC SQL                                                     04000022
             DECLARE ITM-CURSOR CURSOR FOR                              04010022
            SELECT DISTINCT                                             04020022
                   PARTN_NBR                                            04030022
                  ,STR_NBR                                              04040022
                  ,RGST_ID                                              04050022
                  ,TRN_NBR                                              04060022
                  ,STRT_TM                                              04070022
                  ,SLS_CORR_SEQ_NBR                                     04080022
                  ,LNE_ITM_SEQ_NBR                                      04090022
                  ,LIV_RSLU_CDE                                         04100022
                  ,SKU_NBR                                              04110022
                  ,DEPT_NBR                                             04120022
                  ,SUB_CL_NBR                                           04130022
                  ,SLD_QTY                                              04140022
                  ,SBJCT_TO_TX_AMT                                      04150022
                  ,NET_CHRGD_AMT                                        04160022
                  ,TX_RT_PCT                                            04170022
              FROM TEPITM                                               04180022
             WHERE PARTN_NBR        = :EPTRN-PARTN-NBR                  04190022
               AND STR_NBR          = :EPTRN-STR-NBR                    04200022
               AND RGST_ID          = :EPTRN-RGST-ID                    04210022
               AND TRN_NBR          = :EPTRN-TRN-NBR                    04220022
               AND STRT_TM          = :EPTRN-STRT-TM                    04230022
               AND SLS_CORR_SEQ_NBR = :EPTRN-SLS-CORR-SEQ-NBR           04240022
           END-EXEC.                                                    04250022
                                                                        04260022
      ***************************************************************** 04270022
      * PROCEDURE DIVISION.                                           * 04280022
      ***************************************************************** 04290022
       PROCEDURE DIVISION.                                              04300022
                                                                        04310022
      ***************************************************************** 04320022
      * 0000-MAINLINE-PARAGRAPH                                         04330022
      ***************************************************************** 04340022
       0000-MAINLINE-PARAGRAPH.                                         04350022
                                                                        04360022
           PERFORM 1000-INITIALIZE-PROGRAM.                             04370022
                                                                        04380022
           PERFORM 2000-OPEN-TRN-CURSOR.                                04390022
                                                                        04400022
           PERFORM 2100-PROCESS-TRANS                                   04410022
               UNTIL PS-END-OF-TRN-CSR-Y.                               04420022
                                                                        04430022
           PERFORM 2200-CLOSE-TRAN-CURSOR.                              04440022
                                                                        04450022
           DISPLAY '****************************************'           04460022
           DISPLAY '*************** SAKUT042 ***************'           04470022
           DISPLAY '****************************************'           04480022
           MOVE PV-ITM-CURSOR-CNT        TO PV-EDIT-MASK.               04490022
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.         04500022
           DISPLAY 'TEPITM CURSOR ROWS FETCHED.....'                    04510022
               PV-EDIT-MASK.                                            04520022
           MOVE PV-TRAN-CURSOR-CNT       TO PV-EDIT-MASK.               04530022
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.         04540022
           DISPLAY 'TEPTRN ROWS LOCATED............'                    04550022
               PV-EDIT-MASK.                                            04560022
           DISPLAY '****************************************'           04570022
                                                                        04580022
           CLOSE TRAN-UPDT-FILE.                                        04590022
           STOP RUN.                                                    04600022
                                                                        04610022
      ***************************************************************** 04620022
      * 1000-INITIALIZE-PROGRAM                                         04630022
      *     PROCESS CONTROL CARD                                        04640022
      ***************************************************************** 04650022
       1000-INITIALIZE-PROGRAM.                                         04660022
                                                                        04670022
           OPEN INPUT  DATE-CARD-FILE                                   04680022
                OUTPUT TRAN-UPDT-FILE.                                  04690022
                                                                        04700022
           SET PS-END-OF-TRN-CSR-N                                      04710022
               PS-END-OF-ITM-CSR-N TO TRUE.                             04720022
                                                                        04730022
           MOVE ZERO                     TO PV-TRAN-CURSOR-CNT          04740022
                                            PV-ITM-CURSOR-CNT           04750022
                                            PV-DB2-PARTN-NBR.           04760022
                                                                        04770022
           PERFORM 1100-PROCESS-CONTROL-CARD.                           04780022
           PERFORM 1500-SELECT-TEPPART-ROW.                             04790022
           MOVE PV-DB2-PARTN-NBR  TO PV-ABEND-PARTN-NBR.                04800022
           DISPLAY 'PROCESSING PARTN = '  PV-ABEND-PARTN-NBR.           04810022
                                                                        04840022
      *1000-INITIALIZE-PROGRAM-EXIT.                                    04850022
                                                                        04860022
                                                                        04870022
      ***************************************************************** 04880022
      * 1100-PROCESS-CONTROL-CARD                                       04890022
      *     READ CONTROL CARD TO GET POS DATE IN DB2 FORMAT.            04900022
      ***************************************************************** 04910022
       1100-PROCESS-CONTROL-CARD.                                       04920022
                                                                        04930022
           PERFORM 1110-READ-DATE-CARD-FILE.                            04940022
           CLOSE DATE-CARD-FILE.                                        04950022
           IF PS-END-OF-CARD-FILE                                       04960022
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   04970022
               MOVE 'NO CONTROL CARD TO PROCESS' TO PV-OPERATION-MSG-1  04980022
               SET AC-CONTROL-CARD-ERROR TO TRUE                        04990022
               PERFORM 9999-ABEND                                       05000022
           END-IF.                                                      05010022
                                                                        05020022
           DISPLAY PC-CURRENT-PROGRAM-NAME                              05030022
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  05040022
                                                                        05050022
           PERFORM 1200-VALIDATE-DATE.                                  05060022
                                                                        05070022
      *1100-PROCESS-CONTROL-CARD-EXIT.                                  05080022
                                                                        05090022
      ***************************************************************** 05100022
      * 1110-READ-DATE-CARD-FILE                                        05110022
      *     READ DATE CONTROL CARD.                                     05120022
      ***************************************************************** 05130022
       1110-READ-DATE-CARD-FILE.                                        05140022
           READ DATE-CARD-FILE INTO CC-CONTROL-CARD                     05150022
              AT END                                                    05160022
                 SET PS-END-OF-CARD-FILE     TO TRUE.                   05170022
                                                                        05180022
      *1110-READ-DATE-CARD-FILE-EXIT.                                   05190039
                                                                        05200022
      ***************************************************************** 05210022
       1200-VALIDATE-DATE.                                              05220022
                                                                        05230022
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              05240022
                                                                        05250022
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   05260022
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   05270022
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   05280022
           MOVE CC-CONTROL-CARD-DISPLAY      TO DPG52-LK-DATE-INPUT.    05290022
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    05300022
                                                   DPG54 DPG55 DPG56.   05310022
           EVALUATE TRUE                                                05320022
               WHEN DPG54-NO-ERROR                                      05330022
                   CONTINUE                                             05340022
               WHEN OTHER                                               05350022
                   MOVE '1200-VALIDATE-DATE'    TO PV-PARAGRAPH-NAME    05360022
                   SET ABEND-4019-CAL-RTN-ERROR TO TRUE                 05370022
                   MOVE 'INVALID DATE ON DATE CONTROL FILE'             05380022
                                                TO PV-OPERATION-MSG-1   05390022
                   PERFORM 9999-ABEND                                   05400022
           END-EVALUATE.                                                05410022
                                                                        05420022
                                                                        05430022
      ***************************************************************** 05440022
      * 1500-SELECT-TEPPART-ROW                                         05450022
      *     SELECT PARTN_NBR                                            05460022
      ***************************************************************** 05470022
       1500-SELECT-TEPPART-ROW.                                         05480022
                                                                        05490022
           EXEC SQL                                                     05500022
                  SELECT  PARTN_NBR                                     05510033
                    INTO  :EPPART-PARTN-NBR                             05520022
                    FROM  TEPPART                                       05530022
                   WHERE  SLS_DTE       = :CC-INPUT-CCYY-MM-DD          05540022
                    AND   STR_GP_CDE          = 'A'                     05550022
                    AND   DB_STRUC_CDE        = 'O'                     05560022
           END-EXEC.                                                    05570022
                                                                        05580022
           EVALUATE TRUE                                                05590022
               WHEN SQLCODE = ZERO                                      05600022
                  MOVE EPPART-PARTN-NBR TO PV-DB2-PARTN-NBR             05610022
               WHEN OTHER                                               05620022
                  DISPLAY '**  ABEND  **'                               05630022
                  DISPLAY 'SAKUT042'                                    05640022
                  DISPLAY '1500-SELECT-TEPPART-ROW'                     05650022
                  DISPLAY 'ABENDING ON SELECT OF TEPPART'               05660022
                  DISPLAY 'SQLCODE = ' SQLCODE                          05670022
                  DISPLAY '   DATE = ' CC-INPUT-CCYY-MM-DD              05680022
                  MOVE '1500-SELECT-TEPPART-ROW'                        05690022
                                             TO PV-PARAGRAPH-NAME       05700022
                  MOVE 'ERROR SELECT OF TEPPART ROWS'                   05710022
                                             TO PV-DB2-OPERATION        05720022
                  PERFORM 9100-SQL-ABEND                                05730022
           END-EVALUATE.                                                05740022
                                                                        05750022
      *1500-SELECT-TEPPART-ROW.                                         05760022
                                                                        05770022
      ***************************************************************** 06050022
      * 2000-OPEN-TRN-CURSOR                                            06060022
      *     OPEN THE TRN-CURSOR.                                        06070022
      ***************************************************************** 06080022
       2000-OPEN-TRN-CURSOR.                                            06090022
                                                                        06100022
           EXEC SQL                                                     06110022
                OPEN TRN-CURSOR                                         06120022
           END-EXEC.                                                    06130022
                                                                        06140022
           EVALUATE TRUE                                                06150022
               WHEN SQLCODE = ZERO                                      06160022
                   CONTINUE                                             06170022
               WHEN SQLWARN0 NOT = SPACES                               06180022
               WHEN SQLCODE NOT = ZEROES                                06190022
                   MOVE '2000-OPEN-TRN-CURSOR'                          06200022
                                             TO PV-PARAGRAPH-NAME       06210022
                   MOVE 'ERROR ON OPEN OF TRN-CURSOR'                   06220022
                                             TO PV-DB2-OPERATION        06230022
                   PERFORM 9100-SQL-ABEND                               06240022
           END-EVALUATE.                                                06250022
                                                                        06260022
      *2000-OPEN-TRN-CURSOR-EXIT.                                       06270022
                                                                        06280022
      ***************************************************************** 06290022
      * 2100-PROCESS-TRANS.                                             06300022
      *     PROCESS EACH ROW IN THE TRN CURSOR.                         06310022
      ***************************************************************** 06320022
       2100-PROCESS-TRANS.                                              06330022
                                                                        06340022
           PERFORM 2150-FETCH-TRAN-CURSOR.                              06350022
                                                                        06360022
           IF PS-END-OF-TRN-CSR-N                                       06370022
C25054        IF EPTRN-PARTN-NBR = PV-PREV-PARTN-NBR                    06381049
C25054           CONTINUE                                               06390022
C25054        ELSE                                                      06400022
C25054           PERFORM 5000-SELECT-ASSOC-DISCOUNT                     06410022
C25054           MOVE EPTRN-PARTN-NBR TO PV-PREV-PARTN-NBR              06420022
C25054        END-IF                                                    06430022
C25054        PERFORM 4000-OPEN-ITM-CURSOR                              06440022
C25054        PERFORM 4100-FETCH-ITM-CURSOR                             06450022
C25054           UNTIL PS-END-OF-ITM-CSR-Y                              06460022
C25054        SET PS-END-OF-ITM-CSR-N TO TRUE                           06470022
C25054        PERFORM 4200-CLOSE-ITM-CURSOR                             06480022
           END-IF.                                                      06490022
                                                                        06500022
      *2100-PROCESS-TRANS-EXIT.                                         06510022
                                                                        06520022
      ***************************************************************** 06530022
      * 2150-FETCH-TRAN-CURSOR                                          06540022
      *     FETCH THE TRAN-CURSOR                                       06550022
      ***************************************************************** 06560022
       2150-FETCH-TRAN-CURSOR.                                          06570022
                                                                        06580022
           EXEC SQL                                                     06590022
                FETCH TRN-CURSOR                                        06600022
                 INTO :EPTRN-PARTN-NBR                                  06610022
                     ,:EPTRN-STR-NBR                                    06620022
                     ,:EPTRN-RGST-ID                                    06630022
                     ,:EPTRN-TRN-NBR                                    06640022
                     ,:EPTRN-STRT-TM                                    06650022
                     ,:EPTRN-SLS-CORR-SEQ-NBR                           06660022
                     ,:EPTRN-SLS-DTE                                    06670022
           END-EXEC.                                                    06680022
                                                                        06690022
           EVALUATE TRUE                                                06700022
               WHEN SQLCODE = ZERO                                      06710022
                   ADD 1                     TO PV-TRAN-CURSOR-CNT      06720022
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          06730022
                   SET PS-END-OF-TRN-CSR-Y                              06740022
                                             TO TRUE                    06750022
               WHEN SQLWARN0 NOT = SPACES                               06760022
               WHEN SQLCODE NOT = ZEROES                                06770022
                   MOVE '2150-FETCH-TRAN-CURSOR'                        06780022
                                             TO PV-PARAGRAPH-NAME       06790022
                   MOVE 'ERROR ON FETCH OF TRAN-CURSOR'                 06800022
                                             TO PV-DB2-OPERATION        06810022
                   MOVE PV-DB2-PARTN-NBR     TO PV-ABEND-PARTN-NBR      06820022
                   PERFORM 9100-SQL-ABEND                               06830022
           END-EVALUATE.                                                06840022
                                                                        06850022
      *2150-FETCH-TRAN-CURSOR-EXIT.                                     06860022
                                                                        06870022
      ***************************************************************** 06880022
      * 2200-CLOSE-TRAN-CURSOR                                          06890022
      *     CLOSE THE TRAN-CURSOR                                       06900022
      ***************************************************************** 06910022
       2200-CLOSE-TRAN-CURSOR.                                          06920022
                                                                        06930022
           EXEC SQL                                                     06940022
                CLOSE TRN-CURSOR                                        06950022
           END-EXEC.                                                    06960022
                                                                        06970022
           EVALUATE TRUE                                                06980022
               WHEN SQLCODE = ZERO                                      06990022
                    CONTINUE                                            07000022
               WHEN SQLWARN0 NOT = SPACES                               07010022
               WHEN SQLCODE NOT = ZEROES                                07020022
                   MOVE '2200-CLOSE-TRAN-CURSOR'                        07030022
                                             TO PV-PARAGRAPH-NAME       07040022
                   MOVE 'ERROR ON CLOSE OF TRN-CURSOR'                  07050022
                                             TO PV-DB2-OPERATION        07060022
                   PERFORM 9100-SQL-ABEND                               07070022
           END-EVALUATE.                                                07080022
                                                                        07090022
      ***************************************************************** 07100022
      * 4000-OPEN-ITM-CURSOR                                            07110022
      *     OPEN THE ITM-CURSOR.                                        07120022
      ***************************************************************** 07130022
       4000-OPEN-ITM-CURSOR.                                            07140022
                                                                        07150022
           EXEC SQL                                                     07160022
                OPEN ITM-CURSOR                                         07170022
           END-EXEC.                                                    07180022
                                                                        07190022
           EVALUATE TRUE                                                07200022
               WHEN SQLCODE = ZERO                                      07210022
                   CONTINUE                                             07220022
               WHEN SQLWARN0 NOT = SPACES                               07230022
               WHEN SQLCODE NOT = ZEROES                                07240022
                   MOVE '4000-OPEN-ITM-CURSOR'                          07250022
                                             TO PV-PARAGRAPH-NAME       07260022
                   MOVE 'ERROR ON OPEN OF ITM-CURSOR'                   07270022
                                             TO PV-DB2-OPERATION        07280022
                   PERFORM 9100-SQL-ABEND                               07290022
           END-EVALUATE.                                                07300022
                                                                        07310022
      *4000-OPEN-ITM-CURSOR-EXIT.                                       07320022
                                                                        07330022
      ***************************************************************** 07340022
      * 4100-FETCH-ITM-CURSOR                                           07350022
      *     FETCH THE ITM-CURSOR.                                       07360022
      ***************************************************************** 07370022
       4100-FETCH-ITM-CURSOR.                                           07380022
                                                                        07390022
           EXEC SQL                                                     07400022
                FETCH ITM-CURSOR                                        07410022
                 INTO :EPITM-PARTN-NBR                                  07420022
                     ,:EPITM-STR-NBR                                    07430022
                     ,:EPITM-RGST-ID                                    07440022
                     ,:EPITM-TRN-NBR                                    07450022
                     ,:EPITM-STRT-TM                                    07460022
                     ,:EPITM-SLS-CORR-SEQ-NBR                           07470022
                     ,:EPITM-LNE-ITM-SEQ-NBR                            07480022
                     ,:EPITM-LIV-RSLU-CDE                               07490022
                     ,:EPITM-SKU-NBR                                    07500022
                     ,:EPITM-DEPT-NBR                                   07510022
                     ,:EPITM-SUB-CL-NBR                                 07520022
                     ,:EPITM-SLD-QTY                                    07530022
                     ,:EPITM-SBJCT-TO-TX-AMT                            07540022
                     ,:EPITM-NET-CHRGD-AMT                              07550022
                     ,:EPITM-TX-RT-PCT                                  07560022
           END-EXEC.                                                    07570022
                                                                        07580022
           EVALUATE TRUE                                                07590022
               WHEN SQLCODE = ZERO                                      07600022
                   ADD 1                     TO PV-ITM-CURSOR-CNT       07610022
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          07620022
                   SET PS-END-OF-ITM-CSR-Y   TO TRUE                    07630022
               WHEN SQLWARN0 NOT = SPACES                               07640022
               WHEN SQLCODE NOT = ZEROES                                07650022
                   MOVE '4100-FETCH-ITM-CURSOR'                         07660022
                                             TO PV-PARAGRAPH-NAME       07670022
                   MOVE 'ERROR ON FETCH OF ITM-CURSOR'                  07680022
                                             TO PV-DB2-OPERATION        07690022
      ***************************************************************** 07700022
      *  MOVE CURSOR INFORMATION TO ABEND DISPLAYS                      07710022
      ***************************************************************** 07720022
                   MOVE PV-DB2-PARTN-NBR     TO PV-ABEND-PARTN-NBR      07730022
                   MOVE EPITM-STR-NBR        TO PV-ABEND-STR-NBR        07740022
                   MOVE EPITM-RGST-ID        TO PV-ABEND-RGST-ID        07750022
                   MOVE EPITM-TRN-NBR        TO PV-ABEND-TRN-NBR        07760022
                   PERFORM 9100-SQL-ABEND                               07770022
           END-EVALUATE.                                                07780022
                                                                        07790022
           IF PS-END-OF-ITM-CSR-N                                       07800022
               PERFORM 4110-PROCESS-ITEMS                               07810022
           END-IF.                                                      07820022
                                                                        07830022
                                                                        07840022
      *4100-FETCH-ITM-CURSOR-EXIT.                                      07850022
                                                                        07860022
      ***************************************************************** 07870022
      * 4110-PROCESS-ITEMS                                              07880022
      *     PROCESS THE ITM-CURSOR.                                     07890022
      *     SKIP ITEMS WITH NON DISCOUNTABLE DEPARTMENTS                07900022
      ***************************************************************** 07910022
       4110-PROCESS-ITEMS.                                              07920022
           IF EPITM-DEPT-NBR = '833' OR                                 07930022
              EPITM-DEPT-NBR = '834' OR                                 07940022
              EPITM-DEPT-NBR = '981' OR                                 07950022
              EPITM-DEPT-NBR = '983' OR                                 07960022
              EPITM-DEPT-NBR = '098' OR                                 07970022
SEPHOR        EPITM-DEPT-NBR = '712' OR                                 07980048
             (EPITM-DEPT-NBR = '099' AND                                07990022
              EPITM-SUB-CL-NBR = '20') OR                               08000022
             (EPITM-DEPT-NBR = '099' AND                                08010022
              EPITM-SUB-CL-NBR = '70') OR                               08020022
             (EPITM-DEPT-NBR = '800' AND                                08030022
              EPITM-SUB-CL-NBR = '11') OR                               08040022
              EPITM-LIV-RSLU-CDE NOT = '+'                              08050022
               NEXT SENTENCE                                            08060022
           ELSE                                                         08070022
               PERFORM 5100-FIND-DEPT-EMP-DISC-RATE                     08080022
               IF PS-DISCOUNTABLE-ITEM                                  08090022
                   PERFORM 5200-CALC-ASSOCIATE-DISCOUNT                 08100022
                   IF PS-EMPDIS-NOT-ZERO                                08110022
                       COMPUTE EPITM-NET-CHRGD-AMT =                    08120022
                               EPITM-NET-CHRGD-AMT +                    08130022
                               PV-EMPLOYEE-DISCOUNT                     08140022
                       COMPUTE EPITM-SBJCT-TO-TX-AMT =                  08150022
                               EPITM-SBJCT-TO-TX-AMT +                  08160022
                               PV-EMPLOYEE-DISCOUNT                     08170022
                       PERFORM 8500-WRITE-TRAN-UPDT-RECORD              08180022
                   END-IF                                               08190022
               END-IF                                                   08200022
           END-IF.                                                      08210022
                                                                        08220022
      *4110-PROCESS-ITEMS-EXIT.                                         08230022
                                                                        08240022
      ***************************************************************** 08250022
      * 4200-CLOSE-ITM-CURSOR                                           08260022
      *     CLOSE THE ITM-CURSOR.                                       08270022
      ***************************************************************** 08280022
       4200-CLOSE-ITM-CURSOR.                                           08290022
                                                                        08300022
           EXEC SQL                                                     08310022
                CLOSE ITM-CURSOR                                        08320022
           END-EXEC.                                                    08330022
                                                                        08340022
           EVALUATE TRUE                                                08350022
               WHEN SQLCODE = ZERO                                      08360022
                    CONTINUE                                            08370022
               WHEN SQLWARN0 NOT = SPACES                               08380022
               WHEN SQLCODE NOT = ZEROES                                08390022
                   MOVE '4200-CLOSE-ITM-CURSOR'                         08400022
                                             TO PV-PARAGRAPH-NAME       08410022
                   MOVE 'ERROR ON CLOSE OF ITM-CURSOR'                  08420022
                                             TO PV-DB2-OPERATION        08430022
                   PERFORM 9100-SQL-ABEND                               08440022
           END-EVALUATE.                                                08450022
                                                                        08460022
      *4200-CLOSE-ITM-CURSOR-EXIT.                                      08470022
                                                                        08480022
      ***************************************************************** 08490022
      * 5000-SELECT-ASSOC-DISCOUNT                                      08500022
      *     SELECT THE ASSOCIATE DISCOUNT BY SELECTING A SINGLE         08510022
      *     NON-ECOMMERCE, NON-SFS, KOHL'S CHARGE VISIBLE ASSOCIATE     08520022
      *     DISCOUNT FROM A DIFFERENT B&M STORE EARLIER IN THE DAY.     08530022
      *     DATE WILL VARY DUE TO LATE TRANSACTIONS - THIS PARAGRAPH    08540022
      *     WILL BE PERFORMED ONCE PER TRANSACTION.                     08550022
      ***************************************************************** 08560022
       5000-SELECT-ASSOC-DISCOUNT.                                      08570022
                                                                        08580022
           EXEC SQL                                                     08590022
                  SELECT EID.EMP_DISC_PCT                               08600022
                    INTO :PV-DISCOUNT-PERCENT                           08610022
                    FROM TEPPART PRT                                    08620022
                        ,TEPTRN  TRN                                    08630022
                        ,XST_LOC LOC                                    08640022
                        ,TEPITM  ITM                                    08650022
                        ,TEPEID  EID                                    08660022
                   WHERE                                                08670022
                         PRT.SLS_DTE   = :EPTRN-SLS-DTE                 08680036
                     AND PRT.DB_STRUC_CDE  = 'O'                        08690022
                     AND PRT.STR_GP_CDE    = 'A'                        08700022
                     AND TRN.PARTN_NBR  = PRT.PARTN_NBR                 08710022
                     AND ITM.PARTN_NBR  = TRN.PARTN_NBR                 08720022
                     AND ITM.STR_NBR = TRN.STR_NBR                      08730022
                     AND ITM.RGST_ID = TRN.RGST_ID                      08740022
                     AND ITM.TRN_NBR = TRN.TRN_NBR                      08750022
                     AND ITM.STRT_TM = TRN.STRT_TM                      08760022
                     AND ITM.SLS_CORR_SEQ_NBR = TRN.SLS_CORR_SEQ_NBR    08770022
                     AND ITM.PARTN_NBR = EID.PARTN_NBR                  08780022
                     AND ITM.STR_NBR = EID.STR_NBR                      08790022
                     AND ITM.RGST_ID = EID.RGST_ID                      08800022
                     AND ITM.TRN_NBR = EID.TRN_NBR                      08810022
                     AND ITM.STRT_TM = EID.STRT_TM                      08820022
                     AND ITM.SLS_CORR_SEQ_NBR = EID.SLS_CORR_SEQ_NBR    08830022
                     AND ITM.LNE_ITM_SEQ_NBR = EID.LNE_ITM_SEQ_NBR      08840022
                     AND LOC.LOC_NBR = TRN.STR_NBR                      08850022
                     AND EID.SLS_CORR_SEQ_NBR = 0                       08860022
                     AND TRN.TRN_TYP_CDE IN ('02')                      08870022
                     AND LOC.E_BUS_IND  <> 'Y'                          08880030
                     AND (TRN.OMNI_CHNL_FFLMT_TYP_CDE <> 'SFS'          08890030
                      OR  TRN.OMNI_CHNL_FFLMT_TYP_CDE IS NULL)          08900030
                     AND EID.EMP_DISC_CTG_CDE = 'K'                     08910022
                     AND EID.EMP_DISC_SRC_CDE = 'P'                     08920022
                     AND EID.EMP_DISC_PCT  > 0                          08930022
                 WITH UR                                                08940022
                 FETCH FIRST 1 ROWS ONLY                                08950022
           END-EXEC.                                                    08960022
                                                                        08970022
           EVALUATE TRUE                                                08980022
               WHEN SQLCODE = ZERO                                      08990022
                  DISPLAY EPTRN-SLS-DTE ' ASSOC DISC PCT IS: '          09000022
                          PV-DISCOUNT-PERCENT                           09010022
               WHEN OTHER                                               09020022
                  MOVE ZEROES TO PV-DISCOUNT-PERCENT                    09030022
                  DISPLAY 'NO ASSOC DISC PCT WAS FOUND FOR '            09040022
                           EPTRN-SLS-DTE                                09050022
                  MOVE '5000-SELECT-ASSOCIATE-DISCOUNT'                 09060028
                                                 TO PV-PARAGRAPH-NAME   09070022
                  MOVE 'ERROR ON SELECT ASSOC DISC'                     09080022
                                                 TO PV-DB2-OPERATION    09090022
                  PERFORM 9100-SQL-ABEND                                09100022
           END-EVALUATE.                                                09110022
                                                                        09120022
      *5000-SELECT-ASSOCIATE-DISCOUNT.                                  09130022
                                                                        09140022
      ******************************************************************09150022
      * 5100-FIND-DEPT-EMP-DISC-RATE                                    09160022
      * CHECK THIS ITEM'S DEPARTMENT TO DETERMINE IF THAT DEPARTMENT    09170022
      * REPRESENTS NON-DISCOUNTABLE ITEMS                               09180022
      ******************************************************************09190022
       5100-FIND-DEPT-EMP-DISC-RATE.                                    09200022
                                                                        09210022
           EXEC SQL                                                     09220022
               SELECT MDSE_LNE_CDE                                      09230022
                 INTO :XST00015-MDSE-LNE-CDE                            09240022
                 FROM XST_DEPT                                          09250022
                WHERE DEPT_NBR =                                        09260022
                    CAST(SUBSTR(:EPITM-DEPT-NBR,1,3) AS DECIMAL(4,0))   09270022
           END-EXEC.                                                    09280022
                                                                        09290022
           EVALUATE SQLCODE                                             09300022
               WHEN ZERO                                                09310022
                   IF XST00015-MDSE-LNE-CDE  = 'M'                      09320022
                       SET PS-NON-DISC-ITEM TO TRUE                     09330022
                   ELSE                                                 09340022
                       SET PS-DISCOUNTABLE-ITEM TO TRUE                 09350022
                   END-IF                                               09360022
                                                                        09370022
               WHEN +100                                                09380022
                   SET PS-DISCOUNTABLE-ITEM TO TRUE                     09390022
                                                                        09400022
               WHEN OTHER                                               09410022
                   DISPLAY '                            '               09420022
                   DISPLAY '5100-FIND-DEPT-EMP-DISC-RATE'               09430022
                   DISPLAY 'ERROR SELECTING FROM XST_DEPT'              09440022
                   DISPLAY 'DEPT NBR = ' EPITM-DEPT-NBR                 09450022
                   MOVE '5100-FIND-DEPT-EMP-DISC-RATE'                  09460022
                                                 TO PV-PARAGRAPH-NAME   09470022
                   MOVE 'FIND DEPT DISC RATE'    TO PV-DB2-OPERATION    09480022
                   PERFORM 9100-SQL-ABEND                               09490022
           END-EVALUATE.                                                09500022
                                                                        09510022
      *5100-EXIT.                                                       09520022
                                                                        09530022
      ******************************************************************09540022
      * 5200-CALC-ASSOCIATE-DISCOUNT                                    09550022
      * CALCULATE THE ASSOCIATE DISCOUNT                                09560022
      ******************************************************************09570022
       5200-CALC-ASSOCIATE-DISCOUNT.                                    09580022
                                                                        09590022
           MOVE EPITM-SBJCT-TO-TX-AMT   TO PV-NET-CHARGED-AMT.          09600022
                                                                        09610022
           COMPUTE PV-EMPLOYEE-DISCOUNT ROUNDED =                       09620022
                  (PV-NET-CHARGED-AMT *                                 09630022
                   PV-DISCOUNT-PERCENT).                                09640022
                                                                        09650022
           COMPUTE PV-EMPLOYEE-DISCOUNT =                               09660022
                   PV-EMPLOYEE-DISCOUNT * -1.                           09670022
                                                                        09680022
           IF PV-EMPLOYEE-DISCOUNT = 0                                  09690022
              SET PS-ZERO-EMPDIS TO TRUE                                09700022
           ELSE                                                         09710022
              SET PS-EMPDIS-NOT-ZERO TO TRUE                            09720022
           END-IF.                                                      09730022
                                                                        09740022
      *5200-CALC-ASSOCIATE-DISCOUNT.                                    09750022
                                                                        09760022
      ***************************************************************** 09770022
      * 8500-WRITE-TRAN-UPDT-RECORD                                     09780022
      *     WRITE THE OUTPUT RECORD WITH ITEM UPDATE DETAILS.           09790022
      ***************************************************************** 09800022
       8500-WRITE-TRAN-UPDT-RECORD.                                     09810022
                                                                        09820022
           INITIALIZE SA045-TRAN-UPDT-RECORD.                           09830022
           MOVE EPITM-PARTN-NBR        TO SA045-EPITM-PARTN-NBR.        09840022
           MOVE EPITM-STR-NBR          TO SA045-EPITM-STR-NBR.          09850022
           MOVE EPITM-RGST-ID          TO SA045-EPITM-RGST-ID.          09860022
           MOVE EPITM-TRN-NBR          TO SA045-EPITM-TRN-NBR.          09870022
           MOVE EPITM-STRT-TM          TO SA045-EPITM-STRT-TM.          09880022
           MOVE EPITM-SLS-CORR-SEQ-NBR TO SA045-EPITM-SLS-CORR-SEQ-NBR. 09890022
           MOVE EPITM-LNE-ITM-SEQ-NBR  TO SA045-EPITM-LNE-ITM-SEQ-NBR.  09900022
           MOVE EPITM-DEPT-NBR         TO SA045-EPITM-DEPT-NBR.         09910022
           MOVE EPITM-SUB-CL-NBR       TO SA045-EPITM-SUB-CL-NBR.       09920022
           MOVE EPITM-LIV-RSLU-CDE     TO SA045-EPITM-LIV-RSLU-CDE.     09930022
           MOVE EPITM-SKU-NBR          TO SA045-EPITM-SKU-NBR.          09940022
           MOVE EPITM-SBJCT-TO-TX-AMT  TO SA045-EPITM-SBJCT-TO-TX-AMT.  09950022
           MOVE EPITM-NET-CHRGD-AMT    TO SA045-EPITM-NET-CHRGD-AMT.    09960022
           MOVE EPITM-TX-RT-PCT        TO SA045-EPITM-TX-RT-PCT.        09970022
           MOVE PV-DISCOUNT-PERCENT    TO SA045-EPEID-EMP-DISC-PCT.     09980022
           MOVE PV-EMPLOYEE-DISCOUNT   TO SA045-EPEID-EMP-DISC-AMT.     09990022
           MOVE 'S'                    TO SA045-EPEID-EMP-DISC-SRC-CDE. 10000022
           MOVE 'K'                    TO SA045-EPEID-EMP-DISC-CTG-CDE. 10010022
                                                                        10020022
           WRITE TRAN-UPDT-RECORD                                       10030022
               FROM SA045-TRAN-UPDT-RECORD.                             10040022
                                                                        10050022
      *8500-WRITE-TRAN-UPDT-RECORD-EXIT.                                10060022
                                                                        10070022
      ***************************************************************** 10080022
      * 9100-SQL-ABEND                                                  10090022
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.10100022
      ***************************************************************** 10110022
       9100-SQL-ABEND.                                                  10120022
           DISPLAY '** ABEND     **'.                                   10130022
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        10140022
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              10150022
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               10160022
           DISPLAY '** PARTN NBR ** = ' PV-ABEND-PARTN-NBR.             10170022
           DISPLAY '** STORE NBR ** = ' PV-ABEND-STR-NBR.               10180022
           DISPLAY '** RGST ID   ** = ' PV-ABEND-RGST-ID.               10190022
           DISPLAY '** TRAN NBR  ** = ' PV-ABEND-TRN-NBR.               10200022
                                                                        10210022
      ***************************************************************** 10220022
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     10230022
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      10240022
      ***************************************************************** 10250022
           COPY DPPD004.                                                10260022
                                                                        10270022
           SET AC-DB2-ERROR TO TRUE.                                    10280022
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         10290022
                                                                        10300022
      ***************************************************************** 10310022
      * 9999-ABEND                                                      10320022
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  10330022
      ***************************************************************** 10340022
       9999-ABEND.                                                      10350022
           DISPLAY '** ABEND           **'.                             10360022
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  10370022
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        10380022
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       10390022
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       10400022
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         10410022
