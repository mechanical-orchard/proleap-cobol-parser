       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT215.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            03/27/2003.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  EXTRACTS AFTER UNIT BOOKING ADJUSTMENT TRANSACTIONS FROM      *        
      *  FASHION SALES' ADJUSTMENT AUDIT TABLE TO FEED UNIT VARIANCE   *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES (MADE BY)          *        
      * -------  ----------  ----------------------------------------  *        
      * 24521    04-05-2003  ADD TRUE-CUTOFF-QTY TO OUTPUT FOR VARIANCE*        
      *                      ALSO, ADD LOGIC TO FILL IN INVENTORY DATE *        
      *                      AND SKU STATUS.        (V. LEE YANG)      *        
      *          05-09-2003  SELECT CODE 'P' RECORDS FOR UNIT BOOKING  *        
      *                      EXTRACT FROM FS ADJUST AUDIT TABLE        *        
W26600* W26600   05-21-2004  STORE EXPANSION PROJECT.                  *        
W26600*                      USE LOC_NBR FROM TINVPAR INSTEAD OF 3 CHAR*        
W26600*                      STORE NUMBER.                             *        
W28545* W28545   07-19-2006  DEPARTMENT LEVEL PHYSICAL INVENTORY       *        
W28545*                      CHANGES. INCORPORATE INVENTORY ID.        *        
W28545*                      - J. SELWITSCHKA / STRATAGEM              *        
W33304* W33304   08-11-2008  HILO CHANGES                              *        
W33304*                      MODIFY FST00003 CURSOR TO NOW ONLY FETCH  *        
W33304*                      STORES THAT ARE NOT DELETED BY MERGING    *        
W33304*                      Z400 SELECT SQL INTO THIS CURSOR.         *        
W33304*                      - D. THEEL                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT REQUEST-CNTL-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT AFT-UNIT-BOOK-ADJ-FILE                                        
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  REQUEST-CNTL-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  REQUEST-CNTL-REC                PIC X(80).                           
                                                                                
       FD  AFT-UNIT-BOOK-ADJ-FILE                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 060 CHARACTERS                                       
           DATA RECORD IS AFT-UNIT-BOOK-ADJ-RECORD.                             
       01  AFT-UNIT-BOOK-ADJ-RECORD        PIC X(060).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-DEFAULT-STAT             PIC X(02)       VALUE '20'.          
           05  PC-REGIONAL-CLEARANCE       PIC X(02)       VALUE '25'.          
           05  PC-COMPANY-OPERCO           PIC X(02)       VALUE '03'.          
           05  PC-CORPORATE-STORE          PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PC-MINUS                    PIC X(01)       VALUE '-'.           
           05  PC-YES                      PIC X(01)       VALUE 'Y'.           
           05  PC-PROCESSED                PIC X(01)       VALUE 'P'.           
           05  PC-COMPLETE                 PIC X(01)       VALUE 'C'.           
           05  PC-REQ-COMMENT              PIC X(01)       VALUE '*'.           
           05  PC-INV-OL-ADJ-BATCH         PIC X(04)       VALUE 'INOL'.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-FST00003          PIC X           VALUE 'N'.           
               88  END-OF-FST00003                         VALUE 'Y'.           
               88  NOT-END-OF-FST00003                     VALUE 'N'.           
           05  PS-END-OF-REQUEST-CNTL      PIC X           VALUE 'N'.           
               88  END-OF-REQUEST-CNTL                     VALUE 'Y'.           
               88  NOT-END-OF-REQUEST-CNTL                 VALUE 'N'.           
           05  PS-VALID-RQ-REQ-DATE        PIC X(10)       VALUE SPACES.        
               88  VALID-RQ-REQ-DATE                       VALUE SPACES.        
           05  FILLER REDEFINES PS-VALID-RQ-REQ-DATE.                           
               10  PS-VALID-RQ-REQ-DATE-CCYY                                    
                                           PIC X(04).                           
                   88  INVALID-RQ-REQ-DATE-CCYY            VALUE '####'.        
               10  PS-VALID-RQ-REQ-DATE-DSH1                                    
                                           PIC X(01).                           
                   88  INVALID-RQ-REQ-DATE-DSH1            VALUE '?'.           
               10  PS-VALID-RQ-REQ-DATE-MM                                      
                                           PIC X(02).                           
                   88  INVALID-RQ-REQ-DATE-MM              VALUE '##'.          
               10  PS-VALID-RQ-REQ-DATE-DSH2                                    
                                           PIC X(01).                           
                   88  INVALID-RQ-REQ-DATE-DSH2            VALUE '?'.           
               10  PS-VALID-RQ-REQ-DATE-DD                                      
                                           PIC X(02).                           
                   88  INVALID-RQ-REQ-DATE-DD              VALUE '##'.          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROCESSING REQUEST CONTROL ***'.                                
                                                                                
       01  RQ-REQ-CNTL-AREA.                                                    
           05  RQ-REQ-IND                  PIC X(01)       VALUE SPACES.        
           05  RQ-REQ-DATE                 PIC X(10)       VALUE SPACES.        
           05  FILLER REDEFINES RQ-REQ-DATE.                                    
               10  RQ-REQ-DATE-CCYY        PIC X(04).                           
               10  RQ-REQ-DATE-DSH1        PIC X(01).                           
                   88  VALID-RQ-REQ-DATE-DSH1              VALUE '-'.           
               10  RQ-REQ-DATE-MM          PIC X(02).                           
                   88  VALID-RQ-REQ-DATE-MM                VALUES ARE           
                       '01' THRU '12'.                                          
               10  RQ-REQ-DATE-DSH2        PIC X(01).                           
                   88  VALID-RQ-REQ-DATE-DSH2              VALUE '-'.           
               10  RQ-REQ-DATE-DD          PIC X(02).                           
                   88  VALID-RQ-REQ-DATE-DD                VALUES ARE           
                       '01' THRU '31'.                                          
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-REQ-OPT-BATCH            PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-REQ-OPT-UNT-BKG-CDE      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RQ-REQ-OPT-TRN-STAT-CDE     PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(60)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PLND-INV-TMST.                                                
               10  PV-PLND-INV-DTE         PIC X(10)       VALUE SPACES.        
               10  FILLER                  PIC X(16)       VALUE                
                   '-23.59.59.999999'.                                          
           05  PV-PLND-INV-TMST-RDF REDEFINES PV-PLND-INV-TMST                  
                                           PIC X(26).                           
           05  PV-PREV-LOC-NBR             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
W26600*    05  PV-STORE-NBR-3              PIC 9(03)       VALUE ZEROES.        
W26600     05  PV-LOC-NBR                  PIC S9(4)  COMP VALUE ZEROES.        
           05  PV-BATCH-NBR                PIC X(04)       VALUE SPACES.        
           05  PV-UNIT-BOOKING-CDE         PIC X(01)       VALUE SPACES.        
           05  PV-TRANS-STAT-CDE           PIC X(01)       VALUE SPACES.        
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
           05  PV-RECORD-EXTRACTED         PIC S9(09)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-TOT-RECORD-EXTRACTED     PIC S9(11)      VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** OUTPUT RECORDS WS BEGIN ***'.                                   
                                                                                
      *---------------------------------------------------------------* INKCS046
      *    VARIANCE DATA FOR THE DATA WAREHOUSE                       * INKCS046
      *---------------------------------------------------------------* INKCS046
      *01  IN009-VARIANCE-RECORD.                                               
           COPY INRD009.                                                        
                                                                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------* INKCS046
      *    TINVPAR - INVENTORY PARAMETER TABLE                        * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TINVPAR                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
W28545*---------------------------------------------------------------* INKCS046
W28545*    TINCNTL - INVENTORY CYCLE CONTROL TABLE                    * INKCS046
W28545*---------------------------------------------------------------* INKCS046
W28545     EXEC SQL                                                     INKCS046
W28545         INCLUDE TINCNTL                                          INKCS046
W28545     END-EXEC.                                                    INKCS046
W28545                                                                  INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TDTATTR - DATE ATTRIBUTE TABLE                             * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TDTATTR                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    TUPCPLS - UPC PRODUCT LISTING TABLE                        * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE TUPCPLS                                          INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------* INKCS046
      *    FST00003 - FASHION SALES ADJUSTMENT AUDIT TABLE            * INKCS046
      *---------------------------------------------------------------* INKCS046
           EXEC SQL                                                     INKCS046
               INCLUDE FST00003                                         INKCS046
           END-EXEC.                                                    INKCS046
                                                                        INKCS046
      *---------------------------------------------------------------*         
      *    FST00003 - FASHION SALES ADJ AUDIT CURSOR                  *         
      *---------------------------------------------------------------*         
           EXEC SQL   DECLARE FST00003-CURSOR CURSOR FOR                        
W33304          SELECT  A.ITM_NBR                                               
W33304                 ,A.LOC_NBR                                               
W33304                 ,A.ACTN_CDE                                              
W33304                 ,A.ADJ_QTY                                               
W33304                 ,B.INV_ID                                                
W33304                 ,B.PLND_INV_DTE                                          
W33304           FROM   FSTT_OH_ADJ_AUD A                                       
W33304                 ,TINVPAR B                                               
W33304                 ,TINCNTL C                                               
W33304          WHERE A.PRCG_DTE              = :RQ-REQ-DATE                    
W33304            AND A.BTCH_NBR              = :PV-BATCH-NBR                   
W33304            AND A.UNT_BKG_CDE           = :PV-UNIT-BOOKING-CDE            
W33304            AND A.TRN_STAT_CDE          = :PV-TRANS-STAT-CDE              
W33304            AND A.LOC_NBR               = B.LOC_NBR                       
W33304            AND B.LOC_INV_STAT_CDE      = 'IN'                            
W33304            AND B.ACTL_FIN_BK_DTE       = '9999-09-09'                    
W33304            AND B.PLND_INV_DTE          < :DTATTR-FSCL-MN-END-DTE         
W33304            AND C.ACTV_IND              = 'Y'                             
W33304            AND B.INV_ID                = C.INV_ID                        
W33304          ORDER BY A.LOC_NBR                                              
W33304          FOR FETCH ONLY                                                  
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B300-EXTRACT-DATA                                            
               UNTIL END-OF-REQUEST-CNTL.                                       
                                                                                
           PERFORM B400-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           MOVE ZEROES TO PV-TOT-RECORD-EXTRACTED.                              
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           OPEN INPUT  REQUEST-CNTL-FILE                                        
                OUTPUT AFT-UNIT-BOOK-ADJ-FILE.                                  
                                                                                
           DISPLAY '  ----+----1----+----2----+----3----+----4'                 
                   '----+----5----+----6----+----7'.                            
           PERFORM Z600-READ-REQUEST.                                           
                                                                                
      ******************************************************************        
      *    SELECT AND PRODUCE THE VARIOUS REPORTS                               
      ******************************************************************        
       B300-EXTRACT-DATA.                                                       
                                                                                
           MOVE ZEROES TO PV-RECORD-EXTRACTED.                                  
                                                                                
           PERFORM C200-PROCESS-REQUEST.                                        
                                                                                
           IF VALID-RQ-REQ-DATE                                                 
               AND RQ-REQ-IND NOT = PC-REQ-COMMENT                              
               DISPLAY '      DATE REQUESTED: ' RQ-REQ-DATE                     
                                                                                
               PERFORM Z100-OPEN-FST00003                                       
               PERFORM Z200-FETCH-FST00003                                      
                                                                                
               PERFORM C100-PROCESS-FST00003                                    
                   UNTIL END-OF-FST00003                                        
                                                                                
               PERFORM Z300-CLOSE-FST00003                                      
                                                                                
               DISPLAY '      RECORD EXTRACTED:  ' PV-RECORD-EXTRACTED          
               DISPLAY ' '                                                      
                                                                                
               ADD PV-RECORD-EXTRACTED TO PV-TOT-RECORD-EXTRACTED               
           END-IF.                                                              
                                                                                
           PERFORM Z600-READ-REQUEST.                                           
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B400-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '  TOTAL EXTRACTED:  ' PV-TOT-RECORD-EXTRACTED.              
                                                                                
           CLOSE   REQUEST-CNTL-FILE                                            
                   AFT-UNIT-BOOK-ADJ-FILE.                                      
                                                                                
      ******************************************************************        
      *    PROCESS FST00003 CURSOR                                              
      ******************************************************************        
       C100-PROCESS-FST00003.                                                   
                                                                                
           MOVE ZEROES              TO IN009-UPC-ID                             
                                       IN009-UNT-CTOFF                          
                                       IN009-TRUE-CUTOFF-QTY.                   
                                                                                
           MOVE FST00003-ITM-NBR    TO IN009-ITM-NBR.                           
           MOVE FST00003-LOC-NBR    TO IN009-STR-NBR.                           
                                                                                
           IF FST00003-LOC-NBR NOT = PV-PREV-LOC-NBR                            
               MOVE FST00003-LOC-NBR TO PV-PREV-LOC-NBR                         
W26600*                                 PV-STORE-NBR-3                          
                                        PV-LOC-NBR                              
           END-IF.                                                              
                                                                                
           MOVE INVPAR-PLND-INV-DTE TO IN009-INV-DTE                            
                                       PV-PLND-INV-DTE.                         
                                                                                
           MOVE PC-CORPORATE-STORE  TO UPCPLS-STR-NBR.                          
           PERFORM Z500-GET-SKU-STATUS.                                         
           IF UPCPLS-UPC-STAT-CDE = PC-REGIONAL-CLEARANCE                       
               MOVE FST00003-LOC-NBR  TO UPCPLS-STR-NBR                         
               PERFORM Z500-GET-SKU-STATUS                                      
           END-IF.                                                              
           MOVE UPCPLS-UPC-STAT-CDE TO IN009-STAT-CDE.                          
                                                                                
           IF FST00003-ACTN-CDE = PC-MINUS                                      
               COMPUTE IN009-PHY-CNT = -1 * FST00003-ADJ-QTY                    
           ELSE                                                                 
               MOVE FST00003-ADJ-QTY TO IN009-PHY-CNT                           
           END-IF.                                                              
                                                                                
           PERFORM Z700-WRITE-EXTRACT.                                          
                                                                                
           PERFORM Z200-FETCH-FST00003.                                         
                                                                                
      ******************************************************************        
      *    PROCESS REQUEST CONTROL CARD                                         
      ******************************************************************        
       C200-PROCESS-REQUEST.                                                    
                                                                                
           SET VALID-RQ-REQ-DATE                TO TRUE.                        
           DISPLAY '  ' RQ-REQ-CNTL-AREA.                                       
                                                                                
           IF RQ-REQ-IND = PC-REQ-COMMENT                                       
               NEXT SENTENCE                                                    
           ELSE                                                                 
               IF RQ-REQ-DATE-CCYY NOT NUMERIC                                  
                   SET INVALID-RQ-REQ-DATE-CCYY TO TRUE                         
               END-IF                                                           
               IF RQ-REQ-DATE-MM NOT NUMERIC                                    
                   OR NOT VALID-RQ-REQ-DATE-MM                                  
                   SET INVALID-RQ-REQ-DATE-MM   TO TRUE                         
               END-IF                                                           
               IF RQ-REQ-DATE-DD NOT NUMERIC                                    
                   OR NOT VALID-RQ-REQ-DATE-DD                                  
                   SET INVALID-RQ-REQ-DATE-DD   TO TRUE                         
               END-IF                                                           
               IF NOT VALID-RQ-REQ-DATE-DSH1                                    
                   OR NOT VALID-RQ-REQ-DATE-DSH2                                
                   IF NOT VALID-RQ-REQ-DATE-DSH1                                
                       SET INVALID-RQ-REQ-DATE-DSH1 TO TRUE                     
                   END-IF                                                       
                   IF NOT VALID-RQ-REQ-DATE-DSH2                                
                       SET INVALID-RQ-REQ-DATE-DSH2 TO TRUE                     
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF VALID-RQ-REQ-DATE                                             
                   IF RQ-REQ-OPT-BATCH = SPACES                                 
                       MOVE PC-INV-OL-ADJ-BATCH     TO PV-BATCH-NBR             
                   ELSE                                                         
                       DISPLAY '      REQUESTED BATCH NUMBER: '                 
                               RQ-REQ-OPT-BATCH                                 
                       MOVE RQ-REQ-OPT-BATCH        TO PV-BATCH-NBR             
                   END-IF                                                       
                                                                                
                   IF RQ-REQ-OPT-UNT-BKG-CDE = SPACES                           
***                    MOVE PC-PROCESSED        TO PV-UNIT-BOOKING-CDE          
                   ELSE                                                         
                       DISPLAY '      REQUESTED UNIT BOOKING CODE: '            
                               RQ-REQ-OPT-UNT-BKG-CDE                           
                       MOVE RQ-REQ-OPT-UNT-BKG-CDE                              
                           TO PV-UNIT-BOOKING-CDE                               
                   END-IF                                                       
                                                                                
                   IF RQ-REQ-OPT-TRN-STAT-CDE = SPACES                          
                       MOVE PC-COMPLETE         TO PV-TRANS-STAT-CDE            
                   ELSE                                                         
                       DISPLAY '      REQUESTED TRANS STATUS CODE: '            
                               RQ-REQ-OPT-TRN-STAT-CDE                          
                       MOVE RQ-REQ-OPT-TRN-STAT-CDE TO PV-TRANS-STAT-CDE        
                   END-IF                                                       
               ELSE                                                             
                   DISPLAY '   ' PS-VALID-RQ-REQ-DATE                           
                           ' **** INVALID DATE ****'                            
               END-IF                                                           
                                                                                
               IF VALID-RQ-REQ-DATE                                             
                   EXEC SQL                                                     
                       SELECT  FSCL_MN_BEG_DTE                                  
                              ,FSCL_MN_END_DTE                                  
                         INTO  :DTATTR-FSCL-MN-BEG-DTE                          
                              ,:DTATTR-FSCL-MN-END-DTE                          
                         FROM  TDTATTR                                          
                         WHERE KY_DTE = :RQ-REQ-DATE                            
                   END-EXEC                                                     
                                                                                
                   EVALUATE SQLCODE                                             
                       WHEN +0                                                  
                           DISPLAY '        REQ BEG DTE '                       
                                   DTATTR-FSCL-MN-BEG-DTE                       
                           DISPLAY '        REQ END DTE '                       
                                   DTATTR-FSCL-MN-END-DTE                       
                       WHEN OTHER                                               
                           DISPLAY '  *** C200-PROCESS-REQUEST *** '            
                           DISPLAY '  REQUESTED DATE: ' RQ-REQ-DATE             
                           MOVE 'SELECT FROM DTATTR  '                          
                               TO PV-OPER-ATTEMPTED                             
                           PERFORM Z999-SQL-ABEND                               
                   END-EVALUATE                                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    OPEN FST00003 CURSOR                                                 
      ******************************************************************        
       Z100-OPEN-FST00003.                                                      
                                                                                
           SET NOT-END-OF-FST00003 TO TRUE.                                     
                                                                                
           EXEC SQL                                                             
               OPEN FST00003-CURSOR                                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               DISPLAY '  *** Z100-OPEN-FST00003 ***'                           
               DISPLAY ' PROCESS DATE ' RQ-REQ-DATE                             
               DISPLAY ' BATCH NUMBER ' PV-BATCH-NBR                            
               DISPLAY ' UNIT BKG CDE ' PV-UNIT-BOOKING-CDE                     
               DISPLAY ' TRANS STAT   ' PV-TRANS-STAT-CDE                       
               MOVE 'OPEN FST00003-CURSOR' TO PV-OPER-ATTEMPTED                 
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    FETCH THE NEXT RECORD FROM FST00003 CURSOR                           
      ******************************************************************        
       Z200-FETCH-FST00003.                                                     
                                                                                
           EXEC SQL FETCH FST00003-CURSOR                                       
              INTO :FST00003-ITM-NBR                                            
                  ,:FST00003-LOC-NBR                                            
                  ,:FST00003-ACTN-CDE                                           
                  ,:FST00003-ADJ-QTY                                            
W33304            ,:INVPAR-INV-ID                                               
W33304            ,:INVPAR-PLND-INV-DTE                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-FST00003 TO TRUE                                  
               WHEN OTHER                                                       
                   DISPLAY '  *** Z200-FETCH-FST00003 ***'                      
                   MOVE 'FETCH FST00003-CURSOR' TO PV-OPER-ATTEMPTED            
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    CLOSE FST00003 CURSOR                                                
      ******************************************************************        
       Z300-CLOSE-FST00003.                                                     
                                                                                
           EXEC SQL                                                             
                CLOSE FST00003-CURSOR                                           
           END-EXEC                                                             
           IF SQLCODE NOT = +0                                                  
               DISPLAY '  *** Z300-CLOSE-FST00003 ***'                          
               MOVE 'CLOSE FST00003-CURSOR' TO PV-OPER-ATTEMPTED                
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *    GET SKU STATUS FROM TUPCPLS TABLE                                    
      ******************************************************************        
       Z500-GET-SKU-STATUS.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT  UPC_STAT_CDE                                             
                 INTO  :UPCPLS-UPC-STAT-CDE                                     
                 FROM  TUPCPLS                                                  
                 WHERE ITM_NBR         = :IN009-ITM-NBR                         
                   AND STR_NBR         = :UPCPLS-STR-NBR                        
                   AND EFF_BEG_TMST  <= :PV-PLND-INV-TMST-RDF                   
                   AND (EFF_END_TMST >= :PV-PLND-INV-TMST-RDF                   
                        OR EFF_END_TMST IS NULL)                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   IF UPCPLS-STR-NBR = ZEROES                                   
                       DISPLAY '  *** UNABLE TO FIND SKU STATUS'                
                       DISPLAY '  STORE ' UPCPLS-STR-NBR                        
                               ' ITEM ' IN009-ITM-NBR                           
                               ' INV DATE ' PV-PLND-INV-DTE                     
                               '.  DEFAULT STATUS TO ' PC-DEFAULT-STAT          
                   END-IF                                                       
                   MOVE PC-DEFAULT-STAT        TO UPCPLS-UPC-STAT-CDE           
               WHEN OTHER                                                       
                   DISPLAY '  *** Z500-GET-SKU-STATUS *** '                     
                   DISPLAY '  STORE ' UPCPLS-STR-NBR                            
                           ' ITEM ' IN009-ITM-NBR                               
                           ' INV DATE ' PV-PLND-INV-DTE                         
                   MOVE 'SELECT FROM UPCPLS  ' TO PV-OPER-ATTEMPTED             
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    READ REQUEST CONTROL CARD                                            
      ******************************************************************        
       Z600-READ-REQUEST.                                                       
                                                                                
           READ REQUEST-CNTL-FILE                                               
               INTO RQ-REQ-CNTL-AREA                                            
               AT END                                                           
                   SET END-OF-REQUEST-CNTL TO TRUE.                             
                                                                                
      ******************************************************************        
      *    SAVE/WRITE THE EXTRACT MASTER DETAIL RECORDS                         
      ******************************************************************        
       Z700-WRITE-EXTRACT.                                                      
                                                                                
           WRITE AFT-UNIT-BOOK-ADJ-RECORD FROM IN009-VARIANCE-RECORD.           
                                                                                
           ADD 1 TO PV-RECORD-EXTRACTED.                                        
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT215'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
