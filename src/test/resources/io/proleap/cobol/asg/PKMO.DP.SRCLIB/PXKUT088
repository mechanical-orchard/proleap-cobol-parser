      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.   PXKUT088.                                          00050000
       AUTHOR.       RICH KELLEN.                                       00060000
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070000
       DATE-WRITTEN. APRIL 2007.                                        00080000
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ***************************************************************** 00110000
      * WR30405 - MOS FORMAT HIST,EXTR AND SUM FILES FOR ORACLE       * 00120000
      ***************************************************************** 00130000
      ******************** HISTORY OF CHANGES **************************00140000
      * CHG ID/                                                        *00150000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00160000
      * -------  ----------  ----------------------------------------  *00170000
      * WR31380  06/18/2007  INITIAL IMPLEMENTATION                    *00180000
032310* WR36394  03/23/2010  ADD MKDN CANCEL FLAG TO PXRD090/PXRD095.  *00181001
      ******************************************************************00182000
      ******************************************************************00183000
       ENVIRONMENT DIVISION.                                            00184000
      ******************************************************************00185000
      *                                                                 00186000
       CONFIGURATION SECTION.                                           00187000
       SOURCE-COMPUTER.   IBM-3090.                                     00188000
       OBJECT-COMPUTER.   IBM-3090.                                     00189000
      *                                                                 00190000
       INPUT-OUTPUT SECTION.                                            00200000
       FILE-CONTROL.                                                    00210000
      *                                                                 00220000
            SELECT PRCHG-HIST-FILE                                      00230000
              ASSIGN                        TO INP01.                   00240000
      *                                                                 00250000
            SELECT PRCHG-HIST-SUM-FILE                                  00260000
              ASSIGN                        TO INP02.                   00270000
      *                                                                 00280000
            SELECT PRCHG-EXTR-FILE                                      00290000
              ASSIGN                        TO INP03.                   00300000
      *                                                                 00310000
            SELECT PRCHG-HIST-FILE-OUT                                  00320000
              ASSIGN                        TO OUT01.                   00330000
      *                                                                 00340000
            SELECT PRCHG-HIST-SUM-FILE-OUT                              00350000
              ASSIGN                        TO OUT02.                   00360000
      *                                                                 00370000
            SELECT PRCHG-EXTR-FILE-OUT                                  00380000
              ASSIGN                        TO OUT03.                   00381000
      ******************************************************************00382000
       DATA DIVISION.                                                   00383000
      ******************************************************************00384000
       FILE SECTION.                                                    00385000
      *                                                                 00386000
       FD PRCHG-HIST-FILE                                               00387000
           RECORDING MODE IS F                                          00388000
           BLOCK CONTAINS 0 RECORDS                                     00389000
           DATA RECORD IS PRCHG-HIST-RECORD.                            00390000
                                                                        00400000
       01 PRCHG-HIST-RECORD                      PIC X(121).            00410000
      *                                                                 00420000
       FD PRCHG-HIST-SUM-FILE                                           00430000
           RECORDING MODE IS F                                          00440000
           BLOCK CONTAINS 0 RECORDS                                     00450000
           DATA RECORD IS PRCHG-HIST-SUM-RECORD.                        00460000
                                                                        00470000
032310 01 PRCHG-HIST-SUM-RECORD                  PIC X(049).            00480000
      *                                                                 00490000
       FD PRCHG-EXTR-FILE                                               00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS                                     00520000
           DATA RECORD IS PRCHG-EXTR-FILE-REC.                          00530000
                                                                        00540000
       01 PRCHG-EXTR-RECORD                      PIC X(068).            00550000
      *                                                                 00560000
      *                                                                 00570000
       FD PRCHG-HIST-FILE-OUT                                           00580000
           RECORDING MODE IS F                                          00590000
           BLOCK CONTAINS 0 RECORDS                                     00600000
           DATA RECORD IS PRCHG-HIST-RECORD-OUT.                        00610000
                                                                        00620000
       01 PRCHG-HIST-RECORD-OUT                  PIC X(124).            00630000
      *                                                                 00640000
       FD PRCHG-HIST-SUM-FILE-OUT                                       00650000
           RECORDING MODE IS F                                          00660000
           BLOCK CONTAINS 0 RECORDS                                     00670000
           DATA RECORD IS PRCHG-HIST-SUM-RECORD-OUT.                    00680000
                                                                        00690000
CHG130 01 PRCHG-HIST-SUM-RECORD-OUT              PIC X(078).            00700014
      *                                                                 00710000
       FD PRCHG-EXTR-FILE-OUT                                           00720000
           RECORDING MODE IS F                                          00730000
           BLOCK CONTAINS 0 RECORDS                                     00740000
           DATA RECORD IS PRCHG-EXTR-FILE-REC-OUT.                      00750000
                                                                        00760000
       01 PRCHG-EXTR-RECORD-OUT                  PIC X(071).            00770000
      *                                                                 00771000
       WORKING-STORAGE SECTION.                                         00772000
                                                                        00773000
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO      00774000
                                                        COMP SYNC.      00775000
           88  ABEND-4001-WRITE-ERROR                      VALUE +4001. 00776000
           88  ABEND-4002-READ-ERROR                       VALUE +4002. 00777000
           88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003. 00778000
           88  ABEND-4004-TABLE-ERROR                      VALUE +4004. 00779000
           88  ABEND-4005-OPEN-ERROR                       VALUE +4005. 00780000
           88  ABEND-4006-CLOSE-ERROR                      VALUE +4006. 00790000
           88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007. 00800000
           88  ABEND-4008-DATS-ERROR                       VALUE +4008. 00810000
           88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009. 00820000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 00830000
      *                                                                 00840000
       01 PC-PROGRAM-CONSTANTS.                                         00850000
          05  PC-PXKUT088                 PIC X(10)      VALUE          00860000
              'PXKUT088  '.                                             00870000
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)                     00880000
               VALUE 'PXKUT088'.                                        00890000
      *                                                                 00900000
       01 PS-PROGRAM-SWITCHES.                                          00910000
          05  END-OF-HIST-IN-FILE-SW   PIC X(1) VALUE 'N'.              00920000
              88  PS-END-OF-HIST-IN-FILE           VALUE 'Y'.           00930000
          05  END-OF-SUM-IN-FILE-SW    PIC X(1) VALUE 'N'.              00940000
              88  PS-END-OF-SUM-IN-FILE            VALUE 'Y'.           00950000
          05  END-OF-EXTR-IN-FILE-SW   PIC X(1) VALUE 'N'.              00960000
              88  PS-END-OF-EXTR-IN-FILE           VALUE 'Y'.           00970000
CHG130                                                                  00989515
CHG130 01  PC-PROGRAM-VARIABLE.                                         00989614
CHG130    05  PV-PARAGRAPH-NAME             PIC X(30) VALUE SPACES.     00989714
CHG130    05  PV-OPERATION-MSG-1            PIC X(40) VALUE SPACES.     00989814
CHG130    05  PV-OPERATION-MSG-2            PIC X(40) VALUE SPACES.     00989914
CHG130    05  PV-DB2-OPERATION              PIC X(30) VALUE SPACE.      00990014
CHG130*-----------------------------------------------------------      00990114
CHG130* VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         00990214
CHG130*-----------------------------------------------------------      00990314
CHG130     COPY DPWS004.                                                00990414
CHG130                                                                  00990514
CHG130*-----------------------------------------------------------      00990614
CHG130* DB2 ABEND COPYBOOK                                              00990714
CHG130*-----------------------------------------------------------      00990814
CHG130     COPY DPWS900.                                                00990914
CHG130                                                                  00991014
CHG130*-----------------------------------------------------------      00991114
CHG130* DB2 MESSAGE AREA                                                00991214
CHG130*-----------------------------------------------------------      00991314
CHG130     COPY SQLCA2.                                                 00991414
CHG130                                                                  00991514
CHG130*-----------------------------------------------------------      00991814
CHG130* DB2 COMMUNICATIONS AREA                                         00991914
CHG130*-----------------------------------------------------------      00992014
CHG130     EXEC SQL                                                     00992114
CHG130          INCLUDE SQLCA                                           00992214
CHG130     END-EXEC.                                                    00992314
CHG130******************************************************************00992415
      ******************************************************************00993000
      * COPY MEMBER FOR THE MOS PXT_PRCHG_HIST TABLE UPDATE FILE        01000000
      ******************************************************************01010000
      *                                                                 01020000
           COPY PXRD089.                                                01030000
      ******************************************************************01040000
      * COPY MEMBER FOR THE MOS PRCHG_HIST_SUM TABLE UPDATE FILE        01050000
      ******************************************************************01060000
      *                                                                 01070000
           COPY PXRD090.                                                01080000
      ******************************************************************01090000
      * COPY MEMBER FOR THE MOS PXT_PRCHG_EXTR TABLE UPDATE FILE        01100000
      ******************************************************************01110000
      *                                                                 01120000
           COPY PXRD091.                                                01130000
                                                                        01140000
      ******************************************************************01150000
      * COPY MEMBER FOR THE MOS PXT_PRCHG_HIST TABLE UPDATE FILE        01160000
      ******************************************************************01170000
      *                                                                 01180000
           COPY PXRD094.                                                01190000
      ******************************************************************01200000
      * COPY MEMBER FOR THE MOS PRCHG_HIST_SUM TABLE UPDATE FILE        01210000
      ******************************************************************01220000
      *                                                                 01230000
           COPY PXRD095.                                                01240000
      ******************************************************************01250000
      * COPY MEMBER FOR THE MOS PXT_PRCHG_EXTR TABLE UPDATE FILE        01260000
      ******************************************************************01270000
      *                                                                 01280000
           COPY PXRD096.                                                01290000
                                                                        01300000
      ******************************************************************01310000
       PROCEDURE DIVISION.                                              01320000
      ******************************************************************01330000
      *                                                                 01340000
          EJECT                                                         01350000
       0000-MAINLINE-PROCESSING.                                        01360000
      ******************************************************************01370000
      * MAIN PROCESSING ROUTINE                                       * 01380000
      ******************************************************************01390000
                                                                        01400000
            PERFORM 1000-INIT.                                          01410000
            PERFORM 8000-READ-PRCHG-HIST-FILE.                          01420000
            PERFORM 2000-PROCESS-HIST-FILE                              01430000
              UNTIL PS-END-OF-HIST-IN-FILE.                             01440000
                                                                        01450000
            PERFORM 8010-READ-PRCHG-HIST-SUM-FILE.                      01460000
            PERFORM 2010-PROCESS-HIST-SUM-FILE                          01470000
              UNTIL PS-END-OF-SUM-IN-FILE.                              01480000
                                                                        01490000
            PERFORM 8020-READ-PRCHG-EXTR-FILE.                          01500000
            PERFORM 2020-PROCESS-EXTR-FILE                              01510000
              UNTIL PS-END-OF-EXTR-IN-FILE.                             01520000
                                                                        01530000
            PERFORM 9000-END-OF-JOB.                                    01540000
                                                                        01550000
            GOBACK.                                                     01560000
                                                                        01570000
      ******************************************************************01580000
      * OPEN FILES GET CURRENT DATE AND TIME FROM DB2.     *            01590000
      ******************************************************************01600000
      *                                                                 01610000
       1000-INIT.                                                       01620000
           OPEN INPUT  PRCHG-HIST-FILE                                  01630000
                       PRCHG-HIST-SUM-FILE                              01640000
                       PRCHG-EXTR-FILE                                  01650000
                OUTPUT PRCHG-HIST-FILE-OUT                              01660000
                       PRCHG-HIST-SUM-FILE-OUT                          01670000
                       PRCHG-EXTR-FILE-OUT.                             01680000
                                                                        01690000
                                                                        01700000
      ******************************************************************01710000
      * PROCESS INPUT FILES                                             01720000
      ******************************************************************01730000
       2000-PROCESS-HIST-FILE.                                          01740000
           MOVE PXRD089-UNT-OF-WRK-DEV-ID TO PXRD094-UNT-OF-WRK-DEV-ID. 01750000
           MOVE PXRD089-ASSOC-ID          TO PXRD094-ASSOC-ID.          01760000
           MOVE PXRD089-UOW-BEG-TMST      TO PXRD094-UOW-BEG-TMST.      01770000
           MOVE PXRD089-STR-NBR           TO PXRD094-STR-NBR.           01780000
           MOVE PXRD089-SKU-NBR           TO PXRD094-SKU-NBR.           01790000
           MOVE PXRD089-UOW-TYP-CDE       TO PXRD094-UOW-TYP-CDE.       01800000
           MOVE PXRD089-CRE-TMST          TO PXRD094-CRE-TMST.          01810000
           MOVE PXRD089-PRCD-UNT-QTY      TO PXRD094-PRCD-UNT-QTY.      01820000
           MOVE PXRD089-OLD-UNT-RTL-AMT   TO PXRD094-OLD-UNT-RTL-AMT.   01830000
           MOVE PXRD089-NEW-UNT-RTL-AMT   TO PXRD094-NEW-UNT-RTL-AMT.   01840000
           PERFORM 8140-PRCHG-HIST-FILE-OUT.                            01850000
           PERFORM 8000-READ-PRCHG-HIST-FILE.                           01860000
                                                                        01870000
       2010-PROCESS-HIST-SUM-FILE.                                      01880000
           MOVE PXRD090-BUS-AREA-ID       TO PXRD095-BUS-AREA-ID.       01890000
           MOVE PXRD090-BUS-GP-NBR        TO PXRD095-BUS-GP-NBR.        01900000
           MOVE PXRD090-DEPT-NBR          TO PXRD095-DEPT-NBR.          01910000
           MOVE PXRD090-MAJ-CL-NBR        TO PXRD095-MAJ-CL-NBR.        01920000
           MOVE PXRD090-STR-NBR           TO PXRD095-STR-NBR.           01930000
           MOVE PXRD090-UOW-TYP-CDE       TO PXRD095-UOW-TYP-CDE.       01940000
           MOVE PXRD090-SYS-GEND-IND      TO PXRD095-SYS-GEND-IND.      01950000
           MOVE PXRD090-SCND-UNT-QTY      TO PXRD095-SCND-UNT-QTY.      01960000
           MOVE PXRD090-OLD-UNT-RTL-AMT   TO PXRD095-OLD-UNT-RTL-AMT.   01970000
           MOVE PXRD090-NEW-UNT-RTL-AMT   TO PXRD095-NEW-UNT-RTL-AMT.   01980000
032310     MOVE PXRD090-MKDN-CANC-IND     TO PXRD095-MKDN-CANC-IND.     01990000
CHG130     PERFORM 2015-GET-TIME-STAMP.                                 01991014
           PERFORM 8150-PRCHG-HIST-SUM-FILE-OUT.                        02000000
           PERFORM 8010-READ-PRCHG-HIST-SUM-FILE.                       02010000
CHG130******************************************************************02011014
CHG130* GET TIMESTAMP FOR CRE_TMST FIELD FOR HISTORY SUM TABLES.        02012014
CHG130******************************************************************02013014
CHG130                                                                  02020014
CHG130 2015-GET-TIME-STAMP.                                             02030014
CHG130     EXEC SQL                                                     02030114
CHG130          SELECT CURRENT TIMESTAMP - 1 DAYS                       02030214
CHG130          INTO  :PXRD095-CRE-TMST                                 02030314
CHG130          FROM   SYSIBM.SYSDUMMY1                                 02030414
CHG130     END-EXEC.                                                    02030514
CHG130                                                                  02030614
CHG130     IF SQLCODE NOT EQUAL ZERO                                    02030714
CHG130        MOVE '2015-GET-TIME-STAMP' TO PV-PARAGRAPH-NAME           02030814
CHG130        MOVE 'CANNOT GET CURRENT TIMESTAMP'                       02030914
CHG130                                      TO PV-DB2-OPERATION         02031014
CHG130        PERFORM 9100-SQL-ABEND                                    02031114
CHG130     END-IF.                                                      02031214
CHG130                                                                  02031314
                                                                        02031406
       2020-PROCESS-EXTR-FILE.                                          02032006
           MOVE PXRD091-STR-NBR           TO PXRD096-STR-NBR.           02040000
           MOVE PXRD091-SKU-NBR           TO PXRD096-SKU-NBR.           02050000
           MOVE PXRD091-UOW-TYP-CDE       TO PXRD096-UOW-TYP-CDE.       02060000
           MOVE PXRD091-CRE-TMST          TO PXRD096-CRE-TMST.          02070000
           MOVE PXRD091-PRCD-UNT-QTY      TO PXRD096-PRCD-UNT-QTY.      02080000
           MOVE PXRD091-OLD-UNT-RTL-AMT   TO PXRD096-OLD-UNT-RTL-AMT.   02090000
           MOVE PXRD091-NEW-UNT-RTL-AMT   TO PXRD096-NEW-UNT-RTL-AMT.   02100000
           PERFORM 8160-PRCHG-EXTR-FILE-OUT.                            02110000
           PERFORM 8020-READ-PRCHG-EXTR-FILE.                           02120000
                                                                        02130000
      ******************************************************************02140000
      *  READ PRCHG-HIST-FILE                                         * 02150000
      ******************************************************************02160000
       8000-READ-PRCHG-HIST-FILE.                                       02170000
           READ PRCHG-HIST-FILE INTO PXRD089-PRCHG-HIST-UPD-FILE        02180000
             AT END                                                     02190000
               SET PS-END-OF-HIST-IN-FILE TO TRUE.                      02200000
                                                                        02210000
      ******************************************************************02220000
      *  READ PRCHG-HIST-SUM-FILE                                     * 02230000
      ******************************************************************02240000
       8010-READ-PRCHG-HIST-SUM-FILE.                                   02250000
           READ PRCHG-HIST-SUM-FILE INTO PXRD090-PRCHG-HIST-SUM-FILE    02260000
             AT END                                                     02270000
               SET PS-END-OF-SUM-IN-FILE TO TRUE.                       02280000
                                                                        02290000
      ******************************************************************02300000
      *  READ PRCHG-EXTR-FILE                                         * 02310000
      ******************************************************************02320000
       8020-READ-PRCHG-EXTR-FILE.                                       02330000
           READ PRCHG-EXTR-FILE INTO PXRD091-PRCHG-EXTR-UPD-FILE        02340000
             AT END                                                     02350000
               SET PS-END-OF-EXTR-IN-FILE TO TRUE.                      02360000
                                                                        02370000
      ******************************************************************02380000
      *  WRITE PRCHG-HIST-FILE-OUT                                     *02390000
      ******************************************************************02400000
       8140-PRCHG-HIST-FILE-OUT.                                        02410000
           WRITE PRCHG-HIST-RECORD-OUT FROM                             02420000
             PXRD094-PRCHG-HIST-UPD-FILE.                               02430000
                                                                        02440000
      ******************************************************************02450000
      *  WRITE PRCHG-HIST-SUM-FILE-OUT                                * 02460000
      ******************************************************************02470000
       8150-PRCHG-HIST-SUM-FILE-OUT.                                    02480000
           WRITE PRCHG-HIST-SUM-RECORD-OUT FROM                         02490000
             PXRD095-PRCHG-HIST-SUM-FILE.                               02500000
                                                                        02510000
      ******************************************************************02520000
      *  WRITE PRCHG-EXTR-FILE-OUT                                    * 02530000
      ******************************************************************02540000
       8160-PRCHG-EXTR-FILE-OUT.                                        02550000
           WRITE PRCHG-EXTR-RECORD-OUT FROM                             02560000
             PXRD096-PRCHG-EXTR-UPD-FILE.                               02570000
                                                                        02580000
      ******************************************************************02592000
      *  CLOSE FILES                                                   *02600000
      ******************************************************************02610000
       9000-END-OF-JOB.                                                 02620000
           CLOSE PRCHG-HIST-FILE                                        02630000
                 PRCHG-HIST-SUM-FILE                                    02640000
                 PRCHG-EXTR-FILE                                        02650000
                 PRCHG-HIST-FILE-OUT                                    02660000
                 PRCHG-HIST-SUM-FILE-OUT                                02670000
                 PRCHG-EXTR-FILE-OUT.                                   02680000
CHG130*---------------------------------------------------------        02690014
CHG130* 9100-SQL-ABEND.                                                 02700014
CHG130*     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.02710014
CHG130*---------------------------------------------------------        02720014
CHG130 9100-SQL-ABEND.                                                  02730014
CHG130                                                                  02740014
CHG130     SET ABEND-4013-DB2-ERROR TO TRUE.                            02750014
CHG130                                                                  02760014
CHG130     DISPLAY '** ABEND     **'.                                   02770014
CHG130     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        02780014
CHG130     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02790014
CHG130     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02800014
CHG130                                                                  02810014
CHG130     COPY DPPD004.                                                02820014
CHG130     CALL 'ILBOABN0' USING ABEND-CODE.                            02830014
CHG130                                                                  02840014
