      *----------------------------------------------------------       00000100
       IDENTIFICATION DIVISION.                                         00000200
      *----------------------------------------------------------       00000300
       PROGRAM-ID.    PXKUT094.                                         00000400
       AUTHOR.        DAVID WELLER.                                     00000500
       DATE-WRITTEN.  SEP, 2009.                                        00000600
                                                                        00000700
      *----------------------------------------------------------       00000800
      * RETURN 4091 WHEN THERE IS NO EVENT NO-OF-DAYS FROM THE          00000900
      *             RUN DATE                                            00001000
      * THIS PROGRAM WAS CLONED FROM PXKUT093, BUT USES RUN DATE        00001100
      * INSTEAD OF CURRENT DATE                                         00001200
      *----------------------------------------------------------       00001300
      * HISTORY LOG:                                                    00001400
      *                                                                 00001500
      *----------------------------------------------------------       00001600
      * DATE:                                                           00001700
      * WR/IR#:                                                         00001800
      * PROGRAMMER:                                                     00001900
      * DESCRIPTION:                                                    00002000
      *                                                                 00002100
      *----------------------------------------------------------       00002200
       ENVIRONMENT DIVISION.                                            00003000
      *----------------------------------------------------------       00004000
       CONFIGURATION SECTION.                                           00005000
       SOURCE-COMPUTER. IBM-3090.                                       00006000
       OBJECT-COMPUTER. IBM-3090.                                       00007000
                                                                        00008000
       INPUT-OUTPUT SECTION.                                            00009000
                                                                        00010000
       FILE-CONTROL.                                                    00020000
                                                                        00030000
           SELECT NUMBER-OF-DAYS-FILE                                   00040000
               ASSIGN TO INP01.                                         00050000
                                                                        00060000
           SELECT CTG-TYP-FILE                                          00070000
               ASSIGN TO INP02.                                         00080000
                                                                        00081000
           SELECT RUN-DATE-FILE                                         00082000
               ASSIGN TO INP03.                                         00083001
                                                                        00090000
      *----------------------------------------------------------       00100000
       DATA DIVISION.                                                   00110000
      *----------------------------------------------------------       00120000
       FILE SECTION.                                                    00130000
                                                                        00140000
       FD  NUMBER-OF-DAYS-FILE                                          00150000
           RECORDING MODE IS F                                          00160000
           LABEL RECORDS ARE STANDARD                                   00170000
           RECORD CONTAINS 80 CHARACTERS                                00180000
           BLOCK CONTAINS 0 RECORDS                                     00190000
           DATA RECORD IS NUMBER-OF-DAYS-RECORD.                        00200000
       01  NUMBER-OF-DAYS-RECORD       PIC X(80).                       00210000
                                                                        00220000
       FD  CTG-TYP-FILE                                                 00230000
           RECORDING MODE IS F                                          00240000
           LABEL RECORDS ARE STANDARD                                   00250000
           RECORD CONTAINS 80 CHARACTERS                                00260000
           BLOCK CONTAINS 0 RECORDS                                     00270000
           DATA RECORD IS CTG-TYP-RECORD.                               00280000
       01  CTG-TYP-RECORD              PIC X(80).                       00290000
                                                                        00301001
       FD  RUN-DATE-FILE                                                00302001
           RECORDING MODE IS F                                          00303001
           LABEL RECORDS ARE STANDARD                                   00304001
           RECORD CONTAINS 80 CHARACTERS                                00305001
           BLOCK CONTAINS 0 RECORDS                                     00306001
           DATA RECORD IS RUN-DATE-RECORD.                              00307001
       01  RUN-DATE-RECORD             PIC X(80).                       00308001
      *---------------------------------------------------------        00310000
                                                                        00320000
       WORKING-STORAGE SECTION.                                         00330000
      *---------------------------------------------------------        00340000
      * PC- PROGRAM CONSTANTS                                           00350000
      *----------------------------------------------------------       00360000
       01  PC-PROGRAM-CONSTANTS.                                        00370000
           05  PC-PROGRAM-NAME              PIC X(08) VALUE 'PXKUT094'. 00380000
           05  PC-DAYS-FR                   PIC S9(3)  VALUE +0.        00390000
                                                                        00430000
      *----------------------------------------------------------       00440000
      * PS- PROGRAM SWITCHES                                            00450000
      *----------------------------------------------------------       00460000
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-NUMBER-OF-DAYS-FILE-SW    PIC  X(01)  VALUE 'N'.      00480000
               88  PS-NUMBER-OF-DAYS-FILE-END           VALUE 'Y'.      00490000
               88  PS-NUMBER-OF-DAYS-FILE-START         VALUE 'N'.      00500000
           05  PS-RUN-DATE-FILE-SW          PIC  X(01)  VALUE 'N'.      00501001
               88  PS-RUN-DATE-FILE-END                 VALUE 'Y'.      00502001
               88  PS-RUN-DATE-FILE-START               VALUE 'N'.      00503001
           05  PS-CTG-TYP-FILE-SW           PIC  X(01)  VALUE 'N'.      00510000
               88  PS-CTG-TYP-FILE-END                  VALUE 'Y'.      00520000
               88  PS-CTG-TYP-FILE-START                VALUE 'N'.      00530000
           05  PS-PRCHG-FOUND-SW            PIC  X(01)  VALUE 'N'.      00540000
               88  PS-PRCHG-FOUND                       VALUE 'Y'.      00550000
               88  PS-PRCHG-NOT-FOUND                   VALUE 'N'.      00560000
                                                                        00570000
      *----------------------------------------------------------       00580000
      * PV- PROGRAM VARIABLES                                           00590000
      *----------------------------------------------------------       00600000
       01 PV-PROGRAM-VARIABLES.                                         00610000
          05  ABEND-CODE              PIC S9(4)  VALUE ZEROS COMP SYNC. 00620000
          05  PV-RUN-DATE                   PIC X(10).                  00630001
          05  PV-DAYS-FR-DATE               PIC X(10).                  00640000
          05  PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.   00650000
          05  PV-EVNT-COUNT                 PIC S9(4)  VALUE +0 COMP.   00660000
                                                                        00670000
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.   00680000
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.   00690000
                                                                        00700000
      ***************************************************************** 00710000
      *  PARAMETER IN RECORD LAYOUT                                     00720000
      ***************************************************************** 00730000
      *01  PX035-PARAMETER-RECORD.                                      00740000
           COPY PXRD035.                                                00750000
                                                                        00760000
      ***************************************************************** 00770000
      *  PARAMETER IN RECORD LAYOUT                                     00780000
      ***************************************************************** 00790000
      *01  PX092-PARAMETER-RECORD.                                      00800000
           COPY PXRD092.                                                00810000
                                                                        00811001
      ***************************************************************** 00812001
      *  RUN DATE IN RECORD LAYOUT                                      00813001
      ***************************************************************** 00814001
      *01  PX110-RUN-DATE-RECORD.                                       00815001
           COPY PXRD110.                                                00816001
                                                                        00820000
      *----------------------------------------------------------       00830000
      * DATE ROUTINE COPY MEMBERS                                       00840000
      *----------------------------------------------------------       00850000
           COPY DPWS500.                                                00860000
                                                                        00870000
      *----------------------------------------------------------       00880000
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         00890000
      *----------------------------------------------------------       00900000
           COPY DPWS004.                                                00910000
                                                                        00920000
      *---------------------------------------------                    00930000
      *  DB2 COMMUNICATION AREA.                                        00940000
      *---------------------------------------------                    00950000
           EXEC SQL                                                     00960000
                INCLUDE SQLCA                                           00970000
           END-EXEC.                                                    00980000
                                                                        00990000
           COPY SQLCA2.                                                 01000000
                                                                        01010000
      *---------------------------------------------                    01020000
      *  DB2 TABLE XST00063 (XST_PRCHG)                                 01030000
      *---------------------------------------------                    01040000
           EXEC SQL                                                     01050000
                INCLUDE XST00063                                        01060000
           END-EXEC.                                                    01070000
                                                                        01080000
      *---------------------------------------------                    01090000
      *  DB2 TABLE XST00064 (XST_STR_GP_PRCHG)                          01100000
      *---------------------------------------------                    01110000
           EXEC SQL                                                     01120000
                INCLUDE XST00064                                        01130000
           END-EXEC.                                                    01140000
                                                                        01150000
      *----------------------------------------------------------       01160000
       PROCEDURE DIVISION.                                              01170000
      *----------------------------------------------------------       01180000
       0000-MAINLINE.                                                   01190000
                                                                        01200000
           PERFORM 1000-INITIALIZATION.                                 01210000
                                                                        01220000
           PERFORM 2000-FIND-EVNT                                       01230000
              UNTIL PS-CTG-TYP-FILE-END.                                01250000
                                                                        01260000
           PERFORM 9999-WRAPUP.                                         01270000
                                                                        01280000
           GOBACK.                                                      01290000
                                                                        01330000
      *----------------------------------------------------------       01340000
      * 1000-INITIALIZATION.                                            01350000
      *     INTIALIZES OR MOVES ANY VARIABLES BEFORE IT WILL            01360000
      *     UPDATE OR INSERT A RECORD.                                  01370000
      *----------------------------------------------------------       01380000
       1000-INITIALIZATION.                                             01390000
                                                                        01400000
           OPEN INPUT NUMBER-OF-DAYS-FILE.                              01410000
           OPEN INPUT CTG-TYP-FILE.                                     01420000
           OPEN INPUT RUN-DATE-FILE.                                    01421001
                                                                        01430000
           PERFORM 2010-COMPUTE-STRT-DTE.                               01440003
                                                                        01460000
           PERFORM 2020-READ-CTG-TYP.                                   01470003
                                                                        01520000
      *----------------------------------------------------------       01530000
      * 2000-FIND-EVNT.                                                 01540000
      *     THIS LOOKS FOR AND EVENT WITH                               01550000
      *     1)  EVNT_CTG_CDE = CTG-CDE ON CTG-TYP-FILE                  01560000
      *     2)  PRCHG-TYP-CDE = TYP-CDE ON CTG-TYP-FILE                 01570000
      *     3)  PRCHG-EFF-DTE = CURRENT DATE + DAYS-FR-EVNT-NBR         01580000
      *   FINDING THE EVENT MEANS THE PROGRAM RETURNS 0.                01590000
      *   NOT FINDING THE EVENT MEANS THE PROGRAM RETURNS 4091.         01600000
      *----------------------------------------------------------       01610000
       2000-FIND-EVNT.                                                  01620000
                                                                        01630000
           MOVE PX092-CTG-CDE          TO XST00063-EVNT-CTG-CDE.        01640000
           MOVE PX092-TYP-CDE          TO XST00063-PRCHG-TYP-CDE.       01650000
           MOVE PV-DAYS-FR-DATE        TO XST00064-PRCHG-EFF-DTE.       01660000
                                                                        01670000
           EXEC SQL                                                     01680000
                SELECT COUNT(*)                                         01690000
                INTO  :PV-EVNT-COUNT                                    01700000
                FROM   XST_PRCHG         A                              01710000
                     , XST_STR_GP_PRCHG  B                              01720000
                WHERE  A.EVNT_CTG_CDE    =  :XST00063-EVNT-CTG-CDE      01730000
                  AND  A.PRCHG_TYP_CDE   =  :XST00063-PRCHG-TYP-CDE     01740000
                  AND  B.PRCHG_ID        =  A.PRCHG_ID                  01750000
                  AND  B.PRCHG_EFF_DTE   =  :XST00064-PRCHG-EFF-DTE     01760000
                  AND  A.LAST_ACTN_CDE   <> 'D'                         01761004
                  AND  B.LAST_ACTN_CDE   <> 'D'                         01762004
                WITH UR                                                 01770000
           END-EXEC.                                                    01780000
                                                                        01790000
           EVALUATE TRUE                                                01800000
              WHEN SQLCODE = +0                                         01810000
                  IF PV-EVNT-COUNT > 0                                  01820000
                      DISPLAY '*** FOUND ***'                           01830000
                      DISPLAY 'EVNT-CTG-CDE:  ' XST00063-EVNT-CTG-CDE   01840000
                      DISPLAY 'PRCHG-TYP-CDE: ' XST00063-PRCHG-TYP-CDE  01850000
                      DISPLAY 'PRCHG-EFF-DTE: ' XST00064-PRCHG-EFF-DTE  01860000
                      DISPLAY '***'                                     01870000
                      SET PS-PRCHG-FOUND TO TRUE                        01880000
                  ELSE                                                  01890000
                      DISPLAY '*** NOT FOUND ***'                       01900000
                      DISPLAY 'EVNT-CTG-CDE:  ' XST00063-EVNT-CTG-CDE   01910000
                      DISPLAY 'PRCHG-TYP-CDE: ' XST00063-PRCHG-TYP-CDE  01920000
                      DISPLAY 'PRCHG-EFF-DTE: ' XST00064-PRCHG-EFF-DTE  01930000
                      DISPLAY '***'                                     01940000
                  END-IF                                                01950000
              WHEN SQLCODE = +100                                       01960000
                DISPLAY '*** NOT FOUND ***'                             01970000
                DISPLAY 'EVNT-CTG-CDE:  ' XST00063-EVNT-CTG-CDE         01980000
                DISPLAY 'PRCHG-TYP-CDE: ' XST00063-PRCHG-TYP-CDE        01981000
                DISPLAY 'PRCHG-EFF-DTE: ' XST00064-PRCHG-EFF-DTE        01981100
                DISPLAY '***'                                           01981200
              WHEN SQLCODE = -304                                       01981300
                DISPLAY '*** FOUND ***'                                 01981400
                DISPLAY 'EVNT-CTG-CDE:  ' XST00063-EVNT-CTG-CDE         01981500
                DISPLAY 'PRCHG-TYP-CDE: ' XST00063-PRCHG-TYP-CDE        01981600
                DISPLAY 'PRCHG-EFF-DTE: ' XST00064-PRCHG-EFF-DTE        01981700
                DISPLAY '***'                                           01981800
                SET PS-PRCHG-FOUND TO TRUE                              01981900
              WHEN OTHER                                                01982000
                DISPLAY 'SELECT FAILED FOR TABLES'                      01983000
                DISPLAY 'EVNT-CTG-CDE:  ' XST00063-EVNT-CTG-CDE         01983100
                DISPLAY 'PRCHG-TYP-CDE: ' XST00063-PRCHG-TYP-CDE        01983200
                DISPLAY 'PRCHG-EFF-DTE: ' XST00064-PRCHG-EFF-DTE        01983300
                MOVE SQLCODE       TO RETURN-CODE                       01983400
                PERFORM U9980-SQL-ERROR                                 01983500
           END-EVALUATE.                                                01983700
                                                                        01983800
           READ CTG-TYP-FILE INTO PX092-PARAMETER-RECORD                01983900
              AT END                                                    01984000
                 SET PS-CTG-TYP-FILE-END TO TRUE                        01985000
           END-READ.                                                    01986000
                                                                        01990000
       2010-COMPUTE-STRT-DTE.                                           02000000
                                                                        02010000
           READ NUMBER-OF-DAYS-FILE INTO PX035-PARAMETER-RECORD         02020000
              AT END                                                    02030000
                 SET PS-NUMBER-OF-DAYS-FILE-END TO TRUE                 02040000
           END-READ.                                                    02050000
                                                                        02060000
           IF PS-NUMBER-OF-DAYS-FILE-END                                02070000
               MOVE 'NO DAY FILE TO PROCESS'                            02080000
                                       TO PV-OPERATION-MSG-1            02090000
               PERFORM Z910-ABEND                                       02100000
           ELSE                                                         02110000
               DISPLAY 'NBR-OF-DAYS = ' PX035-DAYS-FR-EVNT-NBR          02120000
           END-IF.                                                      02130000
                                                                        02140000
           IF PX035-SIGN = '+' OR PX035-SIGN = ' '                      02150000
               MOVE PX035-DAYS         TO PC-DAYS-FR                    02160000
           ELSE                                                         02170000
               COMPUTE PC-DAYS-FR = PX035-DAYS * -1                     02180000
           END-IF.                                                      02190000
                                                                        02251001
           READ RUN-DATE-FILE       INTO PX110-RUN-DATE-RECORD          02252001
              AT END                                                    02253001
                 SET PS-RUN-DATE-FILE-END TO TRUE                       02254001
           END-READ.                                                    02255001
                                                                        02256001
           IF PS-RUN-DATE-FILE-END                                      02257001
               MOVE 'NO RUN DATE FILE TO PROCESS'                       02258001
                                       TO PV-OPERATION-MSG-1            02259001
               PERFORM Z910-ABEND                                       02259101
           ELSE                                                         02259201
               MOVE PX110-RUN-DATE     TO PV-RUN-DATE                   02259301
               DISPLAY 'RUN-DATE = ' PX110-RUN-DATE                     02259401
           END-IF.                                                      02259501
                                                                        02260000
           IF PX035-SIGN = '+' OR PX035-SIGN = ' '                      02270000
               PERFORM 2011-INCREMENT-DATE                              02280000
           ELSE                                                         02300000
               PERFORM 2012-DECREMENT-DATE                              02301000
           END-IF.                                                      02303000
                                                                        02304000
       2011-INCREMENT-DATE.                                             02309000
                                                                        02310000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02320000
           MOVE PV-RUN-DATE              TO DPG52-LK-DATE-INPUT.        02330001
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       02340000
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       02350000
           SET DPG51-INCREMENT-DATE      TO TRUE.                       02360000
           MOVE PC-DAYS-FR               TO DPG51-INCR-DECR-DAYS-9.     02370000
           PERFORM 9999-DATE-ROUTINE.                                   02380000
           MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-DAYS-FR-DATE.            02390000
                                                                        02400000
           DISPLAY 'PV-DAYS-FR-DATE: ' PV-DAYS-FR-DATE.                 02410000
                                                                        02450000
       2012-DECREMENT-DATE.                                             02460000
                                                                        02470000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              02480000
           MOVE PV-RUN-DATE              TO DPG52-LK-DATE-INPUT.        02490001
           SET DPG51-FISCAL-454-CALENDAR TO TRUE.                       02491000
           SET DPG52-LK-DTE-ISO-FMT      TO TRUE.                       02491100
           SET DPG51-DECREMENT-DATE      TO TRUE.                       02491200
           MOVE PC-DAYS-FR               TO DPG51-INCR-DECR-DAYS-9.     02491300
           PERFORM 9999-DATE-ROUTINE.                                   02491400
           MOVE DPG55-DB2-ISO-DATE-FMT   TO PV-DAYS-FR-DATE.            02491500
                                                                        02491600
           DISPLAY 'PV-DAYS-FR-DATE: ' PV-DAYS-FR-DATE.                 02491700
                                                                        02493000
       2020-READ-CTG-TYP.                                               02493100
                                                                        02493200
           READ CTG-TYP-FILE INTO PX092-PARAMETER-RECORD                02493300
              AT END                                                    02493400
                 SET PS-CTG-TYP-FILE-END TO TRUE                        02493500
           END-READ.                                                    02493600
                                                                        02493700
           IF PS-CTG-TYP-FILE-END                                       02493800
               MOVE 'NO CTG TYP FILE TO PROCESS'                        02493900
                                       TO PV-OPERATION-MSG-1            02494000
               PERFORM Z910-ABEND                                       02494100
           ELSE                                                         02494200
               DISPLAY 'CTG-CDE = ' PX092-CTG-CDE                       02494300
               DISPLAY 'TYP-CDE = ' PX092-TYP-CDE                       02494400
           END-IF.                                                      02494500
                                                                        02494900
                                                                        02496000
      *----------------------------------------------------------       02497000
      * 9999-WRAPUP.                                                    02498000
      *     MOVES ALL ERROR CODES TO THE LINKAGE VARIABLES TO           02499000
      *     BE PASSED BACK TO THE CALLING PROGRAM.  EXITS THE           02500000
      *     PROGRAM.                                                    02510000
      *----------------------------------------------------------       02520000
*                                                                       02530000
      *----------------------------------------------------------       02540000
       U9980-SQL-ERROR.                                                 02550000
      *----------------------------------------------------------       02560000
                                                                        02570000
           MOVE SQLCODE    TO SQLCODE2.                                 02580000
           MOVE SQLERRD(3) TO SQLERRD3.                                 02590000
           DISPLAY '** ABEND **       ** = SQLWARNING'.                 02600000
           DISPLAY '** PROGRAM        ** = PXKUT094'.                   02610000
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  02620000
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  02630000
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   02640000
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  02650000
                                                                        02660000
           MOVE 4013 TO ABEND-CODE.                                     02670000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02680000
                                                                        02720000
      *----------------------------------------------------------       02730000
       U9990-SQL-WARNING.                                               02740000
      *----------------------------------------------------------       02750000
                                                                        02760000
           MOVE SQLCODE    TO SQLCODE2.                                 02770000
           MOVE SQLERRD(3) TO SQLERRD3.                                 02780000
           DISPLAY '** ABEND **       ** = SQLWARNING'.                 02790000
           DISPLAY '** PROGRAM        ** = PXKUT094'.                   02800000
                                                                        02810000
           MOVE 4013 TO ABEND-CODE.                                     02820000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02830000
                                                                        02870000
       9999-WRAPUP.                                                     02880000
           CLOSE NUMBER-OF-DAYS-FILE.                                   02890000
           CLOSE CTG-TYP-FILE.                                          02900000
           CLOSE RUN-DATE-FILE.                                         02901001
                                                                        02910000
           IF PS-PRCHG-NOT-FOUND                                        02920000
               MOVE +4091              TO RETURN-CODE                   02930000
           END-IF.                                                      02940000
                                                                        02980000
      ******************************************************************02990000
      **RUN DATE ROUTINE                                                03000000
      ******************************************************************03010000
       9999-DATE-ROUTINE.                                               03020000
                                                                        03030000
           CALL DP500-KOHLS-CALENDAR-ROUTINE                            03040000
                      USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.        03050000
                                                                        03060000
           EVALUATE TRUE                                                03070000
                WHEN DPG54-NO-ERROR                                     03080000
                     CONTINUE                                           03090000
                WHEN OTHER                                              03100000
                   DISPLAY 'RETRIEVING END DATE...'                     03110000
                   DISPLAY DPG54-ERROR-MESSAGE                          03120000
                   PERFORM Z910-ABEND                                   03130000
           END-EVALUATE.                                                03140000
                                                                        03150000
      ******************************************************************03160000
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *03170000
      ******************************************************************03180000
       Z910-ABEND.                                                      03190000
                                                                        03200000
           DISPLAY '** ABEND           **'.                             03210000
      *    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  03220000
      *    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        03230000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         03240000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.       03250000
                                                                        03260000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03270000
