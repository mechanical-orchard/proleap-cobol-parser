       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         MLKUT708.                                            
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       08/26/2002.                                          
       DATE-COMPILED.                                                           
      ******************************************************************        
      *PURPOSE:                                                                 
      *    THE PROGRAM SCRIPTS SQL UPDATES TO TVAMCL FOR MARKUP AND             
      * MARKDOWNS.                                                              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-TVAMCL-WORK-FILE                                           
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUT-TVAMCL-UPDT-FILE                                          
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-TVAMCL-WORK-FILE                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS IN-TVAMCL-RECORD.                                     
       01  IN-TVAMCL-RECORD              PIC X(63).                             
                                                                                
       FD  OUT-TVAMCL-UPDT-FILE                                                 
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS OUT-TVAMCL-UPDT-RECORD.                               
       01  OUT-TVAMCL-UPDT-RECORD        PIC X(80).                             
                                                                                
      /*****************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  ABEND-CODE                  PIC S9(04) VALUE ZERO COMP SYNC.         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA                 PIC X(30)  VALUE SPACES.                 
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)  VALUE SPACES.                 
           05  PV-READ-RECS-ACCUM      PIC S9(11) COMP-3 VALUE ZEROS.           
           05  PV-WRIT-RECS-ACCUM      PIC S9(11) COMP-3 VALUE ZEROS.           
           05  PV-TVAMCL-READS-ACCUM   PIC S9(11) COMP-3 VALUE ZEROS.           
           05  PV-TVAMCL-WRIT-ACCUM    PIC S9(11) COMP-3 VALUE ZEROS.           
           05  PV-UPDT-COUNT           PIC S9(11) COMP-3 VALUE ZEROS.           
           05  PV-EDIT-ENT-ID          PIC ZZZZZZZZ9     VALUE ZEROS.           
           05  PV-EDIT-WORK-AMOUNT     PIC S9(09)V9(02)  VALUE ZEROS.           
                                                                                
       01  SWITCHES.                                                            
           05  PS-PROCESS-DONE-SW      PIC X(01) VALUE 'N'.                     
               88  PROCESS-COMPLETE              VALUE 'Y'.                     
                                                                                
       01  WS-DCLTVAMCL.                                                        
           10 VAMCL-FSCL-MN-NBR             PIC   X(2).                         
           10 VAMCL-FSCL-MN-END-DTE         PIC   X(10).                        
           10 VAMCL-DEPT-NBR                PIC   X(3).                         
           10 VAMCL-MAJ-CL-NBR              PIC   X(2).                         
           10 VAMCL-ENT-ID                  PIC  S9(9) COMP.                    
           10 VAMCL-SLS-REG-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SLS-PROMO-RTL-AMT       PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SLS-PROCLR-RTL-AMT      PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SLS-CLR-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SLS-OTH-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKDN-PLU-RTL-AMT        PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKDN-RGST-RTL-AMT       PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKDN-UMTCH-RTL-AMT      PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKDN-BYR-RTL-AMT        PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKDN-STR-RTL-AMT        PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKUP-RTL-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-XFER-IN-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-XFER-OUT-RTL-AMT        PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-RCPT-RTL-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-RCPT-NET-COST-AMT       PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-RCPT-GRO-COST-AMT       PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-PADJ-FRGT-COST-AMT      PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-PADJ-DISC-COST-AMT      PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-PADJ-RTL-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-PADJ-NET-COST-AMT       PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-DR-ALW-RTL-AMT          PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-RTV-RTL-AMT             PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-INV-ADJ-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SHNK-RTL-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKUP-INIT-AMT           PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-MKUP-NET-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-END-INV-RTL-AMT         PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-CUM-MKUP-PCT            PIC  S9(4)V9(7) COMP-3.             
           10 VAMCL-END-INV-COST-AMT        PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-SLS-COST-AMT            PIC  S9(11)V9(2) COMP-3.            
           10 VAMCL-GRO-MRGN-AMT            PIC  S9(11)V9(2) COMP-3.            
                                                                                
       01  MKDN-PLU-RTL-AMT-PLUS-LINE.                                          
           05  FILLER               PIC X(45)                                   
               VALUE 'MKDN_PLU_RTL_AMT = MKDN_PLU_RTL_AMT + '.                  
           05  MKDN-PLU-RTL-AMT1    PIC ZZZZZZZZ.99.                            
                                                                                
       01  MKDN-PLU-RTL-AMT-MIN-LINE.                                           
           05  FILLER               PIC X(45)                                   
               VALUE 'MKDN_PLU_RTL_AMT = MKDN_PLU_RTL_AMT - '.                  
           05  MKDN-PLU-RTL-AMT2    PIC ZZZZZZZZ.99.                            
                                                                                
       01  MKDN-RGST-RTL-AMT-PLUS-LINE.                                         
           05  MKDN-RGST-RTL-BYTE1  PIC X(01).                                  
           05  FILLER               PIC X(45)                                   
               VALUE 'MKDN_RGST_RTL_AMT = MKDN_RGST_RTL_AMT + '.                
           05  MKDN-RGST-RTL-AMT1   PIC ZZZZZZZZ.99.                            
                                                                                
       01  MKDN-RGST-RTL-AMT-MIN-LINE.                                          
           05  MKDN-RGST-RTL-BYTE2  PIC X(01).                                  
           05  FILLER               PIC X(45)                                   
               VALUE 'MKDN_RGST_RTL_AMT = MKDN_RGST_RTL_AMT - '.                
           05  MKDN-RGST-RTL-AMT2   PIC ZZZZZZZZ.99.                            
                                                                                
       01  MKDN-UMTCH-RTL-AMT-PLUS-LINE.                                        
           05  MKDN-UMTCH-RTL-BYTE1  PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_UMTCH_RTL_AMT = MKDN_UMTCH_RTL_AMT + '.              
           05  MKDN-UMTCH-RTL-AMT1   PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKDN-UMTCH-RTL-AMT-MIN-LINE.                                         
           05  MKDN-UMTCH-RTL-BYTE2  PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_UMTCH_RTL_AMT = MKDN_UMTCH_RTL_AMT - '.              
           05  MKDN-UMTCH-RTL-AMT2   PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKDN-BYR-RTL-AMT-PLUS-LINE.                                          
           05  MKDN-BYR-RTL-BYTE1    PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_BYR_RTL_AMT = MKDN_BYR_RTL_AMT + '.                  
           05  MKDN-BYR-RTL-AMT1     PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKDN-BYR-RTL-AMT-MIN-LINE.                                           
           05  MKDN-BYR-RTL-BYTE2    PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_BYR_RTL_AMT = MKDN_BYR_RTL_AMT - '.                  
           05  MKDN-BYR-RTL-AMT2     PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKDN-STR-RTL-AMT-PLUS-LINE.                                          
           05  MKDN-STR-RTL-BYTE1    PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_STR_RTL_AMT = MKDN_STR_RTL_AMT + '.                  
           05  MKDN-STR-RTL-AMT1     PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKDN-STR-RTL-AMT-MIN-LINE.                                           
           05  MKDN-STR-RTL-BYTE2    PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKDN_STR_RTL_AMT = MKDN_STR_RTL_AMT - '.                  
           05  MKDN-STR-RTL-AMT2     PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKUP-RTL-AMT-PLUS-LINE.                                              
           05  MKUP-RTL-BYTE1        PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKUP_RTL_AMT = MKUP_RTL_AMT + '.                          
           05  MKUP-RTL-AMT1         PIC ZZZZZZZZ.99.                           
                                                                                
       01  MKUP-RTL-AMT-MIN-LINE.                                               
           05  MKUP-RTL-BYTE2        PIC X(01).                                 
           05  FILLER                PIC X(45)                                  
               VALUE 'MKUP_RTL_AMT = MKUP_RTL_AMT - '.                          
           05  MKUP-RTL-AMT2         PIC ZZZZZZZZ.99.                           
                                                                                
      /****************************************************************         
      *FLOW:    READ THE TVAMCL EXTRACT FILE FOR MARKUP AND MARKDOWNS.          
      *      CREATE A SQL UPDATE SCRIPT FOR ENTIDS AT THE DEPARTMENT            
      *      AND MAJOR CLASS LEVEL WHERE THE MARKUP COLUMN OR MARKDOWN          
      *      COLUMNS CONTINUE TO HAVE A VALUE IN THEM. THE VALUE IS THE         
      *      OPPOSITE OF WHAT THE VALUE CURRENTLY IS.                           
      *                                                                         
      *      WRITE A RECORD.                                                    
      *                                                                         
      *****************************************************************         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAINLINE-PROCESSING.                                                
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS-LOOP                                       
               UNTIL PROCESS-COMPLETE.                                          
                                                                                
           PERFORM 9900-EOJ-TASKS.                                              
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /****************************************************************         
      * 1000-INITIALIZATION.......                                              
      *****************************************************************         
       1000-INITIALIZATION.                                                     
           MOVE '*1000-INITIALIZATION.              *' TO PV-PARA.              
                                                                                
           OPEN  INPUT IN-TVAMCL-WORK-FILE                                      
                OUTPUT OUT-TVAMCL-UPDT-FILE.                                    
                                                                                
           INITIALIZE WS-DCLTVAMCL.                                             
                                                                                
           MOVE SPACES     TO OUT-TVAMCL-UPDT-RECORD.                           
           MOVE ZEROS      TO PV-READ-RECS-ACCUM.                               
           MOVE ZEROS      TO PV-WRIT-RECS-ACCUM.                               
           MOVE ZEROS      TO PV-TVAMCL-READS-ACCUM.                            
           MOVE 'N'        TO PS-PROCESS-DONE-SW.                               
                                                                                
      *** TVAMCL WORK FILE READ....                                             
           PERFORM 9000-READ-TVAMCL-WORK-FILE.                                  
           IF PROCESS-COMPLETE                                                  
               DISPLAY '**  PROGRAM   **  =  MLKUT708'                          
               DISPLAY '**  PARAGRAPH **  = ' PV-PARA                           
               DISPLAY '**  NO RECORDS TO PROCESS **'                           
               MOVE 4095 TO ABEND-CODE                                          
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 2000-MAIN-PROCESS-LOOP.                                                 
      *****************************************************************         
       2000-MAIN-PROCESS-LOOP.                                                  
           MOVE '* 2000-MAIN-PROCESS-LOOP*' TO PV-PARA.                         
                                                                                
           PERFORM 3000-EDIT-TVAMCL-RECORDS.                                    
                                                                                
           PERFORM 9000-READ-TVAMCL-WORK-FILE.                                  
                                                                                
      /****************************************************************         
      * 3000-EDIT-TVAMCL-RECORDS.                                               
      *****************************************************************         
       3000-EDIT-TVAMCL-RECORDS.                                                
           MOVE '*3000-EDIT-TVAMCL-RECORDS *' TO PV-PARA.                       
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           MOVE 'UPDATE DB2TDBA.TVAMCL' TO OUT-TVAMCL-UPDT-RECORD.              
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           MOVE 'SET'               TO OUT-TVAMCL-UPDT-RECORD.                  
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           PERFORM 3100-BUILD-SET.                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           STRING 'WHERE FSCL_MN_END_DTE = ' DELIMITED BY SIZE                  
                  '''2002-08-31''' DELIMITED BY SIZE                            
                                    INTO OUT-TVAMCL-UPDT-RECORD.                
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           STRING 'AND DEPT_NBR = ' DELIMITED BY SIZE                           
                  '''' DELIMITED BY SIZE                                        
                  VAMCL-DEPT-NBR DELIMITED BY SIZE                              
                  '''' DELIMITED BY SIZE                                        
                                    INTO OUT-TVAMCL-UPDT-RECORD.                
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           STRING 'AND MAJ_CL_NBR = ' DELIMITED BY SIZE                         
                  '''' DELIMITED BY SIZE                                        
                  VAMCL-MAJ-CL-NBR DELIMITED BY SIZE                            
                  '''' DELIMITED BY SIZE                                        
                                    INTO OUT-TVAMCL-UPDT-RECORD.                
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           MOVE VAMCL-ENT-ID        TO PV-EDIT-ENT-ID.                          
           STRING 'AND ENT_ID = ' DELIMITED BY SIZE                             
                  PV-EDIT-ENT-ID DELIMITED BY SIZE                              
                  ';' DELIMITED BY SIZE                                         
                                    INTO OUT-TVAMCL-UPDT-RECORD.                
           PERFORM 4000-WRITE-TVAMCL-UPDATE.                                    
                                                                                
      *****************************************************************         
      * 3100-BUILD-SET.                                                         
      *****************************************************************         
       3100-BUILD-SET.                                                          
           MOVE '*3100-BUILD-SET *' TO PV-PARA.                                 
                                                                                
           MOVE ZEROS               TO PV-UPDT-COUNT.                           
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKDN-PLU-RTL-AMT IS NUMERIC AND                             
              VAMCL-MKDN-PLU-RTL-AMT NOT EQUAL ZEROS                            
             IF VAMCL-MKDN-PLU-RTL-AMT LESS ZERO                                
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKDN-PLU-RTL-AMT             
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKDN-PLU-RTL-AMT1                 
               MOVE MKDN-PLU-RTL-AMT-PLUS-LINE                                  
                                           TO OUT-TVAMCL-UPDT-RECORD            
               ADD 1                  TO PV-UPDT-COUNT                          
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               MOVE VAMCL-MKDN-PLU-RTL-AMT TO MKDN-PLU-RTL-AMT2                 
               MOVE MKDN-PLU-RTL-AMT-MIN-LINE                                   
                                           TO OUT-TVAMCL-UPDT-RECORD            
               ADD 1                  TO PV-UPDT-COUNT                          
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKDN-RGST-RTL-AMT IS NUMERIC AND                            
              VAMCL-MKDN-RGST-RTL-AMT NOT EQUAL ZEROS                           
             IF VAMCL-MKDN-RGST-RTL-AMT LESS ZEROS                              
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-RGST-RTL-BYTE1                      
               ELSE                                                             
                 MOVE ','           TO MKDN-RGST-RTL-BYTE1                      
               END-IF                                                           
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKDN-RGST-RTL-AMT            
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKDN-RGST-RTL-AMT1                
               MOVE MKDN-RGST-RTL-AMT-PLUS-LINE                                 
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-RGST-RTL-BYTE2                      
               ELSE                                                             
                 MOVE ','           TO MKDN-RGST-RTL-BYTE2                      
               END-IF                                                           
               MOVE VAMCL-MKDN-RGST-RTL-AMT TO MKDN-RGST-RTL-AMT2               
               MOVE MKDN-RGST-RTL-AMT-MIN-LINE                                  
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKDN-UMTCH-RTL-AMT IS NUMERIC  AND                          
              VAMCL-MKDN-UMTCH-RTL-AMT NOT EQUAL ZEROS                          
             IF VAMCL-MKDN-UMTCH-RTL-AMT LESS ZEROS                             
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-UMTCH-RTL-BYTE1                     
               ELSE                                                             
                 MOVE ','           TO MKDN-UMTCH-RTL-BYTE1                     
               END-IF                                                           
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKDN-UMTCH-RTL-AMT           
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKDN-UMTCH-RTL-AMT1               
               MOVE MKDN-UMTCH-RTL-AMT-PLUS-LINE                                
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-UMTCH-RTL-BYTE2                     
               ELSE                                                             
                 MOVE ','           TO MKDN-UMTCH-RTL-BYTE2                     
               END-IF                                                           
               MOVE VAMCL-MKDN-UMTCH-RTL-AMT TO MKDN-UMTCH-RTL-AMT2             
               MOVE MKDN-UMTCH-RTL-AMT-MIN-LINE                                 
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKDN-BYR-RTL-AMT IS NUMERIC  AND                            
              VAMCL-MKDN-BYR-RTL-AMT NOT EQUAL ZEROS                            
             IF VAMCL-MKDN-BYR-RTL-AMT LESS ZEROS                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-BYR-RTL-BYTE1                       
               ELSE                                                             
                 MOVE ','           TO MKDN-BYR-RTL-BYTE1                       
               END-IF                                                           
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKDN-BYR-RTL-AMT             
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKDN-BYR-RTL-AMT1                 
               MOVE MKDN-BYR-RTL-AMT-PLUS-LINE                                  
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-BYR-RTL-BYTE2                       
               ELSE                                                             
                 MOVE ','           TO MKDN-BYR-RTL-BYTE2                       
               END-IF                                                           
               MOVE VAMCL-MKDN-BYR-RTL-AMT TO MKDN-BYR-RTL-AMT2                 
               MOVE MKDN-BYR-RTL-AMT-MIN-LINE                                   
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKDN-STR-RTL-AMT IS NUMERIC  AND                            
              VAMCL-MKDN-STR-RTL-AMT NOT EQUAL ZEROS                            
             IF VAMCL-MKDN-STR-RTL-AMT LESS ZEROS                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-STR-RTL-BYTE1                       
               ELSE                                                             
                 MOVE ','           TO MKDN-STR-RTL-BYTE1                       
               END-IF                                                           
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKDN-STR-RTL-AMT             
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKDN-STR-RTL-AMT1                 
               MOVE MKDN-STR-RTL-AMT-PLUS-LINE                                  
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKDN-STR-RTL-BYTE2                       
               ELSE                                                             
                 MOVE ','           TO MKDN-STR-RTL-BYTE2                       
               END-IF                                                           
               MOVE VAMCL-MKDN-STR-RTL-AMT TO MKDN-STR-RTL-AMT2                 
               MOVE MKDN-STR-RTL-AMT-MIN-LINE                                   
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE SPACES              TO OUT-TVAMCL-UPDT-RECORD.                  
           IF VAMCL-MKUP-RTL-AMT IS NUMERIC  AND                                
              VAMCL-MKUP-RTL-AMT NOT EQUAL ZEROS                                
             IF VAMCL-MKUP-RTL-AMT LESS ZEROS                                   
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKUP-RTL-BYTE1                           
               ELSE                                                             
                 MOVE ','           TO MKUP-RTL-BYTE1                           
               END-IF                                                           
               COMPUTE PV-EDIT-WORK-AMOUNT = VAMCL-MKUP-RTL-AMT                 
                                           * -1                                 
               MOVE PV-EDIT-WORK-AMOUNT    TO MKUP-RTL-AMT1                     
               MOVE MKUP-RTL-AMT-PLUS-LINE                                      
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             ELSE                                                               
               IF PV-UPDT-COUNT EQUAL ZERO                                      
                 MOVE SPACE         TO MKUP-RTL-BYTE2                           
               ELSE                                                             
                 MOVE ','           TO MKUP-RTL-BYTE2                           
               END-IF                                                           
               MOVE VAMCL-MKUP-RTL-AMT TO MKUP-RTL-AMT2                         
               MOVE MKUP-RTL-AMT-MIN-LINE                                       
                                    TO OUT-TVAMCL-UPDT-RECORD                   
               ADD 1                TO PV-UPDT-COUNT                            
               PERFORM 4000-WRITE-TVAMCL-UPDATE                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * 4000-WRITE-TVAMCL-UPDATE.                                               
      *****************************************************************         
       4000-WRITE-TVAMCL-UPDATE.                                                
           MOVE '*4000-WRITE-TVAMCL-UPDATE *' TO PV-PARA.                       
                                                                                
           WRITE OUT-TVAMCL-UPDT-RECORD.                                        
                                                                                
           ADD 1                   TO PV-TVAMCL-WRIT-ACCUM.                     
                                                                                
      /****************************************************************         
      * 9000-READ-TVAMCL-WORK-FILE.                                             
      *****************************************************************         
       9000-READ-TVAMCL-WORK-FILE.                                              
           MOVE '*9000-READ-TVAMCL-WORK-FILE*' TO PV-PARA.                      
                                                                                
           READ IN-TVAMCL-WORK-FILE NEXT RECORD                                 
             INTO WS-DCLTVAMCL                                                  
               AT END  MOVE 'Y'    TO PS-PROCESS-DONE-SW.                       
                                                                                
           IF NOT PROCESS-COMPLETE                                              
               ADD 1               TO PV-TVAMCL-READS-ACCUM                     
           END-IF.                                                              
                                                                                
      /****************************************************************         
      * 9900-EOJ-TASKS.                                                         
      *****************************************************************         
       9900-EOJ-TASKS.                                                          
           MOVE '*9900-EOJ-TASKS. *' TO PV-PARA.                                
           DISPLAY '*********************************'                          
           DISPLAY '**  CONTROL SUMMARY - MLKUT708 **'                          
           DISPLAY '* TVAMCL WRK READS   = ', PV-TVAMCL-READS-ACCUM             
           DISPLAY '*            WRITES  = ', PV-TVAMCL-WRIT-ACCUM              
           DISPLAY '*********************************'                          
                                                                                
           CLOSE IN-TVAMCL-WORK-FILE                                            
                 OUT-TVAMCL-UPDT-FILE.                                          
                                                                                
