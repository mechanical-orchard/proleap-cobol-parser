000100 IDENTIFICATION DIVISION.                                                 
000200 TITLE 'EXTRACT SE DETAIL ROWS FOR INVENTORY STORES'.                     
000300                                                                          
000400 PROGRAM-ID.             PCKUT056.                                        
000500 AUTHOR.                 DENNIS STEFFEN.                                  
000600 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000700 DATE-WRITTEN            NOVEMBER 2005.                                   
000800 DATE-COMPILED.                                                           
000900                                                                          
001000 ENVIRONMENT DIVISION.                                                    
001100 CONFIGURATION SECTION.                                                   
001200                                                                          
001300 SOURCE-COMPUTER.        IBM-3090.                                        
001400 OBJECT-COMPUTER.        IBM-3090.                                        
001500                                                                          
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT INVENTORY-STORE-FILE                                          
002100         ASSIGN TO INP01.                                                 
002200                                                                          
002300     SELECT ALL-SE-FILE                                                   
002400         ASSIGN TO INP02.                                                 
002500                                                                          
002600     SELECT SE-ARCHIVE-FILE                                               
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002900                                                                          
003000 DATA DIVISION.                                                           
003100 FILE SECTION.                                                            
003200                                                                          
003300                                                                          
003400 FD  INVENTORY-STORE-FILE                                                 
003500     RECORDING MODE IS F                                                  
003600     LABEL RECORDS ARE STANDARD                                           
003700     BLOCK CONTAINS 0 RECORDS.                                            
003800                                                                          
003900 01  INVENTORY-STORE-RECORD           PIC  X(080).                        
004000                                                                          
004100 FD  ALL-SE-FILE                                                          
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  ALL-TYPE-SE-RECORD               PIC  X(077).                        
004700                                                                          
004800 FD  SE-ARCHIVE-FILE                                                      
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  SE-ARCHIVE-REC                   PIC X(160).                         
005400                                                                          
005500/                                                                         
005600 WORKING-STORAGE SECTION.                                                 
005700                                                                          
005800 01  FILLER.                                                              
005900     05  FILLER                      PIC  X(36)    VALUE                  
006000         ' ***** WORKING STORAGE PGM=PCKUT056 '.                          
006100     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.           
006200                                                                          
007500 01  PV-WHEN-COMPILED.                                                    
007600     05  PV-COMPILE-DATE          PIC X(08).                              
007700     05  PV-COMPILE-TIME          PIC X(08).                              
007800                                                                          
006300 01  PS-SWITCHES.                                                         
006400     05  PS-EOF-EXTRACT-SW            PIC X(01) VALUE 'N'.                
006500         88  PS-EOF-EXTRACT-YES                 VALUE 'Y'.                
006600         88  PS-EOF-EXTRACT-NO                  VALUE 'N'.                
006700     05  PS-EOF-PC046-SW              PIC X(01) VALUE 'N'.                
006800         88  PS-EOF-PC046-YES                   VALUE 'Y'.                
006900         88  PS-EOF-PC046-NO                    VALUE 'N'.                
007000                                                                          
007100     05  PS-END-CSR-SW                PIC X(01) VALUE 'N'.                
007200         88  PS-END-CSR-YES                     VALUE 'Y'.                
007300         88  PS-END-CSR-NO                      VALUE 'N'.                
007400     05  PS-ERROR-SW                  PIC X(01) VALUE 'N'.                
007500         88  PS-ERROR-YES                       VALUE 'Y'.                
007600         88  PS-ERROR-NO                        VALUE 'N'.                
007700                                                                          
007800                                                                          
007900 01  PC-CONSTANTS.                                                        
008000     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
008100                                                   'PCKUT042'.            
008200     05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
008300                                                   'ILBOABN0'.            
008400                                                                          
008500 01  PV-MISC-STUFF.                                                       
008600     05  PV-ABEND-PARAGRAPH           PIC  X(30) VALUE SPACES.            
008700     05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(60) VALUE SPACE.             
008800     05  PV-SQL-CODE                  PIC  9(04) VALUE ZERO.              
008900     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
009000                                                   COMP SYNC.             
009100     05  PV-STR-COUNT                 PIC S9(05) COMP-3                   
009200                                              VALUE ZERO.                 
009300     05  PV-READ-COUNTER              PIC S9(09) COMP-3                   
009400                                              VALUE ZERO.                 
009500     05  GT-ALL-COUNT                 PIC S9(13) COMP-3                   
009600                                              VALUE ZERO.                 
009700     05  GT-PURGE-COUNT               PIC S9(13) COMP-3                   
009800                                              VALUE ZERO.                 
009900     05  GT-KEEP-COUNT                PIC S9(13) COMP-3                   
010000                                              VALUE ZERO.                 
010100     05  PV-READ-PC046-COUNTER        PIC S9(09) COMP-3                   
010200                                              VALUE ZERO.                 
010300     05  PV-WRITE-1-COUNTER           PIC S9(09) COMP-3                   
010400                                              VALUE ZERO.                 
010500     05  PV-WRITE-2-COUNTER           PIC S9(09) COMP-3                   
010600                                              VALUE ZERO.                 
010700     05  PV-WRITE-OUT04-COUNTER       PIC S9(09) COMP-3                   
010800                                              VALUE ZERO.                 
010900     05  PV-NOT-INV-COUNTER           PIC S9(09) COMP-3                   
011000                                              VALUE ZERO.                 
011100     05  PV-BYPASS-COUNT              PIC S9(09) COMP-3                   
011200                                              VALUE ZERO.                 
011300     05  PV-STORE-COUNT               PIC S9(05) COMP-3                   
011400                                              VALUE ZERO.                 
011500     05  PV-DISP-NBR                  PIC ZZZ,ZZZ,ZZZ,ZZ9.                
011600     05  PV-DISP-LOC                  PIC ZZZ9.                           
011700     05  PV-DISP-NBR-2                PIC ZZZ,ZZZ,ZZ9.                    
011800     05  PV-DISP-NBR-3                PIC ZZZ,ZZZ,ZZ9.                    
011900     05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
012000     05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
012100     05  PV-TMST                      PIC X(26).                          
012200                                                                          
012300 01  ABEND-CODE                       PIC S9(04)     VALUE +0             
012400                                                     COMP SYNC.           
012500     88  ABEND-4001-WRITE-ERROR                      VALUE +4001.         
012600     88  ABEND-4002-READ-ERROR                       VALUE +4002.         
012700     88  ABEND-4003-CTRL-CARD-ERROR                  VALUE +4003.         
012800     88  ABEND-4004-TABLE-ERROR                      VALUE +4004.         
012900     88  ABEND-4005-OPEN-ERROR                       VALUE +4005.         
013000     88  ABEND-4006-CLOSE-ERROR                      VALUE +4006.         
013100     88  ABEND-4007-SEQUENCE-ERROR                   VALUE +4007.         
013200     88  ABEND-4008-DATS-ERROR                       VALUE +4008.         
013300     88  ABEND-4009-KEY-DATS-ERROR                   VALUE +4009.         
013400     88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
013500                                                                          
013600*    INTRANSIT UNLOAD FILE                                                
013700     COPY PCRD045.                                                        
013800                                                                          
013900*    INVENTORY STORE FILE                                                 
014000     COPY PCRD046.                                                        
014100                                                                          
014200*    KEYED PURGE FILE                                                     
014300     COPY PCRD047.                                                        
014400                                                                          
014500*    ARCHIVED PURGE EXTRACT                                               
014600     COPY PCRD048.                                                        
014700                                                                          
014800*    REPORT EXTRACT                                                       
014900     COPY PCRD049.                                                        
015000/                                                                         
015100     EXEC SQL                                                             
015200         INCLUDE SQLCA                                                    
015300     END-EXEC.                                                            
015400                                                                          
015500     COPY SQLCA2.                                                         
015600                                                                          
015700     COPY DPWS004.                                                        
015800/                                                                         
015900*    BUILT FROM INP01 INVENTORY-STORE-FILE                                
016000 01  PT-LOC-TABLE.                                                        
016100     05  PT-LOC-ELEMENT OCCURS 9999 TIMES INDEXED BY PT-IDX.              
016200         10  PT-STORE                 PIC S9(04) COMP.                    
016300         10  PT-INV-DTE               PIC  X(10).                         
016400         10  PT-PLND-INV-DTE          PIC  X(10).                         
016500         10  PT-FIN-BK-DTE            PIC  X(10).                         
016600         10  PT-FIN-DTE-ADJ-QTY       PIC S9(04) COMP.                    
016700         10  PT-FIN-BK-DTE-ADJ        PIC  X(10).                         
016800         10  PT-BEGIN-DTE             PIC  X(10).                         
016900         10  PT-END-DTE               PIC  X(10).                         
017000                                                                          
017100 01  PT-TOT-TABLE.                                                        
017200     05  PT-TOT-ELEMENT OCCURS 9999 TIMES INDEXED BY PT-TOT-IDX.          
017300         10  PT-LOC                   PIC S9(04) COMP.                    
017400         10  PT-ALL-TOT               PIC S9(11) COMP.                    
017500         10  PT-PURGE-TOT             PIC S9(11) COMP.                    
017600         10  PT-KEEP-TOT              PIC S9(11) COMP.                    
017700         10  PT-MIN-DTE               PIC X(10).                          
017800         10  PT-MAX-DTE               PIC X(10).                          
017900         10  PT-MIN-DTE-ALL           PIC X(10).                          
018000         10  PT-MAX-DTE-ALL           PIC X(10).                          
018100/                                                                         
018200 PROCEDURE DIVISION.                                                      
018300                                                                          
018400     PERFORM 1000-INITIALIZATION.                                         
018500                                                                          
018600     PERFORM 2000-PROCESS UNTIL PS-EOF-EXTRACT-YES                        
018700                            OR PS-ERROR-YES                               
018800                                                                          
018900     PERFORM 9000-QUIT.                                                   
019000                                                                          
019100     GOBACK.                                                              
019200                                                                          
019300 1000-INITIALIZATION.                                                     
019400     MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
019500     DISPLAY 'START PCKUT056'                                             
022000     MOVE WHEN-COMPILED               TO PV-WHEN-COMPILED                 
022100                                         PGM-COMPILED                     
022200     DISPLAY SPACES                                                       
022300     DISPLAY 'COMPILE DATE: ' PV-COMPILE-DATE                             
022400             ' ' PV-COMPILE-TIME                                          
022500                                                                          
019600                                                                          
019700     INITIALIZE PT-LOC-TABLE                                              
019800                PT-TOT-TABLE.                                             
019900                                                                          
020000     PERFORM VARYING PT-TOT-IDX FROM 1 BY 1                               
020100         UNTIL PT-TOT-IDX > 9999                                          
020200             MOVE '0001-01-01'    TO PT-MAX-DTE (PT-TOT-IDX)              
020300                                     PT-MAX-DTE-ALL (PT-TOT-IDX)          
020400             MOVE '9999-09-09'    TO PT-MIN-DTE (PT-TOT-IDX)              
020500                                     PT-MIN-DTE-ALL (PT-TOT-IDX)          
020600     END-PERFORM                                                          
020700                                                                          
020800     EXEC SQL                                                             
020900         SET :PV-TMST = CURRENT TIMESTAMP                                 
021000     END-EXEC                                                             
021100                                                                          
021200     OPEN INPUT ALL-SE-FILE                                               
021300                INVENTORY-STORE-FILE                                      
021400     OPEN OUTPUT                                                          
021500          SE-ARCHIVE-FILE                                                 
021600                                                                          
021700     IF PS-ERROR-NO                                                       
021800         PERFORM 8000-READ-INV-STORES                                     
021900         IF PS-EOF-PC046-YES                                              
022000             DISPLAY SPACES                                               
022100             DISPLAY ALL '*'                                              
022200             DISPLAY '  INV STORES FILE IS EMPTY '                        
022300             DISPLAY ALL '*'                                              
022400             DISPLAY SPACES                                               
022500             SET PS-ERROR-YES         TO TRUE                             
022600         END-IF                                                           
022700     END-IF                                                               
022800                                                                          
022900     IF PS-ERROR-NO                                                       
023000                                                                          
023100         PERFORM UNTIL PS-EOF-PC046-YES                                   
023200             MOVE PC046-LOC-NBR       TO PT-STORE                         
023300                                   (PC046-LOC-NBR)                        
023400             MOVE PC046-INV-DTE       TO PT-INV-DTE                       
023500                                   (PC046-LOC-NBR)                        
023600             MOVE PC046-FIN-BK-DTE    TO PT-FIN-BK-DTE                    
023700                                   (PC046-LOC-NBR)                        
023800             MOVE PC046-PLND-INV-DTE  TO PT-PLND-INV-DTE                  
023900                                   (PC046-LOC-NBR)                        
024000             MOVE PC046-FIN-DTE-ADJ-QTY                                   
024100                                      TO PT-FIN-DTE-ADJ-QTY               
024200                                   (PC046-LOC-NBR)                        
024300             MOVE PC046-FIN-BK-DTE-ADJ TO PT-FIN-BK-DTE-ADJ               
024400                                   (PC046-LOC-NBR)                        
024500             MOVE PC046-BEGIN-DTE     TO PT-BEGIN-DTE                     
024600                                   (PC046-LOC-NBR)                        
024700             MOVE PC046-END-DTE       TO PT-END-DTE                       
024800                                   (PC046-LOC-NBR)                        
024900                                                                          
025000             ADD 1                    TO PV-STR-COUNT                     
025100                                                                          
025200             PERFORM 8000-READ-INV-STORES                                 
025300         END-PERFORM                                                      
025400                                                                          
025500         DISPLAY ' STORE INVENTORY INFO'                                  
025600         DISPLAY 'STR    INV-DTE     BOOK-DTE   PARM    ADJ-BOOK'         
025700         PERFORM VARYING PT-IDX FROM 1 BY 1 UNTIL PT-IDX > 9999           
025800             IF PT-STORE(PT-IDX) > ZERO                                   
025900                 DISPLAY PT-STORE (PT-IDX) '  '                           
026000                     PT-INV-DTE (PT-IDX) '  '                             
026100                     PT-FIN-BK-DTE (PT-IDX) '  '                          
026200                     PT-FIN-DTE-ADJ-QTY (PT-IDX)  '   '                   
026300                     PT-FIN-BK-DTE-ADJ (PT-IDX)                           
026400                 IF PT-INV-DTE (PT-IDX) > PT-FIN-BK-DTE (PT-IDX)          
026500                     DISPLAY '***'                                        
026600                 END-IF                                                   
026700             END-IF                                                       
026800         END-PERFORM                                                      
026900         DISPLAY SPACES                                                   
027000         DISPLAY 'TOTAL STORES ELIGIBLE FOR PURGING = : '                 
027100                                         PV-STR-COUNT                     
027200         IF PV-STR-COUNT = ZERO                                           
027300             SET PS-ERROR-YES         TO TRUE                             
027400         END-IF                                                           
027500     END-IF.                                                              
027600                                                                          
027700     IF PS-ERROR-NO                                                       
027800         PERFORM 8050-READ-EXTRACT                                        
027900         IF PS-EOF-EXTRACT-YES                                            
028000             DISPLAY SPACES                                               
028100             DISPLAY '!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!'                    
028200             DISPLAY '*******************************'                    
028300             DISPLAY PV-ABEND-PARAGRAPH                                   
028400             DISPLAY '  BMC UNLOAD FILE IS EMPTY '                        
028500             DISPLAY '  BIG BIG TROUBLE          '                        
028600             DISPLAY '*******************************'                    
028700             DISPLAY '!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!'                    
028800             DISPLAY SPACES                                               
028900             SET ABEND-4002-READ-ERROR  TO TRUE                           
029000             CALL 'ILBOABN0' USING ABEND-CODE                             
029100     END-IF.                                                              
029200/                                                                         
029300 2000-PROCESS.                                                            
029400                                                                          
029500     MOVE '2000-PROCESS'                  TO PV-ABEND-PARAGRAPH.          
029600                                                                          
029700     IF PC045-LOC-NBR = PT-STORE (PC045-LOC-NBR)                          
030000                                                                          
030100*        OUT01                                                            
030200         PERFORM 2100-FORMAT-PCRD048                                      
030300         PERFORM 8100-WRITE-ARCHIVE                                       
030400     ELSE                                                                 
030500*        SKIPPED RECORDS                                                  
030600         ADD +1                       TO PV-WRITE-2-COUNTER               
030700                                                                          
030800     END-IF.                                                              
030900                                                                          
031000     PERFORM 8050-READ-EXTRACT.                                           
031100                                                                          
031200 2100-FORMAT-PCRD048.                                                     
031300                                                                          
031400     INITIALIZE PC048-SE-ARCHIVE-REC.                                     
031500     MOVE PC045-INTR-UPC-ID           TO PC048-INTR-UPC-ID                
031600     MOVE PC045-LOC-NBR               TO PC048-LOC-NBR                    
031700     MOVE PC045-TRN-CLSN-CDE          TO PC048-TRN-CLSN-CDE               
031800     MOVE PC045-IN-TRNST-TRN-TMST     TO PC048-IN-TRNST-TRN-TMST          
031900     MOVE PC045-TRN-TYP-CDE           TO PC048-TRN-TYP-CDE                
032000     MOVE PC045-TRN-CLSN-PRCG-QTY     TO PC048-TRN-CLSN-PRCG-QTY          
032100     MOVE PC045-TRN-CLSN-BAL-QTY      TO PC048-TRN-CLSN-BAL-QTY           
032200     MOVE PC045-UPD-USR-ID            TO PC048-UPD-USR-ID                 
032300     MOVE PC045-OTBND-TRIP-ID         TO PC048-OTBND-TRIP-ID              
032400     MOVE PC045-NULL-1                TO PC048-NULL-1                     
032500     MOVE PC045-PO-NBR                TO PC048-PO-NBR                     
032600     MOVE PC045-NULL-2                TO PC048-NULL-2                     
032700     MOVE PC045-RCVR-SEQ-NBR          TO PC048-RCVR-SEQ-NBR               
032800     MOVE PC045-NULL-3                TO PC048-NULL-3                     
032900     MOVE PC045-PROCESS-DTE           TO PC048-PROCESS-DTE                
033000                                                                          
033100     SET PT-IDX                       TO PC045-LOC-NBR                    
033200     MOVE PT-INV-DTE (PT-IDX)         TO PC048-INV-DTE                    
033300     MOVE PT-PLND-INV-DTE(PT-IDX) TO PC048-PLND-INV-DTE                   
033400     MOVE PT-FIN-BK-DTE (PT-IDX)      TO PC048-FIN-BK-DTE                 
033500     MOVE PT-FIN-DTE-ADJ-QTY (PT-IDX)                                     
033600                                      TO PC048-FIN-DTE-ADJ-QTY            
033700     MOVE PT-FIN-BK-DTE-ADJ(PT-IDX) TO PC048-FIN-BK-DTE-ADJ               
033800     MOVE PT-BEGIN-DTE (PT-IDX)       TO PC048-BEGIN-DTE                  
033900     MOVE PT-END-DTE (PT-IDX)         TO PC048-END-DTE.                   
034000/                                                                         
034100 8000-READ-INV-STORES.                                                    
034200                                                                          
034300     MOVE '8000-READ-INV-STORES'  TO PV-ABEND-PARAGRAPH.                  
034400                                                                          
034500     READ INVENTORY-STORE-FILE                                            
034600         INTO                                                             
034700             PC046-INV-PARM-REC                                           
034800         AT END                                                           
034900             SET PS-EOF-PC046-YES     TO TRUE                             
035000         NOT AT END                                                       
035100             ADD 1                    TO PV-READ-PC046-COUNTER            
035200                                                                          
035300     END-READ.                                                            
035400                                                                          
035500 8050-READ-EXTRACT.                                                       
035600                                                                          
035700     MOVE '8050-READ-EXTRACT   '  TO PV-ABEND-PARAGRAPH.                  
035800                                                                          
035900     READ ALL-SE-FILE                                                     
036000         INTO                                                             
036100             PC045-SE-UNLOAD-REC                                          
036200         AT END                                                           
036300             SET PS-EOF-EXTRACT-YES   TO TRUE                             
036400         NOT AT END                                                       
036500             MOVE PC045-LOC-NBR       TO PT-LOC                           
036600                                         (PC045-LOC-NBR)                  
036700             ADD 1                    TO PV-READ-COUNTER                  
036800                                         PT-ALL-TOT(PC045-LOC-NBR)        
036900             IF PC045-IN-TRNST-TRN-TMST(1:10) <                           
037000                               PT-MIN-DTE-ALL (PC045-LOC-NBR)             
037100                 MOVE PC045-IN-TRNST-TRN-TMST(1:10)                       
037200                             TO PT-MIN-DTE-ALL (PC045-LOC-NBR)            
037300             END-IF                                                       
037400                                                                          
037500             IF PC045-IN-TRNST-TRN-TMST(1:10) >                           
037600                               PT-MAX-DTE-ALL (PC045-LOC-NBR)             
037700                 MOVE PC045-IN-TRNST-TRN-TMST(1:10)                       
037800                             TO PT-MAX-DTE-ALL (PC045-LOC-NBR)            
037900             END-IF                                                       
038000                                                                          
038100     END-READ.                                                            
038200                                                                          
038300 8100-WRITE-ARCHIVE.                                                      
038400*    OUT01                                                                
038500     MOVE '8100-WRITE-ARCHIVE  '  TO PV-ABEND-PARAGRAPH.                  
038600     WRITE                                                                
038700         SE-ARCHIVE-REC                                                   
038800         FROM PC048-SE-ARCHIVE-REC                                        
038900     END-WRITE.                                                           
039000     ADD 1                            TO PV-WRITE-1-COUNTER               
039100                                 PT-PURGE-TOT(PC048-LOC-NBR).             
039200     IF PC048-IN-TRNST-TRN-TMST(1:10) <                                   
039300                               PT-MIN-DTE (PC048-LOC-NBR)                 
039400         MOVE PC048-IN-TRNST-TRN-TMST(1:10)                               
039500                             TO PT-MIN-DTE (PC048-LOC-NBR)                
039600     END-IF                                                               
039700                                                                          
039800     IF PC048-IN-TRNST-TRN-TMST(1:10) >                                   
039900                               PT-MAX-DTE (PC048-LOC-NBR)                 
040000         MOVE PC048-IN-TRNST-TRN-TMST(1:10)                               
040100                             TO PT-MAX-DTE (PC048-LOC-NBR)                
040200     END-IF.                                                              
040300/                                                                         
040400 9000-QUIT.                                                               
040500                                                                          
040600     DISPLAY SPACES.                                                      
040700                                                                          
040800     MOVE PV-READ-COUNTER             TO PV-DISP-NBR                      
040900     DISPLAY 'STORE EXPECTED RECORDS READ          = '                    
041000                                          PV-DISP-NBR                     
041100                                                                          
041200     MOVE PV-WRITE-1-COUNTER          TO PV-DISP-NBR                      
041300     DISPLAY 'STORE EXPECTED RECORDS TO BE DELETED = '                    
041400                                                 PV-DISP-NBR              
041500                                                                          
041600     MOVE PV-WRITE-2-COUNTER          TO PV-DISP-NBR                      
041700     DISPLAY 'STORE EXPECTED RECORDS TO BE KEPT    = '                    
041800                                                 PV-DISP-NBR              
041900                                                                          
042000     PERFORM 9001-PRINT-TOTALS-TABLE.                                     
042100                                                                          
042200     MOVE PV-WRITE-OUT04-COUNTER      TO PV-DISP-NBR                      
042300                                                                          
042400     DISPLAY 'REPORT RECORDS WRITTEN               = '                    
042500                                                 PV-DISP-NBR.             
042600                                                                          
042700 9001-PRINT-TOTALS-TABLE.                                                 
042800                                                                          
042900     DISPLAY SPACES                                                       
043000     DISPLAY SPACES                                                       
043100                                                                          
043200     DISPLAY 'STORE        PURGE        KEEP          TOTAL  '            
043300             '   PURGE DATE RANGE                 FILE DATE RANGE'        
043300             '   INV DATE'                                                
043400                                                                          
043500     PERFORM VARYING PT-TOT-IDX FROM 1 BY 1                               
043600             UNTIL PT-TOT-IDX > 9999                                      
043700         SET PT-IDX                   TO PT-TOT-IDX                       
043800         IF PT-STORE(PT-IDX) > ZERO                                       
043900             AND PT-INV-DTE (PT-IDX) > SPACES                             
044000             INITIALIZE PC049-SE-PURGE-RPT-REC                            
044100             INSPECT PT-MIN-DTE (PT-TOT-IDX)                              
044200                     REPLACING ALL '9999-09-09' BY SPACES                 
044300             INSPECT PT-MAX-DTE (PT-TOT-IDX)                              
044400                     REPLACING ALL '0001-01-01' BY SPACES                 
044500             INSPECT PT-MIN-DTE-ALL (PT-TOT-IDX)                          
044600                     REPLACING ALL '9999-09-09' BY SPACES                 
044700             INSPECT PT-MAX-DTE-ALL (PT-TOT-IDX)                          
044800                     REPLACING ALL '0001-01-01' BY SPACES                 
044900                                                                          
045000             MOVE PT-STORE(PT-IDX) TO PV-DISP-LOC                         
045100                                        PC049-LOC-NBR                     
045200             MOVE PT-ALL-TOT (PT-TOT-IDX) TO PV-DISP-NBR                  
045300                                             PC049-ALL-TOT                
045400             MOVE PT-PURGE-TOT(PT-TOT-IDX) TO PV-DISP-NBR-2               
045500                                              PC049-PURGE-TOT             
045600             MOVE PT-KEEP-TOT(PT-TOT-IDX) TO PV-DISP-NBR-3                
045700                                             PC049-KEEP-TOT               
045800             MOVE PT-MIN-DTE (PT-TOT-IDX) TO PC049-MIN-DTE                
045900             MOVE PT-MAX-DTE (PT-TOT-IDX) TO PC049-MAX-DTE                
046000             MOVE PT-MIN-DTE-ALL (PT-TOT-IDX)                             
046100                                      TO PC049-MIN-DTE-ALL                
046200             MOVE PT-MAX-DTE-ALL (PT-TOT-IDX)                             
046300                                      TO PC049-MAX-DTE-ALL                
046400             MOVE PT-INV-DTE (PT-IDX)                                     
046500                                      TO PC049-INV-DTE                    
046600                                                                          
046700             DISPLAY PV-DISP-LOC  '   '                                   
046800                     PV-DISP-NBR-2 '   '                                  
046900                     PV-DISP-NBR-3 '   '                                  
047000                     PV-DISP-NBR '   '                                    
047100                     PT-MIN-DTE (PT-TOT-IDX) '   '                        
047200                     PT-MAX-DTE (PT-TOT-IDX) '       '                    
047300                     PT-MIN-DTE-ALL (PT-TOT-IDX) '   '                    
047400                     PT-MAX-DTE-ALL (PT-TOT-IDX) ' '                      
047500                     PT-INV-DTE (PT-IDX)                                  
047600                                                                          
047700             ADD PT-ALL-TOT (PT-TOT-IDX) TO GT-ALL-COUNT                  
047800             ADD PT-PURGE-TOT(PT-TOT-IDX) TO GT-PURGE-COUNT               
047900             ADD PT-KEEP-TOT(PT-TOT-IDX) TO GT-KEEP-COUNT                 
048000*            PERFORM 8400-WRITE-REPORT-EXTRACT                            
048100         END-IF                                                           
048200     END-PERFORM                                                          
048300                                                                          
048400     INITIALIZE PC049-SE-PURGE-RPT-REC                                    
048500     MOVE 9999                        TO PC049-LOC-NBR                    
048600     MOVE GT-ALL-COUNT                TO PV-DISP-NBR                      
048700                                         PC049-ALL-TOT                    
048800     MOVE GT-PURGE-COUNT              TO PV-DISP-NBR-2                    
048900                                         PC049-PURGE-TOT                  
049000     MOVE GT-KEEP-COUNT               TO PV-DISP-NBR-3                    
049100                                         PC049-KEEP-TOT                   
049200*    PERFORM 8400-WRITE-REPORT-EXTRACT                                    
049300                                                                          
049400     DISPLAY '-----        -----        ------        ------ '            
049500     DISPLAY SPACES                                                       
049600             DISPLAY '       '                                            
049700                     PV-DISP-NBR-2 '   '                                  
049800                     PV-DISP-NBR-3 '   '                                  
049900                     PV-DISP-NBR                                          
050000                                                                          
050100     CLOSE ALL-SE-FILE                                                    
050200           INVENTORY-STORE-FILE                                           
050300           SE-ARCHIVE-FILE.                                               
050400/                                                                         
050500 9100-SQL-ABEND.                                                          
050600******************************************************************        
050700*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
050800*  ERROR OR WARNING CODE OCCURS.                                          
050900******************************************************************        
051000     DISPLAY '9100-SQL-ABEND'.                                            
051100     DISPLAY '** ABEND     **'.                                           
051200     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
051300     DISPLAY '** PARAGRAPH ** = ' PV-ABEND-PARAGRAPH.                     
051400     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
051500     DISPLAY '**              ** = '                                      
051600     MOVE SQLCODE TO PV-SQL-CODE.                                         
051700     DISPLAY '** SQL CODE  ** = ' PV-SQL-CODE.                            
051800     MOVE +4013 TO PV-ABEND-CODE.                                         
051900     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
052000                                                                          
052100******************************************************************        
052200*  THIS MODULE PERFORMS THE SQL WARNING.                         *        
052300******************************************************************        
052400                                                                          
052500 9800-SQL-WARNING.                                                        
052600                                                                          
052700     MOVE SQLCODE               TO SQLCODE2.                              
052800     MOVE SQLERRD(3)            TO SQLERRD3.                              
052900     DISPLAY '** ABEND **       ** = SQLWARNING'.                         
053000     DISPLAY '** PROGRAM        ** = PCKUT051'.                           
053100     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
053200     DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
053300     DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
053400                                                                          
053500     COPY DPPD004.                                                        
053600     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
053700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
053800/                                                                         
053900******************************************************************        
054000* THIS MODULE PERFORMS THE SQL ERROR.                            *        
054100******************************************************************        
054200                                                                          
054300 9900-SQL-ERROR.                                                          
054400                                                                          
054500     MOVE SQLCODE               TO SQLCODE2.                              
054600     MOVE SQLERRD(3)            TO SQLERRD3.                              
054700     DISPLAY '** ABEND **       ** = SQLERROR'.                           
054800     DISPLAY '** PROGRAM        ** = PCKUT051'.                           
054900     DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
055000     DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION                 
055100     DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1               
055200     DISPLAY SPACES                                                       
055300                                                                          
055400     COPY DPPD004.                                                        
055500     SET ABEND-4013-DB2-ERROR   TO TRUE                                   
055600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
