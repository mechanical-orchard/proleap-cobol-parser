       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     INKUT302.                                                
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   09/28/2006.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           INKUT302 - FORMAT ADJUSTMENT FILE INTO PCRD010       *        
      *                      LAYOUT                                    *        
      ******************************************************************        
      ******************************************************************        
      *    WR/PITS#     DATE        DESCRIPTION                        *        
      *    --------   --------     ---------------------------------   *        
      *    PKE2470    11/19/10     INITIAL IMPLEMENTATION              *        
      *                            CREATED BY COPYING SIMILAR PROGRAM  *        
      *                            IN TEST USED FOR MANUAL MASS/ADD    *        
      *                            DELETE PROCESS.MODIFIED TO HANDLE   *        
      *                            INVALID SKU IN INPUT.               *        
      *                            THIS IS AN EDIT/REFORMATTING PROGRAM*        
      *                            THE LOCATION PROVIDED IN THE INPUT  *        
      *                            FILE SHOULD BE AN ACTIVE LOCATION   *        
      *                            THIS PROGRAM WILL GENERATE A ERROR  *        
      *                            FILE WHICH HAS ANY INVALID SKU/STORE*        
P12569*    PK12569    01/18/13     ADDED LOGIC TO WRITE ALL THE SKUS            
P12569*                            WHICH BELONGS TO INVALID LOCATION   *        
P12569*                            IN THE ERROR FILE.                           
P12569*                            HAVE ALSO INCLUDED ERROR MESSAGE             
P12569*                            IN THE ERROR FILE.                           
P12569*                                                                         
P47829******************************************************************        
P47829*    PK47829                 ADDED CHECK IN PARA                          
P47829*                            6200-SELECT-XMTLOC TO CHECK                  
P47829*                            A.FINCL_CLO_DTE IS NULL FOR DS               
P47829*                            AND CS TYPE OF STORES ALONE.                 
P47829*    PK47829                 MADE CHANGES TO                              
P47829*                            3000-PERFORM-LOOKUP TO                       
P47829*                            PREVENT PROCESSING BEFORE                    
P47829*                            PLANNED INVENTORY IS COMPLETED               
P47829* INC0729760                                                              
P47829******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT MASS-ADDDEL-ADJUSTMENTS                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT FMT-ADJUSTMENTS-OUT                                           
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT INVALID-RECORDS-OUT                                           
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  MASS-ADDDEL-ADJUSTMENTS                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MASS-ADDDEL-ADJ-RECORD       PIC X(80).                              
                                                                                
       FD  FMT-ADJUSTMENTS-OUT                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  FMT-ADJUSTMENTS-RECORD       PIC X(100).                             
                                                                                
       FD  INVALID-RECORDS-OUT                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INVALID-RECORD               PIC X(80).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
        01 PS-PROGRAM-SWITCHES.                                                 
           05  PS-DATA-SW               PIC X(01)   VALUE 'N'.                  
               88  PS-END-OF-DATA                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-DATA               VALUE 'N'.                  
           05  PS-CONTROL-SW            PIC X(01)   VALUE 'N'.                  
               88  PS-END-OF-CONTROL                VALUE 'Y'.                  
               88  PS-NOT-END-OF-CONTROL            VALUE 'N'.                  
           05  PS-VALID-LOC-SW          PIC X(01)   VALUE 'N'.                  
               88  PS-VALID-LOCATION                VALUE 'Y'.                  
               88  PS-INVALID-LOCATION              VALUE 'N'.                  
           05  PS-VALID-SKU-SW          PIC X(01)   VALUE 'N'.                  
               88  PS-VALID-SKU                     VALUE 'Y'.                  
               88  PS-INVALID-SKU                   VALUE 'N'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-DISPLAY               PIC 9(11)   VALUE ZERO.                 
           05  PV-ADD-NUM               PIC S9(04)  COMP                        
                                                    VALUE ZERO.                 
           05  PV-ADJ-QTY               PIC S9(09)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-WRITE-COUNT           PIC S9(09)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INVALID-SKU-STR-COUNT     PIC S9(09)  VALUE ZERO              
                                                    COMP-3.                     
           05  PV-READ-COUNT            PIC S9(09)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-WRITE-NEG-COUNT       PIC S9(09)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-RETRY-COUNT           PIC S9(04)  COMP                        
                                                    VALUE ZERO.                 
           05  PV-PROGRAM-NAME          PIC X(08)   VALUE 'INKUT302'.           
           05  PV-PARAGRAPH-NAME        PIC X(30)   VALUE SPACE.                
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                        PIC X(30)   VALUE SPACE.                
           05  PV-DB2-OPER-ATTEMPTED    PIC X(30)   VALUE SPACE.                
           05  PV-SKU-NUMBER            PIC S9(08)V COMP-3.                     
           05  PV-SKU-NBR               PIC 9(08).                              
           05  PV-SKU-NBR-ALPH REDEFINES                                        
               PV-SKU-NBR               PIC X(08).                              
           05  PV-INPUT-LOC             PIC 9(05)   VALUE ZERO.                 
           05  PV-HOLD-LOC-NBR          PIC S9(04)  VALUE ZERO COMP.            
           05  PV-PREV-LOC-NBR          PIC S9(04)  VALUE ZERO COMP.            
           05  PV-HOLD-KEY-DATE         PIC X(10)   VALUE SPACES.               
P47829     05  PV-CURRENT-DATE-DB2.                                             
P47829         10  PV-CD-CCYY.                                                  
P47829             15  PC-CD-CC        PIC 9(02)    VALUE ZERO.                 
P47829             15  PV-CD-YY        PIC 9(02)    VALUE ZERO.                 
P47829         10  PV-CD-MM            PIC 9(02)    VALUE ZERO.                 
P47829         10  PV-CD-DD            PIC 9(02)    VALUE ZERO.                 
P47829     05  PV-CURRENT-DATE.                                                 
P47829         10  PV-CD-DB2-CCYY.                                              
P47829             15  PV-CD-DB2-CC    PIC 9(02)    VALUE ZERO.                 
P47829             15  PV-CD-DB2-YY    PIC 9(02)    VALUE ZERO.                 
P47829         10  FILLER              PIC X(01)    VALUE '-'.                  
P47829         10  PV-CD-DB2-MM        PIC 9(02)    VALUE ZERO.                 
P47829         10  FILLER              PIC X(01)    VALUE '-'.                  
P47829         10  PV-CD-DB2-DD        PIC 9(02)    VALUE ZERO.                 
                                                                                
       01  ABEND-CODE                   PIC S9(04)  COMP VALUE ZERO.            
                                                                                
       01  CC-INPUT-LAYOUT.                                                     
           05  CC-REC-NBR               PIC 9(02)   VALUE ZEROES.               
           05  FILLER                   PIC X(01)   VALUE SPACES.               
           05  CC-LOC-NBR               PIC 9(04)   VALUE ZEROES.               
           05  FILLER                   PIC X(01)   VALUE SPACES.               
           05  CC-SKU-NBR               PIC 9(08)   VALUE ZEROES.               
           05  FILLER                   PIC X(01)   VALUE SPACES.               
           05  CC-QTY                   PIC 9(04)   VALUE ZEROES.               
           05  FILLER                   PIC X(59)   VALUE SPACES.               
P12569******************************************************************   CL**1
P12569*              E R R O R FILE  L A Y O U T S                    *    CL**1
P12569******************************************************************   CL**1
P12569 01  CC-ERROR-LAYOUT.                                                     
P12569     05  CC-ERR-REC                PIC 9(02)  VALUE ZEROES.               
P12569     05  FILLER                    PIC X(01)  VALUE SPACES.               
P12569     05  CC-ERR-LOC                PIC 9(04)  VALUE ZEROES.               
P12569     05  FILLER                    PIC X(01)  VALUE SPACES.               
P12569     05  CC-ERR-SKU                PIC 9(08)  VALUE ZEROES.               
P12569     05  FILLER                    PIC X(01)  VALUE SPACES.               
P12569     05  CC-ERR-QTY                PIC 9(04)  VALUE ZEROES.               
P12569     05  FILLER                    PIC X(01)  VALUE SPACES.               
P12569     05  CC-ERR-DESCRIPTION        PIC X(22)  VALUE SPACES.               
P12569     05  FILLER                    PIC X(36)  VALUE SPACES.               
      *                                                                         
      ******************************************************************        
      *01  PC010-PCT-INTRANSIT-REC                                              
           COPY PCRD010.                                                        
                                                                                
      ******************************************************************        
      *01  DP004-DB2-ERROR-FIELDS                                               
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TINVPAR                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TSKXREF                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TUPAC                                                
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 TABLE DCLGEN - TINCNTL                                              
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE XMT00002                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 COMMUNICATION AREA                                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 8000-READ-INVENTORY.                                         
           PERFORM 2000-PROCESS-INPUT                                           
                   UNTIL PS-END-OF-DATA.                                        
                                                                                
           PERFORM 9000-EOJ-TASKS.                                              
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * 1000-INITIALIZATION.                                                    
      *****************************************************************         
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE DCLTINVPAR.                                               
                                                                                
           OPEN INPUT MASS-ADDDEL-ADJUSTMENTS                                   
                OUTPUT FMT-ADJUSTMENTS-OUT                                      
                OUTPUT INVALID-RECORDS-OUT.                                     
                                                                                
           MOVE ZEROS               TO PV-WRITE-COUNT.                          
           MOVE ZEROS               TO PV-READ-COUNT.                           
           MOVE 'N'                 TO PS-DATA-SW.                              
                                                                                
P47829*****************************************************************         
P47829     ACCEPT PV-CURRENT-DATE-DB2 FROM DATE YYYYMMDD.                       
P47829     MOVE PV-CD-CCYY TO PV-CD-DB2-CCYY.                                   
P47829     MOVE PV-CD-MM TO PV-CD-DB2-MM.                                       
P47829     MOVE PV-CD-DD TO PV-CD-DB2-DD.                                       
      *****************************************************************         
      * 2000-PROCESS-INPUT.                                                     
      *****************************************************************         
       2000-PROCESS-INPUT.                                                      
                                                                                
           INITIALIZE PC010-PCT-INTRANSIT-REC.                                  
                                                                                
           ADD 1                 TO  PV-READ-COUNT.                             
                                                                                
           IF CC-LOC-NBR NOT = INVPAR-LOC-NBR                                   
               INITIALIZE DCLTINVPAR                                            
               MOVE CC-LOC-NBR       TO PV-HOLD-LOC-NBR                         
               MOVE PV-HOLD-LOC-NBR  TO INVPAR-LOC-NBR                          
               PERFORM 4000-SELECT-TINVPAR                                      
           END-IF.                                                              
                                                                                
           PERFORM 3000-PERFORM-LOOKUP.                                         
           PERFORM 8000-READ-INVENTORY.                                         
                                                                                
      *****************************************************************         
      * 3000-PERFORM-LOOKUP.                                                    
      *****************************************************************         
       3000-PERFORM-LOOKUP.                                                     
                                                                                
           MOVE CC-LOC-NBR          TO PV-INPUT-LOC                             
           MOVE PV-INPUT-LOC        TO PV-HOLD-LOC-NBR                          
           MOVE INVPAR-PLND-INV-DTE TO PV-HOLD-KEY-DATE                         
           MOVE CC-SKU-NBR          TO PV-SKU-NBR                               
           MOVE PV-SKU-NBR          TO PV-SKU-NUMBER                            
                                                                                
           IF PV-HOLD-LOC-NBR = PV-PREV-LOC-NBR                                 
               CONTINUE                                                         
           ELSE                                                                 
             IF PV-HOLD-KEY-DATE = SPACES                                       
P47829         OR PV-CURRENT-DATE <= INVPAR-PLND-INV-DTE                        
P12569         SET PS-INVALID-LOCATION TO TRUE                                  
P12569         MOVE 'INVALID LOCATION ' TO CC-ERR-DESCRIPTION                   
             ELSE                                                               
               PERFORM 6200-SELECT-XMTLOC                                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
P12569     IF PS-INVALID-LOCATION                                               
P47829     DISPLAY 'PLANNED INV DATE IS YET TO BE COMPLETED FOR    '            
P47829     DISPLAY 'LOCATION NUMBER : ' PV-HOLD-LOC-NBR                         
P12569         PERFORM  8200-WRITE-INVALID-SKU-STR                              
P12569     END-IF.                                                              
                                                                                
           IF PS-VALID-LOCATION                                                 
      ***LOOKUP-BY-SKU                                                          
                 EXEC SQL                                                       
                     SELECT DISTINCT A.INTR_UPC_ID,                             
                            A.ITM_NBR,                                          
                            A.SKU_STAT_CDE                                      
                       INTO :SKXREF-INTR-UPC-ID                                 
                           ,:SKXREF-ITM-NBR                                     
                           ,:SKXREF-SKU-STAT-CDE                                
                       FROM TSKXREF A,TUPC B                                    
                      WHERE A.INTR_UPC_ID = B.INTR_UPC_ID AND                   
                            B.EFF_END_DTE IS NULL AND                           
                            A.SKU = :PV-SKU-NUMBER                              
                     WITH UR                                                    
                 END-EXEC                                                       
      *                                                                         
                 EVALUATE TRUE                                                  
                     WHEN SQLCODE = 0                                           
                         MOVE SKXREF-INTR-UPC-ID  TO PC010-INTR-UPC-ID          
                         MOVE PV-HOLD-LOC-NBR     TO PC010-STR-NBR              
                         MOVE CC-QTY              TO PC010-IN-TRNST-QTY         
                         PERFORM 8100-WRITE-PCRD010                             
                     WHEN SQLCODE = +100                                        
P12569                   MOVE 'INVALID SKU '                                    
P12569                                      TO CC-ERR-DESCRIPTION               
                         PERFORM 8200-WRITE-INVALID-SKU-STR                     
                     WHEN OTHER                                                 
                         MOVE '3000-PERFORM-LOOKUP'                             
                                                TO PV-PARAGRAPH-NAME            
                         MOVE 'SELECT ROW FROM TSKXREF'                         
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                         PERFORM 9999-SQL-ABEND                                 
                 END-EVALUATE                                                   
           END-IF.                                                              
      *                                                                         
      *****************************************************************         
      * 4000-SELECT-TINVPAR.                                                    
      *****************************************************************         
       4000-SELECT-TINVPAR.                                                     
                                                                                
            EXEC SQL                                                            
               SELECT ACTL_UNIT_BK_DTE                                          
                     ,MAX(PLND_INV_DTE)                                         
               INTO :INVPAR-ACTL-UNIT-BK-DTE                                    
                   ,:INVPAR-PLND-INV-DTE                                        
               FROM TINVPAR A,                                                  
                    TINCNTL B                                                   
               WHERE B.ACTV_IND = 'Y'                                           
                 AND A.INV_ID = B.INV_ID                                        
                 AND LOC_NBR = :INVPAR-LOC-NBR                                  
                 AND LOC_INV_STAT_CDE = 'IN'                                    
                 AND ACTL_FIN_BK_DTE = '9999-09-09'                             
                 GROUP BY LOC_NBR                                               
                        , ACTL_UNIT_BK_DTE                                      
                        , PLND_INV_DTE                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                  CONTINUE                                                      
               WHEN SQLCODE = +100                                              
                  MOVE '9999-09-09'  TO INVPAR-ACTL-UNIT-BK-DTE                 
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                  MOVE '4000-SELECT-TINVPAR'                                    
                                     TO PV-PARAGRAPH-NAME                       
                  MOVE 'SELECT TINVPAR'                                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 6200-SELECT-XMTLOC                                                      
      ******************************************************************        
       6200-SELECT-XMTLOC.                                                      
                                                                                
           MOVE PV-HOLD-LOC-NBR TO PV-PREV-LOC-NBR.                             
                                                                                
           EXEC SQL                                                             
                SELECT A.LOC_NBR                                                
                  INTO :XMT00001-LOC-NBR                                        
                  FROM XMT_LOC             A                                    
                      ,XMT_DC_LOC_ASGMT    C                                    
                 WHERE A.LOC_NBR = :PV-HOLD-LOC-NBR                             
                   AND A.LOC_NBR = C.LOC_NBR                                    
                   AND C.EFF_BEG_DTE <= :PV-HOLD-KEY-DATE                       
                   AND A.CLO_DTE     >= :PV-HOLD-KEY-DATE                       
                   AND C.EFF_BEG_DTE =                                          
                       (SELECT MIN(B.EFF_BEG_DTE)                               
                           FROM XMT_DC_LOC_ASGMT    B                           
                          WHERE B.LOC_NBR = :PV-HOLD-LOC-NBR)                   
                   AND (A.FINCL_CLO_DTE = '9999-09-09'                          
P47829             OR  (A.LOC_TYP_CDE IN ('DC','CS')                            
P47829             AND A.FINCL_CLO_DTE IS NULL))                                
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   SET PS-VALID-LOCATION TO TRUE                                
               WHEN SQLCODE = +100                                              
P12569             MOVE 'INVALID LOCATION ' TO CC-ERR-DESCRIPTION               
                   SET PS-INVALID-LOCATION TO TRUE                              
               WHEN OTHER                                                       
                   MOVE '6200-SELECT-XMT00001'  TO PV-PARAGRAPH-NAME            
                   MOVE 'SELECT ROW FROM XMT00001'                              
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *****************************************************************         
      * 8000-READ-INVENTORY.                                                    
      *    PROCESS THE INVENTORY ADJUSTMENTS FLAT FILE.                         
      *****************************************************************         
       8000-READ-INVENTORY.                                                     
                                                                                
           READ MASS-ADDDEL-ADJUSTMENTS                                         
               INTO CC-INPUT-LAYOUT                                             
                   AT END SET PS-END-OF-DATA TO TRUE                            
           END-READ.                                                            
                                                                                
      *****************************************************************         
      * 8100-WRITE-PCRD010.                                                     
      * WRITE THE FORMATTED RECORD.                                             
      *****************************************************************         
       8100-WRITE-PCRD010.                                                      
           WRITE FMT-ADJUSTMENTS-RECORD                                         
                FROM PC010-PCT-INTRANSIT-REC.                                   
           ADD 1                 TO  PV-WRITE-COUNT.                            
                                                                                
      *****************************************************************         
      * 8200-WRITE-INVALID-SKU-STR                                              
      * WRITE THE RECORDS WITH INVALID SKU/STR TO ERROR FILE.                   
      *****************************************************************         
       8200-WRITE-INVALID-SKU-STR.                                              
P12569     MOVE CC-REC-NBR       TO CC-ERR-REC.                                 
P12569     MOVE CC-LOC-NBR       TO CC-ERR-LOC.                                 
P12569     MOVE CC-SKU-NBR       TO CC-ERR-SKU.                                 
P12569     MOVE CC-QTY           TO CC-ERR-QTY.                                 
P12569     WRITE INVALID-RECORD                                                 
P12569                           FROM CC-ERROR-LAYOUT                           
           ADD 1                 TO  PV-INVALID-SKU-STR-COUNT.                  
                                                                                
      *****************************************************************         
      * 9000-EOJ-TASKS.                                                         
      *****************************************************************         
       9000-EOJ-TASKS.                                                          
                                                                                
           CLOSE MASS-ADDDEL-ADJUSTMENTS                                        
                 FMT-ADJUSTMENTS-OUT                                            
                 INVALID-RECORDS-OUT.                                           
                                                                                
           DISPLAY '                                                 '.         
           DISPLAY '*************************************************'.         
           DISPLAY '      INKUT302 SUMMARY REPORT:                  *'.         
           DISPLAY '*************************************************'.         
           DISPLAY '   TOTAL ADJUSTMENT RECORDS IN  :', PV-READ-COUNT           
           DISPLAY '   TOTAL ADJUSTMENT RECORDS OUT :', PV-WRITE-COUNT          
           DISPLAY '   TOTAL ERROR RECORDS OUT      :',                         
                                            PV-INVALID-SKU-STR-COUNT            
           DISPLAY '                                                 '.         
           DISPLAY '**************************************************'.        
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE 4013      TO ABEND-CODE.                                        
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
