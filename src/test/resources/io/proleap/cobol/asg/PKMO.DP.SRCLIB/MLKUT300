       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUT300.                                                 
       AUTHOR.        RONNA MCDONALD.                                           
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  09-03-14.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * COPIED MLTRP030 AND CHANGE THE COPYBOOK FROM MLRD030 TO MLRD021         
      ******************************************************************        
      * READ A FILE OF EDITED PURCHASE TRANSACTIONS IN THE MLRD021     *        
      * FORMAT. SUM AND REPORT THE COST, RETAIL, AND NUMBER OF         *        
      * TRANSACTIONS.                                                  *        
      *                                                                *        
      * COMMENT OUT IF STATEMENTS AS NECESSARY TO SELECT PURCHASES     *        
      * AND/OR TRANSFER-FROMS AND/OR TRANSFER-TOS, AND/OR TO SELECT    *        
      * SPECIFIC BATCH NUMBERS.                                        *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * LY1005   2010-05-20  CHANGE REPORT TO PROVIDE OTHER LEVEL RPT  *        
      ******************************************************************        
                                                                       *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING  MODE IS F.                                                
       01  INPUT-RECORD                PIC X(150).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      * INPUT RECORD LAYOUT                                                     
      ******************************************************************        
                                                                                
      *++INCLUDE MLRD021                                                        
           COPY MLRD021.                                                        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-FILE-SW     PIC X           VALUE 'N'.           
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.           
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.           
           05  PS-PRODUCE-TOTAL-SW         PIC X           VALUE 'N'.           
               88  PS-PRODUCE-TOTAL                        VALUE 'Y'.           
               88  PS-NOT-PRODUCE-TOTAL                    VALUE 'N'.           
           05  PS-FIRST-TIME-THROUGH-SW    PIC X           VALUE 'Y'.           
               88  PS-FIRST-TIME-THROUGH                   VALUE 'Y'.           
               88  PS-NOT-FIRST-TIME-THROUGH               VALUE 'N'.           
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(20)       VALUE SPACES.        
               88  RP-REPORT-BY-DEPT                       VALUE                
                   ' DEPT:              '.                                      
               88  RP-REPORT-BY-STORE                      VALUE                
                   ' STORE:             '.                                      
               88  RP-REPORT-BY-DEPT-CLASS                 VALUE                
                   ' DEPT/CLASS:        '.                                      
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '   PURCH COUNT '.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(18)       VALUE                
               '       PURCH COST '.                                            
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               '      PURCH RETAIL '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(17)       VALUE                
               '   PURCH FREIGHT '.                                             
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               'XFER FROM COUNT'.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               ' XFER FROM CST '.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               ' TRANSFER FROM RTL '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '  XFER TO COUNT'.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(15)       VALUE                
               '  XSFER TO CST '.                                               
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(19)       VALUE                
               '   TRANSFER TO RTL '.                                           
           05  FILLER                      PIC X(01)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(20)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(18)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(19)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(17)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(19)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(15)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)      VALUE SPACES.         
           05  FILLER                      PIC X(19)      VALUE ALL '-'.        
           05  FILLER                      PIC X(01)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  RP-REPT-CRITERIA            PIC X(20)       VALUE SPACES.        
               88  RP-GRAND-TOTAL                          VALUE                
                   ' GRAND TOTAL:       '.                                      
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-PURCH-COUNT              PIC ZZZ,ZZZ,ZZZ,ZZ9.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-PURCH-COST               PIC ZZ,ZZZ,ZZZ,ZZ9.99-.              
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-PURCH-RETAIL             PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-PURCH-FREIGHT            PIC Z,ZZZ,ZZZ,ZZ9.99-.               
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-XFER-FROM-COUNT          PIC ZZZ,ZZZ,ZZZ,ZZ9.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-XFER-FROM-COST           PIC ZZZ,ZZZ,ZZ9.99-.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-XFER-FROM-RETAIL         PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-XFER-TO-COUNT            PIC ZZZ,ZZZ,ZZZ,ZZ9.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-XFER-TO-COST             PIC ZZZ,ZZZ,ZZ9.99-.                 
           05  FILLER                      PIC X(01)       VALUE SPACES.        
           05  RP-XFER-TO-RETAIL           PIC ZZZ,ZZZ,ZZZ,ZZ9.99-.             
           05  FILLER                      PIC X(01)       VALUE SPACES.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-HOLD-DEPT                PIC 9(03)       VALUE ZEROES.        
           05  PV-HOLD-DEPT-X REDEFINES PV-HOLD-DEPT                            
                                           PIC X(03).                           
           05  PV-HOLD-STORE               PIC 9(05)       VALUE ZEROES.        
           05  PV-HOLD-STORE-X REDEFINES PV-HOLD-STORE                          
                                           PIC X(05).                           
           05  PV-HOLD-CLASS               PIC 9(02)       VALUE ZEROES.        
           05  PV-HOLD-CLASS-X REDEFINES PV-HOLD-CLASS                          
                                           PIC X(02).                           
           05  PV-PURCH-COUNT              PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-XFER-FROM-COUNT          PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-XFER-TO-COUNT            PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-PURCH-COST         PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-FRGT-AMT           PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-PURCH-RETAIL       PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-FROM-COST     PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-FROM-RETAIL   PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-TO-COST       PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-TO-RETAIL     PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PURCH-COUNT-TTL          PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-XFER-FROM-COUNT-TTL      PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-XFER-TO-COUNT-TTL        PIC S9(13)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-PURCH-COST-TTL     PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-FRGT-AMT-TTL       PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-PURCH-RETAIL-TTL   PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-FROM-COST-TTL PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-FROM-RTL-TTL  PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-TO-COST-TTL   PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-ACCUM-XFER-TO-RETAIL-TTL PIC S9(13)V99   VALUE ZEROES         
                               COMP-3.                                          
                                                                                
      ******************************************************************        
      * PROGRAM LINKAGE SECTION                                                 
      ******************************************************************        
       LINKAGE SECTION.                                                         
                                                                                
       01  PARM-DATA-AREA.                                                      
           05  PARM-LENGTH                  PIC S9(4)                           
                               COMP.                                            
           05  PARM-DATA.                                                       
               10  PRM-REPORT-BREAK-TYPE       PIC X(10)   VALUE SPACES.        
                   88  PRM-REPORT-BY-STORE                 VALUE                
                       'STORE     '.                                            
                   88  PRM-REPORT-BY-DEPT                  VALUE                
                       'DEPT      '.                                            
                   88  PRM-REPORT-BY-DEPT-CLASS            VALUE                
                       'DEPT/CLASS'.                                            
                   88  PRM-VALID-REPORT-BREAK-TYPE         VALUES ARE           
                       'STORE     '                                             
                       'DEPT      '                                             
                       'DEPT/CLASS'.                                            
               10  FILLER                      PIC X(28)   VALUE SPACES.        
               10  FILLER                      PIC X(42)   VALUE SPACES.        
                                                                                
LY1005 PROCEDURE DIVISION USING PARM-DATA-AREA.                                 
                                                                                
       A100-PROGRAM-MAINLINE.                                                   
                                                                                
           INSPECT PARM-DATA                                                    
               REPLACING ALL LOW-VALUES  BY ' '                                 
                         ALL HIGH-VALUES BY ' '.                                
           IF PRM-VALID-REPORT-BREAK-TYPE                                       
               NEXT SENTENCE                                                    
           ELSE                                                                 
               SET PRM-REPORT-BY-DEPT          TO TRUE                          
           END-IF.                                                              
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           PERFORM B500-READ-INPUT.                                             
           MOVE ML021-PURCH-DEPT-NBR    TO PV-HOLD-DEPT.                        
           MOVE ML021-PURCH-CLASS-NBR   TO PV-HOLD-CLASS.                       
           MOVE ML021-PURCH-STORE-NBR   TO PV-HOLD-STORE-X.                     
                                                                                
           PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
                                                                                
               PERFORM B100-DISPLAY-DETAIL                                      
                                                                                
               PERFORM B200-ACCUM-DETAIL                                        
                                                                                
               PERFORM B500-READ-INPUT                                          
           END-PERFORM.                                                         
                                                                                
           PERFORM B100-DISPLAY-DETAIL.                                         
                                                                                
           SET RP-GRAND-TOTAL            TO TRUE.                               
           MOVE PV-PURCH-COUNT-TTL       TO RP-PURCH-COUNT.                     
           MOVE PV-ACCUM-PURCH-COST-TTL  TO RP-PURCH-COST.                      
           MOVE PV-ACCUM-PURCH-RETAIL-TTL TO RP-PURCH-RETAIL.                   
           MOVE PV-ACCUM-FRGT-AMT-TTL    TO RP-PURCH-FREIGHT.                   
           MOVE PV-XFER-FROM-COUNT-TTL   TO RP-XFER-FROM-COUNT.                 
           MOVE PV-XFER-TO-COUNT-TTL     TO RP-XFER-TO-COUNT.                   
           MOVE PV-ACCUM-XFER-FROM-COST-TTL TO RP-XFER-FROM-COST.               
           MOVE PV-ACCUM-XFER-FROM-RTL-TTL TO RP-XFER-FROM-RETAIL.              
           MOVE PV-ACCUM-XFER-TO-COST-TTL TO RP-XFER-TO-COST.                   
           MOVE PV-ACCUM-XFER-TO-RETAIL-TTL TO RP-XFER-TO-RETAIL.               
           DISPLAY RP-REPORT-DETAIL                                             
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       B100-DISPLAY-DETAIL.                                                     
                                                                                
           EVALUATE TRUE                                                        
               WHEN PRM-REPORT-BY-STORE                                         
                   IF ML021-PURCH-STORE-NBR NOT = PV-HOLD-STORE-X               
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-STORE TO TRUE                           
                       STRING PV-HOLD-STORE-X                                   
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN PRM-REPORT-BY-DEPT                                          
                   IF ML021-PURCH-DEPT-NBR NOT = PV-HOLD-DEPT                   
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-DEPT  TO TRUE                           
                       STRING PV-HOLD-DEPT-X                                    
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
               WHEN OTHER                                                       
                   IF (ML021-PURCH-DEPT-NBR NOT = PV-HOLD-DEPT AND              
                           ML021-PURCH-CLASS-NBR NOT = PV-HOLD-CLASS)           
                       OR PS-END-OF-INPUT-FILE                                  
                       SET PS-PRODUCE-TOTAL   TO TRUE                           
                       SET RP-REPORT-BY-DEPT-CLASS TO TRUE                      
                       STRING PV-HOLD-DEPT-X                                    
                              '/' PV-HOLD-CLASS-X                               
                              ': '                                              
                           DELIMITED BY SIZE                                    
                           INTO RP-REPT-CRITERIA                                
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
           IF PS-PRODUCE-TOTAL                                                  
               IF PS-FIRST-TIME-THROUGH                                         
                   DISPLAY RP-REPORT-HEADER-1                                   
                   DISPLAY RP-REPORT-HEADER-2                                   
                   SET PS-NOT-FIRST-TIME-THROUGH TO TRUE                        
               END-IF                                                           
                                                                                
               MOVE PV-PURCH-COUNT           TO RP-PURCH-COUNT                  
               MOVE PV-ACCUM-PURCH-COST      TO RP-PURCH-COST                   
               MOVE PV-ACCUM-PURCH-RETAIL    TO RP-PURCH-RETAIL                 
               MOVE PV-ACCUM-FRGT-AMT        TO RP-PURCH-FREIGHT                
               MOVE PV-XFER-FROM-COUNT       TO RP-XFER-FROM-COUNT              
               MOVE PV-XFER-TO-COUNT         TO RP-XFER-TO-COUNT                
               MOVE PV-ACCUM-XFER-FROM-COST  TO RP-XFER-FROM-COST               
               MOVE PV-ACCUM-XFER-FROM-RETAIL TO RP-XFER-FROM-RETAIL            
               MOVE PV-ACCUM-XFER-TO-COST    TO RP-XFER-TO-COST                 
               MOVE PV-ACCUM-XFER-TO-RETAIL  TO RP-XFER-TO-RETAIL               
               DISPLAY RP-REPORT-DETAIL                                         
                                                                                
               ADD PV-PURCH-COUNT           TO PV-PURCH-COUNT-TTL               
               ADD PV-ACCUM-PURCH-COST      TO PV-ACCUM-PURCH-COST-TTL          
               ADD PV-ACCUM-PURCH-RETAIL    TO PV-ACCUM-PURCH-RETAIL-TTL        
               ADD PV-ACCUM-FRGT-AMT        TO PV-ACCUM-FRGT-AMT-TTL            
               ADD PV-XFER-FROM-COUNT       TO PV-XFER-FROM-COUNT-TTL           
               ADD PV-XFER-TO-COUNT         TO PV-XFER-TO-COUNT-TTL             
               ADD PV-ACCUM-XFER-FROM-COST                                      
                   TO PV-ACCUM-XFER-FROM-COST-TTL                               
               ADD PV-ACCUM-XFER-FROM-RETAIL                                    
                   TO PV-ACCUM-XFER-FROM-RTL-TTL                                
               ADD PV-ACCUM-XFER-TO-COST    TO PV-ACCUM-XFER-TO-COST-TTL        
               ADD PV-ACCUM-XFER-TO-RETAIL                                      
                   TO PV-ACCUM-XFER-TO-RETAIL                                   
               MOVE ZERO TO PV-PURCH-COUNT                                      
                            PV-ACCUM-PURCH-COST                                 
                            PV-ACCUM-PURCH-RETAIL                               
                            PV-ACCUM-FRGT-AMT                                   
                            PV-XFER-FROM-COUNT                                  
                            PV-XFER-TO-COUNT                                    
                            PV-ACCUM-XFER-FROM-COST                             
                            PV-ACCUM-XFER-FROM-RETAIL                           
                            PV-ACCUM-XFER-TO-COST                               
                            PV-ACCUM-XFER-TO-RETAIL                             
               MOVE ML021-PURCH-DEPT-NBR    TO PV-HOLD-DEPT                     
               MOVE ML021-PURCH-CLASS-NBR   TO PV-HOLD-CLASS                    
               MOVE ML021-PURCH-STORE-NBR   TO PV-HOLD-STORE-X                  
               SET PS-NOT-PRODUCE-TOTAL TO TRUE                                 
           END-IF.                                                              
                                                                                
                                                                                
       B200-ACCUM-DETAIL.                                                       
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML021-PURCH-PURCHASE                                        
                   ADD +1 TO PV-PURCH-COUNT                                     
                   IF ML021-PURCH-DR-CR-CDE = '01' OR '07'                      
                       COMPUTE PV-ACCUM-PURCH-COST                              
                             = PV-ACCUM-PURCH-COST                              
                             + ML021-PURCH-EXTD-NET-COST-AMT                    
                       COMPUTE PV-ACCUM-FRGT-AMT                                
                             = PV-ACCUM-FRGT-AMT                                
                             + ML021-PURCH-FRGT-AMT                             
                       COMPUTE PV-ACCUM-PURCH-RETAIL                            
                             = PV-ACCUM-PURCH-RETAIL                            
                             + ML021-PURCH-EXTD-RTL-AMT                         
                   ELSE                                                         
                       COMPUTE PV-ACCUM-PURCH-COST                              
                             = PV-ACCUM-PURCH-COST                              
                             - ML021-PURCH-EXTD-NET-COST-AMT                    
                       COMPUTE PV-ACCUM-FRGT-AMT                                
                             = PV-ACCUM-FRGT-AMT                                
                             - ML021-PURCH-FRGT-AMT                             
                       COMPUTE PV-ACCUM-PURCH-RETAIL                            
                             = PV-ACCUM-PURCH-RETAIL                            
                             - ML021-PURCH-EXTD-RTL-AMT                         
                   END-IF                                                       
               WHEN ML021-PURCH-TRANSFER-FRM-STORE                              
                   ADD +1 TO PV-XFER-FROM-COUNT                                 
                   COMPUTE PV-ACCUM-XFER-FROM-COST                              
                         = PV-ACCUM-XFER-FROM-COST                              
                         + ML021-PURCH-EXTD-NET-COST-AMT                        
                   COMPUTE PV-ACCUM-XFER-FROM-RETAIL                            
                         = PV-ACCUM-XFER-FROM-RETAIL                            
                         + ML021-PURCH-EXTD-RTL-AMT                             
               WHEN ML021-PURCH-TRANSFER-TO-STORE                               
                   ADD +1 TO PV-XFER-TO-COUNT                                   
                   COMPUTE PV-ACCUM-XFER-TO-COST                                
                         = PV-ACCUM-XFER-TO-COST                                
                         + ML021-PURCH-EXTD-NET-COST-AMT                        
                   COMPUTE PV-ACCUM-XFER-TO-RETAIL                              
                         = PV-ACCUM-XFER-TO-RETAIL                              
                         + ML021-PURCH-EXTD-RTL-AMT                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       B500-READ-INPUT.                                                         
                                                                                
           READ INPUT-FILE INTO ML021-PURCH-RECORD                              
               AT END SET PS-END-OF-INPUT-FILE TO TRUE                          
           END-READ.                                                            
                                                                                
