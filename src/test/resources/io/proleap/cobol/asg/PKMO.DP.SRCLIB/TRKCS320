000200 IDENTIFICATION DIVISION.                                                 
000300                                                                          
000400 PROGRAM-ID.    TRKCS320.                                                 
000500 AUTHOR.        MARGARET MORRISON.                                        
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000700 DATE-WRITTEN.  06/2004.                                                  
000800 DATE-COMPILED.                                                           
000900******************************************************************        
001000*    Y320 - DECONSOLIDATION CONTROL PANEL CRITERIA SPECS         *        
001100*                                                                *        
001200*    THIS PROGRAM ALLOWS THE USER TO ENTER DATA TO BE USED BY    *        
001300*    SUBSEQUENT PROGRAMS TO CREATE A LIST OR TO BRING UP A       *        
001400*    DETAIL SCREEN.  THE POE CODE, IF ENTERED, IS EDITTED        *        
001500*    TO BE A VALID CODE.  ALL OTHER EDITS ARE DONE IN THE        *        
001600*    DESTINATION PROGRAMS WHEN THE 'GO-TO' TYPE FUNCTION IS USED *        
001700*    (LOCAL-FUNC-01 THRU LOCAL-FUNC-12, WITH DISPOSITION 'G').   *        
001800******************************************************************        
263697* CHANGED 11/08/2021    BY JIM HUNTER      WR: CHG0263697                 
263697* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263697* MAKING THE CALL DYNAMIC VS STATIC.                                      
263697*---------------------------------------------------------------*         
CICS  * CHANGED  1/16/2023    BY JIM HUNTER      WR: CHG0278495                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001900                                                                          
002000 ENVIRONMENT DIVISION.                                                    
002100                                                                          
002200 DATA DIVISION.                                                           
002300                                                                          
002400 WORKING-STORAGE SECTION.                                                 
002500                                                                          
002600 01  DK-DB2-CONSTANTS.                                                    
002700     05  DK-POE-CDE                PIC  X(03).                            
002810                                                                          
002900 01  PV-PROGRAM-VARIABLES.                                                
003000     05  PV-ASTRK-CNT              PIC S9(04)  VALUE +0                   
003100                                               COMP SYNC.                 
003110     05  PV-POE-CDE                PIC  X(03)  VALUE SPACE.               
003112     05  PV-WEEK-OF-DTE.                                                  
003113         10 PV-WEEK-OF-YYYY        PIC  X(04).                            
003114         10 FILLER                 PIC  X(01) VALUE '-'.                  
003115         10 PV-WEEK-OF-MM          PIC  X(02).                            
003116         10 FILLER                 PIC  X(01) VALUE '-'.                  
003117         10 PV-WEEK-OF-DD          PIC  X(02).                            
003118     05  PV-RETRY-COUNTER          PIC S9(04)  VALUE +0                   
003120                                               COMP SYNC.                 
003170                                                                          
003200 01  PC-PROGRAM-CONSTANTS.                                                
003300     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'TR320A  '.         
003400     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'TRKM320 '.         
003500     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'TRKCS320'.         
003600     05  PC-ASTRK                  PIC  X(01)  VALUE '*'.                 
003700     05  PC-A                      PIC  X(01)  VALUE 'A'.                 
003800     05  PC-I                      PIC  X(01)  VALUE 'I'.                 
005610     05  PC-PRIMARY                PIC XX      VALUE '01'.                
005611     05  PC-MAX-RETRIES            PIC S9(03)  VALUE 3                    
005612                                               COMP-3.                    
005613                                                                          
005614     05  PC-TSYMSG-NUMBERS.                                               
005615         10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.              
005616*             -  REQUIRED ENTRY                                           
005617         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
005618*             -  MUST BE NUMERIC                                          
005619         10  PC-TSYMSG-00082       PIC  9(05)  VALUE  00082.              
005620*             -  SPECIFIC VALUES IN ALL FIELDS ARE REQUIRED               
005621         10  PC-TSYMSG-00342       PIC  9(05)  VALUE  00342.              
005622*             -  MUST BE NUMERIC AND > ZERO                               
005623         10  PC-TSYMSG-01452       PIC  9(05)  VALUE  01452.              
005624*             -  THE PORT CODE IS INVALID                                 
005625         10  PC-TSYMSG-01644       PIC  9(05)  VALUE  01644.              
005626*             -  CODE IS INACTIVE                                         
005627                                                                          
005628 01  PS-PROGRAM-SWITCHES.                                                 
005629     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
005630         88  PS-ERROR                          VALUE  'Y'.                
005631         88  PS-NO-ERROR                       VALUE  'N'.                
005632                                                                          
005633 01  PM-PROGRAM-MESSAGES.                                                 
005634     05  PM-ABEND-INFO-A.                                                 
005635         10  FILLER                PIC X(15)   VALUE                      
005636                                   'DP020-SRC-AID: '.                     
005637         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
005638         10  FILLER                PIC X(19)   VALUE                      
005640                                   '. DP020-FK-ACTION: '.                 
005700         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
005800         10  FILLER                PIC X(22)   VALUE                      
005900                                   '. DP020-FK-DISPN-IND: '.              
006000         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
006100         10  FILLER                PIC X(01)   VALUE '.'.                 
006200         10  FILLER                PIC X(19)   VALUE SPACE.               
006400                                                                          
006500******************************************************************        
006600*    MAP LAYOUT                                                  *        
006700******************************************************************        
006900     COPY TRKM320.                                                        
007000 EJECT                                                                    
007100******************************************************************        
007200*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
007300******************************************************************        
007500     COPY DPWS015.                                                        
007600                                                                          
007700******************************************************************        
007800*    FUNCTION KEYS COPYBOOK                                      *        
007900******************************************************************        
008100     COPY DPWS016.                                                        
008200 EJECT                                                                    
008300******************************************************************        
008400** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
008500** SUB-ROUTINE (DPKUT100).                                      **        
008600******************************************************************        
008700     COPY DPWS010I.                                                       
008800 EJECT                                                                    
008900******************************************************************        
009000** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **        
009100** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **        
009200******************************************************************        
009300     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
009400 EJECT                                                                    
009500*****************************************************************         
009600*    STANDARD COMMAREA.                                                   
009700*****************************************************************         
009800                                                                          
009900     COPY DPWS020.                                                        
010000     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
010100                                                                          
010200*****************************************************************         
010300*    INTER-APPLICATION COMMAREA.                                          
010400*****************************************************************         
010500                                                                          
010600         10  INTER-APPL-COMM-AREA PIC X(200).                             
010700     COPY TRWS320.                                                        
010800*****************************************************************         
010900*    SPECIFIC COMMMAREA FOR TRKCS320.                                     
011000*****************************************************************         
011100         10  ASC-SPECIFIC-COMMAREA.                                       
011200             15  ASC-POE-CDE                    PIC  X(03).               
028601             15  ASC-WEEK-ENTERED-DTE           PIC  X(08).               
028602             15  ASC-WEEK-ENTERED-DTE-N REDEFINES                         
028603                 ASC-WEEK-ENTERED-DTE           PIC  9(08).               
028604             15  ASC-WEEK-FROM-DTE              PIC  X(10).               
028607             15  ASC-WEEK-TO-DTE                PIC  X(10).               
028610             15  ASC-POE-CDE-IND                PIC  X(01).               
028611                 88  ASC-VALID-POE-CDE               VALUE 'Y'.           
028612                 88  ASC-INVALID-POE-CDE             VALUE 'N'.           
028613                 88  ASC-INACTIVE-POE-CDE            VALUE 'I'.           
028614     EJECT                                                                
028615******************************************************************        
028616*    ABEND PROCESSING COPYBOOK.                                  *        
028617******************************************************************        
028618     COPY DPWS013.                                                        
028619                                                                          
028620*****************************************************************         
028621*    DB2 AREA FOR PORT OF ENTRY TABLE                           *         
028622*****************************************************************         
028623     EXEC SQL                                                             
028624          INCLUDE TIMPOE                                                  
028625     END-EXEC.                                                            
028630                                                                          
035900*****************************************************************         
036000*    DB2 AREA FOR TENTNME                                       *         
036100*****************************************************************         
036200     EXEC SQL                                                             
036300          INCLUDE TENTNME                                                 
036400     END-EXEC.                                                            
036401                                                                          
036402*****************************************************************         
036403*    DB2 AREA FOR TEXTENT                                       *         
036404*****************************************************************         
036405     EXEC SQL                                                             
036406          INCLUDE TEXTENT                                                 
036407     END-EXEC.                                                            
036408                                                                          
036409*****************************************************************         
036410*    DB2 AREA FOR COMMUNICATIONS                                *         
036411*****************************************************************         
036412     EXEC SQL                                                             
036413          INCLUDE SQLCA                                                   
036414     END-EXEC.                                                            
036415                                                                          
036416******************************************************************        
036417* D266-GET-BEGIN-END-DATES (TRPD266) WORK FIELDS.                         
036418******************************************************************        
036419     COPY TRRD266.                                                        
036420                                                                          
036486 EJECT                                                                    
036487 LINKAGE SECTION.                                                         
036488                                                                          
036489 01  DFHCOMMAREA.                                                         
036490     05  FILLER                         OCCURS  1 TO 4072 TIMES           
036491                                        DEPENDING ON EIBCALEN.            
036492         10  FILLER                     PIC X.                            
036493                                                                          
036494                                                                          
036495 PROCEDURE DIVISION.                                                      
036496*****************************************************************         
036497** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
036498** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
036499** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
036500**                                                             **         
036501** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
036502** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
036503*****************************************************************         
036504 0000-MAIN-MODULE.                                                        
036505     INITIALIZE DP030-CICS-API-FIELDS.                                    
036506     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
036507     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
036508     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
036509     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
036510     MOVE LENGTH OF TR320AI        TO DP030-MAP-LENGTH (1).               
036511     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
036512     PERFORM 0001-CALL-CICS-ARCH-API.                                     
036513                                                                          
036514     PERFORM 1000-CONTROL-PROCESSING.                                     
036515                                                                          
036516     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
036517     PERFORM 0001-CALL-CICS-ARCH-API.                                     
036518                                                                          
036519 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
036521                                    DFHCOMMAREA                           
036522                                    DP030-CICS-API-FIELDS                 
036523                                    DP020-STANDARD-COMMAREA               
036524                                    TR320AI.                              
036525                                                                          
036526     IF  DP030-RC-CALL-SUCCESSFUL                                         
036527         CONTINUE                                                         
036528     ELSE                                                                 
036529         SET DP013-NO-ROLLBACK                                            
036530             DP013-XCTL-DISPLAY-RESTART                                   
036531             DP013-CICS-ABEND      TO TRUE                                
036532         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
036533                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
036536         MOVE DP030-RETURN-CODE                                           
036537                                   TO DP013-MESSAGE-TEXT (3)              
036538         PERFORM DP013-0000-PROCESS-ABEND                                 
036539     END-IF.                                                              
036540 EJECT                                                                    
036541******************************************************************        
036542*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
036543*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
036544******************************************************************        
036545 1000-CONTROL-PROCESSING.                                                 
036546                                                                          
036547     EVALUATE TRUE                                                        
036548         WHEN DP020-NEXT-ACT-INITIAL                                      
036549             INITIALIZE ASC-SPECIFIC-COMMAREA                             
036550             PERFORM 4000-BUILD-INITIAL-PANEL                             
036551                                                                          
036552         WHEN DP020-NEXT-ACT-READ-MAP                                     
036553             PERFORM 2000-FKEY-PROCESSING                                 
036554                                                                          
036555         WHEN DP020-NEXT-ACT-RETURN                                       
036557             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
036558             MOVE -1      TO APOECODL                                     
036559             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
036560             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036561                                                                          
036562         WHEN OTHER                                                       
036563             SET DP013-LOGIC-ABEND                                        
036564                 DP013-NO-ROLLBACK TO TRUE                                
036565             MOVE '1000-CONTROL-PROCESSING'                               
036566                                   TO  DP013-PARAGRAPH                    
036567             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
036568                                   TO  DP013-MESSAGE-TEXT(1)              
036569             MOVE DP020-NEXT-APPL-ACTIVITY                                
036570                                   TO DP013-MESSAGE-TEXT(2)               
036571             PERFORM DP013-0000-PROCESS-ABEND                             
036572     END-EVALUATE.                                                        
036573                                                                          
036574******************************************************************        
036575* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
036576* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
036577* EDITS.                                                         *        
036578******************************************************************        
036579 2000-FKEY-PROCESSING.                                                    
036580                                                                          
036581     EVALUATE TRUE                                                        
036582                                                                          
036583         WHEN DP020-SRC-AID = DP016-CLEAR                                 
036584             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036585             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
036586                                                                          
036587         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
036588             INITIALIZE ASC-SPECIFIC-COMMAREA                             
036589             PERFORM 4000-BUILD-INITIAL-PANEL                             
036590             PERFORM 3200-RESET-ATTRIBUTES                                
036591                                                                          
036592         WHEN OTHER                                                       
036593             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
036594             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
036595             IF PS-NO-ERROR                                               
036597                 SET DP030-CONTINUE-GO TO TRUE                            
036598                 PERFORM 2100-CHECK-FUNCTION-KEY                          
036599             ELSE                                                         
036600                 SET DP030-OVERRIDE-GO TO TRUE                            
036601             END-IF                                                       
036602     END-EVALUATE.                                                        
036603                                                                          
036604******************************************************************        
036605** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
036606** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
036607** FROM THE CICS ARCHITECTURE API.                              **        
036608******************************************************************        
036609 2100-CHECK-FUNCTION-KEY.                                                 
036610                                                                          
036611     EVALUATE TRUE                                                        
036621         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
036622         WHEN DP020-FK-LOCAL-FUNC-03 (DP020-SRC-AID)                      
043245             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
043255                                                                          
043256         WHEN DP020-SRC-AID = DP016-ENTER                                 
043257             CONTINUE                                                     
043258                                                                          
043259         WHEN OTHER                                                       
043260             SET DP013-NO-ROLLBACK                                        
043261                 DP013-XCTL-DISPLAY-RESTART                               
043262                 DP013-CICS-ABEND  TO TRUE                                
043263             MOVE '2100-CHECK-FUNCTION-KEY'                               
043264                                   TO DP013-PARAGRAPH                     
043265             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
043266                                   TO DP013-MESSAGE-TEXT (1)              
043267             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
043268                                   TO DP013-MESSAGE-TEXT (2)              
043269             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
043270             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
043271                                   TO PM-A-FK-ACTION                      
043272             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
043273                                   TO PM-A-FK-DISPN                       
043274             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
043275             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
043276                                   TO DP013-MESSAGE-TEXT (4)              
043277             PERFORM DP013-0000-PROCESS-ABEND                             
043278     END-EVALUATE.                                                        
043279                                                                          
043280******************************************************************        
043281** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
043282** INTER-APPLICATION COMMAREA.                                  **        
043283******************************************************************        
043284 2110-PREPARE-INTER-APPL-COMM.                                            
043285                                                                          
043286     INITIALIZE TR320-INTER-APPL-COMMAREA.                                
166200                                                                          
166500     MOVE ASC-POE-CDE              TO TR320-POE-CDE.                      
166800     MOVE PV-WEEK-OF-DTE           TO TR320-WEEK-ENTERED-DTE.             
166900     MOVE ASC-WEEK-FROM-DTE        TO TR320-WEEK-FROM-DTE.                
167000     MOVE ASC-WEEK-TO-DTE          TO TR320-WEEK-TO-DTE.                  
167100     MOVE 'WORKING'                TO TR320-VIEW.                         
167300                                                                          
168002 EJECT                                                                    
168003******************************************************************        
168004* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
168005* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
168006* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
168007******************************************************************        
168009 2200-MOVE-SCREEN-TO-COMMAREA.                                            
168010                                                                          
168011     IF  APOECODL > ZERO                                                  
168012         MOVE APOECODI             TO ASC-POE-CDE                         
168013     ELSE                                                                 
168014         IF  APOECODF = DP015-ERASE-EOF                                   
168015             MOVE SPACE            TO ASC-POE-CDE                         
168017         END-IF                                                           
168018     END-IF.                                                              
168019                                                                          
168020     IF  AWEEKOFL > ZERO                                                  
168021         MOVE AWEEKOFI             TO ASC-WEEK-ENTERED-DTE                
168022     ELSE                                                                 
168023         IF  AWEEKOFF = DP015-ERASE-EOF                                   
168024             MOVE SPACE            TO ASC-WEEK-ENTERED-DTE                
168026         END-IF                                                           
168027     END-IF.                                                              
168028                                                                          
168029******************************************************************        
168030* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
168031* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
168032* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
168033* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
168034******************************************************************        
168035 3000-EDIT-DATA-IN-COMMAREA.                                              
168036                                                                          
168038                                                                          
168039     PERFORM 3200-RESET-ATTRIBUTES.                                       
168049                                                                          
168110     IF  (ASC-POE-CDE = SPACE AND                                         
168111          ASC-WEEK-ENTERED-DTE = SPACE)                                   
168114           SET PS-ERROR                                                   
168115               DP020-MSG-FATAL   TO TRUE                                  
168117           MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                      
168118           MOVE DP015-UNP-ALP-BRT-OFF                                     
168119                                 TO APOECODA                              
168120                                    AWEEKOFA                              
168121           MOVE DP015-RED        TO APOECODC                              
168122                                    AWEEKOFC                              
168123           MOVE DP015-REVERSE    TO APOECODH                              
168124                                    AWEEKOFH                              
168125           MOVE -1               TO APOECODL                              
168126           SET DP030-SET-CURSOR-APPL-1 TO TRUE                            
168127     ELSE                                                                 
168128         IF (ASC-POE-CDE = SPACE AND                                      
168129             ASC-WEEK-ENTERED-DTE > SPACE)                                
168132              SET PS-ERROR                                                
168133                  DP020-MSG-FATAL   TO TRUE                               
168134              MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER                   
168135              MOVE DP015-UNP-ALP-BRT-OFF                                  
168136                                    TO APOECODA                           
168137              MOVE DP015-RED        TO APOECODC                           
168138              MOVE DP015-REVERSE    TO APOECODH                           
168139              MOVE -1               TO APOECODL                           
168140              SET DP030-SET-CURSOR-APPL-1 TO TRUE                         
168141                                                                          
168142         ELSE                                                             
168143             IF (ASC-POE-CDE > SPACE AND                                  
168144                 ASC-WEEK-ENTERED-DTE = SPACE)                            
168147                  SET PS-ERROR                                            
168148                      DP020-MSG-FATAL   TO TRUE                           
168149                  MOVE PC-TSYMSG-00007  TO DP020-MSG-NUMBER               
168150                  MOVE DP015-UNP-ALP-BRT-OFF                              
168151                                        TO AWEEKOFA                       
168152                  MOVE DP015-RED        TO AWEEKOFC                       
168153                  MOVE DP015-REVERSE    TO AWEEKOFH                       
168154                  MOVE -1               TO AWEEKOFL                       
168155                  SET DP030-SET-CURSOR-APPL-1 TO TRUE                     
168173            END-IF                                                        
168174         END-IF                                                           
168175     END-IF.                                                              
168176                                                                          
168178                                                                          
168179     IF PS-NO-ERROR                                                       
168180     IF ASC-POE-CDE > SPACE                                               
168181         MOVE ASC-POE-CDE          TO DK-POE-CDE                          
168186         PERFORM 4335-SELECT-TIMPOE-INFO                                  
168192         IF ASC-INVALID-POE-CDE                                           
168194             SET PS-ERROR                                                 
168195                 DP020-MSG-FATAL   TO TRUE                                
168196             MOVE PC-TSYMSG-01452  TO DP020-MSG-NUMBER                    
168197             MOVE DP015-UNP-ALP-BRT-OFF                                   
168198                                   TO APOECODA                            
168199             MOVE DP015-RED        TO APOECODC                            
168200             MOVE DP015-REVERSE    TO APOECODH                            
168201             MOVE -1               TO APOECODL                            
168202             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
168203         ELSE                                                             
168204             IF IMPOE-STAT-CDE = PC-I                                     
168207                 SET PS-ERROR                                             
168208                     DP020-MSG-FATAL   TO TRUE                            
168209                 MOVE PC-TSYMSG-01644  TO DP020-MSG-NUMBER                
168210                 MOVE DP015-UNP-ALP-BRT-OFF                               
168211                                       TO APOECODA                        
168212                 MOVE DP015-RED        TO APOECODC                        
168213                 MOVE DP015-REVERSE    TO APOECODH                        
168214                 MOVE -1               TO APOECODL                        
168215                 SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
168220             END-IF                                                       
168221         END-IF                                                           
168222     END-IF                                                               
168223     END-IF.                                                              
168224                                                                          
168225     IF PS-NO-ERROR                                                       
168226     IF ASC-WEEK-ENTERED-DTE > SPACES                                     
168227         MOVE ASC-WEEK-ENTERED-DTE (1:2) TO PV-WEEK-OF-MM                 
168228         MOVE ASC-WEEK-ENTERED-DTE (3:2) TO PV-WEEK-OF-DD                 
168229         MOVE ASC-WEEK-ENTERED-DTE (5:4) TO PV-WEEK-OF-YYYY               
168230*----TSYMSG-008-MUST BE NUMERIC                                           
168231         MOVE ASC-WEEK-ENTERED-DTE-N TO DP010I-UNEDITED-FIELD             
168232         MOVE LENGTH OF ASC-WEEK-ENTERED-DTE                              
168233                               TO DP010I-MAXIMUM-DIGITS                   
168234         MOVE ZERO             TO DP010I-MAXIMUM-DECIMALS                 
168235         SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
263697         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263697              USING DP010I-NUMERIC-EDIT-AREA                              
168237         IF  DP010I-ERROR-DETECTED                                        
168238              SET PS-ERROR                                                
168239                  DP020-MSG-FATAL   TO TRUE                               
168240              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                   
168242              MOVE DP015-UNP-ALP-BRT-OFF                                  
168243                                    TO AWEEKOFA                           
168244              MOVE DP015-RED        TO AWEEKOFC                           
168245              MOVE DP015-REVERSE    TO AWEEKOFH                           
168246              MOVE -1               TO AWEEKOFL                           
168247              SET DP030-SET-CURSOR-APPL-1 TO TRUE                         
168248         ELSE                                                             
168249              MOVE DP010I-NUMERIC-FIELD                                   
168250                                    TO ASC-WEEK-ENTERED-DTE-N             
168252         END-IF                                                           
168253     END-IF                                                               
168254     END-IF.                                                              
168255                                                                          
168257                                                                          
168258     IF  PS-NO-ERROR                                                      
168259          MOVE PV-WEEK-OF-DTE TO PV-266-DATE-ENTERED                      
168260          PERFORM D266-GET-BEGIN-END-DATES                                
168261          IF PS-266-ERRORS                                                
168262*****   NEED AN ERROR MESSAGE HERE                                        
168263            CONTINUE                                                      
168264           ELSE                                                           
168265            MOVE PV-266-BEGIN-DATE TO ASC-WEEK-FROM-DTE                   
168266            MOVE PV-266-END-DATE   TO ASC-WEEK-TO-DTE                     
168267          END-IF                                                          
168268     END-IF.                                                              
168270                                                                          
168271     IF  PS-NO-ERROR                                                      
168272         MOVE -1                     TO APOECODL                          
168273         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
168280     END-IF.                                                              
168300                                                                          
168346******************************************************************        
168347** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
168348** MAP.                                                         **        
168349******************************************************************        
168350 3200-RESET-ATTRIBUTES.                                                   
168351                                                                          
168352     MOVE DP015-UNP-ALP-NOR-OFF    TO APOECODA.                           
168353     MOVE DP015-GREEN              TO APOECODC.                           
168354     MOVE DP015-UNDERLINE          TO APOECODH.                           
168355                                                                          
168360     MOVE DP015-UNP-ALP-NOR-OFF    TO AWEEKOFA.                           
168361     MOVE DP015-GREEN              TO AWEEKOFC.                           
168362     MOVE DP015-UNDERLINE          TO AWEEKOFH.                           
168363                                                                          
168364*----------------------------------------------------------------*        
168365*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
168366*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
168367*  IT HERE.                                                      *        
168368*----------------------------------------------------------------*        
168369 4000-BUILD-INITIAL-PANEL.                                                
168370     SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
168400     MOVE -1                       TO APOECODL.                           
172500                                                                          
174901                                                                          
174970*----------------------------------------------------------------*        
174971*  SELECT ADDITIONAL TIMPOE DATA.                                *        
174972*----------------------------------------------------------------*        
174973 4335-SELECT-TIMPOE-INFO.                                                 
174974                                                                          
174983     EXEC SQL                                                             
174984         SELECT MIN(A.STAT_CDE)                                           
174985            INTO :IMPOE-STAT-CDE                                          
174986             FROM TIMPOE A                                                
174987             WHERE A.POE_CDE = :DK-POE-CDE                                
174988     END-EXEC                                                             
174989                                                                          
174990         EVALUATE TRUE                                                    
174991             WHEN SQLCODE = ZERO                                          
174992             WHEN SQLCODE = -811                                          
174993                 SET ASC-VALID-POE-CDE TO TRUE                            
174994             WHEN SQLCODE = +100                                          
174995             WHEN SQLCODE = -305                                          
174996                 SET ASC-INVALID-POE-CDE TO TRUE                          
174997                 MOVE SPACES TO IMPOE-STAT-CDE                            
174998                 CONTINUE                                                 
174999             WHEN OTHER                                                   
175000                 MOVE '4335-SELECT-TIMPOE-INFO'                           
175001                                   TO DP013-PARAGRAPH                     
175002                 MOVE 'SQL ERROR SELECTING TIMPOE INFO'                   
175003                                   TO DP013-MESSAGE-TEXT (1)              
175004                 MOVE SQLCA        TO DP013-SQLCA                         
175005                 MOVE 'TIMPOE '   TO DP013-DB2-TABLE-NAME (1)             
175006                 SET DP013-DB2-ABEND                                      
175007                     DP013-XCTL-DISPLAY-RESTART TO TRUE                   
175008                 PERFORM DP013-0000-PROCESS-ABEND                         
175009         END-EVALUATE.                                                    
175010                                                                          
175011******************************************************************        
175012*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
175013******************************************************************        
175014 4400-MOVE-COMMAREA-TO-SCREEN.                                            
175015                                                                          
175016     MOVE ASC-POE-CDE              TO APOECODO.                           
175017     MOVE ASC-WEEK-ENTERED-DTE     TO AWEEKOFO.                           
175018                                                                          
175019******************************************************************        
175020* EXECUTABLE COPYBOOK FOR DATE ROUTINE - D266-GET-BEGIN-END-DATES         
175021******************************************************************        
175022     COPY TRPD266.                                                        
175023                                                                          
175024******************************************************************        
175025*    ABEND PROCESSOR MODULE                                      *        
175030******************************************************************        
175100     COPY DPPD013.                                                        
