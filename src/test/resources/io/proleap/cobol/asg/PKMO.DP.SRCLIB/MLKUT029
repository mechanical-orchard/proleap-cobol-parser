       IDENTIFICATION DIVISION.                                         MLKUT029
                                                                        MLKUT029
       PROGRAM-ID.              MLKUT029.                               MLKUT029
       AUTHOR.                  TROY BIRK.                              MLKUT029
       INSTALLATION.            KOHLS DEPARTMENT STORES.                MLKUT029
       DATE-WRITTEN.            12/08/92.                               MLKUT029
       DATE-COMPILED.                                                   MLKUT029
                                                                        MLKUT029
      ******************************************************************MLKUT029
      *    CONVERT TRANSFER FILE FROM SCANNING SERVICE TO BE IN THE     MLKUT029
      *    SAME FORMAT AS THE DATA ENTRY TRANSFER FILE.                 MLKUT029
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
Y2K   *          01/25/1999  CONVERTED CONTROL CARD FOR Y2K COMPLIANCE.*        
Y2K   *                      ALTER DATE FORMAT USED TO CALL CALENDAR   *        
Y2K   *                      ROUTINE TO GET Y2K COMPLIANT DATE.        *        
Y2K   *                                        -SCOTT JACKSON          *        
Y2K   * 06041    04/19/1999  PROPERLY INITIALIZE CALENDAR ROUTINE      *        
Y2K   *                      PARAMETERS.       -PAUL KEBER             *        
21731 * 21731    12-17-2001  CHANGE MODULE FOR NON-INTELLIGENT SKU     *        
21731 *                      BY ADD TSKXREF FOR LOOKING UP NI-SKU      *        
21731 *                      AND ADDING OTHER ATTRIBUTES TO EXTRACT    *        
RWJ   * RWJ      11-08-2002  ISSUE WHEN THE SKU DOES NOT EXIST ON      *        
RWJ   *                      TSKXREF           -ROD JANSSEN            *        
W21731* WR21731  04-25-2003  USE TSKXREF INSTEAD OF TSK TO GET STATUS  *        
W21731*                      ALSO USE MLRD019 INSTEAD OF MLRD005I      *        
      * 26006    10-28-2003  ADD OPTION TO CONTROL CARD TO ALLOW       *        
      *                      THE USE OF PREVIOUS ENTRY FOR BUILDING    *        
      *                      DUMMY SKU FOR CURRENT INVALID ENTRY.      *        
      *                      ALSO, PROVIDE THE OPTION IN THE CONTROL   *        
      *                      CARD TO ALLOW A USER DEFINED SKU FOR ANY  *        
      *                      INVALID ENTRY ENCOUNTERED.                *        
      * 25247    12-03-2003  CHANGE PROGRAM TO USE THE NEW MLWS003,    *        
      *                      STORE-TO-STORE LAYOUT WHICH ALSO IS A     *        
      *                      PART OF VALIDATION AT POINT OF ENTRY/     *        
      *                      SCANNING BY THE INVENTORY SERVICES.       *        
      * W29145    08-15-2005 KEYMASTER UPGRADE FOR STORE NUMBER        *        
      *                      EXPANSION.  - V. LEE YANG                 *        
W28545* W28545    08-22-2005 ROD JANSSEN THE NEW TRANSFER PROCESS               
835755*PM00835755 12-29-2005 ROD JANSSEN VENDOR LOOKUP DATE FORMAT ISSUE        
PM3403* PM3403   04-20-2012  S. JACKSON  LARGE RETAIL MLRD019 RECOMPILE         
      ******************************************************************        
      *                                                                 MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
       ENVIRONMENT DIVISION.                                            MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
       CONFIGURATION SECTION.                                           MLKUT029
                                                                        MLKUT029
       SOURCE-COMPUTER. IBM-3090.                                       MLKUT029
       OBJECT-COMPUTER. IBM-3090.                                       MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
       INPUT-OUTPUT SECTION.                                            MLKUT029
                                                                        MLKUT029
       FILE-CONTROL.                                                    MLKUT029
                                                                        MLKUT029
           SELECT TRANSFER-FILE                                         MLKUT029
               ASSIGN TO INP01.                                         MLKUT029
                                                                        MLKUT029
           SELECT CONVERTED-TRANSFER-FILE                               MLKUT029
               ASSIGN TO OUT01.                                         MLKUT029
                                                                        MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
       DATA DIVISION.                                                   MLKUT029
      *                                                                 MLKUT029
      *                                                                 MLKUT029
       FILE SECTION.                                                    MLKUT029
                                                                        MLKUT029
       FD  TRANSFER-FILE                                                MLKUT029
W21731     RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                   MLKUT029
W21731     BLOCK CONTAINS 0 RECORDS                                     MLKUT029
W21731     DATA RECORD IS TRANSFER-RECORD.                                      
       01  TRANSFER-RECORD                     PIC X(80).               MLKUT029
                                                                        MLKUT029
       FD  CONVERTED-TRANSFER-FILE                                      MLKUT029
W21731     RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                   MLKUT029
W21731     BLOCK CONTAINS 0 RECORDS                                     MLKUT029
W21731     DATA RECORD IS CONVERTED-TRANSFER-RECORD.                            
       01  CONVERTED-TRANSFER-RECORD           PIC X(80).               MLKUT029
                                                                        MLKUT029
                                                                        MLKUT029
       EJECT                                                            MLKUT029
       WORKING-STORAGE SECTION.                                         MLKUT029
                                                                        MLKUT029
      ******************************************************************MLKUT029
      *    CALENDAR ROTUINE                                             MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
           COPY DPWS500.                                                MLKUT029
                                                                        MLKUT029
      ******************************************************************MLKUT029
      *    RECORD LAYOUT FOR INPUT TRANSFER FILE FROM SCANNING SERVICE. MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
W29145     COPY MLWS003.                                                   CL**3
                                                                        MLKUT029
      ******************************************************************MLKUT029
      *    OUTPUT RECORD LAYOUT FOR STORE TO STORE TRANSFERS.           MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
W21731     COPY MLRD019.                                                MLKUT029
                                                                        MLKUT029
      *                                                                 MLKUT029
W28545*01  ML501-PV-PROGRAM-VARIABLE.                                           
W28545*    TSKXREF                                                              
W28545*    TUPC                                                                 
W28545*    TVNDSTL                                                              
W28545*    TVNDDPT                                                              
W28545     COPY MLWS501.                                                        
                                                                                
      ******************************************************************MLKUT029
      *  SQL ABEND ROUTINE WORKING STORAGE                              MLKUT029
      ******************************************************************MLKUT029
      *                                                                 MLKUT029
           COPY DPWS004.                                                MLKUT029
      *                                                                 MLKUT029
       01  CC-CONTROL-CARD.                                             MLKUT029
           05  CC-CALENDAR-TYPE                PIC X(02)   VALUE SPACES.MLKUT029
           05  FILLER                          PIC X(01)   VALUE SPACES.MLKUT029
Y2K        05  CC-DATE                         PIC 9(08)   VALUE ZEROES.MLKUT029
26006      05  FILLER                          PIC X(01)   VALUE SPACES.MLKUT029
26006      05  CC-INVALID-OPTION               PIC X(08)   VALUE SPACES.MLKUT029
26006          88  CC-INVALID-USE-PREV                     VALUE                
26006                                                      'USE PREV'.          
26006          88  CC-INVALID-USER-DEFINED                 VALUE                
26006                                                      'USER DEF'.          
26006      05  FILLER                          PIC X(01)   VALUE SPACES.MLKUT029
26006      05  CC-INVALID-VALUE-TO-USE         PIC X(08)   VALUE SPACES.MLKUT029
26006      05  FILLER                          PIC X(51)   VALUE SPACES.MLKUT029
                                                                        MLKUT029
       01  PS-SUBSCRIPTS.                                               MLKUT029
           05  PS-SUB1                         PIC 9(02)   VALUE ZERO.  MLKUT029
           05  PS-SUB2                         PIC 9(02)   VALUE ZERO.  MLKUT029
                                                                        MLKUT029
       01  PS-PROGRAM-SWITCHES.                                         MLKUT029
           05  PS-MORE-INPUT-RECS-SW           PIC X(01)   VALUE 'Y'.   MLKUT029
               88  PS-NO-MORE-INPUT-RECS                   VALUE 'N'.   MLKUT029
           05  PS-VALID-UPC-SW                 PIC X(01)   VALUE 'N'.   MLKUT029
               88  PS-VALID-UPC                            VALUE 'Y'.   MLKUT029
               88  PS-INVALID-UPC                          VALUE 'N'.   MLKUT029
25247      05  PS-PRICE-FOUND-SW               PIC X(01)   VALUE 'N'.           
25247          88  PS-PRICE-FOUND                          VALUE 'Y'.           
25247          88  PS-PRICE-NOT-FOUND                      VALUE 'N'.           
                                                                        MLKUT029
       01  PV-PROGRAM-VARIABLES.                                        MLKUT029
W29145     05  PV-EXTENDED-RTL             PIC S9(06)V99   VALUE ZEROES         
W29145                         COMP.                                            
25247      05  PV-TIMESTAMP.                                                    
25247          10  PV-TIMESTAMP-DATE.                                           
25247              15  PV-TIMESTAMP-DATE-CCYY  PIC X(04)   VALUE SPACES.        
25247              15  FILLER                  PIC X(01)   VALUE '-'.           
25247              15  PV-TIMESTAMP-DATE-MM    PIC X(02)   VALUE SPACES.        
25247              15  FILLER                  PIC X(01)   VALUE '-'.           
25247              15  PV-TIMESTAMP-DATE-DD    PIC X(02)   VALUE SPACES.        
25247          10  FILLER                  PIC X(01)       VALUE '-'.           
25247          10  PV-TIMESTAMP-TIME       PIC X(15)       VALUE                
25247              '23.59.59.999999'.                                           
26006      05  PV-DB2-TEST                 PIC S9(04)      VALUE ZEROES         
26006                          COMP.                                            
25247      05  PV-PREV-UPC-X               PIC X(13)       VALUE SPACES.MLKUT029
W21731     05  PV-PREV-SKU                 PIC X(08)       VALUE SPACES.MLKUT029
W21731     05  PV-PREV-DEPT-NBR            PIC 9(03)       VALUE ZEROES.MLKUT029
W21731     05  PV-PREV-CLASS-NBR           PIC 9(02)       VALUE ZEROES.MLKUT029
25247      05  PV-PREV-PRICE               PIC S9(7)V9(2)  VALUE ZEROES MLKUT029
25247                          COMP-3.                                          
           05  PV-DOC-NO.                                               MLKUT029
               10  PV-DOC-NO-FIRST2        PIC 9(02)       VALUE ZEROES.MLKUT029
               10  PV-DOC-NO-LAST3         PIC 9(03)       VALUE ZEROES.MLKUT029
25247      05  PV-HOLD-FROM-STORE-9        PIC 9(04)       VALUE ZEROES.MLKUT029
25247      05  PV-HOLD-FROM-STORE REDEFINES PV-HOLD-FROM-STORE-9.               
25247          10  FILLER                  PIC 9(02).                   MLKUT029
               10  PV-HOLD-FS-LAST2        PIC 9(02).                   MLKUT029
25247      05  PV-HOLD-TO-STORE            PIC X(04)       VALUE SPACES.MLKUT029
           05  PV-HOLD-TO-STORE-RED REDEFINES PV-HOLD-TO-STORE          MLKUT029
25247                                      PIC X(01)       OCCURS 4.    MLKUT029
25247      05  PV-TO-STORE                 PIC 9(04)       VALUE ZEROES.MLKUT029
           05  PV-TO-STORE-RED REDEFINES PV-TO-STORE                    MLKUT029
25247                                      PIC 9(01)       OCCURS 4.    MLKUT029
25247      05  PV-RETRY-COUNT              PIC S9(03)      VALUE ZEROES MLKUT029
25247                          COMP.                                            
W29145     05  PV-TEMP-COUNT               PIC S9(04)      VALUE ZEROES MLKUT029
W29145                         COMP SYNC.                                       
W29145     05  PV-SKU-X-RIGHT              PIC X(08)       VALUE SPACES MLKUT029
W29145                                                     JUST RIGHT.          
W29145     05  PV-SKU-9-RIGHT REDEFINES PV-SKU-X-RIGHT                          
W29145                                     PIC 9(08).                           
21731      05  PV-SKU-HOLD                 PIC S9(08)V     VALUE ZEROES MLKUT029
21731                          COMP-3.                                          
           05  PV-WORK-UPC                 PIC 9(13).                   MLKUT029
           05  PV-WORK-UPC-X  REDEFINES  PV-WORK-UPC.                   MLKUT029
               10  FILLER                  PIC X(4).                    MLKUT029
                   88  PV-KOHLS-UPC                        VALUE '0400'.MLKUT029
W21731         10  PV-UPC-SKU-9            PIC 9(8).                            
               10  PV-UPC-CHK-DIG          PIC 9.                       MLKUT029
           05  PV-PARAGRAPH-NAME           PIC  X(30)      VALUE SPACES.MLKUT029
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)      VALUE SPACES.MLKUT029
W29145     05  PV-RECORD-IN                PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
W29145     05  PV-SPLIT-RECORD             PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
W29145     05  PV-RECORD-OUT               PIC S9(07)      VALUE ZEROES         
W29145                         COMP-3.                                          
      *                                                                 MLKUT029
       01  PC-PROGRAM-CONSTANTS.                                        MLKUT029
           05  PC-TRANSFER-CODE            PIC 9(01)       VALUE 3.     MLKUT029
           05  PC-PGM-DPKUT500             PIC X(08)       VALUE                
                                                           'DPKUT500'.          
           05  PC-MAX-RETRIES              PIC S9(04)      VALUE +3     MLKUT029
                                                    COMP SYNC.          MLKUT029
25247      05  PC-OPERCO-NBR               PIC X(02)       VALUE '03'.  MLKUT002
W29145     05  PC-MAX-UNIT-RTL             PIC S9(04)V99 VALUE +9999.99         
W29145                         COMP-3.                                          
      *                                                                 MLKUT029
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     MLKUT029
                                                           COMP SYNC.   MLKUT029
      ******************************************************************MLKUT029
      * DB2 UPC SKU TABLE DCLGEN                                        MLKUT029
      ******************************************************************MLKUT029
           EXEC SQL                                                     MLKUT029
               INCLUDE TUPCSKU                                          MLKUT029
           END-EXEC.                                                    MLKUT029
25247 *                                                                         
25247 ******************************************************************        
25247 *     DCLGEN FOR THE DB2 TUPCPLS TABLE                                    
25247 ******************************************************************        
25247       EXEC SQL                                                            
25247           INCLUDE TUPCPLS                                                 
25247       END-EXEC.                                                           
25247 *                                                                         
25247 ******************************************************************        
25247 *     DCLGEN FOR THE DB2 TMITGPL (GROUP PRICING) TABLE.                   
25247 ******************************************************************        
25247       EXEC SQL                                                            
25247           INCLUDE TMITGPL                                                 
25247       END-EXEC.                                                           
25247 *                                                                 MLKUT002
21731 ******************************************************************MLKUT029
21731 * DB2 COMMON AREA DCLGEN                                          MLKUT029
21731 ******************************************************************MLKUT029
           EXEC SQL                                                     MLKUT029
               INCLUDE SQLCA                                            MLKUT029
           END-EXEC.                                                    MLKUT029
           EJECT                                                        MLKUT029
                                                                        MLKUT029
           COPY SQLCA2.                                                 MLKUT029
                                                                        MLKUT029
       PROCEDURE DIVISION.                                              MLKUT029
                                                                        MLKUT029
       A100-MAIN-MODULE.                                                MLKUT029
      ******************************************************************MLKUT029
      *  MAIN PROCESSING                                                MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
           OPEN INPUT  TRANSFER-FILE                                    MLKUT029
                OUTPUT CONVERTED-TRANSFER-FILE.                         MLKUT029
                                                                        MLKUT029
           PERFORM B100-EDIT-CONTROL-CARD.                              MLKUT029
W21731     MOVE PC-TRANSFER-CODE TO ML019-TRANSFER-CODE.                MLKUT029
           PERFORM Z100-READ-TRANSFER-FILE.                             MLKUT029
           PERFORM B200-PROCESS-RECORDS                                 MLKUT029
               UNTIL PS-NO-MORE-INPUT-RECS.                             MLKUT029
                                                                        MLKUT029
           CLOSE TRANSFER-FILE                                          MLKUT029
                 CONVERTED-TRANSFER-FILE.                               MLKUT029
                                                                        MLKUT029
W29145     DISPLAY ' '.                                                         
W29145     DISPLAY ' RECORD IN:        ' PV-RECORD-IN.                          
W29145     DISPLAY ' SPLIT RECORD:     ' PV-SPLIT-RECORD.                       
W29145     DISPLAY ' RECORD OUT:       ' PV-RECORD-OUT.                         
W29145     DISPLAY ' '.                                                         
W29145                                                                          
W29145     IF PV-SPLIT-RECORD > ZEROES                                          
W29145         DISPLAY ' *** THERE WERE TRANSACTIONS WITH RETAIL'               
W29145                 ' EXCEEDING ' PC-MAX-UNIT-RTL                            
W29145         DISPLAY ' *** SPLIT TRANSACTIONS CREATED!!!'                     
W29145         MOVE +4059  TO RETURN-CODE                                       
W29145     END-IF.                                                              
                                                                                
           STOP RUN.                                                    MLKUT029
      *                                                                 MLKUT029
       B100-EDIT-CONTROL-CARD.                                          MLKUT029
      ******************************************************************MLKUT029
      *    CALL THE CALENDAR ROUTINE TO VALIDATE THE DATE ON THE        MLKUT029
      *    CONTROL CARD AND TO RETURN THE FISCAL MONTH.                 MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
W21731     MOVE SPACES           TO ML019-TRANSFER-RECORD.                      
           ACCEPT CC-CONTROL-CARD.                                      MLKUT029
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                          MLKUT029
                                                                        MLKUT029
           DISPLAY 'CONTROL CARD : ' CC-CONTROL-CARD.                   MLKUT029
                                                                        MLKUT029
245000     INITIALIZE DPG51                                                     
245100                DPG52                                                     
245200                DPG53                                                     
245300                DPG54                                                     
245400                DPG55                                                     
245500                DPG56.                                                    
245600                                                                          
           MOVE CC-CALENDAR-TYPE TO DPG51-CALENDAR-TYPE.                MLKUT029
           SET  DPG51-DO-NOT-INCR-DECR-DATE                             MLKUT029
                DPG52-LK-DTE-ISO TO TRUE.                               MLKUT029
           MOVE CC-DATE TO DPG52-LK-DATE-INPUT.                         MLKUT029
                                                                        MLKUT029
           CALL PC-PGM-DPKUT500 USING DPG51 DPG52                       MLKUT029
                                      DPG53 DPG54                       MLKUT029
                                      DPG55 DPG56.                      MLKUT029
                                                                        MLKUT029
           IF DPG54-NO-ERROR                                            MLKUT029
W21731         MOVE DPG56-FISCAL-PERIOD-NBR TO ML019-TRANSFER-KOHLS-MM  MLKUT029
Y2K            DISPLAY 'DPG56 FISCAL PERIOD: ', DPG56-FISCAL-PERIOD-NBR MLKUT029
Y2K            DISPLAY 'DPG55-DB2-ISO-DATE : ', DPG55-DB2-ISO-DATE-N    MLKUT029
           ELSE                                                         MLKUT029
               IF DPG54-WARNING-ERROR                                   MLKUT029
                   DISPLAY 'WARNING : ' DPG54-ERROR-MESSAGE             MLKUT029
               ELSE                                                     MLKUT029
                   DISPLAY '** ABEND ** IN MLKUT029'                    MLKUT029
                   DISPLAY 'DATE ERROR : ' DPG54-ERROR-MESSAGE          MLKUT029
                   MOVE +4003 TO ABEND-CODE                             MLKUT029
                   CALL 'ILBOABN0' USING ABEND-CODE                     MLKUT029
               END-IF                                                   MLKUT029
           END-IF.                                                      MLKUT029
                                                                                
25247      DISPLAY ' DB2-ISO-FMT DATE: ' DPG55-DB2-ISO-DATE-FMT.                
25247      MOVE DPG55-DB2-ISO-DATE-FMT        TO PV-TIMESTAMP-DATE.             
                                                                                
                                                                        MLKUT029
       B200-PROCESS-RECORDS.                                            MLKUT029
      ******************************************************************MLKUT029
      *    MOVE DATA FROM THE INPUT FILE TO THE OUTPUT FILE RECORD.     MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
W29145     ADD +1             TO PV-RECORD-IN.                                  
                                                                                
25247      MOVE ML003-FROM-STORE-9                                              
25247          TO ML019-TRANSFER-FROM-STORE-9                                   
25247             PV-HOLD-FROM-STORE-9.                                 MLKUT029
W29145     IF ML003-DATA-ENTRY-SOURCE                                           
25247          MOVE ML003-BATCH-NBR-9                                           
25247              TO ML019-TRANSFER-BATCH-NBR-9                        MLKUT029
W29145     ELSE                                                                 
25247          MOVE ML003-FROM-STORE-9                                          
25247              TO ML019-TRANSFER-BATCH-NBR-9                        MLKUT029
W29145     END-IF.                                                              
           MOVE PV-HOLD-FS-LAST2      TO PV-DOC-NO-FIRST2.              MLKUT029
W21731     MOVE ML003-MM              TO ML019-TRANSFER-MM.             MLKUT029
W21731     MOVE ML003-DD              TO ML019-TRANSFER-DD.             MLKUT029
W21731     MOVE ML003-UNITS           TO ML019-TRANSFER-QTY.            MLKUT029
25247      MOVE ML003-TO-STORE        TO PV-HOLD-TO-STORE.              MLKUT029
25247      MOVE 4 TO PS-SUB1                                            MLKUT029
                     PS-SUB2.                                           MLKUT029
           INITIALIZE PV-TO-STORE.                                      MLKUT029
25247      PERFORM C100-ALIGN-TO-STORE 4 TIMES.                         MLKUT029
W21731     MOVE PV-TO-STORE           TO  ML019-TRANSFER-TO-STORE-9     MLKUT029
                                          PV-DOC-NO-LAST3.              MLKUT029
W21731     MOVE PV-DOC-NO             TO  ML019-TRANSFER-NBR.           MLKUT029
W21731     MOVE ZEROES                TO  ML019-TRANSFER-DOC-NBR.               
W21731     IF ML003-UPC-X = PV-PREV-UPC-X                                       
W29145         AND NOT ML003-DATA-ENTRY-SOURCE                                  
W21731         MOVE PV-PREV-SKU       TO ML019-SKU                              
W21731         MOVE PV-PREV-DEPT-NBR  TO ML019-TRANSFER-DEPT-NBR                
W21731         MOVE PV-PREV-CLASS-NBR TO ML019-TRANSFER-CLASS-NBR               
25247          MOVE PV-PREV-PRICE     TO ML019-TRANSFER-PER-UNT-RTL-AMT         
W21731     ELSE                                                                 
W21731         IF ML003-UPC-X NUMERIC                                           
25247T             OR ML003-UPC-12-X NUMERIC                                    
W21731             PERFORM C200-CONVERT-UPC-TO-SKU                              
W21731         ELSE                                                             
W29145             MOVE ZEROES              TO PV-TEMP-COUNT                    
W29145             INSPECT ML003-SKU-X                                          
W29145                 TALLYING PV-TEMP-COUNT                                   
W29145                 FOR ALL ' '                                              
W29145* UNSTRING/SPLIT THE STRING AT FIRST SPACE                                
W29145             IF PV-TEMP-COUNT = 1                                         
W29145                 MOVE +1              TO PV-TEMP-COUNT                    
W29145                 UNSTRING ML003-SKU-X                                     
W29145                     DELIMITED BY ' '                                     
W29145                     INTO PV-SKU-X-RIGHT                                  
W29145                     POINTER PV-TEMP-COUNT                                
W29145             END-IF                                                       
W29145* DO A PLAIN MOVE OF THE FIELD, IF EITHER MORE THAN 1 SPACE               
W29145* OR LOCATION OF THE ' ' IS BEFORE POSITION 8                             
W29145             IF PV-TEMP-COUNT < 9                                         
W29145                 MOVE ML003-SKU-X     TO PV-SKU-X-RIGHT                   
W29145             END-IF                                                       
W29145             MOVE PV-SKU-9-RIGHT      TO ML019-SKU                        
W21731         END-IF                                                           
W21731                                                                          
26006          IF ML019-SKU EQUAL ZEROES                                        
25247              MOVE ZEROES TO SKXREF-INTR-UPC-ID                            
25247              MOVE ZEROES TO SKXREF-SKU-STAT-CDE                           
26006              PERFORM C300-DEFAULT-SKU                                     
26006          END-IF                                                           
26006                                                                           
W21731         IF ML019-SKU NOT EQUAL ZEROES                                    
W21731             PERFORM D200-READ-TSKXREF                                    
26006              IF SKXREF-DEPT EQUAL ZEROES                                  
26006                  OR SKXREF-SUBCLASS EQUAL ZEROES                          
26006                                                                           
26006                  PERFORM C300-DEFAULT-SKU                                 
26006                                                                           
26006                  IF ML019-TRANSFER-SEQ-NO = ZEROES                        
26006                      MOVE ML019-SKU(1:3)                                  
26006                          TO ML019-TRANSFER-DEPT-NBR                       
26006                      MOVE ML019-TRANSFER-DEPT-NBR TO SKXREF-DEPT          
26006                      MOVE ML019-SKU(4:2)                                  
26006                          TO ML019-TRANSFER-CLASS-NBR                      
26006                      MOVE ML019-TRANSFER-CLASS-NBR                        
26006                          TO SKXREF-SUBCLASS                               
26006                  END-IF                                                   
26006              END-IF                                                       
W21731         ELSE                                                             
W21731             MOVE ZEROES TO SKXREF-DEPT                                   
W21731                            SKXREF-SUBCLASS                               
W21731         END-IF                                                           
W21731         MOVE SKXREF-DEPT     TO ML019-TRANSFER-DEPT-NBR                  
W21731                                 PV-PREV-DEPT-NBR                         
W21731         MOVE SKXREF-SUBCLASS TO ML019-TRANSFER-CLASS-NBR                 
W21731                                 PV-PREV-CLASS-NBR                        
W21731         MOVE ML019-SKU       TO PV-PREV-SKU                              
W21731         MOVE ML003-UPC-X     TO PV-PREV-UPC-X                            
                                                                                
25247          IF ML003-CLEARANCE-AREA                                          
W29145             OR ML003-DATA-ENTRY-SOURCE                                   
W29145* SPLIT TRANSACTIONS?                                                     
W29145             PERFORM                                                      
W29145                 UNTIL ML003-RETAIL-PRICE <= PC-MAX-UNIT-RTL              
W29145                 ADD +1                   TO PV-SPLIT-RECORD              
W29145                 COMPUTE ML003-RETAIL-PRICE = ML003-RETAIL-PRICE          
W29145                             - PC-MAX-UNIT-RTL                            
W29145                 MOVE PC-MAX-UNIT-RTL                                     
W29145                     TO ML019-TRANSFER-PER-UNT-RTL-AMT                    
W29145                 COMPUTE PV-EXTENDED-RTL                                  
W29145                         = ML019-TRANSFER-PER-UNT-RTL-AMT                 
W29145                         * ML019-TRANSFER-QTY                             
W29145                         ON SIZE ERROR                                    
W29145                         MOVE 1000000.00 TO PV-EXTENDED-RTL               
W29145                 END-COMPUTE                                              
W29145                 IF PV-EXTENDED-RTL > 999999.99                           
W29145                     DISPLAY ' *** EXTENDED RETAIL IS TOO BIG.'           
W29145                     DISPLAY ' *** QTY: ' ML019-TRANSFER-QTY              
W29145                             ' UNIT RETAIL: '                             
W29145                             ML019-TRANSFER-PER-UNT-RTL-AMT               
W29145                     PERFORM Z999-ABEND                                   
W29145                 END-IF                                                   
W29145                 PERFORM Z200-WRITE-OUTPUT-REC                            
W29145             END-PERFORM                                                  
25247              MOVE ML003-RETAIL-PRICE                                      
25247                  TO ML019-TRANSFER-PER-UNT-RTL-AMT                        
25247          ELSE                                                             
25247              IF SKXREF-SKU-STAT-CDE = '20'                                
25247                  MOVE ZEROES             TO UPCPLS-STR-NBR                
25247                  PERFORM Z800-GET-PRICE                                   
25247              ELSE                                                         
25247                  MOVE ML019-TRANSFER-FROM-STORE-9                         
25247                      TO UPCPLS-STR-NBR                                    
25247                  PERFORM Z800-GET-PRICE                                   
25247                  IF PS-PRICE-NOT-FOUND                                    
25247                      MOVE ZEROES             TO UPCPLS-STR-NBR            
25247                      PERFORM Z800-GET-PRICE                               
25247                  END-IF                                                   
25247              END-IF                                                       
25247              IF PS-PRICE-NOT-FOUND                                        
25247                  MOVE ZEROES TO UPCPLS-UNT-RTL-AMT                        
25247              END-IF                                                       
25247              MOVE UPCPLS-UNT-RTL-AMT                                      
25247                  TO ML019-TRANSFER-PER-UNT-RTL-AMT                        
25247          END-IF                                                           
25247          MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO PV-PREV-PRICE             
W21731     END-IF.                                                              
                                                                                
W29145     COMPUTE PV-EXTENDED-RTL                                              
W29145             = ML019-TRANSFER-PER-UNT-RTL-AMT                             
W29145             * ML019-TRANSFER-QTY                                         
W29145             ON SIZE ERROR                                                
W29145             MOVE 1000000.00 TO PV-EXTENDED-RTL                           
W29145     END-COMPUTE.                                                         
W29145     IF PV-EXTENDED-RTL > 999999.99                                       
W29145         DISPLAY ' *** EXTENDED RETAIL IS TOO BIG.'                       
W29145         DISPLAY ' *** QTY: ' ML019-TRANSFER-QTY                          
W29145                 ' UNIT RETAIL: ' ML019-TRANSFER-PER-UNT-RTL-AMT          
W29145         PERFORM Z999-ABEND                                               
W29145     END-IF.                                                              
           PERFORM Z200-WRITE-OUTPUT-REC.                               MLKUT029
           PERFORM Z100-READ-TRANSFER-FILE.                             MLKUT029
                                                                        MLKUT029
       C100-ALIGN-TO-STORE.                                             MLKUT029
      ******************************************************************MLKUT029
      *    LEFT-JUSTIY AND ZERO FILL THE TO STORE.                      MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
           IF PV-HOLD-TO-STORE-RED (PS-SUB1) NUMERIC                    MLKUT029
               MOVE PV-HOLD-TO-STORE-RED (PS-SUB1)                      MLKUT029
                                      TO PV-TO-STORE-RED (PS-SUB2)      MLKUT029
               SUBTRACT 1 FROM PS-SUB2                                  MLKUT029
           END-IF.                                                      MLKUT029
                                                                        MLKUT029
           SUBTRACT 1 FROM PS-SUB1.                                     MLKUT029
                                                                        MLKUT029
                                                                        MLKUT029
       C200-CONVERT-UPC-TO-SKU.                                         MLKUT029
      ******************************************************************MLKUT029
      *    CONVERT THE UPC CODE TO A SKU BY READING THE TSK TABLE.      MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
25247T     IF ML003-IS-UPC-12                                                   
25247T         MOVE ML003-UPC-12   TO TUPCSKU-UPC-NBR                       MLKU
25247T                                PV-WORK-UPC                           MLKU
25247T     ELSE                                                                 
25247          MOVE ML003-UPC      TO TUPCSKU-UPC-NBR                       MLKU
25247                                 PV-WORK-UPC                           MLKU
25247T     END-IF.                                                              
           PERFORM D100-READ-TUPSKU.                                    MLKUT029
           IF PS-VALID-UPC                                              MLKUT029
W21731          MOVE SKXREF-SKU           TO ML019-SKU                          
           ELSE                                                         MLKUT029
               IF PV-KOHLS-UPC                                          MLKUT029
W21731             MOVE PV-UPC-SKU-9      TO ML019-SKU                  MLKUT029
               ELSE                                                     MLKUT029
W21731             MOVE ZEROES            TO ML019-SKU                  MLKUT029
                   DISPLAY 'INVALID VENDOR UPC ' PV-WORK-UPC            MLKUT029
               END-IF                                                   MLKUT029
           END-IF.                                                              
                                                                                
                                                                                
26006 ******************************************************************        
26006 *    DEFAULT INVALID SKU/UPC TO SOMETHING ELSE                            
26006 ******************************************************************        
26006  C300-DEFAULT-SKU.                                                        
26006                                                                           
26006      EVALUATE TRUE                                                        
26006          WHEN CC-INVALID-USE-PREV                                         
26006              MOVE PV-PREV-DEPT-NBR  TO ML019-SKU(1:3)                     
26006              MOVE PV-PREV-CLASS-NBR TO ML019-SKU(4:5)                     
26006              MOVE ZEROES TO ML019-TRANSFER-SEQ-NO                         
26006              DISPLAY ' INVALID DEFAULT TO: ' ML019-SKU                    
26006          WHEN CC-INVALID-USER-DEFINED                                     
26006              MOVE CC-INVALID-VALUE-TO-USE TO ML019-SKU                    
26006              DISPLAY ' INVALID DEFAULT TO: ' ML019-SKU                    
26006      END-EVALUATE.                                                        
26006                                                                           
      ******************************************************************        
      *   READS TUPCSKU, CONVERTS UPC CODE TO SKU                       MLKUT029
      ******************************************************************MLKUT029
       D100-READ-TUPSKU.                                                MLKUT029
                                                                        MLKUT029
           PERFORM                                                      MLKUT029
               WITH TEST AFTER                                          MLKUT029
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       MLKUT029
               UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  MLKUT029
                   OR (SQLCODE = 0)                                     MLKUT029
                   OR (SQLCODE = +100)                                  MLKUT029
               EXEC SQL                                                 MLKUT029
W21731           SELECT  SKU                                            MLKUT029
25247                   ,INTR_UPC_ID                                    MLKUT029
W21731                  ,SKU_STAT_CDE                                   MLKUT029
W21731           INTO  :SKXREF-SKU                                      MLKUT029
25247                 ,:SKXREF-INTR-UPC-ID                              MLKUT029
W21731                ,:SKXREF-SKU-STAT-CDE                             MLKUT029
W21731           FROM   TUPCSKU A                                               
W21731                 ,TSKXREF B                                               
W21731           WHERE A.ITEM_NBR = B.ITM_NBR                           MLKUT029
W21731             AND UPC_NBR = :TUPCSKU-UPC-NBR                       MLKUT029
               END-EXEC                                                 MLKUT029
               EVALUATE TRUE                                            MLKUT029
                   WHEN SQLCODE = 0                                     MLKUT029
                       SET PS-VALID-UPC TO TRUE                         MLKUT029
                   WHEN SQLCODE = -904                                  MLKUT029
                   WHEN SQLCODE = -911                                  MLKUT029
                       CONTINUE                                         MLKUT029
                   WHEN SQLCODE = +100                                  MLKUT029
                       SET PS-INVALID-UPC  TO TRUE                      MLKUT029
                   WHEN OTHER                                           MLKUT029
21731                  DISPLAY ' UPC NUMBER: ' TUPCSKU-UPC-NBR                  
                       MOVE 'D100-READ-TUPSKU' TO  PV-PARAGRAPH-NAME    MLKUT029
                       MOVE 'READING SKU/UPC TABLE'                     MLKUT029
                           TO PV-DB2-OPER-ATTEMPTED                     MLKUT029
                       PERFORM Z900-SQL-ABEND                           MLKUT029
               END-EVALUATE                                             MLKUT029
           END-PERFORM.                                                 MLKUT029
                                                                        MLKUT029
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          MLKUT029
               MOVE 'MAX RETRIES EXCEEDED'                              MLKUT029
                   TO PV-DB2-OPER-ATTEMPTED                             MLKUT029
21731          MOVE 'D100-READ-TUPSKU' TO PV-PARAGRAPH-NAME             MLKUT029
               PERFORM Z900-SQL-ABEND                                   MLKUT029
           END-IF.                                                      MLKUT029
                                                                        MLKUT029
21731 ******************************************************************MLKUT029
21731 *   READS TSKXREF TO LOOK UP THE ATTRIBUTES OF THE SKU            MLKUT029
21731 ******************************************************************MLKUT029
21731  D200-READ-TSKXREF.                                               MLKUT029
21731                                                                   MLKUT029
W21731     MOVE ML019-SKU   TO PV-SKU-HOLD.                                     
21731                                                                           
21731      PERFORM                                                      MLKUT029
21731          WITH TEST AFTER                                          MLKUT029
21731          VARYING PV-RETRY-COUNT FROM 1 BY 1                       MLKUT029
21731          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  MLKUT029
21731              OR (SQLCODE = 0)                                     MLKUT029
21731              OR (SQLCODE = +100)                                  MLKUT029
21731                                                                   MLKUT029
21731          EXEC SQL                                                 MLKUT029
21731              SELECT  DEPT                                         MLKUT029
21731                     ,SUBCLASS                                     MLKUT029
25247                     ,INTR_UPC_ID                                  MLKUT029
21731                     ,SKU_STAT_CDE                                 MLKUT029
21731              INTO  :SKXREF-DEPT                                   MLKUT029
21731                   ,:SKXREF-SUBCLASS                               MLKUT029
25247                   ,:SKXREF-INTR-UPC-ID                            MLKUT029
21731                   ,:SKXREF-SKU-STAT-CDE                           MLKUT029
21731              FROM   TSKXREF                                       MLKUT029
21731              WHERE  SKU        = :PV-SKU-HOLD                     MLKUT029
21731          END-EXEC                                                 MLKUT029
21731                                                                   MLKUT029
21731          EVALUATE TRUE                                            MLKUT029
21731              WHEN SQLCODE = 0                                     MLKUT029
21731                  CONTINUE                                         MLKUT029
21731              WHEN SQLCODE = -904                                  MLKUT029
21731              WHEN SQLCODE = -911                                  MLKUT029
21731                  CONTINUE                                         MLKUT029
25247              WHEN SQLCODE = +100                                  MLKUT029
25247                  MOVE ZEROES TO SKXREF-SKU-STAT-CDE                       
25247                  MOVE ZEROES TO SKXREF-INTR-UPC-ID                        
W21731                 IF ML019-TRANSFER-SEQ-NO = ZEROES                        
W21731                     MOVE ML019-SKU(1:3)                                  
W21731                         TO ML019-TRANSFER-DEPT-NBR                       
W21731                     MOVE ML019-TRANSFER-DEPT-NBR TO SKXREF-DEPT          
W21731                     MOVE ML019-SKU(4:2)                                  
W21731                         TO ML019-TRANSFER-CLASS-NBR                      
W21731                     MOVE ML019-TRANSFER-CLASS-NBR                        
W21731                         TO SKXREF-SUBCLASS                               
26006                      PERFORM D300-VERIFY-DEPT-CLS                         
W21731                 ELSE                                                     
W21731                     MOVE ZEROES TO SKXREF-DEPT                           
W21731                                    SKXREF-SUBCLASS                       
W21731                     DISPLAY ' SKU NOT FOUND ON XREF TABLE'       MLKUT029
W21731                             ' UPC: ' ML003-UPC                           
W21731                             ' SKU: ' PV-SKU-HOLD                         
W21731                 END-IF                                                   
21731              WHEN OTHER                                           MLKUT029
21731                  DISPLAY ' UPC: ' ML003-UPC                               
21731                  DISPLAY ' SKU: ' PV-SKU-HOLD                             
21731                  MOVE 'D200-READ-TSKXREF' TO  PV-PARAGRAPH-NAME   MLKUT029
21731                  MOVE 'READING SKU XREF TABLE'                    MLKUT029
21731                      TO PV-DB2-OPER-ATTEMPTED                     MLKUT029
21731                  PERFORM Z900-SQL-ABEND                           MLKUT029
21731          END-EVALUATE                                             MLKUT029
21731      END-PERFORM.                                                 MLKUT029
21731                                                                   MLKUT029
21731      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          MLKUT029
21731          MOVE 'MAX RETRIES EXCEEDED'                              MLKUT029
21731              TO PV-DB2-OPER-ATTEMPTED                             MLKUT029
21731          MOVE 'D200-READ-TSKXREF' TO PV-PARAGRAPH-NAME            MLKUT029
21731          PERFORM Z900-SQL-ABEND                                   MLKUT029
21731      END-IF.                                                      MLKUT029
21731                                                                   MLKUT029
26006 ******************************************************************        
26006 *    READS TSKXREF TO VERIFY THE DEPT                                     
26006 ******************************************************************        
26006  D300-VERIFY-DEPT-CLS.                                            MLKUT029
26006                                                                   MLKUT029
26006      PERFORM                                                      MLKUT029
26006          WITH TEST AFTER                                          MLKUT029
26006          VARYING PV-RETRY-COUNT FROM 1 BY 1                       MLKUT029
26006          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                  MLKUT029
26006              OR (SQLCODE = 0)                                     MLKUT029
26006              OR (SQLCODE = +100)                                  MLKUT029
26006                                                                   MLKUT029
26006          EXEC SQL                                                 MLKUT029
26006              SELECT  1                                            MLKUT029
26006              INTO  :PV-DB2-TEST                                   MLKUT029
26006              FROM   TSKXREF                                       MLKUT029
26006              WHERE  DEPT       = :SKXREF-DEPT                     MLKUT029
26006                AND  SUBCLASS   = :SKXREF-SUBCLASS                 MLKUT029
26006              FETCH FIRST 1 ROW ONLY                                       
26006          END-EXEC                                                 MLKUT029
26006                                                                   MLKUT029
26006          EVALUATE TRUE                                            MLKUT029
26006              WHEN SQLCODE = 0                                     MLKUT029
26006                  CONTINUE                                         MLKUT029
26006              WHEN SQLCODE = -904                                  MLKUT029
26006              WHEN SQLCODE = -911                                  MLKUT029
26006                  CONTINUE                                         MLKUT029
26006              WHEN SQLCODE = +100                                  MLKUT029
26006                  MOVE ZEROES TO SKXREF-DEPT                               
26006                                 SKXREF-SUBCLASS                           
26006                  DISPLAY ' DEPT/SUB-CLASS NOT FOUND ON XREF'      MLKUT029
26006                          ' UPC: ' ML003-UPC                               
26006                          ' SKU: ' PV-SKU-HOLD                             
26006              WHEN OTHER                                           MLKUT029
26006                  DISPLAY ' UPC: ' ML003-UPC                               
26006                  DISPLAY ' SKU: ' PV-SKU-HOLD                             
26006                  MOVE 'D300-VERIFY-DEPT-CLS' TO PV-PARAGRAPH-NAME MLKUT029
26006                  MOVE 'READING SKU XREF TABLE'                    MLKUT029
26006                      TO PV-DB2-OPER-ATTEMPTED                     MLKUT029
26006                  PERFORM Z900-SQL-ABEND                           MLKUT029
26006          END-EVALUATE                                             MLKUT029
26006      END-PERFORM.                                                 MLKUT029
26006                                                                   MLKUT029
26006      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          MLKUT029
26006          MOVE 'MAX RETRIES EXCEEDED'                              MLKUT029
26006              TO PV-DB2-OPER-ATTEMPTED                             MLKUT029
26006          MOVE 'D300-VERIFY-DEPT-CLS' TO PV-PARAGRAPH-NAME         MLKUT029
26006          PERFORM Z900-SQL-ABEND                                   MLKUT029
26006      END-IF.                                                      MLKUT029
26006                                                                   MLKUT029
       Z100-READ-TRANSFER-FILE.                                         MLKUT029
      ******************************************************************MLKUT029
      *    READ RECORD FROM THE TRANSFER TAPE FROM THE SCANNING SERVICE.MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
           READ TRANSFER-FILE                                           MLKUT029
               INTO ML003-TRANSFER-RECORD                               MLKUT029
               AT END SET PS-NO-MORE-INPUT-RECS TO TRUE.                MLKUT029
                                                                        MLKUT029
       Z200-WRITE-OUTPUT-REC.                                           MLKUT029
      ******************************************************************MLKUT029
      *    WRITE THE CONVERTED TRANSFER RECORD.                         MLKUT029
      ******************************************************************MLKUT029
                                                                        MLKUT029
W28545     SET ML501-PV-SKU-LOOKUP      TO TRUE.                                
835755     MOVE PV-TIMESTAMP-DATE       TO ML501-PV-EFF-DTE-IN.                 
835755**** MOVE CC-DATE                 TO ML501-PV-EFF-DTE-IN.                 
W28545     MOVE SPACES                  TO ML501-PV-ENT-ID-IN.                  
W28545     MOVE ML019-SKU               TO ML501-PV-SKU-IN.                     
W28545     MOVE ML019-TRANSFER-DEPT-NBR TO ML501-PV-DEPT-IN-9.                  
W28545     MOVE SPACES                  TO ML501-PV-INHOUSE-NBR-IN.             
W28545     PERFORM ML501-LOOKUP-VENDOR.                                         
W28545     IF SQLCODE = ZEROES                                                  
W28545         OR SQLCODE = +100                                                
W28545         MOVE ML501-PV-ENT-ID-OUT    TO ML019-TRANSFER-ENT-ID             
W28545     ELSE                                                                 
W28545         DISPLAY '***PARAGRAPH - Z200-WRITE-OUTPUT-REC ***'               
W28545         DISPLAY 'CALL TO GET VENDOR  '                                   
W28545         DISPLAY ' SKU:    ' ML019-SKU                                    
W28545                 ' DEPT:   ' ML019-TRANSFER-DEPT-NBR                      
835755                 ' DATE:   ' PV-TIMESTAMP-DATE                            
835755****             ' DATE:   ' CC-DATE                                      
W28545         PERFORM Z900-SQL-ABEND                                           
W28545     END-IF.                                                              
W28545                                                                          
W29145     ADD +1             TO PV-RECORD-OUT.                                 
                                                                                
           WRITE CONVERTED-TRANSFER-RECORD                              MLKUT029
W21731           FROM ML019-TRANSFER-RECORD.                            MLKUT029
                                                                        MLKUT029
25247  Z800-GET-PRICE.                                                          
25247                                                                           
25247      MOVE PV-TIMESTAMP      TO UPCPLS-EFF-BEG-TMST.                       
25247      SET PS-PRICE-NOT-FOUND TO TRUE.                                      
25247                                                                           
25247      PERFORM                                                              
25247          WITH TEST AFTER                                                  
25247          VARYING PV-RETRY-COUNT FROM 1 BY 1                               
25247          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
25247              OR (SQLCODE = 0)                                             
25247              OR (SQLCODE = +100)                                          
25247                                                                           
25247          EXEC SQL                                                         
25247              SELECT  UNT_RTL_AMT                                          
25247                     ,MEITGP_NBR                                           
25247                     ,UPC_STAT_CHG_DTE                                     
25247                     ,UNT_RTL_CHG_DTE                                      
25247              INTO   :UPCPLS-UNT-RTL-AMT                                   
25247                    ,:UPCPLS-MEITGP-NBR                                    
25247                    ,:UPCPLS-UPC-STAT-CHG-DTE                              
25247                    ,:UPCPLS-UNT-RTL-CHG-DTE                               
25247              FROM                                                         
25247                    TUPCPLS                                                
25247              WHERE                                                        
25247                    UPC_ID             = :SKXREF-INTR-UPC-ID               
25247                AND OPERCO_NBR         = :PC-OPERCO-NBR                    
25247                AND STR_NBR            = :UPCPLS-STR-NBR                   
25247                AND EFF_BEG_TMST      <= :UPCPLS-EFF-BEG-TMST              
25247                AND (EFF_END_TMST     >= :UPCPLS-EFF-BEG-TMST              
25247                     OR EFF_END_TMST IS NULL)                              
25247          END-EXEC                                                         
25247                                                                           
25247          EVALUATE TRUE                                                    
25247              WHEN SQLCODE = 0                                             
25247                   SET PS-PRICE-FOUND    TO TRUE                           
25247                   IF UPCPLS-MEITGP-NBR NOT = ZEROES                       
25247                       PERFORM Z850-GET-GROUP-PRICE                        
25247                   END-IF                                                  
25247              WHEN SQLCODE = -904                                          
25247              WHEN SQLCODE = -911                                          
25247              WHEN SQLCODE = +100                                          
25247                  CONTINUE                                                 
25247              WHEN OTHER                                                   
25247                  DISPLAY '***PARAGRAPH - Z800-GET-PRICE ***'              
25247                  DISPLAY ' UPC-ID: ' SKXREF-INTR-UPC-ID                   
25247                          ' STORE:  ' UPCPLS-STR-NBR                       
25247                          ' EFF TIMESTAMP: ' UPCPLS-EFF-BEG-TMST           
25247                  PERFORM Z900-SQL-ABEND                                   
25247          END-EVALUATE                                                     
25247      END-PERFORM.                                                         
25247                                                                           
25247      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
25247          DISPLAY '*** MAX RETRIES - Z800-GET-PRICE ***'                   
25247          DISPLAY ' UPC-ID: ' SKXREF-INTR-UPC-ID                           
25247                  ' STORE:  ' UPCPLS-STR-NBR                               
25247                  ' EFF TIMESTAMP: ' UPCPLS-EFF-BEG-TMST                   
25247          PERFORM Z900-SQL-ABEND                                           
25247      END-IF.                                                              
25247                                                                           
25247  Z850-GET-GROUP-PRICE.                                                    
25247                                                                           
25247      DISPLAY ' *** LOOKING UP GROUP PRICING ***'                          
25247              ' FROM STORE: ' ML019-TRANSFER-FROM-STORE-NBR                
25247              ' TO STORE: ' ML019-TRANSFER-TO-STORE-NBR                    
25247              ' SKU:   ' ML019-SKU.                                        
25247                                                                           
25247      PERFORM                                                              
25247          WITH TEST AFTER                                                  
25247          VARYING PV-RETRY-COUNT FROM 1 BY 1                               
25247          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                          
25247              OR (SQLCODE = 0)                                             
25247              OR (SQLCODE = +100)                                          
25247                                                                           
25247          EXEC SQL                                                         
25247              SELECT  MITGPL_QTY                                           
25247                     ,GP_AMT                                               
25247              INTO    :MITGPL-MITGPL-QTY                                   
25247                     ,:MITGPL-GP-AMT                                       
25247              FROM   TMITGPL                                               
25247              WHERE  OPERCO_NBR    = :PC-OPERCO-NBR                        
25247                AND  MEITGP_NBR    = :UPCPLS-MEITGP-NBR                    
25247                AND  ADLOC_ID      = '   '                                 
25247                AND  (CURRENT DATE BETWEEN                                 
25247                      BEG_DTE AND END_DTE)                                 
25247          END-EXEC                                                         
25247                                                                           
25247          EVALUATE TRUE                                                    
25247              WHEN SQLCODE = 0                                             
25247                   COMPUTE UPCPLS-UNT-RTL-AMT ROUNDED =                    
25247                         (MITGPL-GP-AMT /                                  
25247                          MITGPL-MITGPL-QTY )                              
25247              WHEN SQLCODE = -904                                          
25247              WHEN SQLCODE = -911                                          
25247              WHEN SQLCODE = +100                                          
25247                  CONTINUE                                                 
25247              WHEN OTHER                                                   
25247                  DISPLAY '***PARAGRAPH - Z850-GET-GROUP-PRICE ***'        
25247                  DISPLAY ' UPC-ID: ' SKXREF-INTR-UPC-ID                   
25247                          ' STORE:  ' UPCPLS-STR-NBR                       
25247                          ' GROUP PRICE ID: ' UPCPLS-MEITGP-NBR            
25247                  PERFORM Z900-SQL-ABEND                                   
25247          END-EVALUATE                                                     
25247      END-PERFORM.                                                         
25247                                                                           
25247      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
25247          DISPLAY '*** MAX RETRIES - Z850-GET-GROUP-PRICE ***'             
25247          DISPLAY ' UPC-ID: ' SKXREF-INTR-UPC-ID                           
25247                  ' STORE:  ' UPCPLS-STR-NBR                               
25247                  ' GROUP PRICE ID: ' UPCPLS-MEITGP-NBR                    
25247          PERFORM Z900-SQL-ABEND                                           
25247      END-IF.                                                              
25247                                                                           
      ******************************************************************MLKUT029
      *  STANDARD DB2 ERROR ABEND ROUTINE                               MLKUT029
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             MLKUT029
      ******************************************************************MLKUT029
       Z900-SQL-ABEND.                                                  MLKUT029
                                                                        MLKUT029
           MOVE +4013                 TO ABEND-CODE.                    MLKUT029
           DISPLAY '**  ABEND     **'.                                  MLKUT029
           DISPLAY '**  PROGRAM   **  =  MLKUT029'.                     MLKUT029
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            MLKUT029
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        MLKUT029
                                                                        MLKUT029
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         MLKUT029
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        MLKUT029
                                                                        MLKUT029
           COPY DPPD004.                                                MLKUT029
                                                                                
25247      PERFORM Z999-ABEND.                                                  
                                                                        MLKUT029
       Z999-ABEND.                                                      MLKUT029
                                                                        MLKUT029
           CALL 'ILBOABN0'  USING ABEND-CODE.                           MLKUT029
                                                                        MLKUT029
W28545*ML501-LOOKUP-VENDOR.                                                     
W28545*ML501-LOOKUP-DEFAULT.                                                    
W28545*ML501-LOOKUP-BY-SKU.                                                     
W28545*ML501-LOOKUP-BY-UPC-NBR.                                                 
W28545*ML501-LOOKUP-BY-UPC-ID.                                                  
W28545*ML501-LOOKUP-BY-ITM-NBR.                                                 
W28545*ML501-LOOKUP-BY-INHOUSE.                                                 
W28545     COPY MLPD501.                                                        
W28545                                                                          
