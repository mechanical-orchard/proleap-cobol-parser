       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUP051.                                       
       AUTHOR.                  SUE PARI.                                       
       DATE-WRITTEN.            05/11/00.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM USES AN EXTRACT FILE THAT CONTAINS THE TP-DKEYS   *        
      * OF VENDORS THAT HAVE NOT HAD ANY PO OR EDI ACTIVITY IN 12      *        
      * MONTHS TO INACTIVATE VENDORS ON THE TRADING PARTNER TABLE      *        
      * AND THE CORRESPONDING DOCUMENT RELATIONSHIPS.                  *        
************************************************************************        
JF0503*  DATE  05/27/03                                                         
JF0503*  CHANGE MADE:                                                           
JF0503*   ADDED CODE TO SEND ENT-ID IN MQSERIES UPDATE OF RMS/SUPPLIER.         
JF0503*  MADE BY: JON FIEDLER             WR# : 24663                           
************************************************************************        
JF0603*  DATE  06/16/03                                                         
JF0603*  CHANGE MADE:                                                           
JF0603*  FIXED ISSUE WITH INCORRECT PO AND ASN INDICATORS BEING SENT            
JF0603*  OVER TO THE RMS/SUPPLIER SYSTEM. THIS ISSUE INVOLVES VENDORS           
JF0603*  WHO HAVE MULTIPLE IDS. THE CHECK FOR PO AND ASN SETUP ONLY             
JF0603*  LOOKED AT THE CURRENT TP_DKEY BEING ADDED, UPDATED, OR DELETED.        
JF0603*  THE CODE WILL NOW LOOK AT PO AND ASN SETUP FOR A PARTICULAR            
JF0603*  ENT_ID AND WILL CHECK ALL TP_DKEYS INVOLVING ONE VENDOR FOR            
JF0603*  PO AND ASN SETUP.                                                      
JF0603*  MADE BY: JON FIEDLER             WR# : 23521                           
************************************************************************        
JF06T3*  DATE  06/26/2003                                                       
JF06T3*  CHANGE MADE:                                                           
JF06T3*   CHANGED THE PO/ASN SETUP CHECK TO EXTRACT BOTH TEST AND               
JF06T3*   PRODUCTION TRANSACTION SETS.                                          
JF06T3*  MADE BY: JON FIEDLER             WR# : 23521                           
************************************************************************        
      *  DATE  10/25/2016                                                       
      *  CHANGE MADE:                                                           
      *   CHANGES MADE TO REMOVE THE LOGIC OF POSTING THE DATA FROM             
      *   INPUT FILE TO THE RS.SUPPLIER.PARTNER.LOAD.Q.                         
      *  MADE BY: COGNIZANT               WR# : CHG0152986                      
************************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
              ASSIGN TO INP01.                                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD.                                                        
           05  INP-TP-DKEY             PIC 9(09).                               
           05  FILLER                  PIC X(71).                               
                                                                                
JF0701                                                                          
JF0701* ------------------------------------------------------------- *         
JF0701 WORKING-STORAGE SECTION.                                                 
JF0701* ------------------------------------------------------------- *         
JF0701 01  WS-TP-DKEY              PIC S9(9) COMP.                              
JF0701*----------------------------------------------------------------*        
JF0701*    PROGRAM CONSTANTS                                                    
JF0701*----------------------------------------------------------------*        
JF0701 01  PROGRAM-CONSTANTS.                                                   
JF0701     05  PC-PGM-NAME               PIC X(09) VALUE 'ETKUP051 '.           
JF0701     05  PC-FIFTY-STARS            PIC X(50) VALUE ALL '*'.               
JF0701                                                                          
JF0701 01  PROGRAM-SWITCHES.                                                    
JF0701     05  END-OF-FILE-SW                 PIC X(01)   VALUE 'N'.            
JF0701         88  END-OF-FILE                            VALUE 'Y'.            
JF0701                                                                          
       01  PV-PARAGRAPH-NAME              PIC X(30).                            
       01  PV-DB2-OPER-ATTEMPTED          PIC X(30).                            
                                                                                
       01 ABEND-AREA.                                                           
           05  ABEND-CODE-SQL            PIC S9(04) VALUE ZERO                  
                                                    COMP SYNC.                  
             88 ABEND-4013                         VALUE +4013.                 
             88 ABEND-4020                         VALUE +4020.                 
                                                                                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           DISPLAY ' ****  PROGRAM ETKUP051 BEGINS   ****'                      
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
JF0701* ------------------------------------------------------------- *         
JF0701*  READ CONTROL CARDS                                                     
JF0701* ------------------------------------------------------------- *         
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           MOVE INP-TP-DKEY TO WS-TP-DKEY.                                      
                                                                                
           EXEC SQL                                                             
              UPDATE TTRDPRT                                                    
              SET    INACTV_DTE = CURRENT DATE,                                 
                     LAST_CHG_DTE = CURRENT DATE,                               
                     LAST_CHG_UID = 'ETKUP051'                                  
              WHERE  TP_DKEY = :WS-TP-DKEY                                      
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000                                                
              DISPLAY 'TP-DKEY = ' INP-TP-DKEY                                  
              MOVE '1000-PROCESS-FILE' TO PV-PARAGRAPH-NAME                     
              MOVE 'UPDATE TTRDPRT' TO PV-DB2-OPER-ATTEMPTED                    
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
           EXEC SQL                                                             
              UPDATE TTRDTRN                                                    
              SET    INACTV_DTE = CURRENT DATE,                                 
                     LAST_CHG_DTE = CURRENT DATE,                               
                     LAST_CHG_UID = 'ETKUP051'                                  
              WHERE  TP_DKEY = :WS-TP-DKEY                                      
                AND  INACTV_DTE = '9999-09-09'                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +000 AND SQLCODE NOT = +100                         
              DISPLAY 'TP-DKEY = ' INP-TP-DKEY                                  
              MOVE '1000-PROCESS-FILE' TO PV-PARAGRAPH-NAME                     
              MOVE 'UPDATE TTRDTRN' TO PV-DB2-OPER-ATTEMPTED                    
              PERFORM 9999-SQL-ERROR.                                           
                                                                                
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
       8000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
       9999-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE-SQL.                               
