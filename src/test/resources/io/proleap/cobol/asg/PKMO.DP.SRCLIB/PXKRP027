       IDENTIFICATION DIVISION.                                         00051001
       PROGRAM-ID.      PXKRP027.                                       00052001
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00053001
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00054001
       DATE-WRITTEN.    07/26/2010.                                     00055001
       DATE-COMPILED.                                                   00056001
      ******************************************************************00057001
      * CONSOLIDATION REPORTING - OSA SUMMARY REPORT                    00058001
      ******************************************************************00059001
      *DATE:08/26/2010   MADE BY:THOMAS HANSON  INITIAL IMPLEMENTATION. 00059908
      ******************************************************************00060001
       ENVIRONMENT DIVISION.                                            00088001
       CONFIGURATION SECTION.                                           00089001
       SOURCE-COMPUTER.        IBM-3090.                                00089101
       OBJECT-COMPUTER.        IBM-3090.                                00089201
       INPUT-OUTPUT SECTION.                                            00089301
       FILE-CONTROL.                                                    00089401
                                                                        00089501
           SELECT INPUT-FILE                                            00089601
                  ASSIGN TO INP01.                                      00089701
           SELECT REPORT-FILE                                           00099101
                  ASSIGN TO OUT01.                                      00099201
                                                                        00099301
       DATA DIVISION.                                                   00099401
       FILE SECTION.                                                    00099501
                                                                        00099601
       FD  INPUT-FILE                                                   00099701
           RECORDING MODE F                                             00099801
           BLOCK CONTAINS 0 RECORDS                                     00099901
           LABEL RECORDS ARE OMITTED                                    00100001
           DATA RECORD IS INPUT-RECORD.                                 00101001
       01  INPUT-RECORD                        PIC X(59).               00102007
                                                                        00127001
       FD  REPORT-FILE                                                  00128001
           RECORDING MODE F                                             00129001
           BLOCK CONTAINS 0 RECORDS                                     00129101
           LABEL RECORDS ARE OMITTED                                    00129201
           DATA RECORD IS REPORT-RECORD.                                00129301
       01  REPORT-RECORD                       PIC X(132).              00129405
                                                                        00129501
       WORKING-STORAGE SECTION.                                         00129601
      ******************************************************************00129701
      **SWITCHES                                                        00129801
      ******************************************************************00129901
       01  PS-PROGRAM-SWITCHES.                                         00130001
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00131001
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00132001
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00133001
           05  PS-END-OF-CURSOR-FILE-SW        PIC X(01)   VALUE 'N'.   00134002
               88  PS-END-OF-CURSOR                        VALUE 'Y'.   00135002
               88  PS-NOT-END-OF-CURSOR                    VALUE 'N'.   00136002
      ******************************************************************00159401
      **TIMESTAMP VARIABLES                                             00159501
      ******************************************************************00159601
       01 DF-DATE-TIME-FIELDS.                                          00159701
          05 DF-ACCEPT-DATE.                                            00159801
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   00159901
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   00160001
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   00161001
          05 DF-ACCEPT-TIME.                                            00162001
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   00163001
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   00164001
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   00165001
          05 DF-FORMAT-DATE.                                            00166001
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   00167001
             10  FILLER                        PIC X(01) VALUE '/'.     00168001
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   00169001
             10  FILLER                        PIC X(01) VALUE '/'.     00169101
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   00169201
      ******************************************************************00169301
      **PROGRAM CONSTANTS                                               00169401
      ******************************************************************00169501
       01  PC-PROGRAM-CONSTANTS.                                        00169601
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00169701
               VALUE 'PXKRP027'.                                        00169801
      ******************************************************************00176001
      **PROGRAM COUNTERS                                                00177001
      ******************************************************************00178001
       01  PC-PROGRAM-COUNTER.                                          00179001
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   00179101
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   00179201
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   00179301
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   00179401
           05  PC-REPORT-EFF-DTES              PIC 9(09) VALUE ZEROS.   00179508
      ******************************************************************00179608
      **PROGRAM HOLDS                                                   00179708
      ******************************************************************00179808
       01  PH-PROGRAM-HOLDS.                                            00179908
           05  PH-EFF-DTE-HOLD                 PIC X(10) VALUE SPACES.  00180008
      ******************************************************************00180108
      **REPORT HEADINGS                                                 00180208
      ******************************************************************00181001
       01  RH-TITLE-1.                                                  00182001
           05  FILLER                          PIC X(56)  VALUE SPACES. 00183003
           05  FILLER                          PIC X(32)                00184003
                             VALUE 'CONSOLIDATION REPORTING         '.  00185003
           05  FILLER                          PIC X(44)  VALUE SPACES. 00186003
       01  RH-TITLE-2.                                                  00189301
           05  FILLER                          PIC X(62)  VALUE SPACES. 00189403
           05  FILLER                          PIC X(32)                00189503
                             VALUE 'OSA SUMMARY                     '.  00189603
           05  FILLER                          PIC X(38)  VALUE SPACES. 00189703
       01  RH-HEADINGS-1.                                               00195001
           05  FILLER                          PIC X(02)  VALUE SPACES. 00195103
           05  FILLER                          PIC X(20)                00196002
                             VALUE 'CONSOLIDATION DATE: '.              00197002
           05  RH-HEADINGS-1-CNSLDTN-DTE       PIC X(10)  VALUE SPACES. 00198001
           05  FILLER                          PIC X(100) VALUE SPACES. 00198103
       01  RH-HEADINGS-2.                                               00199001
           05  FILLER                          PIC X(02)  VALUE SPACES. 00199103
           05  FILLER                          PIC X(07)                00199203
                             VALUE 'STORE #'.                           00199303
           05  FILLER                          PIC X(02)  VALUE SPACES. 00199403
           05  FILLER                          PIC X(30)                00199503
                             VALUE 'BUSINESS GROUP                '.    00199603
           05  FILLER                          PIC X(02)  VALUE SPACES. 00199703
           05  FILLER                          PIC X(11)                00199802
                             VALUE 'STR ON HAND'.                       00199901
           05  FILLER                          PIC X(69)  VALUE SPACES. 00200003
       01  RH-HEADINGS-3.                                               00200101
           05  FILLER                          PIC X(02)  VALUE SPACES. 00200203
           05  FILLER                          PIC X(07)                00200303
                             VALUE '======='.                           00200403
           05  FILLER                          PIC X(02)  VALUE SPACES. 00200503
           05  FILLER                          PIC X(30)                00200603
                             VALUE '=============================='.    00200703
           05  FILLER                          PIC X(02)  VALUE SPACES. 00200803
           05  FILLER                          PIC X(11)                00200902
                             VALUE '==========='.                       00201001
           05  FILLER                          PIC X(69)  VALUE SPACES. 00202003
       01  RH-DETAIL-LINE.                                              00259601
           05  FILLER                          PIC X(04)  VALUE SPACES. 00259703
           05  RH-DL-STORE-NBR                 PIC Z9999  VALUE ZEROS.  00259802
           05  FILLER                          PIC X(02)  VALUE SPACES. 00259903
           05  RH-DL-BUS-GRP-DESC              PIC X(30)  VALUE SPACES. 00260002
           05  FILLER                          PIC X(07)  VALUE SPACES. 00260103
           05  RH-DL-STORE-ON-HAND             PIC -ZZZZ9 VALUE ZEROS.  00261003
           05  FILLER                          PIC X(81)  VALUE SPACES. 00262003
       01  RH-BLANK-LINE.                                               00263003
           05  FILLER                          PIC X(132) VALUE SPACES. 00264003
       01  RH-FOOTERS-1.                                                00281001
           05  FILLER                          PIC X(54)  VALUE SPACES. 00282001
           05  FILLER                          PIC X(19)                00283001
               VALUE '***END OF REPORT***'.                             00284001
           05  FILLER                          PIC X(59)  VALUE SPACES. 00284103
      ******************************************************************00285001
      **COPYBOOKS                                                       00286001
      ******************************************************************00287001
      ***OSA INPUT FILE FORMAT                                          00289104
           COPY PXRD103.                                                00289201
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     00289901
           COPY DPWS132O.                                               00290001
      ******************************************************************00291001
      **ERROR HANDLING                                                  00292001
      ******************************************************************00293001
       01  PV-MISC-ABEND-FIELDS.                                        00294001
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00295001
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00296001
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00297001
                                                                        00298001
       PROCEDURE DIVISION.                                              00299001
      ***************************************************************** 00299101
                                                                        00299201
      ******************************************************************00299301
       0000-MAINLINE.                                                   00299401
                                                                        00299501
           PERFORM 1000-INITIALIZATION.                                 00299601
                                                                        00299701
           PERFORM 2000-PROCESS-RECORDS                                 00299801
                   UNTIL PS-END-OF-INPUT-FILE.                          00299902
                                                                        00301001
           PERFORM 9100-WRAP-UP.                                        00302006
           GOBACK.                                                      00303001
                                                                        00304001
      ******************************************************************00306001
      **INITIALIZATION AREA                                             00307001
      ******************************************************************00308001
       1000-INITIALIZATION.                                             00309001
                                                                        00309101
           OPEN INPUT  INPUT-FILE.                                      00309401
           OPEN OUTPUT REPORT-FILE.                                     00311001
                                                                        00312001
      **RETRIEVE DATE FROM SYSTEM                                       00313001
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             00314001
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             00315001
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             00317001
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             00318001
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             00319001
                                                                        00324001
           PERFORM 2999-READ-INPUT-FILE.                                00325001
           IF PS-NOT-END-OF-INPUT-FILE                                  00325102
              PERFORM 1100-HEADING-PREP                                 00326002
              PERFORM 2100-WRITE-HEADINGS                               00326102
              ADD 1 TO PC-REPORT-EFF-DTES                               00326208
              MOVE PXRD103-CONSOLIDATE-EFF-DTE TO PH-EFF-DTE-HOLD       00326308
           END-IF.                                                      00327002
                                                                        00337001
      ******************************************************************00338001
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       00339001
      ******************************************************************00339101
       1100-HEADING-PREP.                                               00339201
                                                                        00339301
      ***PREPS KOHLS STANDARD HEADING                                   00339601
           MOVE PC-CURRENT-PROGRAM-NAME  TO DP132O-PROGRAM-NAME.        00339702
           MOVE DF-FORMAT-DATE           TO DP132O-RUN-DATE.            00339902
           MOVE DF-ACCEPT-HR             TO DP132O-RUN-HOUR.            00340002
           MOVE DF-ACCEPT-MN             TO DP132O-RUN-MINUTE.          00341002
           MOVE 1                        TO DP132O-REPORT-NUMBER.       00342002
                                                                        00357001
      ******************************************************************00373001
      **PROCESSES INPUT FILE                                            00374001
      ******************************************************************00375001
       2000-PROCESS-RECORDS.                                            00376001
                                                                        00377001
           EVALUATE TRUE                                                00378008
              WHEN PXRD103-CONSOLIDATE-EFF-DTE EQUAL PH-EFF-DTE-HOLD    00379008
                 IF PC-REPORT-LINE-COUNTER > 60                         00379308
                    PERFORM 2100-WRITE-HEADINGS                         00379608
                    MOVE PXRD103-CONSOLIDATE-EFF-DTE TO PH-EFF-DTE-HOLD 00379708
                 END-IF                                                 00379808
                 PERFORM 2200-WRITE-DETAIL                              00381008
                                                                        00381108
              WHEN PXRD103-CONSOLIDATE-EFF-DTE NOT EQUAL PH-EFF-DTE-HOLD00382008
                 INITIALIZE PC-PAGE-COUNTER                             00383008
                 PERFORM 2100-WRITE-HEADINGS                            00384008
                 PERFORM 2200-WRITE-DETAIL                              00387008
                 ADD 1 TO PC-REPORT-EFF-DTES                            00387108
                 MOVE PXRD103-CONSOLIDATE-EFF-DTE TO PH-EFF-DTE-HOLD    00388008
                                                                        00392001
           END-EVALUATE.                                                00392108
                                                                        00392208
           PERFORM 2999-READ-INPUT-FILE.                                00393001
                                                                        00394001
      ***************************************************************** 00407001
      **WRITE REPORT HEADERS                                            00408001
      ***************************************************************** 00409001
       2100-WRITE-HEADINGS.                                             00409101
                                                                        00409201
           INITIALIZE PC-REPORT-LINE-COUNTER.                           00409501
                                                                        00409601
           ADD +1                             TO PC-PAGE-COUNTER.       00409702
           MOVE PC-PAGE-COUNTER               TO DP132O-PAGE-NUMBER.    00409802
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      00419201
                               AFTER ADVANCING PAGE.                    00419301
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE   00419702
           WRITE REPORT-RECORD FROM RH-TITLE-2 AFTER ADVANCING 1 LINE.  00422001
                                                                        00422102
           MOVE PXRD103-CONSOLIDATE-EFF-DTE                             00423002
                               TO RH-HEADINGS-1-CNSLDTN-DTE.            00423102
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       00424001
                               AFTER ADVANCING 1 LINES.                 00425003
           WRITE REPORT-RECORD FROM RH-BLANK-LINE                       00425103
                               AFTER ADVANCING 1 LINES.                 00425203
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       00426001
                               AFTER ADVANCING 1 LINES.                 00427001
           WRITE REPORT-RECORD FROM RH-HEADINGS-3                       00428003
                               AFTER ADVANCING 1 LINES.                 00429003
           ADD +5              TO PC-REPORT-LINE-COUNTER.               00432002
                                                                        00434002
      ***************************************************************** 00435001
      **WRITE DETAIL LINE                                               00436001
      ***************************************************************** 00437001
       2200-WRITE-DETAIL.                                               00438001
                                                                        00439001
           INITIALIZE RH-DETAIL-LINE.                                   00439301
           MOVE PXRD103-STORE-NBR       TO RH-DL-STORE-NBR.             00439402
           MOVE PXRD103-BUS-GRP-DESC    TO RH-DL-BUS-GRP-DESC.          00439502
           MOVE PXRD103-FS-ON-HANDS-QTY TO RH-DL-STORE-ON-HAND.         00439602
                                                                        00459901
           WRITE REPORT-RECORD FROM RH-DETAIL-LINE.                     00460001
           ADD +1                       TO PC-REPORT-LINE-COUNTER.      00461002
           ADD +1                       TO PC-REPORT-RECORDS-WRITTEN.   00465002
                                                                        00466001
      ***************************************************************** 00473001
      **READ INPUT FILE                                                 00474001
      ***************************************************************** 00475001
       2999-READ-INPUT-FILE.                                            00476001
                                                                        00477001
           READ INPUT-FILE INTO PXRD103-OSA-CNSLDTN-RPT-DATA            00478002
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 00479001
           END-READ.                                                    00479101
                                                                        00479201
           IF  PS-NOT-END-OF-INPUT-FILE                                 00479301
               ADD +1 TO PC-INPUT-RECORDS-READ                          00479401
           END-IF.                                                      00479501
                                                                        00479601
      ****************************************************************  00564001
      **WRAP UP LOOSE ENDS AND DISPLAY STATS                            00565006
      ****************************************************************  00566001
       9100-WRAP-UP.                                                    00567006
                                                                        00568001
           IF PC-REPORT-RECORDS-WRITTEN > 0                             00569005
              PERFORM 9200-WRITE-FOOTER                                 00569106
           END-IF.                                                      00569201
                                                                        00569301
           CLOSE INPUT-FILE.                                            00569402
           CLOSE REPORT-FILE.                                           00571002
                                                                        00572001
           PERFORM 9999-DISPLAY-STATISTICS.                             00573001
                                                                        00573106
      ***************************************************************** 00573206
      **WRITE THE END OF REPORT FOOTER                                  00573306
      ***************************************************************** 00573406
       9200-WRITE-FOOTER.                                               00573506
                                                                        00573606
            WRITE REPORT-RECORD FROM RH-FOOTERS-1.                      00573706
                                                                        00574001
      ****************************************************************  00576001
      *DISPLAY PROGRAM STATISTICS                                       00577001
      ****************************************************************  00578001
       9999-DISPLAY-STATISTICS.                                         00579001
                                                                        00579101
           DISPLAY '*************************************************'. 00579201
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.00579301
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         00579401
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         00579501
           DISPLAY '*************************************************'. 00579601
           DISPLAY '* INPUT RECORDS READ.........: '                    00585001
                                          PC-INPUT-RECORDS-READ.        00586001
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    00589001
                                          PC-REPORT-RECORDS-WRITTEN.    00589101
           DISPLAY '* # OF EFFECTIVE DATES IN RPT: '                    00589208
                                          PC-REPORT-EFF-DTES .          00589308
           DISPLAY '*************************************************'. 00589601
                                                                        00589701
      ****************************************************************  00589802
