000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT037.                                                 
000500 AUTHOR.        DEANNA BRANTNER.                                          
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL, 2003.                                              
000800 DATE-COMPILED.                                                           
000900*****************************************************************         
001000*  THIS PROGRAM WILL REFORMAT THE LEGACY TABLES:  TPOINST,      *         
001100*  TINSTCD TO PO DOMAIN TEMPLATES:  XXT_PO_INSTR                          
      * ****************************************************************        
      * CHANGE DESCRIPTION                                                      
      * ****************************************************************        
      * DATE  05/12/2010                                                        
      *  CHANGE MADE: ADDED A NEW IR INDICATOR(WHSE_RPLNT_IND) IN               
      *  TPURCHS TABLE UNDER IMPORT REPLENISHMENT PROJECT. CHANGES MADE         
      *  ACCORDINGLY FOR THE SAME.                                              
      * MADE BY: MONIKA SACHDEVA         TKID: TKMAW78                          
      * CHANGES TAGGED AS MS 05/12/2010                                         
001400*****************************************************************         
001500 ENVIRONMENT DIVISION.                                                    
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT TPURCHS-FILE                                                  
002010         ASSIGN TO INP01.                                                 
002100                                                                          
002101     SELECT TPOINST-FILE                                                  
002102         ASSIGN TO INP02.                                                 
002103                                                                          
002104     SELECT TPOAGMT-FILE                                                  
002105         ASSIGN TO INP03.                                                 
002106                                                                          
002110     SELECT OUTPUT-FILE                                                   
002120         ASSIGN TO OUT01.                                                 
002130                                                                          
002200*****************************************************************         
002300 DATA DIVISION.                                                           
002400*****************************************************************         
002500 FILE SECTION.                                                            
002600                                                                          
002700 FD  TPURCHS-FILE                                                         
002800     RECORDING MODE IS F                                                  
002810     BLOCK CONTAINS 0 RECORDS                                             
002900     LABEL RECORDS ARE STANDARD.                                          
003000                                                                          
      ** CHANGE THE RECORD LENGTH TO ACCOMODATE THE NEW INDICATOR **            
      ** MS 05/12/2010 BEG                                                      
003300*01  TPURCHS-RECORD               PIC X(481).                             
       01  TPURCHS-RECORD               PIC X(482).                             
      ** MS 05/12/2010 END                                                      
003700                                                                          
003701 FD  TPOINST-FILE                                                         
003702     RECORDING MODE IS F                                                  
003703     BLOCK CONTAINS 0 RECORDS                                             
003704     LABEL RECORDS ARE STANDARD.                                          
003705                                                                          
003706 01  TPOINST-RECORD               PIC X(216).                             
003707                                                                          
003708 FD  TPOAGMT-FILE                                                         
003709     RECORDING MODE IS F                                                  
003710     BLOCK CONTAINS 0 RECORDS                                             
003711     LABEL RECORDS ARE STANDARD.                                          
003712                                                                          
003713 01  TPOAGMT-RECORD               PIC X(099).                             
003714                                                                          
003715 FD  OUTPUT-FILE                                                          
003720     RECORDING MODE IS F                                                  
003730     BLOCK CONTAINS 0 RECORDS                                             
003740     LABEL RECORDS ARE STANDARD.                                          
003750                                                                          
003760 01  OUTPUT-RECORD                PIC X(168).                             
003770                                                                          
003900*****************************************************************         
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-TPURCHS-SW            PIC X(01)  VALUE 'N'.               
004400         88  PS-EOF-TPURCHS-YES                  VALUE 'Y'.               
004500         88  PS-EOF-TPURCHS-NO                   VALUE 'N'.               
004501     05  PS-EOF-TPOINST-SW            PIC X(01)  VALUE 'N'.               
004502         88  PS-EOF-TPOINST-YES                  VALUE 'Y'.               
004503         88  PS-EOF-TPOINST-NO                   VALUE 'N'.               
004504     05  PS-EOF-TPOAGMT-SW            PIC X(01)  VALUE 'N'.               
004505         88  PS-EOF-TPOAGMT-YES                  VALUE 'Y'.               
004506         88  PS-EOF-TPOAGMT-NO                   VALUE 'N'.               
004507     05  PS-TPOINST-FOUND-SW          PIC X(01)  VALUE 'N'.               
004508         88  PS-TPOINST-FOUND                    VALUE 'Y'.               
004509         88  PS-TPOINST-NOT-FOUND                VALUE 'N'.               
004510     05  PS-TPOAGMT-FOUND-SW          PIC X(01)  VALUE 'N'.               
004511         88  PS-TPOAGMT-FOUND                    VALUE 'Y'.               
004512         88  PS-TPOAGMT-NOT-FOUND                VALUE 'N'.               
004600                                                                          
004610     05  PS-TPOAGMT-WRITTEN-SW        PIC X(01)  VALUE 'N'.               
004620         88  PS-TPOAGMT-ALL-WRITTEN              VALUE 'Y'.               
004630         88  PS-TPOAGMT-NOT-WRITTEN              VALUE 'N'.               
004631     05  PS-TPOINST-WRITTEN-SW        PIC X(01)  VALUE 'N'.               
004632         88  PS-TPOINST-ALL-WRITTEN              VALUE 'Y'.               
004633         88  PS-TPOINST-NOT-WRITTEN              VALUE 'N'.               
004640                                                                          
004700 01  PC-PROGRAM-CONSTANTS.                                                
004800     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT037'.        
005000                                                                          
005100 01  PV-PROGRAM-VARIABLES.                                                
005900     05  PV-RECS-READ-TPURCHS         PIC  9(09) VALUE  0.                
005901     05  PV-RECS-READ-TPOINST         PIC  9(09) VALUE  0.                
005902     05  PV-RECS-READ-TPOAGMT         PIC  9(09) VALUE  0.                
006000     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
007000                                                                          
007100 01  PV-ABEND-FIELDS.                                                     
007400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT037'.        
007700     05  FILLER                       PIC  X(01) VALUE SPACES.            
007701     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
007702     05  FILLER                       PIC  X(01) VALUE SPACES.            
007703     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
007704     05  FILLER                       PIC  X(01) VALUE SPACES.            
007705     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
008200                                                                          
008400***********************************************************               
008500*  COPYBOOK USED FOR THE TPURCHS INPUT FILE               *               
008600***********************************************************               
008601     EXEC SQL                                                             
008602         INCLUDE TPURCHS                                                  
008603     END-EXEC.                                                            
008604                                                                          
008605***********************************************************               
008606*  COPYBOOK USED FOR THE TPOINST INPUT FILE               *               
008607***********************************************************               
008608     COPY XLRD028.                                                        
008609                                                                          
008610***********************************************************               
008611*  COPYBOOK USED FOR THE TPOAGMT INPUT FILE               *               
008612***********************************************************               
008613     EXEC SQL                                                             
008614         INCLUDE TPOAGMT                                                  
008615     END-EXEC.                                                            
008616                                                                          
008900***********************************************************               
009000*  COPYBOOK USED FOR THE LOGISTIC SKU OUTPUT FILE         *               
009100***********************************************************               
009110 01  PC-OUTPUT-RECORD.                                                    
009200     COPY XLRD016.                                                        
009300                                                                          
010800*****************************************************************         
010900 PROCEDURE DIVISION.                                                      
011000*****************************************************************         
011100     DISPLAY '**** BEGINNING XLKUT037 ****'.                              
011200     DISPLAY ' '.                                                         
011300                                                                          
011400     OPEN INPUT  TPURCHS-FILE                                             
011500                 TPOINST-FILE                                             
011501                 TPOAGMT-FILE                                             
011502          OUTPUT OUTPUT-FILE.                                             
011503                                                                          
012100     PERFORM 1000-PROCESS-INPUT-FILE                                      
012200       UNTIL PS-EOF-TPURCHS-YES.                                          
012300                                                                          
012310     CLOSE TPURCHS-FILE                                                   
012320           TPOINST-FILE                                                   
012321           TPOAGMT-FILE                                                   
012322           OUTPUT-FILE.                                                   
012500                                                                          
012600     DISPLAY '   INPUT TPURCHS RECS    = ' PV-RECS-READ-TPURCHS.          
012601     DISPLAY '   INPUT TPOINST RECS    = ' PV-RECS-READ-TPOINST.          
012602     DISPLAY '   INPUT TPOAGMT RECS    = ' PV-RECS-READ-TPOAGMT.          
012700     DISPLAY '  OUTPUT XLRD016 RECS    = ' PV-RECORDS-WRITTEN.            
012800     DISPLAY ' '.                                                         
012900     DISPLAY '****  END OF  XLKUT037  ****'.                              
013000                                                                          
013100     GOBACK.                                                              
013200                                                                          
015200*****************************************************************         
015300*    READ THE PURCHASE ORDER                                    *         
015310*         READ ALL OF THE AGREEMENT ROWS FOR THAT PURCHASE      *         
015320*         ORDER, WRITING OUT ANY REQUIRED OUTPUT RECORDS.       *         
015330*         NOTE: OUTPUT RECORDS WILL ONLY BE WRITTEN FOR SOME    *         
015331*               ROWS.                                           *         
015332*                                                               *         
015340*         READ ALL OF THE PO INSTRUCTION ROWS FOR THAT PURCHASE *         
015350*         ORDER, WRITING OUT ONE RECORD FOR EACH ROW.           *         
015400*****************************************************************         
015500 1000-PROCESS-INPUT-FILE.                                                 
015600                                                                          
015700     MOVE '1000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
015800                                                                          
015900     PERFORM 2000-READ-TPURCHS-INPUT-FILE.                                
015920     PERFORM 3000-READ-TPOINST-INPUT-FILE.                                
015930     PERFORM 4000-READ-TPOAGMT-INPUT-FILE.                                
016000                                                                          
016001     PERFORM UNTIL (PS-EOF-TPURCHS-YES)                                   
016010                                                                          
016013         PERFORM 2400-PROCESS-OTHER-FILES                                 
016015                                                                          
016060         PERFORM 2000-READ-TPURCHS-INPUT-FILE                             
016410                                                                          
016500     END-PERFORM.                                                         
016501                                                                          
016502*****************************************************************         
016503*  READ A TPURCHS RECORD                                                  
016504*****************************************************************         
016505 2000-READ-TPURCHS-INPUT-FILE.                                            
016506                                                                          
016507     MOVE '2000-READ-TPURCHS-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
016509                                                                          
016510     INITIALIZE XL016-DATA-AREA.                                          
016511     INITIALIZE DCLTPURCHS.                                               
016520                                                                          
016530     READ TPURCHS-FILE                                                    
016540          INTO DCLTPURCHS                                                 
016550            AT END                                                        
016560               SET PS-EOF-TPURCHS-YES TO TRUE                             
016570     END-READ.                                                            
016580                                                                          
016590     IF PS-EOF-TPURCHS-YES                                                
016600        CONTINUE                                                          
016700     ELSE                                                                 
016800        MOVE PURCHS-PO-NBR  TO XL016-PO-NBR                               
016900        ADD +1 TO PV-RECS-READ-TPURCHS                                    
017000     END-IF.                                                              
017001                                                                          
017002*****************************************************************         
017003*                                                                         
017004*****************************************************************         
017005 2400-PROCESS-OTHER-FILES.                                                
017006                                                                          
017007     MOVE '2100-READ-OTHER-FILES' TO PV-ABEND-PARAGRAPH.                  
017011                                                                          
017012     SET PS-TPOINST-NOT-WRITTEN TO TRUE.                                  
017013     PERFORM UNTIL (PS-TPOINST-ALL-WRITTEN OR                             
017014                    PS-EOF-TPOINST-YES)                                   
017015                                                                          
017016       EVALUATE TRUE                                                      
017017           WHEN XL028-POINST-PO-NBR > PURCHS-PO-NBR                       
017018               SET PS-TPOINST-ALL-WRITTEN TO TRUE                         
017019                                                                          
017020           WHEN XL028-POINST-PO-NBR < PURCHS-PO-NBR                       
017021               PERFORM 3000-READ-TPOINST-INPUT-FILE                       
017022                                                                          
017023           WHEN OTHER                                                     
017024               INITIALIZE XL016-RECORD                                    
017025               MOVE XL028-POINST-PO-NBR    TO XL016-PO-NBR                
017026               MOVE 1                      TO XL016-INSTR-ID              
017027               MOVE XL028-POINST-RECIP-CDE TO XL016-RECIP-CDE             
017028               IF XL028-INSTCD-DESC(1:3) = '   '                          
017028                   MOVE XL028-INSTCD-DESC(4:147) TO                       
017028                                                  XL016-INSTR-DESC        
017028               END-IF                                                     
017028               IF XL028-INSTCD-DESC(1:2) = '  '                           
017028                   MOVE XL028-INSTCD-DESC(3:148) TO                       
017028                                                  XL016-INSTR-DESC        
017028               END-IF                                                     
017028               IF XL028-INSTCD-DESC(1:1) = ' '                            
017028                   MOVE XL028-INSTCD-DESC(2:149) TO                       
                                                        XL016-INSTR-DESC        
017028               ELSE                                                       
017028                   MOVE XL028-INSTCD-DESC   TO    XL016-INSTR-DESC        
017028               END-IF                                                     
017030               PERFORM 6000-WRITE-OUTPUT                                  
017031               PERFORM 3000-READ-TPOINST-INPUT-FILE                       
017033       END-EVALUATE                                                       
017034                                                                          
017035     END-PERFORM.                                                         
017036                                                                          
017037     SET PS-TPOAGMT-NOT-WRITTEN TO TRUE                                   
017038     PERFORM UNTIL (PS-TPOAGMT-ALL-WRITTEN OR                             
017039                    PS-EOF-TPOAGMT-YES)                                   
017040                                                                          
017041         EVALUATE TRUE                                                    
017042             WHEN POAGMT-PO-NBR > PURCHS-PO-NBR                           
017043                  SET PS-TPOAGMT-ALL-WRITTEN TO TRUE                      
017044                                                                          
017045             WHEN POAGMT-PO-NBR < PURCHS-PO-NBR                           
017046                  PERFORM 4000-READ-TPOAGMT-INPUT-FILE                    
017047                                                                          
017048             WHEN OTHER                                                   
017049                  IF POAGMT-AGRMT-TYP-ID = '3' AND                        
017050                     POAGMT-TYP-CDE = 0                                   
017051                     INITIALIZE XL016-RECORD                              
017052                     MOVE POAGMT-PO-NBR TO XL016-PO-NBR                   
017053                     MOVE 1             TO XL016-INSTR-ID                 
017054                     MOVE 'ED'          TO XL016-RECIP-CDE                
017055                     MOVE POAGMT-EDI-PROMO-TXT TO                         
017056                                      XL016-INSTR-DESC                    
017057                     PERFORM 6000-WRITE-OUTPUT                            
017058                  END-IF                                                  
017059                                                                          
017060                  PERFORM 4000-READ-TPOAGMT-INPUT-FILE                    
017061                                                                          
017062         END-EVALUATE                                                     
017063                                                                          
017070     END-PERFORM.                                                         
017300                                                                          
019200                                                                          
019300*****************************************************************         
019400*  READ A TPOINST RECORD                                                  
019500*****************************************************************         
019600 3000-READ-TPOINST-INPUT-FILE.                                            
019700                                                                          
019800     MOVE '3000-READ-TPOINST-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
019900                                                                          
020300     INITIALIZE XL028-DATA.                                               
020310                                                                          
020400     READ TPOINST-FILE                                                    
020500          INTO XL028-DATA                                                 
020600            AT END                                                        
020700               SET PS-EOF-TPOINST-YES TO TRUE                             
020800     END-READ.                                                            
020810                                                                          
016590     IF PS-EOF-TPOINST-YES                                                
016600         CONTINUE                                                         
016700     ELSE                                                                 
020900         ADD +1 TO PV-RECS-READ-TPOINST                                   
016700     END-IF.                                                              
022200                                                                          
023200                                                                          
023300*****************************************************************         
023400*  READ A TPOAGMT RECORD                                                  
023500*****************************************************************         
023600 4000-READ-TPOAGMT-INPUT-FILE.                                            
023700                                                                          
023800     MOVE '4000-READ-TPOAGMT-INPUT-FILE' TO PV-ABEND-PARAGRAPH.           
023900                                                                          
024300     INITIALIZE DCLTPOAGMT.                                               
024310                                                                          
024400     READ TPOAGMT-FILE                                                    
024500          INTO DCLTPOAGMT                                                 
024600            AT END                                                        
024700               SET PS-EOF-TPOAGMT-YES TO TRUE                             
024800     END-READ.                                                            
024810                                                                          
016590     IF PS-EOF-TPOAGMT-YES                                                
016600         CONTINUE                                                         
016700     ELSE                                                                 
020900         ADD +1 TO PV-RECS-READ-TPOAGMT                                   
016700     END-IF.                                                              
025800                                                                          
027002*****************************************************************         
027003*  WRITE A RECORDS TO THE LOGISTICS SKU OUTPUT FILE             *         
027004*****************************************************************         
027005 6000-WRITE-OUTPUT.                                                       
027006                                                                          
027007     MOVE '6000-WRITE-OUTPUT ' TO PV-ABEND-PARAGRAPH.                     
027009                                                                          
027010     WRITE OUTPUT-RECORD FROM                                             
027011         XL016-RECORD                                                     
027012     END-WRITE.                                                           
027020     ADD +1                 TO PV-RECORDS-WRITTEN.                        
027200                                                                          
