       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP363.                                         00030004
       AUTHOR.        INFOSYS.                                          00040004
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  10-14-2008.                                       00060004
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP363 - MONTHLY DETAILED MERCHANDISE LEDGER       *00100003
      *                      TABLE UPDATE (MLT_MNLY_VND)               *00110004
      ******************************************************************00110100
      ******************************************************************00110200
      *    WR/PITS#     DATE        DESCRIPTION                        *00110300
      *    --------   --------     ---------------------------------   *00110400
      *    W34517     10/14/2008   CLONE OF MLKUP353                   *00110807
      *                                                                *00111107
      ******************************************************************00111302
                                                                        00111402
       ENVIRONMENT DIVISION.                                            00111502
                                                                        00112000
       CONFIGURATION SECTION.                                           00113000
       SOURCE-COMPUTER.        IBM-3090.                                00114000
       OBJECT-COMPUTER.        IBM-3090.                                00115000
       INPUT-OUTPUT SECTION.                                            00116000
       FILE-CONTROL.                                                    00117000
           SELECT CONDENSED-FILE                                        00118000
               ASSIGN TO INP01.                                         00119000
                                                                        00120000
                                                                        00130000
       DATA DIVISION.                                                   00140000
       FILE SECTION.                                                    00150000
                                                                        00160000
       FD  CONDENSED-FILE                                               00170000
           LABEL RECORDS ARE STANDARD                                   00180000
           RECORDING MODE IS F                                          00190000
           BLOCK CONTAINS 0 RECORDS.                                    00200000
       01  CONDENSED-RECORD            PIC X(400).                      00210000
      *                                                                 00220000
                                                                        00230000
       WORKING-STORAGE SECTION.                                         00240000
                                                                        00250000
       01  FILLER                      PIC X(28)   VALUE                00260000
                'BEGINNING OF WORKING STORAGE'.                         00270000
                                                                        00280000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00290000
                                                                        00300000
       01  PC-PROGRAM-CONSTANTS.                                        00310000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK181'.    00320005
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP363'.  00330003
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00340000
                                                                        00350000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00360000
           05  PE-MSG-01                       PIC X(50) VALUE          00370000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00380000
           05  PE-MSG-02                       PIC X(50) VALUE          00390000
                'ATTEMPT TO UPDATE ROW ON TABLE MLT_MNLY_VND FAILED'.   00400004
           05  PE-MSG-03                       PIC X(50) VALUE          00410000
                'ATTEMPT TO INSERT ROW ON TABLE MLT_MNLY_VND FAILED'.   00420004
           05  PE-MSG-04                       PIC X(50) VALUE          00430000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00440000
           05  PE-MSG-05                       PIC X(50) VALUE          00450000
                'UPDATE TO TCKRSTINF FAILED                      '.     00460000
           05  PE-MSG-06                       PIC X(50) VALUE          00470000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00480000
           05  PE-MSG-07                       PIC X(50) VALUE          00490000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00500000
      *                                                                 00510000
       01  PS-PROGRAM-SWITCHES.                                         00520000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00530000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00540000
           05  PS-MLT00018-CURSOR-EOF-SW    PIC X(01)     VALUE 'N'.    00550004
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00560000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00570000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00580000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00590000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00600000
                                                                        00610000
       01  PI-PROGRAM-INDICATORS.                                       00620000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00630000
               88  PI-UPDATE-MLT00018-ROW                 VALUE 'U'.    00640004
               88  PI-INSERT-MLT00018-ROW                 VALUE 'I'.    00650004
      *                                                                 00660000
       01  PROGRAM-VARIABLES.                                           00670000
           05  PV-RETRY-COUNT            PIC S9(04) VALUE +0.           00680000
           05  PV-DEADLOCK-COUNT         PIC S9(04) VALUE +0.           00690000
W28582     05  PV-ML-LWST-ID             PIC 9(09)  VALUE 0.            00700000
W28582***  05  PV-ENT-ID                 PIC 9(09)  VALUE 0.            00710000
      *                                                                 00720000
           05  PV-PARAGRAPH-NAME         PIC  X(30) VALUE SPACES.       00730000
           05  PV-ABEND-MESSAGE          PIC  X(75) VALUE SPACES.       00740000
           05  PV-OPERATION-ATTEMPTED    PIC  X(50) VALUE SPACES.       00750000
           05  PV-CURRENT-TIMESTAMP      PIC  X(26) VALUE SPACES.       00760000
           05  PV-COMMIT-TIMESTAMP       PIC  X(26) VALUE SPACES.       00770000
           05  PV-MKUP-NET-AMT           PIC S9(11)V99 VALUE +0 COMP-3. 00780000
           05  PV-MKUP-INIT-AMT          PIC S9(11)V99 VALUE +0 COMP-3. 00790000
           05  PV-NET-COST               PIC S9(11)V99 VALUE +0 COMP-3. 00800000
           05  PV-NET-RETAIL             PIC S9(11)V99 VALUE +0 COMP-3. 00810000
                                                                        00820000
      *    05  PV-DEPT-NBR               PIC 9(03)   VALUE ZEROES.      00830006
      *    05  PV-DEPT-NBR-2 REDEFINES                                  00840006
      *        PV-DEPT-NBR               PIC S9(4)V  COMP-3.            00850006
      *    05  PV-SUB-CL-NBR             PIC 9(02)   VALUE ZEROES.      00860006
      *    05  PV-DEPT-NBR-2 REDEFINES                                  00870006
      *        PV-DEPT-NBR               PIC S9(4)V  COMP-3.            00880006
      *                                                                 00890000
       01  PA-PROGRAM-ACCUMULATORS.                                     00900000
           05  PA-RECORD-COUNTS.                                        00910000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      00920000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      00930000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      00940000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      00950000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      00960000
                                                                        00970000
       01  PD-PROGRAM-DISPLAYS.                                         00980000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            00990000
       01  SAVE-AREA.                                                   01000000
           05  SV-PRIMARY-KEY.                                          01010000
               10  SV-PARTN-NBR          PIC S9(4)  COMP     VALUE +0.  01020000
               10  SV-DEPT-NBR           PIC S9(4)V COMP-3   VALUE +0.  01030000
               10  SV-MAJ-CL-NBR         PIC S9(4)V COMP-3   VALUE +0.  01040000
               10  SV-SUB-CL-NBR         PIC S9(4)V COMP-3   VALUE +0.  01050000
               10  SV-LOC-NBR            PIC S9(4)  COMP     VALUE +0.  01060000
W28582         10  SV-ML-LWST-ID         PIC S9(9)  COMP     VALUE +0.  01070000
W28582***      10  SV-ENT-ID             PIC S9(9)  COMP     VALUE +0.  01071000
           05  SV-UPDATE-FIELDS.                                        01072000
               10  SV-SLS-REG-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01073000
               10  SV-SLS-PROMO-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01074000
               10  SV-SLS-PROCLR-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01075000
               10  SV-SLS-CLR-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01076000
               10  SV-SLS-OTH-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01077000
               10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01078000
               10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01079000
               10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01080000
               10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01081000
               10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01082000
               10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01083000
               10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01084000
               10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01085000
               10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01086000
               10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01087000
               10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01088000
               10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01089000
               10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089100
               10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01089200
               10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01089300
               10  SV-RCPT-GRO-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01089400
               10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01089500
               10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01089600
               10  SV-PADJ-DISC-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01089700
               10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01089800
               10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01089900
               10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01090000
               10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01090100
               10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01090200
               10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01090300
               10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01090400
               10  SV-DR-ALW-RTL-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01090500
               10  SV-RTV-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01090600
               10  SV-MKUP-INIT-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01090700
               10  SV-MKUP-NET-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01090800
               10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01090900
               10  SV-ONORD-RTL-AMT      PIC S9(11)V99  COMP-3 VALUE +0.01091000
               10  SV-ONORD-COST-AMT     PIC S9(11)V99  COMP-3 VALUE +0.01091100
               10  SV-SHNK-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01091200
               10  SV-END-INV-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01091300
      *                                                                 01091400
      ******************************************************************01091500
      * MLRD156 - MTH DETAILED MERCH LEDGER UPDATE TXN LAYOUT           01091600
      ******************************************************************01091700
           COPY MLRD156.                                                01091800
      *                                                                 01091900
      ******************************************************************01092000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01093000
      ******************************************************************01094000
           COPY DPWS004.                                                01095000
      *                                                                 01096000
      ******************************************************************01097000
      *  USED FOR DB2 ERRORS                                            01098000
      ******************************************************************01099000
           EXEC SQL                                                     01100000
               INCLUDE SQLCA                                            01110000
           END-EXEC.                                                    01120000
      *                                                                 01130000
      ******************************************************************01140000
      *  MERCHANDISE LEDGER MONTHLY DETAIL TABLE                        01150000
      ******************************************************************01160000
           EXEC SQL                                                     01170000
               INCLUDE MLT00018                                         01180004
           END-EXEC.                                                    01190000
      *                                                                 01200000
      ***************************************************************** 01210000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01220000
      ***************************************************************** 01230000
           EXEC SQL                                                     01240000
               INCLUDE TCKRSTCN                                         01250000
           END-EXEC.                                                    01260000
      *                                                                 01270000
           EXEC SQL                                                     01280000
               INCLUDE TCKRSTIN                                         01290000
           END-EXEC.                                                    01300000
      *                                                                 01310000
      ******************************************************************01320000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01330000
      ******************************************************************01340000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01350000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +35  COMP. 01360000
           05  CR-CHECKPOINT-RESTART-VAR.                               01370000
               10  CR-PREV-DTL-KEY.                                     01380000
                   15  CR-PARTN-NBR         PIC  S9(4)  COMP            01390000
                                                         VALUE ZEROES.  01400000
                   15  CR-DEPT-NBR          PIC  S9(4)V COMP-3          01410000
                                                         VALUE ZEROES.  01420000
                   15  CR-MAJ-CL-NBR        PIC  S9(4)V COMP-3          01430000
                                                         VALUE ZEROES.  01440000
                   15  CR-SUB-CL-NBR        PIC  S9(4)V COMP-3          01450000
                                                         VALUE ZEROES.  01460000
                   15  CR-LOC-NBR           PIC  S9(4)  COMP            01470000
                                                         VALUE ZEROES.  01480000
W28582             15  CR-ML-LWST-ID        PIC  S9(9)  COMP            01490000
W28582***          15  CR-ENT-ID            PIC  S9(9)  COMP            01500000
                                                         VALUE ZEROES.  01510000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01520000
      *                                                                 01530000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01540000
           05  CK-SAVE-PREV-KEY.                                        01550000
               10  CK-PARTN-NBR         PIC  S9(4)  COMP VALUE ZEROES.  01560000
               10  CK-DEPT-NBR          PIC  S9(4)V COMP-3              01570000
                                                         VALUE ZEROES.  01580000
               10  CK-MAJ-CL-NBR        PIC  S9(4)V COMP-3              01590000
                                                         VALUE ZEROES.  01600000
               10  CK-SUB-CL-NBR        PIC  S9(4)V COMP-3              01610000
                                                         VALUE ZEROES.  01620000
               10  CK-LOC-NBR           PIC  S9(4)  COMP VALUE ZEROES.  01630000
W28582         10  CK-ML-LWST-ID        PIC  S9(9)  COMP VALUE ZEROES.  01640000
W28582***      10  CK-ENT-ID            PIC  S9(9)  COMP VALUE ZEROES.  01650000
                                                                        01660000
                                                                        01670000
      *-------------------*                                             01680000
       PROCEDURE DIVISION.                                              01690000
      *-------------------*                                             01700000
                                                                        01710000
       0000-MLKUP363.                                                   01720003
                                                                        01730000
           PERFORM 1000-INITIALIZATION.                                 01740000
      *                                                                 01750000
           PERFORM 2000-PROCESS-INPUT                                   01760000
                UNTIL PS-EOF-CONDENSED-FILE.                            01770000
      *                                                                 01780000
           PERFORM 8000-EOJ-ROUTINE.                                    01790000
                                                                        01800000
           STOP RUN.                                                    01810000
                                                                        01820000
      ******************************************************************01830000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01840000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      01850000
      ******************************************************************01860000
      *                                                                 01870000
       1000-INITIALIZATION.                                             01880000
      *                                                                 01890000
           OPEN INPUT CONDENSED-FILE.                                   01900000
                                                                        01910000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         01920000
                                                                        01930000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              01940000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           01950000
               PERFORM 7500-SELECT-RESTART-INFO                         01960000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    01970000
                                            CR-CHECKPOINT-RESTART-VAR   01980000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        01990000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02000000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02010000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02020000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 02030000
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    02040000
W28582         DISPLAY 'ML-LWST-ID      ' CR-ML-LWST-ID                 02050000
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     02060000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02070000
           ELSE                                                         02080000
               SET PS-FIRST-COMMIT TO TRUE                              02090000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02100000
           END-IF.                                                      02110000
      *                                                                 02120000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02130000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02140000
            OR PS-EOF-CONDENSED-FILE.                                   02150000
      *                                                                 02160000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02170000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02180000
                        PA-INPUT-COUNT                                  02190000
               DISPLAY 'DEPARTMENT NBR        ' ML156-DEPT-NBR          02200000
               DISPLAY 'MAJOR CLASS NBR       ' ML156-MAJ-CL-NBR        02210000
               DISPLAY 'SUB CLASS NBR         ' ML156-SUB-CL-NBR        02220000
               DISPLAY 'LOCATION NBR          ' ML156-LOC-NBR           02230000
W28582         DISPLAY 'ML-LWST-ID            ' ML156-ML-LWST-ID        02240000
W28582         MOVE ML156-ML-LWST-ID   TO PV-ML-LWST-ID                 02250000
W28582         DISPLAY 'ML-LWST-ID            ' PV-ML-LWST-ID           02260000
W28582***      DISPLAY 'ENT-ID                ' ML156-ENT-ID            02270000
W28582***      MOVE ML156-ENT-ID   TO PV-ENT-ID                         02280000
W28582***      DISPLAY 'ENT-ID                ' PV-ENT-ID               02290000
           END-IF.                                                      02300000
                                                                        02310000
           IF PS-EOF-CONDENSED-FILE                                     02320000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02330000
                   CONTINUE                                             02340000
               ELSE                                                     02350000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02360000
           ELSE                                                         02370000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02380000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02390000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02400000
           END-IF.                                                      02410000
                                                                        02420000
      ******************************************************************02430000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02440000
      ******************************************************************02450000
       2000-PROCESS-INPUT.                                              02460000
      *                                                                 02470000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02480000
      *                                                                 02490000
           PERFORM 7000-SELECT-DB-ROW.                                  02500000
      *                                                                 02510000
           IF PS-PROGRAM-DEADLOCKED                                     02520000
               CONTINUE                                                 02530000
           ELSE                                                         02540000
               EVALUATE TRUE                                            02550000
                   WHEN PI-UPDATE-MLT00018-ROW                          02560004
                       PERFORM 7100-UPDATE-MLT00018-ROW                 02570004
                   WHEN PI-INSERT-MLT00018-ROW                          02580004
                       PERFORM 7200-INSERT-MLT00018-ROW                 02590004
                   WHEN OTHER                                           02600000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02610000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02620000
                       PERFORM 9999-SQL-ABEND                           02630000
               END-EVALUATE                                             02640000
           END-IF.                                                      02650000
      *                                                                 02660000
           IF PS-PROGRAM-DEADLOCKED                                     02670000
               CONTINUE                                                 02680000
           ELSE                                                         02690000
               PERFORM 7700-COMMIT-PROCESSING                           02700000
               PERFORM 4000-READ-CONDENSED-FILE                         02710000
           END-IF.                                                      02720000
      *                                                                 02730000
      *                                                                 02740000
      ******************************************************************02750000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02760000
      ******************************************************************02770000
       2100-CALCULATE-UPDATES.                                          02780000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02790000
      *                                                                 02800000
           COMPUTE SV-SLS-REG-RTL-AMT =                                 02810000
                   MLT00018-SLS-REG-RTL-AMT + ML156-SLS-REG-RTL-AMT.    02820004
           COMPUTE SV-SLS-PROMO-RTL-AMT =                               02830000
                   MLT00018-SLS-PROMO-RTL-AMT + ML156-SLS-PROMO-RTL-AMT.02840004
           COMPUTE SV-SLS-PROCLR-RTL-AMT =                              02850000
                 MLT00018-SLS-PROCLR-RTL-AMT + ML156-SLS-PROCLR-RTL-AMT.02860004
           COMPUTE SV-SLS-CLR-RTL-AMT =                                 02870000
                   MLT00018-SLS-CLR-RTL-AMT + ML156-SLS-CLR-RTL-AMT.    02880004
           COMPUTE SV-SLS-OTH-RTL-AMT =                                 02890000
                   MLT00018-SLS-OTH-RTL-AMT + ML156-SLS-OTH-RTL-AMT.    02900004
           COMPUTE SV-MKDN-PLU-RTL-AMT  =                               02910000
                   MLT00018-MKDN-PLU-RTL-AMT + ML156-MKDN-PLU-RTL-AMT.  02920004
           COMPUTE SV-MKDN-RGST-RTL-AMT =                               02930000
                   MLT00018-MKDN-RGST-RTL-AMT + ML156-MKDN-RGST-RTL-AMT.02940004
           COMPUTE SV-MKDN-UMTCH-RTL-AMT =                              02950000
                 MLT00018-MKDN-UMTCH-RTL-AMT + ML156-MKDN-UMTCH-RTL-AMT.02960004
           COMPUTE SV-MKDN-BYR-RTL-AMT  =                               02970000
                   MLT00018-MKDN-BYR-RTL-AMT + ML156-MKDN-BYR-RTL-AMT.  02980004
           COMPUTE SV-MKDN-STR-RTL-AMT  =                               02990000
                   MLT00018-MKDN-STR-RTL-AMT + ML156-MKDN-STR-RTL-AMT.  03000004
           COMPUTE SV-MKDN-RTL-AMT      =                               03010000
                   MLT00018-MKDN-RTL-AMT    + ML156-MKDN-RTL-AMT.       03020004
           COMPUTE SV-MKUP-RTL-AMT      =                               03020100
                   MLT00018-MKUP-RTL-AMT    + ML156-MKUP-RTL-AMT.       03020204
           COMPUTE SV-XFER-IN-RTL-AMT   =                               03020300
                   MLT00018-XFER-IN-RTL-AMT + ML156-XFER-IN-RTL-AMT.    03020404
           COMPUTE SV-XFER-OUT-RTL-AMT  =                               03020500
                   MLT00018-XFER-OUT-RTL-AMT + ML156-XFER-OUT-RTL-AMT.  03020604
           COMPUTE SV-RCPT-RTL-AMT      =                               03020700
                   MLT00018-RCPT-RTL-AMT    + ML156-RCPT-RTL-AMT.       03020804
           COMPUTE SV-RCPT-NET-COST-AMT =                               03020900
                   MLT00018-RCPT-NET-COST-AMT + ML156-RCPT-NET-COST-AMT.03021004
           COMPUTE SV-RCPT-GRO-COST-AMT =                               03021100
                   MLT00018-RCPT-GRO-COST-AMT + ML156-RCPT-GRO-COST-AMT.03021204
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03021300
                 MLT00018-PADJ-FRGT-COST-AMT + ML156-PADJ-FRGT-COST-AMT.03021404
           COMPUTE SV-PADJ-DISC-COST-AMT =                              03021500
                 MLT00018-PADJ-DISC-COST-AMT + ML156-PADJ-DISC-COST-AMT.03021604
           COMPUTE SV-PADJ-RTL-AMT      =                               03021700
                   MLT00018-PADJ-RTL-AMT    + ML156-PADJ-RTL-AMT.       03021804
           COMPUTE SV-PADJ-NET-COST-AMT =                               03021900
                   MLT00018-PADJ-NET-COST-AMT + ML156-PADJ-NET-COST-AMT.03022004
           COMPUTE SV-PADJ-FRGT-COST-AMT =                              03022100
                 MLT00018-PADJ-FRGT-COST-AMT + ML156-PADJ-FRGT-COST-AMT.03022204
           COMPUTE SV-DR-ALW-RTL-AMT    =                               03022300
                   MLT00018-DR-ALW-RTL-AMT  + ML156-DR-ALW-RTL-AMT.     03022404
           COMPUTE SV-RTV-RTL-AMT       =                               03022500
                   MLT00018-RTV-RTL-AMT     + ML156-RTV-RTL-AMT.        03022604
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03022700
                   MLT00018-INV-ADJ-RTL-AMT + ML156-INV-ADJ-RTL-AMT.    03022804
           COMPUTE SV-PADJ-RTV-COST-AMT  = MLT00018-PADJ-RTV-COST-AMT   03022904
                                         + ML156-PADJ-RTV-COST-AMT.     03023000
           COMPUTE SV-PADJ-RTV-RTL-AMT   = MLT00018-PADJ-RTV-RTL-AMT    03023104
                                         + ML156-PADJ-RTV-RTL-AMT.      03023200
           COMPUTE SV-PADJ-DA-COST-AMT   = MLT00018-PADJ-DA-COST-AMT    03023304
                                         + ML156-PADJ-DA-COST-AMT.      03023400
           COMPUTE SV-PADJ-DA-RTL-AMT    = MLT00018-PADJ-DA-RTL-AMT     03023504
                                         + ML156-PADJ-DA-RTL-AMT.       03023600
           COMPUTE SV-PADJ-OTH-COST-AMT  = MLT00018-PADJ-OTH-COST-AMT   03023704
                                         + ML156-PADJ-OTH-COST-AMT.     03023800
           COMPUTE SV-PADJ-OTH-RTL-AMT   = MLT00018-PADJ-OTH-RTL-AMT    03023904
                                         + ML156-PADJ-OTH-RTL-AMT.      03024000
           COMPUTE SV-MKDN-PROMO-RTL-AMT = MLT00018-MKDN-PROMO-RTL-AMT  03024104
                                         + ML156-MKDN-PROMO-RTL-AMT.    03024200
           COMPUTE SV-MKDN-PRPT-RTL-AMT  = MLT00018-MKDN-PRPT-RTL-AMT   03024304
                                         + ML156-MKDN-PRPT-RTL-AMT.     03024400
           COMPUTE SV-MKDN-GLDST-RTL-AMT = MLT00018-MKDN-GLDST-RTL-AMT  03024504
                                         + ML156-MKDN-GLDST-RTL-AMT.    03024600
           COMPUTE SV-MKDN-OTH-RTL-AMT   = MLT00018-MKDN-OTH-RTL-AMT    03024704
                                         + ML156-MKDN-OTH-RTL-AMT.      03024800
                                                                        03024900
      *--- INITIAL MARKUP AMOUNT                                        03025000
           COMPUTE PV-MKUP-INIT-AMT =                                   03026000
                   ML156-RCPT-RTL-AMT - ML156-RCPT-NET-COST-AMT.        03027000
      *            ML156-RCPT-RTL-AMT - ML156-RCPT-GRO-COST-AMT.        03028000
           COMPUTE SV-MKUP-INIT-AMT   =                                 03029000
                   MLT00018-MKUP-INIT-AMT + PV-MKUP-INIT-AMT.           03030004
                                                                        03040000
      *--- NET MARKUP AMOUNT                                            03050000
      *    COMPUTE PV-MKUP-NET-AMT =                                    03060000
      *           (ML156-RCPT-RTL-AMT -                                 03070000
      *           (ML156-RCPT-GRO-COST-AMT +                            03080000
      *            ML156-PADJ-FRGT-COST-AMT) -                          03090000
      *            ML156-PADJ-DISC-COST-AMT).                           03100000
           COMPUTE PV-NET-RETAIL =                                      03110000
                  (ML156-RCPT-RTL-AMT +                                 03120000
                   ML156-PADJ-RTL-AMT +                                 03130000
                   ML156-MKUP-RTL-AMT +                                 03140000
                  (ML156-XFER-IN-RTL-AMT -                              03150000
                   ML156-XFER-OUT-RTL-AMT))                             03160000
           END-COMPUTE.                                                 03170000
           COMPUTE PV-NET-COST = ML156-RCPT-NET-COST-AMT +              03180000
                                 ML156-PADJ-NET-COST-AMT                03190000
           END-COMPUTE.                                                 03200000
                                                                        03210000
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        03220000
           END-COMPUTE.                                                 03230000
                                                                        03240000
           COMPUTE SV-MKUP-NET-AMT     =                                03250000
                   MLT00018-MKUP-NET-AMT + PV-MKUP-NET-AMT.             03260004
                                                                        03270000
           COMPUTE SV-INV-ADJ-RTL-AMT  =                                03280000
                   MLT00018-INV-ADJ-RTL-AMT + ML156-INV-ADJ-RTL-AMT.    03290004
      *                                                                 03300000
      ******************************************************************03310000
      * READS THE CONDENSED FILE INTO THE MLT_MNLY_VND LAYOUT           03320004
      ******************************************************************03330000
       4000-READ-CONDENSED-FILE.                                        03340000
           READ CONDENSED-FILE INTO ML156-RECORD                        03350000
              AT END                                                    03360000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03370000
     *                                                                  03380000
           IF PS-EOF-CONDENSED-FILE                                     03390000
               MOVE ML156-MTH-PARTN-NBR       TO CR-PARTN-NBR           03400000
               MOVE ML156-DEPT-NBR            TO CR-DEPT-NBR            03410000
               MOVE ML156-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03420000
               MOVE ML156-SUB-CL-NBR          TO CR-SUB-CL-NBR          03430000
               MOVE ML156-LOC-NBR             TO CR-LOC-NBR             03440000
W28582         MOVE ML156-ML-LWST-ID          TO PV-ML-LWST-ID          03450000
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          03460000
W28582***      MOVE ML156-ENT-ID              TO PV-ENT-ID              03470000
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              03480000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03490000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03500000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03510000
                                             TCKRSTINF-WORK-STRG-DESC   03520000
               EXEC SQL                                                 03530000
                 UPDATE TCKRSTINF                                       03540000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03550000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03560000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03570000
               END-EXEC                                                 03580000
               IF SQLCODE = 0                                           03590000
                   CONTINUE                                             03600000
               ELSE                                                     03610000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03620000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03630000
                   PERFORM 9999-SQL-ABEND                               03640000
               END-IF                                                   03650000
            ELSE                                                        03660000
                ADD 1                        TO PA-INPUT-COUNT          03670000
            END-IF.                                                     03680000
      *                                                                 03690000
      ******************************************************************03700000
      * SELECT A ROW FROM THE MONTHLY DTL TABLE THAT MATCHES INPUT KEY  03710000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03720000
      ******************************************************************03730000
       7000-SELECT-DB-ROW.                                              03740000
      *                                                                 03750000
           EXEC SQL                                                     03760000
               SELECT SLS_REG_RTL_AMT                                   03770000
                     ,SLS_PROMO_RTL_AMT                                 03780000
                     ,SLS_PROCLR_RTL_AMT                                03790000
                     ,SLS_CLR_RTL_AMT                                   03800000
                     ,SLS_OTH_RTL_AMT                                   03810000
                     ,MKDN_PLU_RTL_AMT                                  03820000
                     ,MKDN_PROMO_RTL_AMT                                03830000
                     ,MKDN_RGST_RTL_AMT                                 03840000
                     ,MKDN_GLDST_RTL_AMT                                03850000
                     ,MKDN_UMTCH_RTL_AMT                                03860000
                     ,MKDN_BYR_RTL_AMT                                  03870000
                     ,MKDN_STR_RTL_AMT                                  03880000
                     ,MKDN_PRPT_RTL_AMT                                 03890000
                     ,MKDN_OTH_RTL_AMT                                  03900000
                     ,MKDN_RTL_AMT                                      03910000
                     ,MKUP_RTL_AMT                                      03920000
                     ,XFER_IN_RTL_AMT                                   03930000
                     ,XFER_OUT_RTL_AMT                                  03940000
                     ,RCPT_RTL_AMT                                      03950000
                     ,RCPT_NET_COST_AMT                                 03960000
                     ,RCPT_GRO_COST_AMT                                 03970000
                     ,PADJ_RTL_AMT                                      03980000
                     ,PADJ_FRGT_COST_AMT                                03990000
                     ,PADJ_DISC_COST_AMT                                04000000
                     ,PADJ_RTV_COST_AMT                                 04010000
                     ,PADJ_RTV_RTL_AMT                                  04020000
                     ,PADJ_DA_COST_AMT                                  04030000
                     ,PADJ_DA_RTL_AMT                                   04040000
                     ,PADJ_NET_COST_AMT                                 04050000
                     ,PADJ_OTH_COST_AMT                                 04060000
                     ,PADJ_OTH_RTL_AMT                                  04070000
                     ,DR_ALW_RTL_AMT                                    04080000
                     ,RTV_RTL_AMT                                       04090000
                     ,MKUP_INIT_AMT                                     04100000
                     ,MKUP_NET_AMT                                      04110000
                     ,INV_ADJ_RTL_AMT                                   04120000
                 INTO :MLT00018-SLS-REG-RTL-AMT                         04130004
                     ,:MLT00018-SLS-PROMO-RTL-AMT                       04140004
                     ,:MLT00018-SLS-PROCLR-RTL-AMT                      04150004
                     ,:MLT00018-SLS-CLR-RTL-AMT                         04160004
                     ,:MLT00018-SLS-OTH-RTL-AMT                         04170004
                     ,:MLT00018-MKDN-PLU-RTL-AMT                        04180004
                     ,:MLT00018-MKDN-PROMO-RTL-AMT                      04190004
                     ,:MLT00018-MKDN-RGST-RTL-AMT                       04200004
                     ,:MLT00018-MKDN-GLDST-RTL-AMT                      04210004
                     ,:MLT00018-MKDN-UMTCH-RTL-AMT                      04220004
                     ,:MLT00018-MKDN-BYR-RTL-AMT                        04230004
                     ,:MLT00018-MKDN-STR-RTL-AMT                        04240004
                     ,:MLT00018-MKDN-PRPT-RTL-AMT                       04250004
                     ,:MLT00018-MKDN-OTH-RTL-AMT                        04260004
                     ,:MLT00018-MKDN-RTL-AMT                            04270004
                     ,:MLT00018-MKUP-RTL-AMT                            04280004
                     ,:MLT00018-XFER-IN-RTL-AMT                         04290004
                     ,:MLT00018-XFER-OUT-RTL-AMT                        04300004
                     ,:MLT00018-RCPT-RTL-AMT                            04310004
                     ,:MLT00018-RCPT-NET-COST-AMT                       04320004
                     ,:MLT00018-RCPT-GRO-COST-AMT                       04330004
                     ,:MLT00018-PADJ-RTL-AMT                            04340004
                     ,:MLT00018-PADJ-FRGT-COST-AMT                      04350004
                     ,:MLT00018-PADJ-DISC-COST-AMT                      04360004
                     ,:MLT00018-PADJ-RTV-COST-AMT                       04370004
                     ,:MLT00018-PADJ-RTV-RTL-AMT                        04380004
                     ,:MLT00018-PADJ-DA-COST-AMT                        04390004
                     ,:MLT00018-PADJ-DA-RTL-AMT                         04400004
                     ,:MLT00018-PADJ-NET-COST-AMT                       04410004
                     ,:MLT00018-PADJ-OTH-COST-AMT                       04420004
                     ,:MLT00018-PADJ-OTH-RTL-AMT                        04430004
                     ,:MLT00018-DR-ALW-RTL-AMT                          04440004
                     ,:MLT00018-RTV-RTL-AMT                             04450004
                     ,:MLT00018-MKUP-INIT-AMT                           04460004
                     ,:MLT00018-MKUP-NET-AMT                            04470004
                     ,:MLT00018-INV-ADJ-RTL-AMT                         04480004
               FROM MLT_MNLY_VND                                        04490004
              WHERE   PARTN_NBR           = :ML156-MTH-PARTN-NBR        04500000
                AND   DEPT_NBR            = :ML156-DEPT-NBR             04510000
                AND   MAJ_CL_NBR          = :ML156-MAJ-CL-NBR           04520000
                AND   SUB_CL_NBR          = :ML156-SUB-CL-NBR           04530000
                AND   LOC_NBR             = :ML156-LOC-NBR              04540000
W28582          AND   ML_LO_MDSE_LVL_ID   = :ML156-ML-LWST-ID           04550000
W28582***       AND   ENT_ID              = :ML156-ENT-ID               04560000
           END-EXEC.                                                    04570000
      *                                                                 04580000
           EVALUATE SQLCODE                                             04590000
               WHEN 0                                                   04600000
                   SET PI-UPDATE-MLT00018-ROW     TO TRUE               04610004
               WHEN +100                                                04620000
                   SET PI-INSERT-MLT00018-ROW     TO TRUE               04630004
               WHEN -911                                                04640000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            04650000
                   ADD +1             TO PV-DEADLOCK-COUNT              04660000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04670000
                       MOVE '7000-SELECT-DB-ROW' TO                     04680000
                                                    PV-PARAGRAPH-NAME   04690000
                       PERFORM 9000-DEADLOCK-ERROR                      04700000
                   ELSE                                                 04710000
                       PERFORM 7999-RESTART-PROCESS                     04720000
                   END-IF                                               04730000
               WHEN OTHER                                               04740000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     04750000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             04760000
                   PERFORM 9999-SQL-ABEND                               04770000
           END-EVALUATE.                                                04780000
      *                                                                 04790000
      *                                                                 04800000
      ***************************************************************** 04810000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON PARTITION NBR,   04820000
      * DEPT, MAJ CL, SUB CL, LOC NBR, AND ENT ID.  CURRENT TABLE       04830000
      * VALUES UPDATED TO INCLUDE VALUES FROM INPUT REC MLRD154         04840000
      ***************************************************************** 04850000
       7100-UPDATE-MLT00018-ROW.                                        04860004
      *                                                                 04870000
           PERFORM 2100-CALCULATE-UPDATES.                              04880000
      *                                                                 04890000
           EXEC SQL                                                     04900000
               UPDATE MLT_MNLY_VND                                      04910004
                   SET SLS_REG_RTL_AMT     = :SV-SLS-REG-RTL-AMT        04920000
                     , SLS_PROMO_RTL_AMT   = :SV-SLS-PROMO-RTL-AMT      04930000
                     , SLS_PROCLR_RTL_AMT  = :SV-SLS-PROCLR-RTL-AMT     04940000
                     , SLS_CLR_RTL_AMT     = :SV-SLS-CLR-RTL-AMT        04941000
                     , SLS_OTH_RTL_AMT     = :SV-SLS-OTH-RTL-AMT        04942000
                     , MKDN_PLU_RTL_AMT    = :SV-MKDN-PLU-RTL-AMT       04943000
                     , MKDN_PROMO_RTL_AMT  = :SV-MKDN-PROMO-RTL-AMT     04944000
                     , MKDN_RGST_RTL_AMT   = :SV-MKDN-RGST-RTL-AMT      04945000
                     , MKDN_GLDST_RTL_AMT  = :SV-MKDN-GLDST-RTL-AMT     04946000
                     , MKDN_UMTCH_RTL_AMT  = :SV-MKDN-UMTCH-RTL-AMT     04947000
                     , MKDN_BYR_RTL_AMT    = :SV-MKDN-BYR-RTL-AMT       04948000
                     , MKDN_STR_RTL_AMT    = :SV-MKDN-STR-RTL-AMT       04949000
                     , MKDN_PRPT_RTL_AMT   = :SV-MKDN-PRPT-RTL-AMT      04949100
                     , MKDN_OTH_RTL_AMT    = :SV-MKDN-OTH-RTL-AMT       04949200
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04949300
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04949400
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04949500
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL-AMT       04949600
                     , RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04949700
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST-AMT      04949800
                     , RCPT_GRO_COST_AMT   = :SV-RCPT-GRO-COST-AMT      04949900
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04950000
                     , PADJ_FRGT_COST_AMT  = :SV-PADJ-FRGT-COST-AMT     04950100
                     , PADJ_DISC_COST_AMT  = :SV-PADJ-DISC-COST-AMT     04950200
                     , PADJ_RTV_COST_AMT   = :SV-PADJ-RTV-COST-AMT      04950300
                     , PADJ_RTV_RTL_AMT    = :SV-PADJ-RTV-RTL-AMT       04950400
                     , PADJ_DA_COST_AMT    = :SV-PADJ-DA-COST-AMT       04950500
                     , PADJ_DA_RTL_AMT     = :SV-PADJ-DA-RTL-AMT        04950600
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST-AMT      04950700
                     , PADJ_OTH_COST_AMT   = :SV-PADJ-OTH-COST-AMT      04950800
                     , PADJ_OTH_RTL_AMT    = :SV-PADJ-OTH-RTL-AMT       04950900
                     , DR_ALW_RTL_AMT      = :SV-DR-ALW-RTL-AMT         04951000
                     , RTV_RTL_AMT         = :SV-RTV-RTL-AMT            04951100
                     , MKUP_INIT_AMT       = :SV-MKUP-INIT-AMT          04951200
                     , MKUP_NET_AMT        = :SV-MKUP-NET-AMT           04951300
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04951400
              WHERE   PARTN_NBR            = :ML156-MTH-PARTN-NBR       04951500
                AND   DEPT_NBR             = :ML156-DEPT-NBR            04951600
                AND   MAJ_CL_NBR           = :ML156-MAJ-CL-NBR          04951700
                AND   SUB_CL_NBR           = :ML156-SUB-CL-NBR          04951800
                AND   LOC_NBR              = :ML156-LOC-NBR             04951900
W28582          AND   ML_LO_MDSE_LVL_ID    = :ML156-ML-LWST-ID          04952000
W28582***       AND   ENT_ID               = :ML156-ENT-ID              04953000
           END-EXEC                                                     04954000
      *                                                                 04955000
           EVALUATE SQLCODE                                             04956000
               WHEN 0                                                   04957000
                   CONTINUE                                             04958000
               WHEN -911                                                04959000
                   ADD +1             TO PV-DEADLOCK-COUNT              04960000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04970000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04980000
                       MOVE '7100-UPDATE-MLT00018-ROW' TO               04990004
                                                    PV-PARAGRAPH-NAME   05000000
                       PERFORM 9000-DEADLOCK-ERROR                      05010000
                   ELSE                                                 05020000
                       PERFORM 7999-RESTART-PROCESS                     05030000
                   END-IF                                               05040000
               WHEN OTHER                                               05050000
                   MOVE '7100-UPDATE-MLT00018-ROW' TO PV-PARAGRAPH-NAME 05060004
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05070000
                   PERFORM 9999-SQL-ABEND                               05080000
           END-EVALUATE.                                                05090000
      *                                                                 05100000
           ADD 1 TO PA-UPDATE-COUNT.                                    05110000
      *                                                                 05120000
      ***************************************************************** 05130000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DTL TABLE. INITIAL   * 05140000
      * ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW.             * 05150000
      * CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC* 05160000
      ***************************************************************** 05170000
       7200-INSERT-MLT00018-ROW.                                        05180004
      *                                                                 05190000
           COMPUTE PV-MKUP-INIT-AMT =                                   05200000
                   ML156-RCPT-RTL-AMT - ML156-RCPT-NET-COST-AMT.        05210000
      *            ML156-RCPT-RTL-AMT - ML156-RCPT-GRO-COST-AMT.        05220000
                                                                        05230000
      *    COMPUTE PV-MKUP-NET-AMT =                                    05240000
      *           (ML156-RCPT-RTL-AMT -                                 05250000
      *           (ML156-RCPT-GRO-COST-AMT +                            05260000
      *            ML156-PADJ-FRGT-COST-AMT) -                          05270000
      *            ML156-PADJ-DISC-COST-AMT).                           05280000
      *                                                                 05290000
           COMPUTE PV-NET-RETAIL =                                      05300000
                  (ML156-RCPT-RTL-AMT +                                 05310000
                   ML156-PADJ-RTL-AMT +                                 05320000
                   ML156-MKUP-RTL-AMT +                                 05330000
                  (ML156-XFER-IN-RTL-AMT -                              05340000
                   ML156-XFER-OUT-RTL-AMT))                             05350000
           END-COMPUTE.                                                 05360000
           COMPUTE PV-NET-COST = ML156-RCPT-NET-COST-AMT +              05370000
                                 ML156-PADJ-NET-COST-AMT                05380000
           END-COMPUTE.                                                 05390000
                                                                        05400000
           COMPUTE PV-MKUP-NET-AMT = PV-NET-RETAIL - PV-NET-COST        05410000
           END-COMPUTE.                                                 05420000
           EXEC SQL                                                     05430000
               INSERT INTO MLT_MNLY_VND                                 05440004
                    (  PARTN_NBR                                        05450000
                     , DEPT_NBR                                         05460000
                     , MAJ_CL_NBR                                       05470000
                     , SUB_CL_NBR                                       05480000
                     , LOC_NBR                                          05490000
W28582               , ML_LO_MDSE_LVL_ID                                05500000
W28582***            , ENT_ID                                           05510000
                     , FSCL_MN_END_DTE                                  05520000
                     , SLS_REG_RTL_AMT                                  05530000
                     , SLS_PROMO_RTL_AMT                                05540000
                     , SLS_PROCLR_RTL_AMT                               05550000
                     , SLS_CLR_RTL_AMT                                  05560000
                     , SLS_OTH_RTL_AMT                                  05570000
                     , MKDN_PLU_RTL_AMT                                 05580000
                     , MKDN_PROMO_RTL_AMT                               05590000
                     , MKDN_RGST_RTL_AMT                                05600000
                     , MKDN_GLDST_RTL_AMT                               05610000
                     , MKDN_UMTCH_RTL_AMT                               05620000
                     , MKDN_BYR_RTL_AMT                                 05630000
                     , MKDN_STR_RTL_AMT                                 05640000
                     , MKDN_PRPT_RTL_AMT                                05650000
                     , MKDN_OTH_RTL_AMT                                 05660000
                     , MKDN_RTL_AMT                                     05670000
                     , MKUP_RTL_AMT                                     05680000
                     , XFER_IN_RTL_AMT                                  05690000
                     , XFER_OUT_RTL_AMT                                 05700000
                     , RCPT_RTL_AMT                                     05710000
                     , RCPT_NET_COST_AMT                                05720000
                     , RCPT_GRO_COST_AMT                                05730000
                     , PADJ_RTL_AMT                                     05740000
                     , PADJ_FRGT_COST_AMT                               05750000
                     , PADJ_DISC_COST_AMT                               05760000
                     , PADJ_RTV_COST_AMT                                05770000
                     , PADJ_RTV_RTL_AMT                                 05780000
                     , PADJ_DA_COST_AMT                                 05790000
                     , PADJ_DA_RTL_AMT                                  05800000
                     , PADJ_NET_COST_AMT                                05810000
                     , PADJ_OTH_COST_AMT                                05811000
                     , PADJ_OTH_RTL_AMT                                 05812000
                     , DR_ALW_RTL_AMT                                   05813000
                     , RTV_RTL_AMT                                      05814000
                     , MKUP_INIT_AMT                                    05815000
                     , MKUP_NET_AMT                                     05816000
                     , INV_ADJ_RTL_AMT                                  05817000
                     , ONORD_RTL_AMT                                    05818000
                     , ONORD_COST_AMT                                   05819000
                     , SHNK_RTL_AMT                                     05820000
                     , END_INV_RTL_AMT                                  05830000
R28582               , END_INV_COST_AMT                                 05840000
R28582               , INV_COST_ADJ1_AMT                                05850000
R28582               , INV_COST_ADJ2_AMT                                05860000
R28582               , END_INV_COST1_AMT                                05870000
R28582               , END_INV_COST2_AMT                                05880000
R28582               , ADJ_GRO_MRGN_AMT   )                             05890000
               VALUES                                                   05900000
                   (   :ML156-MTH-PARTN-NBR                             05910000
                     , :ML156-DEPT-NBR                                  05920000
                     , :ML156-MAJ-CL-NBR                                05930000
                     , :ML156-SUB-CL-NBR                                05940000
                     , :ML156-LOC-NBR                                   05950000
W28582               , :ML156-ML-LWST-ID                                05960000
W28582***            , :ML156-ENT-ID                                    05970000
                     , :ML156-FSCL-MN-END-DTE                           05980000
                     , :ML156-SLS-REG-RTL-AMT                           05990000
                     , :ML156-SLS-PROMO-RTL-AMT                         06000000
                     , :ML156-SLS-PROCLR-RTL-AMT                        06010000
                     , :ML156-SLS-CLR-RTL-AMT                           06020000
                     , :ML156-SLS-OTH-RTL-AMT                           06030000
                     , :ML156-MKDN-PLU-RTL-AMT                          06040000
                     , :ML156-MKDN-PROMO-RTL-AMT                        06050000
                     , :ML156-MKDN-RGST-RTL-AMT                         06060000
                     , :ML156-MKDN-GLDST-RTL-AMT                        06061000
                     , :ML156-MKDN-UMTCH-RTL-AMT                        06062000
                     , :ML156-MKDN-BYR-RTL-AMT                          06063000
                     , :ML156-MKDN-STR-RTL-AMT                          06064000
                     , :ML156-MKDN-PRPT-RTL-AMT                         06065000
                     , :ML156-MKDN-OTH-RTL-AMT                          06066000
                     , :ML156-MKDN-RTL-AMT                              06067000
                     , :ML156-MKUP-RTL-AMT                              06068000
                     , :ML156-XFER-IN-RTL-AMT                           06069000
                     , :ML156-XFER-OUT-RTL-AMT                          06070000
                     , :ML156-RCPT-RTL-AMT                              06080000
                     , :ML156-RCPT-NET-COST-AMT                         06090000
                     , :ML156-RCPT-GRO-COST-AMT                         06100000
                     , :ML156-PADJ-RTL-AMT                              06110000
                     , :ML156-PADJ-FRGT-COST-AMT                        06120000
                     , :ML156-PADJ-DISC-COST-AMT                        06130000
                     , :ML156-PADJ-RTV-COST-AMT                         06140000
                     , :ML156-PADJ-RTV-RTL-AMT                          06150000
                     , :ML156-PADJ-DA-COST-AMT                          06160000
                     , :ML156-PADJ-DA-RTL-AMT                           06170000
                     , :ML156-PADJ-NET-COST-AMT                         06171000
                     , :ML156-PADJ-OTH-COST-AMT                         06172000
                     , :ML156-PADJ-OTH-RTL-AMT                          06173000
                     , :ML156-DR-ALW-RTL-AMT                            06174000
                     , :ML156-RTV-RTL-AMT                               06175000
                     , :PV-MKUP-INIT-AMT                                06176000
                     , :PV-MKUP-NET-AMT                                 06177000
                     , :ML156-INV-ADJ-RTL-AMT                           06178000
                     , 0                                                06179000
                     , 0                                                06180000
                     , 0                                                06190000
                     , 0                                                06200000
R28582               , 0                                                06210000
R28582               , 0                                                06220000
R28582               , 0                                                06230000
R28582               , 0                                                06240000
R28582               , 0                                                06250000
R28582               , 0                       )                        06260000
           END-EXEC                                                     06270000
      *                                                                 06280000
           EVALUATE SQLCODE                                             06290000
               WHEN 0                                                   06300000
                   CONTINUE                                             06310000
               WHEN -911                                                06320000
                   ADD +1             TO PV-DEADLOCK-COUNT              06330000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06340000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06350000
                       MOVE '7200-INSERT-MLT00018-ROW' TO               06360004
                                                    PV-PARAGRAPH-NAME   06370000
                       PERFORM 9000-DEADLOCK-ERROR                      06380000
                   ELSE                                                 06390000
                       PERFORM 7999-RESTART-PROCESS                     06400000
                   END-IF                                               06410000
               WHEN OTHER                                               06420000
                   MOVE '7200-INSERT-MLT00018-ROW' TO PV-PARAGRAPH-NAME 06430004
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED06440000
                   PERFORM 9999-SQL-ABEND                               06450000
           END-EVALUATE.                                                06460000
                                                                        06470000
           ADD 1 TO PA-INSERT-COUNT.                                    06480000
                                                                        06490000
      ******************************************************************06500000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *06510000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *06520000
      *            CONTROL TABLE                                       *06530000
      ******************************************************************06540000
       7300-GET-COMMIT-TIMESTAMP.                                       06550000
                                                                        06560000
           EXEC SQL                                                     06570000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        06580000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       06590000
               INTO :PV-COMMIT-TIMESTAMP                                06600000
               FROM TCKRSTCNTL                                          06610000
              WHERE JOB_NAME  = :PC-JOB-NAME                            06620000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        06630000
           END-EXEC.                                                    06640000
                                                                        06650000
           EVALUATE TRUE                                                06660000
               WHEN SQLCODE = 0                                         06670000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  06680000
                   CONTINUE                                             06690000
               WHEN SQLCODE NOT EQUAL ZERO                              06700000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME06710000
                   PERFORM 9999-SQL-ABEND                               06720000
           END-EVALUATE.                                                06730000
      *                                                                 06740000
      *                                                                 06750000
      ******************************************************************06760000
      * 7400-INIT-CHECKPOINT-SELECT                                    *06770000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *06780000
      *            IF WE ARE IN A RESTART CONDITION.                   *06790000
      ******************************************************************06800000
       7400-INIT-CHECKPOINT-SELECT.                                     06810000
                                                                        06820000
           EXEC SQL                                                     06830000
             SELECT  CKPT_STAT_CODE                                     06840000
                    ,CKPT_FRQNCY_QTY                                    06850000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         06860000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        06870000
               FROM  TCKRSTCNTL                                         06880000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06890000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06900000
           END-EXEC.                                                    06910000
                                                                        06920000
           IF SQLCODE = 0                                               06930000
               CONTINUE                                                 06940000
           ELSE                                                         06950000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  06960000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     06970000
               PERFORM 9999-SQL-ABEND                                   06980000
           END-IF.                                                      06990000
      *                                                                 07000000
      *                                                                 07010000
      ******************************************************************07020000
      * 7500-SELECT-RESTART-INFO                                       *07030000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07040000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07050000
      ******************************************************************07060000
       7500-SELECT-RESTART-INFO.                                        07070000
                                                                        07080000
           EXEC SQL                                                     07090000
             SELECT  WORK_STRG_DESC                                     07100000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07110000
               FROM  TCKRSTINF                                          07120000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07130000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07140000
           END-EXEC.                                                    07150000
                                                                        07160000
           IF SQLCODE = 0                                               07170000
               CONTINUE                                                 07180000
           ELSE                                                         07190000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07200000
               PERFORM 9999-SQL-ABEND                                   07210000
           END-IF.                                                      07220000
                                                                        07230000
      ******************************************************************07240000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07250000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07260000
      ******************************************************************07270000
       7600-SET-CURRENT-TIMESTAMP.                                      07280000
                                                                        07290000
           EXEC SQL                                                     07300000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07310000
           END-EXEC.                                                    07320000
                                                                        07330000
           IF SQLCODE = 0                                               07340000
               CONTINUE                                                 07350000
           ELSE                                                         07360000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07370000
               PERFORM 9999-SQL-ABEND                                   07380000
           END-IF.                                                      07390000
      *                                                                 07400000
      *                                                                 07410000
      ******************************************************************07420000
      * 7700-COMMIT-PROCESSING.                                        *07430000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *07440000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*07450000
      ******************************************************************07460000
       7700-COMMIT-PROCESSING.                                          07470000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     07480000
                                                                        07490000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          07500000
                                                                        07510000
      * PREPARE COMMIT RECORD FOR UPDATE                                07520000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                07530000
               MOVE ML156-MTH-PARTN-NBR       TO CR-PARTN-NBR           07540000
               MOVE ML156-DEPT-NBR            TO CR-DEPT-NBR            07550000
               MOVE ML156-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          07560000
               MOVE ML156-SUB-CL-NBR          TO CR-SUB-CL-NBR          07570000
               MOVE ML156-LOC-NBR             TO CR-LOC-NBR             07580000
W28582         MOVE ML156-ML-LWST-ID          TO PV-ML-LWST-ID          07590000
W28582         MOVE PV-ML-LWST-ID             TO CR-ML-LWST-ID          07600000
W28582***      MOVE ML156-ENT-ID              TO PV-ENT-ID              07610000
W28582***      MOVE PV-ENT-ID                 TO CR-ENT-ID              07620000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    07630000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  07640000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       07650000
                                             TCKRSTINF-WORK-STRG-DESC   07660000
               IF PS-FIRST-COMMIT                                       07670000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                07680000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                07690000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       07700000
               END-IF                                                   07710000
               PERFORM 7800-COMMIT-CHANGES                              07720000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        07730000
           END-IF.                                                      07740000
      *                                                                 07750000
      *                                                                 07760000
      ******************************************************************07770000
      * 7800-COMMIT-CHANGES.                                            07780000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      07790000
      *            TAKE A COMMIT.                                       07800000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    07810000
      ******************************************************************07820000
       7800-COMMIT-CHANGES.                                             07830000
                                                                        07840000
           EXEC SQL                                                     07850000
             UPDATE TCKRSTINF                                           07860000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          07870000
              WHERE JOB_NAME       = :PC-JOB-NAME                       07880000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   07890000
           END-EXEC.                                                    07900000
                                                                        07910000
           IF SQLCODE = 0                                               07920000
               CONTINUE                                                 07930000
           ELSE                                                         07940000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED07950000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     07960000
               PERFORM 9999-SQL-ABEND                                   07970000
           END-IF.                                                      07980000
                                                                        07990000
           EXEC SQL                                                     08000000
               COMMIT                                                   08010000
           END-EXEC.                                                    08020000
                                                                        08030000
           IF SQLCODE = 0                                               08040000
               ADD 1 TO PA-COMMIT-COUNT                                 08050000
           ELSE                                                         08060000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08070000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08080000
               PERFORM 9999-SQL-ABEND                                   08090000
           END-IF.                                                      08100000
      *                                                                 08110000
      ******************************************************************08120000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08130000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08140000
      ******************************************************************08150000
       7900-UPDATE-CHECKPOINT-STATUS.                                   08160000
                                                                        08170000
           EXEC SQL                                                     08180000
             UPDATE  TCKRSTCNTL                                         08190000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08200000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08210000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08220000
           END-EXEC.                                                    08230000
                                                                        08240000
           IF SQLCODE = 0                                               08250000
               CONTINUE                                                 08260000
           ELSE                                                         08270000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08280000
               PERFORM 9999-SQL-ABEND                                   08290000
           END-IF.                                                      08300000
                                                                        08310000
           EJECT                                                        08320000
      *                                                                 08330000
      *                                                                 08340000
      ******************************************************************08350000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST MLT_MNLY_VND ROW FOR  08360004
      * UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST  08370000
      * COMMIT.                                                         08380000
      ******************************************************************08390000
       7999-RESTART-PROCESS.                                            08400000
      *                                                                 08410000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08420000
      *                                                                 08430000
           CLOSE CONDENSED-FILE.                                        08440000
      *                                                                 08450000
           PERFORM 7500-SELECT-RESTART-INFO.                            08460000
      *                                                                 08470000
           DISPLAY '**** RESTART **** '.                                08480000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 08490000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   08500000
                                                                        08510000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      08520000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        08530000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       08540000
           IF PS-FIRST-COMMIT                                           08550000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               08560000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     08570000
                      ' BEGINNING'                                      08580000
           ELSE                                                         08590000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           08600000
                        TCKRSTINF-WORK-STRG-DESC (1:35)                 08610000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 08620000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         08630000
                    CR-CHECKPOINT-RESTART-AREA                          08640000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                08650000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   08660000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 08670000
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                 08680000
               DISPLAY 'LOCATION NBR    ' CR-LOC-NBR                    08690000
W28582         DISPLAY 'ML-LWST-ID      ' CR-ML-LWST-ID                 08700000
W28582***      DISPLAY 'ENT-ID          ' CR-ENT-ID                     08701000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           08702000
           END-IF.                                                      08703000
                                                                        08704000
           OPEN INPUT CONDENSED-FILE.                                   08705000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      08706000
      *                                                                 08707000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          08708000
           PERFORM 4000-READ-CONDENSED-FILE                             08709000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              08710000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  08720000
                    PA-INPUT-COUNT                                      08730000
           DISPLAY 'PARTITION NBR         ' ML156-MTH-PARTN-NBR.        08740000
           DISPLAY 'DEPARTMENT NBR        ' ML156-DEPT-NBR.             08750000
           DISPLAY 'MAJOR CLASS NBR       ' ML156-MAJ-CL-NBR.           08760000
           DISPLAY 'SUB CLASS NBR         ' ML156-SUB-CL-NBR.           08770000
           DISPLAY 'LOCATION NBR          ' ML156-LOC-NBR.              08780000
W28582     DISPLAY 'ML-LWST-ID            ' ML156-ML-LWST-ID.           08790000
W28582     MOVE ML156-ML-LWST-ID TO PV-ML-LWST-ID.                      08800000
W28582     DISPLAY 'ML-LWST-ID            ' PV-ML-LWST-ID.              08810000
W28582***  DISPLAY 'ENT-ID                ' ML156-ENT-ID.               08820000
W28582***  MOVE ML156-ENT-ID TO PV-ENT-ID.                              08830000
W28582***  DISPLAY 'ENT-ID                ' PV-ENT-ID.                  08840000
      ******************************************************************08850000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *08860000
      ******************************************************************08870000
       8000-EOJ-ROUTINE.                                                08880000
      *                                                                 08890000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       08900000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       08910000
                                                                        08920000
      *                                                                 08930000
           DISPLAY SPACES.                                              08940000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         08950000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        08960000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        08970000
           DISPLAY SPACES.                                              08980000
      *                                                                 08990000
           CLOSE CONDENSED-FILE.                                        09000000
      *                                                                 09010000
      *                                                                 09020000
      ******************************************************************09030000
      *  PERFORMED BY 7100-UPDATE AND 7200-INSERT                       09040000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09050000
      ******************************************************************09060000
       9000-DEADLOCK-ERROR.                                             09070000
      *                                                                 09080000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09090000
               PERFORM 9999-SQL-ABEND.                                  09100000
      *                                                                 09110000
      ******************************************************************09120000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09130000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09140000
      ******************************************************************09150000
       9999-SQL-ABEND.                                                  09160000
                                                                        09170000
           MOVE +4013                 TO ABEND-CODE.                    09180000
           DISPLAY '**  ABEND     **'.                                  09190000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09200000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09210000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09220000
                                                                        09230000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09240000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09250000
                                                                        09260000
           COPY DPPD004.                                                09270000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09280000
