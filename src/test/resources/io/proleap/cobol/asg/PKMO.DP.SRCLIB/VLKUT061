000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             VLKUT061.                                        
000600 AUTHOR.                 MIKE FRIESS - TKMA508.                           
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            SEPTEMBER 2010.                                  
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*                                                               *         
001300*                 AUDIT FAILURE - MARKER FILE                   *         
001400*---------------------------------------------------------------*         
001500*  THIS PROGRAM WILL PRODUCE THE AUDIT FAILURE MARKER FILE.     *         
001600*---------------------------------------------------------------*         
001700*****************************************************************         
001800 ENVIRONMENT DIVISION.                                                    
001900*****************************************************************         
002000                                                                          
002100 CONFIGURATION SECTION.                                                   
002200                                                                          
002300 SOURCE-COMPUTER.        IBM-3090.                                        
002400 OBJECT-COMPUTER.        IBM-3090.                                        
002500                                                                          
002600 INPUT-OUTPUT SECTION.                                                    
002700                                                                          
002800 FILE-CONTROL.                                                            
002900                                                                          
003000     SELECT AUD-EXTRACT-DATA-FILE                                         
003100         ASSIGN TO INP01.                                                 
003200                                                                          
003300     SELECT AUD-EXTRACT-MARKER-FILE                                       
003400         ASSIGN TO OUT01.                                                 
003500                                                                          
003600******************************************************************        
003700 DATA DIVISION.                                                           
003800******************************************************************        
003900                                                                          
004000 FILE SECTION.                                                            
004100                                                                          
004200 FD  AUD-EXTRACT-DATA-FILE                                                
004300     RECORDING MODE IS F                                                  
004400     LABEL RECORDS ARE STANDARD                                           
004500     BLOCK CONTAINS 0 RECORDS.                                            
004600 01  AUD-EXTRACT-RECORD               PIC  X(206).                        
004700                                                                          
004800 FD  AUD-EXTRACT-MARKER-FILE                                              
004900     RECORDING MODE IS F                                                  
005000     BLOCK CONTAINS 0  RECORDS.                                           
005100 01  AUD-MARKER-RECORD                PIC  X(78).                         
005200                                                                          
005300 WORKING-STORAGE SECTION.                                                 
005400                                                                          
005500 01  FILLER                      PIC  X(46)    VALUE                      
005600     '** WORKING STORAGE FOR VLKUT061 STARTS HERE **'.                    
005700                                                                          
005800 01  PROGRAM-SWITCHES.                                                    
005900     05  END-OF-FILE-SW              PIC  X(01)    VALUE 'Y'.             
006000         88  END-OF-FILE                           VALUE 'N'.             
006100                                                                          
006200 01  PC-CONSTANTS.                                                        
006300     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
006400                                                   'VLKUT061'.            
006500 01  PV-ACCUMULATOR-FIELDS.                                               
006600     05  PV-MARKER-CNT               PIC  9(13)    VALUE ZERO.            
006700***** FILE NAME CONSISTS OF RUN-DATE-AUDITMARK.MKR                        
006800*****     EXAMPLE: 20010911_AUDITMARK.MKR                                 
006900     05  PV-FILE-NAME.                                                    
007000         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
007100         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
007200         10  PV-AUDTLOAD-MKR          PIC  X(19) VALUE SPACES.            
007300                                                                          
007400                                                                          
007500 01  PV-ABEND-FIELDS.                                                     
007600     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO             
007700                                                   COMP SYNC.             
007800     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
007900     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
008000     05  PV-CURRENT-TMST-DB2         PIC X(26)     VALUE SPACE.           
008100     05  PV-CURRENT-TMST.                                                 
008200         10  PV-CURR-DATE.                                                
008300             15  PV-CURR-DATE-CC     PIC  9(02)    VALUE ZERO.            
008400             15  PV-CURR-DATE-YY     PIC  9(02)    VALUE ZERO.            
008500             15  PV-CURR-DASH1       PIC  X        VALUE SPACE.           
008600             15  PV-CURR-DATE-MM     PIC  9(02)    VALUE ZERO.            
008700             15  PV-CURR-DASH2       PIC  X        VALUE SPACE.           
008800             15  PV-CURR-DATE-DD     PIC  9(02)    VALUE ZERO.            
008900             15  PV-CURR-DASH3       PIC  X        VALUE SPACE.           
009000         10  PV-CURR-TIME.                                                
009100             15  PV-CURR-TIME-HH     PIC  9(02)    VALUE ZERO.            
009200             15  PV-CURR-TIME-P1     PIC  X        VALUE SPACE.           
009300             15  PV-CURR-TIME-MM     PIC  9(02)    VALUE ZERO.            
009400             15  PV-CURR-TIME-P2     PIC  X        VALUE SPACE.           
009500             15  PV-CURR-TIME-SS     PIC  9(02)    VALUE ZERO.            
009600             15  FILL                PIC  X(07)    VALUE SPACE.           
009700                                                                          
009800     05  PV-CURR-CCYY.                                                    
009900         10  PV-CURR-CC              PIC  X(02)    VALUE SPACE.           
010000         10  PV-CURR-YY              PIC  X(02)    VALUE SPACE.           
010100                                                                          
010200     05  PV-MARKER-DATE.                                                  
010300         10  PV-MARKER-CCYY          PIC  X(04)    VALUE SPACE.           
010400         10  PV-MARKER-MM            PIC  X(02)    VALUE SPACE.           
010500         10  PV-MARKER-DD            PIC  X(02)    VALUE SPACE.           
010600                                                                          
010700     05  PV-MARKER-TIME.                                                  
010800         10  PV-MARKER-HH            PIC  X(02)    VALUE SPACE.           
010900         10  PV-MARKER-MN            PIC  X(02)    VALUE SPACE.           
011000         10  PV-MARKER-SS            PIC  X(02)    VALUE SPACE.           
011100                                                                          
011200     05  PV-ABEND-PROGRAM             PIC X(8)   VALUE 'VLKUT061'.        
011300     05  PV-ABEND-PARAGRAPH           PIC X(50)  VALUE SPACES.            
011400     05  PV-ABEND-OPERATION           PIC X(50)  VALUE SPACES.            
011500     05  PV-ABEND-STRUCTURE-1         PIC X(8)   VALUE SPACES.            
011600     05  PV-ABEND-STRUCTURE-2         PIC X(8)   VALUE SPACES.            
011700     05  PV-ABEND-STRUCTURE-3         PIC X(8)   VALUE SPACES.            
011800     05  PV-ABEND-STATUS              PIC XX     VALUE SPACES.            
011900     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
012000     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
012100     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
012200     05  PV-DB2-OPERATION-MESSAGE-4   PIC  X(79) VALUE SPACES.            
012300     05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
012400     05  PV-ERROR-MESSAGE-1           PIC  X(60) VALUE SPACES.            
012500     05  PV-ERROR-MESSAGE-2           PIC  X(60) VALUE SPACES.            
012600     05  PV-SQLCODE                   PIC  9(9)  VALUE ZERO.              
012700 EJECT                                                                    
012800******************************************************************        
012900*  COPYBOOKS.                                                             
013000******************************************************************        
013100*                                                                         
013200*----------------------------------------------------------------*        
013300*    AUDIT FAILURE MARKER FILE LAYOUT                                     
013400*----------------------------------------------------------------*        
013500     COPY VLRD069.                                                        
013600*----------------------------------------------------------------*        
013700*    AUDIT FAILURE INPUT FILE                                             
013800*----------------------------------------------------------------*        
013900     COPY VLRD068.                                                        
014000*----------------------------------------------------------------*        
014100                                                                          
014200*****************************************************************         
014300*  *** DB2 MESSAGE AREAS ****                                   *         
014400*****************************************************************         
014500     COPY DPWS004.                                                        
014600                                                                          
014700*---------------------------------------------------------------*         
014800*    DB2 AREA FOR COMMUNICATIONS                                *         
014900*---------------------------------------------------------------*         
015000     EXEC SQL                                                             
015100          INCLUDE SQLCA                                                   
015200     END-EXEC.                                                            
015300                                                                          
015400*****************************************************************         
015500                                                                          
015600 LINKAGE SECTION.                                                         
015700                                                                          
015800*****************************************************************         
015900 PROCEDURE DIVISION.                                                      
016000*****************************************************************         
016100                                                                          
016200 0000-MAINLINE-PROCESSING.                                                
016300                                                                          
016400     PERFORM 1000-INITIALIZATION.                                         
016500     PERFORM 2000-PROCESS-EXTRACT-RECS                                    
016600       UNTIL END-OF-FILE.                                                 
016610                                                                          
016700     PERFORM 3000-BUILD-AUD-MARKER-FILE.                                  
016800                                                                          
016900     CLOSE AUD-EXTRACT-DATA-FILE                                          
017000           AUD-EXTRACT-MARKER-FILE.                                       
017100     STOP RUN.                                                            
017200                                                                          
017300 1000-INITIALIZATION.                                                     
017400******************************************************************        
017500*  OPEN FILES AND READ THE FIRST INPUT RECORD                    *        
017600******************************************************************        
017700                                                                          
017800     OPEN INPUT  AUD-EXTRACT-DATA-FILE                                    
017900          OUTPUT AUD-EXTRACT-MARKER-FILE.                                 
018000                                                                          
018100** GET CURRENT TIMESTAMP                                                  
018200                                                                          
018300     EXEC SQL                                                             
018400         SET :PV-CURRENT-TMST-DB2 = CURRENT TIMESTAMP                     
018500     END-EXEC.                                                            
018600                                                                          
018700     EVALUATE TRUE                                                        
018800         WHEN SQLCODE = ZERO                                              
018900             MOVE PV-CURRENT-TMST-DB2    TO PV-CURRENT-TMST               
019000             MOVE PV-CURR-DATE-CC        TO PV-CURR-CC                    
019100             MOVE PV-CURR-DATE-YY        TO PV-CURR-YY                    
019200             MOVE PV-CURR-CCYY           TO PV-MARKER-CCYY                
019300             MOVE PV-CURR-DATE-MM        TO PV-MARKER-MM                  
019400             MOVE PV-CURR-DATE-DD        TO PV-MARKER-DD                  
019500             MOVE PV-CURR-TIME-HH        TO PV-MARKER-HH                  
019600             MOVE PV-CURR-TIME-MM        TO PV-MARKER-MN                  
019700             MOVE PV-CURR-TIME-SS        TO PV-MARKER-SS                  
019800                                                                          
019900         WHEN OTHER                                                       
020000             MOVE '1000-INITIALIZATION'  TO PV-ABEND-PARAGRAPH            
020100             MOVE 'SET CURRENT TIMESTAMP'                                 
020200                                         TO PV-ABEND-OPERATION            
020300             PERFORM 9900-DB2-ABEND                                       
020400         END-EVALUATE.                                                    
020500                                                                          
020600     PERFORM 7000-READ-INPUT.                                             
020700                                                                          
020800                                                                          
020900 2000-PROCESS-EXTRACT-RECS.                                               
021000******************************************************************        
021100*  PROCESS THE INPUT FILE TO CREATE THE OUTPUT FILES AND REPORT  *        
021200******************************************************************        
021300                                                                          
021400     ADD 1 TO PV-MARKER-CNT.                                              
021500                                                                          
021600     PERFORM 7000-READ-INPUT.                                             
021700                                                                          
021800                                                                          
021900 3000-BUILD-AUD-MARKER-FILE.                                              
022000******************************************************************        
022100*  BUILD AUDIT FAILURE MARKER RECORD.                            *        
022200******************************************************************        
022300                                                                          
022400     INITIALIZE VL069-MARKER-RECORD.                                      
022500     MOVE PV-MARKER-DATE      TO PV-CREATE-DATE.                          
022600     MOVE 'AUDITDATA.DAT'     TO PV-AUDTLOAD-MKR.                         
022700     MOVE PV-FILE-NAME        TO VL069-FILE-NAME.                         
022800     MOVE PV-MARKER-CNT       TO VL069-REC-CNT.                           
022900     MOVE PV-MARKER-DATE      TO VL069-BUILD-DATE.                        
023000     MOVE PV-MARKER-TIME      TO VL069-BUILD-TIME.                        
023100     MOVE 'X'                 TO VL069-ANCHOR-CONSTANT.                   
023200                                                                          
023300     WRITE AUD-MARKER-RECORD                                              
023400                  FROM VL069-MARKER-RECORD.                               
023500                                                                          
023600                                                                          
023700 7000-READ-INPUT.                                                         
023800******************************************************************        
023900*  READ A AUDIT FAILURE EXTRACT RECORD.                          *        
024000******************************************************************        
024100                                                                          
024200     READ AUD-EXTRACT-DATA-FILE                                           
024300         AT END                                                           
024400           SET END-OF-FILE TO TRUE                                        
024500     END-READ.                                                            
024600                                                                          
024700******************************************************************        
024800*  DB2 ABEND ROUTINE                                             *        
024900******************************************************************        
025000 9900-DB2-ABEND.                                                          
025100                                                                          
025200     MOVE SQLCODE TO PV-SQLCODE.                                          
025300     DISPLAY '*** DB2 ERROR '.                                            
025400     DISPLAY '*** SQLCODE   = ' PV-SQLCODE.                               
025500     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
025600     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
025700     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
025800     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
025900     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-2.               
026000     DISPLAY '***           = ' PV-DB2-OPERATION-MESSAGE-3.               
026100     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
026200                                                                          
026300     COPY DPPD004.                                                        
026400                                                                          
026500     MOVE +4013                  TO PV-ABEND-CODE.                        
026600     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
026700*                                                                         
