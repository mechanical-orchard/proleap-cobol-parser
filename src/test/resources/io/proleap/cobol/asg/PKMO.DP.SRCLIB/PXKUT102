       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    PXKUT102.                                         00020000
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  07/26/2010.                                       00050000
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      * THIS PROGRAM WILL CONVERT THE CONSOLIDATION SKUS CSV FILE       00080000
      * INTO A MAINFRAME FRIENDLY FORMAT USED IN SUBSEQUENT REPORTING.  00090000
      ******************************************************************00110000
      * DATE: 07/26/2010 | MADE BY: THOMAS HANSON | PROJECT: WR37816    00120000
      * DESCRIPTION OF CHANGES: INITIAL PROGRAM IMPLEMENTATION.         00121000
      ******************************************************************00130000
       ENVIRONMENT DIVISION.                                            00140000
       CONFIGURATION SECTION.                                           00150000
       SOURCE-COMPUTER.    IBM-3090.                                    00160000
       OBJECT-COMPUTER.    IBM-3090.                                    00170000
       INPUT-OUTPUT SECTION.                                            00180000
       FILE-CONTROL.                                                    00190000
                                                                        00200000
           SELECT CONSOLIDATION-SKUS-CSV-FILE                           00210000
                  ASSIGN TO INP01.                                      00220000
           SELECT MF-CONVERTED-OUTPUT-FILE                              00270000
                  ASSIGN TO OUT01.                                      00280000
                                                                        00290000
       DATA DIVISION.                                                   00300000
       FILE SECTION.                                                    00310000
                                                                        00320000
       FD  CONSOLIDATION-SKUS-CSV-FILE                                  00330000
           RECORDING MODE IS F                                          00340002
           LABEL RECORDS OMITTED                                        00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           DATA RECORD IS CONSOLIDATION-SKUS-CSV-RECORD.                00370000
       01  CONSOLIDATION-SKUS-CSV-RECORD       PIC X(365).              00380002
                                                                        00390000
       FD  MF-CONVERTED-OUTPUT-FILE                                     00513000
           RECORDING MODE IS F                                          00514000
           LABEL RECORDS OMITTED                                        00515000
           BLOCK CONTAINS 0 RECORDS                                     00516000
           DATA RECORD IS MF-CONVERTED-OUTPUT-RECORD.                   00517000
       01  MF-CONVERTED-OUTPUT-RECORD          PIC X(365).              00518003
                                                                        00519000
       WORKING-STORAGE SECTION.                                         00520000
      ******************************************************************00530000
      **PROGRAM SWITCHES                                                00540000
      ******************************************************************00550000
       01  PS-SWITCH-AREAS.                                             00560000
           05  PS-END-OF-FILE-SW               PIC X(01)  VALUE 'N'.    00570003
               88  PS-END-OF-FILE                         VALUE 'Y'.    00580003
               88  PS-NOT-END-OF-FILE                     VALUE 'N'.    00590003
      ******************************************************************00631008
      **SYSTEM DATE AND TIME VARIABLES                                  00632008
      ******************************************************************00633008
       01  SYS-DATE-TIME.                                               00634008
           05  SYS-DATE                        PIC X(08) VALUE SPACES.  00635017
           05  SYS-TIME                        PIC X(08) VALUE SPACES.  00636017
                                                                        00637008
       01  ST-BEG-TIME.                                                 00638008
           05  ST-BEG-HR                       PIC 9(02) VALUE ZEROS.   00639017
           05  FILLER                          PIC X(01) VALUE ':'.     00639117
           05  ST-BEG-MN                       PIC 9(02) VALUE ZEROS.   00639217
                                                                        00639308
       01  ST-END-TIME.                                                 00639408
           05  ST-END-HR                       PIC 9(02) VALUE ZEROS.   00639517
           05  FILLER                          PIC X(01) VALUE ':'.     00639617
           05  ST-END-MN                       PIC 9(02) VALUE ZEROS.   00639717
                                                                        00639808
       01  SD-EDIT-DATE.                                                00639908
           05  SD-MONTH                        PIC 9(02) VALUE ZEROS.   00640017
           05  FILLER                          PIC X(01) VALUE '-'.     00640117
           05  SD-DAY                          PIC 9(02) VALUE ZEROS.   00640217
           05  FILLER                          PIC X(01) VALUE '-'.     00640317
           05  SD-CENT                         PIC 9(02) VALUE ZEROS.   00640417
           05  SD-YEAR                         PIC 9(02) VALUE ZEROS.   00640517
      ******************************************************************00690000
      **PROGRAM ACCUMULATORS (COUNTERS)                                 00700000
      ******************************************************************00710000
       01  PA-PROGRAM-ACCUMULATORS.                                     00720000
           05  PA-READ                         PIC 9(11) VALUE ZEROS.   00730003
           05  PA-WRITE                        PIC 9(11) VALUE ZEROS.   00740003
      ******************************************************************00780200
      **PROGRAM VARIABLES                                               00780300
      ******************************************************************00780400
       01  PV-PROGRAM-VARIABLES.                                        00780500
           05  PV-CSV-INPUT-RECORD             PIC X(365) VALUE SPACES. 00780612
           05  PV-TEMP-RTL                     PIC X(10)  VALUE SPACES. 00780715
           05  PV-DECIMAL-IND                  PIC 9      VALUE ZERO.   00780813
      ******************************************************************00783109
      **PROGRAM CONSTANTS                                               00783209
      ******************************************************************00783309
       01  PC-PROGRAM-CONSTANTS.                                        00783409
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'PXKUT102'.00784009
      ******************************************************************00789703
      **ABEND CODE INFORMATION                                          00789804
      ******************************************************************00789903
       01  AC-ABEND-FIELDS.                                             00790000
           05  AC-ABEND-CODE                   PIC S9(04) VALUE ZERO    00800000
                                                           COMP SYNC.   00810000
               88  AC-EMPTY-FILE-ERROR                   VALUE +4005.   00820004
       01  AC-MISC-ABEND-FIELDS.                                        00840004
           05  AC-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00850004
           05  AC-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00860004
           05  AC-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00870004
      ******************************************************************00890000
      **OUTPUT COPYBOOK                                                 00900003
      ******************************************************************00910000
           COPY PXRD102.                                                00940003
                                                                        00980000
       PROCEDURE DIVISION.                                              00990000
      ******************************************************************01000000
       0000-MAINLINE.                                                   01010000
                                                                        01020000
           PERFORM 1000-INITIALIZATION.                                 01030000
                                                                        01031000
           PERFORM 2000-PROCESS-CSV                                     01032012
                   UNTIL PS-END-OF-FILE.                                01033003
                                                                        01039000
           PERFORM 9100-WRAP-UP.                                        01040018
           GOBACK.                                                      01050000
                                                                        01060000
      ******************************************************************01070000
      **INITIALIZATION ROUTINE                                          01080000
      ******************************************************************01090000
       1000-INITIALIZATION.                                             01100000
                                                                        01110000
           OPEN OUTPUT MF-CONVERTED-OUTPUT-FILE.                        01111004
           OPEN INPUT CONSOLIDATION-SKUS-CSV-FILE.                      01120000
                                                                        01160000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01161008
           MOVE SYS-TIME (1:2) TO ST-BEG-HR.                            01163008
           MOVE SYS-TIME (3:2) TO ST-BEG-MN.                            01164008
           MOVE SYS-DATE (1:2) TO SD-CENT.                              01166008
           MOVE SYS-DATE (3:2) TO SD-YEAR.                              01167008
           MOVE SYS-DATE (5:2) TO SD-MONTH.                             01168008
           MOVE SYS-DATE (7:2) TO SD-DAY.                               01169008
                                                                        01190000
      *--> DO TWO FIRST READS FROM THE INPUT FILE                       01200004
      *--> THE FIRST WILL REMOVE THE LINE OF FIELD HEADERS              01201004
      *--> THE SECOND WILL BE THE FIRST ACTUAL DATA CONTENT LINE        01202004
           PERFORM 5000-READ-FROM-INPUT-CSV-FILE.                       01210004
           IF PS-NOT-END-OF-FILE                                        01211004
              PERFORM 5000-READ-FROM-INPUT-CSV-FILE                     01220004
           ELSE                                                         01221004
              SET AC-EMPTY-FILE-ERROR     TO TRUE                       01222004
              MOVE '1000-INITIALIZATION'  TO AC-PARAGRAPH-NAME          01222104
              MOVE 'EMPTY CSV INPUT FILE' TO AC-OPERATION-MSG-1         01222204
              MOVE 'RE-PULL FILE & RERUN' TO AC-OPERATION-MSG-2         01222304
              PERFORM 9000-NON-SQL-ABEND                                01223004
           END-IF.                                                      01230004
                                                                        01247000
      ******************************************************************01248000
      **MAIN PROCESSING OF EACH LINE                                    01249017
      ******************************************************************01250000
       2000-PROCESS-CSV.                                                01260012
                                                                        01261009
           UNSTRING PV-CSV-INPUT-RECORD                                 01270112
           DELIMITED BY ","                                             01270212
              INTO PXRD102-SKU-NBR,                                     01270312
                   PXRD102-SKU-DESC,                                    01270412
                   PXRD102-NRF-COLOR-DESC,                              01270512
                   PXRD102-VS-STYLE-NBR,                                01270612
                   PXRD102-STYLE-ID-NBR,                                01270712
                   PXRD102-VS-STYLE-DESC,                               01270812
                   PXRD102-LABEL-NAME,                                  01270912
                   PXRD102-SUB-CL-NBR,                                  01271012
                   PXRD102-SUB-CL-DESC,                                 01271112
                   PXRD102-MAJ-CL-NBR,                                  01271212
                   PXRD102-MAJ-CL-DESC,                                 01271312
                   PXRD102-DEPT-NBR,                                    01271412
                   PXRD102-DEPT-DESC,                                   01271512
                   PXRD102-DMA-NBR,                                     01271612
                   PXRD102-DMA-DESC,                                    01271712
                   PV-TEMP-RTL,                                         01271815
                   PXRD102-FLOOR-PAD-CDE,                               01272012
                   PXRD102-FLOOR-PAD-DESC,                              01272112
                   PXRD102-CONSOLIDATE-EFF-DTE,                         01272212
                   PXRD102-ACTUAL-EVNT-EFF-DTE,                         01272312
                   PXRD102-QTY-AS-GIVEN-MIO,                            01272412
                   PXRD102-BUS-GRP-NBR,                                 01272512
                   PXRD102-BUS-GRP-DESC                                 01272612
           END-UNSTRING.                                                01272712
                                                                        01272815
      *    CONVERT EDITED PIC(X) FROM CSV TO DECIMAL PIC 999V99 FIELD   01273415
           COMPUTE                                                      01273515
              PXRD102-CORP-UNIT-RTL-AMT = FUNCTION NUMVAL(PV-TEMP-RTL). 01273615
                                                                        01273715
           PERFORM 4000-WRITE-TO-OUTPUT-MF-FILE.                        01276012
           PERFORM 5000-READ-FROM-INPUT-CSV-FILE.                       01280004
                                                                        01767000
      ******************************************************************01782604
      **WRITE OUT TO FILE...                                            01782717
      ******************************************************************01782804
       4000-WRITE-TO-OUTPUT-MF-FILE.                                    01782904
                                                                        01783000
           WRITE MF-CONVERTED-OUTPUT-RECORD                             01784004
                 FROM PXRD102-CNSLDTN-SKUS.                             01784116
           ADD +1 TO PA-WRITE.                                          01785004
                                                                        01785103
      ******************************************************************01785204
      **READ IN A NEW CSV RECORD...                                     01785317
      ******************************************************************01785404
       5000-READ-FROM-INPUT-CSV-FILE.                                   01785504
                                                                        01785604
           INITIALIZE PXRD102-CNSLDTN-SKUS.                             01785716
                                                                        01785904
           READ CONSOLIDATION-SKUS-CSV-FILE                             01786104
              INTO PV-CSV-INPUT-RECORD                                  01786212
                 AT END                                                 01786304
                 SET PS-END-OF-FILE TO TRUE                             01786404
           END-READ.                                                    01786504
                                                                        01786604
           IF  PS-NOT-END-OF-FILE                                       01786704
               ADD +1                       TO PA-READ                  01786804
           END-IF.                                                      01786904
                                                                        01787004
      ******************************************************************01787103
      **PERFORMS AN ABEND WHEN A NON-SQL BASED ERROR TAKES PLACE DURING 01787317
      **PROGRAM EXECUTION. LIKELY A CONTROL CARD OR DATE ROUTINE ISSUE. 01787417
      ******************************************************************01787503
       9000-NON-SQL-ABEND.                                              01787603
                                                                        01787703
           PERFORM 9100-WRAP-UP.                                        01787818
                                                                        01787903
           DISPLAY '** ABEND           **'.                             01788003
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          01788106
           DISPLAY '** PARAGRAPH       ** = ' AC-PARAGRAPH-NAME.        01788204
           DISPLAY '** OPERATION       ** = ' AC-OPERATION-MSG-1.       01788304
           DISPLAY '**                 ** = ' AC-OPERATION-MSG-2.       01788404
                                                                        01788503
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         01788604
                                                                        01788700
      ******************************************************************01788800
      **WRAP UP PROCESSING                                              01788903
      ******************************************************************01789000
       9100-WRAP-UP.                                                    01790018
                                                                        01800000
           CLOSE MF-CONVERTED-OUTPUT-FILE.                              01801004
           CLOSE CONSOLIDATION-SKUS-CSV-FILE.                           01810004
                                                                        01850000
           PERFORM 9999-DISPLAY-STATISTICS.                             01860000
                                                                        01870000
      ******************************************************************01880000
      **DISPLAY PROGRAM STATS                                           01890000
      ******************************************************************01900000
       9999-DISPLAY-STATISTICS.                                         01910000
                                                                        01920000
           MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                 01921008
           MOVE SYS-TIME (1:2) TO ST-END-HR.                            01923008
           MOVE SYS-TIME (3:2) TO ST-END-MN.                            01924008
                                                                        01925006
           DISPLAY '*************************************************'. 01930000
           DISPLAY '* PROGRAM   = ' PC-PROGRAM-NAME.                    01940007
           DISPLAY '* PURPOSE   = CONVERT CSV TO FLAT FILE.'.           01950007
           DISPLAY '* RUN DATE  = ' SD-EDIT-DATE.                       01951008
           DISPLAY '* BEG TIME  = ' ST-BEG-TIME.                        01952008
           DISPLAY '* END TIME  = ' ST-END-TIME.                        01953008
           DISPLAY '*************************************************'. 01970000
           DISPLAY '* INPUT FILE RECS READ: ' PA-READ.                  01980003
           DISPLAY '* OUTFILE RECS WRITTEN: ' PA-WRITE.                 01990003
           DISPLAY '*************************************************'. 02000000
                                                                        02100000
      ******************************************************************02110000
