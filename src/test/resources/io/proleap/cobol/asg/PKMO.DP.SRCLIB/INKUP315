       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUP315.                                        
       AUTHOR.                 BOB MCASEY.                                      
       INSTALLATION.           KOHLS.                                           
       DATE-WRITTEN.           MARCH 2000.                                      
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                                         
      *    THIS PROGRAM IS A COMBINATION OF INKUT168 AND INKUT119.              
      *    (THESE 2 PROGRAMS HAVE BEEN OBSOLETED)                               
      *    IT UPDATES TINVPAR AND CREATES ADJUSTMENT RECORDS THAT WILL          
      *    BE USED TO BOOK THE FINAL INVENTORY.                                 
      *                                                                         
      * WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
      * ------- ---------- ------------ --------------------------              
      * 04/06/2000          CHANGED BY:  B MCASEY WR:20839                      
      *  MODIFIED INVENTORY ADJUSTMENT CALCULATION TO INCLUDE THE               
      *  ESTIMATED INVENTORY ADJUSTMENT FROM A PRIOR 'FAKE' INVENTORY.          
      *                                                                         
W26600* W26600  04/01/2004 M.JOHNSON    STORE EXPANSION                         
W20445* W20445  09-01-2004 ROD JANSSEN  DETAILED INVNENTORY ADJUSTMENT          
W28582* W28582  12-23-2005 CLARK SCHWENN VENDOR BRAND CHANGES                   
W28582* W28582                           ADDED PARA 3000-LOOKUP-ENT-ID          
W28545* W28545  08-10-2006 ADD LOGIC FOR DEPARTMENT LEVEL                       
W28545*                    INVENTORY PROCESS.                                   
W28545*                    -B. GRAHAM - STRATAGEM                               
J28545* J28545  08-18-2006 ADD LOGIC TO DE-ACTIVATE INVENTORY CYCLES.           
J28545*                    -J. SELWITSCHKA - STRATAGEM                          
RONNAM* RONNAM  02-07-2008 IF THE DKEY IS ZERO, FIND THE DKEY FOR               
RONNAM*                    UNKNOWN BRAND UNKNOWN VENDOR AND DEPT OF             
RONNAM*                    THE RECORD.                                          
W33304* W33304  12-12-2008 HILO CHANGES. REMOVE CYCLE DEACTIVATION FILE         
W33304*                    AND LOGIC.                                           
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SHORT-EXTR-FILE            ASSIGN TO INP01.                   
W33304     SELECT CC-OVERRIDE-FILE           ASSIGN TO INP02.                   
           SELECT SHORTAGE-ADJUSTMENT-FILE   ASSIGN TO OUT01.                   
W33304*    SELECT CYCLE-DE-ACT-FILE          ASSIGN TO OUT02.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SHORT-EXTR-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 170 CHARACTERS                                       
           DATA RECORD IS SHORT-REC.                                            
W33304 01  SHORT-REC                   PIC X(170).                              
                                                                                
W33304 FD  CC-OVERRIDE-FILE                                                     
W33304     RECORDING MODE IS F                                                  
W33304     LABEL RECORDS ARE STANDARD                                           
W33304     BLOCK CONTAINS 0 RECORDS                                             
W33304     RECORD CONTAINS 80 CHARACTERS                                        
W33304     DATA RECORD IS CC-OVERRIDE-RECORD.                                   
W33304 01  CC-OVERRIDE-RECORD                   PIC X(80).                      
                                                                                
       FD  SHORTAGE-ADJUSTMENT-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS ADJUST-REC.                                           
       01  ADJUST-REC                  PIC X(80).                               
                                                                                
W33304*FD  CYCLE-DE-ACT-FILE                                                    
W33304*    RECORDING MODE IS F                                                  
W33304*    LABEL RECORDS ARE STANDARD                                           
W33304*    BLOCK CONTAINS 0 RECORDS.                                            
W33304*01  CYCLE-DE-ACT-REC            PIC X(80).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.         
           05  PV-DB2-OPERATION-ATTEMPTED      PIC X(30)   VALUE SPACE.         
W26600     05  PV-TOTAL-RTL-ADJ-NUM            PIC S9(11)V99                    
W26600                                                     VALUE ZERO.          
W26600     05  PV-TOTAL-RTL-ADJ-DIS            PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
                                                            VALUE ZERO.         
W26600     05  PV-TOTAL-EST-RTL-ADJ-NUM        PIC S9(11)V99                    
W26600                                                      VALUE ZERO.         
W26600     05  PV-TOTAL-EST-RTL-ADJ-DIS        PIC ZZZ,ZZZ,ZZZ,ZZ9.99-          
0400RM                                                      VALUE ZERO.         
           05  PV-RETRY-COUNT                  PIC S9(4)                        
                                               COMP SYNC   VALUE +0.            
W26600     05  PV-STR-NBR                      PIC 9(05).                       
           05  FILLER REDEFINES PV-STR-NBR.                                     
W26600*        10  FILLER                      PIC X(01).                       
W26600         10  PV-STR-NBR-X                PIC X(05).                       
           05  PV-SQLCODE                      PIC S9(04) VALUE ZERO.           
                                                                                
           05  PV-OPN-FSCL-MTH-END-DTE.                                         
               10  PV-OPN-FSCL-FSCL-YYCC.                                       
                   15 PV-OPN-FSCL-FSCL-CC      PIC X(02)   VALUE SPACE.         
                   15 PV-OPN-FSCL-FSCL-YY      PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-MM         PIC X(02)   VALUE SPACE.         
               10  FILLER                      PIC X(01)   VALUE '-'.           
               10  PV-OPN-FSCL-FSCL-DD         PIC X(02)   VALUE SPACE.         
                                                                                
           05  PV-CURRENT-DATE.                                                 
               10 PV-CURRENT-DATE-CCYY   PIC X(04)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-MM    PIC X(02)   VALUE SPACE.               
               10  FILLER                PIC X(01)   VALUE '-'.                 
               10  PV-CURRENT-DATE-DD    PIC X(02)   VALUE SPACE.               
                                                                                
           05  PV-HOLD-STORE-NBR         PIC X(05)  VALUE SPACE.        INKUT119
           05  PV-BOOK-ADJ-FSCL-PER      PIC X(02).                     INKUT119
           05  PV-DB2-FIN-BOOK-DATE      PIC X(10).                     INKUT119
W33304     05  PV-HOLD-RUN-DATE          PIC X(10)  VALUE SPACES.       INKUT119
           05  PV-SQL-SUB                PIC S9(4)  COMP.               INKUT119
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES            PIC S9(4)  COMP VALUE +3.      INKUT119
           05  PC-9S-DATE                PIC X(10)  VALUE '9999-09-09'. INKUT119
           05  PC-INKUP315               PIC X(08)  VALUE 'INKUP315'.   INKUT119
           05  PC-5161                   PIC 9(09)  VALUE 5161.         INKUT119
           05  PC-2230                   PIC 9(05)  VALUE 2230.         INKUT119
                                                                                
       01  ABEND-CODE                    PIC S9(04)   VALUE +0.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EXTR-EOF-SW            PIC X(01)     VALUE 'N'.               
               88  PS-SHORT-EXTR-EOF                   VALUE 'Y'.               
W33304     05  PS-FINCL-INV-STAT-CDE     PIC X(02)     VALUE SPACES.            
W33304         88  PS-LOCATION-PROCESS                 VALUE 'BK'.              
W33304     05  PS-CC-OVERRIDE-EOF-SW     PIC X(01)     VALUE 'N'.               
W33304         88  PS-CC-OVERRIDE-EOF                  VALUE 'Y'.               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-READ                    PIC 9(09) VALUE ZERO.            
           05  PA-RECS-BYPASSED                PIC 9(09) VALUE ZERO.            
           05  PA-RECS-WRITTEN                 PIC 9(09) VALUE ZERO.            
           05  PA-ROWS-UPDATED                 PIC 9(09) VALUE ZERO.            
                                                                                
W33304 01  CC-CONTROL-CARD.                                                     
W33304    05  CC-CONTROL-DATE.                                                  
W33304        10  CC-CONTROL-CCYY              PIC X(04)  VALUE SPACES.         
W33304        10  CC-CONTROL-HYPHEN1           PIC X(01)  VALUE SPACES.         
W33304        10  CC-CONTROL-MM                PIC X(02)  VALUE SPACES.         
W33304        10  CC-CONTROL-HYPHEN2           PIC X(01)  VALUE SPACES.         
W33304        10  CC-CONTROL-DD                PIC X(02)  VALUE SPACES.         
W33304    05  FILLER                           PIC X(70)  VALUE SPACES.         
      ******************************************************************        
      *    INPUT - INVENTORY SHORTAGE EXTRACT RECORD                            
      ******************************************************************        
W20445     COPY INRD014.                                                        
                                                                                
      ******************************************************************        
      *    OUTPUT - MERCH LEDGER ADJUSTMENT RECORD                              
      ******************************************************************        
W20445     COPY MLRD018.                                                        
                                                                                
      *****************************************************************         
      * DB2 DEPT BRAND VENDOR RELATINSHIP TABLE  -                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE IMT00017                                                 
           END-EXEC.                                                            
      ******************************************************************        
      * DA BRAND PRORATION PERCENT TABLE FOR MARKDOWN ALLOWANCES                
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE IMT00020                                                   
           END-EXEC.                                                            
      ******************************************************************        
      * ML UNKNOWN VENDOR/BRAND TABLE                                           
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE MLT00011                                                   
           END-EXEC.                                                            
      ******************************************************************        
      * ML BRAND/VENDOR/DEPT RELATIONSHIP VIEW                                  
      ******************************************************************        
           EXEC SQL                                                             
             INCLUDE MLV00000                                                   
           END-EXEC.                                                            
      ***************************************************************           
      *  ERROR MESSAGE AREA FOR DB2                                             
      ***************************************************************           
           COPY DPWS004.                                                        
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           COPY SQLCA2.                                                         
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 M/L OPEN FISCAL MONTH TABLE                      
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
28545                                                                           
                                                                                
      ***************************************************************           
      *    LAYOUT USED FOR DB2 INVENTORY PARAMETERS TABLE.                      
      ***************************************************************           
           EXEC SQL                                                     INKUT119
                INCLUDE TINVPAR                                         INKUT119
           END-EXEC.                                                    INKUT119
                                                                                
      ******************************************************************        
      *  VENDOR STYLE TABLE                                                     
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TVNDSTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  TMDARVS TABLE                                                          
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDARVS                                                  
           END-EXEC.                                                            
                                                                                
      ***************************************************************           
      *    USED FOR DB2 ERRORS.                                                 
      ***************************************************************           
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 UPC/SKU TABLE                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCSKU                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TUPC                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * DB2 SKU CROSS REFERENCE TABLE                                           
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  BRAND UTILITY BVWS001                                                  
      ******************************************************************        
           COPY BVWS001.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-INKUP315.                                                           
                                                                                
           OPEN INPUT   SHORT-EXTR-FILE                                         
W33304          INPUT   CC-OVERRIDE-FILE                                        
W33304          OUTPUT  SHORTAGE-ADJUSTMENT-FILE.                               
                                                                                
           PERFORM 1000-INIT-PROCESS.                                           
                                                                                
           PERFORM 4000-READ-SHORT-EXTR.                                        
                                                                                
           IF PS-SHORT-EXTR-EOF                                                 
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2000-PROCESS-SHORTAGE-RECORDS                            
                   UNTIL PS-SHORT-EXTR-EOF.                                     
                                                                                
                                                                                
           MOVE PV-TOTAL-RTL-ADJ-NUM     TO PV-TOTAL-RTL-ADJ-DIS.               
0400RM     MOVE PV-TOTAL-EST-RTL-ADJ-NUM TO PV-TOTAL-EST-RTL-ADJ-DIS.           
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
           DISPLAY '* TOTAL RECORDS READ      : ' PA-RECS-READ.                 
           DISPLAY '* TOTAL RECORDS BYPASSED  : ' PA-RECS-BYPASSED.             
           DISPLAY '* TOTAL RECORDS WRITTEN   : ' PA-RECS-WRITTEN.              
           DISPLAY '* TINVPAR ROWS UPDATED    : ' PA-ROWS-UPDATED.              
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
0400RM     DISPLAY '* TOTAL ESTIMATED ADJUSTMENTS: '                            
0400RM                PV-TOTAL-EST-RTL-ADJ-DIS.                                 
           DISPLAY '* TOTAL RETAIL ADJUSTMENTS: ' PV-TOTAL-RTL-ADJ-DIS.         
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * '.              
                                                                                
           CLOSE SHORT-EXTR-FILE                                                
W33304           CC-OVERRIDE-FILE                                               
W33304           SHORTAGE-ADJUSTMENT-FILE.                                      
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1000-INIT-PROCESS.                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:4) TO PV-CURRENT-DATE-CCYY.            
           MOVE FUNCTION CURRENT-DATE (5:2) TO PV-CURRENT-DATE-MM.              
           MOVE FUNCTION CURRENT-DATE (7:2) TO PV-CURRENT-DATE-DD.              
           MOVE PV-CURRENT-DATE             TO PV-DB2-FIN-BOOK-DATE.            
                                                                                
           PERFORM 5000-READ-FSCL-MN-TABLE.                                     
                                                                                
           DISPLAY 'MLCNTL-OPEN-FSCL-MN-DTE: '                                  
                    MLCNTL-OPN-FSCL-MN-DTE.                                     
                                                                                
           MOVE MLCNTL-OPN-FSCL-MN-DTE TO PV-OPN-FSCL-MTH-END-DTE.              
                                                                                
W33304     PERFORM 7000-READ-CC-OVERRIDE-FILE.                                  
W33304     IF CC-CONTROL-CCYY NOT NUMERIC                                       
W33304        OR CC-CONTROL-MM NOT NUMERIC                                      
W33304        OR CC-CONTROL-DD NOT NUMERIC                                      
W33304        OR CC-CONTROL-HYPHEN1 NOT = '-'                                   
W33304        OR CC-CONTROL-HYPHEN2 NOT = '-'                                   
W33304        MOVE +4002                 TO ABEND-CODE                          
W33304        DISPLAY '**  ABEND     **'                                        
W33304        DISPLAY '**  PROGRAM   **  =  INKUP315'                           
W33304        DISPLAY '**  INVALID PROCESSING DATE '                            
W33304        CALL 'ILBOABN0'  USING ABEND-CODE                                 
W33304     ELSE                                                                 
W33304        MOVE CC-CONTROL-DATE TO PV-HOLD-RUN-DATE                          
W33304     END-IF.                                                              
      ******************************************************************        
      *  THIS PARAGRAPH WRITES ADJUSTMENT RECORDS THAT WILL BE USED             
      *  IN THE FINAL BOOKING OF THE INVENTORY.  A TRANSACTION CODE             
      *  OF "8" FLAGS THE RECORD AS AN INVENTORY ADJUSTMENT RECORD.             
      *  THE BOOKING MONTH IS THE MONTH THAT THESE ADJUSTMENTS ARE TO           
      *  BE APPLIED.                                                            
      ******************************************************************        
       2000-PROCESS-SHORTAGE-RECORDS.                                           
                                                                                
W33304     MOVE IN014-FINCL-INV-STAT-CDE                                        
W33304                                 TO PS-FINCL-INV-STAT-CDE.                
W33304     IF PS-LOCATION-PROCESS                                               
W33304        MOVE '8'                    TO ML018-ADJ-CDE                      
W33304        MOVE IN014-STR-NBR          TO PV-STR-NBR                         
W33304        MOVE PV-STR-NBR-X           TO ML018-ADJ-STORE-NBR                
W33304        MOVE IN014-DEPT-NBR         TO ML018-ADJ-DEPT-NBR                 
W33304        MOVE IN014-SUB-CL-NBR       TO ML018-ADJ-CLASS-NBR                
W33304        MOVE MLCNTL-OPN-FSCL-MN-NBR TO ML018-ADJ-KOHLS-MM                 
W33304        MOVE PV-OPN-FSCL-FSCL-YY    TO ML018-ADJ-YY                       
W33304        MOVE PV-OPN-FSCL-FSCL-MM    TO ML018-ADJ-MM                       
W33304        MOVE PV-OPN-FSCL-FSCL-DD    TO ML018-ADJ-DD                       
W33304        MOVE IN014-ML-LWST-ID       TO ML018-ADJ-ML-LWST-ID               
W33304        PERFORM  3000-LOOKUP-ENT-ID                                       
W33304        MOVE ZERO                   TO ML018-ADJ-AT-COST-AMT              
W33304        COMPUTE ML018-ADJ-AT-RTL-AMT ROUNDED =                            
W33304            (IN014-SHNK-RTL-AMT     +                                     
W33304             IN014-SHORTAGE-RTL-AMT +                                     
W33304             IN014-ESTIMATED-INV-ADJ-AMT) * -1                            
W33304                                                                          
W33304        ADD ML018-ADJ-AT-RTL-AMT        TO PV-TOTAL-RTL-ADJ-NUM           
W33304        ADD IN014-ESTIMATED-INV-ADJ-AMT                                   
W33304                                     TO PV-TOTAL-EST-RTL-ADJ-NUM          
W33304        IF ML018-ADJ-STORE-NBR = PV-HOLD-STORE-NBR                INKUT119
W33304            CONTINUE                                                      
W33304        ELSE                                                              
W33304            PERFORM 8405-UPDATE-TINVPAR                              INKUT
W33304            MOVE SPACES              TO PV-HOLD-STORE-NBR                 
W33304            MOVE ML018-ADJ-STORE-NBR TO PV-HOLD-STORE-NBR                 
W33304        END-IF                                                       INKUT
W33304                                                                          
W33304*    ONLY RECORDS WITH ADJUSTMENTS ARE WRITTEN OUT                        
W33304        IF ML018-ADJ-AT-RTL-AMT = ZERO                                    
W33304            ADD 1 TO PA-RECS-BYPASSED                                     
W33304        ELSE                                                              
W33304            WRITE ADJUST-REC FROM ML018-ADJUSTMENT-RECORD                 
W33304            ADD 1 TO PA-RECS-WRITTEN                                      
W33304        END-IF                                                       INKUT
W33304     ELSE                                                                 
W33304            ADD 1 TO PA-RECS-BYPASSED                                     
W33304     END-IF.                                                              
                                                                                
           PERFORM 4000-READ-SHORT-EXTR.                                        
                                                                                
      ******************************************************************        
      * PROCESS BRAND VENDOR LOOKUP FOR ENT ID FROM MLV_BRND_VND                
      * WITH BV0001 UTILITY SEARCING BY DEPT NR, AND DATALESS KEY.              
      ******************************************************************        
W28582 3000-LOOKUP-ENT-ID.                                                      
                                                                                
            INITIALIZE ML018-ADJ-ENT-ID.                                        
RONNAM      IF IN014-ML-LWST-ID = 0                                             
RONNAM          MOVE PC-5161 TO BV001-PV-ENT-ID-IN-9                            
RONNAM          MOVE PC-2230 TO BV001-PV-BRAND-IN-9                             
RONNAM          SET BV001-PV-BRND-VND-DPT-LOOKUP TO TRUE                        
RONNAM          MOVE IN014-DEPT-NBR   TO BV001-PV-DEPT-IN-9                     
RONNAM          PERFORM BV001-LOOKUP-MAIN                                       
RONNAM          IF BV001-PV-FOUND-REL-OR-UNK                                    
RONNAM              MOVE BV001-PV-ENT-ID-OUT  TO ML018-ADJ-ENT-ID               
RONNAM              MOVE BV001-PV-RLTNSHP-ID-OUT                                
RONNAM                                        TO ML018-ADJ-ML-LWST-ID           
RONNAM          ELSE                                                            
RONNAM              DISPLAY 'ERROR LOOKING UP UNKNOWN DBV'                      
RONNAM              DISPLAY 'ML018-ADJ-CDE........', ML018-ADJ-CDE              
RONNAM              DISPLAY 'PV-STR-NBR...........', PV-STR-NBR                 
RONNAM              DISPLAY 'ML018-ADJ-STORE-NBR..', ML018-ADJ-STORE-NBR        
RONNAM              DISPLAY 'ML018-ADJ-DEPT-NBR...', ML018-ADJ-DEPT-NBR         
RONNAM              DISPLAY 'ML018-ADJ-CLASS-NBR..', ML018-ADJ-CLASS-NBR        
RONNAM              DISPLAY 'ML018-ADJ-KOHLS-MM...', ML018-ADJ-KOHLS-MM         
RONNAM              DISPLAY 'ML018-ADJ-YY.........', ML018-ADJ-YY               
RONNAM              DISPLAY 'ML018-ADJ-MM.........', ML018-ADJ-MM               
RONNAM              DISPLAY 'ML018-ADJ-DD.........', ML018-ADJ-DD               
RONNAM             DISPLAY 'ML018-ADJ-ML-LWST-ID.', ML018-ADJ-ML-LWST-ID        
RONNAM              PERFORM 9000-BV-LOOKUP-ERROR                                
RONNAM          END-IF                                                          
RONNAM      ELSE                                                                
RONNAM                                                                          
                MOVE IN014-ML-LWST-ID                                           
                       TO BV001-PV-RLTNSHP-ID-IN-9                              
                MOVE IN014-DEPT-NBR   TO BV001-PV-DEPT-IN-9                     
                                                                                
                SET BV001-PV-REL-ID-LOOKUP TO TRUE                              
                PERFORM BV001-LOOKUP-MAIN                                       
                                                                                
                IF BV001-PV-FOUND-REL-OR-UNK                                    
                    MOVE BV001-PV-ENT-ID-OUT  TO ML018-ADJ-ENT-ID               
                ELSE                                                            
                    DISPLAY 'ERROR LOOKING UP ENT ID WITH DATALESS KEY'         
TEMP                DISPLAY 'ML018-ADJ-CDE........', ML018-ADJ-CDE              
TEMP                DISPLAY 'PV-STR-NBR...........', PV-STR-NBR                 
TEMP                DISPLAY 'ML018-ADJ-STORE-NBR..', ML018-ADJ-STORE-NBR        
TEMP                DISPLAY 'ML018-ADJ-DEPT-NBR...', ML018-ADJ-DEPT-NBR         
TEMP                DISPLAY 'ML018-ADJ-CLASS-NBR..', ML018-ADJ-CLASS-NBR        
TEMP                DISPLAY 'ML018-ADJ-KOHLS-MM...', ML018-ADJ-KOHLS-MM         
TEMP                DISPLAY 'ML018-ADJ-YY.........', ML018-ADJ-YY               
TEMP                DISPLAY 'ML018-ADJ-MM.........', ML018-ADJ-MM               
TEMP                DISPLAY 'ML018-ADJ-DD.........', ML018-ADJ-DD               
TEMP               DISPLAY 'ML018-ADJ-ML-LWST-ID.', ML018-ADJ-ML-LWST-ID        
                    PERFORM 9000-BV-LOOKUP-ERROR                                
                END-IF                                                          
RONNA       END-IF.                                                             
                                                                                
                                                                                
       4000-READ-SHORT-EXTR.                                                    
                                                                                
           READ SHORT-EXTR-FILE INTO IN014-SHORTAGE-EXTR-REC                    
               AT END                                                           
                   SET PS-SHORT-EXTR-EOF TO TRUE.                               
                                                                                
           IF PS-SHORT-EXTR-EOF                                                 
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-RECS-READ                                            
           END-IF.                                                              
                                                                                
      ***************************************************************           
      *    READ THE OPEN FISCAL MONTH FILE                                      
      ***************************************************************           
       5000-READ-FSCL-MN-TABLE.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT  OPN_FSCL_MN_DTE                                     
                         ,  OPN_FSCL_MN_NBR                                     
                    INTO   :MLCNTL-OPN-FSCL-MN-DTE                              
                         , :MLCNTL-OPN-FSCL-MN-NBR                              
                    FROM    TMLCNTL                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '5000-READ-OPEN-FSCL-MN-TABLE'                   
                                                TO PV-PARAGRAPH-NAME            
                       MOVE    'SELECT ROW FROM TMLCNTL'                        
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                       PERFORM 9950-SQL-ERROR                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
               DISPLAY '** MAX RETRIES EXCEEDED ON TMLCNTL **'                  
               MOVE '5000-READ-OPEN-FSCL-MN-TABLE' TO PV-PARAGRAPH-NAME         
               MOVE 'SELECT ROW FROM TMLCNTL'                                   
                                          TO PV-DB2-OPERATION-ATTEMPTED         
               PERFORM 9950-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
W33304******************************************************************        
W33304* 7000-READ-CC-OVERRIDE-FILE.                                             
W33304******************************************************************        
W33304 7000-READ-CC-OVERRIDE-FILE.                                              
W33304                                                                          
W33304     READ CC-OVERRIDE-FILE INTO CC-CONTROL-CARD                           
W33304         AT END                                                           
W33304            SET PS-CC-OVERRIDE-EOF TO TRUE.                               
W33304                                                                          
W33304     IF NOT PS-CC-OVERRIDE-EOF                                            
W33304         DISPLAY 'INPUT CONTROL CARD DATE: ' CC-CONTROL-CARD              
W33304     END-IF.                                                              
W33304                                                                          
      ***************************************************************           
      *    UPDATE THE INVENTORY PARAMETER DATABASE FOR EACH STORE               
W33304*    WITH FINCL_INV_STAT_CDE  =  'TC'                                     
      ***************************************************************           
28545  8405-UPDATE-TINVPAR.                                                     
28545                                                                           
           PERFORM                                                      INKUT119
               WITH TEST AFTER                                          INKUT119
               VARYING PV-SQL-SUB                                       INKUT119
               FROM 1 BY 1                                              INKUT119
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                       INKUT119
                         OR SQLCODE = 0 OR +100)                        INKUT119
                                                                        INKUT119
               EXEC SQL                                                 INKUT119
                    UPDATE TINVPAR                                      INKUT119
W33304              SET    FINCL_INV_STAT_CDE  =  'TC'                  INKUT119
                         , CHG_TMST            =   CURRENT TIMESTAMP    INKUT119
                         , CHG_ID_NBR          =  :PC-INKUP315          INKUT119
                    WHERE  ACTL_FIN_BK_DTE     =  :PC-9S-DATE           INKUT119
W33304              AND    FINCL_INV_STAT_CDE  =  'BK'                  INKUT119
W33304              AND    LOC_INV_STAT_CDE    =  'IN'                  INKUT119
W33304              AND    LOC_NBR             =  :IN014-STR-NBR        INKUT119
W33304              AND    INV_ID              =  :IN014-INV-ID                 
W33304              AND    PLND_FIN_BK_DTE    <=  :PV-HOLD-RUN-DATE             
               END-EXEC                                                 INKUT119
                                                                        INKUT119
               EVALUATE TRUE                                            INKUT119
                   WHEN SQLCODE = 0                                     INKUT119
                       ADD 1 TO PA-ROWS-UPDATED                                 
                   WHEN SQLCODE = -904                                  INKUT119
                   WHEN SQLCODE = -914                                  INKUT119
                   WHEN SQLCODE = 0                                     INKUT119
                   WHEN SQLCODE = +100                                  INKUT119
                       CONTINUE                                         INKUT119
                   WHEN SQLWARN NOT = SPACES                            INKUT119
                   WHEN SQLCODE NOT = ZERO                              INKUT119
W33304                 MOVE '8405-UPDATE-TINVPAR'                       INKUT119
                                     TO  PV-PARAGRAPH-NAME              INKUT119
                       MOVE 'UPDATE TINVPAR'                            INKUT119
                                     TO  PV-DB2-OPERATION-ATTEMPTED     INKUT119
                       PERFORM 9950-SQL-ERROR                           INKUT119
               END-EVALUATE                                             INKUT119
           END-PERFORM.                                                 INKUT119
                                                                        INKUT119
           IF  PV-SQL-SUB > PC-MAX-RETRIES                              INKUT119
W33304         MOVE '8405-UPDATE-TINVPAR'                               INKUT119
                                     TO  PV-PARAGRAPH-NAME              INKUT119
               MOVE 'UPDATE RETRIES EXCEEDED'                           INKUT119
                                     TO  PV-DB2-OPERATION-ATTEMPTED     INKUT119
               PERFORM 9950-SQL-ERROR                                   INKUT119
           END-IF.                                                      INKUT119
                                                                        INKUT119
      ******************************************************************        
      *  ABEND CALL FOR ERRORS RETURNED BY BRAND VENDOR LOOKUP UTILITY          
      ******************************************************************        
       9000-BV-LOOKUP-ERROR.                                                    
           DISPLAY 'GOT TO 9000-BV-LOOKUP-ERROR'.                               
           DISPLAY '***************************************************'        
           DISPLAY BV001-PV-RETURN-MSG-1                                        
           DISPLAY '***************************************************'        
           DISPLAY BV001-PV-RETURN-MSG-2                                        
           DISPLAY '***************************************************'        
           IF BV001-PV-SQL-ERROR                                                
              MOVE BV001-PV-SQLCODE-OUT TO PV-SQLCODE                           
              DISPLAY 'SQL CODE = ' PV-SQLCODE                                  
           END-IF.                                                              
           PERFORM 9950-SQL-ERROR.                                              
                                                                                
                                                                                
                                                                                
       9950-SQL-ERROR.                                                          
           MOVE +4013 TO ABEND-CODE.                                            
           DISPLAY '** ABEND **     ** = SQLERROR'.                             
           DISPLAY '** PROGRAM      ** = INKUP315'.                             
           DISPLAY '** PARAGRAPH    ** = ' PV-PARAGRAPH-NAME.                   
           DISPLAY '** OPERATION    ** = ' PV-DB2-OPERATION-ATTEMPTED.          
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
       EJECT                                                                    
025900*****************************************************************         
026000*                                                                         
026100*****************************************************************         
026200     COPY BVPD001.                                                        
