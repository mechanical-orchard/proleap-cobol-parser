000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    CNKCS212.                                                 
000300 AUTHOR.        DENNIS STEFFEN.                                           
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  APRIL 1998.                                               
000600 DATE-COMPILED.                                                           
000700                                                                          
000800*---------------------------------------------------------------*         
000900*    Y211 - P2L STORE ZONE LIST                                           
001000*                                                                         
001100*    THIS PROGRAM GENERATES A LIST OF PACK-TO-LIGHT STORE ZONES           
001200*    WITH THEIR ZONE ABREVIATIONS AND DESCRIPTIONS.                       
001300*    SELECT AND F17 TRANSFERS CONTROL TO CNKCS213 FOR A LIST              
001400*    OF DEPARTMENTS SERVICED BY THAT ZONE.                                
001500*                                                                         
001600*---------------------------------------------------------------*         
263455* CHANGED 11/02/2021    BY JIM HUNTER      WR: CHG0263455                 
263455* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263455* MAKING THE CALL DYNAMIC VS STATIC.                                      
263455*----------------------------------------------------------------*        
CICS  * CHANGED 10/25/2022    BY JIM HUNTER      WR: CHG0276961                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001700                                                                          
001800 ENVIRONMENT DIVISION.                                                    
001900 DATA DIVISION.                                                           
002000/                                                                         
002100 WORKING-STORAGE SECTION.                                                 
002200                                                                          
002300 01  DK-DB2-KEYS.                                                         
002400     05  DK-OPER-UNT-ID                 PIC  S9(04) COMP.                 
002500     05  DK-FCLTY-ID                    PIC  S9(04) COMP.                 
002600     05  DK-LBL-TYP-CDE                 PIC  X(02).                       
002700                                                                          
002800 01  PC-PROGRAM-CONSTANTS.                                                
002900     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
003000                                                    'CN212A  '.           
003100     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
003200                                                    'CNKM212 '.           
003300     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
003400                                                    'CNKCS212'.           
003500     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
003600     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
003700     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
003800     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12             
003900                                                    COMP SYNC.            
004000     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
004100                                                    COMP SYNC.            
004200     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
004300                                                    +1000000              
004400                                                    COMP-3.               
004500     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +7              
004600                                                    COMP SYNC.            
004700     05  PC-4                           PIC S9(04)  VALUE +4              
004800                                                    COMP SYNC.            
004900     05  PC-ADD                         PIC  X(03)  VALUE 'ADD'.          
005000     05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.          
005100     05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.          
005200     05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.          
005300     05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.          
005400                                                                          
005500     05  PC-TSYMSG-NUMBERS.                                               
005600         10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.          
005700         10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.          
005800         10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.          
005900         10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.          
006000         10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.          
006100         10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.          
006200         10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.          
006300         10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.          
006400         10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.          
006500         10  PC-TSYMSG-00059            PIC  9(05)  VALUE 00059.          
006600         10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.          
006700         10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.          
006800         10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.          
006900         10  PC-TSYMSG-00254            PIC  9(05)  VALUE 00254.          
007000         10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.          
007100         10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.          
007200         10  PC-TSYMSG-01004            PIC  9(05)  VALUE 01004.          
007210         10  PC-TSYMSG-01692            PIC  9(05)  VALUE 01692.          
007300         10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.          
007400                                                                          
007500 01  PS-PROGRAM-SWITCHES.                                                 
007600     05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.            
007700         88  PS-NO-ERROR                            VALUE 'Y'.            
007800         88  PS-ERROR                               VALUE 'N'.            
007900     05  PS-ZONE-SW                     PIC  X(01)  VALUE 'Y'.            
008000         88  PS-VALID-ZONE                          VALUE 'Y'.            
008100         88  PS-INVALID-ZONE                        VALUE 'N'.            
008200     05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.            
008300         88  PS-COMMAREA-VALID                      VALUE 'Y'.            
008400         88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.            
008500     05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.            
008600         88  PS-DONE                                VALUE 'Y'.            
008700         88  PS-NOT-DONE                            VALUE 'N'.            
008800     05  PS-NEXT-SCREEN-SW            PIC  X    VALUE 'N'.                
008900         88  PS-NO-NEXT-SCREEN                  VALUE 'N'.                
009000         88  PS-NEXT-SCREEN                     VALUE 'Y'.                
009100                                                                          
009200 01  PV-PROGRAM-VARIABLES.                                                
009300     05  PV-SUB                         PIC S9(04)  VALUE +0              
009400                                                    COMP SYNC.            
009500     05  PV-REMAINDER                   PIC S9(04)  VALUE +0              
009600                                                    COMP SYNC.            
009700     05  PV-READ-COUNT                  PIC S9(09)  VALUE +0              
009800                                                    COMP-3.               
009900     05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0              
010000                                                    COMP-3.               
010100     05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0              
010200                                                    COMP-3.               
010300     05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0              
010400                                                    COMP-3.               
010500     05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0              
010600                                                    COMP-3.               
010700     05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0              
010800                                                    COMP-3.               
010900     05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0              
011000                                                    COMP-3.               
011100     05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0              
011200                                                    COMP SYNC.            
011300     05  PV-CICS-RESP                   PIC S9(04)  VALUE +0              
011400                                                    COMP SYNC.            
011500     05  PV-ITEM                        PIC S9(04)  VALUE +0              
011600                                                    COMP SYNC.            
011700     05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0              
011800                                                    COMP SYNC.            
011900     05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0              
012000                                                    COMP SYNC.            
012100     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0              
012200                                                    COMP SYNC.            
012300     05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0              
012400                                                    COMP SYNC.            
012500     05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0              
012600                                                    COMP SYNC.            
012700     05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
012800                                                    COMP SYNC.            
012900     05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0              
013000                                                    COMP SYNC.            
013100     05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0              
013200                                                    COMP SYNC.            
013300     05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0              
013400                                                    COMP SYNC.            
013500     05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0              
013600                                                    COMP SYNC.            
013700     05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0              
013800                                                    COMP SYNC.            
013900     05  PV-SQL-SUB                     PIC S9(04)  VALUE +0              
014000                                                    COMP SYNC.            
014100     05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0              
014200                                                    COMP SYNC.            
014300     05  PV-STR-ZN-NULL-IND             PIC S9(04) VALUE ZERO             
014400                                                    COMP SYNC.            
014500     05  PV-SEARCH-ZONE                 PIC X(05)  VALUE SPACES.          
014600     05  PV-CICS-SYSTEM                 PIC X(08)  VALUE SPACES.          
014700         88  PV-TEST-CICS                     VALUE 'CICST1  '.           
014800                                                                          
014900*---------------------------------------------------------------*         
015000*   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   *         
015100*   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           *         
015200*   APPLICATION-SPECIFIC COMMAREA.                              *         
015300*---------------------------------------------------------------*         
015400 01  LQW-LIST-QUEUE-WORK-AREA.                                            
015500     05  LQW-BLOCK-MODIFIED             PIC X(01)   VALUE SPACE.          
015600     05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES                   
015700                                        INDEXED BY LQW-IDX.               
015800         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.               
015900         10  LQW-TEMP-SELECTED-SW       PIC X(01).                        
016000         10  LQW-SELECTED-SW            PIC X(01).                        
016100             88  LQW-SELECT                         VALUE 'S'.            
016200             88  LQW-DESELECT                       VALUE ' '.            
016300         10  LQW-ACTION                 PIC X(03).                        
016400         10  LQW-STORE-ZONE             PIC X(05).                        
016500         10  LQW-STORE-ZONE-DESC        PIC X(25).                        
016600         10  LQW-LBL-TYP-CDE            PIC X(02).                        
016700                                                                          
016800/                                                                         
016900*---------------------------------------------------------------*         
017000*  RECORD LAYOUT FOR THE LIST SELECTION TEMP STORAGE QUEUE.     *         
017100*---------------------------------------------------------------*         
017200     COPY CNWS007.                                                        
017300                                                                          
017400/                                                                         
017500*---------------------------------------------------------------*         
017600*    MAP LAYOUT                                                 *         
017700*---------------------------------------------------------------*         
017800                                                                          
017900     COPY CNKM212.                                                        
018000                                                                          
018100 01  MR-OCCURS-LAYOUT                   REDEFINES  CN212AO.               
018200     05  FILLER                         PIC X(12).                        
018300                                                                          
018400     05  FILLER                         PIC X(30).                        
018500                                                                          
018600     05  MR-SCROLL-AREA                 OCCURS 12 TIMES.                  
018700         10  MR-SELECTL                 PIC S9(04) COMP.                  
018800         10  MR-SELECTA                 PIC X(01).                        
018900         10  MR-SELECTC                 PIC X(01).                        
019000         10  MR-SELECTH                 PIC X(01).                        
019100         10  MR-SELECT                  PIC X(01).                        
019200                                                                          
019300         10  MR-ACTIONL                 PIC S9(04) COMP.                  
019400         10  MR-ACTIONA                 PIC X(01).                        
019500         10  MR-ACTIONC                 PIC X(01).                        
019600         10  MR-ACTIONH                 PIC X(01).                        
019700         10  MR-ACTION                  PIC X(03).                        
019800                                                                          
019900         10  MR-STORE-ZONE-L            PIC S9(04) COMP.                  
020000         10  MR-STORE-ZONE-A            PIC X(01).                        
020100         10  MR-STORE-ZONE-C            PIC X(01).                        
020200         10  MR-STORE-ZONE-H            PIC X(01).                        
020300         10  MR-STORE-ZONE              PIC X(05).                        
020400                                                                          
020500         10  MR-STORE-ZONE-DESC-L       PIC S9(04) COMP.                  
020600         10  MR-STORE-ZONE-DESC-A       PIC X(01).                        
020700         10  MR-STORE-ZONE-DESC-C       PIC X(01).                        
020800         10  MR-STORE-ZONE-DESC-H       PIC X(01).                        
020900         10  MR-STORE-ZONE-DESC         PIC X(20).                        
021000                                                                          
021100/                                                                         
021200*---------------------------------------------------------------*         
021300*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
021400*---------------------------------------------------------------*         
021500     COPY DPWS015.                                                        
021600                                                                          
021700/                                                                         
021800*---------------------------------------------------------------*         
021900*    NUMERIC EDIT LAYOUT.                                       *         
022000*---------------------------------------------------------------*         
022100     COPY DPWS010I.                                                       
022200                                                                          
022300/                                                                         
022400*---------------------------------------------------------------*         
022500*    FUNCTION KEYS COPYBOOK.                                    *         
022600*---------------------------------------------------------------*         
022700     COPY DPWS016.                                                        
022800                                                                          
022900/                                                                         
023000*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
023200*---------------------------------------------------------------*         
023300     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
023500/                                                                         
023600*---------------------------------------------------------------*         
023700*    STANDARD COMMAREA.                                         *         
023800*---------------------------------------------------------------*         
023900                                                                          
024000     COPY DPWS020.                                                        
024100     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
024200                                                                          
024300*---------------------------------------------------------------*         
024400* INTER-APPLICATION COMMAREA.                                   *         
024500*   CNWS001 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    *         
024600*             SPEC SCREEN.                                      *         
024700*   CNWS003 - IS THE OUTPUT LAYOUT THAT WILL BE PASSED FROM     *         
024800*             THIS LIST PROGRAM FOR A LOCAL-GO FUNCTION.        *         
024900*---------------------------------------------------------------*         
025000                                                                          
025100         10  INTER-APPL-COMM-AREA       PIC X(200).                       
025200     COPY CNWS001.                                                        
025300     COPY CNWS003.                                                        
025400*---------------------------------------------------------------*         
025500*  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   *         
025600*---------------------------------------------------------------*         
025700         10  ASC-SPECIFIC-COMMAREA.                                       
025800             15  ASC-LIST-KEYS.                                           
025900*  THESE ARE THE KEYS PASSED FROM THE CRIT SPEC SCREEN                    
026000                 20  ASC-KEY-FCLTY-ID      PIC S9(04) COMP.               
026100                 20  ASC-KEY-OPER-UNT-ID   PIC S9(04) COMP.               
026200             15  ASC-BLOCK-ENTRIES      OCCURS 2 TIMES.                   
026300                 20  ASC-BLOCK-MODIFIED-SW                                
026400                                        PIC  X(01).                       
026500                     88  ASC-BLOCK-NOT-MODIFIED    VALUE 'N'.             
026600                     88  ASC-BLOCK-MODIFIED        VALUE 'Y'.             
026700                                                                          
026800                 20  ASC-ITEM-ARRAY.                                      
026900                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.                
027000                         30  ASC-LIST-ITEM-NUMBER                         
027100                                        PIC S9(09) COMP-3.                
027200                         30  ASC-TEMP-SELECTED-SW                         
027300                                        PIC X(01).                        
027400                             88  ASC-VALID-SELECT  VALUE ' '              
027500                                                         'S'              
027600                                                         'B'              
027700                                                         'E'.             
027800                             88  ASC-TEMP-NO-SELECT                       
027900                                                   VALUE ' '.             
028000                             88  ASC-TEMP-SELECT   VALUE 'S'.             
028100                             88  ASC-TEMP-BEGIN    VALUE 'B'.             
028200                             88  ASC-TEMP-END      VALUE 'E'.             
028300                         30  ASC-SELECTED-SW                              
028400                                        PIC X(01).                        
028500                             88  ASC-NO-SELECT     VALUE ' '.             
028600                             88  ASC-SELECT        VALUE 'S'.             
028700                             88  ASC-BEGIN         VALUE 'B'.             
028800                             88  ASC-END           VALUE 'E'.             
028900                         30  ASC-ACTION          PIC X(03).               
029000                         30  ASC-STORE-ZONE      PIC X(05).               
029100                         30  ASC-STORE-ZONE-DESC PIC X(25).               
029200                         30  ASC-LBL-TYP-CDE     PIC X(02).               
029300                                                                          
029400             15  ASC-BLK-SUB            PIC S9(04) COMP SYNC.             
029500             15  ASC-ITEM-SUB           PIC S9(04) COMP SYNC.             
029600             15  ASC-NUMBER-OF-ITEMS-READ                                 
029700                                        PIC S9(09) COMP-3.                
029800             15  ASC-NUMBER-OF-SELECTED-ITEMS                             
029900                                        PIC S9(09) COMP-3.                
030000             15  ASC-ITEMS-DISPLAYED    PIC S9(03) COMP-3.                
030100             15  ASC-ITEM-AT-TOP-OF-PANEL                                 
030200                                        PIC S9(09) COMP-3.                
030300             15  ASC-ITEM-AT-BOT-OF-PANEL                                 
030400                                        PIC S9(09) COMP-3.                
030500             15  ASC-HIGHEST-BLOCK-WRITTEN                                
030600                                        PIC S9(04) COMP SYNC.             
030700             15  ASC-START-OF-SELECTED-RANGE                              
030800                                        PIC S9(09) COMP-3.                
030900             15  ASC-END-OF-SELECTED-RANGE                                
031000                                        PIC S9(09) COMP-3.                
031100             15  ASC-ITEMS-LEFT-INDICATOR                                 
031200                                        PIC X(01).                        
031300                 88  ASC-ITEMS-LEFT-ON-DB          VALUE 'Y'.             
031400                 88  ASC-NO-ITEMS-LEFT-ON-DB       VALUE 'N'.             
031500             15  ASC-TEMP-QUEUE-UPDATE-SW                                 
031600                                        PIC X(01).                        
031700                 88  ASC-UPDATE-TSQ                VALUE 'Y'.             
031800                 88  ASC-NO-TSQ-UPDATE             VALUE ' '.             
031900             15  ASC-LIST-QUEUE-NAME.                                     
032000                 20  ASC-LIST-TERM-ID   PIC X(04).                        
032100                 20  ASC-LIST-TASK-NUMBER                                 
032200                                        PIC S9(05) COMP-3.                
032300                 20  ASC-LIST-SEQ       PIC 9(01).                        
032400             15  ASC-SEL-QUEUE-NAME.                                      
032500                 20  ASC-SEL-TERM-ID    PIC X(04).                        
032600                 20  ASC-SEL-TASK-NUMBER                                  
032700                                        PIC S9(05) COMP-3.                
032800                 20  ASC-SEL-SEQ        PIC 9(01).                        
032900                                                                          
033000             15  ASC-CURSOR-SW          PIC X(01).                        
033100                 88  ASC-NO-CURSOR                 VALUE 'Z'.             
033200                 88  ASC-TYPE-CURSOR               VALUE 'S'.             
033300                 88  ASC-LOC-CURSOR                VALUE 'N'.             
033400                 88  ASC-DESC-CURSOR               VALUE 'T'.             
033500             15  ASC-START-ITEM-REQUEST.                                  
033600                 20  ASC-START-ITEM-REQUEST-N                             
033700                                        PIC 9(07).                        
033800             15  ASC-FCLTY-NME          PIC X(25).                        
033900             15  ASC-FCLTY-ID-X.                                          
034000                 20 ASC-FCLTY-ID        PIC 9(04).                        
034100             15  ASC-OPER-UNT-ID-X.                                       
034200                 20 ASC-OPER-UNT-ID     PIC 9(04).                        
034300/                                                                         
034400*---------------------------------------------------------------*         
034500*    ABEND PROCESSING COPYBOOK.                                 *         
034600*---------------------------------------------------------------*         
034700     COPY DPWS013.                                                        
034800                                                                          
034900     EXEC SQL                                                             
035000          INCLUDE TSTRZN                                                  
035100     END-EXEC.                                                            
035200/                                                                         
035300     EXEC SQL                                                             
035400          INCLUDE SQLCA                                                   
035500     END-EXEC.                                                            
035600                                                                          
035700                                                                          
035800     EXEC SQL                                                             
035900         DECLARE ZONE-CSR CURSOR FOR                                      
036000         SELECT                                                           
036100             STR_ZN_ID                                                    
036200           , STR_ZN_DESC                                                  
036300         FROM TSTRZN                                                      
036400         ORDER BY STR_ZN_ID                                               
036500     END-EXEC.                                                            
036600/                                                                         
036700 LINKAGE SECTION.                                                         
036800                                                                          
036900 01  DFHCOMMAREA.                                                         
037000     05  FILLER                         OCCURS  1 TO 4072 TIMES           
037100                                        DEPENDING ON EIBCALEN.            
037200         10  FILLER                     PIC X.                            
037300                                                                          
037400/                                                                         
037500 PROCEDURE DIVISION.                                                      
037600*---------------------------------------------------------------*         
037700*   THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. *         
037800*   FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  *         
037900*   PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. *         
038000*---------------------------------------------------------------*         
038100 0000-MAIN-MODULE.                                                        
038200                                                                          
038300     INITIALIZE DP030-CICS-API-FIELDS.                                    
038400                                                                          
038500     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
038600     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
038700     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
038800                                                                          
038900     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
039000                                                                          
039100     MOVE LENGTH OF CN212AI        TO DP030-MAP-LENGTH (1).               
039200     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
039300                                                                          
039400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
039500     PERFORM 1000-CONTROL-PROCESSING                                      
039600                                                                          
039700     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
039800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
039900                                                                          
040000*---------------------------------------------------------------*         
040100*   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   *         
040200*   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         *         
040300*---------------------------------------------------------------*         
040400 0001-CALL-CICS-ARCH-API.                                                 
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
040600                                    DFHCOMMAREA                           
040700                                    DP030-CICS-API-FIELDS                 
040800                                    DP020-STANDARD-COMMAREA               
040900                                    CN212AI.                              
041000                                                                          
041100     IF  DP030-RC-CALL-SUCCESSFUL                                         
041200         CONTINUE                                                         
041300     ELSE                                                                 
041400         SET DP013-NO-ROLLBACK                                            
041500             DP013-XCTL-DISPLAY-RESTART                                   
041600             DP013-CICS-ABEND      TO TRUE                                
041700         MOVE '0001-CALL-CICS-ARCH-API'                                   
041800                                   TO DP013-PARAGRAPH                     
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
042100         MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)              
042200         PERFORM DP013-0000-PROCESS-ABEND                                 
042300     END-IF.                                                              
042400                                                                          
042500*----------------------------------------------------------------*        
042600* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
042700* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
042800*                                                                *        
042900* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
043000*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
043100*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
043200*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
043300*----------------------------------------------------------------*        
043400                                                                          
043500 1000-CONTROL-PROCESSING.                                                 
043600                                                                          
043700     EVALUATE TRUE                                                        
043800         WHEN DP020-NEXT-ACT-INITIAL                                      
043900             INITIALIZE ASC-SPECIFIC-COMMAREA                             
044000             MOVE EIBTRMID         TO ASC-LIST-TERM-ID                    
044100                                      ASC-SEL-TERM-ID                     
044200             MOVE DP020-SRC-TASK-NUMBER                                   
044300                                   TO ASC-LIST-TASK-NUMBER                
044400                                      ASC-SEL-TASK-NUMBER                 
044500             MOVE PC-LIST-QUEUE-SEQ                                       
044600                                   TO ASC-LIST-SEQ                        
044700             MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                         
044800             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
044900                                                                          
045000***-----$TEMPLATE NOTE$-------------------------------------------        
045100*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
045200*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
045300*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
045400***---------------------------------------------------------------        
045500             IF  PS-COMMAREA-NOT-VALID                                    
045600                 MOVE PC-TSYMSG-99901                                     
045700                                   TO DP020-MSG-NUMBER                    
045800                 SET DP020-MSG-FATAL                                      
045900                                   TO TRUE                                
046000                 SET DP020-NEXT-ACT-APPL-ERROR                            
046100                                   TO TRUE                                
046200             ELSE                                                         
046300                 PERFORM 4000-BUILD-INITIAL-PANEL                         
046400                 PERFORM 3200-RESET-ATTRIBUTES                            
046500             END-IF                                                       
046600                                                                          
046700         WHEN DP020-NEXT-ACT-READ-MAP                                     
046800             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
046900                                   TO PV-TOP-ITEM                         
047000             PERFORM 2000-PROCESS-PANEL                                   
047100                                                                          
047200         WHEN DP020-NEXT-ACT-RETURN                                       
047300             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
047400                                   TO PV-TOP-ITEM                         
047500             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
047600             PERFORM 6000-REFRESH-PANEL-FROM-TEMP                         
047700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
047800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
047900                                                                          
048000         WHEN OTHER                                                       
048100             SET DP013-LOGIC-ABEND TO TRUE                                
048200             SET DP013-NO-ROLLBACK TO TRUE                                
048300             MOVE '1000-CONTROL-PROCESSING'                               
048400                                   TO DP013-PARAGRAPH                     
048500             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
048600                                   TO DP013-MESSAGE-TEXT(1)               
048700             PERFORM DP013-0000-PROCESS-ABEND                             
048800     END-EVALUATE.                                                        
048900                                                                          
049000/                                                                         
049100*----------------------------------------------------------------*        
049200* MOVE DATA PASSED THROUGH THE INTER-APPL COMMAREA               *        
049300*----------------------------------------------------------------*        
049400                                                                          
049500 1100-PROCESS-INTER-APPL-COMM.                                            
049600                                                                          
049700     SET PS-COMMAREA-VALID         TO TRUE.                               
049800     MOVE SPACE                    TO ASC-START-ITEM-REQUEST.             
049900                                                                          
050000/                                                                         
050100*----------------------------------------------------------------*        
050200* DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *        
050300* ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *        
050400* USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *        
050500* OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *        
050600* FUNCTION KEYS CAN GET THIS FAR.                                *        
050700*----------------------------------------------------------------*        
050800                                                                          
050900 2000-PROCESS-PANEL.                                                      
051000     SET PS-NEXT-SCREEN TO TRUE.                                          
051100     EVALUATE TRUE                                                        
051200         WHEN DP020-SRC-AID = DP016-CLEAR                                 
051300             MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL            
051400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
051500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
051600                                                                          
051700         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
051800             MOVE ASC-KEY-OPER-UNT-ID                                     
051900                                   TO DK-OPER-UNT-ID                      
052000             MOVE ASC-KEY-FCLTY-ID TO DK-FCLTY-ID                         
052100             PERFORM 2050-DELETE-LIST-QUEUE                               
052200             PERFORM 4000-BUILD-INITIAL-PANEL                             
052300                                                                          
052400         WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
052500             PERFORM 2140-DESELECT-ALL-ITEMS                              
052600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
052700                                                                          
052800         WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
052900             PERFORM 2150-SELECT-ALL-ITEMS                                
053000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
053100                                                                          
053200         WHEN OTHER                                                       
053300             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
053400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
053500             IF  PS-NO-ERROR                                              
053600                 SET DP030-CONTINUE-GO TO TRUE                            
053700                 PERFORM 2100-CHECK-FUNCTION-KEY                          
053800             ELSE                                                         
053900                 SET DP030-OVERRIDE-GO TO TRUE                            
054000             END-IF                                                       
054100     END-EVALUATE.                                                        
054200                                                                          
054300/                                                                         
054400*----------------------------------------------------------------*        
054500*  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *        
054600*----------------------------------------------------------------*        
054700 2050-DELETE-LIST-QUEUE.                                                  
054800                                                                          
054900     EXEC CICS                                                            
055000         DELETEQ TS                                                       
055100           QUEUE (ASC-LIST-QUEUE-NAME)                                    
055200            RESP (PV-CICS-RESP)                                           
055300     END-EXEC.                                                            
055400                                                                          
055500     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
055600        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
055700         CONTINUE                                                         
055800     ELSE                                                                 
055900         SET DP013-CICS-ABEND      TO TRUE                                
056000         SET DP013-NO-ROLLBACK     TO TRUE                                
056100                                                                          
056200         MOVE '2050-DELETE-LIST-QUEUE'                                    
056300                                   TO DP013-PARAGRAPH                     
056400         MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'            
056500                                   TO DP013-MESSAGE-TEXT (1)              
056600         PERFORM DP013-0000-PROCESS-ABEND                                 
056700     END-IF.                                                              
056800                                                                          
056900     MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.          
057000                                                                          
057100/                                                                         
057200*----------------------------------------------------------------*        
057300* ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*        
057400* NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *        
057500* CICS ARCHITECTURE API.                                         *        
057600*----------------------------------------------------------------*        
057700 2100-CHECK-FUNCTION-KEY.                                                 
057800                                                                          
057900     SET PS-NEXT-SCREEN TO TRUE.                                          
058000     EVALUATE TRUE                                                        
058100*        WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
058200         WHEN DP020-FK-LOCAL-FUNC-01(DP020-SRC-AID)                       
058300             IF ASC-NUMBER-OF-SELECTED-ITEMS = ZERO                       
058400                 MOVE PC-TSYMSG-01692  TO DP020-MSG-NUMBER                
058600                 MOVE -1               TO MR-SELECTL (1)                  
058610                 SET DP020-MSG-FATAL                                      
058700                     DP030-SET-CURSOR-APPL-1                              
058900*                    PS-ERROR                                             
059000*                    ASC-NO-TSQ-UPDATE                                    
059010*                    DP020-NEXT-ACT-APPL-ERROR                            
059021                     DP030-OVERRIDE-GO                                    
059030                                       TO TRUE                            
059100             ELSE                                                         
059200                 PERFORM 5000-PREPARE-SEL-QUEUE                           
059210             END-IF                                                       
059300                                                                          
059400         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
059500             MOVE +1               TO PV-TOP-ITEM                         
059600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
059700             PERFORM 3200-RESET-ATTRIBUTES                                
059800                                                                          
059900         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
060000             PERFORM 2110-BROWSE-BACKWARD                                 
060100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
060200             PERFORM 3200-RESET-ATTRIBUTES                                
060300                                                                          
060400         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
060500             PERFORM 2120-BROWSE-FORWARD                                  
060600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
060700             PERFORM 3200-RESET-ATTRIBUTES                                
060800                                                                          
060900         WHEN DP020-SRC-AID = DP016-ENTER                                 
061000             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
061100             IF  PS-NO-ERROR                                              
061200                 IF ASC-OPER-UNT-ID = ASC-KEY-OPER-UNT-ID                 
061300                     AND                                                  
061400                     ASC-FCLTY-ID = ASC-KEY-FCLTY-ID                      
061500                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
061600                     PERFORM 3200-RESET-ATTRIBUTES                        
061700                 ELSE                                                     
061800                     MOVE ASC-OPER-UNT-ID                                 
061900                                   TO DK-OPER-UNT-ID                      
062000                                      ASC-KEY-OPER-UNT-ID                 
062100                     MOVE ASC-FCLTY-ID TO DK-FCLTY-ID                     
062200                                          ASC-KEY-FCLTY-ID                
062300                     PERFORM 2050-DELETE-LIST-QUEUE                       
062400                     PERFORM 4000-BUILD-INITIAL-PANEL                     
062500                     PERFORM 3200-RESET-ATTRIBUTES                        
062600                 END-IF                                                   
062700             ELSE                                                         
062800                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
062900             END-IF                                                       
063000                                                                          
063100         WHEN OTHER                                                       
063200             SET DP013-NO-ROLLBACK                                        
063300                 DP013-XCTL-DISPLAY-RESTART                               
063400                 DP013-CICS-ABEND  TO TRUE                                
063500             MOVE '2100-CHECK-FUNCTION-KEY'                               
063600                                   TO DP013-PARAGRAPH                     
063700             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
063800                                   TO DP013-MESSAGE-TEXT (1)              
063900             PERFORM DP013-0000-PROCESS-ABEND                             
064000     END-EVALUATE.                                                        
064100                                                                          
064200/                                                                         
064300*----------------------------------------------------------------*        
064400*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
064500*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
064600*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
064700*    MESSAGE.                                                    *        
064800*----------------------------------------------------------------*        
064900                                                                          
065000 2110-BROWSE-BACKWARD.                                                    
065100                                                                          
065200     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
065300         GIVING PV-TOP-ITEM.                                              
065400                                                                          
065500     IF  ((PV-TOP-ITEM < +1)                                              
065600       OR (PV-TOP-ITEM = +1))                                             
065700         MOVE +1                   TO PV-TOP-ITEM                         
065800*        -- TOP OF LIST --                                                
065900         MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER                    
066000         SET DP020-MSG-INFORMATIONAL                                      
066100                                   TO TRUE                                
066200     END-IF.                                                              
066300                                                                          
066400/                                                                         
066500*----------------------------------------------------------------*        
066600*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
066700*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
066800*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
066900*----------------------------------------------------------------*        
067000                                                                          
067100 2120-BROWSE-FORWARD.                                                     
067200                                                                          
067300     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
067400         GIVING PV-TOP-ITEM.                                              
067500                                                                          
067600     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
067700         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
067800                                   TO PV-TOP-ITEM                         
067900*        -- BOTTOM OF LIST --                                             
068000         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
068100         SET DP020-MSG-INFORMATIONAL                                      
068200                                   TO TRUE                                
068300     ELSE                                                                 
068400         COMPUTE PV-BOTTOM-ITEM =                                         
068500             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
068600         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
068700*            -- BOTTOM OF LIST --                                         
068800             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
068900             SET DP020-MSG-INFORMATIONAL                                  
069000                                   TO TRUE                                
069100         END-IF                                                           
069200     END-IF.                                                              
069300                                                                          
069400/                                                                         
069500*----------------------------------------------------------------*        
069600*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
069700*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
069800*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
069900*    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *        
070000*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
070100*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
070200*----------------------------------------------------------------*        
070300                                                                          
070400 2130-CHECK-FOR-JUMP-BROWSE.                                              
070500                                                                          
070600     SET PS-NO-ERROR               TO TRUE.                               
070700     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
070800         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
070900                                   TO PV-TOP-ITEM                         
071000     ELSE                                                                 
071100         MOVE ASC-START-ITEM-REQUEST                                      
071200                                   TO DP010I-UNEDITED-FIELD               
071300         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
071400         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
071500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
071600                                   TO TRUE                                
263455         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263455              USING DP010I-NUMERIC-EDIT-AREA                              
071800                                                                          
071900         IF  NOT DP010I-ERROR-DETECTED                                    
072000             MOVE DP010I-NUMERIC-FIELD                                    
072100                                   TO ASC-START-ITEM-REQUEST-N            
072200             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
072300                 IF  ASC-START-ITEM-REQUEST-N >                           
072400                                          ASC-NUMBER-OF-ITEMS-READ        
072500                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
072600                     COMPUTE PV-TOP-ITEM =                                
072700                             ASC-NUMBER-OF-ITEMS-READ                     
072800                             - PC-ITEMS-PER-PANEL + 1                     
072900                     IF  PV-TOP-ITEM < +1                                 
073000                         MOVE +1   TO PV-TOP-ITEM                         
073100                         MOVE PC-TSYMSG-00070                             
073200                                   TO DP020-MSG-NUMBER                    
073300                         SET DP020-MSG-INFORMATIONAL                      
073400                                   TO TRUE                                
073500                     END-IF                                               
073600                 ELSE                                                     
073700                     MOVE ASC-START-ITEM-REQUEST-N                        
073800                                   TO PV-TOP-ITEM                         
073900                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
074000                     COMPUTE PV-BOTTOM-ITEM =                             
074100                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
074200                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
074300*                        -- BOTTOM OF LIST --                             
074400                         MOVE PC-TSYMSG-00070                             
074500                                   TO DP020-MSG-NUMBER                    
074600                         SET DP020-MSG-INFORMATIONAL                      
074700                                   TO TRUE                                
074800                     END-IF                                               
074900                 END-IF                                                   
075000             ELSE                                                         
075100*                --- VALUE OUT OF RANGE ----                              
075200                 MOVE PC-TSYMSG-00101                                     
075300                                   TO DP020-MSG-NUMBER                    
075400                 MOVE -1           TO ASTRTITL                            
075500                 SET DP030-SET-CURSOR-APPL-1                              
075600                                   TO TRUE                                
075700                 MOVE DP015-REVERSE                                       
075800                                   TO ASTRTITH                            
075900                 MOVE DP015-RED    TO ASTRTITC                            
076000                 MOVE DP015-UNP-NUM-BRT-MDT                               
076100                                   TO ASTRTITA                            
076200                 SET DP020-MSG-FATAL                                      
076300                                   TO TRUE                                
076400                 SET PS-ERROR      TO TRUE                                
076500             END-IF                                                       
076600                                                                          
076700         ELSE                                                             
076800             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
076900             MOVE -1               TO ASTRTITL                            
077000             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
077100             MOVE DP015-REVERSE    TO ASTRTITH                            
077200             MOVE DP015-RED        TO ASTRTITC                            
077300             MOVE DP015-UNP-NUM-BRT-MDT                                   
077400                                   TO ASTRTITA                            
077500             SET DP020-MSG-FATAL   TO TRUE                                
077600             SET PS-ERROR          TO TRUE                                
077700         END-IF                                                           
077800     END-IF.                                                              
077900                                                                          
078000/                                                                         
078100*----------------------------------------------------------------*        
078200*    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
078300*    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
078400*----------------------------------------------------------------*        
078500 2140-DESELECT-ALL-ITEMS.                                                 
078600                                                                          
078700     MOVE +1                       TO ASC-BLK-SUB.                        
078800                                                                          
078900     PERFORM 2141-CLEAR-SELECTED-INDS                                     
079000         VARYING ASC-ITEM-SUB                                             
079100         FROM 1 BY 1                                                      
079200         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
079300                                                                          
079400     ADD +1                        TO ASC-BLK-SUB.                        
079500                                                                          
079600     PERFORM 2141-CLEAR-SELECTED-INDS                                     
079700         VARYING ASC-ITEM-SUB                                             
079800         FROM 1 BY 1                                                      
079900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
080000                                                                          
080100     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
080200                                   TO PV-LIST-ITEM-NUMBER.                
080300                                                                          
080400     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
080500         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
080600                                                                          
080700     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
080800        (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)              
080900         MOVE +1                   TO ASC-BLK-SUB                         
081000                                                                          
081100         PERFORM 5100-WRITE-TEMP-STORAGE                                  
081200                                                                          
081300         MOVE +1                   TO PV-BLOCK-NUMBER                     
081400                                                                          
081500         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
081600             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
081700                                                                          
081800         PERFORM                                                          
081900             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
082000                                                                          
082100             PERFORM 4410-READ-TEMP-STORAGE                               
082200                                                                          
082300             PERFORM 2141-CLEAR-SELECTED-INDS                             
082400                 VARYING ASC-ITEM-SUB FROM 1 BY 1                         
082500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
082600                                                                          
082700             PERFORM 5100-WRITE-TEMP-STORAGE                              
082800                                                                          
082900             ADD +1                TO PV-BLOCK-NUMBER                     
083000                                                                          
083100         END-PERFORM                                                      
083200                                                                          
083300         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
083400             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
083500                                                                          
083600         PERFORM                                                          
083700             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
083800                   ASC-HIGHEST-BLOCK-WRITTEN                              
083900                                                                          
084000             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
084100                                   TO PV-BLOCK-NUMBER                     
084200                                                                          
084300             PERFORM 4410-READ-TEMP-STORAGE                               
084400                                                                          
084500             PERFORM 2141-CLEAR-SELECTED-INDS                             
084600                 VARYING ASC-ITEM-SUB                                     
084700                 FROM 1 BY 1                                              
084800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
084900                                                                          
085000             PERFORM 5100-WRITE-TEMP-STORAGE                              
085100                                                                          
085200             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
085300                                                                          
085400         END-PERFORM                                                      
085500                                                                          
085600         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
085700                                   TO PV-BLOCK-NUMBER                     
085800                                                                          
085900         PERFORM 4410-READ-TEMP-STORAGE                                   
086000     END-IF.                                                              
086100                                                                          
086200                                                                          
086300     MOVE +0                       TO ASC-NUMBER-OF-SELECTED-ITEMS        
086400     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.        
086500     MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.          
086600                                                                          
086700     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
086800                                                                          
086900/                                                                         
087000*----------------------------------------------------------------*        
087100*    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
087200*----------------------------------------------------------------*        
087300                                                                          
087400 2141-CLEAR-SELECTED-INDS.                                                
087500                                                                          
087600     IF  ASC-LIST-ITEM-NUMBER (ASC-BLK-SUB ASC-ITEM-SUB)                  
087700             NOT EQUAL ZERO                                               
087800         IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
087900             SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
088000                 ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            
088100                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
088200                                   TO TRUE                                
088300         END-IF                                                           
088400     END-IF.                                                              
088500                                                                          
088600/                                                                         
088700*----------------------------------------------------------------*        
088800*  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *        
088900*  FOR ALL ITEMS.                                                *        
089000*----------------------------------------------------------------*        
089100                                                                          
089200 2150-SELECT-ALL-ITEMS.                                                   
089300                                                                          
089400     MOVE +1                       TO ASC-BLK-SUB.                        
089500                                                                          
089600     PERFORM 2151-SET-SELECTED-INDS                                       
089700         VARYING ASC-ITEM-SUB                                             
089800         FROM 1 BY 1                                                      
089900         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
090000                                                                          
090100     ADD +1                        TO ASC-BLK-SUB.                        
090200                                                                          
090300     PERFORM 2151-SET-SELECTED-INDS                                       
090400         VARYING ASC-ITEM-SUB                                             
090500         FROM 1 BY 1                                                      
090600         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
090700                                                                          
090800     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
090900                                   TO PV-LIST-ITEM-NUMBER.                
091000                                                                          
091100     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
091200         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
091300                                                                          
091400     IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                             
091500        (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)            
091600         MOVE +1                   TO ASC-BLK-SUB                         
091700                                                                          
091800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
091900                                                                          
092000         MOVE +1                   TO PV-BLOCK-NUMBER                     
092100                                                                          
092200         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
092300             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
092400                                                                          
092500         PERFORM                                                          
092600             UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY               
092700                                                                          
092800             PERFORM 4410-READ-TEMP-STORAGE                               
092900                                                                          
093000             PERFORM 2151-SET-SELECTED-INDS                               
093100                 VARYING ASC-ITEM-SUB                                     
093200                 FROM 1 BY 1                                              
093300                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
093400                                                                          
093500             PERFORM 5100-WRITE-TEMP-STORAGE                              
093600                                                                          
093700             ADD +1                TO PV-BLOCK-NUMBER                     
093800         END-PERFORM                                                      
093900                                                                          
094000         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
094100             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
094200                                                                          
094300         PERFORM                                                          
094400             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
094500                   ASC-HIGHEST-BLOCK-WRITTEN                              
094600                                                                          
094700             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
094800                                   TO PV-BLOCK-NUMBER                     
094900                                                                          
095000             PERFORM 4410-READ-TEMP-STORAGE                               
095100                                                                          
095200             PERFORM 2151-SET-SELECTED-INDS                               
095300                 VARYING ASC-ITEM-SUB                                     
095400                 FROM 1 BY 1                                              
095500                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
095600                                                                          
095700             PERFORM 5100-WRITE-TEMP-STORAGE                              
095800                                                                          
095900             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
096000         END-PERFORM                                                      
096100                                                                          
096200         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
096300                                   TO PV-BLOCK-NUMBER                     
096400                                                                          
096500         PERFORM 4410-READ-TEMP-STORAGE                                   
096600     END-IF.                                                              
096700                                                                          
096800                                                                          
096900     MOVE ASC-NUMBER-OF-ITEMS-READ TO                                     
097000          ASC-NUMBER-OF-SELECTED-ITEMS.                                   
097100                                                                          
097200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
097300                                                                          
097400/                                                                         
097500*----------------------------------------------------------------*        
097600*    MARK EACH ITEM AS BEING SELECTED.                           *        
097700*----------------------------------------------------------------*        
097800                                                                          
097900 2151-SET-SELECTED-INDS.                                                  
098000                                                                          
098100     IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)                   
098200             NOT EQUAL ZERO                                               
098300         IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
098400             SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                     
098500                 ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                
098600                 ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                          
098700                                   TO TRUE                                
098800         END-IF                                                           
098900     END-IF.                                                              
099000                                                                          
099100/                                                                         
099200*----------------------------------------------------------------*        
099300*  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *        
099400*  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *        
099500*----------------------------------------------------------------*        
099600 2200-MOVE-SCREEN-TO-COMMAREA.                                            
099700                                                                          
099800     MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.                     
099900     MOVE +1                       TO PV-SUB.                             
100000                                                                          
100100     PERFORM 3100-SET-SUBSCRIPTS.                                         
100200                                                                          
100300     IF  ASTRTITL > ZERO                                                  
100400         MOVE ASTRTITI             TO ASC-START-ITEM-REQUEST              
100500     ELSE                                                                 
100600         IF  ASTRTITF = DP015-ERASE-EOF                                   
100700             INITIALIZE ASC-START-ITEM-REQUEST                            
100800         END-IF                                                           
100900     END-IF.                                                              
101000                                                                          
101100                                                                          
101200     PERFORM 2210-MOVE-LINES-TO-COMMAREA                                  
101300         VARYING PV-SUB                                                   
101400         FROM 1 BY 1                                                      
101500         UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                              
101600                                                                          
101700/                                                                         
101800*----------------------------------------------------------------*        
101900*  MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA *        
102000*----------------------------------------------------------------*        
102100 2210-MOVE-LINES-TO-COMMAREA.                                             
102200                                                                          
102300     IF  MR-SELECTL (PV-SUB) > ZERO                                       
102400         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                             
102500                                   TO TRUE                                
102600         MOVE MR-SELECT (PV-SUB)   TO ASC-TEMP-SELECTED-SW                
102700                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
102800     ELSE                                                                 
102900         IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                        
103000             SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE                 
103100             MOVE SPACE            TO ASC-TEMP-SELECTED-SW                
103200                                 (ASC-BLK-SUB, ASC-ITEM-SUB)              
103300         END-IF                                                           
103400     END-IF.                                                              
103500                                                                          
103600                                                                          
103700     ADD +1                        TO ASC-ITEM-SUB.                       
103800                                                                          
103900     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
104000         ADD +1                    TO ASC-BLK-SUB                         
104100         MOVE +1                   TO ASC-ITEM-SUB                        
104200     END-IF.                                                              
104300                                                                          
104400/                                                                         
104500*----------------------------------------------------------------*        
104600*  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *        
104700*  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *        
104800*----------------------------------------------------------------*        
104900 3000-EDIT-DATA-IN-COMMAREA.                                              
105000                                                                          
105100     SET PS-NO-ERROR               TO TRUE.                               
105200                                                                          
105300     PERFORM 3200-RESET-ATTRIBUTES.                                       
105400                                                                          
105500     MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                             
105600**                                                                        
105700* CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.                      
105800* EDITTING IS DONE FROM THE BOTTOM UP.                                    
105900**                                                                        
106000     COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                               
106100         ASC-ITEMS-DISPLAYED - 1.                                         
106200                                                                          
106300     PERFORM 3100-SET-SUBSCRIPTS.                                         
106400     PERFORM 3300-EDIT-SELECTIONS                                         
106500         VARYING PV-SUB                                                   
106600         FROM PV-SUB BY -1                                                
106700         UNTIL PV-SUB < +1                                                
106800                                                                          
106900     IF  PS-NO-ERROR                                                      
107000         PERFORM 3400-CHECK-RANGE-SEQUENCE                                
107100     END-IF.                                                              
107200                                                                          
107300     IF  ((PS-NO-ERROR)                                                   
107400       AND (ASC-START-OF-SELECTED-RANGE > ZERO)                           
107500       AND (ASC-END-OF-SELECTED-RANGE > ZERO))                            
107600             PERFORM 3500-MARK-RANGE-AS-SELECTED                          
107700     END-IF.                                                              
107800/                                                                         
107900*----------------------------------------------------------------*        
108000*    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
108100*    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
108200*    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
108300*                                                                *        
108400*    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
108500*    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
108600*    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
108700*    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
108800*----------------------------------------------------------------*        
108900                                                                          
109000 3100-SET-SUBSCRIPTS.                                                     
109100                                                                          
109200     COMPUTE ASC-ITEM-SUB =                                               
109300         (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.                
109400                                                                          
109500     IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                 
109600         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
109700                                                                          
109800         MOVE PC-MAX-BLOCKS-IN-ARRAY                                      
109900                                   TO ASC-BLK-SUB                         
110000     ELSE                                                                 
110100         MOVE +1                   TO ASC-BLK-SUB                         
110200     END-IF.                                                              
110300                                                                          
110400*----------------------------------------------------------------*        
110500*  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *        
110600*----------------------------------------------------------------*        
110700                                                                          
110800 3200-RESET-ATTRIBUTES.                                                   
110900                                                                          
111000     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA                            
111100     MOVE DP015-GREEN              TO ASTRTITC                            
111200     MOVE DP015-UNDERLINE          TO ASTRTITH                            
111300                                                                          
111400*    IF DP020-NEXT-ACT-RETURN                                             
111500*    OR DP020-NEXT-ACT-READ-MAP                                           
111600         PERFORM VARYING PV-SUB                                           
111700             FROM 1 BY 1 UNTIL PV-SUB > ASC-ITEMS-DISPLAYED               
111800                 MOVE DP015-UNP-ALP-NOR-OFF                               
111900                                   TO MR-SELECTA (PV-SUB)                 
112000                 MOVE DP015-ASK-NOR-OFF                                   
112100                                   TO MR-ACTIONA (PV-SUB)                 
112200                 MOVE DP015-BLUE                                          
112300                                   TO MR-ACTIONC (PV-SUB)                 
112400                 MOVE DP015-UNDERLINE                                     
112500                                   TO MR-SELECTH (PV-SUB)                 
112600*                                     MR-ACTIONH (PV-SUB)                 
112700         END-PERFORM.                                                     
112800*    ELSE                                                                 
112900*        PERFORM VARYING PV-SUB FROM 1 BY 1                               
113000*            UNTIL PV-SUB > PC-ITEMS-PER-PANEL                            
113100*            MOVE DP015-UNP-ALP-NOR-OFF                                   
113200*                                  TO MR-SELECTA (PV-SUB)                 
113300*            MOVE DP015-GREEN      TO MR-SELECTC (PV-SUB)                 
113400*            MOVE DP015-UNDERLINE  TO MR-SELECTH (PV-SUB)                 
113500*        END-PERFORM.                                                     
113600*    END-IF.                                                              
113700                                                                          
113800*----------------------------------------------------------------*        
113900*  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *        
114000*  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *        
114100*  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *        
114200*  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *        
114300*----------------------------------------------------------------*        
114400 3300-EDIT-SELECTIONS.                                                    
114500                                                                          
114600     EVALUATE TRUE                                                        
114700         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
114800             = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)                 
114900                 CONTINUE                                                 
115000                                                                          
115100         WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)                 
115200             IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                
115300                 ADD +1            TO ASC-NUMBER-OF-SELECTED-ITEMS        
115400                 MOVE ASC-TEMP-SELECTED-SW                                
115500                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
115600                                   TO ASC-SELECTED-SW                     
115700                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
115800             END-IF                                                       
115900                                                                          
116000         WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)             
116100                 = SPACE                                                  
116200             EVALUATE TRUE                                                
116300                 WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)               
116400                     SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS        
116500                     MOVE SPACE    TO ASC-SELECTED-SW                     
116600                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
116700                                                                          
116800                 WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                
116900                     MOVE ZERO     TO ASC-START-OF-SELECTED-RANGE         
117000                     MOVE SPACE    TO ASC-SELECTED-SW                     
117100                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
117200                                                                          
117300                 WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)                  
117400                     MOVE ZERO     TO ASC-END-OF-SELECTED-RANGE           
117500                     MOVE SPACE    TO ASC-SELECTED-SW                     
117600                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
117700             END-EVALUATE                                                 
117800                                                                          
117900         WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                   
118000             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
118100                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
118200             END-IF                                                       
118300             PERFORM 3310-EDIT-RANGE                                      
118400                                                                          
118500         WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)                     
118600             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
118700                 SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS            
118800             END-IF                                                       
118900             PERFORM 3310-EDIT-RANGE                                      
119000                                                                          
119100         WHEN OTHER                                                       
119200             MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                    
119300             SET DP020-MSG-FATAL   TO TRUE                                
119400             MOVE -1               TO MR-SELECTL (PV-SUB)                 
119500             SET DP030-SET-CURSOR-APPL-1                                  
119600                                   TO TRUE                                
119700             MOVE DP015-RED        TO MR-SELECTC (PV-SUB)                 
119800             MOVE DP015-REVERSE    TO MR-SELECTH (PV-SUB)                 
119900             SET PS-ERROR          TO TRUE                                
120000             IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)                    
120100                 SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS             
120200                 MOVE SPACE        TO ASC-SELECTED-SW                     
120300                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
120400             END-IF                                                       
120500     END-EVALUATE.                                                        
120600                                                                          
120700                                                                          
120800     SUBTRACT +1 FROM PV-ITEM-IN-USE.                                     
120900     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
121000                                                                          
121100     IF  ASC-ITEM-SUB < 1                                                 
121200         SUBTRACT +1 FROM ASC-BLK-SUB                                     
121300         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
121400     END-IF.                                                              
121500/                                                                         
121600*----------------------------------------------------------------*        
121700*  IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A       *        
121800*  RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1     *        
121900*  ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) *        
122000*  THE ENDING.                                                   *        
122100*----------------------------------------------------------------*        
122200 3310-EDIT-RANGE.                                                         
122300                                                                          
122400     IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                        
122500         IF  ASC-START-OF-SELECTED-RANGE = ZERO                           
122600             MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)        
122700                                   TO ASC-SELECTED-SW                     
122800                                        (ASC-BLK-SUB ASC-ITEM-SUB)        
122900             MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE         
123000         ELSE                                                             
123100             IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE             
123200                 CONTINUE                                                 
123300             ELSE                                                         
123400*                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --                
123500                 MOVE PC-TSYMSG-00054                                     
123600                                   TO DP020-MSG-NUMBER                    
123700                 SET DP020-MSG-FATAL                                      
123800                                   TO TRUE                                
123900                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
124000                 SET DP030-SET-CURSOR-APPL-1                              
124100                                   TO TRUE                                
124200                 MOVE DP015-RED    TO MR-SELECTC (PV-SUB)                 
124300*                MOVE DP015-UNDERLINE                                     
124400*                                  TO MR-SELECTH (PV-SUB)                 
124500                 SET PS-ERROR      TO TRUE                                
124600             END-IF                                                       
124700         END-IF                                                           
124800     ELSE                                                                 
124900         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
125000             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
125100                                   TO ASC-SELECTED-SW                     
125200                                     (ASC-BLK-SUB ASC-ITEM-SUB)           
125300             MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE           
125400         ELSE                                                             
125500             IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE               
125600                 CONTINUE                                                 
125700             ELSE                                                         
125800*                -- ONLY ONE END-OF-RANGE ALLOWED --                      
125900                 MOVE PC-TSYMSG-00055                                     
126000                                   TO DP020-MSG-NUMBER                    
126100                 SET DP020-MSG-FATAL                                      
126200                                   TO TRUE                                
126300                 MOVE -1           TO MR-SELECTL (PV-SUB)                 
126400                 SET DP030-SET-CURSOR-APPL-1                              
126500                                   TO TRUE                                
126600                 SET PS-ERROR      TO TRUE                                
126700             END-IF                                                       
126800         END-IF                                                           
126900     END-IF.                                                              
127000                                                                          
127100*----------------------------------------------------------------*        
127200*  IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,     *        
127300*  MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.   *        
127400*----------------------------------------------------------------*        
127500 3400-CHECK-RANGE-SEQUENCE.                                               
127600                                                                          
127700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
127800       AND  ASC-END-OF-SELECTED-RANGE > ZERO                              
127900       AND  ASC-START-OF-SELECTED-RANGE >                                 
128000                                         ASC-END-OF-SELECTED-RANGE        
128100*        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                        
128200         MOVE PC-TSYMSG-00052      TO DP020-MSG-NUMBER                    
128300         SET DP020-MSG-FATAL       TO TRUE                                
128400         SET PS-ERROR              TO TRUE                                
128500         PERFORM 3410-POSITION-AT-ERROR                                   
128600     END-IF.                                                              
128700                                                                          
128800*----------------------------------------------------------------*        
128900*  BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE     *        
129000*  PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR  *        
129100*  SHOULD BE POSITIONED ON A RANGE ERROR.                        *        
129200*----------------------------------------------------------------*        
129300 3410-POSITION-AT-ERROR.                                                  
129400                                                                          
129500     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
129600                                                                          
129700     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
129800                    - PV-TOP-ITEM + 1.                                    
129900                                                                          
130000     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
130100         CONTINUE                                                         
130200     ELSE                                                                 
130300         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
130400         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
130500         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
130600     END-IF.                                                              
130700                                                                          
130800     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
130900                    - PV-TOP-ITEM + 1.                                    
131000                                                                          
131100     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
131200         CONTINUE                                                         
131300     ELSE                                                                 
131400         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
131500         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
131600         MOVE DP015-RED            TO MR-SELECTC (PV-SUB)                 
131700     END-IF.                                                              
131800                                                                          
131900/                                                                         
132000*----------------------------------------------------------------*        
132100*    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
132200*    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
132300*    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
132400*----------------------------------------------------------------*        
132500                                                                          
132600 3500-MARK-RANGE-AS-SELECTED.                                             
132700                                                                          
132800     MOVE +1                       TO ASC-BLK-SUB.                        
132900                                                                          
133000     PERFORM 3510-MARK-SELECTED-INDS                                      
133100         VARYING ASC-ITEM-SUB                                             
133200         FROM 1 BY 1                                                      
133300         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
133400                                                                          
133500     ADD +1                        TO ASC-BLK-SUB.                        
133600                                                                          
133700     PERFORM 3510-MARK-SELECTED-INDS                                      
133800         VARYING ASC-ITEM-SUB                                             
133900         FROM 1 BY 1                                                      
134000         UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                         
134100                                                                          
134200     MOVE ASC-LIST-ITEM-NUMBER(1 1)                                       
134300                                   TO PV-LIST-ITEM-NUMBER.                
134400                                                                          
134500     COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
134600         1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
134700               PC-ITEMS-PER-PANEL).                                       
134800                                                                          
134900     MOVE ASC-START-OF-SELECTED-RANGE                                     
135000                                   TO PV-START-OF-SELECTED-RANGE.         
135100                                                                          
135200     COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
135300         1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
135400               PC-ITEMS-PER-PANEL).                                       
135500                                                                          
135600     MOVE ASC-END-OF-SELECTED-RANGE                                       
135700                                   TO PV-END-OF-SELECTED-RANGE.           
135800                                                                          
135900     COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
136000         1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
136100               PC-ITEMS-PER-PANEL).                                       
136200                                                                          
136300     COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
136400         PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
136500                                                                          
136600     IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
136700        (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
136800         MOVE +1                   TO ASC-BLK-SUB                         
136900         PERFORM 5100-WRITE-TEMP-STORAGE                                  
137000         COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
137100             PV-FIRST-BLOCK-IN-ARRAY - 1                                  
137200                                                                          
137300         PERFORM                                                          
137400             UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
137500                   PV-PREV-BLOCK-IN-ARRAY                                 
137600             MOVE PV-FIRST-BLOCK-IN-RANGE                                 
137700                                   TO PV-BLOCK-NUMBER                     
137800             PERFORM 4410-READ-TEMP-STORAGE                               
137900             PERFORM 3510-MARK-SELECTED-INDS                              
138000                 VARYING ASC-ITEM-SUB                                     
138100                 FROM 1 BY 1                                              
138200                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
138300             PERFORM 5100-WRITE-TEMP-STORAGE                              
138400             ADD +1                TO PV-FIRST-BLOCK-IN-RANGE             
138500         END-PERFORM                                                      
138600                                                                          
138700         COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
138800             PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
138900         PERFORM                                                          
139000             UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
139100                   PV-LAST-BLOCK-IN-RANGE                                 
139200             MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
139300                                   TO PV-BLOCK-NUMBER                     
139400             PERFORM 4410-READ-TEMP-STORAGE                               
139500             PERFORM 3510-MARK-SELECTED-INDS                              
139600                 VARYING ASC-ITEM-SUB                                     
139700                 FROM 1 BY 1                                              
139800                 UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                  
139900             PERFORM 5100-WRITE-TEMP-STORAGE                              
140000             ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY              
140100         END-PERFORM                                                      
140200                                                                          
140300         MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
140400                                   TO PV-BLOCK-NUMBER                     
140500         PERFORM 4410-READ-TEMP-STORAGE                                   
140600     END-IF.                                                              
140700                                                                          
140800     MOVE +0                       TO ASC-START-OF-SELECTED-RANGE         
140900                                      ASC-END-OF-SELECTED-RANGE.          
141000                                                                          
141100/                                                                         
141200*----------------------------------------------------------------*        
141300* SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *        
141400*----------------------------------------------------------------*        
141500                                                                          
141600 3510-MARK-SELECTED-INDS.                                                 
141700                                                                          
141800     IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB) >                 
141900         ASC-START-OF-SELECTED-RANGE)                                     
142000     OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB) =                 
142100         ASC-START-OF-SELECTED-RANGE)                                     
142200                                                                          
142300         IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB) <             
142400             ASC-END-OF-SELECTED-RANGE)                                   
142500         OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB) =             
142600             ASC-END-OF-SELECTED-RANGE)                                   
142700                                                                          
142800             IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)                  
142900                 SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)           
143000                     ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)           
143100                                   TO TRUE                                
143200                 SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      
143300                                   TO TRUE                                
143400                 ADD +1            TO ASC-NUMBER-OF-SELECTED-ITEMS        
143500             END-IF                                                       
143600         END-IF                                                           
143700     END-IF.                                                              
143800                                                                          
143900/                                                                         
144000*----------------------------------------------------------------*        
144100*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
144200*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
144300*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
144400*    DISPLAY THE PANEL TO USER.                                  *        
144500*----------------------------------------------------------------*        
144600                                                                          
144700 4000-BUILD-INITIAL-PANEL.                                                
144800                                                                          
144900     INITIALIZE ASC-BLOCK-ENTRIES (1)                                     
145000                ASC-BLOCK-ENTRIES (2).                                    
145100                                                                          
145200     MOVE +1                       TO ASC-BLK-SUB.                        
145300                                                                          
145400     SET ASC-ITEMS-LEFT-ON-DB      TO TRUE.                               
145500                                                                          
145600     MOVE +0                       TO ASC-ITEM-SUB                        
145700                                      ASC-NUMBER-OF-ITEMS-READ            
145800                                      ASC-NUMBER-OF-SELECTED-ITEMS        
145900                                      ASC-HIGHEST-BLOCK-WRITTEN           
146000                                      ASC-START-OF-SELECTED-RANGE         
146100                                      ASC-END-OF-SELECTED-RANGE           
146200                                      ASC-ITEM-AT-TOP-OF-PANEL.           
146300                                                                          
146400                                                                          
146500     PERFORM 4010-OPEN-CURSOR.                                            
146600                                                                          
146700     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
146800                                                                          
146900     PERFORM 4020-CLOSE-CURSOR.                                           
147000                                                                          
147100     MOVE +1                       TO PV-TOP-ITEM.                        
147200                                                                          
147300     IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
147400         IF DP020-NEXT-ACT-INITIAL                                        
147500             SET DP020-NEXT-ACT-APPL-ERROR                                
147600                                   TO TRUE                                
147700             SET DP020-MSG-FATAL   TO TRUE                                
147800             MOVE PC-TSYMSG-00050  TO DP020-MSG-NUMBER                    
147900         ELSE                                                             
148000             SET DP020-MSG-FATAL   TO TRUE                                
148100             MOVE PC-TSYMSG-00050  TO DP020-MSG-NUMBER                    
148200             SET ASC-BLOCK-MODIFIED (1)                                   
148300                 ASC-BLOCK-MODIFIED (2)                                   
148400                                   TO TRUE                                
148500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
148600         END-IF                                                           
148700     ELSE                                                                 
148800         MOVE -1                   TO MR-SELECTL (1)                      
148900         SET DP030-SET-CURSOR-APPL-1                                      
149000                                   TO TRUE                                
149100         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                             
149200     END-IF.                                                              
149300                                                                          
149400/                                                                         
149500*----------------------------------------------------------------*        
149600* OPEN THE FACILITY CURSOR.                                      *        
149700*----------------------------------------------------------------*        
149800                                                                          
149900 4010-OPEN-CURSOR.                                                        
150000                                                                          
150100     EXEC SQL                                                             
150200         OPEN ZONE-CSR                                                    
150300     END-EXEC                                                             
150400                                                                          
150500     EVALUATE TRUE                                                        
150600         WHEN SQLCODE = ZERO                                              
150700              CONTINUE                                                    
150800         WHEN SQLCODE = -904                                              
150900         WHEN SQLCODE = -911                                              
151000         WHEN SQLCODE = -913                                              
151100              PERFORM 9900-SOFT-ABORT                                     
151200         WHEN SQLWARN0 NOT = SPACE                                        
151300         WHEN SQLCODE  NOT = ZERO                                         
151400             MOVE '4010-OPEN-CURSOR'                                      
151500                                   TO DP013-PARAGRAPH                     
151600             MOVE 'OPEN ZONE-CSR CURSOR'                                  
151700                                   TO DP013-MESSAGE-TEXT (1)              
151800             MOVE SQLCA       TO DP013-SQLCA                              
151900             MOVE 'TSTRZN '   TO DP013-DB2-TABLE-NAME (1)                 
152000             SET DP013-DB2-ABEND                                          
152100                                   TO TRUE                                
152200             PERFORM DP013-0000-PROCESS-ABEND                             
152300     END-EVALUATE.                                                        
152400                                                                          
152500/                                                                         
152600*----------------------------------------------------------------*        
152700* CLOSE THE ZONE CURSOR.                                                  
152800*----------------------------------------------------------------*        
152900                                                                          
153000 4020-CLOSE-CURSOR.                                                       
153100                                                                          
153200     EXEC SQL                                                             
153300         CLOSE ZONE-CSR                                                   
153400     END-EXEC.                                                            
153500                                                                          
153600     EVALUATE TRUE                                                        
153700         WHEN SQLCODE = ZERO                                              
153800              CONTINUE                                                    
153900         WHEN SQLWARN0 NOT = SPACE                                        
154000         WHEN SQLCODE  NOT = ZERO                                         
154100              MOVE '4020-CLOSE-CURSOR'                                    
154200                                   TO DP013-PARAGRAPH                     
154300              MOVE 'CLOSE ZONE-CSR CURSOR'                                
154400                                   TO DP013-MESSAGE-TEXT (1)              
154500              MOVE SQLCA           TO DP013-SQLCA                         
154600              MOVE 'TSTRZN '       TO DP013-DB2-TABLE-NAME (1)            
154700              SET DP013-DB2-ABEND  TO TRUE                                
154800              PERFORM DP013-0000-PROCESS-ABEND                            
154900     END-EVALUATE.                                                        
155000                                                                          
155100/                                                                         
155200*----------------------------------------------------------------*        
155300*    DEPENDING ON WHAT WAS PASSED IN THE COMMAREA, SETUP THE     *        
155400*    DB2 KEYS IN PREPARATION FOR THE OPEN CURSOR.                *        
155500*----------------------------------------------------------------*        
155600                                                                          
155700/                                                                         
155800*----------------------------------------------------------------*        
155900*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
156000*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
156100*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
156200*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
156300*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
156400*                                                                *        
156500*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
156600*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
156700*    UPON INITIALIZATION.                                        *        
156800*----------------------------------------------------------------*        
156900                                                                          
157000 4300-READ-ITEMS-FROM-DB.                                                 
157100                                                                          
157200                                                                          
157300     PERFORM                                                              
157400         VARYING PV-READ-COUNT                                            
157500         FROM 1 BY 1                                                      
157600         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
157700            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
157800                                                                          
157900         PERFORM 4310-FETCH                                               
158000                                                                          
158100         IF  ASC-ITEMS-LEFT-ON-DB                                         
158200             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
158300                 ADD +1            TO ASC-ITEM-SUB                        
158400             ELSE                                                         
158500                 ADD +1            TO ASC-BLK-SUB                         
158600                 MOVE +1           TO ASC-ITEM-SUB                        
158700                                                                          
158800                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
158900                     MOVE +1       TO ASC-BLK-SUB                         
159000                     SET ASC-BLOCK-MODIFIED (1)                           
159100                                   TO TRUE                                
159200                     PERFORM 5100-WRITE-TEMP-STORAGE                      
159300                     MOVE ASC-BLOCK-ENTRIES                               
159400                               (PC-MAX-BLOCKS-IN-ARRAY)                   
159500                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
159600                     ADD +1        TO ASC-BLK-SUB                         
159700                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
159800                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
159900                                   TO TRUE                                
160000                 END-IF                                                   
160100             END-IF                                                       
160200             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
160300             MOVE ASC-NUMBER-OF-ITEMS-READ                                
160400                                   TO ASC-LIST-ITEM-NUMBER                
160500                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
160600                                                                          
160700             MOVE STRZN-STR-ZN-ID                                         
160800                                   TO  ASC-STORE-ZONE                     
160900                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161000             MOVE STRZN-STR-ZN-DESC                                       
161100                                   TO  ASC-STORE-ZONE-DESC                
161200                                       (ASC-BLK-SUB ASC-ITEM-SUB)         
161300         END-IF                                                           
161400     END-PERFORM.                                                         
161500                                                                          
161600     IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
161700         MOVE +1                   TO ASC-BLK-SUB                         
161800         PERFORM 5100-WRITE-TEMP-STORAGE                                  
161900         ADD +1                    TO ASC-BLK-SUB                         
162000         PERFORM 5100-WRITE-TEMP-STORAGE                                  
162100                                                                          
162200         MOVE 1                    TO ASC-BLK-SUB                         
162300                                      PV-BLOCK-NUMBER                     
162400         PERFORM 4410-READ-TEMP-STORAGE                                   
162500         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
162600             MOVE 2                TO ASC-BLK-SUB                         
162700                                      PV-BLOCK-NUMBER                     
162800             PERFORM 4410-READ-TEMP-STORAGE                               
162900         END-IF                                                           
163000     END-IF.                                                              
163100                                                                          
163200/                                                                         
163300*----------------------------------------------------------------*        
163400*    FETCH THE STORE ZONE CURSOR.                                         
163500*----------------------------------------------------------------*        
163600                                                                          
163700 4310-FETCH.                                                              
163800                                                                          
163900     EXEC SQL                                                             
164000         FETCH ZONE-CSR                                                   
164100          INTO                                                            
164200               :STRZN-STR-ZN-ID                                           
164300            ,  :STRZN-STR-ZN-DESC                                         
164400     END-EXEC.                                                            
164500                                                                          
164600     EVALUATE TRUE                                                        
164700         WHEN SQLCODE = ZERO                                              
164800              CONTINUE                                                    
164900         WHEN SQLCODE = +100                                              
165000              SET ASC-NO-ITEMS-LEFT-ON-DB                                 
165100                                   TO TRUE                                
165200         WHEN SQLWARN0 NOT = SPACE                                        
165300         WHEN SQLCODE < ZERO                                              
165400         WHEN SQLCODE > ZERO                                              
165500              MOVE '4310-FETCH-CURSOR'                                    
165600                                   TO DP013-PARAGRAPH                     
165700              MOVE 'FETCH ZONE CURSOR'                                    
165800                                   TO DP013-MESSAGE-TEXT (1)              
165900              MOVE SQLCA           TO DP013-SQLCA                         
166000              MOVE 'TSTRZN '       TO DP013-DB2-TABLE-NAME (1)            
166100              SET DP013-DB2-ABEND  TO TRUE                                
166200              PERFORM DP013-0000-PROCESS-ABEND                            
166300     END-EVALUATE.                                                        
166400/                                                                         
166500*----------------------------------------------------------------*        
166600* MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *        
166700*----------------------------------------------------------------*        
166800                                                                          
166900 4400-MOVE-COMMAREA-TO-SCREEN.                                            
167000                                                                          
167100     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
167200         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
167300         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
167400             MOVE +1               TO ASC-BLK-SUB                         
167500             PERFORM 5100-WRITE-TEMP-STORAGE                              
167600             ADD +1                TO ASC-BLK-SUB                         
167700             PERFORM 5100-WRITE-TEMP-STORAGE                              
167800             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
167900                 GIVING PV-BLOCK-NUMBER                                   
168000                 REMAINDER PV-REMAINDER                                   
168100             IF  PV-REMAINDER > ZERO                                      
168200                 ADD +1            TO PV-BLOCK-NUMBER                     
168300             END-IF                                                       
168400             MOVE +1               TO ASC-BLK-SUB                         
168500             PERFORM 4410-READ-TEMP-STORAGE                               
168600             ADD +1                TO ASC-BLK-SUB                         
168700                                      PV-BLOCK-NUMBER                     
168800             PERFORM 4410-READ-TEMP-STORAGE                               
168900     END-IF.                                                              
169000                                                                          
169100     MOVE +1                       TO ASC-BLK-SUB.                        
169200                                                                          
169300     COMPUTE ASC-ITEM-SUB =                                               
169400         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
169500         + PC-ITEMS-PER-PANEL).                                           
169600                                                                          
169700     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
169800         ADD +1                    TO ASC-BLK-SUB                         
169900         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
170000     END-IF.                                                              
170100                                                                          
170200     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
170300         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
170400                                                                          
170500     PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                                
170600     PERFORM 4420-FORMAT-LIST-LINES                                       
170700         VARYING PV-SUB                                                   
170800         FROM PC-ITEMS-PER-PANEL BY -1                                    
170900         UNTIL PV-SUB < +1.                                               
171000                                                                          
171100     COMPUTE ASC-ITEMS-DISPLAYED =                                        
171200         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
171300                                                                          
171400     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
171500                                      ASTRTITO.                           
171600                                                                          
171700     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
171800     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
171900     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
172000                                   TO ASELITO.                            
172100                                                                          
172200     IF  DP020-MSG-NUMBER-X = SPACE                                       
172300         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
172400*            --- MORE ITEMS TO DISPLAY ----                               
172500             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
172600             SET DP020-MSG-INFORMATIONAL                                  
172700                                   TO TRUE                                
172800         END-IF                                                           
172900     END-IF.                                                              
173000                                                                          
173100*----------------------------------------------------------------*        
173200*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
173300*----------------------------------------------------------------*        
173400                                                                          
173500 4410-READ-TEMP-STORAGE.                                                  
173600                                                                          
173700     EXEC CICS                                                            
173800         READQ TS                                                         
173900             QUEUE (ASC-LIST-QUEUE-NAME)                                  
174000             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
174100             ITEM  (PV-BLOCK-NUMBER)                                      
174200             RESP  (PV-CICS-RESP)                                         
174300     END-EXEC.                                                            
174400                                                                          
174500     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
174600       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
174700         CONTINUE                                                         
174800     ELSE                                                                 
174900         SET DP013-CICS-ABEND      TO TRUE                                
175000         SET DP013-NO-ROLLBACK     TO TRUE                                
175100         MOVE '4410-READ-TEMP-STORAGE'                                    
175200                                   TO DP013-PARAGRAPH                     
175300         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
175400                                   TO DP013-MESSAGE-TEXT(1)               
175500         PERFORM DP013-0000-PROCESS-ABEND                                 
175600     END-IF.                                                              
175700                                                                          
175800     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                              
175900                                   TO TRUE.                               
176000                                                                          
176100     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
176200         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
176300     END-IF.                                                              
176400                                                                          
176500/                                                                         
176600*----------------------------------------------------------------*        
176700*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
176800*----------------------------------------------------------------*        
176900                                                                          
177000 4420-FORMAT-LIST-LINES.                                                  
177100                                                                          
177200     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
177300         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
177400         MOVE DP015-ASK-DRK-OFF    TO MR-SELECTA (PV-SUB)                 
177500                                      MR-ACTIONA (PV-SUB)                 
177600         MOVE SPACE                TO MR-SELECT  (PV-SUB)                 
177700                                      MR-ACTION  (PV-SUB)                 
177800                                      MR-STORE-ZONE   (PV-SUB)            
177900                                      MR-STORE-ZONE-DESC (PV-SUB)         
178000     ELSE                                                                 
178100         IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
178200                                                   > SPACE                
178300             MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         
178400                                   TO MR-SELECT  (PV-SUB)                 
178500         ELSE                                                             
178600             MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)              
178700                                   TO MR-SELECT  (PV-SUB)                 
178800         END-IF                                                           
178900                                                                          
179000         IF  DP030-SET-CURSOR-TRAILER                                     
179100             IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)        
179200                 > SPACE)                                                 
179300               OR (PV-SUB = 1))                                           
179400                SET DP030-SET-CURSOR-APPL-1 TO TRUE                       
179500                MOVE -1            TO MR-SELECTL (PV-SUB)                 
179600             END-IF                                                       
179700         END-IF                                                           
179800                                                                          
179900         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
180000             MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)                   
180100                                   TO MR-ACTION (PV-SUB)                  
180200                                                                          
180300             MOVE ASC-STORE-ZONE   (ASC-BLK-SUB ASC-ITEM-SUB)             
180400                                   TO MR-STORE-ZONE   (PV-SUB)            
180500             MOVE ASC-STORE-ZONE-DESC                                     
180600                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
180700                                   TO MR-STORE-ZONE-DESC (PV-SUB)         
180800                                                                          
180900         END-IF                                                           
181000     END-IF.                                                              
181100                                                                          
181200     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
181300                                                                          
181400     IF  ASC-ITEM-SUB < + 1                                               
181500         MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                        
181600         MOVE +1                   TO ASC-BLK-SUB                         
181700     END-IF.                                                              
181800                                                                          
181900/                                                                         
182000*----------------------------------------------------------------*        
182100*  IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF    *        
182200*  RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,       *        
182300*  ISSUE A WARNING.                                              *        
182400*----------------------------------------------------------------*        
182500 4430-CHECK-FOR-BEGIN-AND-END.                                            
182600                                                                          
182700     IF  ASC-START-OF-SELECTED-RANGE > ZERO                               
182800         IF  ASC-END-OF-SELECTED-RANGE = ZERO                             
182900*            -- NO END-OF-RANGE SPECFIED ---                              
183000             MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER                    
183100             SET DP020-MSG-WARNING TO TRUE                                
183200             PERFORM 4431-POSITION-AT-WARNING                             
183300         END-IF                                                           
183400     ELSE                                                                 
183500         IF  ASC-END-OF-SELECTED-RANGE > ZERO                             
183600*            -- NO BEGINNING-OF-RANGE SPECFIED ---                        
183700             MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER                    
183800             SET DP020-MSG-WARNING TO TRUE                                
183900             PERFORM 4431-POSITION-AT-WARNING                             
184000         END-IF                                                           
184100     END-IF.                                                              
184200                                                                          
184300/                                                                         
184400*----------------------------------------------------------------*        
184500*  ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,   *        
184600*  DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE    *        
184700*  IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND         *        
184800*  POSITION THE CURSOR THERE.                                    *        
184900*----------------------------------------------------------------*        
185000 4431-POSITION-AT-WARNING.                                                
185100                                                                          
185200     COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                         
185300                    - PV-TOP-ITEM + 1.                                    
185400                                                                          
185500     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
185600         CONTINUE                                                         
185700     ELSE                                                                 
185800         SET DP030-SET-CURSOR-APPL-1                                      
185900                                   TO TRUE                                
186000         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
186100         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
186200         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
186300     END-IF.                                                              
186400                                                                          
186500     COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                           
186600                    - PV-TOP-ITEM + 1.                                    
186700                                                                          
186800     IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                              
186900         CONTINUE                                                         
187000     ELSE                                                                 
187100         SET DP030-SET-CURSOR-APPL-1                                      
187200                                   TO TRUE                                
187300         MOVE -1                   TO MR-SELECTL (PV-SUB)                 
187400         MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)                 
187500         MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)                 
187600     END-IF.                                                              
187700                                                                          
187800/                                                                         
187900*----------------------------------------------------------------*        
188000* PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *        
188100* ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *        
188200* FOR THE SELECTED ITEMS QUEUE.                                  *        
188300*                                                                *        
188400* FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *        
188500* QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *        
188600* THE ARRAY IS FULL.                                             *        
188700*----------------------------------------------------------------*        
188800                                                                          
188900 5000-PREPARE-SEL-QUEUE.                                                  
189000                                                                          
189100     MOVE +1                       TO ASC-BLK-SUB.                        
189200     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
189300     ADD +1                        TO ASC-BLK-SUB.                        
189400     PERFORM 5100-WRITE-TEMP-STORAGE.                                     
189500                                                                          
189600     MOVE ASC-SEL-QUEUE-NAME       TO CN003-SEL-QUEUE-NAME.               
189700     PERFORM 6010-DELETE-SEL-QUEUE.                                       
189800                                                                          
189900     IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO                              
190000         SET ASC-UPDATE-TSQ        TO TRUE                                
190100     END-IF.                                                              
190200                                                                          
190300     SET CN007-IDX                 TO 1.                                  
190400     MOVE ZERO                     TO CN003-NUMBER-OF-TS-ITEMS.           
190500     INITIALIZE CN007-SELECTED-ITEM-ARRAY.                                
190600                                                                          
190700                                                                          
190800     PERFORM 5200-FILL-SEL-QUEUE-RECORD                                   
190900                 VARYING PV-ITEM  FROM 1 BY 1                             
191000                 UNTIL   PV-ITEM > ASC-HIGHEST-BLOCK-WRITTEN              
191100                                                                          
191200     IF  CN007-IDX > 1                                                    
191300         PERFORM 5205-WRITE-SEL-QUEUE                                     
191400         ADD +1                    TO CN003-NUMBER-OF-TS-ITEMS            
191500     END-IF.                                                              
191600                                                                          
191700/                                                                         
191800*----------------------------------------------------------------*        
191900*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
192000*    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
192100*----------------------------------------------------------------*        
192200                                                                          
192300 5100-WRITE-TEMP-STORAGE.                                                 
192400                                                                          
192500     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
192600                                   TO PV-LIST-ITEM-NUMBER.                
192700     COMPUTE PV-BLOCK-NUMBER =                                            
192800         1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).            
192900                                                                          
193000     IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)                     
193100     OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)                     
193200         IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                              
193300             PERFORM 5105-REWRITE-ASC-LISTQ                               
193400         END-IF                                                           
193500     ELSE                                                                 
193600         COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
193700         ASC-HIGHEST-BLOCK-WRITTEN + 1                                    
193800         PERFORM 5110-WRITE-ASC-LISTQ                                     
193900         ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN           
194000     END-IF.                                                              
194100                                                                          
194200     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)                              
194300                                   TO TRUE.                               
194400                                                                          
194500/                                                                         
194600*----------------------------------------------------------------*        
194700*  REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-     *        
194800*  SPECIFIC COMMAREA.                                            *        
194900*----------------------------------------------------------------*        
195000 5105-REWRITE-ASC-LISTQ.                                                  
195100                                                                          
195200     EXEC CICS                                                            
195300         WRITEQ TS                                                        
195400             QUEUE (ASC-LIST-QUEUE-NAME)                                  
195500             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
195600             ITEM  (PV-BLOCK-NUMBER)                                      
195700             REWRITE                                                      
195800             RESP  (PV-CICS-RESP)                                         
195900     END-EXEC.                                                            
196000                                                                          
196100     IF  PV-CICS-RESP = DFHRESP(NORMAL)                                   
196200         CONTINUE                                                         
196300     ELSE                                                                 
196400         SET DP013-CICS-ABEND      TO TRUE                                
196500         SET DP013-NO-ROLLBACK     TO TRUE                                
196600         MOVE '5105-REWRITE-ASC-LISTQ'                                    
196700                                   TO DP013-PARAGRAPH                     
196800         MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'             
196900                                   TO DP013-MESSAGE-TEXT(1)               
197000         PERFORM DP013-0000-PROCESS-ABEND                                 
197100     END-IF.                                                              
197200                                                                          
197300/                                                                         
197400*----------------------------------------------------------------*        
197500*  WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-       *        
197600*  SPECIFIC COMMAREA.                                            *        
197700*----------------------------------------------------------------*        
197800 5110-WRITE-ASC-LISTQ.                                                    
197900                                                                          
198000     EXEC CICS                                                            
198100         WRITEQ TS                                                        
198200             QUEUE (ASC-LIST-QUEUE-NAME)                                  
198300             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
198400             ITEM  (PV-BLOCK-NUMBER)                                      
198500             RESP  (PV-CICS-RESP)                                         
198600     END-EXEC.                                                            
198700                                                                          
198800     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
198900         SET DP013-CICS-ABEND      TO TRUE                                
199000         SET DP013-NO-ROLLBACK     TO TRUE                                
199100         MOVE '5110-WRITE-ASC-LISTQ'                                      
199200                                   TO DP013-PARAGRAPH                     
199300         MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'                   
199400                                   TO DP013-MESSAGE-TEXT(1)               
199500         PERFORM DP013-0000-PROCESS-ABEND                                 
199600     END-IF.                                                              
199700                                                                          
199800/                                                                         
199900*----------------------------------------------------------------*        
200000* READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *        
200100* TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *        
200200*----------------------------------------------------------------*        
200300                                                                          
200400 5200-FILL-SEL-QUEUE-RECORD.                                              
200500                                                                          
200600     PERFORM 6112-READ-LIST-QUEUE.                                        
200700                                                                          
200800     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
200900         PERFORM                                                          
201000         VARYING LQW-IDX                                                  
201100         FROM 1 BY 1                                                      
201200         UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL                    
201300           OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO                  
201400                                                                          
201500             IF  LQW-SELECT (LQW-IDX)                                     
201600                 MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)                      
201700                                   TO CN007-ITEM      (CN007-IDX)         
201800                 MOVE SPACE        TO CN007-ACTION-SW (CN007-IDX)         
201900                 MOVE LQW-STORE-ZONE (LQW-IDX)                            
202000                                   TO CN007-STORE-ZONE                    
202100                                                     (CN007-IDX)          
202200                 SET CN007-IDX UP BY 1                                    
202300                 IF  CN007-IDX > CN007-MAX-ITEMS                          
202400                     PERFORM 5205-WRITE-SEL-QUEUE                         
202500                     ADD +1        TO CN003-NUMBER-OF-TS-ITEMS            
202600                     INITIALIZE CN007-SELECTED-ITEM-ARRAY                 
202700                     SET CN007-IDX TO 1                                   
202800                 END-IF                                                   
202900             END-IF                                                       
203000         END-PERFORM                                                      
203100     END-IF.                                                              
203200                                                                          
203300/                                                                         
203400*----------------------------------------------------------------*        
203500*    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *        
203600*----------------------------------------------------------------*        
203700                                                                          
203800 5205-WRITE-SEL-QUEUE.                                                    
203900                                                                          
204000     MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                    
204100                                   TO CN007-SELECTED-ITEMS.               
204200                                                                          
204300     EXEC CICS                                                            
204400          WRITEQ TS                                                       
204500              QUEUE (ASC-SEL-QUEUE-NAME)                                  
204600              FROM  (CN007-TS-SEL-REC)                                    
204700              ITEM  (PV-SEL-QUEUE-ITEM)                                   
204800              RESP  (PV-CICS-RESP)                                        
204900     END-EXEC.                                                            
205000                                                                          
205100     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
205200         CONTINUE                                                         
205300     ELSE                                                                 
205400         SET DP013-CICS-ABEND      TO TRUE                                
205500         SET DP013-NO-ROLLBACK     TO TRUE                                
205600         MOVE '5205-WRITE-SEL-QUEUE'                                      
205700                                   TO DP013-PARAGRAPH                     
205800         MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'                  
205900                                   TO DP013-MESSAGE-TEXT(1)               
206000         PERFORM DP013-0000-PROCESS-ABEND                                 
206100     END-IF.                                                              
206200                                                                          
206300/                                                                         
206400*----------------------------------------------------------------*        
206500*  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *        
206600*  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *        
206700*  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *        
206800*  THE USER.                                                     *        
206900*----------------------------------------------------------------*        
207000 6000-REFRESH-PANEL-FROM-TEMP.                                            
207100                                                                          
207200     IF  ASC-UPDATE-TSQ                                                   
207300         SET ASC-NO-TSQ-UPDATE     TO TRUE                                
207400         MOVE ZERO                 TO PV-SAVE-ITEM                        
207500                                      PV-SEL-QUEUE-ITEM                   
207600         MOVE ASC-NUMBER-OF-SELECTED-ITEMS                                
207700                                   TO PV-HOLD-SELECTED-ITEMS              
207800         PERFORM 6100-UPDATE-TSQ                                          
207900             UNTIL PV-ITEMS-PROCESSED >                                   
208000                                     ASC-NUMBER-OF-SELECTED-ITEMS         
208100         PERFORM 6010-DELETE-SEL-QUEUE                                    
208200         INITIALIZE ASC-BLOCK-ENTRIES (1)                                 
208300                    ASC-BLOCK-ENTRIES (2)                                 
208400     END-IF.                                                              
208500                                                                          
208600/                                                                         
208700*----------------------------------------------------------------*        
208800*    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *        
208900*----------------------------------------------------------------*        
209000 6010-DELETE-SEL-QUEUE.                                                   
209100                                                                          
209200     EXEC CICS                                                            
209300         DELETEQ TS                                                       
209400             QUEUE (ASC-SEL-QUEUE-NAME)                                   
209500             RESP  (PV-CICS-RESP)                                         
209600     END-EXEC.                                                            
209700                                                                          
209800     IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                              
209900         (PV-CICS-RESP = DFHRESP(QIDERR)))                                
210000         CONTINUE                                                         
210100     ELSE                                                                 
210200         SET DP013-CICS-ABEND      TO TRUE                                
210300         SET DP013-NO-ROLLBACK     TO TRUE                                
210400         MOVE '6010-DELETE-SEL-QUEUE'                                     
210500                                   TO DP013-PARAGRAPH                     
210600         MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'                 
210700                                   TO DP013-MESSAGE-TEXT (1)              
210800         PERFORM DP013-0000-PROCESS-ABEND                                 
210900     END-IF.                                                              
211000                                                                          
211100/                                                                         
211200*----------------------------------------------------------------*        
211300*    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
211400*    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
211500*    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
211600*    THE TEMP QUEUE.                                             *        
211700*----------------------------------------------------------------*        
211800                                                                          
211900 6100-UPDATE-TSQ.                                                         
212000                                                                          
212100     ADD 1                         TO PV-SEL-QUEUE-ITEM.                  
212200     PERFORM 6105-READ-SEL-QUEUE.                                         
212300                                                                          
212400     PERFORM                                                              
212500         VARYING CN007-IDX                                                
212600         FROM 1 BY 1                                                      
212700         UNTIL ((PV-ITEMS-PROCESSED >                                     
212800                              PV-HOLD-SELECTED-ITEMS)                     
212900         OR (CN007-IDX GREATER THAN CN007-MAX-ITEMS))                     
213000         IF  CN007-ACTION-SW (CN007-IDX) > SPACE                          
213100             PERFORM 6110-PROCESS-ITEMS                                   
213200         END-IF                                                           
213300         ADD +1                    TO PV-ITEMS-PROCESSED                  
213400     END-PERFORM.                                                         
213500                                                                          
213600     PERFORM 6111-WRITE-LIST-QUEUE.                                       
213700                                                                          
213800/                                                                         
213900*----------------------------------------------------------------*        
214000*    READ THE LIST QUEUE RECORD.                                 *        
214100*----------------------------------------------------------------*        
214200                                                                          
214300 6105-READ-SEL-QUEUE.                                                     
214400                                                                          
214500     EXEC CICS                                                            
214600          READQ TS                                                        
214700              QUEUE (ASC-SEL-QUEUE-NAME)                                  
214800              INTO  (CN007-TS-SEL-REC)                                    
214900              ITEM  (PV-SEL-QUEUE-ITEM)                                   
215000              RESP  (PV-CICS-RESP)                                        
215100     END-EXEC.                                                            
215200                                                                          
215300     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
215400         CONTINUE                                                         
215500     ELSE                                                                 
215600         SET DP013-CICS-ABEND      TO TRUE                                
215700         SET DP013-NO-ROLLBACK     TO TRUE                                
215800         MOVE '6105-READ-SEL-QUEUE'                                       
215900                                   TO DP013-PARAGRAPH                     
216000         MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'                   
216100                                   TO DP013-MESSAGE-TEXT(1)               
216200         PERFORM DP013-0000-PROCESS-ABEND                                 
216300     END-IF.                                                              
216400                                                                          
216500/                                                                         
216600*----------------------------------------------------------------*        
216700*    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
216800*    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
216900*    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
217000*----------------------------------------------------------------*        
217100                                                                          
217200 6110-PROCESS-ITEMS.                                                      
217300                                                                          
217400     COMPUTE PV-ITEM = 1 + ((CN007-ITEM (CN007-IDX) - 1)                  
217500                     / PC-ITEMS-PER-PANEL).                               
217600                                                                          
217700     IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
217800         IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
217900             PERFORM 6111-WRITE-LIST-QUEUE                                
218000         END-IF                                                           
218100         MOVE PV-ITEM              TO PV-SAVE-ITEM                        
218200                                                                          
218300         PERFORM 6112-READ-LIST-QUEUE                                     
218400         IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                       
218500             CONTINUE                                                     
218600         END-IF                                                           
218700     END-IF.                                                              
218800                                                                          
218900     COMPUTE PV-SUB = (CN007-ITEM (CN007-IDX)                             
219000                    - LQW-LIST-ITEM-NUMBER (1)) + 1.                      
219100                                                                          
219200     SET LQW-DESELECT (PV-SUB)     TO TRUE.                               
219300     MOVE LQW-SELECTED-SW (PV-SUB) TO LQW-TEMP-SELECTED-SW                
219400                                     (PV-SUB).                            
219500     EVALUATE TRUE                                                        
219600         WHEN CN007-INQ (CN007-IDX)                                       
219700              MOVE PC-INQ          TO LQW-ACTION (PV-SUB)                 
219800                                                                          
219900         WHEN CN007-UPD (CN007-IDX)                                       
220000              MOVE PC-UPD          TO LQW-ACTION (PV-SUB)                 
220100                                                                          
220200         WHEN CN007-DEL (CN007-IDX)                                       
220300              MOVE PC-DEL          TO LQW-ACTION (PV-SUB)                 
220400                                                                          
220500         WHEN CN007-ERR (CN007-IDX)                                       
220600              MOVE PC-ERR          TO LQW-ACTION (PV-SUB)                 
220700     END-EVALUATE.                                                        
220800                                                                          
220900     SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                        
221000                                                                          
221100/                                                                         
221200*----------------------------------------------------------------*        
221300*    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
221400*----------------------------------------------------------------*        
221500                                                                          
221600 6111-WRITE-LIST-QUEUE.                                                   
221700                                                                          
221800     EXEC CICS                                                            
221900         WRITEQ TS                                                        
222000             QUEUE (ASC-LIST-QUEUE-NAME)                                  
222100             FROM  (LQW-LIST-QUEUE-WORK-AREA)                             
222200             ITEM  (PV-SAVE-ITEM)                                         
222300             REWRITE                                                      
222400             RESP  (PV-CICS-RESP)                                         
222500     END-EXEC.                                                            
222600                                                                          
222700     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
222800         CONTINUE                                                         
222900     ELSE                                                                 
223000         SET DP013-CICS-ABEND      TO TRUE                                
223100         SET DP013-NO-ROLLBACK     TO TRUE                                
223200         MOVE '6111-WRITE-LIST-QUEUE'                                     
223300                                   TO DP013-PARAGRAPH                     
223400         MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                          
223500                                   TO DP013-MESSAGE-TEXT(1)               
223600         PERFORM DP013-0000-PROCESS-ABEND                                 
223700     END-IF.                                                              
223800                                                                          
223900/                                                                         
224000*----------------------------------------------------------------*        
224100*    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
224200*----------------------------------------------------------------*        
224300                                                                          
224400 6112-READ-LIST-QUEUE.                                                    
224500                                                                          
224600     EXEC CICS                                                            
224700         READQ TS                                                         
224800             QUEUE (ASC-LIST-QUEUE-NAME)                                  
224900             INTO  (LQW-LIST-QUEUE-WORK-AREA)                             
225000             ITEM  (PV-ITEM)                                              
225100             RESP  (PV-CICS-RESP)                                         
225200     END-EXEC.                                                            
225300                                                                          
225400     IF  PV-CICS-RESP = DFHRESP (NORMAL)                                  
225500         CONTINUE                                                         
225600     ELSE                                                                 
225700         SET DP013-CICS-ABEND      TO TRUE                                
225800         SET DP013-NO-ROLLBACK     TO TRUE                                
225900         MOVE '6112-READ-LIST-QUEUE'                                      
226000                                   TO DP013-PARAGRAPH                     
226100         MOVE 'ATTEMPTING TO READQ LIST QUEUE'                            
226200                                   TO DP013-MESSAGE-TEXT(1)               
226300         PERFORM DP013-0000-PROCESS-ABEND                                 
226400     END-IF.                                                              
226500/                                                                         
226600 9900-SOFT-ABORT.                                                         
226700     SET PS-ERROR         TO TRUE.                                        
226800     SET DP020-MSG-FATAL  TO TRUE.                                        
226900     MOVE 'THROUGH SOFT-ABORT PARA'                                       
227000                      TO DP013-MESSAGE-TEXT (1)                           
227100     MOVE PC-TSYMSG-00551 TO DP020-MSG-NUMBER.                            
227200     IF PS-NO-NEXT-SCREEN                                                 
227300        SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                             
227400     END-IF.                                                              
227500     PERFORM 9901-BACKUP.                                                 
227600     MOVE 'PROGRAM EXIT CALL' TO DP013-MESSAGE-TEXT (1).                  
227700     PERFORM 0001-CALL-CICS-ARCH-API.                                     
227800                                                                          
227900 9901-BACKUP.                                                             
228000     EXEC CICS SYNCPOINT                                                  
228100          ROLLBACK                                                        
228200     END-EXEC.                                                            
228300                                                                          
228400/                                                                         
228500*----------------------------------------------------------------*        
228600*    ABEND PROCESSOR MODULE                                      *        
228700*----------------------------------------------------------------*        
228800                                                                          
228900     COPY DPPD013.                                                        
