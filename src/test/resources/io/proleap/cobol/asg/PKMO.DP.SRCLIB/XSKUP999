       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XSKUP999.                                                  
       AUTHOR.       COGNIZANT.                                                 
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 09/25/2012.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS IS A DATA CORRECTION PROGRAM WHICH WILL UPDATE            *        
      * XST_SKU_LOC TABLE THAT HAS MISMATCH RECORDS SEEN IN XSK07505   *        
      * INTEGRITY REPORT.                                              *        
C91027******************************************************************        
C91027* CHANGE HISTORY:                                                         
C91027* WR/PBI         DATE              DESCRIPTION OF CHANGES                 
C91027* -------     ----------  ----------------------------------------        
C91027******************************************************************        
C91027* CHG0091027  05/21/14    CHANGED THE LAYOUT OF THE COPYBOOK              
C91027*                         XSRD999. CHANEGD THE POSITION OF STR            
C91027*                         NBR.                                            
C91027******************************************************************00279499
TESTDH*             03/14/15    MODIFIED PROGRAM TO COMMIT THE CHANGES          
TESTDH*                         FOR EVERY 10K RECORDS.                          
      ******************************************************************        
C07650* CHG0107650  03/19/15    MODIFIED PROGRAM TO COMMIT THE CHANGES          
C07650*                         FOR EVERY 5K RECORDS.                           
C07650******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT SKU-LOC-ERR-FILE ASSIGN TO INP01.                             
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  SKU-LOC-ERR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-LOC-ERROR-RECORD             PIC X(201).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-UPCPLS-EFF-BEG-TMST        PIC X(26)  VALUE SPACES.            
           05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.            
           05 PV-SQLCODE-DISPLAY            PIC +(8)9  VALUE ZEROES.            
           05 PV-DB2-OPERATION-ATTEMPTED    PIC X(30)  VALUE SPACES.            
           05 PV-BEGN-DT-ERR                PIC X(18)  VALUE                    
                                            'MISMATCH  BEGIN DT'.               
           05 PV-DATA-ERR                   PIC X(18)  VALUE                    
                                            'MISMATCH          '.               
TESTDH     05 PV-COMMIT-COUNT               PIC 9(04)  VALUE ZEROES.            
TESTDH     05 PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.           
           05 PV-UPC-ID                     PIC S9(10) VALUE ZEROES.            
           05 PV-INTERNAL-UPC-ID            PIC S9(10) COMP VALUE ZEROS.        
           05 PV-TEMP-STR-NBR               PIC  9(04) COMP VALUE ZEROS.        
           05 PV-STR-NBR                    PIC S9(04) COMP VALUE ZEROS.        
           05 PV-EFF-END-TMST-IND           PIC S9(04) VALUE +0 COMP.   00310003
           05 PV-ORIG-CLR-DTE               PIC S9(04) VALUE +0 COMP.   00310003
           05 PV-MKDN-CDE                   PIC S9(04) VALUE +0 COMP.   00310003
C91027     05 PV-STORE-NBR-1                PIC  X(04) VALUE SPACES.    00310003
C91027     05 PV-STORE-NBR-2                PIC  X(04) VALUE SPACES.    00310003
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                  PIC X(01)  VALUE 'N'.       00280003
               88  PS-DATA-ERROR                       VALUE 'Y'.       00290003
               88  PS-NO-DATA-ERROR                    VALUE 'N'.       00300003
           05  PS-BEG-DT-SW                 PIC X(01)  VALUE 'N'.       00280003
               88  PS-BEG-DT-ERROR                     VALUE 'Y'.       00290003
               88  PS-NO-BEG-DT-ERROR                  VALUE 'N'.       00300003
           05  PS-SOR-DATA-SW               PIC X(01)  VALUE 'N'.       00280003
               88  PS-SOR-DATA-FOUND                   VALUE 'Y'.       00290003
               88  PS-SOR-DATA-NOT-FOUND               VALUE 'N'.       00300003
           05  PS-XS-DATA-SW                PIC X(01)  VALUE 'N'.       00280003
               88  PS-XS-DATA-FOUND                    VALUE 'Y'.       00290003
               88  PS-XS-DATA-NOT-FOUND                VALUE 'N'.       00300003
           05  PV-MKDN-CDE-IND              PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-MKDN-CDE-NULL                    VALUE -1.        00320003
               88  PV-MKDN-CDE-VALUE                   VALUE  0.        00320003
           05  PS-END-OF-SOR-SKU-LOC-SW     PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-SOR-SKU-LOC               VALUE 'Y'.               
               88  PS-NOT-END-OF-SOR-SKU-LOC           VALUE 'N'.               
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-SKU-LOC-ROWS-READ         PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-ROWS-CLOSED       PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-ROWS-INSERTED     PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-ROWS-UPDATED      PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
TESTDH     05  PA-COMMIT-TAKEN              PIC S9(09) COMP-3                   
TESTDH                                                 VALUE ZEROES.            
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE 0.                 
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.             
           88  ABEND-4002-CC-INVALID-ERR               VALUE +4002.             
                                                                                
TESTDH*****************************************************************         
TESTDH* PROGRAM CONSTANTS                                                       
TESTDH*****************************************************************         
TESTDH 01  PC-PROGRAM-CONSTANTS.                                                
TESTDH     05  PC-CURRENT-PROGRAM-NAME       PIC X(08)                          
TESTDH                                             VALUE 'XSKUP999'.            
TESTDH     05  PC-MAX-RETRIES                PIC S9(04) VALUE +5 COMP.          
C07650     05  PC-COMMIT-MAX                 PIC 9(09) VALUE 4999.              
TESTDH                                                                          
      *****************************************************************         
      * XST_SKU_LOC TABLE DCLGEN.                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XST00008                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TUPCPLS TABLE DCLGEN.                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * XS DOMAIN SKU LOC MISMATCH ERROR REPORT FILE LAYOUT.                    
      *****************************************************************         
           COPY XSRD999.                                                        
                                                                                
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 4000-READ-SKU-LOC-ERROR-FILE.                                
                                                                                
           PERFORM 2000-PROCESS-EXTRA-RECORDS                                   
             UNTIL PS-END-OF-SOR-SKU-LOC.                                       
                                                                                
           PERFORM 3000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           INITIALIZE DCLXST00008                                               
                      DCLTUPCPLS.                                               
                                                                                
           OPEN INPUT SKU-LOC-ERR-FILE.                                         
                                                                                
      ******************************************************************        
      * 2000-PROCESS-SKU-LOC-RECS.                                              
      ******************************************************************        
       2000-PROCESS-EXTRA-RECORDS.                                              
                                                                                
           SET PS-NO-DATA-ERROR      TO TRUE.                                   
           SET PS-NO-BEG-DT-ERROR    TO TRUE.                                   
                                                                                
           INITIALIZE DCLXST00008                                               
                      DCLTUPCPLS.                                               
                                                                                
           MOVE XSRD999-UPC-ID       TO PV-UPC-ID.                              
           MOVE PV-UPC-ID            TO PV-INTERNAL-UPC-ID.                     
C91027*--  INPUT REPORT FILE HAVE FIVE DIGIT ALPHA NUMERIC STORE NUMBER         
C91027*--  STORE NUMBER 0 IS RIGHT JUSTIFIED AND OTHER STORE NUMBERS            
C91027*--  ARE LEFT JUSTIFIED. CHANGED THE COPYBOOK LAYOUT TO MOVE THE          
C91027*--  CORRECT STORE NUMBER. BELOW CODE IS TO HANDLE STORE NUMBER           
C91027*--  ZERO.                                                                
C91027     INITIALIZE                       PV-STORE-NBR-1                      
C91027                                      PV-STORE-NBR-2.                     
C91027     UNSTRING XSRD999-STR-NBR    DELIMITED BY ALL SPACE                   
C91027                                 INTO PV-STORE-NBR-1                      
C91027                                      PV-STORE-NBR-2.                     
C91027     IF PV-STORE-NBR-1 IS NUMERIC                                         
C91027        MOVE PV-STORE-NBR-1    TO PV-TEMP-STR-NBR                         
C91027     ELSE                                                                 
C91027        MOVE PV-STORE-NBR-2    TO PV-TEMP-STR-NBR                         
C91027     END-IF.                                                              
           MOVE PV-TEMP-STR-NBR      TO PV-STR-NBR.                             
                                                                                
           EVALUATE XSRD999-ERR-MSG                                             
             WHEN PV-BEGN-DT-ERR                                                
               SET PS-BEG-DT-ERROR   TO TRUE                                    
               PERFORM 2100-PROCESS-SKU                                         
             WHEN PV-DATA-ERR                                                   
               SET PS-DATA-ERROR     TO TRUE                                    
               PERFORM 2100-PROCESS-SKU                                         
             WHEN OTHER                                                         
               DISPLAY 'ERROR MESSAGE N/A: ' XSRD999-ERR-MSG                    
               DISPLAY 'FOR UPC ID       : ' PV-INTERNAL-UPC-ID                 
               DISPLAY 'FOR STORE NO     : ' PV-STR-NBR                         
               DISPLAY ' '                                                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4000-READ-SKU-LOC-ERROR-FILE.                                
                                                                                
      ******************************************************************        
      * 2100-PROCESS-SKU.                                                       
      ******************************************************************        
       2100-PROCESS-SKU.                                                        
                                                                                
           PERFORM 2110-SELECT-TUPCPLS.                                         
           PERFORM 2120-SELECT-XST-SKU-LOC.                                     
                                                                                
           IF  PS-SOR-DATA-FOUND                                                
           AND PS-XS-DATA-FOUND                                                 
               IF  UPCPLS-EFF-BEG-TMST     = XST00008-BEG-TMST                  
               AND UPCPLS-UNT-RTL-AMT      = XST00008-UNT-RTL-AMT               
               AND UPCPLS-UNT-COST-AMT     = XST00008-UNT-COST-AMT              
               AND UPCPLS-UPC-STAT-CDE     = XST00008-LOC-SKU-STAT-CDE          
               AND UPCPLS-UPC-STAT-CHG-DTE = XST00008-UPC-STAT-CHG-DTE          
               AND UPCPLS-UNT-RTL-CHG-DTE  = XST00008-UNT-RTL-CHG-DTE           
               AND UPCPLS-MKDN-CDE         = XST00008-MKDN-CDE                  
                  CONTINUE                                                      
               ELSE                                                             
                  MOVE UPCPLS-UPC-ID       TO XST00008-INTR-UPC-ID              
                  MOVE UPCPLS-STR-NBR      TO XST00008-LOC-NBR                  
                  MOVE UPCPLS-UNT-RTL-AMT  TO XST00008-UNT-RTL-AMT              
                  MOVE UPCPLS-UNT-COST-AMT TO XST00008-UNT-COST-AMT             
                  MOVE UPCPLS-UPC-STAT-CDE TO XST00008-LOC-SKU-STAT-CDE         
                  MOVE UPCPLS-UPC-STAT-CHG-DTE                                  
                                           TO XST00008-UPC-STAT-CHG-DTE         
                  MOVE UPCPLS-UNT-RTL-CHG-DTE                                   
                                           TO XST00008-UNT-RTL-CHG-DTE          
                  MOVE UPCPLS-CRE-TMST     TO XST00008-SOR-LAST-MSG-TMST        
                  MOVE UPCPLS-MKDN-CDE     TO XST00008-MKDN-CDE                 
                                                                                
                  IF  UPCPLS-EFF-BEG-TMST = XST00008-BEG-TMST                   
                      MOVE UPCPLS-EFF-BEG-TMST  TO XST00008-BEG-TMST            
                      PERFORM 2140-UPDATE-SKU-LOC                               
                  ELSE                                                          
                      MOVE UPCPLS-EFF-BEG-TMST  TO XST00008-BEG-TMST            
                      PERFORM 2120-CLOSE-CURRENT-XS-ROW                         
                      PERFORM 2130-INSERT-SKU-LOC                               
                  END-IF                                                        
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 2110-SELECT-TUPCPLS.                                                    
      ******************************************************************        
       2110-SELECT-TUPCPLS.                                                     
                                                                                
           SET PS-SOR-DATA-NOT-FOUND TO TRUE.                                   
                                                                                
               EXEC SQL                                                         
                   SELECT UPC_ID                                                
                         ,OPERCO_NBR                                            
                         ,STR_NBR                                               
                         ,EFF_BEG_TMST                                          
                         ,EFF_END_TMST                                          
                         ,UPC_STAT_CDE                                          
                         ,UNT_COST_AMT                                          
                         ,UNT_RTL_AMT                                           
                         ,MEITGP_NBR                                            
                         ,CRE_BY_USR_ID                                         
                         ,CRE_TMST                                              
                         ,ITM_NBR                                               
                         ,PLU_IND                                               
                         ,UPC_STAT_CHG_DTE                                      
                         ,UNT_RTL_CHG_DTE                                       
                         ,ORIG_CLR_DTE                                          
                         ,MKDN_CDE                                              
                     INTO :UPCPLS-UPC-ID                                        
                         ,:UPCPLS-OPERCO-NBR                                    
                         ,:UPCPLS-STR-NBR                                       
                         ,:UPCPLS-EFF-BEG-TMST                                  
                         ,:UPCPLS-EFF-END-TMST:PV-EFF-END-TMST-IND              
                         ,:UPCPLS-UPC-STAT-CDE                                  
                         ,:UPCPLS-UNT-COST-AMT                                  
                         ,:UPCPLS-UNT-RTL-AMT                                   
                         ,:UPCPLS-MEITGP-NBR                                    
                         ,:UPCPLS-CRE-BY-USR-ID                                 
                         ,:UPCPLS-CRE-TMST                                      
                         ,:UPCPLS-ITM-NBR                                       
                         ,:UPCPLS-PLU-IND                                       
                         ,:UPCPLS-UPC-STAT-CHG-DTE                              
                         ,:UPCPLS-UNT-RTL-CHG-DTE                               
                         ,:UPCPLS-ORIG-CLR-DTE:PV-ORIG-CLR-DTE                  
                         ,:UPCPLS-MKDN-CDE:PV-MKDN-CDE                          
                   FROM TUPCPLS                                                 
                     WHERE UPC_ID  = :PV-INTERNAL-UPC-ID                        
                       AND STR_NBR = :PV-STR-NBR                                
                       AND EFF_END_TMST IS NULL                                 
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-SOR-DATA-FOUND     TO TRUE                       
                    WHEN SQLCODE = +100                                         
                        SET PS-SOR-DATA-NOT-FOUND TO TRUE                       
                        DISPLAY 'SOR UPC ID NOT FOUND - '                       
                                 PV-INTERNAL-UPC-ID ' - '                       
                                 PV-STR-NBR                                     
                        DISPLAY ' '                                             
                    WHEN OTHER                                                  
                        MOVE '2110-SELECT-TUPCPLS'                              
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT TUPCPLS TABLE'                             
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2120-SELECT-XST-SKU-LOC.                                                
      ******************************************************************        
       2120-SELECT-XST-SKU-LOC.                                                 
                                                                                
           SET PS-XS-DATA-NOT-FOUND TO TRUE.                                    
                                                                                
               EXEC SQL                                                         
                   SELECT INTR_UPC_ID                                           
                         ,LOC_NBR                                               
                         ,BEG_TMST                                              
                         ,UNT_RTL_AMT                                           
                         ,UNT_COST_AMT                                          
                         ,LOC_SKU_STAT_CDE                                      
                         ,UPC_STAT_CHG_DTE                                      
                         ,UNT_RTL_CHG_DTE                                       
                         ,SOR_LAST_MSG_TMST                                     
                         ,MKDN_CDE                                              
                     INTO :XST00008-INTR-UPC-ID                                 
                         ,:XST00008-LOC-NBR                                     
                         ,:XST00008-BEG-TMST                                    
                         ,:XST00008-UNT-RTL-AMT                                 
                         ,:XST00008-UNT-COST-AMT                                
                         ,:XST00008-LOC-SKU-STAT-CDE                            
                         ,:XST00008-UPC-STAT-CHG-DTE                            
                         ,:XST00008-UNT-RTL-CHG-DTE                             
                         ,:XST00008-SOR-LAST-MSG-TMST                           
                         ,:XST00008-MKDN-CDE :PV-MKDN-CDE                       
                   FROM XST_SKU_LOC                                             
                     WHERE INTR_UPC_ID  = :PV-INTERNAL-UPC-ID                   
                       AND LOC_NBR = :PV-STR-NBR                                
                       AND END_TMST IS NULL                                     
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-XS-DATA-FOUND     TO TRUE                        
                    WHEN SQLCODE = +100                                         
                        SET PS-XS-DATA-NOT-FOUND TO TRUE                        
                        DISPLAY 'XS UPC ID NOT FOUND - '                        
                                 PV-INTERNAL-UPC-ID ' - '                       
                                 PV-STR-NBR                                     
                        DISPLAY ' '                                             
                    WHEN OTHER                                                  
                        MOVE '2120-SELECT-XST-SKU-LOC'                          
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT XST_SKU_LOC TABLE'                         
                                     TO PV-DB2-OPERATION-ATTEMPTED              
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2120-CLOSE-CURRENT-XS-ROW.                                              
      ******************************************************************        
       2120-CLOSE-CURRENT-XS-ROW.                                               
                                                                                
           MOVE UPCPLS-EFF-BEG-TMST  TO PV-UPCPLS-EFF-BEG-TMST.                 
                                                                                
               EXEC SQL                                                         
                  UPDATE XST_SKU_LOC                                            
                     SET END_TMST   =                                           
                     TIMESTAMP(:PV-UPCPLS-EFF-BEG-TMST) - 1 MICROSECOND         
                     ,LAST_UPD_TMST = CURRENT TIMESTAMP                         
                  WHERE INTR_UPC_ID = :XST00008-INTR-UPC-ID                     
                    AND LOC_NBR     = :XST00008-LOC-NBR                         
                    AND END_TMST IS NULL                                        
               END-EXEC                                                         
                                                                                
            EVALUATE TRUE                                                       
              WHEN SQLCODE = ZERO                                               
                 ADD +1         TO PA-SKU-LOC-ROWS-CLOSED                       
TESTDH           ADD +1         TO PV-COMMIT-COUNT                              
TESTDH           IF PV-COMMIT-COUNT > PC-COMMIT-MAX                             
TESTDH              MOVE 0 TO PV-COMMIT-COUNT                                   
TESTDH              PERFORM 3500-UPDATE-COMMIT                                  
TESTDH           END-IF                                                         
              WHEN OTHER                                                        
                  MOVE '2120-CLOSE-CURRENT-XS-ROW'                              
                                TO PV-PARAGRAPH-NAME                            
                  MOVE 'UPDATE SKU LOC TABLE'                                   
                                TO PV-DB2-OPERATION-ATTEMPTED                   
                  PERFORM 9100-SQL-ABEND                                        
            END-EVALUATE.                                                       
                                                                                
      ******************************************************************        
      * 2130-INSERT-SKU-LOC.                                                    
      ******************************************************************        
       2130-INSERT-SKU-LOC.                                                     
                                                                                
            EXEC SQL                                                            
              INSERT INTO XST_SKU_LOC                                           
                  (INTR_UPC_ID                                                  
                  ,LOC_NBR                                                      
                  ,BEG_TMST                                                     
                  ,END_TMST                                                     
                  ,UNT_RTL_AMT                                                  
                  ,UNT_COST_AMT                                                 
                  ,LOC_SKU_STAT_CDE                                             
                  ,UPC_STAT_CHG_DTE                                             
                  ,UNT_RTL_CHG_DTE                                              
                  ,GP_PRIC_ID                                                   
                  ,GP_RTL_QTY                                                   
                  ,GP_RTL_AMT                                                   
                  ,LAST_UPD_TMST                                                
                  ,SOR_LAST_MSG_TMST                                            
                  ,MKDN_CDE                                                     
                  ,ORIG_CLR_DTE)                                                
              VALUES                                                            
                  (:XST00008-INTR-UPC-ID                                        
                  ,:XST00008-LOC-NBR                                            
                  ,:XST00008-BEG-TMST                                           
                  ,NULL                                                         
                  ,:XST00008-UNT-RTL-AMT                                        
                  ,:XST00008-UNT-COST-AMT                                       
                  ,:XST00008-LOC-SKU-STAT-CDE                                   
                  ,:XST00008-UPC-STAT-CHG-DTE                                   
                  ,:XST00008-UNT-RTL-CHG-DTE                                    
                  ,NULL                                                         
                  ,NULL                                                         
                  ,NULL                                                         
                  ,CURRENT TIMESTAMP                                            
                  ,:XST00008-SOR-LAST-MSG-TMST                                  
                  ,:XST00008-MKDN-CDE:PV-MKDN-CDE-IND                           
                  ,NULL)                                                        
            END-EXEC                                                            
                                                                                
            EVALUATE TRUE                                                       
              WHEN SQLCODE = ZERO                                               
               ADD +1       TO PA-SKU-LOC-ROWS-INSERTED                         
TESTDH         ADD +1         TO PV-COMMIT-COUNT                                
TESTDH         IF PV-COMMIT-COUNT > PC-COMMIT-MAX                               
TESTDH            MOVE 0 TO PV-COMMIT-COUNT                                     
TESTDH            PERFORM 3500-UPDATE-COMMIT                                    
TESTDH         END-IF                                                           
              WHEN OTHER                                                        
               MOVE '2130-INSERT-SKU-LOC'                                       
                             TO PV-PARAGRAPH-NAME                               
               MOVE 'INSERT SKU LOC TABLE'                                      
                             TO PV-DB2-OPERATION-ATTEMPTED                      
               PERFORM 9100-SQL-ABEND                                           
            END-EVALUATE.                                                       
                                                                                
      ******************************************************************        
      * 2140-UPDATE-SKU-LOC.                                                    
      ******************************************************************        
       2140-UPDATE-SKU-LOC.                                                     
                                                                                
               EXEC SQL                                                         
                   UPDATE XST_SKU_LOC                                           
                      SET BEG_TMST     = :XST00008-BEG-TMST                     
                     ,UNT_RTL_AMT      = :XST00008-UNT-RTL-AMT                  
                     ,UNT_COST_AMT     = :XST00008-UNT-COST-AMT                 
                     ,LOC_SKU_STAT_CDE = :XST00008-LOC-SKU-STAT-CDE             
                     ,UPC_STAT_CHG_DTE = :XST00008-UPC-STAT-CHG-DTE             
                     ,UNT_RTL_CHG_DTE  = :XST00008-UNT-RTL-CHG-DTE              
                     ,MKDN_CDE         = :XST00008-MKDN-CDE                     
                     ,LAST_UPD_TMST    = CURRENT TIMESTAMP                      
                   WHERE INTR_UPC_ID   = :XST00008-INTR-UPC-ID                  
                     AND LOC_NBR       = :XST00008-LOC-NBR                      
                     AND END_TMST      IS NULL                                  
               END-EXEC                                                         
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                 ADD +1  TO PA-SKU-LOC-ROWS-UPDATED                             
TESTDH           ADD +1         TO PV-COMMIT-COUNT                              
TESTDH           IF PV-COMMIT-COUNT > PC-COMMIT-MAX                             
TESTDH              MOVE 0 TO PV-COMMIT-COUNT                                   
TESTDH              PERFORM 3500-UPDATE-COMMIT                                  
TESTDH           END-IF                                                         
               WHEN OTHER                                                       
                 MOVE '2140-UPDATE-SKU-LOC'                                     
                         TO PV-PARAGRAPH-NAME                                   
                 MOVE 'UPDATE SKU LOC TABLE'                                    
                         TO PV-DB2-OPERATION-ATTEMPTED                          
                 PERFORM 9100-SQL-ABEND                                         
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 3000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       3000-EOJ-ROUTINE.                                                        
                                                                                
TESTDH     PERFORM 3500-UPDATE-COMMIT.                                          
TESTDH                                                                          
           CLOSE SKU-LOC-ERR-FILE.                                              
                                                                                
           PERFORM 9005-DISPLAY-TOTALS.                                         
                                                                                
TESTDH******************************************************************        
TESTDH* 3500-UPDATE-COMMIT.                                                     
TESTDH******************************************************************        
TESTDH 3500-UPDATE-COMMIT.                                                      
TESTDH                                                                          
TESTDH     PERFORM                                                              
TESTDH         WITH TEST AFTER                                                  
TESTDH         VARYING PV-SQL-SUB                                               
TESTDH         FROM 1 BY 1                                                      
TESTDH         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
TESTDH                   OR  SQLCODE = 0)                                       
TESTDH                                                                          
TESTDH         EXEC SQL                                                         
TESTDH             COMMIT                                                       
TESTDH         END-EXEC                                                         
TESTDH                                                                          
TESTDH         EVALUATE TRUE                                                    
TESTDH             WHEN SQLCODE = -904                                          
TESTDH             WHEN SQLCODE = -911                                          
TESTDH             WHEN SQLCODE = -913                                          
TESTDH             WHEN SQLCODE = 0                                             
TESTDH                 ADD +1 TO PA-COMMIT-TAKEN                                
TESTDH                 CONTINUE                                                 
TESTDH             WHEN SQLWARN NOT EQUAL SPACES                                
TESTDH             WHEN SQLCODE NOT EQUAL ZERO                                  
TESTDH                 MOVE '3500-UPDATE-COMMIT'                                
TESTDH                               TO  PV-PARAGRAPH-NAME                      
TESTDH                 MOVE 'COMMIT FAILURE'                                    
TESTDH                              TO  PV-DB2-OPERATION-ATTEMPTED              
TESTDH                 PERFORM 9100-SQL-ABEND                                   
TESTDH         END-EVALUATE                                                     
TESTDH     END-PERFORM.                                                         
TESTDH                                                                          
TESTDH     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
TESTDH        MOVE '3500-UPDATE-COMMIT'                                         
TESTDH                              TO  PV-PARAGRAPH-NAME                       
TESTDH        MOVE 'COMMIT RETRIES EXCEEDED'                                    
TESTDH                              TO  PV-DB2-OPERATION-ATTEMPTED              
TESTDH        PERFORM 9100-SQL-ABEND                                            
TESTDH     END-IF.                                                              
TESTDH                                                                          
      ******************************************************************        
      * 4000-READ-SKU-LOC-ERROR-FILE.                                           
      ******************************************************************        
       4000-READ-SKU-LOC-ERROR-FILE.                                            
                                                                                
           READ SKU-LOC-ERR-FILE                                                
           INTO XSRD999-SKU-LOC-ERROR-FILE                                      
             AT END                                                             
               SET PS-END-OF-SOR-SKU-LOC TO TRUE                                
             NOT AT END                                                         
               ADD +1                    TO  PA-SKU-LOC-ROWS-READ               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 9100-SQL-ABEND.                                                         
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY ' '.                                                         
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = XSKUP999'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * 9005-DISPLAY-TOTALS.                                                    
      ******************************************************************        
       9005-DISPLAY-TOTALS.                                                     
                                                                                
           DISPLAY '******** XSKUP999 TOTALS *********'.                        
           DISPLAY ' SKU-LOC RECS READ    : ' PA-SKU-LOC-ROWS-READ.             
           DISPLAY ' SKU-LOC ROWS UPDATED : ' PA-SKU-LOC-ROWS-UPDATED.          
           DISPLAY ' SKU-LOC ROWS CLOSED  : ' PA-SKU-LOC-ROWS-CLOSED.           
           DISPLAY ' SKU-LOC ROWS INSERTED: ' PA-SKU-LOC-ROWS-INSERTED.         
TESTDH     DISPLAY ' NUMBER OF COMMIT TAKN: ' PA-COMMIT-TAKEN.                  
           DISPLAY '***** END OF XSKUP999 TOTALS *****'.                        
           DISPLAY ' '.                                                         
