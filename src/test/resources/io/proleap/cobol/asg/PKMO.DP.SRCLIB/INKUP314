       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKUP314                                                  
       AUTHOR.        JULIE SEIDLER.                                            
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  9-24-99.                                                  
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUP314 - INVENTORY LEDGER WEEKLY SHRINK RECALC                        
      ******************************************************************        
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TINSHNK *        
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *        
      **                                                              **        
W33304* TINCNTL(OPN FSCL MTH) WILL BE USED TO DETERMINE WHICH MONTH'S *         
      *    ROW WILL BE UPDATED/RECALCULATED                                     
      ******************************************************************        
W34517******************************************************************00200036
W34517*  CHANGE LOG                                                     00210036
W34517*  DATE       CHANGED BY    DESC                                  00220036
W34517*  ---------- ------------- --------------------------------------00230036
W34517*  08-13-2008 S. BARTELT    W34517 CHANGE TABLE ACCESS FROM       00240036
W34517*                           TINSHNK TO INT_INV_SHRK               00250036
W34517*                                                                 00270036
W33304*  10-28-2008 VIGNESH/      W33304 HOLDING INVENTORY LEDGER OPEN          
W33304*              INFOSYS      (HILO).                                       
C14666*                                                                 00270036
C14666*  07-10-2012 J SELWITSCHKA CRQ000000014666                               
C14666*                         - REVISE SHRINK ACCRUAL RATE PROCESS.           
C14666*                           CHANGES TO HANDLE ECOM, EXPANDED AND          
C14666*                           NEGATIVE SHRINK PERCENTAGES.                  
C14666*                         - COMMENTED 2 DISPLAY STMTS IN                  
C14666*                           PARA 2050-REFORMAT-MONTH AT THE               
C14666*                           REQUEST OF SERVICE DELIVERY                   
W34517******************************************************************00200036
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PA-PROGRAM-ACCUMULATOR.                                              
           05  PA-DEADLOCK-COUNT               PIC  9(04) VALUE ZEROES.         
           05  PA-DEPTS-PROCESSED              PIC  9(09) VALUE ZEROES.         
           05  PA-COMMIT-COUNT                 PIC  9(09) VALUE ZEROES.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-EMPTY-SW        PIC X     VALUE 'N'.             
               88  PS-EMPTY-CONTROL-FILE                 VALUE 'Y'.             
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.             
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.             
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
           05  PS-RECALC-CURSOR-SW             PIC X     VALUE 'N'.             
               88  PS-END-OF-RECALC-CURSOR               VALUE 'Y'.             
           05  PS-DEPT-SHRINK-PCT-SW           PIC X     VALUE 'N'.             
               88  PS-DEPT-SHRINK-RETRIEVED              VALUE 'Y'.             
           05  PS-CURRENT-PURCH-COST-SW        PIC X     VALUE 'N'.             
               88  PS-NO-CURRENT-PURCH-COSTS             VALUE 'Y'.             
           05  PS-PROGRAM-DEADLOCK-SW          PIC X     VALUE 'N'.             
               88  PS-PROGRAM-DEADLOCKED                 VALUE 'Y'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'INK219'.            
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'INKUP314'.          
           05  PC-MAX-DEADLOCKS        PIC S9(03)    VALUE +3.                  
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                'CONTROL CARD IS EMPTY        '.                                
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-06                       PIC X(30) VALUE                  
W34517          'ERROR ON UPDTE OF INT_INV_SHRK'.                               
           05  PE-MSG-07                       PIC X(50) VALUE                  
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-08                       PIC X(50) VALUE                  
                'UPDATE WORK_STRG - TCKRSTINF '.                                
           05  PE-MSG-09                       PIC X(50) VALUE                  
                'PGM ABEND DURING A COMMIT    '.                                
           05  PE-MSG-10                       PIC X(50) VALUE                  
                'UPDATE OF TCKRSTCNTL FAILED  '.                                
           05  PE-MSG-11                       PIC X(50) VALUE                  
                'ERROR IN SELECT - TCKRSTINF  '.                                
           05  PE-MSG-12                       PIC X(50) VALUE                  
                'MAXIMUM DEADLOCK CNT EXCEEDED'.                                
           05  PE-MSG-13                       PIC X(50) VALUE                  
                'UNABLE TO OBTAIN TIMESTAMP   '.                                
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-RETRY-COUNT               PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +3.                
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES.         
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES.         
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES.         
           05  PV-TINSHNK-UPDATE-VARIABLES.                                     
               10  PV-HOLD-FISCAL-WK-END-DTE   PIC  X(10) VALUE SPACES.         
               10  PV-HOLD-FISCAL-MN-END-DTE   PIC  X(10) VALUE SPACES.         
               10  PV-HOLD-DEPT-NBR            PIC  X(3)  VALUE SPACES.         
               10  PV-HOLD-SUB-CL-NBR          PIC  X(02) VALUE SPACES.         
               10  PV-HOLD-STR-NBR             PIC  S9(04) COMP.                
C14666         10  PV-HOLD-E-BUS-IND           PIC  X(01) VALUE SPACES.         
C14666     05  PV-DEPT-SHRINK-PCT              PIC  SV9(4) COMP-3.              
W33304     05  PV-MIN-INCNTL-OPN-FSCL-MN-DTE   PIC X(10) VALUE SPACES.          
W33304     05  PV-MAX-INCNTL-OPN-FSCL-MN-DTE   PIC X(10) VALUE SPACES.          
W33304     05  PV-MIN-INCNTL-OPN-FSCL-MN-NBR   PIC X(02) VALUE SPACES.          
W33304     05  PV-MAX-INCNTL-OPN-FSCL-MN-NBR   PIC X(02) VALUE SPACES.          
W33304     05  PV-NULL                         PIC S9(04)  VALUE +0        CL*34
W33304                                                      COMP SYNC.     CL*34
C14666     05  PV-E-BUS-NULL-IND               PIC S9(04)  VALUE +0        CL*34
C14666                                                      COMP SYNC.     CL*34
                                                                                
      * REFORMAT CALENDAR NUMBER TO PIC 9 FIELD                                 
       01  REFORMAT-ALPHA-CALENDAR.                                             
           05  FMT-CALENDAR-MO-NUMERIC          PIC 9(2)  VALUE ZEROES.         
                                                                                
      * TABLE TO CONTAIN DEPT SHRINK AMOUNTS                                    
       01  SA-DPTSHK-ROW.                                                       
           05  FILLER                              PIC S9(6)V  COMP-3.          
           05  FILLER                              PIC  X(4).                   
           05  FILLER                              PIC  X(26).                  
           05  SA-DPTSHK-SHRINK-PCTS OCCURS 12 TIMES.                           
C14666         10  SA-DPTSHK-SHRINK-PCT            PIC SV9(4)  COMP-3.          
       01  SA-DPTSHK-SUB                           PIC S9(4) VALUE ZERO         
                                                             COMP SYNC.         
      ****************************************************                      
W33304*  DB2 INVENTORY CONTROL TABLE                     *                      
      ****************************************************                      
           EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
W33304****************************************************                      
W33304*  DB2 TINVPAR TABLE                               *                      
W33304****************************************************                      
W33304     EXEC SQL                                                             
W33304         INCLUDE TINVPAR                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      ****************************************************                      
      *  DB2 MERCHANDISE AREA TABLE - BUSINESS STRUCTURE *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *  DEPARTMENT SHRINK TABLE                         *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDPTSHK                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ****************************************************                      
      *  INVENTORY WEEKLY SHRINK TABLE                   *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TINSHNK                                                  
           END-EXEC.                                                            
W34517****************************************************                      
W34517*  BRAND VENDOR INVENTORY WEEKLY SHRINK TABLE      *                      
W34517****************************************************                      
W34517     EXEC SQL                                                             
W34517         INCLUDE INT00006                                                 
W34517     END-EXEC.                                                            
C14666****************************************************                      
C14666*  DB2 LOCATION TABLE                              *                      
C14666****************************************************                      
C14666     EXEC SQL                                                             
C14666         INCLUDE XMT00001                                                 
C14666     END-EXEC.                                                            
      ****************************************************                      
      *  M/L DATE TABLE                                  *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
      *********** THIS CURSOR WILL DRIVE PROGRAM PROCESSING ************        
           EXEC SQL                                                             
               DECLARE RECALC_UPDATE_CSR CURSOR WITH HOLD FOR                   
W33304          SELECT  A.FSCL_WK_END_DTE                                       
W33304                 ,A.DEPT_NBR                                              
W33304                 ,A.SUB_CL_NBR                                            
W33304                 ,A.STR_NBR                                               
W33304                 ,A.FSCL_MN_END_DTE                                       
W33304                 ,A.SLS_RTL_AMT                                           
W33304                 ,A.ML_PSTG_MN_END_DTE                                    
C14666                 ,D.E_BUS_IND                                             
      *           FROM TINSHNK                                                  
W33304            FROM INT_INV_SHRK A                                           
W33304                ,TINVPAR B                                                
W33304                ,TINCNTL C                                                
C14666                ,XMT_LOC D                                                
W33304            WHERE A.FSCL_MN_END_DTE    = C.OPN_FSCL_MN_DTE                
      * CHECKPOINT RESTART AFTER LAST ROW PROCESSED                             
W33304*             AND A.DEPT_NBR           > :PV-HOLD-DEPT-NBR                
C14666              AND A.DEPT_NBR          >= :PV-HOLD-DEPT-NBR                
W33304              AND A.STR_NBR            = B.LOC_NBR                        
C14666              AND B.LOC_NBR            = D.LOC_NBR                        
W33304              AND B.ACTL_FIN_BK_DTE    = '9999-09-09'                     
W33304              AND B.LOC_INV_STAT_CDE   = 'IN'                             
W33304              AND B.INV_ID             = C.INV_ID                         
W33304              AND C.ACTV_IND           = 'Y'                              
                 ORDER BY DEPT_NBR                                              
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04) VALUE +38  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-PREV-DTL-KEY          PIC  X(27) VALUE SPACES.            
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                         
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                         
                   15  CR-DEPT-NBR          PIC  X(03).                         
                   15  CR-SUB-CL-NBR        PIC  X(02).                         
                   15  CR-STR-NBR           PIC  S9(04) COMP.                   
               10  CR-COMMIT-COUNT          PIC  9(09) VALUE ZEROES.            
           EJECT                                                                
                                                                                
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-END-OF-RECALC-CURSOR.                                  
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
                                                                                
      * RETRIEVE DATES FROM DTATTR / PREPARE RESTART INFORMATION                
           PERFORM 4000-READ-OPN-FSCL-MTH.                                      
           DISPLAY 'OPEN FISCAL MONTH: '                                        
W33304         PV-MIN-INCNTL-OPN-FSCL-MN-DTE ' TO '                             
W33304         PV-MAX-INCNTL-OPN-FSCL-MN-DTE                                    
                   '    OPEN FISCAL MONTH NBR:  '                               
W33304         PV-MIN-INCNTL-OPN-FSCL-MN-NBR ' TO '                             
W33304         PV-MAX-INCNTL-OPN-FSCL-MN-NBR.                                   
W33304     MOVE PV-MIN-INCNTL-OPN-FSCL-MN-DTE TO CR-FISCAL-MN-END-DTE.          
                                                                                
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
                PERFORM 7700-SELECT-RESTART-INFO                                
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                           
                                              CR-CHECKPOINT-RESTART-VAR         
                DISPLAY 'RESTART - LAST DEPT PROCESSED : ', CR-DEPT-NBR         
                MOVE CR-DEPT-NBR                    TO PV-HOLD-DEPT-NBR         
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'        
           END-IF.                                                              
                                                                                
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
                                                                                
      * OPEN RECALC CURSOR & PERFORM PRIMER READ                                
           PERFORM 7100-OPEN-RECALC-CURSOR.                                     
           PERFORM 7150-PROCESS-RECALC-CURSOR.                                  
                                                                                
           IF  PS-END-OF-RECALC-CURSOR                                          
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESSED AFTER RESTART'              
               ELSE                                                             
                   DISPLAY 'NO RECORDS TO SELECTED FOR RECALCULATION'           
               END-IF                                                           
W33304     ELSE                                                                 
W33304         PERFORM 7000-SELECT-DB2-DATES                                    
           END-IF.                                                              
                                                                                
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
                                                                                
      ******************************************************************        
      * MAIN PROCESSING PARAGRAPH - PERFORMED UNTIL ALL ROWS ON                 
      * INT_INV_SHRK  HAVE BEEN UPDATED (IE. END OF CURSOR PROCESSING)          
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE 'N' TO PS-DEPT-SHRINK-PCT-SW                                    
                       PS-PROGRAM-DEADLOCK-SW.                                  
           INITIALIZE  SA-DPTSHK-ROW.                                           
                                                                                
W34517     IF INT00006-DEPT-NBR = PV-HOLD-DEPT-NBR                              
W33304        AND INT00006-FSCL-MN-END-DTE = PV-HOLD-FISCAL-MN-END-DTE          
C14666        AND XMT00001-E-BUS-IND = PV-HOLD-E-BUS-IND                        
              CONTINUE                                                          
           ELSE                                                                 
              IF NOT PS-FIRST-TIME-THRU                                         
                 PERFORM 7800-COMMIT-PROCESSING                                 
              END-IF                                                            
W34517        MOVE INT00006-FSCL-WK-END-DTE TO PV-HOLD-FISCAL-WK-END-DTE        
W34517        MOVE INT00006-FSCL-MN-END-DTE TO PV-HOLD-FISCAL-MN-END-DTE        
W34517        MOVE INT00006-DEPT-NBR        TO PV-HOLD-DEPT-NBR                 
W34517        MOVE INT00006-SUB-CL-NBR      TO PV-HOLD-SUB-CL-NBR               
W34517        MOVE INT00006-STR-NBR         TO PV-HOLD-STR-NBR                  
C14666        MOVE XMT00001-E-BUS-IND       TO PV-HOLD-E-BUS-IND                
              PERFORM 7200-OBTAIN-SHRINK-PCTS                                   
           END-IF.                                                              
                                                                                
           IF PS-FIRST-TIME-THRU                                                
               MOVE 'N' TO PS-FIRST-TIME-SW                                     
           END-IF.                                                              
                                                                                
      * RE-CALCULATE SHRINK AMOUNT                                              
           PERFORM 2700-RE-CALC-AMOUNTS.                                        
                                                                                
      * UPDATE TABLE                                                            
W34517     PERFORM 7500-UPDATE-INT-INV-SHRK-TABLE.                              
                                                                                
           IF PS-PROGRAM-DEADLOCKED                                             
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7150-PROCESS-RECALC-CURSOR                               
W33304         PERFORM 7000-SELECT-DB2-DATES                                    
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * REFORMAT CALENDAR TO NUMERIC FIELD FOR USE AS SUBSCRIPT                 
      ******************************************************************        
       2050-REFORMAT-MONTH.                                                     
                                                                                
           MOVE DTATTR-CAL-MN-NBR       TO REFORMAT-ALPHA-CALENDAR.             
           MOVE FMT-CALENDAR-MO-NUMERIC TO SA-DPTSHK-SUB.                       
                                                                                
C14666*    DISPLAY 'DTATTR-FSCL-YR-NBR ' DTATTR-FSCL-YR-NBR                     
C14666*            '       DTATTR-CAL-MN-NBR ' DTATTR-CAL-MN-NBR.               
      ******************************************************************        
W34517*FINAL CALCULATIONS OF VALUES TO UPDATE INT_INV_SHRK     *                
      ******************************************************************        
       2700-RE-CALC-AMOUNTS.                                                    
                                                                                
W34517     COMPUTE INT00006-SHNK-RTL-AMT ROUNDED =                              
W34517         INT00006-SLS-RTL-AMT * PV-DEPT-SHRINK-PCT.                       
                                                                                
      ******************************************************************        
      * READ M/L OPEN FISCAL MONTH                                              
      ******************************************************************        
       4000-READ-OPN-FSCL-MTH.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
W33304                  MIN(OPN_FSCL_MN_DTE)                                    
W33304                 ,MAX(OPN_FSCL_MN_DTE)                                    
W33304                 ,MIN(OPN_FSCL_MN_NBR)                                    
W33304                 ,MAX(OPN_FSCL_MN_NBR)                                    
                    INTO                                                        
W33304                 :PV-MIN-INCNTL-OPN-FSCL-MN-DTE :PV-NULL                  
W33304                ,:PV-MAX-INCNTL-OPN-FSCL-MN-DTE :PV-NULL                  
W33304                ,:PV-MIN-INCNTL-OPN-FSCL-MN-NBR :PV-NULL                  
W33304                ,:PV-MAX-INCNTL-OPN-FSCL-MN-NBR :PV-NULL                  
                    FROM                                                        
W33304                  TINCNTL                                                 
W33304              WHERE                                                       
W33304                  ACTV_IND = 'Y'                                          
W33304                  WITH UR                                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
W33304             WHEN SQLCODE        = +100                                   
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '4000-READ-OPN-FSCL-MTH'                         
                                                TO PV-PARAGRAPH-NAME            
W33304                 MOVE    'SELECT ROW FROM TINCNTL'                        
                                          TO PV-OPERATION-ATTEMPTED             
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W33304         DISPLAY '** MAX RETRIES EXCEEDED ON TINCNTL **'                  
               MOVE    '4000-READ-OPN-FSCL-MTH'                                 
                                                TO PV-PARAGRAPH-NAME            
W33304         MOVE    'SELECT ROW FROM TINCNTL'                                
                                        TO PV-OPERATION-ATTEMPTED               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 7000-SELECT-DB2-DATES                                                   
      *      RETURNS DATE INFO BASED ON CONTROL CARD FSCL MONTH END DATE        
      ******************************************************************        
       7000-SELECT-DB2-DATES.                                                   
                                                                                
           EXEC SQL                                                             
               SELECT  CAL_MN_NBR                                               
                      ,PFM_END_DTE                                              
                      ,SEA_BEG_DTE                                              
                      ,PREV_SEA_END_DTE                                         
                      ,FSCL_YR_NBR                                              
                INTO  :DTATTR-CAL-MN-NBR                                        
                     ,:DTATTR-PFM-END-DTE                                       
                     ,:DTATTR-SEA-BEG-DTE                                       
                     ,:DTATTR-PREV-SEA-END-DTE                                  
                     ,:DTATTR-FSCL-YR-NBR                                       
                FROM TDTATTR                                                    
W33304          WHERE KY_DTE = :INT00006-FSCL-MN-END-DTE                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   PERFORM 2050-REFORMAT-MONTH                                  
               WHEN OTHER                                                       
                  MOVE '7000-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * OPEN CURSOR FOR UPDATE OF RECALCULATED COLUMNS ON TMNDEPT               
      *****************************************************************         
       7100-OPEN-RECALC-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               OPEN RECALC_UPDATE_CSR                                           
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  MOVE '7100-OPEN-RECALC-CSR' TO PV-PARAGRAPH-NAME              
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * FETCH A ROW FOR UPDATE                                                  
      *****************************************************************         
       7150-PROCESS-RECALC-CURSOR.                                              
                                                                                
           EXEC SQL                                                             
                  FETCH RECALC_UPDATE_CSR                                       
W34517             INTO :INT00006-FSCL-WK-END-DTE                               
W34517                 ,:INT00006-DEPT-NBR                                      
W34517                 ,:INT00006-SUB-CL-NBR                                    
W34517                 ,:INT00006-STR-NBR                                       
W34517                 ,:INT00006-FSCL-MN-END-DTE                               
W34517                 ,:INT00006-SLS-RTL-AMT                                   
W33304                 ,:INT00006-ML-PSTG-MN-END-DTE                            
C14666                 ,:XMT00001-E-BUS-IND :PV-E-BUS-NULL-IND                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
C14666             IF PV-E-BUS-NULL-IND < +0                                    
C14666                 MOVE 'N'                TO DPTSHK-E-BUS-IND              
C14666             ELSE                                                         
C14666                 MOVE XMT00001-E-BUS-IND TO DPTSHK-E-BUS-IND              
C14666             END-IF                                                       
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-RECALC-CURSOR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '7150-PROCESS-RECALC'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      *****************************************************************         
      * RETRIEVES CURRENT MONTH SHRINK PERCENT                                  
      *****************************************************************         
       7200-OBTAIN-SHRINK-PCTS.                                                 
                                                                                
           PERFORM 7220-SELECT-SHRINK-KEY.                                      
           PERFORM 7240-LOAD-SHRINK-TABLE.                                      
                                                                                
           MOVE DCLTDPTSHK TO SA-DPTSHK-ROW.                                    
                                                                                
           PERFORM 7260-CURR-MONTH-SHRINK-PCT.                                  
                                                                                
      *****************************************************************         
      * OBTAIN MDSEAR_KEY AND DEPT FROM SHRINK AND MBS TABLE                    
      *****************************************************************         
       7220-SELECT-SHRINK-KEY.                                                  
                                                                                
           EXEC SQL                                                             
               SELECT  A.MDSEAR_DKEY                                            
                      ,A.DEPT_NBR                                               
                      ,B.SHRINK_YR_NBR                                          
               INTO    :MDSEAR-DKEY                                             
                      ,:MDSEAR-DEPT-NBR                                         
                      ,:DPTSHK-SHRINK-YR-NBR                                    
               FROM  TMDSEAR A                                                  
                    ,TDPTSHK B                                                  
               WHERE A.OPERCO_NBR    = '03'                                     
                 AND A.MDSELV_CDE    = 'DPT'                                    
                 AND A.MDSEAR_DKEY   =  B.MDSEAR_DKEY                           
W34517           AND A.DEPT_NBR      = :INT00006-DEPT-NBR                       
                 AND B.SHRINK_YR_NBR = :DTATTR-FSCL-YR-NBR                      
C14666           AND B.E_BUS_IND     = :DPTSHK-E-BUS-IND                        
W33304           AND :INT00006-FSCL-MN-END-DTE BETWEEN                          
                                        A.STRT_DTE AND A.END_DTE                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
W34517             DISPLAY 'DEPT:        ' INT00006-DEPT-NBR                    
                   DISPLAY 'FSCL YR NBR: ' DTATTR-FSCL-YR-NBR                   
W33304             DISPLAY 'FSCL MN END: ' INT00006-FSCL-MN-END-DTE             
                   MOVE 'SELECT - TMDSEAR/TDPTSHK '                             
                                          TO PV-OPERATION-ATTEMPTED             
                   MOVE '7220-SELECT-SHRINK-KEY'                                
                                          TO PV-PARAGRAPH-NAME                  
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *------------------------------------------------------------             
      * THE SHRINK PERCENTS ARE STORED IN THE DPTSHK TABLE BY                   
      * CALENDAR MONTH.                                                         
      *------------------------------------------------------------             
                                                                                
       7240-LOAD-SHRINK-TABLE.                                                  
                                                                                
           EXEC SQL                                                             
               SELECT  JAN_SHRINK_PCT                                           
                    ,  FEB_SHRINK_PCT                                           
                    ,  MAR_SHRINK_PCT                                           
                    ,  APR_SHRINK_PCT                                           
                    ,  MAY_SHRINK_PCT                                           
                    ,  JUN_SHRINK_PCT                                           
                    ,  JUL_SHRINK_PCT                                           
                    ,  AUG_SHRINK_PCT                                           
                    ,  SEP_SHRINK_PCT                                           
                    ,  OCT_SHRINK_PCT                                           
                    ,  NOV_SHRINK_PCT                                           
                    ,  DEC_SHRINK_PCT                                           
               INTO   :DPTSHK-JAN-SHRINK-PCT                                    
                    , :DPTSHK-FEB-SHRINK-PCT                                    
                    , :DPTSHK-MAR-SHRINK-PCT                                    
                    , :DPTSHK-APR-SHRINK-PCT                                    
                    , :DPTSHK-MAY-SHRINK-PCT                                    
                    , :DPTSHK-JUN-SHRINK-PCT                                    
                    , :DPTSHK-JUL-SHRINK-PCT                                    
                    , :DPTSHK-AUG-SHRINK-PCT                                    
                    , :DPTSHK-SEP-SHRINK-PCT                                    
                    , :DPTSHK-OCT-SHRINK-PCT                                    
                    , :DPTSHK-NOV-SHRINK-PCT                                    
                    , :DPTSHK-DEC-SHRINK-PCT                                    
               FROM    TDPTSHK                                                  
               WHERE   MDSEAR_DKEY   = :MDSEAR-DKEY                             
                 AND   SHRINK_YR_NBR = :DPTSHK-SHRINK-YR-NBR                    
C14666           AND   E_BUS_IND     = :DPTSHK-E-BUS-IND                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'DKEY:      ' MDSEAR-DKEY                            
                   DISPLAY 'SHRINK-YR: ' DPTSHK-SHRINK-YR-NBR                   
                   MOVE 'SELECT - TDPTSHRK '                                    
                                          TO PV-OPERATION-ATTEMPTED             
                   MOVE '7240-LOAD-SHRINK-TABLES'                               
                                          TO PV-PARAGRAPH-NAME                  
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7260-CURR-MONTH-SHRINK-PCT RETRIEVES THE SHRINK PERCENT FOR THE*        
      *      CURRENT CALENDAR MONTH STORED ON THE 'SA-DPTSHK-ROW' TABLE*        
      ******************************************************************        
       7260-CURR-MONTH-SHRINK-PCT.                                              
                                                                                
           MOVE SA-DPTSHK-SHRINK-PCT(SA-DPTSHK-SUB) TO                          
                                                    PV-DEPT-SHRINK-PCT.         
                                                                                
      ******************************************************************        
W34517 7500-UPDATE-INT-INV-SHRK-TABLE.                                          
           EXEC SQL                                                             
W34517         UPDATE INT_INV_SHRK                                              
W34517            SET  SHNK_RTL_AMT     = :INT00006-SHNK-RTL-AMT                
W34517          WHERE  FSCL_WK_END_DTE  = :INT00006-FSCL-WK-END-DTE             
W34517          AND    FSCL_MN_END_DTE  = :INT00006-FSCL-MN-END-DTE             
W34517          AND    DEPT_NBR         = :INT00006-DEPT-NBR                    
W34517          AND    SUB_CL_NBR       = :INT00006-SUB-CL-NBR                  
W34517          AND    STR_NBR          = :INT00006-STR-NBR                     
W33304          AND    ML_PSTG_MN_END_DTE                                       
W33304                                  = :INT00006-ML-PSTG-MN-END-DTE          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN -911                                                        
                   ADD +1             TO PA-DEADLOCK-COUNT                      
                   IF PA-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
W34517                 MOVE '7500-UPDATE-INT-INV-SHRK-TABLE' TO                 
                                                    PV-PARAGRAPH-NAME           
                       PERFORM 9000-DEADLOCK-ERROR                              
                   ELSE                                                         
                       PERFORM 7999-RESTART-PROCESS                             
                   END-IF                                                       
               WHEN OTHER                                                       
                    MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED             
W34517              MOVE '7500-UPDATE-INT-INV-SHRK-TABLE'                       
                                          TO PV-PARAGRAPH-NAME                  
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
           IF PS-PROGRAM-DEADLOCKED                                             
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-DEPTS-PROCESSED.                                     
                                                                                
      ******************************************************************        
      * PERFORMED BY END OF JOB PROCESSING                                      
      ******************************************************************        
       7550-CLOSE-RECALC-CURSOR.                                                
           EXEC SQL                                                             
               CLOSE RECALC_UPDATE_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7550-CLOSE-RECALC_CURSOR' TO PV-PARAGRAPH-NAME        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      * FOLLOWING PARAGRAPHS ARE FOR CHECKPOINT RESTART LOGIC                   
                                                                                
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *        
      *            CONTROL TABLE                                       *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
                                                                                
           EXEC SQL                                                             
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
                   MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED         
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *        
      *            IF WE ARE IN A RESTART CONDITION.                   *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *        
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
      *                                                                         
      *    DISPLAY 'CURRENT TIMESTAMP ', PV-CURRENT-TIMESTAMP                   
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-13             TO PV-OPERATION-ATTEMPTED             
               MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *        
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
                                                                                
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
                                                                                
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
      *        PREPARE COMMIT RECORD FOR UPDATE                                 
               ADD 1                           TO PA-COMMIT-COUNT               
W34517         DISPLAY 'PA-COMMIT-TAKEN ', PA-COMMIT-COUNT                      
W33304         MOVE INT00006-FSCL-MN-END-DTE   TO CR-FISCAL-MN-END-DTE          
               MOVE PV-HOLD-FISCAL-WK-END-DTE  TO CR-FISCAL-WK-END-DTE          
               MOVE PV-HOLD-DEPT-NBR           TO CR-DEPT-NBR                   
               MOVE PV-HOLD-SUB-CL-NBR         TO CR-SUB-CL-NBR                 
               MOVE PV-HOLD-STR-NBR            TO CR-STR-NBR                    
               MOVE PA-COMMIT-COUNT            TO CR-COMMIT-COUNT               
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                          
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                             TCKRSTINF-WORK-STRG-DESC           
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
               PERFORM 7850-COMMIT-CHANGES                                      
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.              
      *            TAKE A COMMIT.                                               
      *  ==> PERFORMED FROM: 7800-COMMIT-PROCESSING.                            
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
      ******   DISPLAY 'COMMIT ==>> ' TCKRSTINF-WORK-STRG-DESC 1:38             
      ******            ' <<=='                                                 
           ELSE                                                                 
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7900-UPDATE-CHECKPOINT-STATUS                                           
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE           
      *                                                                         
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED        
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST INT_INV_SHRK                  
      * ROW FOR UPDATE.  FIND RESTART RECORD AND CKPT RESTART AT                
      * POINT OF LAST COMMIT.                                                   
      ******************************************************************        
       7999-RESTART-PROCESS.                                                    
                                                                                
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                                   
                                                                                
           PERFORM 7700-SELECT-RESTART-INFO.                                    
                                                                                
           DISPLAY '**** RESTART **** '.                                        
           DISPLAY '** SQLCODE -911 ** ', PA-DEADLOCK-COUNT, 'TIMES'.           
           DISPLAY 'TCKRSTINF-LAST CKPT ',                                      
                                        TCKRSTINF-WORK-STRG-DESC(1:38).         
                                                                                
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN              
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE                
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                               
           IF PS-FIRST-COMMIT                                                   
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:38)                       
               MOVE ZERO TO PV-HOLD-DEPT-NBR                                    
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'             
                      ' BEGINNING'                                              
           ELSE                                                                 
               DISPLAY 'TCKRSTINF-LAST CKPT '                                   
                        TCKRSTINF-WORK-STRG-DESC (1:38)                         
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA          
               MOVE TCKRSTINF-WORK-STRG-DESC TO                                 
                    CR-CHECKPOINT-RESTART-AREA                                  
               MOVE CR-DEPT-NBR              TO PV-HOLD-DEPT-NBR                
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                        
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE                  
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE                  
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                           
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                         
               DISPLAY 'STORE NBR       ' CR-STR-NBR                            
           END-IF.                                                              
                                                                                
      * POSITION CURSOR                                                         
           PERFORM 7100-OPEN-RECALC-CURSOR.                                     
                                                                                
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 7550-CLOSE-RECALC-CURSOR.                                    
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
                                                                                
           DISPLAY '                                             '.             
           DISPLAY 'DEPTS PROCESSED ', PA-DEPTS-PROCESSED.                      
           DISPLAY '                                             '.             
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                         
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
                                                                                
      ******************************************************************        
      *  PERFORMED BY      PROCESS-RECALC-CURSOR                                
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)           
      ******************************************************************        
       9000-DEADLOCK-ERROR.                                                     
                                                                                
               MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND.                                          
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
