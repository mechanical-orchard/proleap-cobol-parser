       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      PXKRP123.                                       00020001
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040000
       DATE-WRITTEN.    06/18/2012.                                     00050002
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070000
      **NEW STORE WHITE TICKET PRICE CHANGE SUMMARY REPORT              00080001
      ******************************************************************00090000
      *DATE: 06/18/2012 CHANGED BY: DAVID WELLER                        00110401
      *      INITIAL DESIGN                                             00110501
      ******************************************************************00160000
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.        IBM-3090.                                00190000
       OBJECT-COMPUTER.        IBM-3090.                                00200000
       INPUT-OUTPUT SECTION.                                            00210000
       FILE-CONTROL.                                                    00220000
                                                                        00230000
           SELECT INPUT-FILE                                            00240000
                  ASSIGN TO INP01.                                      00250000
           SELECT REPORT-FILE                                           00280200
                  ASSIGN TO OUT01.                                      00290000
                                                                        00300000
       DATA DIVISION.                                                   00310000
       FILE SECTION.                                                    00320000
                                                                        00330000
       FD  INPUT-FILE                                                   00340000
           RECORDING MODE F                                             00350000
           BLOCK CONTAINS 0 RECORDS                                     00360000
           LABEL RECORDS ARE OMITTED                                    00370000
           DATA RECORD IS INPUT-RECORD.                                 00380000
       01  INPUT-RECORD                    PIC X(250).                  00390003
                                                                        00540000
       FD  REPORT-FILE                                                  00600200
           RECORDING MODE F                                             00600300
           BLOCK CONTAINS 0 RECORDS                                     00600400
           LABEL RECORDS ARE OMITTED                                    00600500
           DATA RECORD IS REPORT-RECORD.                                00600600
       01  REPORT-RECORD                   PIC X(133).                  00600700
                                                                        00600800
       WORKING-STORAGE SECTION.                                         00600900
      ******************************************************************00601000
      **SWITCHES                                                        00601100
      ******************************************************************00601200
       01  PS-PROGRAM-SWITCHES.                                         00601300
           05  PS-END-OF-INPUT-FILE-SW         PIC X(01)   VALUE 'N'.   00601400
               88  PS-END-OF-INPUT-FILE                    VALUE 'Y'.   00610000
               88  PS-NOT-END-OF-INPUT-FILE                VALUE 'N'.   00620000
      ******************************************************************00700200
      **PROGRAM HOLDS                                                   00700300
      ******************************************************************00700400
       01  PH-PROGRAM-HOLDS-1.                                          00700500
           05  PH-STR-NBR                    PIC S9(04) COMP            00700600
                                                        VALUE ZEROS.    00710000
           05  PH-STR-TOTAL-QTY              PIC  9(09) VALUE ZEROS.    00750004
      ******************************************************************00840000
      **TIMESTAMP VARIABLES                                             00850000
      ******************************************************************00860000
       01 DF-DATE-TIME-FIELDS.                                          00870000
          05 DF-ACCEPT-DATE.                                            00880000
             10  DF-ACCEPT-YR                  PIC 9(02) VALUE ZEROS.   00890000
             10  DF-ACCEPT-MO                  PIC 9(02) VALUE ZEROS.   00900000
             10  DF-ACCEPT-DY                  PIC 9(02) VALUE ZEROS.   00910000
          05 DF-ACCEPT-TIME.                                            00920000
             10  DF-ACCEPT-HR                  PIC 9(02) VALUE ZEROS.   00930000
             10  DF-ACCEPT-MN                  PIC 9(02) VALUE ZEROS.   00940000
             10  DF-ACCEPT-SS                  PIC 9(04) VALUE ZEROS.   00950000
          05 DF-FORMAT-DATE.                                            00960000
             10  DF-FORMAT-DATE-MM             PIC 9(02) VALUE ZEROS.   00970000
             10  FILLER                        PIC X(01) VALUE '/'.     00980000
             10  DF-FORMAT-DATE-DD             PIC 9(02) VALUE ZEROS.   00990000
             10  FILLER                        PIC X(01) VALUE '/'.     01000000
             10  DF-FORMAT-DATE-YY             PIC 9(02) VALUE ZEROS.   01010000
      ******************************************************************01020000
      **PROGRAM CONSTANTS                                               01030000
      ******************************************************************01040000
       01  PC-PROGRAM-CONSTANTS.                                        01050000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                01060000
               VALUE 'PXKRP123'.                                        01070001
      ******************************************************************01080000
      **PROGRAM COUNTERS                                                01090000
      ******************************************************************01100000
       01  PC-PROGRAM-COUNTER.                                          01110000
           05  PC-INPUT-RECORDS-READ           PIC 9(09) VALUE ZEROS.   01120000
           05  PC-REPORT-RECORDS-WRITTEN       PIC 9(09) VALUE ZEROS.   01140000
           05  PC-REPORT-LINE-COUNTER          PIC 9(03) VALUE ZEROS.   01160000
           05  PC-PAGE-COUNTER                 PIC 9(03) VALUE ZEROS.   01170000
      ******************************************************************01180000
      **REPORT HEADINGS                                                 01190000
      ******************************************************************01200000
       01  RH-TITLE-1.                                                  01210000
           05  FILLER                          PIC X(45)  VALUE SPACES. 01220001
           05  FILLER                          PIC X(43)                01230002
                   VALUE 'NEW STORE WHITE TICKET PRICE CHANGE REPORT '. 01240001
           05  FILLER                          PIC X(45)  VALUE SPACES. 01250001
       01  RH-TITLE-2.                                                  01260002
           05  FILLER                          PIC X(55)  VALUE SPACES. 01270001
           05  FILLER                          PIC X(23)                01280001
                 VALUE ' SUMMARY BY DEPARTMENT '.                       01290002
           05  FILLER                          PIC X(55)  VALUE SPACES. 01300001
       01  RH-TITLE-3.                                                  01301002
           05  FILLER                          PIC X(55)  VALUE SPACES. 01302002
           05  FILLER                          PIC X(23)                01303002
                 VALUE '  MARKUPS / MARKDOWNS  '.                       01304002
           05  FILLER                          PIC X(55)  VALUE SPACES. 01305002
       01  RH-HEADINGS-1.                                               01310000
           05  FILLER                          PIC X(02)  VALUE SPACES. 01320000
           05  FILLER                          PIC X(14)                01330000
                             VALUE ' STORE NUMBER:'.                    01340000
           05  RH-HEADINGS-1-STR-NBR           PIC Z9999  VALUE ZEROS.  01350000
           05  FILLER                          PIC X(01)  VALUE SPACES. 01360000
           05  RH-HEADING-1-STR-NAME           PIC X(20)  VALUE SPACES. 01370000
           05  FILLER                          PIC X(90)  VALUE SPACES. 01380000
       01  RH-HEADINGS-2.                                               01390000
           05  FILLER                          PIC X(17)                01400000
                             VALUE ' EFFECTIVE DATE: '.                 01410000
           05  RH-HEADINGS-2-EFF-DATE          PIC X(10)  VALUE SPACES. 01420000
       01  RH-HEADINGS-4.                                               01560000
           05  FILLER                          PIC X(04)                01580001
               VALUE 'DEPT'.                                            01590001
           05  FILLER                          PIC X(01)  VALUE SPACES. 01600000
           05  FILLER                          PIC X(20)                01610001
               VALUE 'DEPT'.                                            01611001
           05  FILLER                          PIC X(01)  VALUE SPACES. 01630001
           05  FILLER                          PIC X(11)                01640001
               VALUE '      TOTAL'.                                     01660001
       01  RH-HEADINGS-5.                                               01740000
           05  FILLER                          PIC X(04)                01750001
               VALUE 'NBR '.                                            01760001
           05  FILLER                          PIC X(01)  VALUE SPACES. 01770000
           05  FILLER                          PIC X(20)                01780001
               VALUE 'NAME'.                                            01790001
           05  FILLER                          PIC X(01)  VALUE SPACES. 01800000
           05  FILLER                          PIC X(11)                01810001
               VALUE '      UNITS'.                                     01811001
       01  RH-HEADINGS-6.                                               02040000
           05  FILLER                          PIC X(04)                02050001
               VALUE '----'.                                            02060001
           05  FILLER                          PIC X(01)  VALUE SPACES. 02070000
           05  FILLER                          PIC X(20)                02080001
               VALUE '--------------------'.                            02090001
           05  FILLER                          PIC X(01)  VALUE SPACES. 02100000
           05  FILLER                          PIC X(11)                02110001
               VALUE '-----------'.                                     02120001
       01  RH-DETAIL-LINE.                                              02590600
           05  RH-DEPT-NBR                     PIC ZZZZ   VALUE ZEROS.  02590701
           05  FILLER                          PIC X(01)  VALUE SPACES. 02590800
           05  RH-DEPT-NM                      PIC X(20)  VALUE SPACES. 02591701
           05  FILLER                          PIC X(01)  VALUE SPACES. 02600000
           05  RH-STR-OH-QTY                   PIC ZZZZZZZZZZ9.         02610007
       01  RH-TOTAL-1.                                                  02620002
           05  FILLER                          PIC X(37)                02630004
               VALUE '-------------------------------------'.           02640004
       01  RH-TOTAL-2.                                                  02680002
           05  FILLER                          PIC X(26)                02690001
               VALUE 'TOTAL STORE UNITS'.                               02700001
           05  RH-STR-TOTAL-QTY                PIC ZZZZZZZZZZ9.         02710007
       01  RH-FOOTERS-1.                                                02760000
           05  FILLER                          PIC X(54)  VALUE SPACES. 02770000
           05  FILLER                          PIC X(19)                02780000
               VALUE '***END OF REPORT***'.                             02790000
      ******************************************************************02800000
      **COPYBOOKS                                                       02810000
      ******************************************************************02820000
      ***INPUT FILE FORMAT                                              02850000
           COPY PXRD141.                                                02860001
      ***STANDARD KOHLS HEADING FOR 132 BYTE REPORT                     02890000
           COPY DPWS132O.                                               02900000
      ******************************************************************02910000
      **ERROR HANDLING                                                  02920000
      ******************************************************************02930000
       01  PV-MISC-ABEND-FIELDS.                                        02940000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   02950000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   02960000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   02970000
                                                                        02980000
       PROCEDURE DIVISION.                                              02990000
      ***************************************************************** 03000000
                                                                        03010000
           PERFORM 1000-INITIALIZATION.                                 03020000
           PERFORM 2000-PROCESS-RECORDS                                 03030000
                   UNTIL PS-END-OF-INPUT-FILE.                          03040000
           PERFORM 9999-WRAPUP.                                         03050000
           GOBACK.                                                      03060000
                                                                        03070000
      ******************************************************************03090000
      **INITIALIZATION AREA                                             03100000
      ******************************************************************03110000
       1000-INITIALIZATION.                                             03120000
                                                                        03130000
           OPEN INPUT  INPUT-FILE                                       03160000
                OUTPUT REPORT-FILE.                                     03180000
                                                                        03190000
      **RETRIEVE DATE FROM SYSTEM                                       03200000
           ACCEPT DF-ACCEPT-TIME FROM TIME.                             03210000
           ACCEPT DF-ACCEPT-DATE FROM DATE.                             03220000
                                                                        03230000
           MOVE DF-ACCEPT-YR          TO DF-FORMAT-DATE-YY.             03240000
           MOVE DF-ACCEPT-MO          TO DF-FORMAT-DATE-MM.             03250000
           MOVE DF-ACCEPT-DY          TO DF-FORMAT-DATE-DD.             03260000
                                                                        03270000
           MOVE ZEROS                 TO PH-STR-TOTAL-QTY.              03280004
           PERFORM 2999-READ-INPUT-FILE.                                03280200
                                                                        03310000
           IF  PS-NOT-END-OF-INPUT-FILE                                 03320000
               PERFORM 1100-HEADING-PREP                                03330000
               PERFORM 2100-WRITE-HEADINGS                              03400000
               MOVE PXRD141-STR-NBR      TO PH-STR-NBR                  03430001
           END-IF.                                                      03440000
                                                                        03450000
      ******************************************************************03470000
      **MOVE DATA TO FIELDS IN HEADING FOR REPORT                       03480000
      ******************************************************************03490000
       1100-HEADING-PREP.                                               03500000
                                                                        03510000
      ***PREPS KOHLS STANDARD HEADING                                   03540000
           MOVE PC-CURRENT-PROGRAM-NAME                                 03550000
                                     TO DP132O-PROGRAM-NAME.            03560000
           MOVE DF-FORMAT-DATE       TO DP132O-RUN-DATE.                03570000
           MOVE DF-ACCEPT-HR         TO DP132O-RUN-HOUR.                03580000
           MOVE DF-ACCEPT-MN         TO DP132O-RUN-MINUTE.              03590000
           MOVE 1                    TO DP132O-REPORT-NUMBER.           03600001
                                                                        03610000
      ******************************************************************03701100
      **PROCESSES INPUT FILE                                            03701200
      ******************************************************************03701300
       2000-PROCESS-RECORDS.                                            03701400
                                                                        03701500
           EVALUATE TRUE                                                03701600
              WHEN PXRD141-STR-NBR EQUAL PH-STR-NBR                     03710001
                                                                        03711000
                   IF  PC-REPORT-LINE-COUNTER > 57                      03720002
                       PERFORM 2300-WRITE-FOOTERS                       03740000
                       PERFORM 2100-WRITE-HEADINGS                      03750000
                   END-IF                                               03790000
                                                                        03830100
                   PERFORM 2200-WRITE-DETAIL                            03830200
                                                                        03830400
              WHEN PXRD141-STR-NBR NOT EQUAL PH-STR-NBR                 03830501
                   INITIALIZE PC-PAGE-COUNTER                           03830600
                   PERFORM 2400-WRITE-TOTALS                            03830702
                   PERFORM 2300-WRITE-FOOTERS                           03830800
                   PERFORM 1100-HEADING-PREP                            03831000
                   PERFORM 2100-WRITE-HEADINGS                          03920000
                   PERFORM 2200-WRITE-DETAIL                            03940000
                   MOVE PXRD141-STR-NBR TO PH-STR-NBR                   03950001
           END-EVALUATE.                                                03980000
                                                                        03990000
           PERFORM 2999-READ-INPUT-FILE.                                04000000
           IF  PS-END-OF-INPUT-FILE                                     04001005
               PERFORM 2400-WRITE-TOTALS                                04002005
           END-IF.                                                      04003005
                                                                        04010000
      ***************************************************************** 04030000
      **WRITE REPORT HEADERS                                            04040000
      ***************************************************************** 04050000
       2100-WRITE-HEADINGS.                                             04060000
                                                                        04070000
           INITIALIZE PC-REPORT-LINE-COUNTER.                           04100000
                                                                        04110000
           ADD +1                      TO PC-PAGE-COUNTER.              04120000
           MOVE PC-PAGE-COUNTER        TO DP132O-PAGE-NUMBER.           04130000
                                                                        04140000
           MOVE PXRD141-STR-NBR        TO RH-HEADINGS-1-STR-NBR.        04150001
           MOVE PXRD141-LOC-NM         TO RH-HEADING-1-STR-NAME.        04160001
           MOVE PXRD141-EFF-DATE       TO RH-HEADINGS-2-EFF-DATE.       04190001
                                                                        04200000
           WRITE REPORT-RECORD FROM DP132O-STANDRD-HEADER-132-COLS      04210000
                               AFTER ADVANCING PAGE.                    04220000
                                                                        04230000
           WRITE REPORT-RECORD FROM RH-TITLE-1 AFTER ADVANCING 1 LINE.  04240000
           WRITE REPORT-RECORD FROM RH-TITLE-2 AFTER ADVANCING 1 LINE.  04260102
           WRITE REPORT-RECORD FROM RH-TITLE-3 AFTER ADVANCING 1 LINE.  04260202
                                                                        04260800
           WRITE REPORT-RECORD FROM RH-HEADINGS-1                       04270000
                               AFTER ADVANCING 2 LINES.                 04280000
           WRITE REPORT-RECORD FROM RH-HEADINGS-2                       04290000
                               AFTER ADVANCING 1 LINES.                 04300000
                                                                        04330000
           WRITE REPORT-RECORD FROM RH-HEADINGS-4                       04450200
                               AFTER ADVANCING 2 LINES.                 04450300
           WRITE REPORT-RECORD FROM RH-HEADINGS-5                       04450400
                               AFTER ADVANCING 1 LINES.                 04450500
           WRITE REPORT-RECORD FROM RH-HEADINGS-6                       04450600
                               AFTER ADVANCING 1 LINES.                 04450700
                                                                        04450800
           ADD +11             TO PC-REPORT-LINE-COUNTER.               04450902
                                                                        04453000
      ***************************************************************** 04670200
      **WRITE DETAIL LINE                                               04670300
      ***************************************************************** 04670400
       2200-WRITE-DETAIL.                                               04670500
                                                                        04670600
           INITIALIZE RH-DETAIL-LINE.                                   04670700
                                                                        04670800
           MOVE PXRD141-DEPT-NBR       TO RH-DEPT-NBR.                  04671008
                                                                        04671600
           MOVE PXRD141-DEPT-NM        TO RH-DEPT-NM.                   04671701
           MOVE PXRD141-STR-OH-QTY     TO RH-STR-OH-QTY.                04672401
                                                                        04680000
           ADD +1                      TO PC-REPORT-LINE-COUNTER.       04830000
           ADD PXRD141-STR-OH-QTY      TO PH-STR-TOTAL-QTY.             04831006
                                                                        04840000
           WRITE REPORT-RECORD FROM RH-DETAIL-LINE.                     04850000
                                                                        04860000
           ADD +1                      TO PC-REPORT-RECORDS-WRITTEN.    04900000
                                                                        04910000
      ***************************************************************** 04930000
      **WRITE FOOTERS                                                   04940000
      ***************************************************************** 04950000
       2300-WRITE-FOOTERS.                                              04960000
                                                                        04970000
           WRITE REPORT-RECORD FROM RH-FOOTERS-1.                       05000000
                                                                        05010000
      ***************************************************************** 05011002
      **WRITE TOTALS                                                    05012002
      ***************************************************************** 05013002
       2400-WRITE-TOTALS.                                               05014002
                                                                        05015002
           MOVE PH-STR-TOTAL-QTY    TO RH-STR-TOTAL-QTY.                05015104
           WRITE REPORT-RECORD FROM RH-TOTAL-1                          05016002
               AFTER ADVANCING 2 LINES.                                 05016102
           WRITE REPORT-RECORD FROM RH-TOTAL-2                          05016202
               AFTER ADVANCING 1 LINES.                                 05016302
           MOVE ZEROS               TO PH-STR-TOTAL-QTY.                05016404
                                                                        05017002
      ***************************************************************** 05030000
      **READ INPUT FILE                                                 05040000
      ***************************************************************** 05050000
       2999-READ-INPUT-FILE.                                            05060000
                                                                        05070000
           READ INPUT-FILE INTO PXRD141-WHITE-TICKET-REC                05080001
                AT END SET PS-END-OF-INPUT-FILE TO TRUE                 05090000
           END-READ.                                                    05100000
                                                                        05110000
           IF  PS-NOT-END-OF-INPUT-FILE                                 05120000
               ADD +1 TO PC-INPUT-RECORDS-READ                          05130000
           END-IF.                                                      05140000
                                                                        05150000
      ****************************************************************  06140000
      *END OF PROGRAM ROUTINE                                           06150000
      ****************************************************************  06160000
       9999-WRAPUP.                                                     06170000
                                                                        06180000
           IF  PC-REPORT-RECORDS-WRITTEN > 0                            06190000
               PERFORM 2300-WRITE-FOOTERS                               06200000
           END-IF.                                                      06210000
                                                                        06220000
           CLOSE INPUT-FILE                                             06230000
                 REPORT-FILE.                                           06250000
                                                                        06260000
           PERFORM 9999-DISPLAY-STATISTICS.                             06270000
                                                                        06280000
      ****************************************************************  06300000
      *DISPLAY PROGRAM STATISTICS                                       06310000
      ****************************************************************  06320000
       9999-DISPLAY-STATISTICS.                                         06330000
                                                                        06340000
           DISPLAY '*************************************************'. 06350000
           DISPLAY '* PROGRAM...............: ' PC-CURRENT-PROGRAM-NAME.06360000
           DISPLAY '* DATE..................: ' DF-ACCEPT-DATE.         06370000
           DISPLAY '* TIMESTAMP.............: ' DF-ACCEPT-TIME.         06380000
           DISPLAY '*************************************************'. 06390000
           DISPLAY '* INPUT RECORDS READ.........: '                    06400000
                                          PC-INPUT-RECORDS-READ.        06410000
           DISPLAY '* REPORT RECORDS WRITTEN.....: '                    06440000
                                          PC-REPORT-RECORDS-WRITTEN.    06450000
           DISPLAY '*************************************************'. 06480000
                                                                        06490000
