000100*-----------------------*                                         03/19/93
000200 IDENTIFICATION DIVISION.                                         VLKCS503
000300*-----------------------*                                            LV011
000400 PROGRAM-ID.    VLKCS503.                                            CL**1
000500 AUTHOR.        MARY SARDINA - KOHLS.                                   *1
000600 INSTALLATION.  KOHLS DEPARTMENT STORES.                             CL**1
000700 DATE-WRITTEN.  03-16-93.                                            CL**1
000800 DATE-COMPILED.                                                      CL**1
000900                                                                     CL**1
001000*---------------------------------------------------------------*    CL**1
001100*    V503 - PRETICKET REQUEST DETAIL INQUIRY SCREENS            *    CL**1
001200*                                                               *    CL**1
001300*    THIS PROGRAM WILL DISPLAY COMMENTS THAT HAVE BEEN ENTERED  *    CL**1
001400*    BY THE VL TICKET STAFF.                                    *    CL**1
001900*                                                               *    CL**1
002000*---------------------------------------------------------------*    CL**1
263627* CHANGED 11/05/2021    BY JIM HUNTER      WR: CHG0263627                 
263627* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263627* MAKING THE CALL DYNAMIC VS STATIC.                                      
263627*----------------------------------------------------------------*        
CICS  * CHANGED 11/11/2022    BY JIM HUNTER      WR: CHG0278236                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *****************************************************************         
002100                                                                     CL**1
00021  EJECT                                                            DPKCS036
00022  ENVIRONMENT DIVISION.                                            DPKCS036
00023                                                                   DPKCS036
00024  DATA DIVISION.                                                   DPKCS036
00025                                                                   DPKCS036
00026  WORKING-STORAGE SECTION.                                         DPKCS036
00027                                                                   DPKCS036
00037                                                                   DPKCS036
00038  01  PC-PROGRAM-CONSTANTS.                                        DPKCS036
004000     05  PC-MAX-XTRA-LINES              PIC S9(04) COMP           00380003
004100                                                   VALUE +20.     00381003
00039      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         DPKCS036
00040                                                     'VL503A  '.   DPKCS036
00041      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         DPKCS036
00042                                                     'VLKM503 '.   DPKCS036
00043      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         DPKCS036
00044                                                     'VLKCS503'.   DPKCS036
00045      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      DPKCS036
00046      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      DPKCS036
00047      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    DPKCS036
00048      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12     DPKCS036
00049                                                     COMP SYNC.    DPKCS036
00050      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      DPKCS036
00051                                                     COMP SYNC.    DPKCS036
00052      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         DPKCS036
00053                                                     +1000000      DPKCS036
00054                                                     COMP-3.       DPKCS036
00057      05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       DPKCS036
00058                                                     COMP-3.       DPKCS036
00059      05  PC-PERCENT                     PIC  X(01)  VALUE '%'.    DPKCS036
00060      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  DPKCS036
00061      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  DPKCS036
00062      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  DPKCS036
00063      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  DPKCS036
00064 *                                                                 DPKCS036
005100                                                                     CL**1
005200 01  PC-TSYMSG-NUMBERS.                                              CL**1
005300     05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.     CL**1
005400     05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.     CL**1
005500     05  PC-TSYMSG-00059                PIC 9(5)    VALUE 00059.     CL**7
005600     05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.     CL**7
005700     05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.     CL**1
005800     05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.     CL**1
005900     05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.     CL**1
006000     05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.     CL**6
006100     05  PC-TSYMSG-00302                PIC 9(5)    VALUE 00302.     CL**1
00080 *                                                                 DPKCS036
00081  01  PS-PROGRAM-SWITCHES.                                         DPKCS036
009200     05  PS-COMMENTS-SW                 PIC  X(01)  VALUE 'Y'.    00980007
009300         88  PS-NO-COMMENTS-LEFT                    VALUE 'N'.    00990007
009400         88  PS-COMMENTS-LEFT                       VALUE 'Y'.    01000007
00082      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    DPKCS036
00083          88  PS-NO-ERROR                            VALUE 'Y'.    DPKCS036
00084          88  PS-ERROR                               VALUE 'N'.    DPKCS036
00085      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    DPKCS036
00086          88  PS-COMMAREA-VALID                      VALUE 'Y'.    DPKCS036
00087          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    DPKCS036
00088      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    DPKCS036
00089          88  PS-DONE                                VALUE 'Y'.    DPKCS036
00090          88  PS-NOT-DONE                            VALUE 'N'.    DPKCS036
00091 *                                                                 DPKCS036
00092  01  PV-PROGRAM-VARIABLES.                                        DPKCS036
011300     05  PV-NUMBER-OF-EXTRA-LINES       PIC S9(09) COMP.          01180003
00093      05  PV-SUB                         PIC S9(04)  VALUE +0      DPKCS036
00094                                                     COMP SYNC.    DPKCS036
00095      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      DPKCS036
00096                                                     COMP SYNC.    DPKCS036
00097      05  PV-COMM-ITEM                   PIC S9(04)  VALUE +0      DPKCS036
00098                                                     COMP SYNC.    DPKCS036
00099      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      DPKCS036
00100                                                     COMP-3.       DPKCS036
00101      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      DPKCS036
00102                                                     COMP-3.       DPKCS036
00103      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      DPKCS036
00104                                                     COMP-3.       DPKCS036
00105      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      DPKCS036
00106                                                     COMP-3.       DPKCS036
00107      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      DPKCS036
00108                                                     COMP-3.       DPKCS036
00109      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      DPKCS036
00110                                                     COMP-3.       DPKCS036
00111      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      DPKCS036
00112                                                     COMP SYNC.    DPKCS036
00113      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      DPKCS036
00114                                                     COMP SYNC.    DPKCS036
00115      05  PV-ITEM                        PIC S9(04)  VALUE +0      DPKCS036
00116                                                     COMP SYNC.    DPKCS036
00117      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      DPKCS036
00118                                                     COMP SYNC.    DPKCS036
00119      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      DPKCS036
00120                                                     COMP SYNC.    DPKCS036
00121      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      DPKCS036
00122                                                     COMP SYNC.    DPKCS036
00123      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      DPKCS036
00124                                                     COMP SYNC.    DPKCS036
00125      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      DPKCS036
00126                                                     COMP SYNC.    DPKCS036
00127      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      DPKCS036
00128                                                     COMP SYNC.    DPKCS036
00129      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      DPKCS036
00130                                                     COMP SYNC.    DPKCS036
00131      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      DPKCS036
00132                                                     COMP SYNC.    DPKCS036
00133      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      DPKCS036
00134                                                     COMP SYNC.    DPKCS036
00135      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      DPKCS036
00136                                                     COMP SYNC.    DPKCS036
00137      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      DPKCS036
00138                                                     COMP SYNC.    DPKCS036
00139      05  PV-SQL-SUB                     PIC S9(04)  VALUE +0      DPKCS036
00140                                                     COMP SYNC.    DPKCS036
00141      05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      DPKCS036
00142                                                     COMP SYNC.    DPKCS036
00143 ******************************************************************DPKCS036
00144 **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **DPKCS036
00145 **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **DPKCS036
00146 **  APPLICATION-SPECIFIC COMMAREA.                              **DPKCS036
00147 ******************************************************************DPKCS036
019606 01  LQW-LIST-QUEUE-WORK-AREA.                                    POKCS088
019607     05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.  POKCS088
019608     05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES           POKCS088
019609                                        INDEXED BY LQW-IDX.       POKCS088
019610         10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.       POKCS088
019616         10  LQW-PRE-TKT-CMNT-DESC      PIC  X(50).                     *2
019620                                                                  POKCS088
00163      EJECT                                                        DPKCS036
00164 *---------------------------------------------------------------* DPKCS036
00165 *  RECORD LAYOUT FOR THE INCOMING TEMP STORAGE QUEUE            * DPKCS036
00166 *---------------------------------------------------------------* DPKCS036
00167                                                                   DPKCS036
00168      COPY VLWS001.                                                DPKCS036
00169                                                                   DPKCS036
00170      EJECT                                                        DPKCS036
00171 *---------------------------------------------------------------* DPKCS036
00172 *    MAP LAYOUT                                                 * DPKCS036
00173 *---------------------------------------------------------------* DPKCS036
00174                                                                   DPKCS036
00175      COPY VLKM503.                                                DPKCS036
00176 *                                                                 DPKCS036
023400                                                                     CL**1
023500 01  MR-OCCURS-LAYOUT          REDEFINES  VL503AO.                   CL**1
023600     05  FILLER                PIC X(42).                               *1
023700     05  MR-SCROLL-AREA OCCURS 12 TIMES.                             CL**1
024210         10  MR-TEXTL          PIC S9(04) COMP.                      CL**1
024220         10  MR-TEXTA          PIC  X(01).                           CL**1
024230         10  MR-TEXTC          PIC  X(01).                           CL**1
024240         10  MR-TEXTH          PIC  X(01).                           CL**1
024250         10  MR-TEXT           PIC  X(50).                           CL**1
00216      EJECT                                                        DPKCS036
00217 *---------------------------------------------------------------* DPKCS036
00218 *    ATTRIBUTE SETTINGS COPYBOOK.                               * DPKCS036
00219 *---------------------------------------------------------------* DPKCS036
00220                                                                   DPKCS036
00221      COPY DPWS015.                                                DPKCS036
00222                                                                   DPKCS036
00223 *---------------------------------------------------------------* DPKCS036
00224 *    FUNCTION KEYS COPYBOOK.                                    * DPKCS036
00225 *---------------------------------------------------------------* DPKCS036
00226                                                                   DPKCS036
00227      COPY DPWS016.                                                DPKCS036
00228                                                                   DPKCS036
00229 *---------------------------------------------------------------* DPKCS036
00230 *    NUMERIC EDIT LAYOUT.                                       * DPKCS036
00231 *---------------------------------------------------------------* DPKCS036
00232                                                                   DPKCS036
00233      COPY DPWS010I.                                               DPKCS036
00234  EJECT                                                            DPKCS036
00235 *---------------------------------------------------------------* DPKCS036
00236 *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          * DPKCS036
00237 *---------------------------------------------------------------* DPKCS036
00238                                                                   DPKCS036
00239      COPY DPWS030.                                                DPKCS036
CICS       COPY DPWS930.                                                DPKCS036
00240  EJECT                                                            DPKCS036
00241 *---------------------------------------------------------------* DPKCS036
00242 *    STANDARD COMMAREA.                                         * DPKCS036
00243 *---------------------------------------------------------------* DPKCS036
00244                                                                   DPKCS036
00245      COPY DPWS020.                                                DPKCS036
00246      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                DPKCS036
00247                                                                   DPKCS036
00248 *---------------------------------------------------------------* DPKCS036
00249 * INTER-APPLICATION COMMAREA.                                   * DPKCS036
00250 *   VLWS002 - IS THE INPUT LAYOUT WHEN COMING FROM THE CRIT.    * DPKCS036
00251 *             SPEC SCREEN.                                      * DPKCS036
00254 *---------------------------------------------------------------* DPKCS036
00255                                                                   DPKCS036
00256          10  INTER-APPL-COMM-AREA       PIC X(200).               DPKCS036
00257      COPY VLWS002.                                                DPKCS036
00259 *---------------------------------------------------------------* DPKCS036
00260 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * DPKCS036
00261 *---------------------------------------------------------------* DPKCS036
00262          10  ASC-SPECIFIC-COMMAREA.                               DPKCS036
00263              15  ASC-LIST-KEYS.                                   DPKCS036
00264                  20  ASC-KEY-MESSAGE-NUMBER          PIC S9(05)   DPKCS036
00265                                                      COMP-3.      DPKCS036
00266                  20  ASC-KEY-SYSTEM-ID               PIC  X(02).  DPKCS036
00267                  20  ASC-KEY-MESSAGE-TEXT            PIC  X(30).  DPKCS036
00268                  20  ASC-KEY-REVIEW-IND              PIC  X(01).  DPKCS036
013100             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                   CL**1
013101                 20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  POKCS088
013102                     88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   POKCS088
013110                     88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   POKCS088
013200                 20  ASC-ITEM-ARRAY.                                 CL**1
013300                                                                     CL**1
013400                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.           CL**1
013500                         30  ASC-LIST-ITEM-NUMBER    PIC S9(09)      CL**1
013600                                                     COMP-3.         CL**1
013720                         30  ASC-PRE-TKT-CMNT-DESC   PIC X(50).      CL**1
013800                                                                     CL**1
00299                                                                   DPKCS036
00300              15  ASC-BLK-SUB                         PIC S9(04)   DPKCS036
00301                                                           COMP    DPKCS036
00302                                                           SYNC.   DPKCS036
00303              15  ASC-ITEM-SUB                        PIC S9(04)   DPKCS036
00304                                                           COMP    DPKCS036
00305                                                           SYNC.   DPKCS036
00306              15  ASC-NUMBER-OF-COMM-TS-ITEMS         PIC S9(09)   DPKCS036
00307                                                           COMP-3. DPKCS036
00308              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   DPKCS036
00309                                                           COMP-3. DPKCS036
00310              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   DPKCS036
00311                                                           COMP-3. DPKCS036
00312              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   DPKCS036
00313                                                           COMP-3. DPKCS036
00314              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   DPKCS036
00315                                                           COMP-3. DPKCS036
00316              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   DPKCS036
00317                                                           COMP    DPKCS036
00318                                                           SYNC.   DPKCS036
00319              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   DPKCS036
00320                                                           COMP-3. DPKCS036
00321              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   DPKCS036
00322                                                           COMP-3. DPKCS036
00323              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  DPKCS036
00324                  88  ASC-ITEMS-LEFT-ON-TS            VALUE 'Y'.   DPKCS036
00325                  88  ASC-NO-ITEMS-LEFT-ON-TS         VALUE 'N'.   DPKCS036
00326              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  DPKCS036
00327                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   DPKCS036
00328                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   DPKCS036
00329              15  ASC-LIST-QUEUE-NAME.                             DPKCS036
00330                  20  ASC-LIST-TERM-ID                PIC  X(04).  DPKCS036
00331                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   DPKCS036
00332                                                           COMP-3. DPKCS036
00333                  20  ASC-LIST-SEQ                    PIC  9(01).  DPKCS036
00329              15  ASC-COMM-QUEUE-NAME                 PIC X(08).   DPKCS036
00345              15  ASC-START-ITEM-REQUEST.                          DPKCS036
00346                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  DPKCS036
00347      EJECT                                                        DPKCS036
00348 *---------------------------------------------------------------* DPKCS036
00349 *    ABEND PROCESSING COPYBOOK.                                 * DPKCS036
00350 *---------------------------------------------------------------* DPKCS036
00351                                                                   DPKCS036
00352      COPY DPWS013.                                                DPKCS036
00353                                                                   DPKCS036
00354  EJECT                                                            DPKCS036
00431                                                                   DPKCS036
00432  EJECT                                                            DPKCS036
00433  LINKAGE SECTION.                                                 DPKCS036
00434                                                                   DPKCS036
00435  01  DFHCOMMAREA.                                                 DPKCS036
00436      05  FILLER                         OCCURS  1 TO 4072 TIMES   DPKCS036
00437                                         DEPENDING ON EIBCALEN.    DPKCS036
00438          10  FILLER                     PIC X.                    DPKCS036
00439                                                                   DPKCS036
00440  EJECT                                                            DPKCS036
00441  PROCEDURE DIVISION.                                              DPKCS036
00442 ******************************************************************DPKCS036
00443 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **DPKCS036
00444 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **DPKCS036
00445 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **DPKCS036
00446 ******************************************************************DPKCS036
00447  0000-MAIN-MODULE.                                                DPKCS036
00448      INITIALIZE DP030-CICS-API-FIELDS.                            DPKCS036
00449      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       DPKCS036
00450      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          DPKCS036
00451      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         DPKCS036
00452      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       DPKCS036
00453      MOVE LENGTH OF VL503AI        TO DP030-MAP-LENGTH (1).       DPKCS036
00454      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     DPKCS036
00455      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS036
00456 *                                                                 DPKCS036
00457      PERFORM 1000-CONTROL-PROCESSING                              DPKCS036
00458 *                                                                 DPKCS036
00459      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     DPKCS036
00460      PERFORM 0001-CALL-CICS-ARCH-API.                             DPKCS036
00461                                                                   DPKCS036
00462 ******************************************************************DPKCS036
00463 **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **DPKCS036
00464 **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **DPKCS036
00465 ******************************************************************DPKCS036
00466  0001-CALL-CICS-ARCH-API.                                         DPKCS036
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
00468                                     DFHCOMMAREA                           
00469                                     DP030-CICS-API-FIELDS                 
00470                                     DP020-STANDARD-COMMAREA               
00471                                     VL503AI.                              
00472 *                                                                 DPKCS036
00473      IF  DP030-RC-CALL-SUCCESSFUL                                 DPKCS036
00474          CONTINUE                                                 DPKCS036
00475      ELSE                                                         DPKCS036
00476          SET DP013-NO-ROLLBACK                                    DPKCS036
00477              DP013-XCTL-DISPLAY-RESTART                           DPKCS036
00478              DP013-CICS-ABEND      TO TRUE                        DPKCS036
00479          MOVE '0001-CALL-CICS-ARCH-API'                           DPKCS036
00480                                    TO DP013-PARAGRAPH             DPKCS036
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00483          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      DPKCS036
00484          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
00485      END-IF.                                                      DPKCS036
00486 *----------------------------------------------------------------*DPKCS036
00487 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *DPKCS036
00488 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *DPKCS036
00489 *                                                                *DPKCS036
00490 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *DPKCS036
00491 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *DPKCS036
00492 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *DPKCS036
00493 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *DPKCS036
00494 *----------------------------------------------------------------*DPKCS036
00495                                                                   DPKCS036
00496  1000-CONTROL-PROCESSING.                                         DPKCS036
00497                                                                   DPKCS036
00498      EVALUATE TRUE                                                DPKCS036
00499          WHEN DP020-NEXT-ACT-INITIAL                              DPKCS036
00500              INITIALIZE ASC-SPECIFIC-COMMAREA                     DPKCS036
00501              MOVE EIBTRMID         TO ASC-LIST-TERM-ID            DPKCS036
00503              MOVE DP020-SRC-TASK-NUMBER                           DPKCS036
00504                                    TO ASC-LIST-TASK-NUMBER        DPKCS036
00506              MOVE PC-LIST-QUEUE-SEQ                               DPKCS036
00507                                    TO ASC-LIST-SEQ                DPKCS036
00509              PERFORM 1100-PROCESS-INTER-APPL-COMM                 DPKCS036
00510 ***-----$TEMPLATE NOTE$-------------------------------------------DPKCS036
00511 *** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,DPKCS036
00512 *** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO  DPKCS036
00513 *** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.DPKCS036
00514 ***---------------------------------------------------------------DPKCS036
00515              IF  PS-COMMAREA-NOT-VALID                            DPKCS036
00516 ***              MOVE PC-TSYMSG-99901                             DPKCS036
00517 ***                                TO DP020-MSG-NUMBER            DPKCS036
00518 ***              SET DP020-MSG-FATAL                              DPKCS036
00519 ***                                TO TRUE                        DPKCS036
00520                  SET DP020-NEXT-ACT-APPL-ERROR                    DPKCS036
00521                                    TO TRUE                        DPKCS036
00522              ELSE                                                 DPKCS036
00523                  PERFORM 4000-BUILD-INITIAL-PANEL                 DPKCS036
00524              END-IF                                               DPKCS036
00525                                                                   DPKCS036
00526          WHEN DP020-NEXT-ACT-READ-MAP                             DPKCS036
00527              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        DPKCS036
00528                                    TO PV-TOP-ITEM                 DPKCS036
00529              PERFORM 2000-PROCESS-PANEL                           DPKCS036
00530                                                                   DPKCS036
00531          WHEN DP020-NEXT-ACT-RETURN                               DPKCS036
00532              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        DPKCS036
00533                                    TO PV-TOP-ITEM                 DPKCS036
00534              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    DPKCS036
00536              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS036
00537              PERFORM 3000-EDIT-DATA-IN-COMMAREA                      CL**2
00538                                                                   DPKCS036
00539          WHEN OTHER                                               DPKCS036
00540              SET DP013-LOGIC-ABEND TO TRUE                        DPKCS036
00541              SET DP013-NO-ROLLBACK TO TRUE                        DPKCS036
00542              MOVE '1000-CONTROL-PROCESSING'                       DPKCS036
00543                                    TO DP013-PARAGRAPH             DPKCS036
00544              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    DPKCS036
00545                                    TO DP013-MESSAGE-TEXT(1)       DPKCS036
00546              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS036
00547      END-EVALUATE.                                                DPKCS036
00548                                                                   DPKCS036
00549  EJECT                                                            DPKCS036
00550 *----------------------------------------------------------------*DPKCS036
00551 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *DPKCS036
00552 * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *DPKCS036
00553 *----------------------------------------------------------------*DPKCS036
00554                                                                   DPKCS036
00555  1100-PROCESS-INTER-APPL-COMM.                                    DPKCS036
00556                                                                   DPKCS036
00557      SET PS-COMMAREA-VALID         TO TRUE.                       DPKCS036
00558      MOVE SPACE                    TO ASC-START-ITEM-REQUEST.     DPKCS036
00559      IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT          DPKCS036
042100             MOVE VL002-NUMBER-OF-COMM-TS-ITEMS                         *1
042200                                   TO ASC-NUMBER-OF-COMM-TS-ITEMS    CL**1
042100             MOVE VL002-COMMENT-QUEUE-NAME                              *1
042200                                   TO ASC-COMM-QUEUE-NAME            CL**1
00576      ELSE                                                         DPKCS036
00557          SET PS-COMMAREA-NOT-VALID     TO TRUE                    DPKCS036
00576      END-IF.                                                      DPKCS036
00577                                                                   DPKCS036
00578  EJECT                                                            DPKCS036
00579 *----------------------------------------------------------------*DPKCS036
00580 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *DPKCS036
00581 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *DPKCS036
00582 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *DPKCS036
00583 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *DPKCS036
00584 * FUNCTION KEYS CAN GET THIS FAR.                                *DPKCS036
00585 *----------------------------------------------------------------*DPKCS036
00586                                                                   DPKCS036
00587  2000-PROCESS-PANEL.                                              DPKCS036
00588      EVALUATE TRUE                                                DPKCS036
00589          WHEN DP020-SRC-AID = DP016-CLEAR                         DPKCS036
00590              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    DPKCS036
00591              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   DPKCS036
00592              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS036
00593                                                                   DPKCS036
00594          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    DPKCS036
00603              PERFORM 2050-DELETE-LIST-QUEUE                       DPKCS036
00604              PERFORM 4000-BUILD-INITIAL-PANEL                     DPKCS036
00605                                                                   DPKCS036
00614          WHEN OTHER                                               DPKCS036
00615              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 DPKCS036
00616              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   DPKCS036
00617              IF  PS-NO-ERROR                                      DPKCS036
00618                  SET DP030-CONTINUE-GO                            DPKCS036
00619                                  TO TRUE                          DPKCS036
00620                  PERFORM 2100-CHECK-FUNCTION-KEY                  DPKCS036
00621              ELSE                                                 DPKCS036
00622                  SET DP030-OVERRIDE-GO                            DPKCS036
00623                                  TO TRUE                          DPKCS036
00624              END-IF                                               DPKCS036
00625      END-EVALUATE.                                                DPKCS036
00626  EJECT                                                            DPKCS036
00627 *----------------------------------------------------------------*DPKCS036
00628 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *DPKCS036
00629 *----------------------------------------------------------------*DPKCS036
00630  2050-DELETE-LIST-QUEUE.                                          DPKCS036
00631                                                                   DPKCS036
00632      EXEC CICS                                                    DPKCS036
00633          DELETEQ TS                                               DPKCS036
00634              QUEUE (ASC-LIST-QUEUE-NAME)                          DPKCS036
00635              RESP  (PV-CICS-RESP)                                 DPKCS036
00636      END-EXEC.                                                    DPKCS036
00637                                                                   DPKCS036
00638      IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                       DPKCS036
00639         (PV-CICS-RESP = DFHRESP(QIDERR))                          DPKCS036
00640          CONTINUE                                                 DPKCS036
00641      ELSE                                                         DPKCS036
00642          SET DP013-CICS-ABEND      TO TRUE                        DPKCS036
00643          SET DP013-NO-ROLLBACK     TO TRUE                        DPKCS036
00644                                                                   DPKCS036
00645          MOVE '2050-DELETE-LIST-QUEUE'                            DPKCS036
00646                                    TO DP013-PARAGRAPH             DPKCS036
00647          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    DPKCS036
00648                                    TO DP013-MESSAGE-TEXT (1)      DPKCS036
00649          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
00650      END-IF.                                                      DPKCS036
00651                                                                   DPKCS036
00652      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  DPKCS036
00653  EJECT                                                            DPKCS036
00654 *----------------------------------------------------------------*DPKCS036
00655 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*DPKCS036
00656 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *DPKCS036
00657 * CICS ARCHITECTURE API.                                         *DPKCS036
00658 *----------------------------------------------------------------*DPKCS036
00659  2100-CHECK-FUNCTION-KEY.                                         DPKCS036
00660      EVALUATE TRUE                                                DPKCS036
      * WHEN THE "INCLUDE" KEY IS PRESSED, DELETE THE OLD COMMENT QUEUE,        
      * AND CREATE A NEW ONE WITH WHATEVER IS IN THE LIST QUEUE.                
      * WE AREN'T USING THE LIST QUEUE, BECAUSE IT ALSO HAS THE PADDED          
      * LINES INCLUDED IN IT.                                                   
00661          WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)              DPKCS036
00666              PERFORM 5000-PREPARE-COMM-QUEUE                      DPKCS036
00675                                                                   DPKCS036
00676          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  DPKCS036
00677              MOVE +1               TO PV-TOP-ITEM                 DPKCS036
00678              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS036
00679                                                                   DPKCS036
00680          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   DPKCS036
00681              PERFORM 2110-BROWSE-BACKWARD                         DPKCS036
00682              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS036
00683                                                                   DPKCS036
00684          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   DPKCS036
00685              PERFORM 2120-BROWSE-FORWARD                          DPKCS036
00686              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 DPKCS036
00687                                                                   DPKCS036
00688          WHEN DP020-SRC-AID = DP016-ENTER                         DPKCS036
00689              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   DPKCS036
00690              IF  PS-ERROR                                         DPKCS036
00691                  CONTINUE                                         DPKCS036
00692              ELSE                                                 DPKCS036
00693                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             DPKCS036
00694              END-IF                                               DPKCS036
00695                                                                   DPKCS036
00696          WHEN OTHER                                               DPKCS036
00697              SET DP013-NO-ROLLBACK                                DPKCS036
00698                  DP013-XCTL-DISPLAY-RESTART                       DPKCS036
00699                  DP013-CICS-ABEND  TO TRUE                        DPKCS036
00700              MOVE '2100-CHECK-FUNCTION-KEY'                       DPKCS036
00701                                    TO DP013-PARAGRAPH             DPKCS036
00702              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      DPKCS036
00703                                    TO DP013-MESSAGE-TEXT (1)      DPKCS036
00704              PERFORM DP013-0000-PROCESS-ABEND                     DPKCS036
00705      END-EVALUATE.                                                DPKCS036
00706 *                                                                 DPKCS036
00707 *----------------------------------------------------------------*DPKCS036
00708 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *DPKCS036
00709 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *DPKCS036
00710 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *DPKCS036
00711 *    MESSAGE.                                                    *DPKCS036
00712 *----------------------------------------------------------------*DPKCS036
00713                                                                   DPKCS036
00714  2110-BROWSE-BACKWARD.                                            DPKCS036
00715      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    DPKCS036
00716          GIVING PV-TOP-ITEM.                                      DPKCS036
00717 *                                                                 DPKCS036
00718      IF  ((PV-TOP-ITEM < +1)                                      DPKCS036
00719        OR (PV-TOP-ITEM = +1))                                     DPKCS036
00720          MOVE +1                   TO PV-TOP-ITEM                 DPKCS036
00721 *        -- TOP OF LIST --                                        DPKCS036
00722          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            DPKCS036
00723          SET DP020-MSG-INFORMATIONAL TO TRUE                      DPKCS036
00724      END-IF.                                                      DPKCS036
00725                                                                   DPKCS036
00726  EJECT                                                            DPKCS036
00727 *----------------------------------------------------------------*DPKCS036
00728 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *DPKCS036
00729 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *DPKCS036
00730 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *DPKCS036
00731 *----------------------------------------------------------------*DPKCS036
00732                                                                   DPKCS036
00733  2120-BROWSE-FORWARD.                                             DPKCS036
00734                                                                   DPKCS036
00735      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              DPKCS036
00736          GIVING PV-TOP-ITEM.                                      DPKCS036
00737      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   DPKCS036
00738          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            DPKCS036
00739                                    TO PV-TOP-ITEM                 DPKCS036
00740 *        -- BOTTOM OF LIST --                                     DPKCS036
00741          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            DPKCS036
00742          SET DP020-MSG-INFORMATIONAL                              DPKCS036
00743                                    TO TRUE                        DPKCS036
00744      ELSE                                                         DPKCS036
00745          COMPUTE PV-BOTTOM-ITEM =                                 DPKCS036
00746              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 DPKCS036
00747          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            DPKCS036
00748 *            -- BOTTOM OF LIST --                                 DPKCS036
00749              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            DPKCS036
00750              SET DP020-MSG-INFORMATIONAL                          DPKCS036
00751                                    TO TRUE                        DPKCS036
00752          END-IF                                                   DPKCS036
00753      END-IF.                                                      DPKCS036
00754  EJECT                                                            DPKCS036
00755 *----------------------------------------------------------------*DPKCS036
00756 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *DPKCS036
00757 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *DPKCS036
00758 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *DPKCS036
00759 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *DPKCS036
00760 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *DPKCS036
00761 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *DPKCS036
00762 *----------------------------------------------------------------*DPKCS036
00763                                                                   DPKCS036
00764  2130-CHECK-FOR-JUMP-BROWSE.                                      DPKCS036
00765      SET PS-NO-ERROR               TO TRUE.                       DPKCS036
00766      IF (ASC-START-ITEM-REQUEST = SPACE)                          DPKCS036
00767          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            DPKCS036
00768                                    TO PV-TOP-ITEM                 DPKCS036
00769      ELSE                                                         DPKCS036
00770          MOVE ASC-START-ITEM-REQUEST                              DPKCS036
00771                                    TO DP010I-UNEDITED-FIELD       DPKCS036
00772          MOVE +5                   TO DP010I-MAXIMUM-DIGITS       DPKCS036
00773          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     DPKCS036
00774          SET DP010I-NEGATIVE-NOT-ALLOWED                          DPKCS036
00775                                    TO TRUE                        DPKCS036
263627         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263627              USING DP010I-NUMERIC-EDIT-AREA                              
00777                                                                   DPKCS036
00778          IF  NOT DP010I-ERROR-DETECTED                            DPKCS036
00779              MOVE DP010I-NUMERIC-FIELD                            DPKCS036
00780                                    TO  ASC-START-ITEM-REQUEST-N   DPKCS036
00781              IF  ASC-START-ITEM-REQUEST-N > ZERO                  DPKCS036
00782                  IF  ASC-START-ITEM-REQUEST-N >                   DPKCS036
00783                                           ASC-NUMBER-OF-ITEMS-READDPKCS036
00784                      MOVE SPACE TO ASC-START-ITEM-REQUEST         DPKCS036
00785                      COMPUTE PV-TOP-ITEM =                        DPKCS036
00786                              ASC-NUMBER-OF-ITEMS-READ             DPKCS036
00787                              - PC-ITEMS-PER-PANEL + 1             DPKCS036
00788                      IF  PV-TOP-ITEM < +1                         DPKCS036
00789                          MOVE +1   TO PV-TOP-ITEM                 DPKCS036
00790                          MOVE PC-TSYMSG-00070                     DPKCS036
00791                                    TO DP020-MSG-NUMBER            DPKCS036
00792                          SET DP020-MSG-INFORMATIONAL              DPKCS036
00793                                    TO TRUE                        DPKCS036
00794                      END-IF                                       DPKCS036
00795                  ELSE                                             DPKCS036
00796                      MOVE ASC-START-ITEM-REQUEST-N                DPKCS036
00797                                    TO PV-TOP-ITEM                 DPKCS036
00798                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      DPKCS036
00799                      COMPUTE PV-BOTTOM-ITEM =                     DPKCS036
00800                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     DPKCS036
00801                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READDPKCS036
00802 *                        -- BOTTOM OF LIST --                     DPKCS036
00803                          MOVE PC-TSYMSG-00070                     DPKCS036
00804                                    TO DP020-MSG-NUMBER            DPKCS036
00805                          SET DP020-MSG-INFORMATIONAL              DPKCS036
00806                                    TO TRUE                        DPKCS036
00807                      END-IF                                       DPKCS036
00808                  END-IF                                           DPKCS036
00809              ELSE                                                 DPKCS036
00810 *                --- VALUE OUT OF RANGE ----                      DPKCS036
00811                  MOVE PC-TSYMSG-00101                             DPKCS036
00812                                    TO DP020-MSG-NUMBER            DPKCS036
00813                  MOVE -1           TO ASTRTITL                    DPKCS036
00814                  SET DP030-SET-CURSOR-APPL-1                      DPKCS036
00815                                    TO TRUE                        DPKCS036
00816                  MOVE DP015-REVERSE                               DPKCS036
00817                                    TO ASTRTITH                    DPKCS036
00818                  MOVE DP015-RED    TO ASTRTITC                    DPKCS036
00819                  MOVE DP015-UNP-NUM-BRT-MDT                       DPKCS036
00820                                    TO ASTRTITA                    DPKCS036
00821                  SET DP020-MSG-FATAL                              DPKCS036
00822                                    TO TRUE                        DPKCS036
00823                  SET PS-ERROR      TO TRUE                        DPKCS036
00824              END-IF                                               DPKCS036
00825                                                                   DPKCS036
00826          ELSE                                                     DPKCS036
00827              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            DPKCS036
00828              MOVE -1               TO ASTRTITL                    DPKCS036
00829              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  DPKCS036
00830              MOVE DP015-REVERSE    TO ASTRTITH                    DPKCS036
00831              MOVE DP015-RED        TO ASTRTITC                    DPKCS036
00832              MOVE DP015-UNP-NUM-BRT-MDT                           DPKCS036
00833                                    TO ASTRTITA                    DPKCS036
00834              SET DP020-MSG-FATAL   TO TRUE                        DPKCS036
00835              SET PS-ERROR          TO TRUE                        DPKCS036
00836          END-IF                                                   DPKCS036
00837      END-IF.                                                      DPKCS036
00838  EJECT                                                            DPKCS036
01049 ******************************************************************DPKCS036
01050 ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **DPKCS036
01051 ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **DPKCS036
01052 ******************************************************************DPKCS036
01053  2200-MOVE-SCREEN-TO-COMMAREA.                                    DPKCS036
01054                                                                   DPKCS036
01055      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             DPKCS036
01056      MOVE +1                       TO PV-SUB.                     DPKCS036
01057 *                                                                 DPKCS036
01058      PERFORM 3100-SET-SUBSCRIPTS.                                 DPKCS036
01059      IF  ASTRTITL > ZERO                                          DPKCS036
01060          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST          DPKCS036
01061      ELSE                                                         DPKCS036
01062          IF  ASTRTITF = DP015-ERASE-EOF                           DPKCS036
01063              INITIALIZE ASC-START-ITEM-REQUEST                    DPKCS036
01064          END-IF                                                   DPKCS036
01065      END-IF.                                                      DPKCS036
01066      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          DPKCS036
01067          VARYING PV-SUB                                           DPKCS036
01068          FROM 1 BY 1                                              DPKCS036
01069          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      DPKCS036
01070 ******************************************************************DPKCS036
01071 ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**DPKCS036
01072 ******************************************************************DPKCS036
01073  2210-MOVE-LINES-TO-COMMAREA.                                     DPKCS036
01074      IF  MR-TEXTL (PV-SUB) > ZERO                                 DPKCS036
01075          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             DPKCS036
01076          MOVE MR-TEXT  (PV-SUB)                                   DPKCS036
01077                                    TO ASC-PRE-TKT-CMNT-DESC       DPKCS036
01078                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      DPKCS036
01079      ELSE                                                         DPKCS036
01080          IF  MR-TEXTA (PV-SUB) = DP015-ERASE-EOF                  DPKCS036
01081                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE     DPKCS036
01082                  MOVE SPACE        TO ASC-PRE-TKT-CMNT-DESC       DPKCS036
01083                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      DPKCS036
01084          END-IF                                                   DPKCS036
01085      END-IF.                                                      DPKCS036
01086      ADD +1                        TO ASC-ITEM-SUB.               DPKCS036
01087      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        DPKCS036
01088          ADD +1                    TO ASC-BLK-SUB                 DPKCS036
01089          MOVE +1                   TO ASC-ITEM-SUB                DPKCS036
01090      END-IF.                                                      DPKCS036
01091 ******************************************************************DPKCS036
01092 ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **DPKCS036
01093 ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **DPKCS036
01094 ******************************************************************DPKCS036
01095  3000-EDIT-DATA-IN-COMMAREA.                                      DPKCS036
01096      SET PS-NO-ERROR               TO TRUE.                       DPKCS036
01097      PERFORM 3200-RESET-ATTRIBUTES.                               DPKCS036
01120  EJECT                                                            DPKCS036
01121 *----------------------------------------------------------------*DPKCS036
01122 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *DPKCS036
01123 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*DPKCS036
01124 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *DPKCS036
01125 *                                                                *DPKCS036
01126 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *DPKCS036
01127 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *DPKCS036
01128 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *DPKCS036
01129 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *DPKCS036
01130 *----------------------------------------------------------------*DPKCS036
01131                                                                   DPKCS036
01132  3100-SET-SUBSCRIPTS.                                             DPKCS036
01133                                                                   DPKCS036
01134      COMPUTE ASC-ITEM-SUB =                                       DPKCS036
01135          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        DPKCS036
01136                                                                   DPKCS036
01137      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         DPKCS036
01138          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            DPKCS036
01139                                                                   DPKCS036
01140          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               DPKCS036
01141      ELSE                                                         DPKCS036
01142          MOVE +1                     TO ASC-BLK-SUB               DPKCS036
01143      END-IF.                                                      DPKCS036
01144 ******************************************************************DPKCS036
01145 ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **DPKCS036
01146 ******************************************************************DPKCS036
01147                                                                   DPKCS036
01148  3200-RESET-ATTRIBUTES.                                           DPKCS036
01149      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   DPKCS036
01150      MOVE DP015-GREEN              TO ASTRTITC.                   DPKCS036
01151      MOVE DP015-UNDERLINE          TO ASTRTITH.                   DPKCS036
01152 *                                                                 DPKCS036
01153      PERFORM                                                      DPKCS036
01154          VARYING PV-SUB                                           DPKCS036
01155          FROM 1 BY 1                                              DPKCS036
01156          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        DPKCS036
01157          MOVE DP015-UNP-ALP-NOR-OFF                               DPKCS036
01158                                    TO MR-TEXTA (PV-SUB)           DPKCS036
01159          MOVE DP015-GREEN          TO MR-TEXTC (PV-SUB)           DPKCS036
01160          MOVE DP015-UNDERLINE      TO MR-TEXTH (PV-SUB)           DPKCS036
01161      END-PERFORM.                                                 DPKCS036
01442  EJECT                                                            DPKCS036
01443 *----------------------------------------------------------------*DPKCS036
01444 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *DPKCS036
01445 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *DPKCS036
01446 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *DPKCS036
01447 *    DISPLAY THE PANEL TO USER.                                  *DPKCS036
01448 *----------------------------------------------------------------*DPKCS036
01449                                                                   DPKCS036
01450  4000-BUILD-INITIAL-PANEL.                                        DPKCS036
01451                                                                   DPKCS036
01452      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            DPKCS036
01453                                  ASC-BLOCK-ENTRIES (2).           DPKCS036
01454      MOVE +1                 TO ASC-BLK-SUB.                      DPKCS036
01455      SET ASC-ITEMS-LEFT-ON-TS                                     DPKCS036
               PS-COMMENTS-LEFT    TO TRUE.                             DPKCS036
01457                                                                   DPKCS036
01458      MOVE +0                 TO ASC-ITEM-SUB                      DPKCS036
01459                                 ASC-NUMBER-OF-ITEMS-READ          DPKCS036
01461                                 ASC-HIGHEST-BLOCK-WRITTEN         DPKCS036
01462                                 ASC-START-OF-SELECTED-RANGE       DPKCS036
01463                                 ASC-END-OF-SELECTED-RANGE         DPKCS036
01464                                 ASC-ITEM-AT-TOP-OF-PANEL          DPKCS036
01464                                 PV-NUMBER-OF-EXTRA-LINES.         DPKCS036
01468                                                                   DPKCS036
01469      PERFORM 4300-READ-ITEMS-FROM-TS.                             DPKCS036
01470                                                                   DPKCS036
01473      MOVE +1                   TO  PV-TOP-ITEM.                   DPKCS036
01474      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          DPKCS036
01475          SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    DPKCS036
01476          SET DP020-MSG-FATAL   TO  TRUE                           DPKCS036
01477          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               DPKCS036
01478      ELSE                                                         DPKCS036
01479          MOVE -1               TO MR-TEXTL (1)                    DPKCS036
01480          SET DP030-SET-CURSOR-APPL-1                              DPKCS036
01481                                TO TRUE                            DPKCS036
01482          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     DPKCS036
01483      END-IF.                                                      DPKCS036
01484  EJECT                                                            DPKCS036
01666  EJECT                                                            DPKCS036
01667 *----------------------------------------------------------------*DPKCS036
01668 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *DPKCS036
01669 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *DPKCS036
01670 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *DPKCS036
01671 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *DPKCS036
01672 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *DPKCS036
01673 *                                                                *DPKCS036
01674 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *DPKCS036
01675 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *DPKCS036
01676 *    UPON INITIALIZATION.                                        *DPKCS036
01677 *----------------------------------------------------------------*DPKCS036
01678                                                                   DPKCS036
01679  4300-READ-ITEMS-FROM-TS.                                         DPKCS036
01680                                                                   DPKCS036
01681      PERFORM                                                      DPKCS036
01682          VARYING PV-COMM-ITEM                                     DPKCS036
01683          FROM 1 BY 1                                              DPKCS036
01684          UNTIL PS-NO-COMMENTS-LEFT                                DPKCS036
01686                                                                   DPKCS036
01687          PERFORM 9100-READ-COMM-QUEUE                             DPKCS036
01688                                                                   DPKCS036
               SET VL001-IDX TO +1                                              
               PERFORM  UNTIL (PS-NO-COMMENTS-LEFT)                             
                           OR (VL001-IDX > VL001-MAX-ITEMS)                     
                                                                                
01690              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                DPKCS036
01691                  ADD +1            TO ASC-ITEM-SUB                DPKCS036
01692              ELSE                                                 DPKCS036
01693                  ADD +1            TO ASC-BLK-SUB                 DPKCS036
01694                  MOVE +1           TO ASC-ITEM-SUB                DPKCS036
01695                                                                   DPKCS036
01696                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         DPKCS036
01697                      MOVE +1       TO ASC-BLK-SUB                 DPKCS036
01698                      SET ASC-BLOCK-MODIFIED (1)                   DPKCS036
01699                                    TO TRUE                        DPKCS036
01700                      PERFORM 5100-WRITE-TEMP-STORAGE              DPKCS036
01701                      MOVE ASC-BLOCK-ENTRIES                       DPKCS036
01702                                (PC-MAX-BLOCKS-IN-ARRAY)           DPKCS036
01703                                 TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB) DPKCS036
01704                      ADD +1        TO ASC-BLK-SUB                 DPKCS036
01705                      INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)   DPKCS036
01706                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      DPKCS036
01707                                    TO TRUE                        DPKCS036
01708                  END-IF                                           DPKCS036
01709              END-IF                                               DPKCS036
01710              ADD +1                TO ASC-NUMBER-OF-ITEMS-READ    DPKCS036
01711              MOVE ASC-NUMBER-OF-ITEMS-READ                        DPKCS036
01712                                    TO ASC-LIST-ITEM-NUMBER        DPKCS036
01713                                       (ASC-BLK-SUB ASC-ITEM-SUB)  DPKCS036
      * IF THE LAST COMM RECORD WAS READ, OR IF THE LAST ITEM WITHIN            
      * A RECORD HAS BEEN REACHED, PAD THE LIST WITH SPACES UNTIL THE           
      * MAX HAS BEEN REACHED (VL001-IDX WILL NOT BE INCREMENTED, SO             
      * THE LOOP WILL CONTINUE UNTIL NO COMMENTS ARE LEFT )                     
                   IF ASC-ITEMS-LEFT-ON-TS                                      
                   AND VL001-ITEM (VL001-IDX) > +0                              
013720                 MOVE VL001-PRE-TKT-CMNT-DESC (VL001-IDX)            CL**1
013720                                   TO  ASC-PRE-TKT-CMNT-DESC         CL**1
01715                                        (ASC-BLK-SUB ASC-ITEM-SUB) DPKCS036
                       SET VL001-IDX UP BY +1                                   
                   ELSE                                                         
339842                ADD +1   TO  PV-NUMBER-OF-EXTRA-LINES             12550003
339843                IF PV-NUMBER-OF-EXTRA-LINES = PC-MAX-XTRA-LINES   12551003
339844                    SET PS-NO-COMMENTS-LEFT TO TRUE               12553008
339845                END-IF                                            12554007
339848                MOVE SPACES        TO  ASC-PRE-TKT-CMNT-DESC      12590003
01715                                        (ASC-BLK-SUB ASC-ITEM-SUB) DPKCS036
                   END-IF                                                       
01726          END-PERFORM                                              DPKCS036
01726      END-PERFORM.                                                 DPKCS036
01727                                                                   DPKCS036
01728      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          DPKCS036
01729          MOVE +1               TO ASC-BLK-SUB                     DPKCS036
01730          PERFORM 5100-WRITE-TEMP-STORAGE                          DPKCS036
01731          ADD +1                TO ASC-BLK-SUB                     DPKCS036
01732          PERFORM 5100-WRITE-TEMP-STORAGE                          DPKCS036
01733                                                                   DPKCS036
01734          MOVE 1                TO ASC-BLK-SUB                     DPKCS036
01735                                   PV-BLOCK-NUMBER                 DPKCS036
01736          PERFORM 4410-READ-TEMP-STORAGE                           DPKCS036
01737          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             DPKCS036
01738              MOVE 2            TO ASC-BLK-SUB                     DPKCS036
01739                                   PV-BLOCK-NUMBER                 DPKCS036
01740              PERFORM 4410-READ-TEMP-STORAGE                       DPKCS036
01741          END-IF                                                   DPKCS036
01742      END-IF.                                                      DPKCS036
01802  EJECT                                                            DPKCS036
01803 *----------------------------------------------------------------*DPKCS036
01804 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *DPKCS036
01805 *----------------------------------------------------------------*DPKCS036
01806                                                                   DPKCS036
01807  4400-MOVE-COMMAREA-TO-SCREEN.                                    DPKCS036
01808      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             DPKCS036
01809          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          DPKCS036
01810          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          DPKCS036
01811              MOVE +1    TO ASC-BLK-SUB                            DPKCS036
01812              PERFORM 5100-WRITE-TEMP-STORAGE                      DPKCS036
01813              ADD +1     TO ASC-BLK-SUB                            DPKCS036
01814              PERFORM 5100-WRITE-TEMP-STORAGE                      DPKCS036
01815              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             DPKCS036
01816                  GIVING PV-BLOCK-NUMBER                           DPKCS036
01817                  REMAINDER PV-REMAINDER                           DPKCS036
01818              IF  PV-REMAINDER > ZERO                              DPKCS036
01819                  ADD +1 TO PV-BLOCK-NUMBER                        DPKCS036
01820              END-IF                                               DPKCS036
01821              MOVE +1    TO ASC-BLK-SUB                            DPKCS036
01822              PERFORM 4410-READ-TEMP-STORAGE                       DPKCS036
01823              ADD +1     TO ASC-BLK-SUB                            DPKCS036
01824                            PV-BLOCK-NUMBER                        DPKCS036
01825              PERFORM 4410-READ-TEMP-STORAGE                       DPKCS036
01826      END-IF.                                                      DPKCS036
01827      MOVE +1            TO ASC-BLK-SUB.                           DPKCS036
01828      COMPUTE ASC-ITEM-SUB =                                       DPKCS036
01829          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              DPKCS036
01830          + PC-ITEMS-PER-PANEL).                                   DPKCS036
01831 *                                                                 DPKCS036
01832      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        DPKCS036
01833          ADD +1         TO ASC-BLK-SUB                            DPKCS036
01834          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            DPKCS036
01835      END-IF.                                                      DPKCS036
01836 *                                                                 DPKCS036
01837      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           DPKCS036
01838          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                DPKCS036
01840      PERFORM 4420-FORMAT-LIST-LINES                               DPKCS036
01841          VARYING PV-SUB                                           DPKCS036
01842          FROM PC-ITEMS-PER-PANEL BY -1                            DPKCS036
01843          UNTIL PV-SUB < +1.                                       DPKCS036
01844 *                                                                 DPKCS036
01845      COMPUTE ASC-ITEMS-DISPLAYED =                                DPKCS036
01846          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          DPKCS036
01847      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL    DPKCS036
01848                                       ASTRTITO.                   DPKCS036
01849      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    DPKCS036
01850      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    DPKCS036
01853 *                                                                 DPKCS036
01854      IF  DP020-MSG-NUMBER-X = SPACE                               DPKCS036
01855          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  DPKCS036
01856 *            --- MORE ITEMS TO DISPLAY ----                       DPKCS036
01857              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            DPKCS036
01858              SET DP020-MSG-INFORMATIONAL TO TRUE                  DPKCS036
01859          END-IF                                                   DPKCS036
01860      END-IF.                                                      DPKCS036
01861 *----------------------------------------------------------------*DPKCS036
01862 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *DPKCS036
01863 *----------------------------------------------------------------*DPKCS036
01864                                                                   DPKCS036
01865  4410-READ-TEMP-STORAGE.                                          DPKCS036
01866                                                                   DPKCS036
01867      EXEC CICS                                                    DPKCS036
01868          READQ TS                                                 DPKCS036
01869              QUEUE (ASC-LIST-QUEUE-NAME)                          DPKCS036
01870              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               DPKCS036
01871              ITEM  (PV-BLOCK-NUMBER)                              DPKCS036
01872              RESP  (PV-CICS-RESP)                                 DPKCS036
01873      END-EXEC.                                                    DPKCS036
01874                                                                   DPKCS036
01875      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       DPKCS036
01876        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     DPKCS036
01877          CONTINUE                                                 DPKCS036
01878      ELSE                                                         DPKCS036
01879          SET DP013-CICS-ABEND     TO TRUE                         DPKCS036
01880          SET DP013-NO-ROLLBACK    TO TRUE                         DPKCS036
01881          MOVE '4410-READ-TEMP-STORAGE'                            DPKCS036
01882                                   TO DP013-PARAGRAPH              DPKCS036
01883          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      DPKCS036
01884                                   TO DP013-MESSAGE-TEXT(1)        DPKCS036
01885          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
01886      END-IF.                                                      DPKCS036
01887                                                                   DPKCS036
01888      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             DPKCS036
01889 *                                                                 DPKCS036
01890      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         DPKCS036
01891          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               DPKCS036
01892      END-IF.                                                      DPKCS036
01893  EJECT                                                            DPKCS036
01894 *----------------------------------------------------------------*DPKCS036
01895 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *DPKCS036
01896 *----------------------------------------------------------------*DPKCS036
01897                                                                   DPKCS036
01898  4420-FORMAT-LIST-LINES.                                          DPKCS036
01899      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      DPKCS036
01900          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                DPKCS036
01901          MOVE DP015-ASK-DRK-OFF  TO MR-TEXTA (PV-SUB)             DPKCS036
01902          MOVE DP015-HL-OFF       TO MR-TEXTH (PV-SUB)             DPKCS036
01903          MOVE SPACE              TO MR-TEXT  (PV-SUB)             DPKCS036
01909      ELSE                                                         DPKCS036
01918 *                                                                 DPKCS036
01919          IF  DP030-SET-CURSOR-TRAILER                             DPKCS036
01923                 SET DP030-SET-CURSOR-APPL-1 TO TRUE               DPKCS036
01924                 MOVE -1          TO MR-TEXTL (PV-SUB)             DPKCS036
01926          END-IF                                                   DPKCS036
01927 *                                                                 DPKCS036
01928          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           DPKCS036
01929              MOVE ASC-PRE-TKT-CMNT-DESC                           DPKCS036
                                   (ASC-BLK-SUB ASC-ITEM-SUB)           DPKCS036
01930                                  TO MR-TEXT (PV-SUB)              DPKCS036
01939          END-IF                                                   DPKCS036
01940      END-IF.                                                      DPKCS036
01941      SUBTRACT +1 FROM ASC-ITEM-SUB                                DPKCS036
01942      IF  ASC-ITEM-SUB < + 1                                       DPKCS036
01943          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  DPKCS036
01944          MOVE +1                 TO ASC-BLK-SUB                   DPKCS036
01945      END-IF.                                                      DPKCS036
01997  EJECT                                                            DPKCS036
01998 *----------------------------------------------------------------*DPKCS036
01999 * PREPARE THE COMMENT TEMP STORAGE QUEUE. MOVE ALL THE COMMENTS  *DPKCS036
02000 * FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT FOR THE COMMENT *DPKCS036
02001 * QUEUE.                                                         *DPKCS036
02002 *                                                                *DPKCS036
02003 * FIRST, DELETE THE COMMENT QUEUE - NEXT LOAD THE COMMENTS FROM  *DPKCS036
02004 * THE LIST QUEUE TO THE COMMENT QUEUE, WRITING COMMENT QUEUE     *DPKCS036
02005 * ITEMS WHEN THE ARRAY IF FULL                                   *DPKCS036
02006 *----------------------------------------------------------------*DPKCS036
02007                                                                   DPKCS036
02008  5000-PREPARE-COMM-QUEUE.                                         DPKCS036
02009                                                                   DPKCS036
02010      MOVE +1                   TO  ASC-BLK-SUB.                   DPKCS036
02011      PERFORM 5100-WRITE-TEMP-STORAGE.                             DPKCS036
02012      ADD +1                    TO  ASC-BLK-SUB.                   DPKCS036
02013      PERFORM 5100-WRITE-TEMP-STORAGE.                             DPKCS036
02014                                                                   DPKCS036
           PERFORM 9300-DELETE-COMM-QUEUE                                       
02019                                                                   DPKCS036
02020      SET VL001-IDX             TO  1.                             DPKCS036
02021      MOVE ZERO                 TO VL002-NUMBER-OF-COMM-RECS       DPKCS036
                                        VL002-NUMBER-OF-COMM-TS-ITEMS.  DPKCS036
                                                                                
02022      INITIALIZE VL001-SELECTED-ITEM-ARRAY.                        DPKCS036
02023                                                                   DPKCS036
02024      PERFORM 5200-FILL-COMM-QUEUE-RECORD                          DPKCS036
02025              VARYING PV-ITEM                                      DPKCS036
02026              FROM 1 BY 1                                          DPKCS036
02027              UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     DPKCS036
02028                                                                   DPKCS036
02029      IF  VL001-IDX > 1                                            DPKCS036
02031          ADD +1                TO VL002-NUMBER-OF-COMM-TS-ITEMS   DPKCS036
               MOVE VL002-NUMBER-OF-COMM-TS-ITEMS                               
                                     TO PV-COMM-ITEM                            
02030          PERFORM 9200-WRITE-COMM-QUEUE                            DPKCS036
02032      END-IF.                                                      DPKCS036
02033  EJECT                                                            DPKCS036
02034 *----------------------------------------------------------------*DPKCS036
02035 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *DPKCS036
02036 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *DPKCS036
02037 *----------------------------------------------------------------*DPKCS036
02038                                                                   DPKCS036
02039  5100-WRITE-TEMP-STORAGE.                                         DPKCS036
02040      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     DPKCS036
02041                                    TO PV-LIST-ITEM-NUMBER.        DPKCS036
02042      COMPUTE PV-BLOCK-NUMBER =                                    DPKCS036
02043          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    DPKCS036
02044                                                                   DPKCS036
02045      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             DPKCS036
02046      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             DPKCS036
02047          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      DPKCS036
02048              PERFORM 5105-REWRITE-ASC-LISTQ                       DPKCS036
02049          END-IF                                                   DPKCS036
02050      ELSE                                                         DPKCS036
02051          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       DPKCS036
02052          ASC-HIGHEST-BLOCK-WRITTEN + 1                            DPKCS036
02053          PERFORM 5110-WRITE-ASC-LISTQ                             DPKCS036
02054          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   DPKCS036
02055      END-IF.                                                      DPKCS036
02056                                                                   DPKCS036
02057      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             DPKCS036
02058 ******************************************************************DPKCS036
02059 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **DPKCS036
02060 ** SPECIFIC COMMAREA.                                           **DPKCS036
02061 ******************************************************************DPKCS036
02062  5105-REWRITE-ASC-LISTQ.                                          DPKCS036
02063      EXEC CICS                                                    DPKCS036
02064          WRITEQ TS                                                DPKCS036
02065              QUEUE (ASC-LIST-QUEUE-NAME)                          DPKCS036
02066              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               DPKCS036
02067              ITEM  (PV-BLOCK-NUMBER)                              DPKCS036
02068              REWRITE                                              DPKCS036
02069              RESP  (PV-CICS-RESP)                                 DPKCS036
02070      END-EXEC.                                                    DPKCS036
02071                                                                   DPKCS036
02072      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           DPKCS036
02073          CONTINUE                                                 DPKCS036
02074      ELSE                                                         DPKCS036
02075          SET DP013-CICS-ABEND            TO TRUE                  DPKCS036
02076          SET DP013-NO-ROLLBACK           TO TRUE                  DPKCS036
02077          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       DPKCS036
02078          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     DPKCS036
02079                                          TO DP013-MESSAGE-TEXT(1) DPKCS036
02080          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
02081      END-IF.                                                      DPKCS036
02082                                                                   DPKCS036
02083 ******************************************************************DPKCS036
02084 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **DPKCS036
02085 ** SPECIFIC COMMAREA.                                           **DPKCS036
02086 ******************************************************************DPKCS036
02087  5110-WRITE-ASC-LISTQ.                                            DPKCS036
02088      EXEC CICS                                                    DPKCS036
02089          WRITEQ TS                                                DPKCS036
02090              QUEUE (ASC-LIST-QUEUE-NAME)                          DPKCS036
02091              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               DPKCS036
02092              ITEM  (PV-BLOCK-NUMBER)                              DPKCS036
02093              RESP  (PV-CICS-RESP)                                 DPKCS036
02094      END-EXEC.                                                    DPKCS036
02095      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      DPKCS036
02096          SET DP013-CICS-ABEND      TO TRUE                        DPKCS036
02097          SET DP013-NO-ROLLBACK     TO TRUE                        DPKCS036
02098          MOVE '5110-WRITE-ASC-LISTQ'                              DPKCS036
02099                                    TO DP013-PARAGRAPH             DPKCS036
02100          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           DPKCS036
02101                                    TO DP013-MESSAGE-TEXT(1)       DPKCS036
02102          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
02103      END-IF.                                                      DPKCS036
02104  EJECT                                                            DPKCS036
02105 *----------------------------------------------------------------*DPKCS036
02106 * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *DPKCS036
02107 * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *DPKCS036
02108 *----------------------------------------------------------------*DPKCS036
02109                                                                   DPKCS036
02110  5200-FILL-COMM-QUEUE-RECORD.                                     DPKCS036
02111                                                                   DPKCS036
02112      PERFORM 6112-READ-LIST-QUEUE.                                DPKCS036
02113                                                                   DPKCS036
02114      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       DPKCS036
02115          PERFORM                                                  DPKCS036
02116          VARYING LQW-IDX                                          DPKCS036
02117          FROM 1 BY 1                                              DPKCS036
02118          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            DPKCS036
02119            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          DPKCS036
02120                                                                   DPKCS036
02121              IF  LQW-PRE-TKT-CMNT-DESC (LQW-IDX) > SPACES         DPKCS036
                       ADD +1 TO VL002-NUMBER-OF-COMM-RECS                      
                       MOVE VL002-NUMBER-OF-COMM-RECS                           
                                         TO VL001-ITEM (VL001-IDX)              
                                            VL001-SEQ-NBR (VL001-IDX)           
02122                  MOVE LQW-PRE-TKT-CMNT-DESC (LQW-IDX)             DPKCS036
02123                                    TO VL001-PRE-TKT-CMNT-DESC     DPKCS036
                                             (VL001-IDX)                DPKCS036
02128                  SET VL001-IDX UP BY 1                            DPKCS036
02129                  IF  VL001-IDX > VL001-MAX-ITEMS                  DPKCS036
02131                      ADD +1     TO VL002-NUMBER-OF-COMM-TS-ITEMS   S0   36
                           MOVE VL002-NUMBER-OF-COMM-TS-ITEMS                   
                                      TO PV-COMM-ITEM                           
02130                      PERFORM 9200-WRITE-COMM-QUEUE                DPKCS036
02132                      INITIALIZE VL001-SELECTED-ITEM-ARRAY         DPKCS036
02133                      SET VL001-IDX TO  1                          DPKCS036
02134                  END-IF                                           DPKCS036
02135              END-IF                                               DPKCS036
02136          END-PERFORM                                              DPKCS036
02137      END-IF.                                                      DPKCS036
02138                                                                   DPKCS036
02189                                                                   DPKCS036
02335 *----------------------------------------------------------------*DPKCS036
02336 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*DPKCS036
02337 *----------------------------------------------------------------*DPKCS036
02338                                                                   DPKCS036
02339  6112-READ-LIST-QUEUE.                                            DPKCS036
02340                                                                   DPKCS036
02341      EXEC CICS                                                    DPKCS036
02342          READQ TS                                                 DPKCS036
02343              QUEUE (ASC-LIST-QUEUE-NAME)                          DPKCS036
02344              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     DPKCS036
02345              ITEM  (PV-ITEM)                                      DPKCS036
02346              RESP  (PV-CICS-RESP)                                 DPKCS036
02347      END-EXEC.                                                    DPKCS036
02348 *                                                                 DPKCS036
02349      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          DPKCS036
02350          CONTINUE                                                 DPKCS036
02351      ELSE                                                         DPKCS036
02352          SET DP013-CICS-ABEND        TO TRUE                      DPKCS036
02353          SET DP013-NO-ROLLBACK       TO TRUE                      DPKCS036
02354          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           DPKCS036
02355          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    DPKCS036
02356                                      TO DP013-MESSAGE-TEXT(1)     DPKCS036
02357          PERFORM DP013-0000-PROCESS-ABEND                         DPKCS036
02358      END-IF.                                                      DPKCS036
02359                                                                   DPKCS036
130430                                                                     CL**1
02335 *----------------------------------------------------------------*DPKCS036
02336 *    READ THE TEMPORARY STORAGE COMMENT QUEUE IN WORKING STORAGE.*DPKCS036
02337 *----------------------------------------------------------------*DPKCS036
130500 9100-READ-COMM-QUEUE.                                               CL**1
130600                                                                     CL**8
130700     EXEC CICS                                                       CL**1
130800         READQ TS                                                    CL**1
130900             QUEUE (ASC-COMM-QUEUE-NAME)                             CL**1
131000             INTO  (VL001-COMMENTS-SEL-REC)                             *1
131100             ITEM  (PV-COMM-ITEM)                                    CL**1
131200             RESP  (PV-CICS-RESP)                                    CL**1
131300     END-EXEC.                                                       CL**1
131400                                                                     CL**1
131500     IF (PV-CICS-RESP EQUAL DFHRESP(NORMAL)                          CL**1
131510     OR  PV-CICS-RESP EQUAL DFHRESP(QIDERR)                          CL**1
131520     OR  PV-CICS-RESP EQUAL DFHRESP(ITEMERR))                        CL**1
131600         CONTINUE                                                    CL**1
131700     ELSE                                                            CL**1
131800         SET DP013-LOGIC-ABEND                                       CL**1
131900             DP013-NO-ROLLBACK     TO TRUE                           CL**1
132000         MOVE '9100-READ-SEL-QUEUE'                                  CL**1
132100                                   TO DP013-PARAGRAPH                CL**1
132200         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'      CL**1
132300                                   TO DP013-MESSAGE-TEXT (1)         CL**1
132400         PERFORM DP013-0000-PROCESS-ABEND                            CL**1
132500     END-IF.                                                         CL**1
                                                                                
131520     IF  PV-CICS-RESP EQUAL DFHRESP(ITEMERR)                         CL**1
131510     OR  PV-CICS-RESP EQUAL DFHRESP(QIDERR)                          CL**1
01689          SET ASC-NO-ITEMS-LEFT-ON-TS   TO TRUE                    DPKCS036
131700     END-IF.                                                         CL**1
132501                                                                     CL**8
130430                                                                     CL**1
02335 *----------------------------------------------------------------*DPKCS036
02336 *  WRITE THE TEMPORARY STORAGE COMMENT QUEUE FROM WORKING STORAGE*DPKCS036
02337 *----------------------------------------------------------------*DPKCS036
132502 9200-WRITE-COMM-QUEUE.                                                 *1
132503                                                                     CL**8
132504     EXEC CICS                                                       CL**1
132505          WRITEQ TS                                                  CL**1
132506              QUEUE  (ASC-COMM-QUEUE-NAME)                           CL**1
132507              FROM   (VL001-COMMENTS-SEL-REC)                           *1
132508              ITEM   (PV-COMM-ITEM)                                  CL**1
132509              RESP   (PV-CICS-RESP)                                  CL**1
132511     END-EXEC.                                                       CL**1
132512                                                                     CL**1
132513     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                      CL**1
132514         SET DP013-CICS-ABEND                                        CL**1
132515             DP013-NO-ROLLBACK     TO TRUE                           CL**1
132516         MOVE '9200-WRITE-COMM-QUEUE'                                   *1
132517                                   TO DP013-PARAGRAPH                CL**1
132518         MOVE 'ATTEMPTING TO WRITE THE COMMENT TS QUEUE'             CL**1
132519                                   TO DP013-MESSAGE-TEXT (1)         CL**1
132520         PERFORM DP013-0000-PROCESS-ABEND                            CL**1
132530     END-IF.                                                         CL**1
132600                                                                     CL**8
056300*----------------------------------------------------------------*   CL**1
056400*    DELETE THE COMMENTS STORAGE QUEUE                           *   CL**1
056500*----------------------------------------------------------------*   CL**1
056600                                                                     CL**1
056700 9300-DELETE-COMM-QUEUE.                                             CL**1
056800                                                                     CL**1
056900     EXEC CICS                                                       CL**1
057000         DELETEQ TS                                                  CL**1
057100             QUEUE (ASC-COMM-QUEUE-NAME)                             CL**1
057200             RESP  (PV-CICS-RESP)                                    CL**1
057300     END-EXEC.                                                       CL**1
057400                                                                     CL**1
057500     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                          CL**1
057600        (PV-CICS-RESP = DFHRESP(QIDERR))                             CL**1
057700         CONTINUE                                                    CL**1
057800     ELSE                                                            CL**1
057900         SET DP013-CICS-ABEND TO TRUE                                CL**1
058000         SET DP013-NO-ROLLBACK TO TRUE                               CL**1
058100                                                                     CL**1
058200         MOVE '9300-DELETE-COMM-QUEUE'   TO DP013-PARAGRAPH          CL**1
058300         MOVE 'ERROR TRYING TO DELETE COMMENT TS QUEUE'              CL**1
058400                        TO DP013-MESSAGE-TEXT (1)                    CL**1
058500         PERFORM DP013-0000-PROCESS-ABEND                            CL**1
058600     END-IF.                                                         CL**1
058700                                                                     CL**1
02360  EJECT                                                            DPKCS036
02361 *----------------------------------------------------------------*DPKCS036
02362 *    ABEND PROCESSOR MODULE                                      *DPKCS036
02363 *----------------------------------------------------------------*DPKCS036
02364                                                                   DPKCS036
02365      COPY DPPD013.                                                DPKCS036
