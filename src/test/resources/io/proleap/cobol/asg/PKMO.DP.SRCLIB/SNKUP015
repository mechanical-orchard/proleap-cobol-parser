00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP015
00003  PROGRAM-ID.    SNKUP015.                                            LV003
00004                                                                   SNKUP015
00005  AUTHOR.        ANNE YEKO.                                        SNKUP015
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP015
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP015
00008  DATE-COMPILED.                                                   SNKUP015
00009                                                                   SNKUP015
00010 ******************************************************************SNKUP015
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP015
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP015
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASNPKG TABLE.          **SNKUP015
00014 ******************************************************************SNKUP015
00015 * CHANGED __/__/__ BY ____________                              **SNKUP015
00016 *   P_________  ________________________________________________**SNKUP015
00017 *               ________________________________________________**SNKUP015
00018 *                                                               **SNKUP015
00019 ******************************************************************SNKUP015
00020                                                                   SNKUP015
00021      EJECT                                                        SNKUP015
00022                                                                   SNKUP015
00023  ENVIRONMENT DIVISION.                                            SNKUP015
00024                                                                   SNKUP015
00025  INPUT-OUTPUT SECTION.                                            SNKUP015
00026                                                                   SNKUP015
00027  FILE-CONTROL.                                                    SNKUP015
00028                                                                   SNKUP015
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP015
00030             ASSIGN TO INP01.                                      SNKUP015
00031                                                                   SNKUP015
00032  DATA DIVISION.                                                   SNKUP015
00033                                                                   SNKUP015
00034  FILE SECTION.                                                    SNKUP015
00035                                                                   SNKUP015
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP015
00037      RECORDING MODE IS F                                          SNKUP015
00038      LABEL RECORDS ARE STANDARD                                   SNKUP015
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP015
00041                                                                   SNKUP015
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP015
00044      EJECT                                                        SNKUP015
00045  WORKING-STORAGE SECTION.                                         SNKUP015
00046                                                                   SNKUP015
00047  01  FILLER.                                                      SNKUP015
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP015
00049          ' ***** WORKING STORAGE PGM=SNKUP015 '.                  SNKUP015
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP015
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP015
00052          ' LOCATION='.                                            SNKUP015
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP015
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP015
00055          ' *****'.                                                SNKUP015
00056                                                                   SNKUP015
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP015
00058                                              COMP SYNC.           SNKUP015
00059                                                                   SNKUP015
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP015
00061                                                                   SNKUP015
00062      EJECT                                                        SNKUP015
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP015
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP015
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP015
00066                                                                   SNKUP015
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP015
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP015
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP015
00070                                                                   SNKUP015
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP015
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP015
00073                                              COMP-3.              SNKUP015
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP015
00075                                              COMP-3.              SNKUP015
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP015
00077                                              COMP-3.              SNKUP015
00078                                                                   SNKUP015
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP015
00080                                                                   SNKUP015
00081      COPY SNRD002I.                                               SNKUP015
00082      EJECT                                                        SNKUP015
00083 ******************************************************************SNKUP015
00084 *    DB2 DATABASE AREA                                            SNKUP015
00085 ******************************************************************SNKUP015
00086                                                                   SNKUP015
00087 *    ASN PACKAGE                                                  SNKUP015
00088                                                                   SNKUP015
00089      EXEC SQL                                                     SNKUP015
00090          INCLUDE TASNPKG                                          SNKUP015
00091      END-EXEC.                                                    SNKUP015
00092                                                                   SNKUP015
00093      EJECT                                                        SNKUP015
00094 *    SQL COMMUNICATION AREA                                       SNKUP015
00095                                                                   SNKUP015
00096      EXEC SQL                                                     SNKUP015
00097          INCLUDE SQLCA                                            SNKUP015
00098      END-EXEC.                                                    SNKUP015
00099      EJECT                                                        SNKUP015
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP015
00101                                                                   SNKUP015
00102      COPY SQLCA2.                                                 SNKUP015
00103      EJECT                                                        SNKUP015
00104                                                                   SNKUP015
00105 ***************************************************************** SNKUP015
00106  PROCEDURE DIVISION.                                              SNKUP015
00107 ***************************************************************** SNKUP015
00108                                                                   SNKUP015
00109  A000-MAIN-DRIVER.                                                SNKUP015
00110                                                                   SNKUP015
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP015
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP015
00113                                                                   SNKUP015
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP015
00115                                                                   SNKUP015
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP015
00117                                                                   SNKUP015
00118      PERFORM B100-DELETE-ROWS                                     SNKUP015
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP015
00120                                                                   SNKUP015
00121      DISPLAY 'SNKUP015 RECORDS READ:    ' PV-RECS-READ.           SNKUP015
00122      DISPLAY 'SNKUP015 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP015
00123                                                                   SNKUP015
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP015
00125                                                                   SNKUP015
00126      GOBACK.                                                      SNKUP015
00127                                                                   SNKUP015
00128      EJECT                                                        SNKUP015
00129                                                                   SNKUP015
00130 ***************************************************************** SNKUP015
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP015
00132 ***************************************************************** SNKUP015
00133  B100-DELETE-ROWS.                                                SNKUP015
00134                                                                   SNKUP015
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP015
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP015
00137                                  TO ABEND-PARAGRAPH.              SNKUP015
00138                                                                   SNKUP015
00139      PERFORM B300-DELETE-ASNPKG.                                  SNKUP015
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP015
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP015
00143      END-IF.                                                      SNKUP015
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP015
00145                                                                   SNKUP015
00146      EJECT                                                        SNKUP015
00147                                                                   SNKUP015
00148 ***************************************************************** SNKUP015
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP015
00150 ***************************************************************** SNKUP015
00151  B200-READ-INPUT-FILE.                                            SNKUP015
00152                                                                   SNKUP015
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP015
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP015
00155                                  TO ABEND-PARAGRAPH.              SNKUP015
00156                                                                   SNKUP015
00157      READ ASNHDR-DELETE-FILE                                      SNKUP015
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP015
00159          AT END                                                   SNKUP015
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP015
00161                                                                   SNKUP015
00162      IF NOT PS-END-OF-INPUT                                       SNKUP015
00163          ADD 1                   TO PV-RECS-READ                  SNKUP015
00164      END-IF.                                                      SNKUP015
00165                                                                   SNKUP015
00166      EJECT                                                        SNKUP015
00167                                                                   SNKUP015
00168 ***************************************************************** SNKUP015
00169 *    THIS PARAGRAPH DELETES ALL ASNPKG ROWS THAT ARE EQUAL TO     SNKUP015
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP015
00171 ***************************************************************** SNKUP015
00172  B300-DELETE-ASNPKG.                                              SNKUP015
00173                                                                   SNKUP015
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP015
00175      MOVE 'B300-DELETE-ASNPKG        '                            SNKUP015
00176                                  TO ABEND-PARAGRAPH.              SNKUP015
00177                                                                   SNKUP015
00178      EXEC SQL                                                     SNKUP015
00179          DELETE                                                   SNKUP015
00180          FROM   TASNPKG                                           SNKUP015
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP015
00183                                                                   SNKUP015
00184      IF SQLCODE = ZERO                                            SNKUP015
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP015
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP015
00187      ELSE                                                         SNKUP015
00188          IF SQLCODE = +100                                        SNKUP015
00189              CONTINUE                                             SNKUP015
00190          ELSE                                                     SNKUP015
00191              PERFORM Z998-SQL-ERROR                               SNKUP015
00192          END-IF                                                   SNKUP015
00193      END-IF.                                                      SNKUP015
00194                                                                   SNKUP015
00195      EJECT                                                        SNKUP015
00196                                                                   SNKUP015
00197 ***************************************************************** SNKUP015
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP015
00199 ***************************************************************** SNKUP015
00200  B400-COMMIT.                                                     SNKUP015
00201                                                                   SNKUP015
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP015
00203      MOVE 'B400-COMMIT               '                            SNKUP015
00204                                  TO ABEND-PARAGRAPH.              SNKUP015
00205                                                                   SNKUP015
00206      EXEC SQL                                                     SNKUP015
00207          COMMIT                                                   SNKUP015
00208      END-EXEC.                                                    SNKUP015
00209                                                                   SNKUP015
00210      IF SQLCODE = ZERO                                            SNKUP015
00211          CONTINUE                                                 SNKUP015
00212      ELSE                                                         SNKUP015
00213          PERFORM Z998-SQL-ERROR                                   SNKUP015
00214      END-IF.                                                      SNKUP015
00215                                                                   SNKUP015
00216      EJECT                                                        SNKUP015
00217                                                                   SNKUP015
00218 ***************************************************************** SNKUP015
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP015
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP015
00221 ***************************************************************** SNKUP015
00222  Z998-SQL-ERROR.                                                  SNKUP015
00223                                                                   SNKUP015
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP015
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP015
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP015
00227      DISPLAY '** PROGRAM          ** = SNKUP015'.                 SNKUP015
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP015
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP015
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP015
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP015
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP015
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP015
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP015
