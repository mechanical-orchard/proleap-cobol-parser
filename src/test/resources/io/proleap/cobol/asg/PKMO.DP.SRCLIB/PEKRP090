       IDENTIFICATION DIVISION.                                         00010006
       PROGRAM-ID.         PEKRP090.                                    00020099
       AUTHOR.             JIM ARENDT.                                  00030099
       INSTALLATION.       KOHLS DEPARTMENT STORES.                     00040006
       DATE-WRITTEN.       JANUARY 1998.                                00050099
       DATE-COMPILED.                                                   00060006
                                                                        00070006
      ******************************************************************00080006
      * THIS PROGRAM PRODUCES A MARKDOWN/MARKUP STORE AUDIT REPORT.    *00090099
      * THE PROGRAM READS THREE INPUT FILES:                           *00100099
      * 1) CONTROL FILE THAT CONTAINS A WEEK ENDING DATE THAT THE      *00110099
      *    PROGRAM IS BEING RUN FOR.                                   *00120099
      * 2) INPUT FILE THAT CONTAINS ALL THE RECORDS.  THIS FILE HAS    *00130099
      *    BEEN SORTED BY CATEGORY, TYPE CODE, REASON CODE AND STORE #.*00140099
      * 3) TOTAL FILE THAT CONTAINS THE TOTAL AMOUNT OF MARKDOWN/MARKUP*00150099
      *    FOR EACH CATEGORY/TYPE CODE.  THIS FILE WAS CREATED IN      *00160099
      *    PROGRAM PEKRP085.                                           *00170099
      *                                                                *00180099
      * THE PROGRAM HAS ONE OUTPUT FILE:                               *00190099
      * 1) REPORT FILE CONTAINS THE PRINTED OUTPUT REPORTS.            *00200099
      *                                                                *00210099
      * THIS REPORT RUNS ONCE EVERY WEEK AND AT MONTH END.             *00220099
      ******************************************************************00230006
      * 01/10/00 - JIM ARENDT WR NO. PM00336970                        *00240099
      *    - CHANGED ALL ILB0ABNO TO ILBOABN0.                         *00250099
      *    - CHANGED PROGRAM TO NO LONGER ABEND WHEN INPUT FILE2 OR    *00260099
      *      INPUT FILE3 IS EMPTY.  PROGRAM WILL DISPLAY THE HEADINGS  *00270099
      *      AND AN EMPTY INPUT FILE MESSAGE.                          *00280099
      ******************************************************************00290099
      * 05/17/04 - TODD C. MICHALEK -  WR NO. WR26602                  *00300099
      *    - EXPAND PV-HOLD-STORE-NBR AND RPT-DTL-STORE-NO TO 5 BYTES  *00310099
      *      TO HANDLE THE NEW 5 BYTE ML023-PC-STORE-NBR FIELD         *00320099
      ******************************************************************00330099
      * 10/27/05 - TODD MICHALEK IR  WR28048                           *00340099
      *    - RECOMPILED THE PROGRAM WITH NEW MARKDOWN CODE             *00350099
      *      DESCRIPTIONS (IN PEWS049):                                *00360099
      *      025 - INTERIM CLEARANCE                                   *00370099
      *      026 - FINAL CLEARANCE                                     *00380099
      *      189 - MOS-SALVAGE                                         *00390099
      ******************************************************************00400099
W26606* 01/24/06 - JOHN SELWITSCHKA -  WR NO. WR28582                  *00410099
W26606*    - PRICE CHANGE COPYBOOKS EXPANSION.                         *00420099
W26606*      MODIFIED INPUT-FILE LRECL.                                *00430099
W26606******************************************************************00440099
W39372* W39372  08-02-2012 RONNA MCDONALD - RECOMPILE TO PULL IN NEW   *00450099
W39372*                                     REASON CODE  765 DEFINED   *00460099
W39372*                                     IN PEWS049                 *00470099
W39372******************************************************************00480099
205464*CHG0205464 08-16-2018 JIM ARENDT  -  RECOMPILE TO PULL IN NEW   *00490099
205464*                                     REASON CODE  710 DEFINED   *00500099
205464*                                     IN PEWS049                 *00510099
205464******************************************************************00520099
                                                                        00530099
       ENVIRONMENT DIVISION.                                            00540006
                                                                        00550099
       CONFIGURATION SECTION.                                           00560006
                                                                        00570006
       SOURCE-COMPUTER.    IBM-3090.                                    00580006
       OBJECT-COMPUTER.    IBM-3090.                                    00590006
                                                                        00600006
       INPUT-OUTPUT SECTION.                                            00610006
                                                                        00620006
       FILE-CONTROL.                                                    00630006
                                                                        00640006
           SELECT CONTROL-FILE              ASSIGN TO INP01.            00650009
           SELECT INPUT-FILE                ASSIGN TO INP02.            00660099
           SELECT TOTAL-FILE                ASSIGN TO INP03.            00670099
           SELECT REPORT-FILE               ASSIGN TO RPT01.            00680006
                                                                        00690006
      ******************************************************************00700006
       DATA DIVISION.                                                   00710006
      ******************************************************************00720006
                                                                        00730006
       FILE SECTION.                                                    00740006
                                                                        00750006
       FD  CONTROL-FILE                                                 00760009
           RECORDING MODE IS F                                          00770006
           BLOCK CONTAINS 0 RECORDS                                     00780006
           LABEL RECORDS ARE OMITTED.                                   00790006
                                                                        00800006
       01  CONTROL-RECORD                   PIC  X(80).                 00810099
                                                                        00820099
       FD  INPUT-FILE                                                   00830099
           RECORDING MODE IS F                                          00840099
           BLOCK CONTAINS 0 RECORDS                                     00850099
           LABEL RECORDS ARE OMITTED.                                   00860099
                                                                        00870099
W26606 01  INPUT-RECORD                     PIC  X(130).                00880099
                                                                        00890099
       FD  TOTAL-FILE                                                   00900099
           RECORDING MODE IS F                                          00910006
           BLOCK CONTAINS 0 RECORDS                                     00920006
           LABEL RECORDS ARE OMITTED.                                   00930006
                                                                        00940006
       01  TOTAL-RECORD                     PIC  X(680).                00950099
                                                                        00960099
       FD  REPORT-FILE                                                  00970099
           RECORDING MODE IS F                                          00980099
           BLOCK CONTAINS 0 RECORDS                                     00990099
           LABEL RECORDS ARE OMITTED.                                   01000099
                                                                        01010099
       01  REPORT-RECORD                    PIC  X(132).                01020099
                                                                        01030006
       WORKING-STORAGE SECTION.                                         01040006
                                                                        01050006
      ******************************************************************01060006
      *    CR-CONTROL RECORD                                           *01070006
      ******************************************************************01080006
       01  CR-CONTROL-RECORD.                                           01090007
           05  FILLER                       PIC  X(03)    VALUE SPACE.  01100099
           05  CR-PERIOD-END-DATE.                                      01110011
               10  CR-PERIOD-END-DTE-CC     PIC  X(02)    VALUE SPACE.  01120099
               10  CR-PERIOD-END-DTE-YY     PIC  X(02)    VALUE SPACE.  01130099
               10  FILLER                   PIC  X(01)    VALUE SPACE.  01140099
               10  CR-PERIOD-END-DTE-MM     PIC  X(02)    VALUE SPACE.  01150099
               10  FILLER                   PIC  X(01)    VALUE SPACE.  01160099
               10  CR-PERIOD-END-DTE-DD     PIC  X(02)    VALUE SPACE.  01170099
           05  FILLER                       PIC  X(01)    VALUE SPACE.  01180099
           05  CR-MONTH-WEEK-VALUE          PIC  X(01)    VALUE SPACE.  01190099
           05  FILLER                       PIC  X(65)    VALUE SPACE.  01200099
                                                                        01210009
      ******************************************************************01220006
      *    PC-PROGRAM CONSTANTS                                        *01230006
      ******************************************************************01240006
       01  PC-PROGRAM-CONSTANTS.                                        01250006
           05  PC-PROGRAM-NAME              PIC  X(08)    VALUE         01260099
                                                          'PEKRP090'.   01270099
           05  PC-ONE                       PIC  9(01)    VALUE 1.      01280099
           05  PC-TWO                       PIC  9(01)    VALUE 2.      01290099
           05  PC-WEEK-VALUE               PIC   X(17)    VALUE         01300099
               ' FOR WEEK ENDING '.                                     01310099
           05  PC-MONTH-VALUE              PIC   X(17)    VALUE         01320099
               'FOR MONTH ENDING '.                                     01330099
           05  PC-MAX-LINES-PER-PAGE        PIC S9(03)    VALUE +66     01340099
                                                          COMP.         01350099
                                                                        01360099
      ******************************************************************01370006
      *    PROGRAM VARIABLES                                           *01380099
      ******************************************************************01390006
       01  PV-PROGRAM-VARIABLES.                                        01400006
           05  PV-ADVANCE                   PIC  9(01)    VALUE ZERO.   01410099
           05  PV-SUB1                      PIC S9(04)    VALUE +0      01420099
                                                          COMP SYNC.    01430099
           05  PV-SUB2                      PIC S9(04)    VALUE +0      01440099
                                                          COMP SYNC.    01450099
           05  PV-SAVE-REASON-DESC          PIC  X(22)    VALUE SPACE.  01460099
           05  PV-HOLD-RSN-CDE              PIC  X(03)    VALUE SPACE.  01470099
           05  PV-NUM-RSN-CDE       REDEFINES PV-HOLD-RSN-CDE           01480099
                                            PIC  9(03).                 01490099
           05  PV-HOLD-CATEGORY             PIC  X(01)    VALUE SPACE.  01500099
           05  PV-NUM-CATEGORY      REDEFINES PV-HOLD-CATEGORY          01510099
                                            PIC  9(01).                 01520099
           05  PV-HOLD-TYP-CDE              PIC  X(01)    VALUE SPACE.  01530099
           05  PV-NUM-TYP-CDE       REDEFINES PV-HOLD-TYP-CDE           01540099
                                            PIC  9(01).                 01550099
           05  PV-HOLD-STORE-NBR            PIC  X(05)    VALUE SPACE.  01560099
           05  PV-WORK-PERCENT              PIC S9(03)V9(8)             01570099
                                                          VALUE ZERO.   01580099
           05  PV-RSN-CDE-TOTAL             PIC S9(08)V9(2)             01590099
                                                          VALUE ZERO.   01600099
           05  PV-TYPE-TOTAL                PIC S9(10)V9(2)             01610099
                                                          VALUE ZERO.   01620099
           05  PV-CATEGORY-TOTAL            PIC S9(10)V9(2)             01630099
                                                          VALUE ZERO.   01640099
           05  PV-GRAND-TOTAL               PIC S9(11)V9(2)             01650099
                                                          VALUE ZERO.   01660099
           05  PV-ROUNDED-PERCENT           PIC S9(03)V9(1)             01670099
                                                          VALUE ZERO.   01680099
           05  PV-REVERSE-STORE             PIC S9(08)V9(2)             01690099
                                                          VALUE ZERO.   01700099
           05  PV-REVERSE-TOTAL             PIC S9(10)V9(2)             01710099
                                                          VALUE ZERO.   01720099
           05  PV-CURRENT-DATE.                                         01730006
               10  PV-CURRENT-YEAR          PIC  9(02)    VALUE ZERO.   01740099
               10  PV-CURRENT-MONTH         PIC  9(02)    VALUE ZERO.   01750099
               10  PV-CURRENT-DAY           PIC  9(02)    VALUE ZERO.   01760099
           05  PV-CURRENT-TIME.                                         01770006
               10  PV-CURRENT-HOUR          PIC  9(02)    VALUE ZERO.   01780099
               10  PV-CURRENT-MINUTE        PIC  9(02)    VALUE ZERO.   01790099
               10  FILLER                   PIC  9(04)    VALUE ZERO.   01800099
                                                                        01810006
       01  ABEND-CODE                       PIC S9(04)    VALUE +0 COMP.01820099
                                                                        01830099
      ******************************************************************01840099
      *    PROGRAM TABLE                                               *01850099
      ******************************************************************01860099
       01  REPORT-TABLE.                                                01870099
           05  RPT-CATEGORY                 OCCURS 5 TIMES.             01880099
               10  RPT-TYPE-CODE            OCCURS 4 TIMES.             01890099
                   15  RT-VALUE             PIC  X(27).                 01900099
                   15  RT-TOTAL             PIC S9(10)V9(2) COMP-3.     01910099
                                                                        01920099
      ******************************************************************01930006
      *    PS-PROGRAM SWITCHES                                         *01940006
      ******************************************************************01950006
       01  PS-PROGRAM-SWITCHES.                                         01960006
           05  PS-END-OF-CONTROL-FILE-SW      PIC  X(01)  VALUE 'N'.    01970099
               88  PS-END-OF-CONTROL-FILE                 VALUE 'Y'.    01980099
           05  PS-END-OF-FILE-SW              PIC  X(01)  VALUE 'N'.    01990099
               88  PS-END-OF-FILE                         VALUE 'Y'.    02000099
           05  PS-END-OF-TOTAL-SW             PIC  X(01)  VALUE 'N'.    02010099
               88  PS-END-OF-TOTAL                        VALUE 'Y'.    02020099
           05  PS-NEED-REASON-SW              PIC  X(01)  VALUE 'N'.    02030099
               88  PS-NEED-REASON                         VALUE 'Y'.    02040099
           05  PS-WRITE-DETAIL-SW             PIC  X(01)  VALUE 'N'.    02050099
               88  PS-WRITE-DETAIL                        VALUE 'Y'.    02060099
                                                                        02070006
      ******************************************************************02080006
      *    PA-PROGRAM ACCUMULATORS                                     *02090006
      ******************************************************************02100006
       01  PA-PROGRAM-ACCUMULATORS.                                     02110006
           05  PA-LINE-COUNTER                PIC S9(11)  COMP-3        02120099
                                                          VALUE ZERO.   02130099
           05  PA-PAGE-COUNTER                PIC S9(11)  COMP-3        02140099
                                                          VALUE ZERO.   02150099
                                                                        02160006
      ******************************************************************02170006
      *    STANDARD HEADER FOR 132 COLUMN REPORT                       *02180099
      ******************************************************************02190006
           COPY DPWS132O.                                               02200099
                                                                        02210006
      ******************************************************************02220065
      *    INPUT COPYBOOK                                              *02230099
      ******************************************************************02240065
W26606     COPY MLRD023.                                                02250099
                                                                        02260065
      ******************************************************************02270099
      *    PERMANENT PRICE CHANGE COPYBOOK                             *02280099
      ******************************************************************02290099
           COPY PEWS049.                                                02300099
                                                                        02310099
       01  REPORT-TITLE-LINE1.                                          02320099
           05  FILLER                         PIC  X(47)  VALUE SPACES. 02330099
           05  FILLER                         PIC  X(36)  VALUE         02340099
               'MARKDOWN / MARKUP STORE AUDIT REPORT'.                  02350099
           05  FILLER                         PIC  X(49)  VALUE SPACES. 02360099
                                                                        02370006
       01  REPORT-TITLE-LINE2.                                          02380099
           05  FILLER                         PIC  X(52)  VALUE SPACES. 02390099
           05  RPT-TL2-TIME-PERIOD            PIC  X(17)  VALUE SPACES. 02400099
           05  RPT-TL2-WEEKLY-MM              PIC  X(02)  VALUE SPACES. 02410099
           05  FILLER                         PIC  X(01)  VALUE '/'.    02420099
           05  RPT-TL2-WEEKLY-DD              PIC  X(02)  VALUE SPACES. 02430099
           05  FILLER                         PIC  X(01)  VALUE '/'.    02440099
           05  RPT-TL2-WEEKLY-CC              PIC  X(02)  VALUE SPACES. 02450099
           05  RPT-TL2-WEEKLY-YY              PIC  X(02)  VALUE SPACES. 02460099
           05  FILLER                         PIC  X(44)  VALUE SPACES. 02470099
                                                                        02480011
       01  REPORT-STORE-HEADING.                                        02490099
           05  FILLER                         PIC  X(37)  VALUE SPACES. 02500099
           05  FILLER                         PIC  X(11)  VALUE         02510099
               'REASON CODE'.                                           02520099
           05  FILLER                         PIC  X(14)  VALUE SPACES. 02530099
           05  FILLER                         PIC  X(05)  VALUE         02540099
               'STORE'.                                                 02550099
           05  FILLER                         PIC  X(65)  VALUE SPACES. 02560099
                                                                        02570099
       01  REPORT-TYPE-CODE-HEADING.                                    02580099
           05  FILLER                         PIC  X(32)  VALUE SPACES. 02590099
           05  REPORT-TYPE-CODE               PIC  X(27)  VALUE SPACES. 02600099
           05  FILLER                         PIC  X(73)  VALUE SPACES. 02610099
                                                                        02620099
       01  REPORT-DETAIL-LINE.                                          02630083
           05  FILLER                         PIC  X(34)  VALUE SPACES. 02640099
           05  RPT-DTL-REASON-CODE            PIC  ZZZ.                 02650099
           05  FILLER                         PIC  X(01)  VALUE SPACES. 02660099
           05  RPT-DTL-REASON-DESC            PIC  X(22).               02670099
           05  FILLER                         PIC  X(02)  VALUE SPACES. 02680099
           05  RPT-DTL-STORE-NO               PIC  X(05).               02690099
           05  FILLER                         PIC  X(15)  VALUE SPACES. 02700099
           05  RPT-DTL-EXT-RTL-AMT            PIC  ZZ,ZZZ,ZZZ.99-.      02710099
           05  FILLER                         PIC  X(01)  VALUE SPACES. 02720099
           05  RPT-DTL-PERCENT                PIC  ZZZ.9.               02730099
           05  FILLER                         PIC  X(01)  VALUE '%'.    02740099
           05  RPT-DTL-NEGATIVE               PIC  X(01).               02750099
           05  FILLER                         PIC  X(28)  VALUE SPACES. 02760099
                                                                        02770080
       01  REPORT-SUB-TOTAL-LINE.                                       02780099
           05  FILLER                         PIC  X(38)  VALUE SPACES. 02790099
           05  FILLER                         PIC  X(05)  VALUE         02800099
               'TOTAL'.                                                 02810056
           05  FILLER                         PIC  X(36)  VALUE SPACES. 02820099
           05  RPT-SUBT-RTL-AMT               PIC  Z,ZZZ,ZZZ,ZZZ.99-.   02830099
           05  FILLER                         PIC  X(36)  VALUE SPACES. 02840099
                                                                        02850006
       01  REPORT-TYP-CODE-TOTAL-LINE.                                  02860099
           05  FILLER                         PIC  X(32)  VALUE SPACE.  02870099
           05  FILLER                         PIC  X(06)  VALUE         02880099
               'TOTAL '.                                                02890099
           05  RPT-TYP-CODE-VALUE             PIC  X(12)  VALUE SPACE.  02900099
           05  FILLER                         PIC  X(29)  VALUE SPACES. 02910099
           05  RPT-TYP-CODE-TOTAL             PIC  Z,ZZZ,ZZZ,ZZZ.99-.   02920099
           05  FILLER                         PIC  X(36)  VALUE SPACES. 02930099
                                                                        02940011
       01  REPORT-GRAND-TOTAL-LINE.                                     02950099
           05  FILLER                         PIC  X(32)  VALUE SPACE.  02960099
           05  FILLER                         PIC  X(12)  VALUE         02970099
               'GRAND TOTAL:'.                                          02980099
           05  FILLER                         PIC  X(34)  VALUE SPACES. 02990099
           05  RPT-GRAND-TOTAL                PIC  ZZ,ZZZ,ZZZ,ZZZ.99-.  03000099
           05  FILLER                         PIC  X(36)  VALUE SPACES. 03010099
                                                                        03020099
       01  REPORT-BLANK-LINE                  PIC  X(132) VALUE SPACES. 03030099
                                                                        03040006
       01  REPORT-ENDING-LINE.                                          03050007
           05  FILLER                         PIC  X(90)  VALUE SPACES. 03060099
           05  FILLER                         PIC  X(19)  VALUE         03070099
               '***END OF REPORT***'.                                   03080006
           05  FILLER                         PIC  X(91)  VALUE SPACES. 03090099
                                                                        03100099
       01  REPORT-NO-DATA-LINE.                                         03110099
           05  FILLER                         PIC  X(48)  VALUE SPACES. 03120099
           05  FILLER                         PIC  X(32)  VALUE         03130099
               '*** NO INPUT DATA TO PROCESS ***'.                      03140099
           05  FILLER                         PIC  X(52)  VALUE SPACES. 03150099
                                                                        03160006
      ******************************************************************03170006
       PROCEDURE DIVISION.                                              03180099
                                                                        03190099
       A100-MAINLINE.                                                   03200010
                                                                        03210010
           PERFORM A150-INITIALIZE-RTN.                                 03220099
                                                                        03230099
           IF PS-END-OF-TOTAL                                           03240099
              CONTINUE                                                  03250099
           ELSE                                                         03260099
              PERFORM B200-PROCESS-FILE-RTN                             03270099
           END-IF.                                                      03280099
                                                                        03290099
           PERFORM Z998-EOJ-ROUTINE.                                    03300099
                                                                        03310099
           GOBACK.                                                      03320099
                                                                        03330099
      ******************************************************************03340099
      *     - INITIALIZES THE INTERNAL TABLE.                          *03350099
      *     - OPENS THE INPUT/OUTPUT FILES.                            *03360099
      *     - READS THE CONTROL RECORD.                                *03370099
      *     - READS THE TOTAL RECORD.                                  *03380099
      *     - ACCEPTS THE CURRENT DATE AND TIME FOR THE REPORT HEADING *03390099
      ******************************************************************03400099
       A150-INITIALIZE-RTN.                                             03410099
                                                                        03420099
           PERFORM A175-INITIALIZE-TABLE                                03430099
             VARYING PV-SUB1 FROM 1 BY 1                                03440099
               UNTIL PV-SUB1 > 5.                                       03450099
                                                                        03460099
           OPEN INPUT  INPUT-FILE                                       03470099
                       TOTAL-FILE                                       03480099
                       CONTROL-FILE.                                    03490099
           OPEN OUTPUT REPORT-FILE.                                     03500099
                                                                        03510099
           READ CONTROL-FILE INTO CR-CONTROL-RECORD                     03520099
             AT END                                                     03530099
               SET PS-END-OF-CONTROL-FILE TO TRUE.                      03540099
                                                                        03550099
           IF PS-END-OF-CONTROL-FILE                                    03560099
               DISPLAY ' '                                              03570099
               DISPLAY '** ABEND     **'                                03580099
               DISPLAY '** PROGRAM   **  = ' PC-PROGRAM-NAME            03590099
               DISPLAY '** PARAGRAPH **  = A150-INITIALIZE-RTN'         03600099
               DISPLAY '** CONTROL FILE IS EMPTY  '                     03610099
               DISPLAY ' '                                              03620099
               MOVE +4002                TO ABEND-CODE                  03630099
               CALL 'ILBOABN0' USING ABEND-CODE                         03640099
           END-IF.                                                      03650099
                                                                        03660099
           MOVE CR-PERIOD-END-DTE-MM     TO RPT-TL2-WEEKLY-MM.          03670099
           MOVE CR-PERIOD-END-DTE-DD     TO RPT-TL2-WEEKLY-DD.          03680099
           MOVE CR-PERIOD-END-DTE-CC     TO RPT-TL2-WEEKLY-CC.          03690099
           MOVE CR-PERIOD-END-DTE-YY     TO RPT-TL2-WEEKLY-YY.          03700099
                                                                        03710099
           IF CR-MONTH-WEEK-VALUE = 'W'                                 03720099
               MOVE PC-WEEK-VALUE        TO RPT-TL2-TIME-PERIOD         03730099
           ELSE                                                         03740099
               MOVE PC-MONTH-VALUE       TO RPT-TL2-TIME-PERIOD         03750099
           END-IF.                                                      03760099
                                                                        03770099
           READ TOTAL-FILE INTO REPORT-TABLE                            03780099
             AT END                                                     03790099
               SET PS-END-OF-TOTAL       TO TRUE.                       03800099
                                                                        03810099
           IF PS-END-OF-TOTAL                                           03820099
              PERFORM Z550-PRINT-MAIN-HDG                               03830099
              WRITE REPORT-RECORD                                       03840099
                 FROM REPORT-NO-DATA-LINE                               03850099
                    AFTER ADVANCING 3 LINES                             03860099
           END-IF.                                                      03870099
                                                                        03880099
           ACCEPT PV-CURRENT-DATE      FROM DATE.                       03890099
           MOVE PV-CURRENT-MONTH         TO DP132O-RUN-MONTH.           03900099
           MOVE PV-CURRENT-DAY           TO DP132O-RUN-DAY.             03910099
           MOVE PV-CURRENT-YEAR          TO DP132O-RUN-YEAR.            03920099
                                                                        03930099
           ACCEPT PV-CURRENT-TIME      FROM TIME.                       03940099
           MOVE PV-CURRENT-HOUR          TO DP132O-RUN-HOUR.            03950099
           MOVE PV-CURRENT-MINUTE        TO DP132O-RUN-MINUTE.          03960099
                                                                        03970099
           MOVE PC-PROGRAM-NAME          TO DP132O-PROGRAM-NAME.        03980099
           MOVE PC-TWO                   TO DP132O-REPORT-NUMBER.       03990099
           MOVE PE049-MKDN-RPT-RSN-VALUE-TABLE                          04000099
                                         TO PE049-MKDN-RPT-RSN-TABLE.   04010099
                                                                        04020099
      ******************************************************************04030099
       A175-INITIALIZE-TABLE.                                           04040099
                                                                        04050099
           PERFORM                                                      04060099
             VARYING PV-SUB2 FROM 1 BY 1                                04070099
               UNTIL PV-SUB2 > 4                                        04080099
                   MOVE SPACES           TO RT-VALUE (PV-SUB1, PV-SUB2) 04090099
                   MOVE ZEROES           TO RT-TOTAL (PV-SUB1, PV-SUB2) 04100099
           END-PERFORM.                                                 04110099
                                                                        04120099
      ******************************************************************04130099
       B200-PROCESS-FILE-RTN.                                           04140099
                                                                        04150099
           PERFORM C200-INITIAL-READ.                                   04160099
                                                                        04170099
           IF PS-END-OF-FILE                                            04180099
              CONTINUE                                                  04190099
           ELSE                                                         04200099
              PERFORM D200-PROCESS-LOOP                                 04210099
                  UNTIL PS-END-OF-FILE                                  04220099
                                                                        04230099
              PERFORM E100-WRITE-RSN-CODE-LINE                          04240099
                                                                        04250099
              PERFORM F100-WRITE-TOTAL-TYPE-CODE                        04260099
                                                                        04270099
              PERFORM G100-WRITE-TOTAL-CATEGORY                         04280099
                                                                        04290099
              PERFORM H100-WRITE-GRAND-TOTAL                            04300099
           END-IF.                                                      04310099
                                                                        04320099
      ******************************************************************04330099
      *     - READS THE FIRST RECORD FROM THE INPUT FILE               *04340099
      *     - MOVE THE CATEGORY, TYPE CODE, REASON CODE AND STORE      *04350099
      *       NUMBER TO HOLD AREA.                                     *04360099
      ******************************************************************04370099
       C200-INITIAL-READ.                                               04380099
                                                                        04390099
           READ INPUT-FILE INTO ML023-PC-RECORD                         04400099
             AT END                                                     04410099
               SET PS-END-OF-FILE         TO TRUE.                      04420099
                                                                        04430099
           IF PS-END-OF-FILE                                            04440099
              PERFORM Z550-PRINT-MAIN-HDG                               04450099
              WRITE REPORT-RECORD                                       04460099
                 FROM REPORT-NO-DATA-LINE                               04470099
                    AFTER ADVANCING 3 LINES                             04480099
           ELSE                                                         04490099
              MOVE ML023-PC-CATEGORY      TO PV-HOLD-CATEGORY           04500099
              MOVE ML023-PC-TYP-CDE       TO PV-HOLD-TYP-CDE            04510099
              MOVE ML023-PC-STORE-NBR     TO PV-HOLD-STORE-NBR          04520099
              MOVE 'Y'                    TO PS-NEED-REASON-SW          04530099
              MOVE ML023-PC-MKDN-MKUP-RSN-CDE                           04540099
                                          TO PV-HOLD-RSN-CDE            04550099
              PERFORM Z500-PROCESS-PAGE-BREAK                           04560099
           END-IF.                                                      04570099
                                                                        04580099
      ******************************************************************04590006
      *    EVALUATE EACH RECORD READ:                                  *04600099
      *     - IF CATEGORY, TYPE CODE, REASON CODE AND STORE NUMBER     *04610099
      *            = HOLD AREA, INCREMENT THE ACCUMULATOR              *04620099
      *     - IF CATEGORY DOES NOT = HOLD CATEGORY                     *04630099
      *           WRITE THE REASON CODE TOTAL LINE, THE TYPE CODE TOTAL*04640099
      *           LINE, ANT THE CATEGORY TOTAL LINE.                   *04650099
      *     - IF TYPE CODE DOES NOT = HOLD TYPE CODE                   *04660099
      *           WRITE THE REASON CODE TOTAL LINE, AND THE TYPE CODE  *04670099
      *           TOTAL LINE.                                          *04680099
      *     - IF THE STORE NUMBER OR THE REASON CODE DO NOT = HOLD AREA*04690099
      *           WRITE THE REASON CODE TOTAL LINE.                    *04700099
      ******************************************************************04710099
       D200-PROCESS-LOOP.                                               04720099
                                                                        04730099
           IF ML023-PC-CATEGORY = PV-HOLD-CATEGORY                      04740099
               IF ML023-PC-TYP-CDE = PV-HOLD-TYP-CDE                    04750099
                   IF ML023-PC-MKDN-MKUP-RSN-CDE =                      04760099
                                               PV-HOLD-RSN-CDE          04770099
                       IF ML023-PC-STORE-NBR = PV-HOLD-STORE-NBR        04780099
                           ADD ML023-PC-EXTD-RTL-AMT                    04790099
                                          TO PV-RSN-CDE-TOTAL           04800099
                       ELSE                                             04810099
                           PERFORM E100-WRITE-RSN-CODE-LINE             04820099
                       END-IF                                           04830099
                   ELSE                                                 04840099
                       PERFORM E100-WRITE-RSN-CODE-LINE                 04850099
                   END-IF                                               04860099
               ELSE                                                     04870099
                   PERFORM E100-WRITE-RSN-CODE-LINE                     04880099
                   PERFORM F100-WRITE-TOTAL-TYPE-CODE                   04890099
                   PERFORM Z100-TYPE-CODE-HEADING                       04900099
               END-IF                                                   04910099
           ELSE                                                         04920099
               PERFORM E100-WRITE-RSN-CODE-LINE                         04930099
               PERFORM F100-WRITE-TOTAL-TYPE-CODE                       04940099
               PERFORM G100-WRITE-TOTAL-CATEGORY                        04950099
               PERFORM Z100-TYPE-CODE-HEADING                           04960099
           END-IF.                                                      04970099
                                                                        04980099
           READ INPUT-FILE INTO ML023-PC-RECORD                         04990099
             AT END                                                     05000099
               SET PS-END-OF-FILE         TO TRUE.                      05010099
                                                                        05020099
      ******************************************************************05030099
      *     - WRITES THE REASON CODE TOTAL LINE.                       *05040099
      ******************************************************************05050099
       E100-WRITE-RSN-CODE-LINE.                                        05060099
                                                                        05070099
           IF PS-NEED-REASON                                            05080099
               MOVE 'N'                   TO PS-NEED-REASON-SW          05090099
               MOVE PV-NUM-RSN-CDE        TO RPT-DTL-REASON-CODE        05100099
               PERFORM Z300-FIND-DESC                                   05110099
           ELSE                                                         05120099
               MOVE SPACES                TO RPT-DTL-REASON-DESC        05130099
               MOVE ZEROES                TO RPT-DTL-REASON-CODE        05140099
           END-IF.                                                      05150099
                                                                        05160099
           MOVE PV-HOLD-STORE-NBR         TO RPT-DTL-STORE-NO.          05170099
           MOVE PV-RSN-CDE-TOTAL          TO RPT-DTL-EXT-RTL-AMT.       05180099
           ADD  PV-RSN-CDE-TOTAL          TO PV-TYPE-TOTAL.             05190099
                                                                        05200099
           MOVE SPACES                    TO RPT-DTL-NEGATIVE.          05210099
           IF  RT-TOTAL (PV-NUM-CATEGORY, PV-NUM-TYP-CDE) POSITIVE      05220099
           AND PV-RSN-CDE-TOTAL NEGATIVE                                05230099
               MOVE '-'                   TO RPT-DTL-NEGATIVE           05240099
           END-IF.                                                      05250099
                                                                        05260099
           COMPUTE PV-REVERSE-TOTAL =                                   05270099
                      RT-TOTAL (PV-NUM-CATEGORY, PV-NUM-TYP-CDE) * +1.  05280099
           COMPUTE PV-REVERSE-STORE = PV-RSN-CDE-TOTAL * +1.            05290099
                                                                        05300099
           IF PV-REVERSE-STORE = ZERO                                   05310099
               MOVE ZERO                  TO PV-ROUNDED-PERCENT         05320099
           ELSE                                                         05330099
               COMPUTE PV-WORK-PERCENT = (PV-REVERSE-STORE /            05340099
                                          PV-REVERSE-TOTAL) * 100       05350099
               MULTIPLY PV-WORK-PERCENT BY 1                            05360099
                        GIVING PV-ROUNDED-PERCENT ROUNDED               05370099
           END-IF.                                                      05380099
                                                                        05390099
           MOVE PV-ROUNDED-PERCENT        TO RPT-DTL-PERCENT.           05400099
           MOVE PC-ONE                    TO PV-ADVANCE.                05410099
           MOVE 'Y'                       TO PS-WRITE-DETAIL-SW.        05420099
           PERFORM Z200-PRINT-LINE.                                     05430099
                                                                        05440014
           IF ML023-PC-MKDN-MKUP-RSN-CDE = PV-HOLD-RSN-CDE              05450099
               CONTINUE                                                 05460099
           ELSE                                                         05470099
               MOVE 'Y'                   TO PS-NEED-REASON-SW          05480099
           END-IF.                                                      05490099
                                                                        05500099
           MOVE ML023-PC-STORE-NBR        TO PV-HOLD-STORE-NBR.         05510099
           MOVE ML023-PC-MKDN-MKUP-RSN-CDE                              05520099
                                          TO PV-HOLD-RSN-CDE.           05530099
           MOVE ZERO                      TO PV-RSN-CDE-TOTAL.          05540099
           ADD ML023-PC-EXTD-RTL-AMT      TO PV-RSN-CDE-TOTAL.          05550099
                                                                        05560099
      ******************************************************************05570099
      *     - WRITES THE TYPE CODE TOTAL LINE.                         *05580099
      ******************************************************************05590099
       F100-WRITE-TOTAL-TYPE-CODE.                                      05600099
                                                                        05610099
           MOVE PV-TYPE-TOTAL             TO RPT-SUBT-RTL-AMT.          05620099
           ADD  PV-TYPE-TOTAL             TO PV-CATEGORY-TOTAL.         05630099
                                                                        05640099
           MOVE REPORT-SUB-TOTAL-LINE     TO REPORT-BLANK-LINE.         05650099
           MOVE PC-TWO                    TO PV-ADVANCE.                05660099
           PERFORM Z200-PRINT-LINE.                                     05670099
                                                                        05680099
           MOVE ML023-PC-TYP-CDE          TO PV-HOLD-TYP-CDE.           05690099
           MOVE ZERO                      TO PV-TYPE-TOTAL.             05700099
                                                                        05710099
      ******************************************************************05720099
      *     - WRITES THE CATEGORY TOTAL LINE.                          *05730099
      ******************************************************************05740099
       G100-WRITE-TOTAL-CATEGORY.                                       05750099
                                                                        05760099
           EVALUATE PV-HOLD-CATEGORY                                    05770099
               WHEN '1'                                                 05780099
                   MOVE 'BUYER:      '    TO RPT-TYP-CODE-VALUE         05790099
               WHEN '2'                                                 05800099
                   MOVE 'STORE:      '    TO RPT-TYP-CODE-VALUE         05810099
               WHEN '3'                                                 05820099
                   MOVE 'PROMOTIONAL:'    TO RPT-TYP-CODE-VALUE         05830099
               WHEN '4'                                                 05840099
                   MOVE 'REGISTER:   '    TO RPT-TYP-CODE-VALUE         05850099
               WHEN '5'                                                 05860099
                   MOVE 'UNMATCHED:  '    TO RPT-TYP-CODE-VALUE         05870099
           END-EVALUATE.                                                05880099
                                                                        05890099
           MOVE PV-CATEGORY-TOTAL         TO RPT-TYP-CODE-TOTAL.        05900099
           ADD  PV-CATEGORY-TOTAL         TO PV-GRAND-TOTAL.            05910099
                                                                        05920099
           MOVE REPORT-TYP-CODE-TOTAL-LINE                              05930099
                                          TO REPORT-BLANK-LINE.         05940099
           MOVE PC-TWO                    TO PV-ADVANCE.                05950099
           PERFORM Z200-PRINT-LINE.                                     05960099
                                                                        05970099
           MOVE ML023-PC-CATEGORY         TO PV-HOLD-CATEGORY.          05980099
           MOVE ZERO                      TO PV-CATEGORY-TOTAL.         05990099
                                                                        06000099
      ******************************************************************06010099
      *     - WRITES THE GRAND TOTAL LINE.                             *06020099
      ******************************************************************06030099
       H100-WRITE-GRAND-TOTAL.                                          06040099
                                                                        06050099
           MOVE PV-GRAND-TOTAL            TO RPT-GRAND-TOTAL.           06060099
           MOVE REPORT-GRAND-TOTAL-LINE   TO REPORT-BLANK-LINE.         06070099
           MOVE PC-TWO                    TO PV-ADVANCE.                06080099
                                                                        06090099
           PERFORM Z200-PRINT-LINE.                                     06100099
                                                                        06110099
      ******************************************************************06120099
      *     - WRITES THE TYPE CODE HEADING.                            *06130099
      ******************************************************************06140099
       Z100-TYPE-CODE-HEADING.                                          06150099
                                                                        06160099
           IF PA-LINE-COUNTER + 2 > PC-MAX-LINES-PER-PAGE               06170099
               PERFORM Z500-PROCESS-PAGE-BREAK                          06180099
           ELSE                                                         06190099
               MOVE RT-VALUE (PV-NUM-CATEGORY, PV-NUM-TYP-CDE)          06200099
                                          TO REPORT-TYPE-CODE           06210099
               MOVE REPORT-TYPE-CODE-HEADING                            06220099
                                          TO REPORT-BLANK-LINE          06230099
               MOVE PC-TWO                TO PV-ADVANCE                 06240099
               PERFORM Z200-PRINT-LINE                                  06250099
           END-IF.                                                      06260099
                                                                        06270099
      ******************************************************************06280006
       Z200-PRINT-LINE.                                                 06290099
                                                                        06300006
           IF PA-LINE-COUNTER + PV-ADVANCE > PC-MAX-LINES-PER-PAGE      06310099
               PERFORM Z500-PROCESS-PAGE-BREAK                          06320099
               MOVE PV-NUM-RSN-CDE        TO RPT-DTL-REASON-CODE        06330099
               MOVE PV-SAVE-REASON-DESC   TO RPT-DTL-REASON-DESC        06340099
           END-IF.                                                      06350006
                                                                        06360006
           IF PS-WRITE-DETAIL                                           06370099
               MOVE 'N'                   TO PS-WRITE-DETAIL-SW         06380099
               WRITE REPORT-RECORD                                      06390099
                 FROM REPORT-DETAIL-LINE                                06400099
                   AFTER PV-ADVANCE                                     06410099
           ELSE                                                         06420099
               WRITE REPORT-RECORD                                      06430099
                 FROM REPORT-BLANK-LINE                                 06440099
                   AFTER PV-ADVANCE                                     06450099
           END-IF.                                                      06460099
                                                                        06470006
           ADD PV-ADVANCE                 TO PA-LINE-COUNTER.           06480099
           MOVE SPACES                    TO REPORT-BLANK-LINE.         06490099
                                                                        06500046
      ******************************************************************06510099
      *     - SEARCH THE PE049 REASON CODE TABLE TO FIND THE           *06520099
      *       REASON DESCRIPTION FOR A GIVEN REASON CODE.              *06530099
      ******************************************************************06540099
       Z300-FIND-DESC.                                                  06550099
                                                                        06560099
           SET PE049-RSN-CDE-IDX          TO +1.                        06570099
           SEARCH PE049-RSN-CODE-DESC-TABLE                             06580099
               AT END                                                   06590099
                   MOVE PE049-RPT-RSN-CODE-DESC (1)                     06600099
                                          TO PV-SAVE-REASON-DESC        06610099
               WHEN PE049-RPT-RSN-CODE (PE049-RSN-CDE-IDX)              06620099
                                           = PV-HOLD-RSN-CDE            06630099
                   MOVE PE049-RPT-RSN-CODE-DESC (PE049-RSN-CDE-IDX)     06640099
                                          TO PV-SAVE-REASON-DESC        06650099
           END-SEARCH.                                                  06660099
                                                                        06670099
           MOVE PV-SAVE-REASON-DESC       TO RPT-DTL-REASON-DESC.       06680099
                                                                        06690099
      ******************************************************************06700012
      *     - ADVANCES TO A NEW PAGE AND WRITES THE REPORT HEADINGS    *06710012
      ******************************************************************06720012
       Z500-PROCESS-PAGE-BREAK.                                         06730099
                                                                        06740012
           PERFORM Z550-PRINT-MAIN-HDG.                                 06750099
                                                                        06760011
           WRITE REPORT-RECORD                                          06770006
             FROM REPORT-STORE-HEADING                                  06780099
               AFTER ADVANCING 3 LINES.                                 06790099
                                                                        06800006
           MOVE RT-VALUE (PV-NUM-CATEGORY, PV-NUM-TYP-CDE)              06810099
                                          TO REPORT-TYPE-CODE.          06820099
                                                                        06830099
           WRITE REPORT-RECORD                                          06840099
             FROM REPORT-TYPE-CODE-HEADING                              06850099
               AFTER ADVANCING 1 LINE.                                  06860099
                                                                        06870099
           MOVE +7                        TO PA-LINE-COUNTER.           06880099
                                                                        06890099
      ******************************************************************06900099
      *     - WRITES THE REPORTS MAIN HEADINGS.                        *06910099
      ******************************************************************06920099
       Z550-PRINT-MAIN-HDG.                                             06930099
                                                                        06940099
           ADD PC-ONE                     TO PA-PAGE-COUNTER.           06950099
           MOVE PA-PAGE-COUNTER           TO DP132O-PAGE-NUMBER.        06960099
                                                                        06970099
           WRITE REPORT-RECORD                                          06980099
             FROM DP132O-STANDRD-HEADER-132-COLS                        06990099
               AFTER ADVANCING PAGE.                                    07000099
           INITIALIZE DP132O-LASER-NEW-PAGE-INDICATR.                   07010099
                                                                        07020099
           WRITE REPORT-RECORD                                          07030099
             FROM REPORT-TITLE-LINE1                                    07040099
               AFTER ADVANCING 1 LINE.                                  07050099
                                                                        07060099
           WRITE REPORT-RECORD                                          07070099
             FROM REPORT-TITLE-LINE2                                    07080099
               AFTER ADVANCING 1 LINE.                                  07090099
                                                                        07100099
      ******************************************************************07110099
      *     - WRITES AN END OF REPORT LINE TO THE REPORT               *07120099
      ******************************************************************07130099
       Z998-EOJ-ROUTINE.                                                07140099
                                                                        07150099
           ADD PC-TWO                     TO PA-LINE-COUNTER.           07160099
                                                                        07170099
           IF PA-LINE-COUNTER > PC-MAX-LINES-PER-PAGE                   07180099
               PERFORM Z500-PROCESS-PAGE-BREAK                          07190099
           END-IF.                                                      07200099
                                                                        07210099
           WRITE REPORT-RECORD                                          07220099
             FROM REPORT-ENDING-LINE                                    07230099
               AFTER ADVANCING 2 LINES.                                 07240099
                                                                        07250099
           PERFORM Z999-CLOSE-FILES.                                    07260099
                                                                        07270099
      ******************************************************************07280099
      *     - CLOSES THE FILES                                         *07290099
      ******************************************************************07300099
       Z999-CLOSE-FILES.                                                07310099
                                                                        07320099
           CLOSE INPUT-FILE                                             07330099
                 TOTAL-FILE                                             07340099
                 CONTROL-FILE                                           07350099
                 REPORT-FILE.                                           07360099
                                                                        07370099
