       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             INKUT144.                                        
       AUTHOR.                 VINOD/INFOSYS.                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           09-02-08.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      *     PROGRAM TO EXTRACT DATA FOR CREATING CUMULATIVE REPORT.    *        
      ******************************************************************        
      * THIS PROGRAM EXTRACTS DATA FOR CREATING CUMULATIVE REPORT THAT *        
      * NEED TO BREAK AND TOTAL BY CYCLE. THE OUTPUT FILE FROM THIS    *        
      * PROGRAM WILL BE USED IN THE PROGRAM INKRP144.                  *        
      *                                                                *        
      * WR/PRO DATE       CHANGED  DESCRIPTION OF CHANGES              *        
      *                   BY                                           *        
      * ------ ---------- -------  -------------------------------     *        
      * W33304 09-02-2008 VINOD/   NEW PROGRAM CREATED AS PART OF      *        
      *                   INFOSYS  HOLDING INVENTORY LEDGER OPEN - HILO*        
      *                                                                *        
RONNA1* CHG0305858                                                     *        
RONNA1*         02-05-2024 RONNA MCDONALD                              *        
RONNA1*                    REMOVE LOGIC FOR ESTIMATED INVENTORY.       *        
RONNA1*                    THIS IS OBSOLETE.                           *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT MERCH-LEDG-IN-FILE         ASSIGN TO INP01.                   
           SELECT INV-LEDG-OUT-FILE          ASSIGN TO OUT01.                   
           SELECT ERROR-REPORT-FILE          ASSIGN TO OUT02.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  MERCH-LEDG-IN-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       01  MERCH-LEDG-IN-RECORD             PIC  X(400).                        
                                                                                
       FD  INV-LEDG-OUT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS.                                      
                                                                                
       01  INV-LEDG-OUT-RECORD              PIC  X(130).                        
                                                                                
       FD  ERROR-REPORT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       01  ERROR-FILE-RECORD                PIC  X(400).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      ******************************************************************        
      *    CONSTANTS ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08)                          
                                                      VALUE 'INKUT144'.         
           05  PC-STATUS-CODE               PIC  X(02)   VALUE 'IN'.            
           05  PC-MAX-RETRIES               PIC S9(04)   VALUE +3               
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      *    VARIABLES ARE DECLARED HERE                                 *        
      ******************************************************************        
       01  PV-PROGRAM-VARIBLES.                                                 
           05  PV-STORE-NBR                 PIC S9(04)   COMP.                  
           05  PV-OPER-ATTEMPTED            PIC  X(35)   VALUE SPACES.          
           05  PV-PARAGRAPH-NAME            PIC  X(30)   VALUE SPACES.          
           05  PV-DUMMY                     PIC  X(01)   VALUE SPACES.          
           05  PV-RETRY-COUNT               PIC S9(04)   VALUE ZERO             
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      *    PROGRAM SWITCHES ARE DECLARED HERE                          *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MERCH-EDIT-IN-FILE-SW     PIC X(01)    VALUE 'N'.             
               88  PS-MERCH-LEDG-IN-EOF-YES              VALUE 'Y'.             
               88  PS-MERCH-LEDG-IN-EOF-NO               VALUE 'N'.             
           05  PS-STORE-FOUND-SW            PIC X(01)    VALUE 'N'.             
               88  PS-STORE-FOUND-YES                    VALUE 'Y'.             
               88  PS-STORE-FOUND-NO                     VALUE 'N'.             
           05  PS-ERROR-RECORD-SW           PIC X(01)    VALUE 'N'.             
               88  PS-ERROR-RECORD-YES                   VALUE 'Y'.             
               88  PS-ERROR-RECORD-NO                    VALUE 'N'.             
           05  PS-SKIP-RECORD-SW            PIC X(01)    VALUE 'N'.             
               88  PS-SKIP-RECORD-YES                    VALUE 'Y'.             
               88  PS-SKIP-RECORD-NO                     VALUE 'N'.             
                                                                                
      ******************************************************************        
      *    ACCUMULATORS ARE DECLARED HERE                              *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INP-RECS-CNT              PIC 9(09)    VALUE ZEROES.          
           05  PA-TABLE-ACS-CNT             PIC 9(09)    VALUE ZEROES.          
           05  PA-OUT-WRITE-CNT             PIC 9(09)    VALUE ZEROES.          
           05  PA-ERR-WRITE-CNT             PIC 9(09)    VALUE ZEROES.          
           05  PA-SKIP-REC-CNT              PIC 9(09)    VALUE ZEROES.          
                                                                                
      ******************************************************************        
      *    ABEND CODE DECLARATION                                      *        
      ******************************************************************        
       01  ABEND-CODE                       PIC S9(04)   VALUE +0               
                                                         COMP SYNC.             
                                                                                
      ******************************************************************        
      * INPUT FILE COPY MEMBER                                                  
      ******************************************************************        
           COPY MLRD154.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE COPY MEMBER                                                 
      ******************************************************************        
           COPY INRD154.                                                        
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINVPAR                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINCNTL                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    USED FOR SQL COMMUNICATION AREA.                            *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                               *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  PROGRAM PROCESSING STARTS HERE                                *        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATIONS.                                        
           PERFORM 2000-PROCESS-FILE                                            
              UNTIL PS-MERCH-LEDG-IN-EOF-YES.                                   
           PERFORM 6000-END-OF-JOB.                                             
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *   INITIALIZATION PARA                                          *        
      ******************************************************************        
                                                                                
       1000-INITIALIZATIONS.                                                    
                                                                                
           OPEN INPUT  MERCH-LEDG-IN-FILE                                       
                OUTPUT INV-LEDG-OUT-FILE                                        
                OUTPUT ERROR-REPORT-FILE.                                       
           INITIALIZE  PV-PROGRAM-VARIBLES                                      
                       ML154-UPDATE-REC                                         
                       IN154-UPDATE-REC                                         
                       DCLTINVPAR                                               
                       DCLTINCNTL.                                              
           SET PS-MERCH-LEDG-IN-EOF-NO      TO   TRUE.                          
           PERFORM 3000-READ-MERCH-LEDG-IN-FILE.                                
                                                                                
      ******************************************************************        
      *   MAIN PROCESSING PARA                                         *        
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
           IF  ML154-LOC-NBR NOT = PV-STORE-NBR                                 
               SET PS-ERROR-RECORD-NO       TO TRUE                             
               SET PS-SKIP-RECORD-NO        TO TRUE                             
               MOVE ML154-LOC-NBR           TO PV-STORE-NBR                     
                                               IN154-LOC-NBR                    
               PERFORM 2100-SELECT-STORE-DATA                                   
               IF  PS-STORE-FOUND-YES                                           
                   MOVE INVPAR-INV-ID       TO IN154-INV-ID                     
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO IN154-FINCL-INV-STAT-CDE         
                   MOVE INCNTL-OPN-FSCL-MN-DTE                                  
                                            TO IN154-IN-OPN-FSCL-MN-DTE         
               ELSE                                                             
                   PERFORM 2200-CHECK-STATUS-CODE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-ERROR-RECORD-YES                                              
               PERFORM 5000-WRITE-ERROR-OUT-FILE                                
           END-IF.                                                              
                                                                                
           IF  PS-SKIP-RECORD-YES                                               
               ADD 1                        TO PA-SKIP-REC-CNT                  
           END-IF.                                                              
                                                                                
           IF  PS-STORE-FOUND-YES                                               
               PERFORM 4000-WRITE-INV-LEDG-OUT-FILE                             
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-MERCH-LEDG-IN-FILE.                                
                                                                                
      ******************************************************************        
      *   FETCHES STORE DETAILS FOR THE CORRESPONDING ACTIVE STORE     *        
      ******************************************************************        
       2100-SELECT-STORE-DATA.                                                  
                                                                                
           MOVE '2100-SELECT-STORE-DATA'    TO PV-PARAGRAPH-NAME.               
           MOVE ZERO                        TO PV-RETRY-COUNT.                  
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                    OR SQLCODE = +100                                           
                                                                                
           EXEC SQL                                                             
               SELECT A.INV_ID,                                                 
                      A.FINCL_INV_STAT_CDE,                                     
                      B.OPN_FSCL_MN_DTE                                         
                 INTO :INVPAR-INV-ID,                                           
                      :INVPAR-FINCL-INV-STAT-CDE,                               
                      :INCNTL-OPN-FSCL-MN-DTE                                   
                 FROM TINVPAR A,                                                
                      TINCNTL B                                                 
                WHERE A.LOC_NBR = :PV-STORE-NBR                                 
                  AND A.LOC_INV_STAT_CDE = :PC-STATUS-CODE                      
                  AND A.ACTL_FIN_BK_DTE = '9999-09-09'                          
                  AND B.ACTV_IND   = 'Y'                                        
RONNA1****        AND A.SCHD_PHY_CNT_CDE <> 'EI'                                
                  AND A.INV_ID = B.INV_ID                                       
                 WITH UR                                                        
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                    SET PS-STORE-FOUND-YES  TO TRUE                             
                    ADD 1                   TO PA-TABLE-ACS-CNT                 
               WHEN +100                                                        
                    SET PS-STORE-FOUND-NO   TO TRUE                             
                    ADD 1                   TO PA-TABLE-ACS-CNT                 
               WHEN -904                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   DISPLAY "ERROR-STORE : " PV-STORE-NBR                        
                   MOVE 'ERROR IN SELECT - STORE-DATA'                          
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'TINVPAR-1 MAX RETRIES EXCEEDED'                            
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   VALIDATES THE LOCATION STATUS CODE FOR A PARTICULAR STORE    *        
      ******************************************************************        
       2200-CHECK-STATUS-CODE.                                                  
                                                                                
           MOVE '2200-CHECK-STATUS-CODE'    TO PV-PARAGRAPH-NAME.               
           MOVE ZERO                        TO PV-RETRY-COUNT.                  
                                                                                
           PERFORM WITH TEST AFTER                                              
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL PV-RETRY-COUNT > PC-MAX-RETRIES                          
                    OR SQLCODE = +0                                             
                    OR SQLCODE = -811                                           
                    OR SQLCODE = +100                                           
                                                                                
           EXEC SQL                                                             
               SELECT 'X'                                                       
                 INTO :PV-DUMMY                                                 
                 FROM TINVPAR A,                                                
                      TINCNTL B                                                 
                WHERE A.LOC_NBR = :PV-STORE-NBR                                 
                  AND A.LOC_INV_STAT_CDE IN ( 'DB','DA','DR')                   
                  AND A.ACTL_FIN_BK_DTE = '9999-09-09'                          
                  AND B.ACTV_IND   = 'Y'                                        
                  AND A.INV_ID = B.INV_ID                                       
                 WITH UR                                                        
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN -811                                                        
                    SET PS-SKIP-RECORD-YES  TO TRUE                             
               WHEN +100                                                        
                    SET PS-ERROR-RECORD-YES TO TRUE                             
               WHEN -904                                                        
               WHEN -911                                                        
               WHEN -913                                                        
                    CONTINUE                                                    
               WHEN OTHER                                                       
                   DISPLAY "ERROR-STORE : " PV-STORE-NBR                        
                   MOVE 'ERROR IN CHECK STATUS CODE'                            
                                            TO PV-OPER-ATTEMPTED                
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE                                                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               MOVE 'TINVPAR-2 MAX RETRIES EXCEEDED'                            
                                            TO PV-OPER-ATTEMPTED                
               PERFORM 9000-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *   READS INPUT FILE                                             *        
      ******************************************************************        
       3000-READ-MERCH-LEDG-IN-FILE.                                            
                                                                                
           READ MERCH-LEDG-IN-FILE          INTO ML154-UPDATE-REC               
           AT END                                                               
               SET PS-MERCH-LEDG-IN-EOF-YES TO  TRUE                            
           NOT AT END                                                           
               ADD 1                        TO PA-INP-RECS-CNT                  
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *   WRITES INTO THE OUTPUT FILE                                  *        
      ******************************************************************        
       4000-WRITE-INV-LEDG-OUT-FILE.                                            
                                                                                
           MOVE ML154-FSCL-MN-NBR           TO IN154-FSCL-MN-NBR.               
           MOVE ML154-FSCL-MN-END-DTE       TO IN154-FSCL-MN-END-DTE.           
           MOVE ML154-FSCL-WK-NBR           TO IN154-FSCL-WK-NBR.               
           MOVE ML154-FSCL-WK-END-DTE       TO IN154-FSCL-WK-END-DTE.           
           MOVE ML154-DEPT-NBR              TO IN154-DEPT-NBR.                  
           MOVE ML154-MAJ-CL-NBR            TO IN154-MAJ-CL-NBR.                
           MOVE ML154-SUB-CL-NBR            TO IN154-SUB-CL-NBR.                
           MOVE ML154-ML-LWST-ID            TO IN154-ML-LWST-ID.                
           MOVE ML154-SKU-NBR               TO IN154-SKU-NBR.                   
           MOVE ML154-FIN-LIAB-DTE          TO IN154-FIN-LIAB-DTE.              
           MOVE ML154-TRN-TYP-CDE           TO IN154-TRN-TYP-CDE.               
           MOVE ML154-SLS-REG-RTL-AMT       TO IN154-SLS-REG-RTL-AMT.           
           MOVE ML154-SLS-PROMO-RTL-AMT     TO IN154-SLS-PROMO-RTL-AMT.         
           MOVE ML154-SLS-PROCLR-RTL-AMT    TO IN154-SLS-PROCLR-RTL-AMT.        
           MOVE ML154-SLS-CLR-RTL-AMT       TO IN154-SLS-CLR-RTL-AMT.           
           MOVE ML154-SLS-OTH-RTL-AMT       TO IN154-SLS-OTH-RTL-AMT.           
           WRITE INV-LEDG-OUT-RECORD        FROM IN154-UPDATE-REC.              
           ADD 1                            TO   PA-OUT-WRITE-CNT.              
                                                                                
      ******************************************************************        
      *   WRITES INTO THE OUTPUT ERROR FILE                            *        
      ******************************************************************        
       5000-WRITE-ERROR-OUT-FILE.                                               
                                                                                
           WRITE ERROR-FILE-RECORD          FROM ML154-UPDATE-REC.              
           ADD 1                            TO PA-ERR-WRITE-CNT.                
                                                                                
      ******************************************************************        
      *   END OF JOB PROCESSING                                        *        
      ******************************************************************        
       6000-END-OF-JOB.                                                         
                                                                                
           DISPLAY                                                              
               'NUMBER OF RECORDS READ FROM INPUT FILE             : '          
                   PA-INP-RECS-CNT.                                             
           DISPLAY                                                              
               'NUMBER OF CALLS TO THE TABLES TINVPAR/TINCNTL      : '          
                   PA-TABLE-ACS-CNT.                                            
           DISPLAY                                                              
               'NUMBER OF RECORDS WRITTEN TO OUTPUT FILE           : '          
                   PA-OUT-WRITE-CNT.                                            
           DISPLAY                                                              
               'NUMBER OF RECORDS WRITTEN TO ERROR FILE            : '          
                   PA-ERR-WRITE-CNT.                                            
           DISPLAY                                                              
               'NUMBER OF RECORDS BYPASSED                         : '          
                   PA-SKIP-REC-CNT.                                             
                                                                                
           CLOSE MERCH-LEDG-IN-FILE                                             
                 INV-LEDG-OUT-FILE                                              
                 ERROR-REPORT-FILE.                                             
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                           *        
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           CLOSE MERCH-LEDG-IN-FILE                                             
                 INV-LEDG-OUT-FILE                                              
                 ERROR-REPORT-FILE.                                             
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = '  PC-PROGRAM-NAME.                     
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED.                   
           DISPLAY '**  PARAGRAPH **  = '  PV-PARAGRAPH-NAME.                   
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                  *        
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      ******************************************************************        
      *                 END OF PROGRAM INKUT144                        *        
      ******************************************************************        
