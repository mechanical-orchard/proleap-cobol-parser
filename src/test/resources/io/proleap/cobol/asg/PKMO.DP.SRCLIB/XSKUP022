       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP022.                                         00020017
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050014
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070018
      **       VSM ITEM MESSAGE - NUMBER 020 - SUPPLIER CHANGE          00080018
      ******************************************************************00090018
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100024
      ***     ATTRIBUTES ON XST_VND_STYLE:                              00110024
      ***     1. ENT_ID                                                 00120024
      ******************************************************************00130018
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00140018
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00150023
      ******************************************************************00160018
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.  IBM-3090.                                      00190000
       OBJECT-COMPUTER.  IBM-3090.                                      00200000
       DATA DIVISION.                                                   00210000
                                                                        00220005
       WORKING-STORAGE SECTION.                                         00230000
      ******************************************************************00240005
      **PROGRAM SWITCHES                                                00250005
      ******************************************************************00260005
       01  PS-PROGRAM-SWITCHES.                                         00270005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00280005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00290005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00300005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00310020
               88  PS-ACTION-REVERSE                  VALUE 01.         00320020
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00330020
               88  PS-ACTION-NOTHING                  VALUE 02.         00340020
      ******************************************************************00350000
      **PROGRAM VARIABLES                                               00360000
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00400007
      ******************************************************************00410015
      **PROGRAM CALCULATIONS                                            00420015
      ******************************************************************00430015
       01  PC-PROGRAM-CALCULATIONS.                                     00440015
           05  PC-10-DIGIT                 PIC 9(10)  VALUE ZEROS.      00450015
           05  PC-18-DIGIT                 PIC S9(18) COMP VALUE ZEROS. 00460015
           05  PC-09-DIGIT                 PIC S9(09) COMP VALUE ZEROS. 00470015
      ******************************************************************00480000
      **PROGRAM VARIABLES                                               00490019
      ******************************************************************00500000
       01  PV-PROGRAM-VARIABLES.                                        00510019
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00520019
      ******************************************************************00530000
      **COPYBOOKS                                                       00540000
      ******************************************************************00550000
      ***VSM ITEM COPYBOOK                                              00560005
           COPY IMRD008.                                                00570025
      ***ABEND COPYBOOK                                                 00580005
           COPY DPWS004.                                                00590025
      ***DB2 MESSAGE AREA                                               00600000
           COPY SQLCA2.                                                 00610025
      ******************************************************************00620000
      **TABLE INCLUSIONS                                                00630000
      ******************************************************************00640000
      ***DB2 COMMUNICATIONS AREA                                        00650000
           EXEC SQL                                                     00660000
                INCLUDE SQLCA                                           00670000
           END-EXEC.                                                    00680000
      ***XST_VND_STYL TABLE                                             00690000
           EXEC SQL                                                     00700000
               INCLUDE XST00013                                         00710000
           END-EXEC.                                                    00720000
                                                                        00730000
       LINKAGE SECTION.                                                 00740000
      ******************************************************************00750000
      **LINKAGE VARIABLES                                               00760000
      ******************************************************************00770000
       01  LV-IMRD008-RECORD               PIC X(450).                  00780005
       01  LV-RETURN-CODE                  PIC S9(04).                  00790005
       01  LV-ACTION-IND                   PIC 9(02).                   00800020
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00810020
                                                                        00820000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00830005
                                LV-RETURN-CODE,                         00840020
                                LV-ACTION-IND,                          00850020
                                LV-CURRENT-TIMESTAMP.                   00860020
      ******************************************************************00870000
       0000-MAINLINE.                                                   00880000
                                                                        00890000
            PERFORM 1000-INITIALIZATION.                                00900000
            PERFORM 2000-PROCESS-MESSAGE                                00910014
                    UNTIL PS-PROCESS-COMPLETE.                          00920005
            PERFORM 9999-WRAPUP.                                        00930000
                                                                        00940000
       EJECT.                                                           00950005
      ***************************************************************** 00960000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00970007
      ***************************************************************** 00980000
       1000-INITIALIZATION.                                             00990000
                                                                        01000000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    01010019
                                                                        01020000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           01030008
           INITIALIZE PC-PROGRAM-COUNTERS.                              01040018
                                                                        01050008
           MOVE LV-IMRD008-RECORD             TO IMRD008-RECORD.        01060014
           MOVE IM008-ITEM-KEY-RMS-STYL-ID    TO XST00013-STYL-ID.      01070014
                                                                        01080007
      *THIS DONE BECAUSE OF A COMPILER LIMITATION WHEN DEALING WITH     01090015
      *     COMP FIELDS.                                                01100015
           MOVE IM008-SUPPLR-CHG-ENT-ID-NEW   TO PC-10-DIGIT.           01110017
           MOVE PC-10-DIGIT                   TO PC-18-DIGIT.           01120015
           MOVE PC-18-DIGIT                   TO PC-09-DIGIT.           01130015
           MOVE PC-09-DIGIT                   TO XST00013-ENT-ID.       01140017
                                                                        01150000
121402     MOVE LV-CURRENT-TIMESTAMP          TO XST00013-LAST-UPD-TMST.01160023
121402     MOVE IM008-CRE-TMST           TO XST00013-SOR-LAST-MSG-TMST. 01170023
                                                                        01180017
       EJECT.                                                           01190005
      ***************************************************************** 01200000
      **UPDATES SUPPLIER ID ON TABLE XST_VND_STYLE                      01210024
      ***************************************************************** 01220000
       2000-PROCESS-MESSAGE.                                            01230014
                                                                        01240000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01250019
                                                                        01260005
           EXEC SQL                                                     01270005
              UPDATE XST_VND_STYL                                       01280005
                  SET   ENT_ID        = :XST00013-ENT-ID                01290017
                      , LAST_UPD_TMST = :XST00013-LAST-UPD-TMST         01300016
121402                , SOR_LAST_MSG_TMST = :XST00013-SOR-LAST-MSG-TMST 01310023
                  WHERE STYL_ID       = :XST00013-STYL-ID               01320007
           END-EXEC.                                                    01330005
                                                                        01340005
           EVALUATE TRUE                                                01350005
      ***SUCCESSFUL PROCESSING                                          01360024
              WHEN SQLCODE = ZERO                                       01370005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01380008
      ***ROW/TABLE LOCKS                                                01390024
              WHEN SQLCODE = -911                                       01400005
              WHEN SQLCODE = -913                                       01410007
                   ADD +1                      TO PC-RETRY-COUNTER      01420007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01430007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01440007
                   ELSE                                                 01450007
                       ADD +1                  TO PC-RETRY-COUNTER      01460007
                   END-IF                                               01470007
              WHEN OTHER                                                01480005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01490005
           END-EVALUATE.                                                01500005
                                                                        01510000
       EJECT.                                                           01520005
      ******************************************************************01530000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01540000
      ******************************************************************01550000
       9999-WRAPUP.                                                     01560000
                                                                        01570000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01580022
                                                                        01590000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01600022
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01610020
                                                                        01620020
           EXIT PROGRAM.                                                01630000
                                                                        01640000
       EJECT.                                                           01650005
