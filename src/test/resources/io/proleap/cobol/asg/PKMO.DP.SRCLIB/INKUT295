       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      INKUT295.                                       00020000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00030000
       DATE-WRITTEN.    10/14/2002.                                     00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:  THE MODULE EXTRACTS SHORTAGE INFORMATION AND FORMATS A 00080000
      *          DATA WAREHOUSE VERSION OF INVENTORY LEDGER SHORTAGE.   00090000
      *          POSTING MONTH END DATE AND THE TYPE OF STORE IS APPLIED00100000
      *          TO THE EXTRACT RECORD AS WELL.                         00110000
      *                                                                 00120000
      *CHANGE HISTORY:                                                  00130000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00140000
      * -------  ----------  ----------------------------------------   00150000
24029 * 24029    10-14-2002  SHORTAGE TO THE DATA WAREHOUSE.            00160000
24029 *                      (CREATION) -ROD JANSSEN                    00170000
W26600* W26600   05-07-2004  INV LEDGER STORE EXPANSION                 00180000
W26600*                      M JOHNSON                                  00190000
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT   00200000
W26006* W26006   02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE. 00210000
W26006*                      - ERIN SEARL                               00220000
W33304* W33304   06-05-2008  ADDITIONAL FIELDS AND REFORMATTING OF DATA 00230000
W33304*                      FOR HILO PROJECT AND BUSINESS OBJ REPORTING00240000
W33304*                      - S JACKSON                                00250000
      ******************************************************************00260000
                                                                        00270000
       ENVIRONMENT DIVISION.                                            00280000
       CONFIGURATION SECTION.                                           00290000
       SOURCE-COMPUTER. IBM-3090.                                       00300000
       OBJECT-COMPUTER. IBM-3090.                                       00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
                                                                        00340000
           SELECT SHORTAGE-EXTRACT-IN                                   00350000
               ASSIGN TO INP01.                                         00360000
                                                                        00370000
           SELECT STR-TYP-CONTROL-CARD-IN                               00380000
               ASSIGN TO INP02.                                         00390000
                                                                        00400000
           SELECT DATE-CONTROL-CARD-IN                                  00410000
               ASSIGN TO INP03.                                         00420000
                                                                        00430000
           SELECT WAREHOUSE-SHORTAGE-OUT                                00440000
               ASSIGN TO OUT01.                                         00450000
                                                                        00460000
       DATA DIVISION.                                                   00470000
       FILE SECTION.                                                    00480000
                                                                        00490000
       FD  SHORTAGE-EXTRACT-IN                                          00500000
           RECORDING MODE IS F                                          00510000
           LABEL RECORDS ARE STANDARD                                   00520000
           BLOCK CONTAINS 0 RECORDS.                                    00530000
W33304 01  SEI-SHORT-EXTRACT-REC    PIC X(170).                         00540000
                                                                        00550000
       FD  STR-TYP-CONTROL-CARD-IN                                      00560000
           RECORDING MODE IS F                                          00570000
           LABEL RECORDS ARE STANDARD                                   00580000
           BLOCK CONTAINS 0 RECORDS.                                    00590000
       01  STCCI-CONTROL-RECORD.                                        00600000
           05  STCCI-SCAN-IND       PIC X(01).                          00601015
           05  STCCI-REST-RECORD    PIC X(79).                          00602015
                                                                        00630000
       FD  DATE-CONTROL-CARD-IN                                         00640000
           RECORDING MODE IS F                                          00650000
           LABEL RECORDS ARE STANDARD                                   00660000
           BLOCK CONTAINS 0 RECORDS.                                    00670000
       01  DCCI-CONTROL-RECORD.                                         00680000
           05  DCCI-EXTRACT-DTE     PIC X(10).                          00690000
           05  DCCI-REST-RECORD     PIC X(70).                          00700000
                                                                        00710000
       FD  WAREHOUSE-SHORTAGE-OUT                                       00720000
           RECORDING MODE IS F                                          00730000
           LABEL RECORDS ARE STANDARD                                   00740000
           BLOCK CONTAINS 0 RECORDS.                                    00750000
       01  WSO-SHORTAGE-RECORD      PIC X(80).                          00760000
                                                                        00770000
      /***************************************************************  00780000
       WORKING-STORAGE SECTION.                                         00790000
      ****************************************************************  00800000
                                                                        00810000
       01  PC-PROGRAM-CONSTANTS.                                        00820000
           05  PC-TABLE-MAX                 PIC 9(05)  VALUE 9999.      00830000
                                                                        00840000
       01  PS-PROGRAM-SWITCHES.                                         00850000
           05  PS-COMPLETE-SW               PIC X(01)  VALUE 'N'.       00860000
               88  PROCESSING-COMPLETE                 VALUE 'Y'.       00870000
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X(01)  VALUE 'N'.       00880000
               88  END-OF-CONTROL-CARDS                VALUE 'Y'.       00890000
           05  PS-END-OF-CONTROL-CARD2-SW   PIC X(01)  VALUE 'N'.       00900000
               88  END-OF-CONTROL-CARD2                VALUE 'Y'.       00910000
           05  PS-TY-KY-DTE-IS-YREND-SW     PIC X(01)  VALUE 'N'.       00920000
               88  PS-TY-KY-DTE-IS-YREND               VALUE 'Y'.       00930000
           05  PS-STORE-FOUND-SW            PIC X(01)  VALUE 'N'.       00940000
               88  STORE-FOUND                         VALUE 'Y'.       00950000
                                                                        00960000
       01  PV-PROGRAM-VARIABLES.                                        00970000
           05  PV-RECS-READ-ACCUM    PIC S9(15) COMP-3 VALUE ZEROS.     00980000
           05  PV-RECS-WRIT-ACCUM    PIC S9(15) COMP-3 VALUE ZEROS.     00990000
           05  PV-EXTRACT-DATE       PIC X(10)  VALUE SPACES.           01000000
           05  PV-KY-DTE             PIC X(10)  VALUE SPACES.           01010000
           05  PV-DB2-OPER-ATTEMPTED PIC X(10).                         01020000
           05  PV-PARAGRAPH-NAME     PIC X(30).                         01030000
W26600     05  PV-EDIT-NMBR          PIC 9(05)  VALUE ZEROS.            01040000
W33304     05  PV-LOC-NBR            PIC  S9(4) COMP VALUE ZEROS.       01050007
W33304     05  PV-INV-SRVC-ID        PIC S9(9) USAGE COMP.              01060004
W33304         88  PV-RGIS-INV-SRVC  VALUE +8428.                       01070004
W33304         88  PV-WIS-INV-SRVC   VALUE +9203.                       01080004
W33304         88  PV-NO-INV-SRVC    VALUE +117902.                     01081016
                                                                        01090000
       01  ABEND-CODE                PIC S9(04) COMP VALUE ZERO.        01100000
                                                                        01110000
       01  ST-STORE-TABLE.                                              01120000
           05  ST-STORE-ENTRY        OCCURS 10000 TIMES                 01130000
                                     INDEXED BY STR-NDX.                01140000
               10  ST-STR-NBR        PIC S9(04) COMP.                   01150000
               10  ST-STORE-NMBR     PIC X(05).                         01160000
                                                                        01170000
      /**************************************************************** 01180000
      * COPYBOOK - INVENTORY SHORTAGE EXTRACT FILE                      01190000
      ***************************************************************** 01200000
W20445     COPY INRD014.                                                01210000
                                                                        01220000
      /**************************************************************** 01230000
      * COPYBOOK - INVENTORY DATA WAREHOUSE SHORTAGE EXTRACT FILE       01240000
      ***************************************************************** 01250000
           COPY INRD028.                                                01260000
                                                                        01270000
      /**************************************************************** 01280000
      * COPYBOOK - DB2 ERROR FIELDS RECORD LAYOUT.                      01290000
      ***************************************************************** 01300000
           COPY DPWS004.                                                01310000
                                                                        01320000
      /***************************************************************  01330000
      * MERCHANDISE LEDGER CONTROL TABLE                                01340000
      ****************************************************************  01350000
           EXEC SQL                                                     01360000
               INCLUDE TMLCNTL                                          01370000
           END-EXEC.                                                    01380000
                                                                        01390000
      /***************************************************************  01400000
      * DATE ATTRIBUTE TABLE                                            01410000
      ****************************************************************  01420000
           EXEC SQL                                                     01430000
               INCLUDE TDTATTR                                          01440000
           END-EXEC.                                                    01450000
                                                                        01460000
W26600/***************************************************************  01470000
W26600*  DCLGEN FOR THE DB2 XMT LOC TABLE                               01480000
W26600****************************************************************  01490000
W26600     EXEC SQL                                                     01500000
W26600         INCLUDE XMT00001                                         01510000
W26600     END-EXEC.                                                    01520000
                                                                        01530001
W33304/***************************************************************  01540001
W33304*  DCLGEN FOR THE DB2 TINVPAR TABLE                               01550015
W33304****************************************************************  01560001
W33304     EXEC SQL                                                     01570001
W33304         INCLUDE TINVPAR                                          01580001
W33304     END-EXEC.                                                    01590001
                                                                        01600000
      /***************************************************************  01610000
      *  COMMUNICATION AREAS FOR DB2                                    01620000
      ****************************************************************  01630000
           EXEC SQL                                                     01640000
               INCLUDE SQLCA                                            01650000
           END-EXEC.                                                    01660000
                                                                        01670000
      /***************************************************************  01680000
      *  SQL DB2 STORE TABLE CURSOR DECLARATION                         01690000
      ****************************************************************  01700000
W26600     EXEC SQL                                                     01710000
W26600         DECLARE STORE_CURSOR CURSOR FOR                          01720000
W26600             SELECT LOC_NBR                                       01730000
W26000             FROM XMT_LOC                                         01740000
W26600             WHERE LOC_TYP_CDE <> 'OP'                            01750000
W26000               AND (DATE(CURRENT TIMESTAMP) < FINCL_CLO_DTE       01760000
W26000                OR  FINCL_CLO_DTE IS NULL)                        01770000
W26600             ORDER BY LOC_NBR                                     01780000
W26600     END-EXEC.                                                    01790000
                                                                        01800000
      /***************************************************************  01810000
      *FLOW:    THE MODULE SEQUENTIALLY READS THE INVENTORY LEDGER      01820000
      *      SHORTAGE RECORD AND FORMATS A DATA WAREHOUSE VERSION FOR   01830000
      *      SHORTAGE. SHORTAGE INFORMATION IS PULLED FROM THE INRD014  01840000
      *      RECORD LAYOUT. THE MODULE ALSO IS PASSED A TYPE OF STORE   01850000
      *      CONTROL CARD; AS WELL AS, A DATE CONTROL CARD.             01860000
      *                                                                 01870000
      *         THE TYPE OF STORE CONTROL CARD DEFINES WHAT TYPE OF     01880000
      *     STORE IS BEING INVENTORIED. TYPES OF STORES INCLUDE:        01890000
      *     *  Y - PHYSICAL INVENTORIED STORE.                          01900000
      *     *  N - ESTIMATED STORE.                                     01910000
      *     THE INFORMATION IS PASSED TO THE DATA WAREHOUSE.            01920000
      *                                                                 01930000
      *         THE POSTING MONTH END DATE IS ALSO DETERMINED FOR THE   01940000
      *     INVENTORIED PERIOD. THE CONTROL DATE CAN EITHER BE PASSED   01950000
      *     EXTERNALLY FROM A CONTROL CARD OR THE CURRENT POSTING       01960000
      *     FISCAL MONTH END DATE CAN BE USED. DATES SHOULD REFLECT THE 01970000
      *     INVENTORY PERIOD OF INVENTORY. THE INFORMATION IS PASSED TO 01980000
      *     THE WAREHOUSE AS WELL.                                      01990000
      *                                                                 02000000
      *     TABLES USED IN INKUT295.                                    02010000
      *     * TDTATTR - KOHLS DATE ATTRIBUTE TABLE.                     02020000
      *     * TMLCNTL - MERCH LEDGER DATE CONTROL TABLE.                02030000
W20445*     * XMT_LOC - KOHL'S LOCATION TABLE.                          02040000
W33304*     * TINVPAR - INVENTORY PARAMETER TABLE.                      02050001
      *                                                                 02060000
      ****************************************************************  02070000
      ****************************************************************  02080000
       PROCEDURE DIVISION.                                              02090000
      ****************************************************************  02100000
                                                                        02110000
       1000-MAIN-MODULE.                                                02120000
                                                                        02130000
           PERFORM 1000-INITIALIZATION.                                 02140000
                                                                        02150000
           PERFORM 2000-MAIN-PROCESS-LOOP                               02160000
               UNTIL PROCESSING-COMPLETE.                               02170000
                                                                        02180000
           PERFORM 9000-EOJ-TASKS.                                      02190000
                                                                        02200000
       9999-EXIT-PROGRAM.                                               02210000
           STOP RUN.                                                    02220000
                                                                        02230000
      /***************************************************************  02240000
      *  1000-INITIALIZATION.                                           02250000
      ****************************************************************  02260000
       1000-INITIALIZATION.                                             02270000
                                                                        02280000
           DISPLAY '*****************************'.                     02290000
           DISPLAY '* START OF INKUT295         *'.                     02300000
           DISPLAY '*****************************'.                     02310000
           DISPLAY ' '.                                                 02320000
                                                                        02330000
           OPEN INPUT  SHORTAGE-EXTRACT-IN                              02340000
                INPUT  DATE-CONTROL-CARD-IN                             02350000
                INPUT  STR-TYP-CONTROL-CARD-IN                          02360000
                OUTPUT WAREHOUSE-SHORTAGE-OUT.                          02370000
                                                                        02380000
           INITIALIZE DCCI-CONTROL-RECORD.                              02390000
           INITIALIZE STCCI-CONTROL-RECORD.                             02400000
                                                                        02410000
           MOVE ZEROS               TO PV-RECS-READ-ACCUM.              02420000
           MOVE ZEROS               TO PV-RECS-WRIT-ACCUM.              02430000
           MOVE 'N'                 TO PS-COMPLETE-SW.                  02440000
           MOVE 'N'                 TO PS-END-OF-CONTROL-CARDS-SW.      02450000
           MOVE 'N'                 TO PS-END-OF-CONTROL-CARD2-SW.      02460000
                                                                        02470000
      *** DETERMINE THE TYPE OF STORE TO BE PROCESSED....               02480000
      ***   Y - PHYSICAL INVENTORIED STORE.                             02490000
      ***   N - ESTIMATED STORE.                                        02500000
                                                                        02510000
           PERFORM 7100-READ-TYPE-OF-STOREFILE.                         02520000
           IF STCCI-SCAN-IND IS EQUAL TO 'Y'                            02521015
               DISPLAY '** STORE TYPE TO PROCESS: ' STCCI-SCAN-IND      02540000
                       ' - PHYSICAL INVENTORIED STORES'                 02550000
           ELSE                                                         02560000
           IF STCCI-SCAN-IND IS EQUAL TO 'N'                            02561015
               DISPLAY '** STORE TYPE TO PROCESS: ' STCCI-SCAN-IND      02580000
                       ' - ESTIMATED STORES'                            02590000
           ELSE                                                         02600000
               DISPLAY '** ABEND **'                                    02610000
               DISPLAY '** PROGRAM INKUT295 **'                         02620000
               DISPLAY '** PARAGRAPH 1000-INITIALIZATION'               02630000
               DISPLAY '** CONTROL CARD TYPE OF STORE IS INVALID.'      02640000
               MOVE +4003 TO ABEND-CODE                                 02650000
               PERFORM 9975-ABEND                                       02660000
           END-IF.                                                      02670000
                                                                        02680000
      *** DETERMINE THE FISCAL PERIOD....                               02690000
           PERFORM 3000-SET-EXTRACT-DTE.                                02700000
                                                                        02710000
      *** READ THE INVENTORY LEDGER SHORTAGE EXTRACT FILE.              02720000
           PERFORM 7200-READ-SHORTAGE-FILE.                             02730000
                                                                        02740000
      *** LOAD THE STORE TABLE FOR STORE INFORMATION.                   02750000
           IF (NOT PROCESSING-COMPLETE)                                 02760000
               INITIALIZE ST-STORE-TABLE                                02770000
               SET STR-NDX          TO 1                                02780000
               PERFORM 3300-LOAD-STORE-ENTRIES                          02790000
           END-IF.                                                      02800000
                                                                        02810000
      /***************************************************************  02820000
      *  2000-MAIN-PROCESS-LOOP.                                        02830000
      ****************************************************************  02840000
       2000-MAIN-PROCESS-LOOP.                                          02850000
                                                                        02860000
           ADD 1                        TO PV-RECS-READ-ACCUM.          02870000
                                                                        02880000
      *** FORMAT AND WRITE A DATA WAREHOUSE SHORTAGE EXTRACT RECORD.    02890000
           INITIALIZE IN028-SHORTAGE-EXTR-REC.                          02900000
                                                                        02910000
           MOVE 'N/A'                   TO IN028-STR-NBR.               02920000
           MOVE 'N'                     TO PS-STORE-FOUND-SW.           02930000
           PERFORM 4000-FIND-ALPHA-STORE                                02940000
             VARYING STR-NDX FROM 1 BY 1                                02950000
               UNTIL STORE-FOUND OR                                     02960000
                     STR-NDX GREATER PC-TABLE-MAX.                      02970000
                                                                        02980000
           MOVE PV-EXTRACT-DATE         TO IN028-PSTG-MTH-BEG-DTE.      02990002
           MOVE IN014-DEPT-NBR          TO IN028-DEPT-NBR.              03000000
           MOVE IN014-MAJ-CL-NBR        TO IN028-MAJ-CL-NBR.            03010000
           MOVE IN014-SUB-CL-NBR        TO IN028-SUB-CL-NBR.            03020000
           MOVE STCCI-SCAN-IND          TO IN028-SCAN-IND.              03030000
           MOVE IN014-BOOK-INV-RTL-AMT  TO IN028-BOOK-INV-RTL-AMT.      03040000
           MOVE IN014-SLS-BSE-RTL-AMT   TO IN028-SLS-BSE-RTL-AMT.       03050000
           MOVE IN014-SHNK-RTL-AMT      TO IN028-SHNK-RTL-AMT.          03060000
           MOVE IN014-PHY-INV-RTL-AMT   TO IN028-PHY-INV-RTL-AMT.       03070000
           MOVE IN014-ESTIMATED-INV-ADJ-AMT                             03080000
                                        TO IN028-ESTIMATED-INV-ADJ-AMT. 03090000
           MOVE IN014-SHORTAGE-RTL-AMT  TO IN028-SHORTAGE-RTL-AMT.      03100000
W33304     MOVE IN014-INV-ID            TO IN028-INV-ID.                03110007
                                                                        03120000
W33304     IF IN014-STR-NBR = PV-LOC-NBR                                03130005
W33304         MOVE PV-INV-SRVC-ID TO IN028-INV-SRVC-ENT-ID             03140008
W33304     ELSE                                                         03150008
W33304        MOVE IN014-STR-NBR TO PV-LOC-NBR                          03160008
W33304        PERFORM 8300-RETRIEVE-INV-PARMS                           03170002
W33304           EVALUATE TRUE                                          03180002
W33304             WHEN INVPAR-INV-SRVC-CDE = 'RG'                      03190002
W33304                 SET  PV-RGIS-INV-SRVC TO TRUE                    03200005
W33304                 MOVE PV-INV-SRVC-ID   TO IN028-INV-SRVC-ENT-ID   03210005
W33304             WHEN INVPAR-INV-SRVC-CDE = 'WI'                      03220002
W33304                 SET  PV-WIS-INV-SRVC TO TRUE                     03230005
W33304                 MOVE PV-INV-SRVC-ID   TO IN028-INV-SRVC-ENT-ID   03240005
W33304             WHEN INVPAR-INV-SRVC-CDE = 'NO'                      03250002
W33304                 SET  PV-NO-INV-SRVC  TO TRUE                     03251013
W33304                 MOVE PV-INV-SRVC-ID   TO IN028-INV-SRVC-ENT-ID   03260013
W33304             WHEN OTHER                                           03270002
W33304                                                                  03280002
W33304                 DISPLAY '** ABEND **'                            03290002
W33304                 DISPLAY '** PROGRAM INKUT295 **'                 03300002
W33304                 DISPLAY '** PARAGRAPH 8300-RETRIEVE-INV-PARMS'   03310002
W33304                 DISPLAY '** INVALID INVENTORY SERVICE CODE'      03320002
W33304                 MOVE +4003 TO ABEND-CODE                         03330002
W33304                 PERFORM 9975-ABEND                               03340002
W33304           END-EVALUATE                                           03350002
W33304     END-IF.                                                      03360002
W33304                                                                  03370002
           PERFORM 7300-WRITE-WAREHOUSE-SHORT.                          03380002
                                                                        03390000
      *** READ THE INVENTORY LEDGER SHORTAGE EXTRACT FILE.              03400000
           PERFORM 7200-READ-SHORTAGE-FILE.                             03410000
                                                                        03420000
      /***************************************************************  03430000
      *  3000-SET-EXTRACT-DTE.                                          03440000
      *  BUILD EXTRACT CONTROL CARD                                     03450000
      ****************************************************************  03460000
       3000-SET-EXTRACT-DTE.                                            03470000
                                                                        03480000
           PERFORM 7000-READ-CONTROL-CARD-FILE.                         03490000
                                                                        03500000
           IF END-OF-CONTROL-CARDS                                      03510000
           OR DCCI-EXTRACT-DTE = SPACE                                  03520000
               PERFORM 3200-USE-TMLCNTL-DATES                           03530000
           ELSE                                                         03540000
               DISPLAY '** INPUT CONTROL CARD: ' DCCI-CONTROL-RECORD    03550000
               PERFORM 3100-USE-CONTROL-CARD-DTE                        03560000
           END-IF.                                                      03570000
                                                                        03580000
           DISPLAY '** SELECTED EXTRACT DATE: ' PV-EXTRACT-DATE.        03590000
                                                                        03600000
      ****************************************************************  03610000
      *  3100-USE-CONTROL-CARD-DTE.                                     03620000
      *  BUILD EXTRACT CONTROL CARD DATE BASED ON CONTROL CARD DATE     03630000
      ****************************************************************  03640000
       3100-USE-CONTROL-CARD-DTE.                                       03650000
                                                                        03660000
           DISPLAY '** USING CONTROL CARD DATE FOR EXTRACT DATE.'       03670000
                                                                        03680000
           MOVE DCCI-EXTRACT-DTE TO PV-KY-DTE.                          03690000
           PERFORM 8200-SELECT-TY-DATES.                                03700000
                                                                        03710000
           IF PV-KY-DTE = DTATTR-FSCL-MN-END-DTE                        03720000
               MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-EXTRACT-DATE           03730000
               DISPLAY '** UNLOAD EXTRACT DATA'                         03740000
               DISPLAY '   ** EXTR DTE = ' PV-EXTRACT-DATE              03750000
               DISPLAY ' '                                              03760000
           ELSE                                                         03770000
               DISPLAY '** ABEND **'                                    03780000
               DISPLAY '** PROGRAM INKUT295 **'                         03790000
               DISPLAY '** PARAGRAPH 3100-USE-CONTROL-CARD-DTE'         03800000
               DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'        03810000
               DISPLAY '** MONTH-END DATE. '                            03820000
               MOVE +4003 TO ABEND-CODE                                 03830000
               PERFORM 9975-ABEND                                       03840000
           END-IF.                                                      03850000
                                                                        03860000
      ****************************************************************  03870000
      * 3200-USE-TMLCNTL-DATES.                                         03880000
      ****************************************************************  03890000
       3200-USE-TMLCNTL-DATES.                                          03900000
                                                                        03910000
           DISPLAY '** USING TMLCNTL DATES FOR EXTRACT DATE.'           03920000
                                                                        03930000
           PERFORM 8000-GET-OPN-FSCL-MNTH-DATE.                         03940000
                                                                        03950000
           MOVE MLCNTL-PSTG-FSCL-MN-DTE  TO PV-KY-DTE.                  03960000
           PERFORM 8200-SELECT-TY-DATES.                                03970000
                                                                        03980000
           DISPLAY                                                      03990000
               '** PSTG FISCAL MONTH DATE: ' MLCNTL-PSTG-FSCL-MN-DTE.   04000000
           DISPLAY                                                      04010000
               '** MAX POSTING MONTH DATE: ' MLCNTL-MXPSTG-FSCL-MN-DTE. 04020000
                                                                        04030000
           IF PV-KY-DTE = DTATTR-FSCL-MN-END-DTE                        04040000
               MOVE DTATTR-FSCL-MN-BEG-DTE TO PV-EXTRACT-DATE           04050000
               DISPLAY '** UNLOAD EXTRACT DATA'                         04060000
               DISPLAY '   ** EXTR DTE = ' PV-EXTRACT-DATE              04070000
               DISPLAY ' '                                              04080000
           ELSE                                                         04090000
               DISPLAY '** ABEND **'                                    04100000
               DISPLAY '** PROGRAM INKUT295 **'                         04110000
               DISPLAY '** PARAGRAPH 3100-USE-CONTROL-CARD-DTE'         04120000
               DISPLAY '** CONTROL CARD DATE NOT A VALID FISCAL'        04130000
               DISPLAY '** MONTH-END DATE. '                            04140000
               MOVE +4003 TO ABEND-CODE                                 04150000
               PERFORM 9975-ABEND                                       04160000
           END-IF.                                                      04170000
                                                                        04180000
      ******************************************************************04190000
      * 3300-LOAD-STORE-ENTRIES.                                        04200000
      * CONTROL FOR LOADING STORES                                      04210000
      ******************************************************************04220000
       3300-LOAD-STORE-ENTRIES.                                         04230000
                                                                        04240000
W20445     INITIALIZE DCLXMT00001.                                      04250000
                                                                        04260000
           EXEC SQL                                                     04270000
               OPEN STORE_CURSOR                                        04280000
           END-EXEC.                                                    04290000
                                                                        04300000
           IF SQLCODE < ZERO                                            04310000
               DISPLAY '** ABEND - INKUT295 **'                         04320000
               DISPLAY '** OPEN ERROR ON STORE TABLE'                   04330000
               MOVE '3300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME      04340000
               GO TO 9950-SQL-ABEND                                     04350000
           ELSE                                                         04360000
           IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR               04370000
              (SQLWARN0 = 'W')                                          04380000
               DISPLAY '** ABEND - INKUT295 **'                         04390000
               DISPLAY '** OPEN WARNING ON STORE TABLE'                 04400000
               MOVE '3300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME      04410000
               GO TO 9950-SQL-ABEND                                     04420000
           END-IF.                                                      04430000
                                                                        04440000
           PERFORM 3400-FETCH-STORE-TABLE.                              04450000
                                                                        04460000
           PERFORM                                                      04470000
             UNTIL SQLCODE = +100                                       04480000
               IF STR-NDX IS LESS THAN PC-TABLE-MAX                     04490000
                   PERFORM 3500-LOAD-STORES                             04500000
                   SET STR-NDX UP BY 1                                  04510000
               ELSE                                                     04520000
                   DISPLAY '** ABEND **'                                04530000
                   DISPLAY '** PGM = INKUT295 **'                       04540000
                   DISPLAY '** STORE TABLE IS TOO SMALL'                04550000
                   DISPLAY '** INCREASE STORE TABLE SIZE'               04560000
                   MOVE +4004 TO ABEND-CODE                             04570000
                   CALL 'ILBOABN0' USING ABEND-CODE                     04580000
               END-IF                                                   04590000
           END-PERFORM.                                                 04600000
                                                                        04610000
           EXEC SQL                                                     04620000
               CLOSE STORE_CURSOR                                       04630000
           END-EXEC.                                                    04640000
                                                                        04650000
           IF SQLCODE < ZERO                                            04660000
               DISPLAY '** ABEND - INKUT295 **'                         04670000
               DISPLAY '** CLOSE ERROR ON STORE TABLE'                  04680000
               MOVE '3300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME      04690000
               GO TO 9950-SQL-ABEND                                     04700000
           ELSE                                                         04710000
           IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR               04720000
              (SQLWARN0 = 'W')                                          04730000
               DISPLAY '** ABEND - INKUT295 **'                         04740000
               DISPLAY '** CLOSE WARNING ON STORE TABLE'                04750000
               MOVE '3300-LOAD-STORE-ENTRIES' TO PV-PARAGRAPH-NAME      04760000
               GO TO 9950-SQL-ABEND                                     04770000
           END-IF.                                                      04780000
      *                                                                 04790000
      ***************************************************************** 04800000
      * 3400-FETCH-STORE-TABLE - FETCH THE DB2 STORE TABEL INTO THE   * 04810000
      * DCLGEN                                                        * 04820000
      ***************************************************************** 04830000
       3400-FETCH-STORE-TABLE.                                          04840000
                                                                        04850000
           EXEC SQL                                                     04860000
               FETCH STORE_CURSOR                                       04870000
               INTO   :XMT00001-LOC-NBR                                 04880000
           END-EXEC.                                                    04890000
                                                                        04900000
           IF SQLCODE < ZERO                                            04910000
               DISPLAY '** ABEND - INKUT295 **'                         04920000
               DISPLAY '** FETCH ERROR ON STORE TABLE'                  04930000
               MOVE '3400-FETCH-STORE-TABLE' TO PV-PARAGRAPH-NAME       04940000
               GO TO 9950-SQL-ABEND                                     04950000
           ELSE                                                         04960000
           IF ((SQLCODE > 0 ) AND (SQLCODE NOT = 100)) OR               04970000
              (SQLWARN0 = 'W')                                          04980000
               DISPLAY '** ABEND - INKUT295 **'                         04990000
               DISPLAY '** FETCH WARNING ON STORE TABLE'                05000000
               MOVE '3400-FETCH-STORE-TABLE' TO PV-PARAGRAPH-NAME       05010000
               GO TO 9950-SQL-ABEND                                     05020000
           END-IF.                                                      05030000
                                                                        05040000
      ***************************************************************** 05050000
      * 3500-LOAD-STORES.                                               05060000
      * MOVES DB2 STORE INFORMATION INTO A WORKING STORAGE TABLE.       05070000
      ***************************************************************** 05080000
       3500-LOAD-STORES.                                                05090000
                                                                        05100000
W26600     MOVE XMT00001-LOC-NBR    TO ST-STR-NBR (STR-NDX).            05110000
W26600     MOVE XMT00001-LOC-NBR    TO PV-EDIT-NMBR.                    05120000
W26600     MOVE PV-EDIT-NMBR        TO ST-STORE-NMBR (STR-NDX).         05130000
                                                                        05140000
           PERFORM 3400-FETCH-STORE-TABLE.                              05150000
                                                                        05160000
      /*****************************************************************05170000
      * 4000-FIND-ALPHA-STORE.                                          05180000
      * DETERMINE THE ALPHANUMERIC STORE NUMBER.                        05190000
      ******************************************************************05200000
       4000-FIND-ALPHA-STORE.                                           05210000
                                                                        05220000
           IF IN014-STR-NBR = ST-STR-NBR (STR-NDX)                      05230000
               MOVE ST-STORE-NMBR (STR-NDX) TO IN028-STR-NBR            05240000
               MOVE 'Y'                     TO PS-STORE-FOUND-SW        05250000
           END-IF.                                                      05260000
                                                                        05270000
      /***************************************************************  05280000
      *  7000-READ-CONTROL-CARD-FILE.                                   05290000
      *  READ CONTROL CARD FILE                                         05300000
      ****************************************************************  05310000
       7000-READ-CONTROL-CARD-FILE.                                     05320000
                                                                        05330000
           READ DATE-CONTROL-CARD-IN NEXT                               05340000
               AT END  MOVE 'Y'     TO PS-END-OF-CONTROL-CARDS-SW.      05350000
                                                                        05360000
      ****************************************************************  05370000
      *  7100-READ-TYPE-OF-STOREFILE.                                   05380000
      *  READ CONTROL CARD FILE                                         05390000
      ****************************************************************  05400000
       7100-READ-TYPE-OF-STOREFILE.                                     05410000
                                                                        05420000
           READ STR-TYP-CONTROL-CARD-IN NEXT                            05430000
               AT END  MOVE 'Y'     TO PS-END-OF-CONTROL-CARD2-SW.      05440000
                                                                        05450000
      ****************************************************************  05460000
      *  7200-READ-SHORTAGE-FILE.                                       05470000
      *  READ CONTROL CARD FILE                                         05480000
      ****************************************************************  05490000
       7200-READ-SHORTAGE-FILE.                                         05500000
                                                                        05510000
           READ SHORTAGE-EXTRACT-IN NEXT                                05520000
             INTO IN014-SHORTAGE-EXTR-REC                               05530000
               AT END  MOVE 'Y'     TO PS-COMPLETE-SW.                  05540000
                                                                        05550000
      ****************************************************************  05560000
      *  7300-WRITE-WAREHOUSE-SHORT.                                    05570000
      *  WRITE A SHORTAGE RECORD FORMATTED FOR THE DATA WAREHOUSE.      05580000
      ****************************************************************  05590000
       7300-WRITE-WAREHOUSE-SHORT.                                      05600000
                                                                        05610000
           WRITE WSO-SHORTAGE-RECORD FROM IN028-SHORTAGE-EXTR-REC.      05620000
                                                                        05630000
           ADD 1                     TO PV-RECS-WRIT-ACCUM.             05640000
                                                                        05650000
      /***************************************************************  05660000
      *  8000-GET-OPN-FSCL-MNTH-DATE.                                   05670000
      *  GET THE OPEN FISCAL MONTH DATE SO IT CAN BE USED TO GET        05680000
      *  ALL OF THE DATE INFO TO CREATE THE CONTROL CARDS               05690000
      ****************************************************************  05700000
       8000-GET-OPN-FSCL-MNTH-DATE.                                     05710000
                                                                        05720000
           EXEC SQL                                                     05730000
              SELECT  OPN_FSCL_MN_DTE                                   05740000
                    , OPN_FSCL_MN_NBR                                   05750000
                    , OPN_FSCL_WK_DTE                                   05760000
                    , OPN_FSCL_WK_NBR                                   05770000
                    , PSTG_FSCL_MN_DTE                                  05780000
                    , PSTG_FSCL_MN_NBR                                  05790000
                    , PSTG_FSCL_WK_DTE                                  05800000
                    , PSTG_FSCL_WK_NBR                                  05810000
                    , MXPSTG_FSCL_MN_DTE                                05820000
                    , MXPSTG_FSCL_MN_NBR                                05830000
                    , MXPSTG_FSCL_WK_DTE                                05840000
                    , MXPSTG_FSCL_WK_NBR                                05850000
                INTO :MLCNTL-OPN-FSCL-MN-DTE                            05860000
                    ,:MLCNTL-OPN-FSCL-MN-NBR                            05870000
                    ,:MLCNTL-OPN-FSCL-WK-DTE                            05880000
                    ,:MLCNTL-OPN-FSCL-WK-NBR                            05890000
                    ,:MLCNTL-PSTG-FSCL-MN-DTE                           05900000
                    ,:MLCNTL-PSTG-FSCL-MN-NBR                           05910000
                    ,:MLCNTL-PSTG-FSCL-WK-DTE                           05920000
                    ,:MLCNTL-PSTG-FSCL-WK-NBR                           05930000
                    ,:MLCNTL-MXPSTG-FSCL-MN-DTE                         05940000
                    ,:MLCNTL-MXPSTG-FSCL-MN-NBR                         05950000
                    ,:MLCNTL-MXPSTG-FSCL-WK-DTE                         05960000
                    ,:MLCNTL-MXPSTG-FSCL-WK-NBR                         05970000
                FROM TMLCNTL                                            05980000
           END-EXEC.                                                    05990000
                                                                        06000000
           EVALUATE TRUE                                                06010000
               WHEN SQLCODE = ZERO                                      06020000
                   CONTINUE                                             06030000
               WHEN OTHER                                               06040000
                   MOVE    '8000-GET-OPN-FSCL-MNTH-DATE'                06050000
                                                TO PV-PARAGRAPH-NAME    06060000
                   MOVE    'SELECT ROW FROM TDTATTR'                    06070000
                                                TO PV-DB2-OPER-ATTEMPTED06080000
                   PERFORM 9950-SQL-ABEND                               06090000
           END-EVALUATE.                                                06100000
                                                                        06110000
      ****************************************************************  06120000
      *  8200-SELECT-TY-DATES.                                          06130000
      *  GET "THIS YEAR" DATES.                                         06140000
      ****************************************************************  06150000
       8200-SELECT-TY-DATES.                                            06160000
           EXEC SQL                                                     06170000
             SELECT LY_THIS_DAY_KY_DTE                                  06180000
                   ,SEA_BEG_DTE                                         06190000
                   ,SEA_END_DTE                                         06200000
                   ,FSCL_MN_NBR                                         06210000
                   ,FSCL_MN_BEG_DTE                                     06220000
                   ,FSCL_MN_END_DTE                                     06230000
                   ,FSCL_YR_END_DTE                                     06240000
              INTO :DTATTR-LY-THIS-DAY-KY-DTE                           06250000
                  ,:DTATTR-SEA-BEG-DTE                                  06260000
                  ,:DTATTR-SEA-END-DTE                                  06270000
                  ,:DTATTR-FSCL-MN-NBR                                  06280000
                  ,:DTATTR-FSCL-MN-BEG-DTE                              06290000
                  ,:DTATTR-FSCL-MN-END-DTE                              06300000
                  ,:DTATTR-FSCL-YR-END-DTE                              06310000
             FROM TDTATTR                                               06320000
             WHERE KY_DTE = :PV-KY-DTE                                  06330000
           END-EXEC.                                                    06340000
                                                                        06350000
           EVALUATE TRUE                                                06360000
               WHEN SQLCODE = ZERO                                      06370000
                   CONTINUE                                             06380000
               WHEN OTHER                                               06390000
                   DISPLAY '** PV-KY-DTE: ' PV-KY-DTE                   06400000
                   MOVE    '8200-SELECT-TY-DATES'                       06410000
                                                TO PV-PARAGRAPH-NAME    06420000
                   MOVE    'SELECT FROM TDTATTR'                        06430000
                                                TO PV-DB2-OPER-ATTEMPTED06440000
                   PERFORM 9950-SQL-ABEND                               06450000
           END-EVALUATE.                                                06460000
                                                                        06470000
      * CHECK FOR DATE = '9999-09-09' IE. NON-EXISTANT DATE             06480000
           IF DTATTR-LY-THIS-DAY-KY-DTE  = '9999-09-09'                 06490000
               PERFORM 8210-RESELECT-LY-THIS-DAY                        06500000
           END-IF.                                                      06510000
                                                                        06520000
      * CHECK IF SELECTING YEAR END DATE.  THIS SWITCH WILL BE USED WHEN06530000
      * PROCESSING LAST YEAR'S DATES.  IF THE CURRENT FISCAL YEAR HAS   06540000
      * 52 WEEKS AND THE PRIOR YEAR HAS 53 WEEKS, THE                   06550000
      * DTATTR-LY-THIS-DAY-KY-DTE IS NOT THE YEAR END DATE, SO THE      06560000
      * ACTUAL YEAR END DATE HAS TO BE USED INSTEAD.                    06570000
           INITIALIZE PS-TY-KY-DTE-IS-YREND-SW.                         06580000
           IF PV-KY-DTE = DTATTR-FSCL-YR-END-DTE                        06590000
               SET PS-TY-KY-DTE-IS-YREND TO TRUE                        06600000
           END-IF.                                                      06610000
                                                                        06620000
      ****************************************************************  06630000
      *  8210-RESELECT-LY-THIS-DAY.                                     06640000
      *  IF THE YEAR BEING SELECTED CONTAINS 53 WEEKS, THE MONTH END    06650000
      *  DATE DOES NOT EXIST IN THE PREVIOUS YEAR (FOR THE LAST WEEK OF 06660000
      *  THE YEAR) TO GET THE CORRECT DATE, SUBTRACT 7 FROM THE DATE    06670000
      *  TO GET THE ACTUAL MONTH END DATE FROM THE PRIOR YEAR.          06680000
      ****************************************************************  06690000
       8210-RESELECT-LY-THIS-DAY.                                       06700000
           EXEC SQL                                                     06710000
             SELECT LY_THIS_DAY_KY_DTE                                  06720000
              INTO :DTATTR-LY-THIS-DAY-KY-DTE                           06730000
             FROM TDTATTR                                               06740000
             WHERE KY_DTE =  DATE(:PV-KY-DTE) - 7 DAYS                  06750000
           END-EXEC.                                                    06760000
                                                                        06770000
           EVALUATE TRUE                                                06780000
               WHEN SQLCODE = ZERO                                      06790000
                   CONTINUE                                             06800000
               WHEN OTHER                                               06810000
                   DISPLAY '** KEY DATE: ' PV-KY-DTE                    06820000
                   MOVE    '8210-RESELECT-LY-THIS-DAY'                  06830000
                                                TO PV-PARAGRAPH-NAME    06840000
                   MOVE    'SELECT FROM TDTATTR'                        06850000
                                                TO PV-DB2-OPER-ATTEMPTED06860000
                   PERFORM 9950-SQL-ABEND                               06870000
           END-EVALUATE.                                                06880000
                                                                        06890000
W33304****************************************************************  06900008
W33304*  8300-RETRIEVE-INV-PARMS.                                       06910008
W33304*  MAKE A CALL TO TINVPAR USING THE CYCLE NUMBER (INV_ID) FROM    06920008
W33304*  THE INPUT RECORD. RETRIEVE THE INVENTORY SERVICE CODE AND      06930008
W33304*  CONVERT TO THE ENT_ID (VALUE STORED IN WORKING STORAGE).       06940008
W33304*  CONSIDER ONLY ACTIVE STORES, ELSE CONSIDER AS 'NO'             06941012
W33304****************************************************************  06950008
W33304 8300-RETRIEVE-INV-PARMS.                                         06960008
W33304     EXEC SQL                                                     06970008
W33304       SELECT INV_SRVC_CDE                                        06980014
W33304        INTO :INVPAR-INV-SRVC-CDE                                 07000014
W33304       FROM TINVPAR                                               07020008
W33304       WHERE LOC_NBR = :PV-LOC-NBR                                07030008
W33304         AND INV_ID  = :IN014-INV-ID                              07040008
W33304         AND LOC_INV_STAT_CDE = 'IN'                              07041010
W33304     END-EXEC.                                                    07050008
W33304                                                                  07060008
W33304     EVALUATE TRUE                                                07070008
W33304         WHEN SQLCODE = ZERO                                      07080008
W33304             CONTINUE                                             07090008
W33304         WHEN SQLCODE = +100                                      07091011
W33304             MOVE 'NO' TO INVPAR-INV-SRVC-CDE                     07092011
W33304         WHEN OTHER                                               07100008
W33304             DISPLAY '** LOCATION: ' PV-LOC-NBR                   07110009
W33304             DISPLAY '** INV-ID  : ' IN014-INV-ID                 07111009
W33304             MOVE    '8300-RETRIEVE-INV-PARMS'                    07120009
W33304                                          TO PV-PARAGRAPH-NAME    07130008
W33304             MOVE    'SELECT FROM TINVPAR'                        07140009
W33304                                          TO PV-DB2-OPER-ATTEMPTED07150008
W33304             PERFORM 9950-SQL-ABEND                               07160008
W33304     END-EVALUATE.                                                07170008
W33304                                                                  07180008
      /***************************************************************  07190001
      *  9000-EOJ-TASKS.                                                07200000
      ****************************************************************  07210000
       9000-EOJ-TASKS.                                                  07220000
                                                                        07230000
           CLOSE SHORTAGE-EXTRACT-IN                                    07240000
                 DATE-CONTROL-CARD-IN                                   07250000
                 STR-TYP-CONTROL-CARD-IN                                07260000
                 WAREHOUSE-SHORTAGE-OUT.                                07270000
                                                                        07280000
                                                                        07290000
           DISPLAY ' '.                                                 07300000
           DISPLAY '*****************************'.                     07310000
           DISPLAY '* SUMMARY'.                                         07320000
           DISPLAY '*****************************'.                     07330000
           DISPLAY '*  RECORDS READ  = ' PV-RECS-READ-ACCUM.            07340000
           DISPLAY '*          WRITE = ' PV-RECS-WRIT-ACCUM.            07350000
           DISPLAY '*****************************'.                     07360000
           DISPLAY ' '.                                                 07370000
           DISPLAY '*****************************'.                     07380000
           DISPLAY '* END  OF INKUT295          *'.                     07390000
           DISPLAY '*****************************'.                     07400000
                                                                        07410000
      ****************************************************************  07420000
      *  9950-SQL-ABEND.                                                07430000
      *  BUILD SQL ABEND INFO                                           07440000
      ****************************************************************  07450000
       9950-SQL-ABEND.                                                  07460000
                                                                        07470000
           MOVE +4013 TO ABEND-CODE.                                    07480000
           DISPLAY ' '.                                                 07490000
           DISPLAY '** ABEND **         ** = SQLERROR'.                 07500000
           DISPLAY '** PROGRAM          ** = INKUT295'.                 07510000
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.       07520000
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.   07530000
                                                                        07540000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07550000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07560000
           COPY DPPD004.                                                07570000
                                                                        07580000
           PERFORM 9975-ABEND.                                          07590000
                                                                        07600000
      ****************************************************************  07610000
      *  9975-ABEND.                                                    07620000
      *  PROGRAM ABEND PARAGRAPH                                        07630000
      ****************************************************************  07640000
       9975-ABEND.                                                      07650000
                                                                        07660000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07670000
                                                                        07680000
