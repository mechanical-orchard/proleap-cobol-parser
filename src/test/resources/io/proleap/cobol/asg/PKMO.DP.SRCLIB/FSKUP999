       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   FSKUP999.                                                  
       AUTHOR.       HEMAVATHY.                                                 
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 03/14/2019.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * READ INPUT FILES,CHECK ITEM NUMBER IN TSKXREF AND UPDATE  TSKST*        
      ******************************************************************        
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      *  WR/PBI        DATE        DESCRIPTION OF CHANGES                       
      *  ----------    --------    ------------------------------------         
      *                03/14/19    ONE TIME PROGRAM.                            
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT INPUT-SKU-FILE ASSIGN TO INP01.                               
           SELECT INPUT-FRD-FILE ASSIGN TO INP02.                               
           SELECT INPUT-SKU-LRD  ASSIGN TO INP03.                               
           SELECT INPUT-SKU-FRD  ASSIGN TO INP04.                               
           SELECT OUTPUT-AUDIT-FILE ASSIGN TO OUT01.                    00630007
                                                                        00650007
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-SKU-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-SKU-RECORD             PIC X(32).                              
                                                                                
       FD  INPUT-FRD-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-FRD-RECORD             PIC X(80).                              
                                                                                
       FD  INPUT-SKU-LRD                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-SKU-LRD-REC            PIC X(32).                              
                                                                                
       FD  INPUT-SKU-FRD                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-SKU-FRD-REC            PIC X(80).                              
                                                                                
       FD  OUTPUT-AUDIT-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  OUTPUT-AUDIT-REC             PIC X(80).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-COMMIT-COUNT               PIC 9(04)  VALUE ZEROES.            
           05 PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.           
           05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.            
           05 PV-DB2-OPERATION-ATTEMPTED    PIC X(30)  VALUE SPACES.            
           05 PV-SQLCODE-DISPLAY            PIC +(8)9  VALUE ZEROES.            
           05 PV-SKU-NBR                    PIC X(08)  VALUE ZEROES.            
           05 PV-SKU-NBR-NMR                PIC 9(08)  VALUE ZEROES.            
           05 PV-LOC-NBR                    PIC 9(04)  VALUE ZEROES.            
           05 PV-LAST-RCVD-DATE             PIC X(06)  VALUE ZEROES.            
           05 PV-LAST-RCVD-DATE1            PIC 9(06)  VALUE ZEROES.            
           05 PV-SKU-NBR1                   PIC X(08)  VALUE ZEROES.            
           05 PV-SKU-NBR-NMR1               PIC 9(08)  VALUE ZEROES.            
           05 PV-LOC-NBR1                   PIC 9(04)  VALUE ZEROES.            
           05 PV-FIRST-RCVD-DATE            PIC X(06)  VALUE ZEROES.            
           05 PV-FIRST-RCVD-DATE1           PIC 9(06)  VALUE ZEROES.            
           05 PV-NULL-INDICATOR             PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
           05 PV-NULL-INDICAT1              PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
       01  PV-INPUT-FILE-LAYOUT             PIC X(32)  VALUE ZEROES.            
       01  PV-INPUT-FRD-LAYOUT              PIC X(80)  VALUE ZEROES.            
                                                                                
       01  PV-INPUT-SKU1-LAYOUT.                                                
           05 PV-LRD-SKU                    PIC 9(08)  VALUE ZEROES.            
           05 FILLER                        PIC X(24)  VALUE SPACES.            
                                                                                
       01  PV-INPUT-SKU2-LAYOUT.                                                
           05 PV-FRD-SKU                    PIC 9(08)  VALUE ZEROES.            
           05 FILLER                        PIC X(72)  VALUE SPACES.            
                                                                                
       01  PV-OUTPUT-FILE-LAYOUT.                                               
           05 PV-OUT-SKU                    PIC X(08)  VALUE ZEROES.            
           05 FILLER                        PIC X(01)  VALUE ','.               
           05 PV-OUT-LOC                    PIC 9(04)  VALUE ZEROES.            
           05 FILLER                        PIC X(01)  VALUE ','.               
           05 PV-OUT-DATE                   PIC X(06)  VALUE SPACES.            
           05 FILLER                        PIC X(60)  VALUE SPACES.            
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-NOT-END-OF-INPUT-SKU-FILE      PIC X(01)  VALUE 'N'.          
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.              
               88  PS-NOT-END-OF-INPUT-SKU              VALUE 'N'.              
                                                                                
           05  PS-NOT-END-OF-INPUT-FRD-FILE      PIC X(01)  VALUE 'N'.          
               88  PS-END-OF-INPUT-FRD                  VALUE 'Y'.              
               88  PS-NOT-END-OF-INPUT-FRD              VALUE 'N'.              
                                                                                
           05  PS-NOT-END-OF-SKU-LRD-FILE        PIC X(01)  VALUE 'N'.          
               88  PS-END-OF-SKU-LRD                   VALUE 'Y'.               
               88  PS-NOT-END-OF-SKU-LRD                VALUE 'N'.              
                                                                                
           05  PS-NOT-END-OF-SKU-FRD-FILE        PIC X(01)  VALUE 'N'.          
               88  PS-END-OF-SKU-FRD                   VALUE 'Y'.               
               88  PS-NOT-END-OF-SKU-FRD                VALUE 'N'.              
                                                                                
           05  PS-SKU-FOUND-TSKXREF              PIC X(01)  VALUE 'N'.          
               88  PS-SKU-NOT-FOUND-XREF                VALUE 'Y'.              
               88  PS-SKU-FOUND-XREF                    VALUE 'N'.              
                                                                                
           05  PS-ITMLOC-FOUND-TSKST             PIC X(01)  VALUE 'N'.          
               88  PS-ITMLOC-NOT-FOUND                  VALUE 'Y'.              
               88  PS-ITMLOC-FOUND                      VALUE 'N'.              
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-ROWS-READ           PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-INPUT1-ROWS-READ          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-INPUT2-ROWS-READ          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-INPUT3-ROWS-READ          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PV-TSKXREF-SELECT-CNT        PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PV-TSKXREF-SKIP-CNT          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PV-TSKST-SKIP-CNT            PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PV-TSKST-LRD-LOC0-CNT        PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PV-TSKST-FRD-LOC0-CNT        PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-TSKST-ROW-UPDATED         PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-COMMIT-TAKEN              PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-NOT-UPDATD-CNT            PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE 0.                 
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.             
           88  ABEND-4002-CC-INVALID-ERR               VALUE +4002.             
                                                                                
      *****************************************************************         
      * PROGRAM CONSTANTS                                                       
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME       PIC X(08)                          
                                                   VALUE 'FSKUP999'.            
           05  PC-MAX-RETRIES                PIC S9(04) VALUE +5 COMP.          
           05  PC-COMMIT-MAX                 PIC 9(09) VALUE   999.             
                                                                                
      *****************************************************************         
      * TSKST TABLE DCLGEN.                                                     
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKST                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TSKXREF TABLE DCLGEN.                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TSKXREF                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-READ-INPUT-FILE.                                        
                                                                                
           PERFORM 3000-PROCESS-RECORDS                                         
             UNTIL PS-END-OF-INPUT-FILE.                                        
                                                                                
           MOVE '****END OF LRD RECORD****' TO OUTPUT-AUDIT-REC                 
           WRITE OUTPUT-AUDIT-REC.                                              
                                                                                
           PERFORM 3300-READ-FRD-FILE.                                          
                                                                                
           PERFORM 3600-PROCESS-FRD-RECORDS                                     
             UNTIL PS-END-OF-INPUT-FRD.                                         
                                                                                
           PERFORM 4000-READ-SKU-LRD.                                           
                                                                                
           PERFORM 4300-PROCESS-SKU-LRD-CORP                                    
             UNTIL PS-END-OF-SKU-LRD.                                           
                                                                                
           MOVE '****END OF FRD RECORD****' TO OUTPUT-AUDIT-REC                 
           WRITE OUTPUT-AUDIT-REC.                                              
                                                                                
           PERFORM 5000-READ-SKU-FRD.                                           
                                                                                
           PERFORM 5300-PROCESS-SKU-FRD-CORP                                    
             UNTIL PS-END-OF-SKU-FRD.                                           
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
           INITIALIZE PV-INPUT-FILE-LAYOUT                                      
           INITIALIZE PV-INPUT-FRD-LAYOUT                                       
           INITIALIZE PV-INPUT-SKU1-LAYOUT                                      
           INITIALIZE PV-INPUT-SKU2-LAYOUT                                      
           INITIALIZE PV-OUTPUT-FILE-LAYOUT                                     
           OPEN INPUT  INPUT-SKU-FILE                                           
                       INPUT-FRD-FILE                                           
                       INPUT-SKU-LRD                                            
                       INPUT-SKU-FRD                                            
                OUTPUT OUTPUT-AUDIT-FILE.                                       
                                                                                
      ******************************************************************        
      * 2000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       2000-READ-INPUT-FILE.                                                    
                                                                                
           INITIALIZE PV-LAST-RCVD-DATE                                         
           INITIALIZE PV-SKU-NBR                                                
           INITIALIZE PV-LOC-NBR                                                
                                                                                
           READ INPUT-SKU-FILE                                                  
           INTO PV-INPUT-FILE-LAYOUT                                            
             AT END                                                             
               SET PS-END-OF-INPUT-FILE TO TRUE                                 
             NOT AT END                                                         
               ADD +1                    TO  PA-INPUT-ROWS-READ                 
           END-READ.                                                            
      ******************************************************************        
      * 3300-READ-FRD-FILE.                                                     
      ******************************************************************        
       3300-READ-FRD-FILE.                                                      
                                                                                
           INITIALIZE PV-FIRST-RCVD-DATE                                        
           INITIALIZE PV-SKU-NBR1                                               
           INITIALIZE PV-LOC-NBR1                                               
                                                                                
           READ INPUT-FRD-FILE                                                  
           INTO PV-INPUT-FRD-LAYOUT                                             
             AT END                                                             
               SET PS-END-OF-INPUT-FRD TO TRUE                                  
             NOT AT END                                                         
               ADD +1                    TO  PA-INPUT1-ROWS-READ                
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 4000-READ-SKU-LRD.                                                      
      ******************************************************************        
       4000-READ-SKU-LRD.                                                       
                                                                                
           INITIALIZE PV-INPUT-SKU1-LAYOUT                                      
                                                                                
           READ INPUT-SKU-LRD                                                   
           INTO PV-INPUT-SKU1-LAYOUT                                            
             AT END                                                             
               SET PS-END-OF-SKU-LRD TO TRUE                                    
             NOT AT END                                                         
               ADD +1                    TO  PA-INPUT2-ROWS-READ                
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 5000-READ-SKU-FRD.                                                      
      ******************************************************************        
       5000-READ-SKU-FRD.                                                       
                                                                                
           INITIALIZE PV-INPUT-SKU2-LAYOUT                                      
                                                                                
           READ INPUT-SKU-FRD                                                   
           INTO PV-INPUT-SKU2-LAYOUT                                            
             AT END                                                             
               SET PS-END-OF-SKU-FRD TO TRUE                                    
             NOT AT END                                                         
               ADD +1                    TO  PA-INPUT3-ROWS-READ                
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 3000-PROCESS-RECORDS                                                    
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
                                                                                
             UNSTRING PV-INPUT-FILE-LAYOUT  DELIMITED BY ','                    
                 INTO PV-SKU-NBR                                                
                      PV-LOC-NBR                                                
                      PV-LAST-RCVD-DATE                                         
                                                                                
           COMPUTE PV-SKU-NBR-NMR =                                             
                        FUNCTION NUMVAL(PV-SKU-NBR)                             
           COMPUTE PV-LAST-RCVD-DATE1 =                                         
                        FUNCTION NUMVAL(PV-LAST-RCVD-DATE)                      
                                                                                
           INITIALIZE SKXREF-SKU                                                
                      TSKST-LOC-NBR                                             
                      TSKST-LAST-RCVD-DATE                                      
                      TSKST-ITEM-NMBR                                           
                  MOVE PV-SKU-NBR-NMR      TO SKXREF-SKU                        
                  MOVE PV-LOC-NBR          TO TSKST-LOC-NBR                     
                                                                                
                 PERFORM 3100-GET-ITM-NBR                                       
                                                                                
           IF PS-SKU-FOUND-XREF                                                 
                                                                                
                 MOVE SKXREF-ITM-NBR   TO TSKST-ITEM-NMBR                       
                  PERFORM 3400-SELECT-TSKST                                     
                                                                                
             IF PS-ITMLOC-FOUND                                                 
                                                                                
                  IF TSKST-LAST-RCVD-DATE < 190311                              
                  AND PV-LAST-RCVD-DATE1 < 190311                               
                                                                                
                    MOVE PV-LAST-RCVD-DATE1  TO TSKST-LAST-RCVD-DATE            
                                                                                
                     PERFORM 3200-UPDATE-TSKST                                  
                                                                                
                     IF TSKST-LAST-RCVD-DATE = 0                                
                      MOVE 0 TO TSKST-FIRST-RCVD-DATE                           
                      PERFORM 3700-UPDATE-TSKST-FRD                             
                     END-IF                                                     
                                                                                
                     MOVE PV-SKU-NBR-NMR TO PV-OUT-SKU                          
                     MOVE PV-LOC-NBR TO PV-OUT-LOC                              
                     MOVE PV-LAST-RCVD-DATE1 TO PV-OUT-DATE                     
                                                                                
                     WRITE OUTPUT-AUDIT-REC FROM PV-OUTPUT-FILE-LAYOUT          
                                                                                
                  ELSE                                                          
                    DISPLAY '*********NOT UPDATED************'                  
                    DISPLAY 'LAST RECVD DATE IS >= 190311'                      
                    DISPLAY 'SKU NUMBER: ' PV-SKU-NBR-NMR                       
                    DISPLAY 'LOC: ' PV-LOC-NBR                                  
                    DISPLAY 'TSKST LRD: ' TSKST-LAST-RCVD-DATE                  
                    DISPLAY 'FILE LRD: ' PV-LAST-RCVD-DATE1                     
                    DISPLAY '********************************'                  
                    ADD  +1              TO PA-NOT-UPDATD-CNT                   
                  END-IF                                                        
             END-IF                                                             
           END-IF                                                               
                                                                                
              PERFORM 2000-READ-INPUT-FILE.                                     
                                                                                
      ******************************************************************        
      * 3600-PROCESS-FRD-RECORDS                                                
      ******************************************************************        
       3600-PROCESS-FRD-RECORDS.                                                
                                                                                
             UNSTRING PV-INPUT-FRD-LAYOUT  DELIMITED BY ','                     
                 INTO PV-SKU-NBR1                                               
                      PV-LOC-NBR1                                               
                      PV-FIRST-RCVD-DATE                                        
                                                                                
            COMPUTE PV-SKU-NBR-NMR1 =                                           
                         FUNCTION NUMVAL(PV-SKU-NBR1)                           
            COMPUTE PV-FIRST-RCVD-DATE1 =                                       
                         FUNCTION NUMVAL(PV-FIRST-RCVD-DATE)                    
                                                                                
           INITIALIZE SKXREF-SKU                                                
                      TSKST-LOC-NBR                                             
                      TSKST-FIRST-RCVD-DATE                                     
                      TSKST-ITEM-NMBR                                           
                  MOVE PV-SKU-NBR-NMR1     TO SKXREF-SKU                        
                  MOVE PV-LOC-NBR1         TO TSKST-LOC-NBR                     
                  MOVE PV-FIRST-RCVD-DATE1 TO TSKST-FIRST-RCVD-DATE             
                                                                                
           PERFORM 3100-GET-ITM-NBR                                             
                                                                                
            IF PS-SKU-FOUND-XREF                                                
                                                                                
               MOVE SKXREF-ITM-NBR   TO TSKST-ITEM-NMBR                         
                PERFORM 3400-SELECT-TSKST                                       
                                                                                
                  IF PS-ITMLOC-FOUND                                            
                                                                                
                     PERFORM 3700-UPDATE-TSKST-FRD                              
                                                                                
                     MOVE PV-SKU-NBR-NMR1 TO PV-OUT-SKU                         
                     MOVE PV-LOC-NBR1 TO PV-OUT-LOC                             
                     MOVE PV-FIRST-RCVD-DATE1 TO PV-OUT-DATE                    
                                                                                
                     WRITE OUTPUT-AUDIT-REC FROM PV-OUTPUT-FILE-LAYOUT          
                                                                                
                  END-IF                                                        
                                                                                
            END-IF                                                              
                                                                                
            PERFORM 3300-READ-FRD-FILE.                                         
                                                                                
      ******************************************************************        
      * 4300-PROCESS-SKU-LRD-CORP                                               
      ******************************************************************        
       4300-PROCESS-SKU-LRD-CORP.                                               
                                                                                
           INITIALIZE SKXREF-SKU                                                
                      TSKST-LOC-NBR                                             
                      TSKST-ITEM-NMBR                                           
                  MOVE PV-LRD-SKU          TO SKXREF-SKU                        
                  MOVE 0                   TO TSKST-LOC-NBR                     
                                                                                
           PERFORM 3100-GET-ITM-NBR                                             
                                                                                
            IF PS-SKU-FOUND-XREF                                                
                                                                                
                  MOVE SKXREF-ITM-NBR   TO TSKST-ITEM-NMBR                      
                                                                                
                PERFORM 7000-READ-TSKST-MAX-RCVD-DTE                            
                                                                                
               IF PS-ITMLOC-FOUND                                               
                PERFORM 3200-UPDATE-TSKST                                       
               END-IF                                                           
                                                                                
            END-IF                                                              
                                                                                
            PERFORM 4000-READ-SKU-LRD.                                          
                                                                                
      ******************************************************************        
      * 5300-PROCESS-SKU-FRD-CORP                                               
      ******************************************************************        
       5300-PROCESS-SKU-FRD-CORP.                                               
                                                                                
           INITIALIZE SKXREF-SKU                                                
                      TSKST-LOC-NBR                                             
                      TSKST-ITEM-NMBR                                           
                  MOVE PV-FRD-SKU          TO SKXREF-SKU                        
                  MOVE 0                   TO TSKST-LOC-NBR                     
                                                                                
           PERFORM 3100-GET-ITM-NBR                                             
                                                                                
            IF PS-SKU-FOUND-XREF                                                
                                                                                
                  MOVE SKXREF-ITM-NBR   TO TSKST-ITEM-NMBR                      
                                                                                
                  PERFORM 7500-READ-TSKST-MIN-FRD-DTE                           
                                                                                
                IF PS-ITMLOC-FOUND                                              
                   PERFORM 3700-UPDATE-TSKST-FRD                                
                END-IF                                                          
                                                                                
            END-IF                                                              
                                                                                
            PERFORM 5000-READ-SKU-FRD.                                          
                                                                                
      ******************************************************************        
      * 3100-GET-ITM-NBR.                                                       
      ******************************************************************        
       3100-GET-ITM-NBR.                                                        
                                                                                
           INITIALIZE SKXREF-ITM-NBR.                                           
                                                                                
               EXEC SQL                                                         
                 SELECT ITM_NBR                                                 
                   INTO :SKXREF-ITM-NBR                                         
                   FROM TSKXREF                                                 
                  WHERE SKU = :SKXREF-SKU                                       
                  WITH UR                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       ADD  +1              TO PV-TSKXREF-SELECT-CNT            
                       SET PS-SKU-FOUND-XREF    TO TRUE                         
                   WHEN +100                                                    
                       ADD  +1              TO PV-TSKXREF-SKIP-CNT              
                       DISPLAY '*********SKU SKIPPED***************'            
                       DISPLAY 'SKU NOT FOUND IN TSKXREF ' PV-SKU-NBR           
                       DISPLAY '***********************************'            
                       SET PS-SKU-NOT-FOUND-XREF TO TRUE                        
                   WHEN OTHER                                                   
                       MOVE '3100-GET-ITM-NBR'                                  
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'GET ITEM FROM TSKXREF'                             
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'SKU NUMBER = ' SKXREF-SKU                       
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 3400-SELECT-TSKST.                                                      
      ******************************************************************        
       3400-SELECT-TSKST.                                                       
                                                                                
           INITIALIZE TSKST-LAST-RCVD-DATE.                                     
                                                                                
               EXEC SQL                                                         
                 SELECT LAST_RCVD_DATE                                          
                   INTO :TSKST-LAST-RCVD-DATE                                   
                   FROM TSKST                                                   
                  WHERE ITEM_NMBR = :TSKST-ITEM-NMBR                            
                  AND LOC_NBR = :TSKST-LOC-NBR                                  
                 WITH UR                                                        
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       ADD  +1              TO PV-TSKXREF-SELECT-CNT            
                       SET PS-ITMLOC-FOUND  TO TRUE                             
                   WHEN +100                                                    
                       ADD  +1              TO PV-TSKST-SKIP-CNT                
                       DISPLAY '**************************'                     
                       DISPLAY 'ITM/LOC NOT FOUND IN TSKST'                     
                       DISPLAY 'ITEM: ' TSKST-ITEM-NMBR                         
                       DISPLAY 'SKU: '  SKXREF-SKU                              
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '**************************'                     
                       SET PS-ITMLOC-NOT-FOUND  TO TRUE                         
                   WHEN OTHER                                                   
                       MOVE '3400-SELECT-TSKST'                                 
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'GET LRD FROM TSKST'                                
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'ITEM: ' TSKST-ITEM-NMBR                         
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
      *****************************************************************         
      * 3200-UPDATE-TSKST.                                                      
      *****************************************************************         
       3200-UPDATE-TSKST.                                                       
                                                                                
               EXEC SQL                                                         
                   UPDATE TSKST                                                 
                      SET LAST_RCVD_DATE = :TSKST-LAST-RCVD-DATE                
                         ,LAST_UPD_TMST = CURRENT TIMESTAMP                     
                   WHERE ITEM_NMBR       = :TSKST-ITEM-NMBR                     
                   AND   LOC_NBR       = :TSKST-LOC-NBR                         
               END-EXEC.                                                        
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                 ADD +1  TO PA-TSKST-ROW-UPDATED                                
                 ADD +1         TO PV-COMMIT-COUNT                              
                  IF TSKST-LOC-NBR = 0                                          
                     ADD  +1              TO PV-TSKST-LRD-LOC0-CNT              
                  END-IF                                                        
                 IF PV-COMMIT-COUNT > PC-COMMIT-MAX                             
                    MOVE 0 TO PV-COMMIT-COUNT                                   
                    PERFORM 3500-UPDATE-COMMIT                                  
                 END-IF                                                         
               WHEN OTHER                                                       
                       MOVE '3200-UPDATE-TSKST'                                 
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'UPDATE TABLE TSKST'                                
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'ITEM NUMBER = ' TSKST-ITEM-NMBR                 
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
             END-EVALUATE.                                                      
                                                                                
      *****************************************************************         
      * 3700-UPDATE-TSKST-FRD.                                                  
      *****************************************************************         
       3700-UPDATE-TSKST-FRD.                                                   
                                                                                
               EXEC SQL                                                         
                   UPDATE TSKST                                                 
                      SET FIRST_RCVD_DATE = :TSKST-FIRST-RCVD-DATE              
                         ,LAST_UPD_TMST = CURRENT TIMESTAMP                     
                   WHERE ITEM_NMBR       = :TSKST-ITEM-NMBR                     
                   AND   LOC_NBR        = :TSKST-LOC-NBR                        
               END-EXEC.                                                        
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                 ADD +1  TO PA-TSKST-ROW-UPDATED                                
                 ADD +1         TO PV-COMMIT-COUNT                              
                  IF TSKST-LOC-NBR = 0                                          
                     ADD  +1              TO PV-TSKST-FRD-LOC0-CNT              
                  END-IF                                                        
                 IF PV-COMMIT-COUNT > PC-COMMIT-MAX                             
                    MOVE 0 TO PV-COMMIT-COUNT                                   
                    PERFORM 3500-UPDATE-COMMIT                                  
                 END-IF                                                         
               WHEN OTHER                                                       
                       MOVE '3700-UPDATE-TSKST-FRD'                             
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'UPDATE TABLE TSKST FRD'                            
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'ITEM NUMBER = ' TSKST-ITEM-NMBR                 
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 7000-READ-TSKST-MAX-RCVD-DTE.                                           
      ******************************************************************        
       7000-READ-TSKST-MAX-RCVD-DTE.                                            
                                                                                
           INITIALIZE TSKST-LAST-RCVD-DATE                                      
           MOVE ZEROES TO PV-NULL-INDICATOR                                     
                                                                                
               EXEC SQL                                                         
                 SELECT MAX(LAST_RCVD_DATE)                                     
                   INTO :TSKST-LAST-RCVD-DATE :PV-NULL-INDICATOR                
                   FROM TSKST                                                   
                    WHERE ITEM_NMBR       = :TSKST-ITEM-NMBR                    
                    AND LOC_NBR       <> 0                                      
                  WITH UR                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       SET PS-ITMLOC-FOUND TO TRUE                              
                                                                                
                   IF PV-NULL-INDICATOR < 0                                     
                       ADD  +1              TO PV-TSKST-SKIP-CNT                
                       DISPLAY '**************************'                     
                       DISPLAY 'ITM NOT FOUND IN TSKST'                         
                       DISPLAY 'ITEM: ' TSKST-ITEM-NMBR                         
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY '**************************'                     
                       SET PS-ITMLOC-NOT-FOUND  TO TRUE                         
                   END-IF                                                       
                   CONTINUE                                                     
                                                                                
                   WHEN OTHER                                                   
                       MOVE '7000-READ-TSKST-MAX-RCVD-DTE'                      
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'GET MAX OF LAST RCVD DTE FOR LOC0'                 
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'ITM NUMBER = ' TSKST-ITEM-NMBR                  
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 7500-READ-TSKST-MIN-FRD-DTE.                                            
      ******************************************************************        
       7500-READ-TSKST-MIN-FRD-DTE.                                             
                                                                                
           INITIALIZE TSKST-FIRST-RCVD-DATE                                     
           MOVE ZEROES TO PV-NULL-INDICAT1                                      
                                                                                
               EXEC SQL                                                         
                 SELECT MIN(FIRST_RCVD_DATE)                                    
                   INTO :TSKST-FIRST-RCVD-DATE :PV-NULL-INDICAT1                
                   FROM TSKST                                                   
                    WHERE ITEM_NMBR       = :TSKST-ITEM-NMBR                    
                    AND FIRST_RCVD_DATE <> 0                                    
                    AND LOC_NBR       <> 0                                      
                  WITH UR                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE SQLCODE                                                 
                   WHEN +0                                                      
                       SET PS-ITMLOC-FOUND TO TRUE                              
                                                                                
                       IF PV-NULL-INDICAT1 < 0                                  
                        ADD  +1              TO PV-TSKST-SKIP-CNT               
                        DISPLAY '**************************'                    
                        DISPLAY 'ITM NOT FOUND IN TSKST'                        
                        DISPLAY 'ITEM: ' TSKST-ITEM-NMBR                        
                        DISPLAY 'SKU: ' SKXREF-SKU                              
                        DISPLAY '**************************'                    
                        SET PS-ITMLOC-NOT-FOUND  TO TRUE                        
                       END-IF                                                   
                                                                                
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE '7500-READ-TSKST-MIN-FRD-DTE'                       
                                        TO PV-PARAGRAPH-NAME                    
                       MOVE 'GET MIN OF FIRST RCVD DTE FOR LOC0'                
                                        TO PV-DB2-OPERATION-ATTEMPTED           
                       DISPLAY '*******************************'                
                       DISPLAY 'ITM NUMBER = ' TSKST-ITEM-NMBR                  
                       DISPLAY 'SKU: ' SKXREF-SKU                               
                       DISPLAY '*******************************'                
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 3500-UPDATE-COMMIT.                                                     
      ******************************************************************        
       3500-UPDATE-COMMIT.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0)                                       
                                                                                
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = 0                                             
                       ADD +1 TO PA-COMMIT-TAKEN                                
                       DISPLAY '***********COMMIT TAKEN*********'               
                       DISPLAY 'COMMIT COUNT: ' PA-COMMIT-TAKEN                 
                       DISPLAY 'SKU NUMBER: ' PV-SKU-NBR                        
                       DISPLAY 'LOC: ' TSKST-LOC-NBR                            
                       DISPLAY '********************************'               
                       CONTINUE                                                 
                   WHEN SQLWARN NOT EQUAL SPACES                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                       MOVE '3500-UPDATE-COMMIT'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COMMIT FAILURE'                                    
                                    TO  PV-DB2-OPERATION-ATTEMPTED              
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
            IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3500-UPDATE-COMMIT'                                        
                                    TO  PV-PARAGRAPH-NAME                       
               MOVE 'COMMIT RETRIES EXCEEDED'                                   
                                    TO  PV-DB2-OPERATION-ATTEMPTED              
               PERFORM 9100-SQL-ABEND                                           
            END-IF.                                                             
                                                                                
      *****************************************************************         
      * 8000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 3500-UPDATE-COMMIT.                                          
                                                                                
           CLOSE INPUT-SKU-FILE                                                 
                 INPUT-FRD-FILE                                                 
                 INPUT-SKU-LRD                                                  
                 INPUT-SKU-FRD                                                  
                 OUTPUT-AUDIT-FILE.                                             
                                                                                
           PERFORM 9005-DISPLAY-TOTALS.                                         
                                                                                
      ******************************************************************        
      * 9100-SQL-ABEND.                                                         
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY ' '.                                                         
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = FSKUP999'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.                     
                                                                                
           PERFORM 9005-DISPLAY-TOTALS.                                         
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * 9005-DISPLAY-TOTALS.                                                    
      ******************************************************************        
       9005-DISPLAY-TOTALS.                                                     
           DISPLAY '******** FSKUP999 TOTALS *********'.                        
           DISPLAY ' INPUT LRD RECS READ  : ' PA-INPUT-ROWS-READ.               
           DISPLAY ' INPUT FRD RECS READ  : ' PA-INPUT1-ROWS-READ.              
           DISPLAY ' INPUT LRD SKUS READ  : ' PA-INPUT2-ROWS-READ.              
           DISPLAY ' INPUT FRD SKUS READ  : ' PA-INPUT3-ROWS-READ.              
           DISPLAY ' TOTAL ITEM SELECT    : ' PV-TSKXREF-SELECT-CNT.            
           DISPLAY ' SKU NOT FOUND IN XREF: ' PV-TSKXREF-SKIP-CNT.              
           DISPLAY ' ITM/LOC NOT IN  TSKST: ' PV-TSKST-SKIP-CNT.                
           DISPLAY ' TOTAL ROWS UPDATED   : ' PA-TSKST-ROW-UPDATED.             
           DISPLAY ' TOTAL ROWS NOT UPDT  : ' PA-NOT-UPDATD-CNT.                
           DISPLAY ' TOTAL LOC0 LRD UPDT  : ' PV-TSKST-LRD-LOC0-CNT.            
           DISPLAY ' TOTAL LOC0 FRD UPDT  : ' PV-TSKST-FRD-LOC0-CNT.            
           DISPLAY ' NUMBER OF COMMIT TAKN: ' PA-COMMIT-TAKEN.                  
           DISPLAY '***** END OF FSKUP999 TOTALS *****'.                        
           DISPLAY ' '.                                                         
