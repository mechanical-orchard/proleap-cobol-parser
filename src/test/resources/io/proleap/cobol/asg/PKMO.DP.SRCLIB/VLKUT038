000100 IDENTIFICATION DIVISION.                                                 
000300 PROGRAM-ID.             VLKUT038.                                        
000400 AUTHOR.                 VIRGENE LENNEMAN.                                
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           MARCH 2005.                                      
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM READS A FILE OF DEBIT INFORMATION FROM THE                
001200*    COMPLIANCE MGMT SYS., ADDS ADDITIONAL INFORMATION FROM THE           
001300*    AP TABLES AND CREATES A NEW RECORD MATCHING THE COPYBOOK             
001400*    APRD045.                                                             
001500*                                                                         
001500*  BEFORE THE PROCESSING FOR THE AP FILE IS DONE, THE INPUT FILE          
001500*    IS PROCESSED TO SIMPLY ADD UP THE NUMBER OF RECORDS  AND             
001500*    AMOUNTS. THESE AMOUNTS ARE THEN COMPARED TO WHAT CMS SENT            
001500*    SENT ON THE CONTROL RECORD.  IF ALL MATCH THEN PROCESSING CAN        
001500*    CONTINUE, OTHERWISE PROGRAM WILL ABEND.                              
001500*                                                                         
001600*  INPUT: FILE FROM COMPLIANCE MGMT SYSTEM, COPYBOOK VLRD039              
001700*  OUTPUT: AP TRANSACTIONS '6A' (CHARGES) & '6B' (CREDITS),               
001800*          COPYBOOK APRD045                                               
001800*                                                                         
001900*  DB2 TABLES: TAPCNTL, TTRNCDE                                           
002000*                                                                         
002600******************************************************************        
002700/                                                                         
002800 ENVIRONMENT DIVISION.                                                    
002900                                                                          
003000 CONFIGURATION SECTION.                                                   
003100                                                                          
003200 SOURCE-COMPUTER.                        IBM-3090.                        
003300 OBJECT-COMPUTER.                        IBM-3090.                        
003400                                                                          
003500 INPUT-OUTPUT SECTION.                                                    
003600                                                                          
003700 FILE-CONTROL.                                                            
003800                                                                          
003900     SELECT  CMS-EXT-FILE                                                 
004000         ASSIGN TO INP01.                                                 
004100                                                                          
004200     SELECT  AP-TRANSACTION-FILE                                          
004300         ASSIGN TO OUT01.                                                 
004400                                                                          
004200     SELECT  AP-COMPLETE-FILE                                             
004300         ASSIGN TO OUT02.                                                 
004400                                                                          
004500******************************************************************        
004600 DATA DIVISION.                                                           
004700******************************************************************        
004800                                                                          
004900 FILE SECTION.                                                            
005000*                                                                         
005100 FD  CMS-EXT-FILE                                                         
005200     RECORDING MODE IS F                                                  
005300     LABEL RECORDS ARE STANDARD                                           
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  CMS-EXTRACT-RECORD               PIC  X(650).                        
005600*                                                                         
005700 FD  AP-TRANSACTION-FILE                                                  
005800     RECORDING MODE IS F                                                  
005900     LABEL RECORDS ARE STANDARD                                           
006000     BLOCK CONTAINS 0 RECORDS.                                            
006100 01  AP-TRANSACTION-RECORD            PIC  X(500).                        
006400                                                                          
005700 FD  AP-COMPLETE-FILE                                                     
005800     RECORDING MODE IS F                                                  
005900     LABEL RECORDS ARE STANDARD                                           
006000     BLOCK CONTAINS 0 RECORDS.                                            
006100 01  AP-COMPLETE-RECORD               PIC  X(30).                         
006400                                                                          
006500 WORKING-STORAGE SECTION.                                                 
006700 01  ABEND-CODE                       PIC S9(04) VALUE ZERO               
006800                                                 COMP SYNC.               
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
006900                                                                          
013600 01  PC-CONSTANTS.                                                        
013700     05  PC-CURRENT-PGM-VLKUT038      PIC  X(08) VALUE 'VLKUT038'.        
013800                                                                          
013900 01  PS-SWITCHES.                                                         
014000     05  PS-EOF-EXTRACT-SWITCH        PIC  X(01) VALUE 'N'.               
014100         88  PS-EOF-EXTRACT                      VALUE 'Y'.               
014100         88  PS-NOT-EOF-EXTRACT                  VALUE 'N'.               
014000     05  PS-END-INPUT-SWITCH          PIC  X(01) VALUE 'N'.               
014100         88  PS-END-INPUT                        VALUE 'Y'.               
014100         88  PS-NOT-END-INPUT                    VALUE 'N'.               
014000     05  PS-AMOUNTS-MATCH-SWITCH      PIC  X(01) VALUE 'Y'.               
014100         88  PS-AMOUNTS-MATCH                    VALUE 'Y'.               
014100         88  PS-AMOUNTS-NOT-MATCH                VALUE 'N'.               
014000     05  PS-DTL-CNTL-SWITCH           PIC  X(01) VALUE 'Y'.               
014100         88  PS-DTLS-AND-CNTL                    VALUE 'Y'.               
014100         88  PS-CNTL-ONLY                        VALUE 'N'.               
007200                                                                          
007300 01  PV-SAVE-TTRNCDE-TABLE.                                               
007400     05  FILLER OCCURS 2 TIMES.                                           
007500         10 PV-PSTG-TYP-CDE           PIC X(02).                          
007600         10 PV-TRN-TYP-CDE            PIC X(03).                          
007700         10 PV-GL-ACCT-NBR            PIC X(06).                          
007800         10 PV-BTCH-SRC-CDE           PIC X(03).                          
007900                                                                          
008000 01  PV-VARIABLES.                                                        
007100     05  PV-SUB                       PIC 9 VALUE ZERO.                   
008100     05  PV-MEMO-LINE-CHRG.                                               
008200         10  FILLER                   PIC X(33) VALUE                     
008300         'COMPLIANCE MGMT CHARGE : DEBIT # '.                             
008400         10  PV-AP-DOC-NBR-CH         PIC X(16).                          
008500         10  FILLER                   PIC X(01) VALUE '.'.                
008600     05  PV-MEMO-LINE-CREDIT.                                             
008700         10  FILLER                   PIC X(33) VALUE                     
008800         'COMPLIANCE MGMT CREDIT : DEBIT # '.                             
008900         10  PV-AP-DOC-NBR-CR         PIC X(16).                          
009000         10  FILLER                   PIC X(01) VALUE '.'.                
008600     05  PV-COMPLETE-LINE.                                                
008700         10  FILLER                   PIC X(20) VALUE                     
008800         'PROCESSING COMPLETE '.                                          
008700         10  PV-PROCESS-DATE          PIC X(10) VALUE SPACES.             
009100     05  PV-TOTAL-CHARGES             PIC S9(11)V99 VALUE +0.             
009300     05  PV-TOTAL-CREDITS             PIC S9(11)V99 VALUE +0.             
009500     05  PV-NET-AMT                   PIC S9(11)V99 VALUE +0.             
009700     05  PV-TOTAL-WRITTEN             PIC S9(11)    VALUE +0.             
009900                                                                          
010200     05  PV-READ-COUNT                PIC S9(07)    VALUE +0.             
010400     05  PV-WRITE-COUNT               PIC S9(07)    VALUE +0.             
012900     05  PV-TEST-TOTALS.                                                  
013000         10  PV-TEST-REC-CNT          PIC S9(11)    VALUE +0.             
013000         10  PV-TEST-GRO-COST         PIC S9(11)V99 VALUE +0.             
013100         10  PV-TEST-NET-COST         PIC S9(11)V99 VALUE +0.             
012900     05  PV-CMS-CTL-TOTALS.                                               
013000         10  PV-CMS-CTL-REC-CNT       PIC S9(11)    VALUE +0.             
013000         10  PV-CMS-CTL-GRO-COST      PIC S9(11)V99 VALUE +0.             
013100         10  PV-CMS-CTL-NET-COST      PIC S9(11)V99 VALUE +0.             
012800                                                                          
010800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
010900     05  PV-DB2-TEXT                  PIC  X(60) VALUE SPACES.            
011000     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
011100     05  PV-DISP-NUMBER               PIC  ZZ,ZZZ,ZZZ,ZZ9.                
011100     05  PV-DISP-DOLLAR               PIC  ZZ,ZZZ,ZZZ,ZZ9.99-.            
011200     05  PV-RUN-TIME                  PIC  X(08) VALUE SPACES.            
011300     05  PV-RUN-DATE                  PIC  X(10) VALUE SPACES.            
011400                                                                          
012200     05  PV-HOLD-SEQ                  PIC  9(04).                         
012300     05  PV-HOLD-SEQ-PARTS REDEFINES PV-HOLD-SEQ.                         
012400         10  PV-HOLD-SEQ-PART1        PIC  9(01).                         
012500         10  PV-HOLD-SEQ-PART2        PIC  9(01).                         
012600         10  PV-HOLD-SEQ-PART3        PIC  9(01).                         
012700         10  PV-HOLD-SEQ-PART4        PIC  9(01).                         
012800                                                                          
012900     05  PV-FIELD-SIGNS               PIC  X(01) VALUE ' '.               
013000         88  PV-VALID-SIGNS                VALUE ' ', '-'.                
013000         88  PV-POSITIVE                   VALUE ' '.                     
013100         88  PV-NEGATIVE                   VALUE '-'.                     
012800                                                                          
012900     05  PV-TEMP-SEQ.                                                     
013000         10  PV-TEMP-SEQ-PART1        PIC  X(01).                         
013100         10  PV-TEMP-SEQ-PART2        PIC  X(01).                         
013200         10  PV-TEMP-SEQ-PART3        PIC  X(01).                         
013300         10  PV-TEMP-SEQ-PART4        PIC  X(01).                         
013400     05  PV-DOC-NBR-SEQ               PIC  X(16) VALUE SPACES.            
013500                                                                          
014200*** COPYBOOK FOR FILE FROM CMS                                            
014300     COPY VLRD039.                                                        
014400                                                                          
014200*** COPYBOOK FOR AP ADJUSTMENT FILE                                       
014500     COPY APRD045.                                                        
014600                                                                          
014200*** COPYBOOK FOR CALENDAR ROUTINE                                         
014500     COPY DPWS500.                                                        
014600                                                                          
014700     COPY DPWS004.                                                        
014800                                                                          
014900     EXEC SQL                                                             
015000         INCLUDE SQLCA                                                    
015100     END-EXEC.                                                            
015200                                                                          
015300     COPY SQLCA2.                                                         
015400                                                                          
015500     EXEC SQL                                                             
015600         INCLUDE TAPCNTL                                                  
015700     END-EXEC.                                                            
015800                                                                          
015900     EXEC SQL                                                             
016000         INCLUDE TTRNCDE                                                  
016100     END-EXEC.                                                            
016200                                                                          
016700 LINKAGE SECTION.                                                         
016800                                                                          
016900 PROCEDURE DIVISION.                                                      
017000                                                                          
017100 0000-MAINLINE.                                                           
017500     PERFORM 1000-INITIALIZE                                              
017600                                                                          
017700     PERFORM 2000-PROCESS-INPUT                                           
017800         UNTIL PS-END-INPUT.                                              
017900                                                                          
018100     PERFORM 9000-EOJ.                                                    
018200                                                                          
018300     GOBACK.                                                              
018400                                                                          
018600 1000-INITIALIZE.                                                         
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000127     DISPLAY '************************************************'           
000128     DISPLAY '*******  CONTROL CARD ====>  ' CC-CONTROL-CARD.             
000129                                                                          
000130     PERFORM 1050-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED WITHIN CURRENT-DATE IN POSITION 9 FOR 8         
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME.                      
000135                                                                          
000136     DISPLAY '*******  RUN TIME ====> '  PV-RUN-TIME.                     
019900                                                                          
019900***** BEFORE CREATING AN AP FILE, DETAIL RECORDS WILL BE TOTALED          
019900***** IF RECORD COUNTS, GROSS & NET COST AMOUNTS MATCH THE CONTROL        
019900******* RECORD, PROCESSING CAN CONTINUE, ELSE ABEND.                      
020000     OPEN INPUT CMS-EXT-FILE.                                             
024300                                                                          
026900     INITIALIZE VL039-AP-INTERFACE-RECORD.                                
026900     INITIALIZE AP045-KEY-FIELDS                                          
026900                AP045-COMMON-FIELDS                                       
026900                AP045-DETAIL-FIELDS                                       
024300     PERFORM 8000-READ-INPUT.                                             
020700     IF PS-NOT-END-INPUT                                                  
020700         IF VL039-CONTROL-RECORD AND                                      
020710             VL039-CTL-RECORD-COUNT-UNP = 0                               
020710******* THIS MEANS THERE WERE NO CHARGES OR CREDITS -- CN SENT            
020710******* ONLY CONTROL RECORD WITH A ZERO RECORD COUNT                      
020710******* CMS WILL ALWAYS SEND AT LEAST A CONTROL RECORD, THEY WILL         
020710******* NEVER SEND TOTALLY EMPTY FILE.  IF THAT HAPPENS PROBLEM           
020710******* ON THEIR SIDE.                                                    
020720             SET PS-END-INPUT TO TRUE                                     
020720             SET PS-CNTL-ONLY TO TRUE                                     
052700             CLOSE CMS-EXT-FILE                                           
040300             DISPLAY '*****************************************'          
040300             DISPLAY '*************   EMPTY   FILE  ***********'          
040100             DISPLAY '** PROGRAM = VLKUT038                    '          
040200             DISPLAY '** PARAGRAPH = 1000-INITIALIZE           '          
040300             DISPLAY '** NO CHARGES OR CREDITS -- OUT01   EMPTY'          
020800             DISPLAY '*****************************************'          
020720         ELSE                                                             
020720             PERFORM 1100-CHECK-INPUT                                     
017800                 UNTIL PS-END-INPUT                                       
019900             CLOSE CMS-EXT-FILE                                           
019900             IF PS-AMOUNTS-MATCH                                          
019900                 SET PS-NOT-END-INPUT TO TRUE                             
019900                 INITIALIZE PV-TOTAL-CHARGES                              
019900                            PV-TOTAL-CREDITS                              
019900                            PV-NET-AMT                                    
019900                            PV-TOTAL-WRITTEN                              
019900                            PV-CMS-CTL-REC-CNT                            
019900                            PV-CMS-CTL-GRO-COST                           
019900                            PV-CMS-CTL-NET-COST                           
019900                            PV-READ-COUNT                                 
020000                 OPEN INPUT CMS-EXT-FILE                                  
020200                 OPEN OUTPUT AP-TRANSACTION-FILE                          
020200                             AP-COMPLETE-FILE                             
020400                                                                          
026900                 INITIALIZE VL039-AP-INTERFACE-RECORD                     
020500                 PERFORM 8000-READ-INPUT                                  
021100                 PERFORM 7000-SELECT-TAPCNTL                              
021200                                                                          
021300                 INITIALIZE DCLTTRNCDE                                    
021400                 MOVE '6A'            TO TRNCDE-AP-TRN-CDE                
021500                 MOVE 1               TO PV-SUB                           
021600                 PERFORM 7050-SELECT-TTRNCDE                              
021700                                                                          
021800                 INITIALIZE DCLTTRNCDE                                    
021900                 MOVE '6B'            TO TRNCDE-AP-TRN-CDE                
022000                 MOVE 2               TO PV-SUB                           
022100                 PERFORM 7050-SELECT-TTRNCDE                              
052900             END-IF                                                       
052900         END-IF                                                           
020900     ELSE                                                                 
020710******* THIS MEANS A VERY BAD ERROR ON CN SIDE --                         
020710******* ONLY THEY CAN DETERMINE WHY A COMPLETELY EMPTY FILE CAME          
040300         DISPLAY '***********************************************'        
040300         DISPLAY '*************   EMPTY   FILE   ****************'        
040000         DISPLAY '** ABEND                                       '        
040100         DISPLAY '** PROGRAM = VLKUT038                          '        
040200         DISPLAY '** PARAGRAPH = 1000-INITIALIZE                 '        
040300         DISPLAY '** THIS IS VERY BAD ERROR ON THE SIDE OF CN    '        
040300         DISPLAY '** NO CHARGES OR CREDITS -- OUT01 WILL BE EMPTY'        
020800         DISPLAY '***********************************************'        
052700         CLOSE CMS-EXT-FILE                                               
010080         MOVE +4008 TO ABEND-CODE                                         
010090         CALL 'ILBOABN0' USING ABEND-CODE                                 
024300     END-IF.                                                              
022400                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1050-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-PROCESS-DATE                     
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT038 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1050-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO ABEND-CODE                                         
000167         CALL 'ILBOABN0' USING ABEND-CODE                                 
000168     END-IF.                                                              
000169                                                                          
022500******************************************************************        
024000*    BEFORE DOING ANY PROCESSING SPIN THROUGH THE INPUT FILE              
024000*      ADDING THE AMOUNT FIELDS. IF THE TOTALS MATCH WHAT IS ON           
024000*      THE CONTROL RECORD FROM CMS, PROCESSING CAN CONTINUE TO            
024000*      CREATE THE AP FILE.  WE DON'T WANT TO PASS A FILE TO AP IF         
024000*      THE AMOUNTS DON'T MATCH.  THE DISCREPANCY MUST BE CORRECTED        
024000*      BEFORE A FILE CAN BE SENT.                                         
024100******************************************************************        
024200 1100-CHECK-INPUT.                                                        
024300     EVALUATE TRUE                                                        
024300         WHEN VL039-DETAIL-RECORD                                         
024300             PERFORM 1200-ADD-AMOUNTS                                     
024300         WHEN VL039-CONTROL-RECORD                                        
018000             PERFORM 1300-CHECK-TOTALS                                    
024300     END-EVALUATE.                                                        
026100                                                                          
026900     INITIALIZE VL039-AP-INTERFACE-RECORD.                                
024300                                                                          
026200     PERFORM 8000-READ-INPUT.                                             
026300                                                                          
022500******************************************************************        
024000*    ADD GROSS COST AND NET COST TO TOTALS                                
024100******************************************************************        
024200 1200-ADD-AMOUNTS.                                                        
029100     MOVE VL039-GRO-COST-AMT-UNP-SIGN TO PV-FIELD-SIGNS                   
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-TEST-GRO-COST                                     
029100                              = VL039-GRO-COST-AMT-UNP * +1               
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-TEST-GRO-COST                                     
029100                              = VL039-GRO-COST-AMT-UNP * -1               
029100     END-EVALUATE.                                                        
029100                                                                          
029100     MOVE VL039-NET-COST-AMT-UNP-SIGN TO PV-FIELD-SIGNS                   
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-TEST-NET-COST                                     
029100                              = VL039-NET-COST-AMT-UNP * +1               
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-TEST-NET-COST                                     
029100                              = VL039-NET-COST-AMT-UNP * -1               
029100     END-EVALUATE.                                                        
029100                                                                          
029100***** GROSS COST AND NET COST MUST BE THE SAME                            
029100     IF PV-TEST-GRO-COST = PV-TEST-NET-COST                               
029100         SET PS-AMOUNTS-MATCH TO TRUE                                     
029100***  GROSS COST & NET COST ARE THE SAME BECAUSE THE CALCULATION           
029100***  FOR NET COST INCLUDES OTHER FIELDS THAT WE AREN'T USING              
029100***  SINCE GROSS AND NET ARE THE SAME WE ONLY NEED TO ADD EITHER          
029100***  GROSS COST OR NET COST TO THE TOTAL CHARGE OR CREDIT FIELDS.         
029100         EVALUATE TRUE                                                    
029100             WHEN VL039-CHARGE-6A                                         
029100                 ADD PV-TEST-GRO-COST TO PV-TOTAL-CHARGES                 
029100             WHEN VL039-CREDIT-6B                                         
029100                 ADD PV-TEST-GRO-COST TO PV-TOTAL-CREDITS                 
029100         END-EVALUATE                                                     
029100     ELSE                                                                 
029100         SET PS-AMOUNTS-NOT-MATCH TO TRUE                                 
040300         DISPLAY '**********************************************'         
040000         DISPLAY '** ABEND                                      '         
040100         DISPLAY '** PROGRAM = VLKUT038                         '         
040200         DISPLAY '** PARAGRAPH = 1200-ADD-AMOUNTS               '         
040300         DISPLAY '** DISCREPANCY IN GROSS COST AND NET COST     '         
040000         DISPLAY '**   --  AMOUNTS SHOULD BE IDENTICAL          '         
040100         DISPLAY '** VENDOR NUMBER: ' VL039-VND-NBR                       
040100         MOVE PV-TEST-GRO-COST           TO PV-DISP-DOLLAR                
040100         DISPLAY '** TEST GRO-COST: ' PV-DISP-DOLLAR                      
040100         MOVE PV-TEST-NET-COST           TO PV-DISP-DOLLAR                
040100         DISPLAY '** TEST NET-COST: ' PV-DISP-DOLLAR                      
040300         DISPLAY '**********************************************'         
040200         PERFORM 9100-ABEND-BAD-AMTS                                      
029100     END-IF.                                                              
029200                                                                          
022500******************************************************************        
024000*  CHECK THE CONTROL RECORD FROM CMS TO WHAT WAS CALCULATED HERE          
024100******************************************************************        
024200 1300-CHECK-TOTALS.                                                       
029100***  WE NEED TO HAVE THE GRO-COST AND NET COST AMOUNTS SIGNED             
029100***  CMS CANNOT PROVIDE SIGNED PACKED DATA, SO WE HAVE TO DO IT           
029100     MOVE VL039-CTL-GRO-COST-AMT-UNPSIGN TO PV-FIELD-SIGNS.               
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-CMS-CTL-GRO-COST                                  
029100                              = VL039-CTL-GRO-COST-AMT-UNP * +1           
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-CMS-CTL-GRO-COST                                  
029100                              = VL039-CTL-GRO-COST-AMT-UNP * -1           
029100     END-EVALUATE.                                                        
029100                                                                          
029100     MOVE VL039-CTL-NET-COST-AMT-UNPSIGN TO PV-FIELD-SIGNS.               
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-CMS-CTL-NET-COST                                  
029100                              = VL039-CTL-NET-COST-AMT-UNP * +1           
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-CMS-CTL-NET-COST                                  
029100                              = VL039-CTL-NET-COST-AMT-UNP * -1           
029100     END-EVALUATE.                                                        
029100                                                                          
029100     COMPUTE PV-CMS-CTL-REC-CNT = VL039-CTL-RECORD-COUNT-UNP * +1.        
029100***  CONTROL RECORD IS SUBTRACTED FROM THE RECORDS READ LEAVING           
029100***      ONLY THE DETAIL RECORDS                                          
029100     COMPUTE PV-TEST-REC-CNT = PV-TEST-REC-CNT - +1.                      
039800     COMPUTE PV-NET-AMT = PV-TOTAL-CHARGES + PV-TOTAL-CREDITS.            
040000         MOVE PV-TOTAL-CHARGES  TO PV-DISP-DOLLAR                         
040000         DISPLAY '** TOTAL CHARGES ==> ' PV-DISP-DOLLAR                   
040000         MOVE PV-TOTAL-CREDITS  TO PV-DISP-DOLLAR                         
040000         DISPLAY '** TOTAL CREDITS ==> ' PV-DISP-DOLLAR                   
040000         MOVE PV-NET-AMT        TO PV-DISP-DOLLAR                         
040000         DISPLAY '** NET AMOUNT    ==> ' PV-DISP-DOLLAR                   
029100                                                                          
029100***  GRO-COST AND NET COST WILL BE THE SAME                               
040300     IF PV-TEST-REC-CNT   = PV-CMS-CTL-REC-CNT AND                        
040300        (PV-NET-AMT       = PV-CMS-CTL-GRO-COST AND                       
040300         PV-NET-AMT       = PV-CMS-CTL-NET-COST)                          
039900         SET PS-AMOUNTS-MATCH TO TRUE                                     
040200     ELSE                                                                 
039900         SET PS-AMOUNTS-NOT-MATCH TO TRUE                                 
040300         DISPLAY '**********************************************'         
040000         DISPLAY '** ABEND                                      '         
040100         DISPLAY '** PROGRAM = VLKUT038                         '         
040200         DISPLAY '** PARAGRAPH = 1300-CHECK-TOTALS              '         
040300         DISPLAY '** DISCREPANCY IN CONTROL AMT FROM CMS AND    '         
040000         DISPLAY '**   CALULATED AMOUNT IN THIS PGM             '         
040100         MOVE PV-CMS-CTL-REC-CNT         TO PV-DISP-NUMBER                
040100         DISPLAY '** CMS REC CNT:  ' PV-DISP-NUMBER                       
040100         MOVE PV-TEST-REC-CNT            TO PV-DISP-NUMBER                
040100         DISPLAY '** KOHLS REC CNT:  ' PV-DISP-NUMBER                     
040100         MOVE PV-CMS-CTL-GRO-COST        TO PV-DISP-DOLLAR                
040100         DISPLAY '** CMS GRO-COST: ' PV-DISP-DOLLAR                       
040100         MOVE PV-NET-AMT                 TO PV-DISP-DOLLAR                
040100         DISPLAY '** KOHLS GRO-COST: ' PV-DISP-DOLLAR                     
040100         MOVE PV-CMS-CTL-NET-COST        TO PV-DISP-DOLLAR                
040100         DISPLAY '** CMS NET-COST: ' PV-DISP-DOLLAR                       
040100         MOVE PV-NET-AMT                 TO PV-DISP-DOLLAR                
040100         DISPLAY '** KOHLS NET-COST: ' PV-DISP-DOLLAR                     
040300         DISPLAY '**********************************************'         
040200         PERFORM 9100-ABEND-BAD-AMTS                                      
040300     END-IF.                                                              
040300                                                                          
022500******************************************************************        
024000*    PROCESS EACH RECORD FROM CMS ADDING INFORMATION NOT AVAILABLE        
024000*      TO CMS IN ORDER TO CREATE COMPLETE AP RECORD                       
024100******************************************************************        
024200 2000-PROCESS-INPUT.                                                      
026900     INITIALIZE AP045-KEY-FIELDS                                          
027000                AP045-COMMON-FIELDS                                       
027100                AP045-DETAIL-FIELDS.                                      
024300                                                                          
024300     EVALUATE TRUE                                                        
024300         WHEN VL039-DETAIL-RECORD                                         
024300             PERFORM 2100-CMS-FIELDS                                      
024400             PERFORM 2200-ADD-AP-FIELDS                                   
024300         WHEN VL039-CONTROL-RECORD                                        
018000             PERFORM 3000-CONTROL-RECORD                                  
024300     END-EVALUATE.                                                        
026100                                                                          
026200     PERFORM 8000-READ-INPUT.                                             
026300                                                                          
026400******************************************************************        
026500* THIS PARAGRAPH WILL MOVE THE FIELDS FROM CMS TO THE AP COPYBOOK         
026600*     IF ANY CALCULATIONS ARE NEEDED, THEY WILL BE DONE HERE              
026700******************************************************************        
026800 2100-CMS-FIELDS.                                                         
027300     MOVE VL039-VND-NBR               TO AP045-VND-NBR.                   
027500     EVALUATE TRUE                                                        
027500         WHEN VL039-CHARGE-6A                                             
027500             MOVE VL039-DOC-NBR       TO AP045-DOC-NBR                    
027500         WHEN VL039-CREDIT-6B                                             
027500             PERFORM 2300-STRING-DOC-NBR-SEQ                              
027500             MOVE PV-DOC-NBR-SEQ     TO AP045-DOC-NBR                     
027600     END-EVALUATE.                                                        
027700     MOVE VL039-AP-TRN-CDE            TO AP045-AP-TRN-CDE.                
027700     MOVE VL039-DOC-DTE               TO AP045-DOC-DTE.                   
028300     MOVE VL039-RECD-TYP-CDE          TO AP045-RECD-TYP-CDE.              
028600     MOVE 'VLKUT038'                  TO AP045-ENTR-ID.                   
028900     MOVE VL039-ORGN-CDE              TO AP045-ORGN-CDE.                  
029000     MOVE VL039-ENT-ID                TO AP045-ENT-ID.                    
029000     MOVE VL039-ENT-NME-DESC          TO AP045-ENT-NME-DESC               
029100**** STR-NBR SHOULD BE 85                                                 
029100     MOVE VL039-STR-NBR               TO AP045-STR-NBR.                   
029100                                                                          
029100***  WE NEED TO HAVE THE GRO-COST AND NET COST AMOUNTS SIGNED             
029100***  CMS CANNOT PROVIDE SIGNED PACKED DATA, SO WE HAVE TO DO IT           
029100     MOVE VL039-GRO-COST-AMT-UNP-SIGN TO PV-FIELD-SIGNS                   
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE VL039-GRO-COST-AMT                                   
029100                              = VL039-GRO-COST-AMT-UNP * +1               
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE VL039-GRO-COST-AMT                                   
029100                              = VL039-GRO-COST-AMT-UNP * -1               
029100     END-EVALUATE.                                                        
029100     MOVE VL039-GRO-COST-AMT          TO AP045-GRO-COST-AMT.              
029100                                                                          
029100     MOVE VL039-NET-COST-AMT-UNP-SIGN TO PV-FIELD-SIGNS                   
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE VL039-NET-COST-AMT                                   
029100                              = VL039-NET-COST-AMT-UNP * +1               
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE VL039-NET-COST-AMT                                   
029100                              = VL039-NET-COST-AMT-UNP * -1               
029100     END-EVALUATE.                                                        
029100     MOVE VL039-NET-COST-AMT          TO AP045-NET-COST-AMT.              
029100                                                                          
029100***  GROSS COST & NET COST ARE THE SAME BECAUSE THE CALCULATION           
029100***  FOR NET COST INCLUDES OTHER FIELDS THAT WE AREN'T USING              
029100***  SINCE GROSS AND NET ARE THE SAME WE ONLY NEED TO ADD ONE OF          
029100***  THE FIELDS TO THE TOTAL CHARGE OR CREDIT FIELDS                      
029100     EVALUATE TRUE                                                        
029100         WHEN VL039-CHARGE-6A                                             
029100             ADD VL039-GRO-COST-AMT   TO PV-TOTAL-CHARGES                 
029100         WHEN VL039-CREDIT-6B                                             
029100             ADD VL039-GRO-COST-AMT   TO PV-TOTAL-CREDITS                 
029100     END-EVALUATE.                                                        
029200                                                                          
029300******************************************************************        
029400* ADD THE FIELDS FROM AP TABLES TO THE OUTPUT RECORD                      
029500******************************************************************        
029600 2200-ADD-AP-FIELDS.                                                      
031800     MOVE APCNTL-NEXT-BTCH-PRC-DTE    TO AP045-ENTR-DTE                   
031800                                         AP045-INV-CUTOFF-DTE.            
029100     EVALUATE TRUE                                                        
029100         WHEN VL039-CHARGE-6A                                             
031500             MOVE PV-PSTG-TYP-CDE (1) TO AP045-PSTG-TYP-CDE               
031600             MOVE PV-TRN-TYP-CDE (1)  TO AP045-TRN-TYP-CDE                
031700             MOVE PV-GL-ACCT-NBR (1)  TO AP045-GL-ACCT-NBR                
031800             STRING AP045-ENTR-DTE                                        
031900                    PV-BTCH-SRC-CDE (1)                                   
032000                    '00' DELIMITED BY SIZE                                
032200                INTO AP045-PS-BTCH-NBR                                    
030500                                                                          
030500             MOVE VL039-DOC-NBR       TO PV-AP-DOC-NBR-CH                 
030600             MOVE PV-MEMO-LINE-CHRG   TO AP045-RSN-1ST-LNE-TXT            
029100         WHEN VL039-CREDIT-6B                                             
034800             MOVE PV-PSTG-TYP-CDE (2) TO AP045-PSTG-TYP-CDE               
034900             MOVE PV-TRN-TYP-CDE (2)  TO AP045-TRN-TYP-CDE                
035000             MOVE PV-GL-ACCT-NBR (2)  TO AP045-GL-ACCT-NBR                
031800             STRING AP045-ENTR-DTE                                        
031900                    PV-BTCH-SRC-CDE (2)                                   
032000                    '00' DELIMITED BY SIZE                                
032200               INTO AP045-PS-BTCH-NBR                                     
033700*** FOR THE LINE OF TEXT WE ONLY WANT THE AP DOC NBR WITHOUT THE          
033800*** REVERSAL SEQUENCE NUMBER                                              
030500             MOVE VL039-DOC-NBR       TO PV-AP-DOC-NBR-CR                 
034000             MOVE PV-MEMO-LINE-CREDIT TO AP045-RSN-1ST-LNE-TXT            
029100     END-EVALUATE.                                                        
031800                                                                          
028500     MOVE '-22.00.00.000000'          TO AP045-ENTR-TME.                  
030400                                                                          
032300     PERFORM 8100-WRITE-AP-TRANS-REC                                      
032400     ADD 1                            TO PV-TOTAL-WRITTEN.                
035900                                                                          
029300******************************************************************        
029400* FOR A CREDIT (REVERSAL) THE SEQUENCE NBR NEEDS TO BE ATTACHED           
029400*    TO THE DOC-NBR THAT IS SENT TO AP                                    
029500******************************************************************        
036000 2300-STRING-DOC-NBR-SEQ.                                                 
036100     MOVE VL039-REVERSAL-SEQ        TO PV-HOLD-SEQ.                       
036200     IF PV-HOLD-SEQ-PART1 > 0                                             
036300         MOVE PV-HOLD-SEQ TO PV-TEMP-SEQ                                  
036400     ELSE                                                                 
036400         IF PV-HOLD-SEQ-PART2 > 0                                         
036500             MOVE PV-HOLD-SEQ-PART2 TO PV-TEMP-SEQ-PART1                  
036600             MOVE PV-HOLD-SEQ-PART3 TO PV-TEMP-SEQ-PART2                  
036700             MOVE PV-HOLD-SEQ-PART4 TO PV-TEMP-SEQ-PART3                  
036800             MOVE SPACES            TO PV-TEMP-SEQ-PART4                  
036900         ELSE                                                             
036900             IF PV-HOLD-SEQ-PART3 > 0                                     
037000                 MOVE PV-HOLD-SEQ-PART3 TO PV-TEMP-SEQ-PART1              
037100                 MOVE PV-HOLD-SEQ-PART4 TO PV-TEMP-SEQ-PART2              
037200                 MOVE SPACES            TO PV-TEMP-SEQ-PART3              
037300                                           PV-TEMP-SEQ-PART4              
037400             ELSE                                                         
037400                 IF PV-HOLD-SEQ-PART4 > 0                                 
037500                     MOVE PV-HOLD-SEQ-PART4 TO PV-TEMP-SEQ-PART1          
037600                     MOVE SPACES            TO PV-TEMP-SEQ-PART2          
037700                                               PV-TEMP-SEQ-PART3          
037800                                               PV-TEMP-SEQ-PART4          
037900                 END-IF                                                   
038000             END-IF                                                       
038100         END-IF                                                           
038200     END-IF.                                                              
038300                                                                          
038400     STRING VL039-DOC-NBR DELIMITED BY ' '                                
038500            PV-TEMP-SEQ DELIMITED BY ' '                                  
038600            ' ' DELIMITED BY SIZE                                         
038700       INTO PV-DOC-NBR-SEQ.                                               
038800                                                                          
029300******************************************************************        
029400* CREATE THE CONTROL RECORD, THE RECORD COUNTS AND GROSS-COST,            
029400*    NET COST FROM CMS WILL BE COMPARED TO THE TOTALS CREATED             
029400*    AS THE DETAIL RECORDS ARE PROCESSED IN THIS PROGRAM.                 
029400******                                                                    
029400*    IF THE TWO SETS OF TOTALS DON'T MATCH, THEN THE PROGRAM              
029400*    NEEDS TO ABEND, A DETERMINATION MUST BE MADE AS TO WHERE THE         
029400*    PROBLEM IS.                                                          
029300******************************************************************        
038900 3000-CONTROL-RECORD.                                                     
039200     INITIALIZE AP045-KEY-FIELDS                                          
039300                AP045-DETAIL-FIELDS                                       
039400                AP045-COMMON-FIELDS                                       
039500                AP045-CONTROL-FIELDS.                                     
039600                                                                          
039700     MOVE '9'              TO AP045-RECD-TYP-CDE.                         
039600                                                                          
029100***  WE NEED TO HAVE THE GRO-COST AND NET COST AMOUNTS SIGNED             
029100***  CMS CANNOT PROVIDE SIGNED PACKED DATA, SO WE HAVE TO DO IT           
029100     MOVE VL039-CTL-GRO-COST-AMT-UNPSIGN TO PV-FIELD-SIGNS                
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-CMS-CTL-GRO-COST                                  
029100                              = VL039-CTL-GRO-COST-AMT-UNP * +1           
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-CMS-CTL-GRO-COST                                  
029100                              = VL039-CTL-GRO-COST-AMT-UNP * -1           
029100     END-EVALUATE.                                                        
029100                                                                          
029100     MOVE VL039-CTL-NET-COST-AMT-UNPSIGN TO PV-FIELD-SIGNS                
029100     EVALUATE TRUE                                                        
029100         WHEN PV-POSITIVE                                                 
029100             COMPUTE PV-CMS-CTL-NET-COST                                  
029100                              = VL039-CTL-NET-COST-AMT-UNP * +1           
029100         WHEN PV-NEGATIVE                                                 
029100             COMPUTE PV-CMS-CTL-NET-COST                                  
029100                              = VL039-CTL-NET-COST-AMT-UNP * -1           
029100     END-EVALUATE.                                                        
029100                                                                          
029100     COMPUTE PV-CMS-CTL-REC-CNT = VL039-CTL-RECORD-COUNT-UNP * +1.        
029100                                                                          
039800     COMPUTE PV-NET-AMT = PV-TOTAL-CHARGES + PV-TOTAL-CREDITS.            
039900     MOVE PV-NET-AMT   TO AP045-CTL-GRO-COST-AMT                          
040000                          AP045-CTL-NET-COST-AMT.                         
040100     MOVE PV-TOTAL-WRITTEN TO AP045-CTL-RECORD-COUNT.                     
040300     PERFORM 8100-WRITE-AP-TRANS-REC.                                     
040300                                                                          
029300******************************************************************        
029400* SELECT THE NEXT BATCH PROCESSING DATE FROM THE TAPCNTL TABLE.           
029400*    AP WILL GET THE FILE FOR PROCESSING THE NEXT DAY, NOT ON THE         
029400*    SAME DAY THE FILE IS CREATED                                         
029400******************************************************************        
040500 7000-SELECT-TAPCNTL.                                                     
040700     INITIALIZE APCNTL-NEXT-BTCH-PRC-DTE                                  
040800     EXEC SQL                                                             
040900         SELECT NEXT_BTCH_PRC_DTE                                         
041000         INTO :APCNTL-NEXT-BTCH-PRC-DTE                                   
041100         FROM TAPCNTL                                                     
041200     END-EXEC.                                                            
041300                                                                          
041400     EVALUATE TRUE                                                        
041500         WHEN SQLCODE = ZERO                                              
041600             CONTINUE                                                     
041700         WHEN SQLCODE < ZERO                                              
042200         WHEN SQLCODE > ZERO                                              
042300         WHEN SQLWARN0 NOT = SPACES                                       
042400             MOVE '7000-SELECT-TAPCNTL' TO PV-PARAGRAPH                   
042600             MOVE 'SELECT'              TO PV-DB2-FUNCTION                
042700             PERFORM 9200-SQL-ERROR                                       
042800     END-EVALUATE.                                                        
042900                                                                          
029300******************************************************************        
029400* SELECT THE POSTING TYPE CODE, TRANSACTION TYPE CODE, THE GL ACCT        
029400*    NUMBER AND THE BATCH SOURCE CODE FROM TTRNCDE TABLE                  
029400******************************************************************        
043000 7050-SELECT-TTRNCDE.                                                     
043200     EXEC SQL                                                             
043400         SELECT PSTG_TYP_CDE                                              
043500              , TRN_TYP_CDE                                               
043600              , GL_ACCT_NBR                                               
043700              , BTCH_SRC_CDE                                              
043900           INTO :TRNCDE-PSTG-TYP-CDE                                      
044000              , :TRNCDE-TRN-TYP-CDE                                       
044100              , :TRNCDE-GL-ACCT-NBR                                       
044200              , :TRNCDE-BTCH-SRC-CDE                                      
044300           FROM TTRNCDE                                                   
044500          WHERE AP_TRN_CDE    = :TRNCDE-AP-TRN-CDE                        
044700            AND EFF_STRT_DTE <= :APCNTL-NEXT-BTCH-PRC-DTE                 
044900            AND EFF_END_DTE  >= :APCNTL-NEXT-BTCH-PRC-DTE                 
045000     END-EXEC.                                                            
045100                                                                          
045200     EVALUATE TRUE                                                        
045300         WHEN SQLCODE = ZERO                                              
045400             MOVE TRNCDE-PSTG-TYP-CDE TO PV-PSTG-TYP-CDE (PV-SUB)         
045500             MOVE TRNCDE-TRN-TYP-CDE  TO PV-TRN-TYP-CDE (PV-SUB)          
045600             MOVE TRNCDE-GL-ACCT-NBR  TO PV-GL-ACCT-NBR (PV-SUB)          
045700             MOVE TRNCDE-BTCH-SRC-CDE TO PV-BTCH-SRC-CDE (PV-SUB)         
045800                                                                          
045900         WHEN SQLCODE < ZERO                                              
046400         WHEN SQLCODE > ZERO                                              
046500         WHEN SQLWARN0 NOT = SPACES                                       
046600             MOVE '7050-SELECT-TTRNCDE' TO PV-PARAGRAPH                   
046800             MOVE 'SELECT'              TO PV-DB2-FUNCTION                
046900             PERFORM 9200-SQL-ERROR                                       
047000     END-EVALUATE.                                                        
049800                                                                          
029300******************************************************************        
029400* READ THE EXTRACT FILE                                                   
029400******************************************************************        
049900 8000-READ-INPUT.                                                         
050000     READ CMS-EXT-FILE                                                    
050100         INTO VL039-AP-INTERFACE-RECORD                                   
050200     AT END                                                               
050300         SET PS-END-INPUT TO TRUE                                         
050400     NOT AT END                                                           
050500         ADD 1              TO PV-READ-COUNT                              
050500         ADD 1              TO PV-TEST-REC-CNT                            
050600     END-READ.                                                            
050700                                                                          
029300******************************************************************        
029400* WRITE OUT THE AP TRANSACTION FILE                                       
029400******************************************************************        
050800 8100-WRITE-AP-TRANS-REC.                                                 
050900     WRITE AP-TRANSACTION-RECORD                                          
051000         FROM AP045-TRANSACTION-FILE.                                     
051100     ADD 1                  TO PV-WRITE-COUNT.                            
051300                                                                          
029300******************************************************************        
029400* END OF JOB PROCESS                                                      
029400******************************************************************        
051400 9000-EOJ.                                                                
050900     IF PS-DTLS-AND-CNTL                                                  
050900         WRITE AP-COMPLETE-RECORD                                         
051000             FROM PV-COMPLETE-LINE                                        
051600         CLOSE CMS-EXT-FILE                                               
051700               AP-TRANSACTION-FILE                                        
051700               AP-COMPLETE-FILE                                           
051700     ELSE                                                                 
051700         OPEN OUTPUT AP-COMPLETE-FILE                                     
050900         WRITE AP-COMPLETE-RECORD                                         
051000             FROM PV-COMPLETE-LINE                                        
051700         CLOSE AP-COMPLETE-FILE                                           
051700     END-IF.                                                              
051800                                                                          
051900     DISPLAY '************   END OF JOB   ********************'           
052000     MOVE PV-READ-COUNT     TO PV-DISP-NUMBER.                            
052100     DISPLAY 'RECS READ    = ' PV-DISP-NUMBER.                            
052200     MOVE PV-WRITE-COUNT    TO PV-DISP-NUMBER.                            
052300     DISPLAY 'RECS WRITTEN = ' PV-DISP-NUMBER.                            
052400                                                                          
040100     MOVE PV-CMS-CTL-REC-CNT             TO PV-DISP-NUMBER.               
040100     DISPLAY '** CMS DTL REC CNT:  '        PV-DISP-NUMBER.               
040100     MOVE PV-TOTAL-WRITTEN               TO PV-DISP-NUMBER.               
040100     DISPLAY '** KOHLS DTL REC CNT:  '      PV-DISP-NUMBER.               
040100     MOVE PV-CMS-CTL-GRO-COST            TO PV-DISP-DOLLAR.               
040100     DISPLAY '** CMS GRO-COST: '            PV-DISP-DOLLAR.               
040100     MOVE PV-NET-AMT                     TO PV-DISP-DOLLAR.               
040100     DISPLAY '** KOHLS GRO-COST: '          PV-DISP-DOLLAR.               
040100     MOVE PV-CMS-CTL-NET-COST            TO PV-DISP-DOLLAR.               
040100     DISPLAY '** CMS NET-COST: '            PV-DISP-DOLLAR.               
040100     MOVE PV-NET-AMT                     TO PV-DISP-DOLLAR.               
040100     DISPLAY '** KOHLS NET-COST: '          PV-DISP-DOLLAR.               
052500                                                                          
009940******************************************************************        
009950* 9100-ABEND-BAD-AMTS - THIS PARAGRAPH WILL BE PERFORMED IN THE           
009960* EVENT THAT THE TOTALS ON THE CONTROL RECORD FROM CMS DO NOT             
009960* MATCH THE TOTALS CALCULATED WITHIN THIS PROGRAM. IF THERE IS NO         
009960* MATCH, THE DISCREPANCY MUST BE RESOLVED BEFORE SENDING TO AP            
009970******************************************************************        
009980 9100-ABEND-BAD-AMTS.                                                     
052700     CLOSE CMS-EXT-FILE.                                                  
010080     MOVE +4014 TO ABEND-CODE.                                            
010090     CALL 'ILBOABN0' USING ABEND-CODE.                                    
052900                                                                          
009940******************************************************************        
009950* 9200-SQL-ERROR - THIS PARAGRAPH WILL BE PERFORMED WHEN THERE IS         
009960*     AND SQL ERROR.                                                      
009970******************************************************************        
052600 9200-SQL-ERROR.                                                          
052700     CLOSE CMS-EXT-FILE                                                   
052800           AP-TRANSACTION-FILE                                            
052900                                                                          
053000     MOVE SQLCODE                     TO SQLCODE2.                        
053100     MOVE SQLERRD(3)                  TO SQLERRD3.                        
053200                                                                          
053300     DISPLAY '** ABEND **         ** = SQLERROR'.                         
053400     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT038.                
053500     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
053600     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
053700     DISPLAY '**           ** = ' PV-DB2-TEXT.                            
053800                                                                          
053900     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
054000     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
054100     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
054200     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
054300                                                                          
054400     COPY DPPD004.                                                        
054500                                                                          
054600     MOVE +4013                       TO ABEND-CODE.                      
054700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
054800                                                                          
057600******************  END OF VLKUT038  *****************************        
