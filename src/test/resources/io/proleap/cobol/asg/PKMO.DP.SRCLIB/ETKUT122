       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT122.                                       
       AUTHOR.                  TIA JOHNSTON.                                   
       DATE-WRITTEN.            05/15/00.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM REFORMATS THE ACCOUNTS PAYABLE PAYMENT ORDER      *        
      * REMITTANCE ADVICE FILE INTO A FILE THAT CAN BE USED BY         *        
      * APPLICATION INTEGRATOR.                                        *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT OUTPUT-FILE                                                   
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 345 CHARACTERS                                       
           DATA RECORD IS AP001-PAYMENT-REMIT-RECORD.                           
                                                                                
           COPY APRD001.                                                        
                                                                                
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS ET026-PAYMENT-REMIT-RECORD.                           
                                                                                
           COPY ETRD026.                                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-RA-FUNC-CDE                   PIC X(02)   VALUE 'RA'.             
       01  PC-820-SET-CDE                   PIC X(03)   VALUE '820'.            
                                                                                
       01  WS-ENT-ID.                                                           
           05  WS-ENT-ID-NUMBER             PIC 9(09)   VALUE ZEROS.            
           05  WS-ENT-ID-TEST-FLAG          PIC X(01)   VALUE 'T'.              
                                                                                
       01  WS-DB2-ENT-ID                    PIC S9(09)  COMP.                   
       01  WS-DB2-ENT-ID-N                  PIC 9(10).                          
                                                                                
       01  WS-SAVE-DATE                     PIC X(08)   VALUE SPACES.           
       01  WS-SAVE-DOC-NBR                  PIC X(20)   VALUE SPACES.           
       01  WS-SAVE-DOC-TYPE                 PIC X(02)   VALUE SPACES.           
                                                                                
       01  WS-PREV-REC-TYPE                 PIC X(02)   VALUE SPACES.           
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW               PIC X(01)   VALUE 'N'.              
               88  END-OF-FILE                          VALUE 'Y'.              
           05  ERRORS-FOUND-SW              PIC X(01)   VALUE 'N'.              
               88  ERRORS-FOUND                         VALUE 'Y'.              
           05  PROCESS-REC-SW               PIC X(01)   VALUE 'N'.              
               88  NOT-PROCESS-REC                      VALUE 'N'.              
               88  PROCESS-REC                          VALUE 'Y'.              
           05  DTM-TYPE-SW                  PIC X(03)   VALUE SPACES.           
               88  DTM-NONE                             VALUE '   '.            
               88  DTM-ADJ                              VALUE 'ADJ'.            
               88  DTM-PAY                              VALUE 'PAY'.            
           05  ADJ-WRITTEN-SW               PIC X(01)   VALUE 'Y'.              
               88  ADJ-WRITTEN                          VALUE 'Y'.              
               88  ADJ-NOT-WRITTEN                      VALUE 'N'.              
                                                                                
      ******************************************************************        
      *  USED TO REPLACE MOS WITH SPACES IN THE ADJUSTMENT DOC NUMBER  *        
      *  000000790 - WILLIAM CARTER CO THE                             *        
      ******************************************************************        
       01  REPLACE-MOS-ENT                  PIC X(10)   VALUE SPACES.           
           88  REPLACE-MOS                          VALUE '0000000790'.         
                                                                                
      ******************************************************************        
      *  USED TO REPLACE * AND # WITH SPACES IN THE ADJUSTMENT DOC     *        
      *  NUMBER AND THE PAYMENT DOC NUMBER                             *        
      *  000000790 - WILLIAM CARTER CO THE                             *        
      ******************************************************************        
       01  REPLACE-CHR-ENT                  PIC X(10)   VALUE SPACES.           
           88  REPLACE-CHR                          VALUE '0000000790'.         
                                                                                
SP0408******************************************************************        
SP0408*  USED TO REPLACE * WITH A SPACE                                *        
SP0408*  000001289 - ARC INT'L NORTH AMERICA                           *        
SP0408******************************************************************        
SP0408 01  REPLACE-AST-ENT                  PIC X(10)   VALUE SPACES.           
SP0408     88  RPL-ENT-ID-1289                      VALUE '0000001289'.         
                                                                                
       01  WS-ENT-CNTR                      PIC 9(06)   VALUE ZERO.             
                                                                                
       01  PV-PARAGRAPH-NAME                PIC X(30).                          
       01  PV-DB2-OPER-ATTEMPTED            PIC X(30).                          
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
                                                                                
           COPY DPWS004.                                                        
                                                                                
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDFNC                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    PARTNER CURSOR                                                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
             DECLARE PARTNER_CSR CURSOR FOR                                     
               SELECT A.VEND_INCHG_ID,                                          
                      A.VEND_INCHG_ID_DESC,                                     
                      A.ENT_ID                                                  
               FROM   TTRDPRT A,                                                
                      TTRDFNC B,                                                
                      TTRDTRN C                                                 
               WHERE  A.ENT_ID       = :WS-DB2-ENT-ID AND                       
                      A.TP_DKEY      = B.TP_DKEY AND                            
                      A.TP_DKEY      = C.TP_DKEY AND                            
                      A.INACTV_DTE   = '9999-09-09' AND                         
                      B.FUNC_CDE     = :PC-RA-FUNC-CDE AND                      
                      C.INACTV_DTE   = '9999-09-09' AND                         
                      C.TRAN_SET_CDE = :PC-820-SET-CDE                          
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           MOVE SPACES TO AP001-RECORD-TYP-CDE.                                 
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           IF WS-PREV-REC-TYPE = '02' AND ADJ-NOT-WRITTEN                       
              AND PROCESS-REC                                                   
               PERFORM 1800-WRITE-ADJ-RSN-UNKWN                                 
                  THRU 1800-WRITE-ADJ-RSN-UNKWN-EXIT                            
           END-IF.                                                              
                                                                                
           SET ADJ-NOT-WRITTEN TO TRUE.                                         
                                                                                
           IF (DTM-ADJ OR DTM-PAY) AND PROCESS-REC                              
               PERFORM 1700-WRITE-DTM                                           
                  THRU 1700-WRITE-DTM-EXIT                                      
           END-IF.                                                              
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF REMIT-HEADER-RECORD                                               
              PERFORM 1100-RECORD-TYPE-01                                       
                 THRU 1100-RECORD-TYPE-01-EXIT.                                 
                                                                                
           IF ADJUSTMENT-REMIT-RECORD AND PROCESS-REC                           
              PERFORM 1200-RECORD-TYPE-02                                       
                 THRU 1200-RECORD-TYPE-02-EXIT.                                 
                                                                                
           IF ADJUSTMENT-COMMENT-RECORD AND PROCESS-REC                         
              PERFORM 1300-RECORD-TYPE-03                                       
                 THRU 1300-RECORD-TYPE-03-EXIT.                                 
                                                                                
           IF ADJUSTMENT-MEMO-RECORD AND PROCESS-REC                            
              PERFORM 1400-RECORD-TYPE-04                                       
                 THRU 1400-RECORD-TYPE-04-EXIT.                                 
                                                                                
           IF INVOICE-REMIT-RECORD AND PROCESS-REC                              
              PERFORM 1500-RECORD-TYPE-05                                       
                 THRU 1500-RECORD-TYPE-05-EXIT.                                 
                                                                                
           IF INVOICE-COMMENT-RECORD AND PROCESS-REC                            
              PERFORM 1600-RECORD-TYPE-06                                       
                 THRU 1600-RECORD-TYPE-06-EXIT.                                 
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       1100-RECORD-TYPE-01.                                                     
                                                                                
           IF WS-PREV-REC-TYPE = '02' AND ADJ-NOT-WRITTEN                       
              AND PROCESS-REC                                                   
               PERFORM 1800-WRITE-ADJ-RSN-UNKWN                                 
                  THRU 1800-WRITE-ADJ-RSN-UNKWN-EXIT                            
           END-IF.                                                              
                                                                                
           SET ADJ-NOT-WRITTEN TO TRUE.                                         
                                                                                
           IF (DTM-ADJ OR DTM-PAY) AND PROCESS-REC                              
               PERFORM 1700-WRITE-DTM                                           
                  THRU 1700-WRITE-DTM-EXIT                                      
           END-IF.                                                              
                                                                                
           MOVE +0 TO WS-ENT-CNTR.                                              
                                                                                
           MOVE AP001-HDR-ENT-ID TO REPLACE-MOS-ENT.                            
           MOVE AP001-HDR-ENT-ID TO REPLACE-CHR-ENT.                            
SP0408     MOVE AP001-HDR-ENT-ID TO REPLACE-AST-ENT.                            
           MOVE AP001-HDR-ENT-ID TO WS-DB2-ENT-ID.                              
           PERFORM 7000-OPEN-PRT-CURSOR                                         
              THRU 7000-OPEN-PRT-CURSOR-EXIT.                                   
           PERFORM 7100-FETCH-PARTNER                                           
              THRU 7100-FETCH-PARTNER-EXIT.                                     
           PERFORM 7200-CLOSE-PRT-CURSOR                                        
              THRU 7200-CLOSE-PRT-CURSOR-EXIT.                                  
                                                                                
           IF PROCESS-REC                                                       
              PERFORM 1105-PROCESS-TYPE-01                                      
                 THRU 1105-PROCESS-TYPE-01-EXIT.                                
                                                                                
       1100-RECORD-TYPE-01-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1105-PROCESS-TYPE-01.                                                    
                                                                                
      ***************************************************************           
      * THE CODE BELOW CHECKS FOR TRADING PARTNER ID'S WHICH HAVE               
      * BEEN SET UP FOR MORE THAN ONE ENT-ID AND MOVES THE ENT-ID OF            
      * THE PARENT TO THE FILE.                                                 
      ***************************************************************           
                                                                                
           MOVE TRDPRT-VEND-INCHG-ID TO TRDMLT-VND-INCHG-ID.                    
           MOVE TRDPRT-VEND-INCHG-ID-DESC TO TRDMLT-VND-INCHG-ID-DESC.          
                                                                                
           EXEC SQL                                                             
             SELECT PRNT_ENT_ID                                                 
               INTO :TRDMLT-PRNT-ENT-ID                                         
               FROM TTRDMLT                                                     
              WHERE VND_INCHG_ID = :TRDMLT-VND-INCHG-ID                         
                AND VND_INCHG_ID_DESC = :TRDMLT-VND-INCHG-ID-DESC               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +000                                              
                   MOVE TRDMLT-PRNT-ENT-ID TO WS-DB2-ENT-ID-N                   
                   MOVE WS-DB2-ENT-ID-N TO AP001-HDR-ENT-ID                     
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '1100-RECORD-TYPE-01' TO PV-PARAGRAPH-NAME              
                   MOVE 'SELECT TTRDMLT' TO PV-DB2-OPER-ATTEMPTED               
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '01' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           IF TEST-REMIT                                                        
               MOVE AP001-HDR-ENT-ID TO WS-ENT-ID-NUMBER                        
               MOVE WS-ENT-ID TO ET026-HDR-ENT-ID                               
           ELSE                                                                 
               MOVE AP001-HDR-ENT-ID TO ET026-HDR-ENT-ID                        
           END-IF.                                                              
                                                                                
           MOVE AP001-HDR-TRAN-CDE TO ET026-HDR-TRAN-CDE.                       
           MOVE AP001-HDR-CHECK-AMT TO ET026-HDR-CHECK-AMT.                     
           MOVE AP001-HDR-CR-DR-CDE TO ET026-HDR-CR-DR-CDE.                     
           MOVE AP001-HDR-PYMT-METHOD TO ET026-HDR-PYMT-METHOD.                 
           MOVE 'PBC' TO ET026-HDR-PYMT-FMT-CDE.                                
           IF AP001-HDR-ODFI-ROUTING-NBR = SPACES                               
               MOVE SPACES TO ET026-HDR-ODFI-ID-QUAL                            
           ELSE                                                                 
               MOVE '01' TO ET026-HDR-ODFI-ID-QUAL                              
           END-IF.                                                              
           MOVE AP001-HDR-ODFI-ROUTING-NBR                                      
                     TO ET026-HDR-ODFI-ROUTING-NBR.                             
           MOVE AP001-HDR-ODFI-ACCT-NBR                                         
                     TO ET026-HDR-ODFI-ACCT-NBR.                                
           MOVE AP001-HDR-ORIG-COMPANY-ID                                       
                     TO ET026-HDR-ORIG-COMPANY-ID.                              
           IF AP001-HDR-RDFI-ROUTING-NBR = SPACES                               
               MOVE SPACES TO ET026-HDR-RDFI-ID-QUAL                            
           ELSE                                                                 
               MOVE '01' TO ET026-HDR-RDFI-ID-QUAL                              
           END-IF.                                                              
           MOVE AP001-HDR-RDFI-ROUTING-NBR                                      
                     TO ET026-HDR-RDFI-ROUTING-NBR.                             
           MOVE AP001-HDR-RDFI-ACCT-NBR                                         
                     TO ET026-HDR-RDFI-ACCT-NBR.                                
           MOVE AP001-HDR-SETTLEMENT-DTE                                        
                     TO ET026-HDR-SETTLEMENT-DTE.                               
           MOVE '1' TO ET026-HDR-TRACE-TYP.                                     
           MOVE AP001-HDR-CHECK-NBR TO ET026-HDR-CHECK-NBR.                     
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '02' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE 'BT' TO ET026-HDR-REF-ID-QUAL.                                  
           MOVE AP001-HDR-PAY-CYCLE-SEQ-NBR TO ET026-HDR-REF-ID.                
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '03' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE '097' TO ET026-HDR-DATE-ID-QUAL.                                
           MOVE AP001-HDR-CHECK-DTE TO ET026-HDR-DATE-ID.                       
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '04' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE 'PR' TO ET026-ENTITY-ID-CODE.                                   
           MOVE AP001-HDR-ORIG-COMPANY-NME TO ET026-NAME.                       
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-NAME REPLACING ALL '*' BY ' '                      
SP0408     END-IF.                                                              
           MOVE '1' TO ET026-ID-CODE-QUAL.                                      
           MOVE AP001-HDR-ORIG-CO-VENDOR-ID TO ET026-ID-CODE.                   
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '04' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE 'PE' TO ET026-ENTITY-ID-CODE.                                   
           MOVE AP001-HDR-VCHR-VENDOR-NME TO ET026-NAME.                        
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-NAME REPLACING ALL '*' BY ' '                      
SP0408     END-IF.                                                              
           MOVE '1' TO ET026-ID-CODE-QUAL.                                      
           MOVE AP001-HDR-VCHR-VENDOR-ID TO ET026-ID-CODE.                      
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
       1105-PROCESS-TYPE-01-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1200-RECORD-TYPE-02.                                                     
                                                                                
           IF WS-PREV-REC-TYPE = '02' AND ADJ-NOT-WRITTEN                       
               PERFORM 1800-WRITE-ADJ-RSN-UNKWN                                 
                  THRU 1800-WRITE-ADJ-RSN-UNKWN-EXIT                            
           END-IF.                                                              
                                                                                
           SET ADJ-NOT-WRITTEN TO TRUE.                                         
                                                                                
           IF DTM-ADJ OR DTM-PAY                                                
               PERFORM 1700-WRITE-DTM                                           
                  THRU 1700-WRITE-DTM-EXIT                                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-REPL-CHARS-02                                           
              THRU 2000-REPL-CHARS-02-EXIT.                                     
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '05' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           ADD +1 TO WS-ENT-CNTR.                                               
           MOVE WS-ENT-CNTR TO ET026-ENT-COUNTER.                               
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '06' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE AP001-ADJ-NET-AMT TO ET026-ADJ-NET-AMT.                         
           MOVE AP001-ADJ-REASON-CDE TO ET026-ADJ-REASON-CDE.                   
           MOVE AP001-ADJ-REF-ID-QUAL TO ET026-ADJ-REF-ID-QUALIFIER.            
           MOVE AP001-ADJ-DOC-NBR TO ET026-ADJ-DOC-NBR.                         
           IF REPLACE-MOS                                                       
               INSPECT ET026-ADJ-DOC-NBR REPLACING ALL 'MOS' BY '   '           
           END-IF.                                                              
           IF REPLACE-CHR                                                       
               INSPECT ET026-ADJ-DOC-NBR REPLACING ALL '*' BY ' '               
               INSPECT ET026-ADJ-DOC-NBR REPLACING ALL '#' BY ' '               
           END-IF.                                                              
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-ADJ-DOC-NBR REPLACING ALL '*' BY ' '               
SP0408     END-IF.                                                              
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           IF AP001-ADJ-PO-NBR NOT = ZERO AND                                   
              AP001-ADJ-PO-NBR NOT = SPACES                                     
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '07' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'PO' TO ET026-ADJ-REF-ID-QUAL                               
               MOVE AP001-ADJ-PO-NBR TO ET026-ADJ-REF-ID                        
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
               SET ADJ-WRITTEN TO TRUE                                          
           END-IF.                                                              
                                                                                
           IF AP001-ADJ-DEPT-NBR NOT = ZERO AND                                 
              AP001-ADJ-DEPT-NBR NOT = SPACES                                   
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '07' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'DP' TO ET026-ADJ-REF-ID-QUAL                               
               MOVE AP001-ADJ-DEPT-NBR TO ET026-ADJ-REF-ID                      
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
               SET ADJ-WRITTEN TO TRUE                                          
           END-IF.                                                              
                                                                                
           IF AP001-ADJ-STR-NBR NOT = ZERO AND                                  
              AP001-ADJ-STR-NBR NOT = SPACES                                    
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '07' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'ST' TO ET026-ADJ-REF-ID-QUAL                               
               MOVE AP001-ADJ-STR-NBR TO ET026-ADJ-REF-ID                       
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
               SET ADJ-WRITTEN TO TRUE                                          
           END-IF.                                                              
                                                                                
           SET DTM-ADJ TO TRUE.                                                 
                                                                                
           INITIALIZE WS-SAVE-DATE                                              
                      WS-SAVE-DOC-NBR                                           
                      WS-SAVE-DOC-TYPE.                                         
                                                                                
           MOVE AP001-ADJ-DOC-NBR TO WS-SAVE-DOC-NBR.                           
           MOVE 'AM' TO WS-SAVE-DOC-TYPE.                                       
           MOVE AP001-ADJ-DOC-DTE TO WS-SAVE-DATE.                              
                                                                                
       1200-RECORD-TYPE-02-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1300-RECORD-TYPE-03.                                                     
                                                                                
           PERFORM 2100-REPL-CHARS-03                                           
              THRU 2100-REPL-CHARS-03-EXIT.                                     
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '07' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           IF WS-SAVE-DOC-TYPE = 'IV'                                           
               MOVE 'IK' TO ET026-ADJ-REF-ID-QUAL                               
           ELSE                                                                 
               MOVE WS-SAVE-DOC-TYPE TO ET026-ADJ-REF-ID-QUAL                   
           END-IF.                                                              
           MOVE WS-SAVE-DOC-NBR TO ET026-ADJ-REF-ID.                            
           IF ET026-ADJ-REF-ID-QUAL = 'AM' AND REPLACE-MOS                      
               INSPECT ET026-ADJ-REF-ID REPLACING ALL 'MOS' BY '   '            
           END-IF.                                                              
           IF ET026-ADJ-REF-ID-QUAL = 'AM' AND REPLACE-CHR                      
               INSPECT ET026-ADJ-REF-ID REPLACING ALL '*' BY ' '                
               INSPECT ET026-ADJ-REF-ID REPLACING ALL '#' BY ' '                
           END-IF.                                                              
SP0408     IF ET026-ADJ-REF-ID-QUAL = 'AM' AND RPL-ENT-ID-1289                  
SP0408         INSPECT ET026-ADJ-REF-ID REPLACING ALL '*' BY ' '                
SP0408     END-IF.                                                              
           MOVE AP001-ADJ-COMMENT-DESC TO ET026-ADJ-REF-NOTES.                  
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
           SET ADJ-WRITTEN TO TRUE.                                             
                                                                                
       1300-RECORD-TYPE-03-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1400-RECORD-TYPE-04.                                                     
                                                                                
           IF WS-PREV-REC-TYPE = '02' AND ADJ-NOT-WRITTEN                       
               PERFORM 1800-WRITE-ADJ-RSN-UNKWN                                 
                  THRU 1800-WRITE-ADJ-RSN-UNKWN-EXIT                            
           END-IF.                                                              
                                                                                
           SET ADJ-NOT-WRITTEN TO TRUE.                                         
                                                                                
           IF DTM-ADJ OR DTM-PAY                                                
               PERFORM 1700-WRITE-DTM                                           
                  THRU 1700-WRITE-DTM-EXIT                                      
           END-IF.                                                              
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '09' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE AP001-MEMO-SKU-UPC-NBR TO ET026-MEMO-SKU-UPC-NBR.               
           MOVE AP001-MEMO-ITEM-QTY TO ET026-MEMO-ITEM-QTY.                     
           MOVE AP001-MEMO-UOM TO ET026-MEMO-UOM.                               
           MOVE AP001-MEMO-UNIT-COST TO ET026-MEMO-UNIT-COST.                   
           MOVE AP001-MEMO-COST-CDE TO ET026-MEMO-COST-CDE.                     
           MOVE AP001-MEMO-PRODUCT-CDE TO ET026-MEMO-PRODUCT-CDE.               
           MOVE AP001-MEMO-VEND-STYL-NBR TO ET026-MEMO-VEND-STYL-NBR.           
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
       1400-RECORD-TYPE-04-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1500-RECORD-TYPE-05.                                                     
                                                                                
           IF WS-PREV-REC-TYPE = '02' AND ADJ-NOT-WRITTEN                       
               PERFORM 1800-WRITE-ADJ-RSN-UNKWN                                 
                  THRU 1800-WRITE-ADJ-RSN-UNKWN-EXIT                            
           END-IF.                                                              
                                                                                
           SET ADJ-NOT-WRITTEN TO TRUE.                                         
                                                                                
           IF DTM-ADJ OR DTM-PAY                                                
               PERFORM 1700-WRITE-DTM                                           
                  THRU 1700-WRITE-DTM-EXIT                                      
           END-IF.                                                              
                                                                                
           PERFORM 2200-REPL-CHARS-05                                           
              THRU 2200-REPL-CHARS-05-EXIT.                                     
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '05' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           ADD +1 TO WS-ENT-CNTR.                                               
           MOVE WS-ENT-CNTR TO ET026-ENT-COUNTER.                               
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '10' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           MOVE AP001-INV-DOC-TYP TO ET026-PAY-DOC-TYP.                         
           MOVE AP001-INV-DOC-NBR TO ET026-PAY-DOC-NBR.                         
           IF REPLACE-CHR                                                       
               INSPECT ET026-PAY-DOC-NBR REPLACING ALL '*' BY ' '               
               INSPECT ET026-PAY-DOC-NBR REPLACING ALL '#' BY ' '               
           END-IF.                                                              
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-PAY-DOC-NBR REPLACING ALL '*' BY ' '               
SP0408     END-IF.                                                              
           MOVE AP001-INV-NET-AMT TO ET026-PAY-NET-AMT.                         
           MOVE AP001-INV-GROSS-AMT TO ET026-PAY-GROSS-AMT.                     
           MOVE AP001-INV-DISCOUNT-AMT TO ET026-PAY-DISCOUNT-AMT.               
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
           IF AP001-INV-PO-NBR NOT = ZERO AND                                   
              AP001-INV-PO-NBR NOT = SPACES                                     
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '11' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'PO' TO ET026-PAY-REF-ID-QUAL                               
               MOVE AP001-INV-PO-NBR TO ET026-PAY-REF-ID                        
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
           END-IF.                                                              
                                                                                
           IF AP001-INV-DEPT-NBR NOT = ZERO AND                                 
              AP001-INV-DEPT-NBR NOT = SPACES                                   
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '11' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'DP' TO ET026-PAY-REF-ID-QUAL                               
               MOVE AP001-INV-DEPT-NBR TO ET026-PAY-REF-ID                      
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
           END-IF.                                                              
                                                                                
           IF AP001-INV-STR-NBR NOT = ZERO AND                                  
              AP001-INV-STR-NBR NOT = SPACES                                    
               INITIALIZE ET026-PAYMENT-REMIT-RECORD                            
                                                                                
               MOVE '11' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE 'ST' TO ET026-PAY-REF-ID-QUAL                               
               MOVE AP001-INV-STR-NBR TO ET026-PAY-REF-ID                       
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
           END-IF.                                                              
                                                                                
           SET DTM-PAY TO TRUE.                                                 
                                                                                
           INITIALIZE WS-SAVE-DATE                                              
                      WS-SAVE-DOC-NBR                                           
                      WS-SAVE-DOC-TYPE.                                         
                                                                                
           MOVE AP001-INV-DOC-NBR TO WS-SAVE-DOC-NBR.                           
           MOVE AP001-INV-DOC-TYP TO WS-SAVE-DOC-TYPE.                          
           MOVE AP001-INV-DOC-DTE TO WS-SAVE-DATE.                              
                                                                                
       1500-RECORD-TYPE-05-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1600-RECORD-TYPE-06.                                                     
                                                                                
           PERFORM 2300-REPL-CHARS-06                                           
              THRU 2300-REPL-CHARS-06-EXIT.                                     
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '11' TO ET026-RECORD-TYPE-CODE.                                 
                                                                                
           IF WS-SAVE-DOC-TYPE = 'IV'                                           
               MOVE 'IK' TO ET026-PAY-REF-ID-QUAL                               
           ELSE                                                                 
               MOVE WS-SAVE-DOC-TYPE TO ET026-PAY-REF-ID-QUAL                   
           END-IF.                                                              
           MOVE WS-SAVE-DOC-NBR TO ET026-PAY-REF-ID.                            
           IF REPLACE-CHR                                                       
               INSPECT ET026-PAY-REF-ID REPLACING ALL '*' BY ' '                
               INSPECT ET026-PAY-REF-ID REPLACING ALL '#' BY ' '                
           END-IF.                                                              
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-PAY-REF-ID REPLACING ALL '*' BY ' '                
SP0408     END-IF.                                                              
           MOVE AP001-INV-COMMENT-DESC TO ET026-PAY-REF-NOTES.                  
                                                                                
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
                                                                                
       1600-RECORD-TYPE-06-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       1700-WRITE-DTM.                                                          
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           IF DTM-ADJ                                                           
               MOVE '08' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE '097' TO ET026-ADJ-DATE-ID-QUAL                             
               MOVE WS-SAVE-DATE TO ET026-ADJ-DATE-ID                           
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
           END-IF.                                                              
                                                                                
           IF DTM-PAY                                                           
               MOVE '12' TO ET026-RECORD-TYPE-CODE                              
                                                                                
               MOVE '097' TO ET026-PAY-DATE-ID-QUAL                             
               MOVE WS-SAVE-DATE TO ET026-PAY-DATE-ID                           
                                                                                
               WRITE ET026-PAYMENT-REMIT-RECORD                                 
           END-IF.                                                              
                                                                                
           SET DTM-NONE TO TRUE.                                                
                                                                                
       1700-WRITE-DTM-EXIT.                                                     
           EXIT.                                                                
                                                                                
                                                                                
       1800-WRITE-ADJ-RSN-UNKWN.                                                
                                                                                
           INITIALIZE ET026-PAYMENT-REMIT-RECORD.                               
                                                                                
           MOVE '07' TO ET026-RECORD-TYPE-CODE.                                 
           MOVE 'AM' TO ET026-ADJ-REF-ID-QUAL.                                  
           MOVE WS-SAVE-DOC-NBR TO ET026-ADJ-REF-ID.                            
           MOVE 'ADJUSTMENT REASON UNKNOWN' TO ET026-ADJ-REF-NOTES.             
           IF REPLACE-MOS                                                       
               INSPECT ET026-ADJ-REF-ID REPLACING ALL 'MOS' BY '   '            
           END-IF.                                                              
           IF REPLACE-CHR                                                       
               INSPECT ET026-ADJ-REF-ID REPLACING ALL '*' BY ' '                
               INSPECT ET026-ADJ-REF-ID REPLACING ALL '#' BY ' '                
           END-IF.                                                              
SP0408     IF RPL-ENT-ID-1289                                                   
SP0408         INSPECT ET026-ADJ-REF-ID REPLACING ALL '*' BY ' '                
SP0408     END-IF.                                                              
           WRITE ET026-PAYMENT-REMIT-RECORD.                                    
           SET ADJ-WRITTEN TO TRUE.                                             
                                                                                
       1800-WRITE-ADJ-RSN-UNKWN-EXIT.                                           
           EXIT.                                                                
                                                                                
                                                                                
       2000-REPL-CHARS-02.                                                      
                                                                                
           INSPECT AP001-ADJ-DOC-NBR REPLACING ALL '*' BY '-'.                  
                                                                                
       2000-REPL-CHARS-02-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2100-REPL-CHARS-03.                                                      
                                                                                
           INSPECT AP001-ADJ-COMMENT-DESC REPLACING ALL '*' BY '-'.             
                                                                                
       2100-REPL-CHARS-03-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2200-REPL-CHARS-05.                                                      
                                                                                
           INSPECT AP001-INV-DOC-NBR REPLACING ALL '*' BY '-'.                  
                                                                                
       2200-REPL-CHARS-05-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
       2300-REPL-CHARS-06.                                                      
                                                                                
           INSPECT AP001-INV-COMMENT-DESC REPLACING ALL '*' BY '-'.             
                                                                                
       2300-REPL-CHARS-06-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    OPEN PARTNER CURSOR                                                  
      *****************************************************************         
       7000-OPEN-PRT-CURSOR.                                                    
                                                                                
           MOVE '7000-'              TO PV-PARAGRAPH-NAME.                      
           MOVE 'OPEN PARTNER CSR'   TO PV-DB2-OPER-ATTEMPTED.                  
                                                                                
           EXEC SQL                                                             
              OPEN PARTNER_CSR                                                  
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       7000-OPEN-PRT-CURSOR-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    FETCH PARTNER                                                        
      *****************************************************************         
       7100-FETCH-PARTNER.                                                      
                                                                                
           MOVE '7100-'            TO PV-PARAGRAPH-NAME.                        
           MOVE 'FETCH PARTNER'    TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
             FETCH PARTNER_CSR                                                  
             INTO  :TRDPRT-VEND-INCHG-ID,                                       
                   :TRDPRT-VEND-INCHG-ID-DESC,                                  
                   :TRDPRT-ENT-ID                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   SET PROCESS-REC TO TRUE                                      
               WHEN SQLCODE = +100                                              
                   SET ERRORS-FOUND TO TRUE                                     
                   SET NOT-PROCESS-REC TO TRUE                                  
                   DISPLAY 'TP RELN MISSING FOR ENT-ID: ' WS-DB2-ENT-ID         
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       7100-FETCH-PARTNER-EXIT.                                                 
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    CLOSE PARTNER CURSOR                                                 
      *****************************************************************         
       7200-CLOSE-PRT-CURSOR.                                                   
                                                                                
           MOVE '7200-'            TO PV-PARAGRAPH-NAME.                        
           MOVE 'CLOSE PARTNR CSR' TO PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           EXEC SQL                                                             
              CLOSE PARTNER_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   PERFORM 9000-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       7200-CLOSE-PRT-CURSOR-EXIT.                                              
           EXIT.                                                                
                                                                                
                                                                                
       8000-READ-INPUT-FILE.                                                    
                                                                                
           MOVE AP001-RECORD-TYP-CDE TO WS-PREV-REC-TYPE.                       
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
