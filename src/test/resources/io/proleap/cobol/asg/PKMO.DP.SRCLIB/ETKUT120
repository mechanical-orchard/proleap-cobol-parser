       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    ETKUT120.                                                 
       AUTHOR.        SUE PARI.                                                 
       DATE-WRITTEN.  04/06/98.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * CREATES A LIST OF 852 VENDORS                                  *        
      ******************************************************************        
JF0901*DATE  09/28/2001                                                         
JF0901*    CHANGE MADE: ADDED THREE NEW FIELDS (LAST YEARS RANK, BYTE           
JF0901*    COUNTS, AND ACTIVE DATE) AND REMOVED THE INHOUSE NUMBER              
JF0901*    FIELD SINCE IT IS NO LONGER USED.                                    
JF0901*    MADE BY: JON FIEDLER JF0901      WR/IR #: 22790                      
JF0901******************************************************************        
JF0102*                                                                         
JF0102*   DATE 01/02/2002                                                       
JF0102*     FIXED SQL ISSUE WITH THE MONTH OF JANUARY, THE PREVIOUS             
JF0102*     SQL WAS NOT PULLING BACK ANY BYTE COUNTS FOR THIS MONTH.            
JF0102*   MADE BY: JON FIEDLER             WR/IR #: 22855                       
JF0102******************************************************************        
NN0606*DATE 06/26/2006                                                          
NN0606*    CHANGE MADE: CHANGED THE INPUT TO A SORTED EXTRACT FILE              
NN0606*     CREATED FROM THE OUTPUT OF ETKUT860 AND ETKUT861                    
NN0606*     ALSO CHANGED THE REPORT TO INCLUDE ONORDER COLUMN                   
NN0606*    MADE BY: NIELS NIELSEN           WR/IR#                              
NN0606******************************************************************        
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT REPORT-FILE                                                   
               ASSIGN TO RPT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD.                                                        
           05  IN-VENDOR-NAME        PIC X(35).                                 
           05  FILLER                PIC X(2).                                  
           05  IN-DEPARTMENT         PIC X(3).                                  
           05  FILLER                PIC X(3).                                  
           05  IN-FREQUENCY          PIC X(6).                                  
           05  FILLER                PIC X(4).                                  
           05  IN-DAY                PIC X(9).                                  
           05  FILLER                PIC X(2).                                  
           05  IN-ONHANDS            PIC X(3).                                  
           05  FILLER                PIC X(1).                                  
           05  IN-ONORDER            PIC X(1).                                  
           05  FILLER                PIC X(2).                                  
           05  IN-LEVEL              PIC X(9).                                  
           05  FILLER                PIC X(4).                                  
           05  IN-ENTID              PIC X(10).                                 
           05  FILLER                PIC X(37).                                 
                                                                                
                                                                                
       FD  REPORT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-RECORD.                                        
                                                                                
       01  REPORT-RECORD             PIC X(132).                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
JF0901 01  PV-PROGRAM-VARIABLES.                                                
JF0901     05  PV-PARA                 PIC X(30).                               
JF0901     05  PV-DB2-OPER-ATTEMPTED   PIC X(30).                               
JF0901     05  PV-BYTE-CNT-SUM         PIC S9(9)    COMP.                       
JF0901     05  PV-BYTE-CNT-DEC         PIC S9(9)V9  COMP VALUE ZERO.            
JF0901     05  PV-NULL-IND             PIC S9(4)    COMP.                       
JF0901     05  PV-DUN-NBR              PIC X(9).                                
                                                                                
       01 ABEND-CODE   COMP SYNC PIC S9(4)    VALUE +0.                         
                                                                                
       01  LINE-CTR                  PIC 9(2).                                  
       01  WS-PAGE-NBR               PIC 9(2)  VALUE ZERO.                      
                                                                                
       01  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.                       
           88  END-OF-FILE                     VALUE 'Y'.                       
                                                                                
JF0901     COPY DPWS004.                                                        
                                                                                
       01  REPORT-HEADING1.                                                     
           05  FILLER                PIC X(55)  VALUE '  ETKUT863'.             
           05  FILLER                PIC X(21)  VALUE                           
               'P O S   V E N D O R S'.                                         
           05  FILLER                PIC X(37)  VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE 'PAGE:  '.                
           05  RPT-PAGE-NBR          PIC Z9.                                    
           05  FILLER                PIC X(10)  VALUE SPACES.                   
                                                                                
       01  REPORT-HEADING2.                                                     
           05  FILLER                PIC X(2)   VALUE SPACES.                   
           05  FILLER                PIC X(11)  VALUE 'VENDOR NAME'.            
           05  FILLER                PIC X(25)  VALUE SPACES.                   
JF0901     05  FILLER                PIC X(4)   VALUE 'RANK'.                   
JF0901     05  FILLER                PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(4)   VALUE 'DEPT'.                   
           05  FILLER                PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(9)   VALUE 'FREQUENCY'.              
           05  FILLER                PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(3)   VALUE 'DAY'.                    
           05  FILLER                PIC X(5)   VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE 'ONHANDS'.                
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE 'ONORDER'.                
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  FILLER                PIC X(5)   VALUE 'LEVEL'.                  
JF0901     05  FILLER                PIC X(11)  VALUE SPACES.                   
JF0901     05  FILLER                PIC X(10)  VALUE 'KC COUNT  '.             
JF0901     05  FILLER                PIC X(3)   VALUE SPACES.                   
JF0901     05  FILLER                PIC X(9)   VALUE 'ACTV DATE'.              
                                                                                
       01  REPORT-HEADING3.                                                     
           05  FILLER                PIC X(2)   VALUE SPACES.                   
           05  FILLER                PIC X(11)  VALUE '-----------'.            
           05  FILLER                PIC X(24)  VALUE SPACES.                   
JF0901     05  FILLER                PIC X(5)   VALUE '-----'.                  
JF0901     05  FILLER                PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(4)   VALUE '----'.                   
           05  FILLER                PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(9)   VALUE '---------'.              
           05  FILLER                PIC X(2)   VALUE SPACES.                   
           05  FILLER                PIC X(5)   VALUE '-----'.                  
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE '-------'.                
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  FILLER                PIC X(7)   VALUE '-------'.                
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  FILLER                PIC X(5)   VALUE '-----'.                  
           05  FILLER                PIC X(10)  VALUE SPACES.                   
JF0901     05  FILLER                PIC X(10)  VALUE '----------'.             
JF0901     05  FILLER                PIC X(3)   VALUE SPACES.                   
JF0901     05  FILLER                PIC X(10)  VALUE '----------'.             
                                                                                
       01  REPORT-DTL-LINE1.                                                    
           05  FILLER                PIC X(2)   VALUE SPACES.                   
           05  RPT-VENDOR-NAME       PIC X(35)  VALUE SPACES.                   
JF0901     05  RANK.                                                            
JF0901         10 RPT-RANK PIC ZZZZ.                                            
JF0901         10 FILLER REDEFINES RPT-RANK.                                    
JF0901            15 RPT-RANK-DISP PIC ZZZ9.                                    
JF0901     05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  RPT-DEPARTMENT        PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(5)   VALUE SPACES.                   
           05  RPT-FREQUENCY         PIC X(6)   VALUE SPACES.                   
           05  FILLER                PIC X(4)   VALUE SPACES.                   
           05  RPT-DAY               PIC X(9)   VALUE SPACES.                   
           05  FILLER                PIC X(2)   VALUE SPACES.                   
           05  RPT-ONHANDS           PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(8)   VALUE SPACES.                   
           05  RPT-ONORDER           PIC X(3)   VALUE SPACES.                   
           05  FILLER                PIC X(6)   VALUE SPACES.                   
           05  RPT-LEVEL             PIC X(9)   VALUE SPACES.                   
JF0901     05  FILLER                PIC X(6)   VALUE SPACES.                   
JF0901     05  BYTE-CNT.                                                        
JF0901         10  RPT-852-BYTE-CNT      PIC ZZZZZZZZ.Z.                        
JF0901         10  FILLER REDEFINES RPT-852-BYTE-CNT.                           
JF0901             15 RPT-852-BYTE-CNT-DISP PIC ZZZZZZZ9.9.                     
JF0901     05  FILLER                PIC X(3)   VALUE SPACES.                   
JF0901     05  RPT-ACTV-DTE          PIC X(10)  VALUE SPACES.                   
                                                                                
       01  BLANK-LINE.                                                          
           05  FILLER                PIC X(132) VALUE SPACES.                   
                                                                                
JF0901     COPY DPWS200O.                                                       
JF0901                                                                          
JF0901     COPY SQLCA2.                                                         
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        INCLUDE TTRDPRT                                                   
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        INCLUDE TEXTENT                                                   
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        INCLUDE TTRDTRN                                                   
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        INCLUDE TFGTRST                                                   
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        INCLUDE SQLCA                                                     
JF0901     END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT REPORT-FILE.                                             
                                                                                
           PERFORM 3000-PRINT-HEADINGS                                          
              THRU 3000-PRINT-HEADINGS-EXIT.                                    
                                                                                
           PERFORM 2000-READ-FILE                                               
              THRU 2000-READ-FILE-EXIT.                                         
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE INPUT-FILE                                                     
                 REPORT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           IF IN-VENDOR-NAME NOT = RPT-VENDOR-NAME                              
              WRITE REPORT-RECORD FROM BLANK-LINE                               
              ADD 1 TO LINE-CTR                                                 
              MOVE IN-VENDOR-NAME TO RPT-VENDOR-NAME                            
                                                                                
JF0901        MOVE IN-ENTID TO TRDPRT-ENT-ID                                    
JF0901                                                                          
JF0901                                                                          
JF0901           PERFORM 8050-GET-852-ACTV-DTE                                  
JF0901              THRU 8050-GET-852-ACTV-DTE-EXIT                             
JF0901                                                                          
JF0901           IF SQLCODE = +000                                              
JF0901              MOVE TRDTRN-ACTV-DTE TO RPT-ACTV-DTE                        
JF0901           ELSE                                                           
JF0901              MOVE 'INACTIVE' TO RPT-ACTV-DTE                             
JF0901           END-IF                                                         
JF0901                                                                          
JF0901           MOVE 0 TO PV-BYTE-CNT-SUM                                      
JF0901                                                                          
JF0901           PERFORM 8100-GET-BYTE-CNT                                      
JF0901              THRU 8100-GET-BYTE-CNT-EXIT                                 
JF0901                                                                          
JF0901           IF SQLCODE = +000                                              
JF0901              COMPUTE PV-BYTE-CNT-DEC = PV-BYTE-CNT-SUM / 1000            
JF0901              MOVE PV-BYTE-CNT-DEC TO RPT-852-BYTE-CNT-DISP               
JF0901           ELSE                                                           
JF0901              MOVE 0 TO RPT-852-BYTE-CNT-DISP                             
JF0901           END-IF                                                         
JF0901                                                                          
JF0901           PERFORM 8150-GET-TP-RANK                                       
JF0901              THRU 8150-GET-TP-RANK-EXIT                                  
JF0901                                                                          
JF0901           IF SQLCODE = +000                                              
JF0901              MOVE EXTENT-PFY-SLSRNK-NBR  TO RPT-RANK-DISP                
JF0901           ELSE                                                           
JF0901              MOVE 0 TO RPT-RANK                                          
JF0901           END-IF                                                         
JF0901                                                                          
JF0901                                                                          
JF0901     ELSE                                                                 
JF0901        MOVE SPACES TO RPT-VENDOR-NAME                                    
JF0901        MOVE SPACES TO RPT-ACTV-DTE                                       
JF0901        MOVE 0 TO RPT-RANK                                                
JF0901        MOVE 0 TO RPT-852-BYTE-CNT                                        
           END-IF.                                                              
                                                                                
           MOVE IN-DEPARTMENT TO RPT-DEPARTMENT.                                
           MOVE IN-FREQUENCY TO RPT-FREQUENCY.                                  
           MOVE IN-DAY TO RPT-DAY.                                              
           MOVE IN-ONHANDS TO RPT-ONHANDS.                                      
           MOVE IN-LEVEL TO RPT-LEVEL.                                          
                                                                                
                                                                                
           IF IN-ONORDER = 'Y'                                                  
             MOVE 'YES' TO RPT-ONORDER                                          
           ELSE                                                                 
               MOVE 'NO ' TO RPT-ONORDER                                        
           END-IF.                                                              
                                                                                
           IF LINE-CTR > 60                                                     
              PERFORM 3000-PRINT-HEADINGS                                       
                 THRU 3000-PRINT-HEADINGS-EXIT                                  
              WRITE REPORT-RECORD FROM BLANK-LINE                               
              ADD 1 TO LINE-CTR.                                                
                                                                                
           WRITE REPORT-RECORD FROM REPORT-DTL-LINE1.                           
           ADD 1 TO LINE-CTR.                                                   
                                                                                
           MOVE IN-VENDOR-NAME TO RPT-VENDOR-NAME.                              
                                                                                
           PERFORM 2000-READ-FILE                                               
              THRU 2000-READ-FILE-EXIT.                                         
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-FILE.                                                          
                                                                                
           READ INPUT-FILE                                                      
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       2000-READ-FILE-EXIT.                                                     
           EXIT.                                                                
                                                                                
                                                                                
       3000-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO WS-PAGE-NBR.                                                
           MOVE WS-PAGE-NBR TO RPT-PAGE-NBR.                                    
                                                                                
           WRITE REPORT-RECORD FROM REPORT-HEADING1                             
              AFTER ADVANCING PAGE.                                             
                                                                                
           WRITE REPORT-RECORD FROM BLANK-LINE.                                 
           WRITE REPORT-RECORD FROM BLANK-LINE.                                 
           WRITE REPORT-RECORD FROM REPORT-HEADING2.                            
           WRITE REPORT-RECORD FROM REPORT-HEADING3.                            
                                                                                
           MOVE 5 TO LINE-CTR.                                                  
                                                                                
       3000-PRINT-HEADINGS-EXIT.                                                
           EXIT.                                                                
                                                                                
JF0901******************************************************************        
JF0901* GET 852 ACTIVE DATE                                                     
JF0901******************************************************************        
JF0901 8050-GET-852-ACTV-DTE.                                                   
JF0901                                                                          
JF0901     MOVE '8050-'                 TO PV-PARA.                             
JF0901     MOVE 'SELECT ACTV DTE'       TO PV-DB2-OPER-ATTEMPTED.               
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        SELECT B.ACTV_DTE                                                 
JF0901        INTO :TRDTRN-ACTV-DTE                                             
JF0901        FROM TTRDPRT A                                                    
JF0901            ,TTRDTRN B                                                    
JF0901        WHERE A.ENT_ID = :TRDPRT-ENT-ID                                   
JF0901          AND A.TP_DKEY = B.TP_DKEY                                       
JF0901          AND B.INACTV_DTE = '9999-09-09'                                 
JF0901          AND TRAN_SET_CDE = '852'                                        
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EVALUATE TRUE                                                        
JF0901         WHEN SQLCODE = +0                                                
JF0901         WHEN SQLCODE  = +100                                             
JF0901             CONTINUE                                                     
JF0901         WHEN OTHER                                                       
JF0901             PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
JF0901     END-EVALUATE.                                                        
JF0901                                                                          
JF0901 8050-GET-852-ACTV-DTE-EXIT.                                              
JF0901     EXIT.                                                                
JF0901                                                                          
JF0901*****************************************************************         
JF0901* GET 852 BYTE COUNTS FOR SPECIFIED VENDOR                                
JF0901*****************************************************************         
JF0901 8100-GET-BYTE-CNT.                                                       
JF0901     MOVE '8100-'             TO PV-PARA.                                 
JF0901     MOVE 'SELECT TFGTRST'    TO PV-DB2-OPER-ATTEMPTED.                   
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901       SELECT SUM (CHARS_XMIT_NBR)                                        
JF0901       INTO   :PV-BYTE-CNT-SUM:PV-NULL-IND                                
JF0901       FROM   TFGTRST A                                                   
JF0901             ,TTRDPRT B                                                   
JF0102       WHERE  MONTH (CURRENT_DATE - 1 MONTH) =                            
JF0901              MONTH (TP_DKEY_TMST)                                        
JF0901              AND A.TP_DKEY = B.TP_DKEY                                   
JF0901              AND ENT_ID = :TRDPRT-ENT-ID                                 
JF0901              AND TRN_SET_CDE = '852'                                     
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EVALUATE TRUE                                                        
JF0901        WHEN SQLCODE = +0                                                 
JF0901        WHEN SQLCODE  = +100                                              
JF0901            CONTINUE                                                      
JF0901        WHEN OTHER                                                        
JF0901            PERFORM 9000-SQL-ABEND THRU 9000-EXIT                         
JF0901     END-EVALUATE.                                                        
JF0901                                                                          
JF0901 8100-GET-BYTE-CNT-EXIT.                                                  
JF0901     EXIT.                                                                
JF0901                                                                          
JF0901******************************************************************        
JF0901* GET LAST YEARS RANK                                                     
JF0901******************************************************************        
JF0901 8150-GET-TP-RANK.                                                        
JF0901                                                                          
JF0901     MOVE '8150-'             TO PV-PARA.                                 
JF0901     MOVE 'SELECT TP RANK'    TO PV-DB2-OPER-ATTEMPTED.                   
JF0901                                                                          
JF0901     EXEC SQL                                                             
JF0901        SELECT PFY_SLSRNK_NBR                                             
JF0901        INTO :EXTENT-PFY-SLSRNK-NBR                                       
JF0901        FROM TEXTENT A                                                    
JF0901        WHERE A.ENT_ID = :TRDPRT-ENT-ID                                   
JF0901          AND MDSE_VENDOR_IND = 'Y'                                       
JF0901          AND A.STATUS_CDE = 'A'                                          
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     EVALUATE TRUE                                                        
JF0901         WHEN SQLCODE = +0                                                
JF0901         WHEN SQLCODE  = +100                                             
JF0901             CONTINUE                                                     
JF0901         WHEN OTHER                                                       
JF0901             PERFORM 9000-SQL-ABEND THRU 9000-EXIT                        
JF0901     END-EVALUATE.                                                        
JF0901                                                                          
JF0901 8150-GET-TP-RANK-EXIT.                                                   
JF0901     EXIT.                                                                
JF0901                                                                          
JF0901*****************************************************************         
JF0901*    SQL ABEND ROUTINE                                                    
JF0901*****************************************************************         
JF0901 9000-SQL-ABEND.                                                          
JF0901                                                                          
JF0901     MOVE +4013                 TO ABEND-CODE.                            
JF0901     DISPLAY '**  ABEND     **'.                                          
JF0901     DISPLAY '**  PROGRAM   **  = ETKUT863'.                              
JF0901     DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
JF0901     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
JF0901                                                                          
JF0901*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
JF0901*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
JF0901                                                                          
JF0901     COPY DPPD004.                                                        
JF0901                                                                          
JF0901     PERFORM 9999-ABEND THRU 9999-EXIT.                                   
JF0901                                                                          
JF0901 9000-EXIT.                                                               
JF0901     EXIT.                                                                
JF0901                                                                          
JF0901 9999-ABEND.                                                              
JF0901                                                                          
JF0901     CALL 'ILBOABN0'             USING ABEND-CODE.                        
JF0901 9999-EXIT.                                                               
JF0901     EXIT.                                                                
