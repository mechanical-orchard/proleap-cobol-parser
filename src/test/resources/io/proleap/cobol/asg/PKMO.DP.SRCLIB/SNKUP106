000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.   SNKUP106.                                                  
000300 AUTHOR.       DENNIS STEFFEN.                                            
000400 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000500 DATE-WRITTEN. MARCH 2008.                                                
000600 DATE-COMPILED.                                                           
000700 ENVIRONMENT DIVISION.                                                    
000800                                                                          
000900 CONFIGURATION SECTION.                                                   
001000 SOURCE-COMPUTER.        IBM-3090.                                        
001100 OBJECT-COMPUTER.        IBM-3090.                                        
001200                                                                          
001300 INPUT-OUTPUT SECTION.                                                    
001400 FILE-CONTROL.                                                            
001500     SELECT CNTL-ID-FILE ASSIGN TO INP01.                                 
001600                                                                          
001700 DATA DIVISION.                                                           
001800******************************************************************        
001900 FILE SECTION.                                                            
002000                                                                          
002100 FD  CNTL-ID-FILE                                                         
002200     RECORDING MODE IS F                                                  
002300     LABEL RECORDS ARE STANDARD                                           
002400     RECORD CONTAINS 020 CHARACTERS                                       
002500     BLOCK CONTAINS 0 RECORDS                                             
002600     DATA RECORD IS CNTL-ID-RECORD.                                       
002700                                                                          
002800 01  CNTL-ID-RECORD                  PIC X(020).                          
002900                                                                          
003000 WORKING-STORAGE SECTION.                                                 
003100                                                                          
003200 01  PS-PROGRAM-SWITCHES.                                                 
003300     05  PS-END-OF-CNTL-SW           PIC X(01) VALUE 'N'.                 
003400         88  PS-END-OF-CNTL-YES            VALUE 'Y'.                     
003500         88  PS-END-OF-CNTL-NO             VALUE 'N'.                     
003600                                                                          
003700 01  PC-PROGRAM-CONSTANTS.                                                
003800     05  PC-CURRENT-PROGRAM-NAME     PIC X(08) VALUE 'SNKUP106'.          
003900                                                                          
004000 01  PA-PROGRAM-ACCUMULATORS.                                             
004100     05  PA-TOTALS.                                                       
004200         10  PA-TOTAL-CNTL-READ      PIC S9(7) VALUE +0 COMP-3.           
004300         10  PA-TOTAL-CNTL-UPDATED   PIC S9(7) VALUE +0 COMP-3.           
004400         10  PA-UPDATES-NOT-FOUND    PIC S9(7) VALUE +0 COMP-3.           
004500         10  PA-COMMITS-TAKEN        PIC S9(7) VALUE +0 COMP-3.           
004600                                                                          
004700 01  PV-PROGRAM-VARIABLES.                                                
004800     05  PV-DISPLAY-RECORD           PIC Z,ZZZ,ZZ9-.                      
004900                                                                          
005000                                                                          
005100 01  PV-ABEND-VARIABLES.                                                  
005200     05  PV-ABEND-CODE               PIC S9(4) VALUE +0 COMP SYNC.        
005300     05  PV-SQLCODE                  PIC S9(9).                           
005400     05  PV-DISPLAY-SQLCODE          PIC -------999.                      
005500     05  PV-ABEND-PROGRAM            PIC X(08) VALUE 'SNKUP106'.          
005600     05  PV-ABEND-PARAGRAPH          PIC X(25) VALUE SPACES.              
005700     05  PV-ABEND-OPERATION          PIC X(20) VALUE SPACES.              
005800     05  PV-ABEND-STRUCTURE-1        PIC X(08) VALUE SPACES.              
005900     05  PV-DB2-OPERATION-MESSAGE-1  PIC X(79) VALUE SPACES.              
006000                                                                          
006100     COPY SNRD006.                                                        
006200                                                                          
006300     COPY DPWS004.                                                        
006400                                                                          
006500     EXEC SQL                                                             
006600         INCLUDE SQLCA                                                    
006700     END-EXEC.                                                            
006800                                                                          
006900*  (SNT_DAT_CNTL - ASN DATA TRANSFER CONTROL TABLE)                       
007000     EXEC SQL                                                             
007100         INCLUDE SNT00000                                                 
007200     END-EXEC.                                                            
007300/                                                                         
007400 PROCEDURE DIVISION.                                                      
007500                                                                          
007600     PERFORM 1000-INITIALIZE.                                             
007700                                                                          
007800     PERFORM 2000-PROCESS                                                 
007900         UNTIL PS-END-OF-CNTL-YES                                         
008000                                                                          
008100     PERFORM 9000-EOJ-ROUTINE.                                            
008200                                                                          
008300     GOBACK.                                                              
008400                                                                          
008500******************************************************************        
008600*                                                                *        
008700*                I N I T I A L I Z A T I O N                     *        
008800*                                                                *        
008900******************************************************************        
009000 1000-INITIALIZE.                                                         
009100                                                                          
009200     DISPLAY '************ START OF SNKUP106 ***************'             
009300     OPEN INPUT CNTL-ID-FILE.                                             
009400                                                                          
009500     PERFORM 8000-READ-CONTROL-FILE.                                      
009600                                                                          
009700     IF PS-END-OF-CNTL-YES                                                
009800         DISPLAY ' '                                                      
009900         DISPLAY '*************************************'                  
010000         DISPLAY '**    INP01 DRIVER FILE IS EMPTY   **'                  
010100         DISPLAY '**      PROGRAM IS ENDING          **'                  
010200         DISPLAY '*************************************'                  
010300         DISPLAY ' '                                                      
010400     END-IF.                                                              
010500                                                                          
010600                                                                          
010700 2000-PROCESS.                                                            
010800***  DISPLAY '2000-PROCESS'                                               
010900                                                                          
011000     MOVE SN006-DAT-XFER-CNTL-ID      TO SNT00000-DAT-XFER-CNTL-ID        
011100     MOVE SN006-STAT-CDE              TO SNT00000-STAT-CDE                
011200     PERFORM 7000-UPDATE-CNTL                                             
011300                                                                          
011400     IF SQLCODE = ZERO                                                    
011500         PERFORM 7900-COMMIT                                              
011600     END-IF                                                               
011700                                                                          
011800     PERFORM 8000-READ-CONTROL-FILE.                                      
011900                                                                          
012000 7000-UPDATE-CNTL.                                                        
012100***  DISPLAY ' 7000-UPDATE-CNTL'.                                         
012200                                                                          
012300*    IF THE ROW WAS PREVIOUSLY UPDATED THEN IT WILL BE SKIPPED.           
012400*    THIS WOULD HAPPEN IF THE PROGRAM WERE RESTARTED AFTER                
012500*    AN ABEND                                                             
012600                                                                          
012700     EXEC SQL                                                             
012800         UPDATE SNT_DAT_CNTL                                              
012900            SET STAT_CDE         = :SNT00000-STAT-CDE                     
013000             ,  END_PRC_TMST     = CURRENT TIMESTAMP                      
013100          WHERE DAT_XFER_CNTL_ID = :SNT00000-DAT-XFER-CNTL-ID             
013200          AND STAT_CDE  = 'R'                                             
013300                                                                          
013400     END-EXEC.                                                            
013500                                                                          
013600     EVALUATE TRUE                                                        
013700       WHEN SQLCODE = ZERO                                                
013800           ADD +1 TO PA-TOTAL-CNTL-UPDATED                                
013900                                                                          
014000       WHEN SQLCODE = +100                                                
014100           ADD +1 TO PA-UPDATES-NOT-FOUND                                 
014200             MOVE SN006-DAT-XFER-CNTL-ID TO PV-DISPLAY-RECORD             
014300             DISPLAY ' RECORD NOT FOUND OR HAS WRONG STATUS / '           
014400                     ' CNTL-ID = ' PV-DISPLAY-RECORD                      
014500                                                                          
014600       WHEN OTHER                                                         
014700             MOVE '7000-UPDATE-CNTL'    TO PV-ABEND-PARAGRAPH             
014800             MOVE 'UPDATE SNT_DAT_CNTL' TO PV-ABEND-OPERATION             
014900             MOVE SN006-DAT-XFER-CNTL-ID TO PV-DISPLAY-RECORD             
015000             DISPLAY ' CNTL-ID = ' PV-DISPLAY-RECORD                      
015100           PERFORM 9900-DB2-ABEND                                         
015200     END-EVALUATE.                                                        
015300                                                                          
015400 7900-COMMIT.                                                             
015500                                                                          
015600     EXEC SQL                                                             
015700         COMMIT                                                           
015800     END-EXEC.                                                            
015900                                                                          
016000     EVALUATE TRUE                                                        
016100       WHEN SQLCODE = ZERO                                                
016200             ADD 1                    TO PA-COMMITS-TAKEN                 
016300           CONTINUE                                                       
016400                                                                          
016500       WHEN OTHER                                                         
016600           MOVE '7900-COMMIT'        TO PV-ABEND-PARAGRAPH                
016700           MOVE 'COMMIT LOAD TABLES' TO PV-ABEND-OPERATION                
016800           PERFORM 9900-DB2-ABEND                                         
016900     END-EVALUATE.                                                        
017000                                                                          
017100 8000-READ-CONTROL-FILE.                                                  
017200     READ CNTL-ID-FILE                                                    
017300         INTO SN006-CONTROL-RECORD                                        
017400     AT END                                                               
017500         SET PS-END-OF-CNTL-YES       TO TRUE                             
017600     NOT AT END                                                           
017700         ADD +1 TO PA-TOTAL-CNTL-READ                                     
017800     END-READ.                                                            
017900                                                                          
018000 9000-EOJ-ROUTINE.                                                        
018100                                                                          
018200     DISPLAY ' '.                                                         
018300     DISPLAY '*******************************************'.               
018400     DISPLAY '      * SNKUP106 SUMMARY STATISTICS *      '.               
018500     DISPLAY ' '.                                                         
018600     DISPLAY '     --------------------------------    '.                 
018700     DISPLAY '     INPUT REPOSITORY EXTRACT RESULTS    '.                 
018800     DISPLAY '     --------------------------------    '.                 
018900     MOVE PA-TOTAL-CNTL-READ                TO PV-DISPLAY-RECORD.         
019000     DISPLAY '  CORPORATE ASN HDR ROWS READ =' PV-DISPLAY-RECORD.         
019100                                                                          
019200     MOVE PA-TOTAL-CNTL-UPDATED             TO PV-DISPLAY-RECORD.         
019300     DISPLAY '  CORP ASN HDR STATUS UPDATED =' PV-DISPLAY-RECORD.         
019400                                                                          
019500     MOVE PA-UPDATES-NOT-FOUND              TO PV-DISPLAY-RECORD.         
019600     DISPLAY '  UPDATES NOT FOUND           =' PV-DISPLAY-RECORD.         
019700                                                                          
019800     MOVE PA-COMMITS-TAKEN                  TO PV-DISPLAY-RECORD.         
019900     DISPLAY '  COMMITS TAKEN               =' PV-DISPLAY-RECORD.         
020000     DISPLAY '*******************************************'.               
020100                                                                          
020200     DISPLAY ' '.                                                         
020300     DISPLAY '************  END OF SNKUP106  ***************'             
020400                                                                          
020500     CLOSE CNTL-ID-FILE.                                                  
020600                                                                          
020700 9900-DB2-ABEND.                                                          
020800                                                                          
020900     MOVE SQLCODE    TO PV-SQLCODE.                                       
021000     MOVE PV-SQLCODE TO PV-DISPLAY-SQLCODE.                               
021100     DISPLAY '*** DB2 ERROR ***'.                                         
021200     DISPLAY '*** SQLCODE   = ' PV-DISPLAY-SQLCODE.                       
021300     DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
021400     DISPLAY '*** PARAGRAPH = ' PV-ABEND-PARAGRAPH.                       
021500     DISPLAY '*** OPERATION = ' PV-ABEND-OPERATION.                       
021600     DISPLAY '*** MESSAGE   = ' PV-DB2-OPERATION-MESSAGE-1.               
021700     DISPLAY '*** TABLE 1   = ' PV-ABEND-STRUCTURE-1.                     
021800                                                                          
021900******************************************************************        
022000* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
022100* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
022200* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
022300* FORMAT.                                                                 
022400******************************************************************        
022500     COPY DPPD004.                                                        
022600                                                                          
022700     IF SQLCODE NOT = -911                                                
022800         EXEC SQL                                                         
022900             ROLLBACK                                                     
023000         END-EXEC                                                         
023100     END-IF.                                                              
023200                                                                          
023300     MOVE +4013         TO PV-ABEND-CODE.                                 
023400     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
023500******************************************************************        
