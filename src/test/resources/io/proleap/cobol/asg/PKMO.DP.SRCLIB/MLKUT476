       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    MLKUT476.                                         00020010
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  10-01-2000.                                       00050010
       DATE-COMPILED.                                                   00060000
                                                                        00061007
      ******************************************************************00070000
      *PURPOSE:                                                         00080007
      * THIS PROGRAM WILL CREATE THE EXTRACT FOR THE RECALC PROGRAM     00110000
      * ON TABLE TMNDPTC                                                00120010
      ******************************************************************00140000
      * WR/PITS#  DATE      DESCRIPTION                                *00141020
      * -------- --------   -----------------------------------------  *00142020
      * W31475  06/01/2009  COGNIZANT                                  *00143020
      *                     MODIFIED CURSOR DECLARATION FOR MULTI ROW  *00144020
      *                     FETCH AS PART OF ML BATCH PERF TUNE PRJ    *00145020
      ******************************************************************00146020
                                                                        00150007
       ENVIRONMENT DIVISION.                                            00160000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.        IBM-3090.                                00200000
       OBJECT-COMPUTER.        IBM-3090.                                00210000
       INPUT-OUTPUT SECTION.                                            00230000
       FILE-CONTROL.                                                    00240000
                                                                        00250000
           SELECT MNDPTC-EXTR-FILE                                      00260010
               ASSIGN TO OUT01.                                         00270000
                                                                        00280000
       DATA DIVISION.                                                   00300000
       FILE SECTION.                                                    00330000
                                                                        00340000
       FD  MNDPTC-EXTR-FILE                                             00350010
           RECORDING MODE IS F                                          00360000
           LABEL RECORDS ARE STANDARD                                   00370000
           BLOCK CONTAINS 0 RECORDS.                                    00380000
       01  MNDPTC-EXTR-REC             PIC X(04).                       00400010
                                                                        00410000
      ******************************************************************00430000
       WORKING-STORAGE SECTION.                                         00440000
      ******************************************************************00450000
                                                                        00460000
W31475 01  ROW-ID                      PIC S9(04) COMP VALUE 0.         00462019
  |    01  HOST-VARIABLES.                                              00463019
  |        05  HV-MNDPTC-DEPT-NBR      PIC X(03)   VALUE SPACES         00464019
  |                                    OCCURS 100  TIMES.               00465019
  |        05  HV-MNDPTC-CHNL-CDE      PIC X(01)   VALUE SPACES         00468019
W31475                                 OCCURS 100  TIMES.               00469019
       01  PS-PROGRAM-SWITCHES.                                         00470000
           05  PS-TMNDPTC-DONE         PIC X       VALUE 'N'.           00480010
               88  PS-TMNDPTC-IS-DONE              VALUE 'Y'.           00490010
               88  PS-TMNDPTC-IS-NOT-DONE          VALUE 'N'.           00500010
                                                                        00510000
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.              00520000
           05  PA-TMNDPTC-ROWS-FETCHED PIC S9(13)  VALUE ZERO.          00530010
           05  PA-OUTPUT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.          00540000
                                                                        00550000
       01  CD-COUNTER-DISPLAY.                                          00560000
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.           00570000
                                                                        00580000
       01 ABEND-AREAS.                                                  00590000
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO           00600000
                                                   COMP SYNC.           00610000
               88  AC-BAD-DATA                     VALUE +4003.         00620000
               88  AC-DB2-ERROR                    VALUE +4013.         00630000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                00640000
                 '*****       ABEND'.                                   00650000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                00660000
                   '*****   PROGRAM: MLKUT476'.                         00670010
           05  AA-PARAGRAPH-LIT.                                        00680000
               10  FILLER              PIC X(17)   VALUE                00690000
                   '***** PARAGRAPH: '.                                 00700000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        00710000
           05  AA-MESSAGE-LINE.                                         00720000
               10  FILLER              PIC X(06)   VALUE '*****'.       00730000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        00740000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                00750000
                   '*****    DB2 ERROR'.                                00760000
           05  AA-DB2-OPERATION-LIT.                                    00770000
               10  FILLER              PIC X(17)   VALUE                00780000
                   '***** OPERATION: '.                                 00790000
               10  AA-DB2-OPERATION.                                    00800000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        00810000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        00820000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        00830000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        00840000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        00850000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        00860000
                                                                        00870000
      *** TMNDPTC EXTRACT FILE                                          00880010
       01  EXTR-RECORD.                                                 00890000
           05  EXTR-DEPT-NBR    PIC X(03).                              00900000
JS0200     05  EXTR-CHNL-CDE    PIC X(01).                              00911008
       EJECT                                                            00920000
                                                                        00930000
      ******************************************************************00940000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00950000
      ******************************************************************00960000
           COPY DPWS004.                                                00970000
                                                                        00980000
      ******************************************************************00990000
      *  DB2 ERRORS                                                     01000000
      ******************************************************************01010000
           COPY SQLCA2.                                                 01020000
                                                                        01030000
      ******************************************************************01040000
      *  DB2 COMMUNICATION AREA                                         01050000
      ******************************************************************01060000
           EXEC SQL                                                     01070000
                INCLUDE SQLCA                                           01080000
           END-EXEC.                                                    01090000
                                                                        01100000
      ******************************************************************01110000
      *  DB2 TMNDPTC TABLE LAYOUT                                       01120010
      ******************************************************************01130000
           EXEC SQL                                                     01140000
                INCLUDE TMNDPTC                                         01150010
           END-EXEC.                                                    01160000
                                                                        01170000
      ******************************************************************01170108
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO                01170308
      ******************************************************************01170408
           EXEC SQL                                                     01170600
               INCLUDE TDTATTR                                          01170700
           END-EXEC.                                                    01171000
                                                                        01180000
      ******************************************************************01190008
      * M/L DATE CONTROL INFO                                           01240208
      ******************************************************************01240408
           EXEC SQL                                                     01240500
               INCLUDE TMLCNTL                                          01240600
           END-EXEC.                                                    01240700
                                                                        01240800
      ******************************************************************01250000
      *  DB2 TMNDPTC CURSOR                                             01260010
      ******************************************************************01270000
           EXEC SQL                                                     01280000
W31475         DECLARE TMNDPTC_CURSOR CURSOR                            01282019
W31475         WITH ROWSET POSITIONING FOR                              01283019
               SELECT DISTINCT DEPT_NBR                                 01300000
                              ,CHNL_CDE                                 01311009
               FROM TMNDPTC                                             01320010
                   ,TMLCNTL                                             01331000
               WHERE (FSCL_MN_END_DTE BETWEEN                           01332011
                                   OPN_FSCL_MN_DTE                      01333011
                               AND MXPSTG_FSCL_MN_DTE) OR               01334011
                    ((FSCL_MN_END_DTE BETWEEN                           01335011
                      :DTATTR-PFM-END-DTE AND                           01336011
                      MXPSTG_FSCL_MN_DTE) AND                           01337115
                       (END_INV_RTL_AMT  <> 0  OR                       01338015
                        END_INV_COST_AMT <> 0))                         01339015
               ORDER BY DEPT_NBR, CHNL_CDE                              01339111
           END-EXEC.                                                    01380000
                                                                        01390000
      /*****************************************************************01420008
       PROCEDURE DIVISION.                                              01430000
      ******************************************************************01440000
                                                                        01450000
      ******************************************************************01460000
       A000-MAIN-MODULE.                                                01470000
      ******************************************************************01480000
      *                                                                 01490000
           PERFORM B000-INITIALIZE.                                     01500000
                                                                        01510000
           PERFORM B100-PROCESS-DATA.                                   01520000
                                                                        01530000
           PERFORM B900-EOJ-ROUTINE.                                    01540000
                                                                        01550000
           STOP RUN.                                                    01560000
                                                                        01570000
      /*****************************************************************01600008
       B000-INITIALIZE.                                                 01610000
      ******************************************************************01620000
      *                                                                 01630000
           OPEN OUTPUT MNDPTC-EXTR-FILE.                                01640010
           PERFORM 7100-SELECT-DB2-DATES.                               01641000
                                                                        01650000
      /*****************************************************************01690008
       B100-PROCESS-DATA.                                               01700000
      ******************************************************************01710000
      *                                                                 01720000
           PERFORM Z210-OPEN-CURSOR-TMNDPTC.                            01730010
                                                                        01740000
           PERFORM Z220-FETCH-TMNDPTC.                                  01750010
                                                                        01760000
           PERFORM                                                      01770000
             UNTIL PS-TMNDPTC-IS-DONE                                   01780010
W31475         PERFORM VARYING ROW-ID FROM 1 BY 1                       01782019
  |            UNTIL ROW-ID > SQLERRD (3)                               01783019
  |                 PERFORM C100-WRITE-EXTRACT                          01784019
  |            END-PERFORM                                              01785021
W31475*        PERFORM C100-WRITE-EXTRACT                               01786021
               PERFORM Z220-FETCH-TMNDPTC                               01800010
           END-PERFORM.                                                 01810000
                                                                        01820000
           PERFORM Z230-CLOSE-CURSOR-TMNDPTC.                           01830010
                                                                        01840000
      /*****************************************************************01870008
       C100-WRITE-EXTRACT.                                              01880000
      ******************************************************************01890000
      *                                                                 01900000
W31475*    MOVE MNDPTC-DEPT-NBR        TO EXTR-DEPT-NBR.                01901021
  |   *    MOVE MNDPTC-CHNL-CDE        TO EXTR-CHNL-CDE.                01901121
  |        MOVE HV-MNDPTC-DEPT-NBR(ROW-ID)                              01902021
  |                                    TO EXTR-DEPT-NBR.                01903019
  |        MOVE HV-MNDPTC-CHNL-CDE(ROW-ID)                              01906019
W31475                                 TO EXTR-CHNL-CDE.                01907019
           WRITE MNDPTC-EXTR-REC       FROM EXTR-RECORD.                01930010
                                                                        01940008
           ADD 1                       TO PA-OUTPUT-RECS-WRITTEN.       01950000
                                                                        01960000
      /*****************************************************************01990008
       B900-EOJ-ROUTINE.                                                02000000
      ******************************************************************02010000
      *                                                                 02020000
           DISPLAY ' '.                                                 02030000
           DISPLAY '  PROGRAM MLKUT476 PROCESSING SUMMARY'.             02040010
           DISPLAY '  -----------------------------------'.             02050000
           DISPLAY ' '.                                                 02060000
      *                                                                 02070000
           MOVE PA-TMNDPTC-ROWS-FETCHED TO CD-COUNT-HOLD1.              02080010
           DISPLAY 'TMNDPTC ROWS FETCHED =      ' CD-COUNT-HOLD1.       02090010
      *                                                                 02100000
           MOVE PA-OUTPUT-RECS-WRITTEN TO CD-COUNT-HOLD1.               02110000
           DISPLAY 'EXTRACT RECORDS WRITTEN =   ' CD-COUNT-HOLD1.       02120000
      *                                                                 02130000
           DISPLAY ' '.                                                 02140000
                                                                        02150000
           CLOSE MNDPTC-EXTR-FILE.                                      02160010
                                                                        02170000
      /*****************************************************************02220008
       Z210-OPEN-CURSOR-TMNDPTC.                                        02230010
      ******************************************************************02240000
      *                                                                 02250000
           EXEC SQL                                                     02260000
               OPEN TMNDPTC_CURSOR                                      02270010
           END-EXEC.                                                    02280000
                                                                        02290000
           EVALUATE TRUE                                                02300000
               WHEN SQLCODE < 0                                         02310000
                   MOVE 'Z210-OPEN-CURSOR-TMNDPTC'                      02320010
                                       TO AA-PARAGRAPH-NAME             02330000
                   MOVE 'TMNDPTC TABLE ERROR'                           02340010
                                       TO AA-MESSAGE                    02350000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02360000
                   PERFORM Z998-SQL-ERROR                               02370000
               WHEN SQLCODE > 0                                         02380000
               WHEN SQLWARN0 NOT = SPACES                               02390000
                   MOVE 'Z210-OPEN-CURSOR-TMNDPTC'                      02400010
                                       TO AA-PARAGRAPH-NAME             02410000
                   MOVE 'TMNDPTC TABLE WARNING'                         02420010
                                       TO AA-MESSAGE                    02430000
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION              02440000
                   PERFORM Z998-SQL-ERROR                               02450000
           END-EVALUATE.                                                02460000
                                                                        02470000
      /*****************************************************************02500019
       Z220-FETCH-TMNDPTC.                                              02510010
      ******************************************************************02520000
W31475*                                                                 02531019
  |   *    EXEC SQL                                                     02540019
  |   *        FETCH TMNDPTC_CURSOR                                     02550019
  |   *         INTO :MNDPTC-DEPT-NBR                                   02560019
  |   *             ,:MNDPTC-CHNL-CDE                                   02580019
  |   *    END-EXEC.                                                    02590019
  |        INITIALIZE HOST-VARIABLES.                                   02590119
  |                                                                     02590219
  |        EXEC SQL                                                     03180319
  |            FETCH NEXT ROWSET FROM TMNDPTC_CURSOR FOR 100 ROWS       03180419
  |             INTO :HV-MNDPTC-DEPT-NBR                                03180519
  |                 ,:HV-MNDPTC-CHNL-CDE                                03180619
W31475     END-EXEC.                                                    03180719
                                                                        03180816
           EVALUATE TRUE                                                03180916
               WHEN SQLCODE = 0                                         03181016
W31475*            ADD +1              TO PA-TMNDPTC-ROWS-FETCHED       03181121
W31475             ADD +100            TO PA-TMNDPTC-ROWS-FETCHED       03181221
               WHEN SQLCODE = +100                                      03181321
                   MOVE 'Y'            TO PS-TMNDPTC-DONE               03181418
W31475             ADD SQLERRD (3)     TO PA-TMNDPTC-ROWS-FETCHED       03181519
  |                PERFORM VARYING ROW-ID FROM 1 BY 1                   03181619
  |                UNTIL ROW-ID > SQLERRD (3)                           03181719
  |                     PERFORM C100-WRITE-EXTRACT                      03181819
W31475             END-PERFORM                                          03181919
               WHEN SQLCODE < 0                                         03182116
                   MOVE 'Z220-FETCH-TMNDPTC'                            03182216
                                       TO AA-PARAGRAPH-NAME             03182316
                   MOVE 'TMNDPTC TABLE ERROR'                           03182416
                                       TO AA-MESSAGE                    03182516
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03182616
                   PERFORM Z998-SQL-ERROR                               03182716
               WHEN ((SQLCODE > 0) AND (SQLCODE NOT = +100))            03182816
               WHEN SQLWARN0 NOT = SPACES                               03182916
                   MOVE 'Z220-FETCH-TMNDPTC'                            03183016
                                       TO AA-PARAGRAPH-NAME             03183116
                   MOVE 'TMNDPTC TABLE WARNING'                         03183216
                                       TO AA-MESSAGE                    03183316
                   MOVE 'FETCH'        TO AA-DB2-OPERATION              03183416
                   PERFORM Z998-SQL-ERROR                               03183516
           END-EVALUATE.                                                03183616
                                                                        03183716
      /*****************************************************************03183816
       Z230-CLOSE-CURSOR-TMNDPTC.                                       03183916
      ******************************************************************03184016
      *                                                                 03184116
           EXEC SQL                                                     03184216
               CLOSE TMNDPTC_CURSOR                                     03184316
           END-EXEC.                                                    03184416
                                                                        03184516
           EVALUATE TRUE                                                03184616
               WHEN SQLCODE < 0                                         03184716
                   MOVE 'Z230-CLOSE-CURSOR-TMNDPTC'                     03184816
                                       TO AA-PARAGRAPH-NAME             03184916
                   MOVE 'TMNDPTC TABLE ERROR'                           03185016
                                       TO AA-MESSAGE                    03185116
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03185216
                   PERFORM Z998-SQL-ERROR                               03185316
               WHEN SQLCODE > 0                                         03185416
               WHEN SQLWARN0 NOT = SPACES                               03185516
                   MOVE 'Z230-CLOSE-CURSOR-TMNDPTC'                     03185616
                                       TO AA-PARAGRAPH-NAME             03185716
                   MOVE 'TMNDPTC TABLE WARNING'                         03185816
                                       TO AA-MESSAGE                    03185916
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION              03186016
                   PERFORM Z998-SQL-ERROR                               03186116
           END-EVALUATE.                                                03186216
                                                                        03186316
      /*****************************************************************03186416
       7100-SELECT-DB2-DATES.                                           03186516
      ******************************************************************03186616
      *                                                                 03186716
           EXEC SQL                                                     03186816
               SELECT  PFM_END_DTE                                      03186916
                      ,OPN_FSCL_MN_DTE                                  03187016
                INTO  :DTATTR-PFM-END-DTE                               03187116
                     ,:MLCNTL-OPN-FSCL-MN-DTE                           03187216
                FROM TDTATTR                                            03187316
                    ,TMLCNTL                                            03187416
                WHERE KY_DTE  = OPN_FSCL_MN_DTE                         03187516
           END-EXEC.                                                    03187616
      *                                                                 03187716
           EVALUATE SQLCODE                                             03187816
               WHEN 0                                                   03187916
                   DISPLAY 'DTATTR-PFM-END-DTE: '                       03188016
                            DTATTR-PFM-END-DTE                          03188116
                   DISPLAY 'MLCNTL-OPN-FSCL-MN-DTE    : ',              03188216
                            MLCNTL-OPN-FSCL-MN-DTE                      03188316
               WHEN OTHER                                               03188416
                  MOVE '7100-SELECT-DB2-DATES'                          03188516
                                       TO AA-PARAGRAPH-NAME             03188616
                   PERFORM Z998-SQL-ERROR                               03188716
           END-EVALUATE.                                                03188816
      *                                                                 03188916
      /*****************************************************************03189016
       Z998-SQL-ERROR.                                                  03189116
      ******************************************************************03189216
      *                                                                 03189316
                                                                        03189416
           CLOSE MNDPTC-EXTR-FILE.                                      03189516
                                                                        03190000
           DISPLAY AA-ABEND-LIT.                                        03200000
           DISPLAY AA-DB2-ERROR-LIT.                                    03210000
           DISPLAY AA-PROGRAM-LIT.                                      03220000
           DISPLAY AA-PARAGRAPH-LIT.                                    03230000
           DISPLAY AA-MESSAGE-LINE.                                     03240000
           DISPLAY AA-DB2-OPERATION-LIT.                                03250000
           SET AC-DB2-ERROR TO TRUE.                                    03260000
           MOVE +4013                  TO ABEND-CODE.                   03270000
                                                                        03280000
           COPY DPPD004.                                                03290000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03300000
                                                                        03310000
