       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    IMKUT021.                                                 
       AUTHOR.        D. THEEL.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/07/2011.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *   IMKUT021 - WEEKLY VALIDATE TAXWARE CODE - WR39471            *        
      ******************************************************************        
      * READ INPUT FILE WHICH HAS BEEN SORTED IN SUB CL MDSEAR ID/     *        
      * TAXWARE CODE SEQ NBR ORDER INCLUDING ONLY UNIQUE VALUES OF THE *        
      * 2. CHECK IF THIS TAXWARE CODE IS VALID IN DEFAULT MAINTENANCE. *        
      * IF NOT, WRITE OUT THE DEPT, MAJOR CLASS, SUBCLASS AND TAXWARE  *        
      * CODE DESCRIPTION FOR INPUT INTO TAXWARE CODE VARIANCE REPORT.  *        
      *                                                                *        
      *    WR/PITS#     DATE        DESCRIPTION                        *00200031
      *    --------   --------     ---------------------------------   *00210031
      ******************************************************************00270024
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT INPUT-FILE              ASSIGN TO INP01.                      
           SELECT OUTPUT-FILE             ASSIGN TO OUT01.                      
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS INPUT-RECORD.                                         
       01  INPUT-RECORD              PIC  X(138).                               
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS INPUT-RECORD.                                         
       01  OUTPUT-RECORD             PIC  X(78).                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************  00830094
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004       *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************   CL*15
      * PROGRAM ACCUMULATORS                                           *   CL*15
      ******************************************************************   CL*15
       01  PA-PROGRAM-ACCUMULATORS.                                        CL*15
           05  PA-INPUT-RECS-READ           PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TDCVSVL-SELECTS           PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TDCVSVL-NOT-FOUND         PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TMDSEAR-SELECTS           PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TMDSEAR-NOT-FOUND         PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TVSCHVL-SELECTS           PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-TVSCHVL-NOT-FOUND         PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
           05  PA-OUTPUT-RECS-WRITTEN       PIC S9(09) COMP-3              CL*15
                                                       VALUE ZERO.         CL*15
                                                                           CL*15
      ******************************************************************   CL*15
      * PROGRAM CONSTANTS                                              *   CL*15
      ******************************************************************   CL*15
       01  PC-PROGRAM-CONSTANTS.                                           CL*15
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE               CL*15
               'IMKUT021'.                                                 CL*15
010700     05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04)  VALUE +2                
010800                                                     COMP SYNC.           
                                                                           CL**1
      ******************************************************************   CL*15
      *  GENERAL WORKING STORAGE VARIABLES                             *        
      ******************************************************************        
       01  WS-SWITCHES.                                                         
           05  PS-END-OF-FILE-SW              PIC  X(01)  VALUE 'N'.            
               88  PS-END-OF-FILE                         VALUE 'Y'.            
                                                                                
      ******************************************************************   CL**1
      * PROGRAM VARIABLES                                              *   CL**1
      ******************************************************************   CL**1
       01  PV-PROGRAM-VARIABLES.                                           CL**1
           05  PV-RETRY-COUNT               PIC S9(04)  VALUE +0                
                                                        COMP SYNC.              
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30)   VALUE SPACE.       CL**1
           05  PV-PARAGRAPH-NAME            PIC X(30)   VALUE SPACE.       CL**1
           05  PV-ABEND-CODE                PIC S9(04)  VALUE +0  COMP.         
           05  PV-OPERATION-ATTEMPTED       PIC X(30)   VALUE SPACES.           
           05  PV-TDCVSVL-COUNT             PIC S9(7)   VALUE ZERO              
                                                        COMP-3.                 
           05  PV-DEPT-NBR                  PIC   X(03).                        
           05  PV-MAJ-CL-NBR                PIC   X(02).                        
           05  PV-SUB-CL-NBR                PIC   X(03).                        
           05  PV-CHARTC-VAL-DESC           PIC   X(20).                        
                                                                                
33885  01  TITLE-LINE-CSV.                                                      
33885      05  FILLER                       PIC X(21)  VALUE ' ,  '.            
33885      05  TITLE-LINE1-CSV       PIC X(35)  VALUE                           
33885         '  TAXWARE CODE VARIANCES REPORT    '.                            
33885      05  FILLER                       PIC X(21)  VALUE ', , '.            
                                                                                
33885  01  HEADING-LINE-CSV.                                                    
33885      05  FILLER                       PIC X(28)  VALUE                    
33885          'DEPT,MAJOR,SUBCLASS,TAX CODE'.                                  
33885      05  FILLER                       PIC X(50)  VALUE SPACES.            
                                                                                
      *****************************************************************         
      *    INPUT FILE TUPC UNLOAD SKU COPYBOOK                                  
      *****************************************************************         
           COPY IMRD051.                                                        
      *****************************************************************         
      *    OUTPUT FILE TAXWARE VARIANCE REPORT COPYBOOK                         
      *****************************************************************         
           COPY IMRD029.                                                        
      *****************************************************************         
      *  DCLGEN FOR THE TDCVSVL TABLE                                 *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TDCVSVL                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN FOR THE TMDSEAR TABLE                                 *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DCLGEN FOR THE TVSCHVL TABLE                                 *         
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
               INCLUDE TVSCHVL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL COMMUNICATIONS WORK AREA                                  *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      *  MAIN MODULE PROCESSING                                        *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-RECS                                            
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                    *   CL**6
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATON' TO PV-PARAGRAPH-NAME.              03350020
                                                                                
           OPEN INPUT INPUT-FILE.                                               
           OPEN OUTPUT OUTPUT-FILE.                                             
           INITIALIZE IM029-TAX-VARIANCE-REC.                                   
           PERFORM 4000-READ-RECORD.                                    03240094
           WRITE OUTPUT-RECORD FROM TITLE-LINE-CSV.                             
           WRITE OUTPUT-RECORD FROM HEADING-LINE-CSV.                           
                                                                                
      ******************************************************************        
      * 2000-PROCESS-RECS.                                             *        
      * CHECK IF THE INPUT REC EXISTS ON TDCVSVL                       *        
      * IF IT DOES, READ NEXT INPUT REC                                *        
      * IF IT DOES NOT, GET DEPT MCL AND SCL FROM TMDSEAR AND TAXWARE  *        
      *   CODE DESCRIPTION FROM TVSCHVL AND WRITE OUT THE RECORD.      *        
      ******************************************************************        
       2000-PROCESS-RECS.                                                       
                                                                                
           MOVE '2000-PROCESS-RECS' TO PV-PARAGRAPH-NAME.               03350020
           IF IM051-TX-PRD-CDE-SEQ-NBR > ZERO                                   
               PERFORM 3000-CK-DFM                                              
               IF SQLCODE = 0                                                   
                   CONTINUE                                                     
               ELSE                                                             
                   PERFORM 3200-GET-MBS-INFO                                    
                   PERFORM 3400-GET-TAXWARE-DESC                                
                   PERFORM 5000-WRITE-RECORD                                    
               END-IF                                                           
           END-IF.                                                              
           PERFORM 4000-READ-RECORD.                                            
      ******************************************************************        
      * 3000-CK-DFM.                                                   *        
      *    SEE IF THIS SUBCLASS DKEY / TAXWARE CODE COMBINATION        *        
      *      EXISTS IN DFM (TDCVSVL).                                  *        
      ******************************************************************        
       3000-CK-DFM.                                                             
           MOVE '3000-CK-DFM' TO PV-PARAGRAPH-NAME.                     03350020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE IM051-SUB-CL-MDSEAR-ID TO DCVSVL-MDSEAR-ID.                     
           MOVE IM051-TX-PRD-CDE-SEQ-NBR TO DCVSVL-CHARTC-SEQ-NBR.              
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                  SELECT 1                                                      
                    INTO :PV-TDCVSVL-COUNT                                      
                    FROM TDCVSVL                                                
                    WHERE MDSEAR_ID = :DCVSVL-MDSEAR-ID                         
                      AND CHARTC_CDE = 14                                       
                      AND CHARTC_SEQ_NBR = :DCVSVL-CHARTC-SEQ-NBR               
                   FETCH FIRST 1 ROWS ONLY                                      
                   WITH UR                                                      
               END-EXEC                                                         
               EVALUATE SQLCODE                                             0478
                   WHEN 0                                                   0479
                       ADD +1 TO PA-TDCVSVL-SELECTS                        CL*15
                   WHEN +100                                                0479
                       ADD +1 TO PA-TDCVSVL-NOT-FOUND                   01200037
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '3000-CK-DFM'                                   0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'SELECT TDCVSVL'                               CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '3000-CK-DFM'                                           0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'SELECT TDCVSVL'                                       CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3200-GET-MBS-INFO.                                             *        
      *    USING THE MDSEAR-ID FROM DFM, GET THE DEPT/MCL/SCL.         *        
      ******************************************************************        
       3200-GET-MBS-INFO.                                                       
           MOVE '3200-GET-MBS-INFO' TO PV-PARAGRAPH-NAME.               03350020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE IM051-SUB-CL-MDSEAR-ID TO MDSEAR-DKEY.                          
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                  SELECT DEPT_NBR                                               
                        ,MAJ_CL_NBR                                             
                        ,SUB_CL_NBR                                             
                    INTO :PV-DEPT-NBR                                           
                        ,:PV-MAJ-CL-NBR                                         
                        ,:PV-SUB-CL-NBR                                         
                    FROM TMDSEAR                                                
                    WHERE MDSEAR_DKEY = :MDSEAR-DKEY                            
                    WITH UR                                                     
               END-EXEC                                                         
               EVALUATE SQLCODE                                             0478
                   WHEN 0                                                   0479
                       ADD +1 TO PA-TMDSEAR-SELECTS                        CL*15
                   WHEN +100                                                0479
                       ADD +1 TO PA-TMDSEAR-NOT-FOUND                   01200037
                       MOVE SPACES TO PV-DEPT-NBR                               
                                      PV-MAJ-CL-NBR                             
                                      PV-SUB-CL-NBR                             
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '3200-GET-MBS-INFO'                             0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'SELECT TMDSEAR'                               CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '3200-GET-MBS-INFO'                                     0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'SELECT TMDSEAR'                                       CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      * 3400-GET-TAXWARE-DESC.                                         *        
      * GET THE TAXWARE CODE DESC FROM TVSCHVL BASED ON INPUT SEQ NBR  *        
      ******************************************************************        
       3400-GET-TAXWARE-DESC.                                                   
           MOVE '3400-GET-TAXWARE-DESC' TO PV-PARAGRAPH-NAME.           03350020
           INITIALIZE PV-RETRY-COUNT.                                           
                                                                                
           MOVE IM051-TX-PRD-CDE-SEQ-NBR TO VSCHVL-CHARTC-SEQ-NBR.              
                                                                                
051000     PERFORM  WITH TEST AFTER                                             
051100             UNTIL SQLCODE = +0                                           
                      OR SQLCODE = +100                                         
051200                OR PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                
               EXEC SQL                                                         
                  SELECT CHARTC_VAL_DESC                                        
                    INTO :PV-CHARTC-VAL-DESC                                    
                    FROM TVSCHVL                                                
                    WHERE CHARTC_CDE  = 14                                      
                      AND CHARTC_SEQ_NBR = :VSCHVL-CHARTC-SEQ-NBR               
                  WITH UR                                                       
               END-EXEC                                                         
               EVALUATE SQLCODE                                             0478
                   WHEN 0                                                   0479
                       ADD +1 TO PA-TVSCHVL-SELECTS                        CL*15
                   WHEN +100                                                0479
                       ADD +1 TO PA-TVSCHVL-NOT-FOUND                   01200037
                       MOVE SPACES TO PV-CHARTC-VAL-DESC                        
                   WHEN -911                                                0481
                   WHEN -913                                                0481
                       ADD 1               TO PV-RETRY-COUNT                0484
                   WHEN OTHER                                               0491
                       MOVE '3400-GET-TAXWARE-DESC'                         0492
                                          TO PV-PARAGRAPH-NAME              0492
045800                 MOVE 'SELECT TVSCHVL'                               CL*11
045900                                 TO PV-OPERATION-ATTEMPTED           CL**1
                       PERFORM 9999-SQL-ABEND                               0494
               END-EVALUATE                                                 0495
           END-PERFORM.                                                         
046400     IF PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT                           
               MOVE '3400-GET-TAXWARE-DESC'                                 0492
046600                                 TO PV-PARAGRAPH-NAME                CL**1
045800         MOVE 'SELECT TVSCHVL'                                       CL*11
046800                                 TO PV-OPERATION-ATTEMPTED           CL**1
               PERFORM 9999-SQL-ABEND                                       0494
047000     END-IF.                                                              
                                                                                
      ******************************************************************        
      * 4000-READ-RECORD.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - READS THE NEXT RECORD IN THE INPUT FILE.                  *        
      ******************************************************************        
       4000-READ-RECORD.                                                        
           MOVE '4000-READ-RECORD' TO PV-PARAGRAPH-NAME.                        
                                                                                
           READ INPUT-FILE                                                      
               INTO IM051-ITEM-TUPC-UNLD-REC                                    
                   AT END                                                       
                       MOVE 'Y'     TO PS-END-OF-FILE-SW                        
                   NOT AT END                                                   
                       ADD +1                  TO PA-INPUT-RECS-READ            
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 5000-WRITE-RECORD.                                             *        
      *  ==> PROCESSES:                                                *        
      *    - WRITE THE RECORD TO THE OUTPUT FILE.                      *        
      ******************************************************************        
       5000-WRITE-RECORD.                                                       
           MOVE '5000-WRITE-RECORD' TO PV-PARAGRAPH-NAME.                       
           MOVE PV-DEPT-NBR        TO IM029-DEPT.                               
           MOVE PV-MAJ-CL-NBR      TO IM029-MAJOR-CLASS.                        
           MOVE PV-SUB-CL-NBR      TO IM029-SUBCLASS.                           
           MOVE PV-CHARTC-VAL-DESC TO IM029-TAXWARE-DESC.                       
030600     WRITE OUTPUT-RECORD FROM IM029-TAX-VARIANCE-REC.                     
           ADD +1 TO PA-OUTPUT-RECS-WRITTEN.                               CL*15
030700                                                                          
                                                                           CL**1
      ******************************************************************   CL**1
      * PERFORM WRAP UP PROCESSING                                     *   CL*12
      ******************************************************************   CL**1
       9000-EOJ-ROUTINE.                                                   CL**1
           MOVE '9000-EOJ-ROUTINE' TO PV-PARAGRAPH-NAME.                        
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
           PERFORM 9005-DISPLAY-TOTALS.                                    CL*16
                                                                           CL*16
      ******************************************************************   CL*16
      *  DISPLAYS PROGRAM TOTALS                                       *   CL*16
      ******************************************************************   CL*16
       9005-DISPLAY-TOTALS.                                                CL*16
           MOVE '9005-DISPLAY-TOTALS' TO PV-PARAGRAPH-NAME.                     
                                                                           CL*16
           DISPLAY '** IMKUT021 TOTALS ********************'.              CL*16
           DISPLAY 'INPUT RECS READ   ' PA-INPUT-RECS-READ.             01200037
           DISPLAY 'TDCVSVL SELECTS   ' PA-TDCVSVL-SELECTS.             01200037
           DISPLAY 'TDCVSVL NOT FOUND ' PA-TDCVSVL-NOT-FOUND.           01200037
           DISPLAY 'TMDSEAR SELECTS   ' PA-TMDSEAR-SELECTS.             01200037
           DISPLAY 'TMDSEAR NOT FOUND ' PA-TMDSEAR-NOT-FOUND.           01200037
           DISPLAY 'TVSCHVL SELECTS   ' PA-TVSCHVL-SELECTS.             01200037
           DISPLAY 'TVSCHVL NOT FOUND ' PA-TVSCHVL-NOT-FOUND.           01200037
           DISPLAY 'OUTPUT RECS       ' PA-OUTPUT-RECS-WRITTEN.         01200037
           DISPLAY '** END OF IMKUT021 TOTALS *************'.              CL*16
                                                                           CL*16
       9999-SQL-ABEND.                                                          
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
           DISPLAY '** IMKUT021 TOTALS ********************'.              CL*16
           DISPLAY 'INPUT RECS READ   ' PA-INPUT-RECS-READ.             01200037
           DISPLAY 'TDCVSVL SELECTS   ' PA-TDCVSVL-SELECTS.             01200037
           DISPLAY 'TDCVSVL NOT FOUND ' PA-TDCVSVL-NOT-FOUND.           01200037
           DISPLAY 'TMDSEAR SELECTS   ' PA-TMDSEAR-SELECTS.             01200037
           DISPLAY 'TMDSEAR NOT FOUND ' PA-TMDSEAR-NOT-FOUND.           01200037
           DISPLAY 'TVSCHVL SELECTS   ' PA-TVSCHVL-SELECTS.             01200037
           DISPLAY 'TVSCHVL NOT FOUND ' PA-TVSCHVL-NOT-FOUND.           01200037
           DISPLAY 'OUTPUT RECS       ' PA-OUTPUT-RECS-WRITTEN.         01200037
           DISPLAY '** END OF IMKUT021 TOTALS *************'.              CL*16
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
                                                                                
