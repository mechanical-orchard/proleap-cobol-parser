000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    CNKCS213.                                                 
000300 AUTHOR.        DENNIS STEFFEN.                                           
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  APRIL 1998.                                               
000600 DATE-COMPILED.                                                           
000700                                                                          
000800*---------------------------------------------------------------          
000900*         - STORE ZONE / DEPARTMENT LIST                                  
001000*                                                                         
001100*    THIS PROGRAM DISPLAYS THE DEPARTMENT NUMBER AND DESCRIPTION          
001200*    FOR A STORE ZONE SELECTED ON CNKCS212.                               
001300*                                                                         
000800*---------------------------------------------------------------          
263455* CHANGED 11/02/2021    BY JIM HUNTER      WR: CHG0263455                 
263455* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263455* MAKING THE CALL DYNAMIC VS STATIC.                                      
263455*----------------------------------------------------------------*        
CICS  * CHANGED 10/25/2022    BY JIM HUNTER      WR: CHG0276961                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001400                                                                          
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 DATA DIVISION.                                                           
001800                                                                          
001900 WORKING-STORAGE SECTION.                                                 
002000                                                                          
002100 01  PC-PROGRAM-CONSTANTS.                                                
002200     05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE                 
002300                                                    'CN213A  '.           
002400     05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE                 
002500                                                    'CNKM213 '.           
002600     05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE                 
002700                                                    'CNKCS213'.           
002800     05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.              
002900     05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.              
003000     05  PC-DTL-QUEUE-SEQ               PIC  9(01)  VALUE 3.              
003100     05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.            
003200     05  PC-LIST-FORMAT                 PIC  X(01)  VALUE '2'.            
003300     05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12             
003400                                                    COMP SYNC.            
003500     05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2              
003600                                                    COMP SYNC.            
003700     05  PC-MAX-ITEMS                   PIC S9(09)  VALUE                 
003800                                                    +1000000              
003900                                                    COMP-3.               
004000     05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5              
004100                                                    COMP SYNC.            
004200     05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
004300                                                     COMP-3.              
004400                                                                          
004500 01  PC-TSYMSG-NUMBERS.                                                   
004600     05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.          
004700     05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.          
004800     05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
004900     05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
005000     05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.          
005100     05  PC-TSYMSG-00148                PIC 9(5)    VALUE 00148.          
005200     05  PC-TSYMSG-00279                PIC 9(5)    VALUE 00279.          
005300     05  PC-TSYMSG-00302                PIC 9(5)    VALUE 00302.          
005400     05  PC-TSYMSG-00551                PIC 9(5)    VALUE 00551.          
005500                                                                          
005600 01  PS-PROGRAM-SUBSCRIPTS.                                               
005700     05  PV-SUB                         PIC S9(04)   VALUE +0             
005800                                                     COMP SYNC.           
005900                                                                          
006000 01  PS-PROGRAM-SWITCHES.                                                 
006100     05  PS-ERROR-SW                    PIC X(01)    VALUE 'Y'.           
006200         88  PS-NO-ERROR                             VALUE 'Y'.           
006300         88  PS-ERROR                                VALUE 'N'.           
006400     05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
006500         88  PS-COMMAREA-VALID                       VALUE 'Y'.           
006600         88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
006700     05  PS-LIST-END-SW                 PIC X(01)    VALUE 'N'.           
006800         88  PS-LIST-END                             VALUE 'Y'.           
006900         88  PS-NO-LIST-END                          VALUE 'N'.           
007000     EJECT                                                                
007100 01  PV-PROGRAM-VARIABLES.                                                
007200     05  PV-REMAINDER                   PIC S9(04)   VALUE +0             
007300                                                     COMP SYNC.           
007400     05  PV-READ-COUNT                  PIC S9(09)   VALUE +0             
007500                                                     COMP-3.              
007600     05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0             
007700                                                     COMP-3.              
007800     05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0             
007900                                                     COMP-3.              
008000     05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0             
008100                                                     COMP SYNC.           
008200     05  PV-CICS-RESP                   PIC S9(04)   VALUE +0             
008300                                                     COMP SYNC.           
008400     05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0             
008500                                                     COMP SYNC.           
008600     05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0             
008700                                                     COMP SYNC.           
008800     05  PV-SQL-SUB                     PIC S9(4)    COMP.                
008900                                                                          
009000 01  PV-DB2-CRITERIA-AREA.                                                
009100     15  PV-DB2-MESSAGE-NUMBER          PIC S9(05)  VALUE ZERO            
009200                                                    COMP-3.               
009300                                                                          
009400 EJECT                                                                    
009500*---------------------------------------------------------------*         
CICS  *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.          *         
009700*---------------------------------------------------------------*         
009800                                                                          
009900     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
010000 EJECT                                                                    
010100*---------------------------------------------------------------*         
010200*    STANDARD COMMAREA.                                         *         
010300*---------------------------------------------------------------*         
010400                                                                          
010500     COPY DPWS020.                                                        
010600     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
010700                                                                          
010800*---------------------------------------------------------------*         
010900*    INTER-APPLICATION COMMAREA.                                *         
011000*---------------------------------------------------------------*         
011100                                                                          
011200         10  INTER-APPL-COMM-AREA       PIC X(200).                       
011300     COPY CNWS001.                                                        
011400     COPY CNWS003.                                                        
011500*---------------------------------------------------------------*         
011600*    SPECIFIC COMMMAREA FOR CNKCS213.                           *         
011700*---------------------------------------------------------------*         
011800         10  ASC-SPECIFIC-COMMAREA.                                       
011900             15  ASC-LIST-KEYS.                                           
012000                 20  ASC-KEY-ZONE-ID.                                     
012100                     25 ASC-KEY-ZONE-ID-N            PIC  9(05).          
012200             15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                        
012201                 20  ASC-BLOCK-MODIFIED-SW                                
012202                                        PIC  X(01).                       
012203                     88  ASC-BLOCK-NOT-MODIFIED    VALUE 'N'.             
012204                     88  ASC-BLOCK-MODIFIED        VALUE 'Y'.             
012210                                                                          
012300                 20  ASC-ITEM-ARRAY.                                      
012400                                                                          
012500                     25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.                
012600                         30  ASC-LIST-ITEM-NUMBER    PIC S9(09)      CL*11
012700                                                     COMP-3.         CL*11
012800                         30  ASC-DEPT                PIC 9(03).           
012900                         30  ASC-DEPT-DESC           PIC X(25).           
013000                                                                          
013100             15  ASC-BLK-SUB                         PIC S9(04)           
013200                                                         COMP             
013300                                                         SYNC.            
013400             15  ASC-ITEM-SUB                        PIC S9(04)           
013500                                                         COMP             
013600                                                         SYNC.            
013700             15  ASC-SEL-SUB                         PIC S9(04)           
013800                                                         COMP.            
013900             15  ASC-SEL-ITEM                        PIC S9(04)           
014000                                                         COMP.            
014100             15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)           
014200                                                         COMP-3.          
014300             15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)           
014400                                                         COMP-3.          
014500             15  ASC-NUMBER-OF-TS-ITEMS              PIC S9(05)           
014600                                                         COMP-3.          
014700             15  ASC-ITEMS-DISPLAYED                 PIC S9(03)           
014800                                                         COMP-3.          
014900             15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)           
015000                                                         COMP-3.          
015100             15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)           
015200                                                         COMP-3.          
015300             15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)           
015400                                                         COMP             
015500                                                         SYNC.            
015600             15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)           
015700                                                         COMP-3.          
015800             15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)           
015900                                                         COMP-3.          
016000             15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).          
016100                 88  ASC-ITEMS-LEFT-ON-DB              VALUE 'Y'.         
016200                 88  ASC-NO-ITEMS-LEFT-ON-DB           VALUE 'N'.         
016300             15  ASC-USE-SELECTION-QUEUE-IND         PIC  X(01).          
016400                 88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'.         
016500             15  ASC-DESC-LINE-IND                   PIC  X(01).          
016600                 88  ASC-DESC-LINE-DONE                VALUE 'Y'.         
016700             15  ASC-CORR-LINE-IND                   PIC  X(01).          
016800                 88  ASC-CORR-LINE-DONE                VALUE 'Y'.         
016900             15  ASC-LIST-QUEUE-NAME.                                     
017000                 20  ASC-LIST-TERM-ID                PIC  X(04).          
017100                 20  ASC-LIST-TASK-NUMBER            PIC S9(05)           
017200                                                         COMP-3.          
017300                 20  ASC-LIST-SEQ                    PIC  9(01).          
017400             15  ASC-SEL-QUEUE-NAME.                                      
017500                 20  ASC-SEL-TERM-ID                 PIC  X(04).          
017600                 20  ASC-SEL-TASK-NUMBER             PIC S9(05)           
017700                                                          COMP-3.         
017800                 20  ASC-SEL-SEQ                     PIC  9(01).          
017900             15  ASC-DTL-QUEUE-NAME.                                      
018000                 20  ASC-DTL-TERM-ID                 PIC  X(04).          
018100                 20  ASC-DTL-TASK-NUMBER             PIC S9(05)           
018200                                                         COMP-3.          
018300                 20  ASC-DTL-SEQ                     PIC  9(01).          
018400                                                                          
018500             15  ASC-START-ITEM-REQUEST.                                  
018600                 20  ASC-START-ITEM-REQUEST-N        PIC  9(05).          
018700             15  ASC-STORE-ZONE-DESC                 PIC  X(20).          
018800                                                                          
018900     EJECT                                                                
019000*---------------------------------------------------------------*         
019100*    MESSAGE SELECTION TSQ B QUEUE RECORD LAYOUT.               *         
019200*---------------------------------------------------------------*         
019300                                                                          
019400     COPY CNWS007.                                                        
019500                                                                          
019600     EJECT                                                                
019700*---------------------------------------------------------------*         
019800*    ABEND PROCESSING COPYBOOK.                                 *         
019900*---------------------------------------------------------------*         
020000                                                                          
020100     COPY DPWS013.                                                        
020200                                                                          
020300     EJECT                                                                
020400*---------------------------------------------------------------*         
020500*    ATTRIBUTE SETTINGS COPYBOOK.                               *         
020600*---------------------------------------------------------------*         
020700                                                                          
020800     COPY DPWS015.                                                        
020900                                                                          
021000*---------------------------------------------------------------*         
021100*    FUNCTION KEYS COPYBOOK.                                    *         
021200*---------------------------------------------------------------*         
021300                                                                          
021400     COPY DPWS016.                                                        
021500                                                                          
021600*---------------------------------------------------------------*         
021700*    NUMERIC EDIT LAYOUT.                                       *         
021800*---------------------------------------------------------------*         
021900                                                                          
022000     COPY DPWS010I.                                                       
022100                                                                          
022200*---------------------------------------------------------------*         
022300*    MAP LAYOUT                                                 *         
022400*---------------------------------------------------------------*         
022500                                                                          
022600     COPY CNKM213.                                                        
022700                                                                          
022800 01  MR-OCCURS-LAYOUT          REDEFINES  CN213AO.                        
022900     05  FILLER                PIC X(077).                                
023000     05  MR-SCROLL-AREA OCCURS 12 TIMES.                                  
023100         10  MR-DEPTL          PIC S9(04) COMP.                           
023200         10  MR-DEPTA          PIC  X(01).                                
023300         10  MR-DEPTC          PIC  X(01).                                
023400         10  MR-DEPTH          PIC  X(01).                                
023500         10  MR-DEPT           PIC  X(03).                                
023600                                                                          
023700         10  MR-DEPT-DESCL     PIC S9(04) COMP.                           
023800         10  MR-DEPT-DESCA     PIC  X(01).                                
023900         10  MR-DEPT-DESCC     PIC  X(01).                                
024000         10  MR-DEPT-DESCH     PIC  X(01).                                
024100         10  MR-DEPT-DESC      PIC  X(25).                                
024200                                                                          
024300     05  FILLER                PIC X(12).                                 
024400/                                                                         
024500     EXEC SQL                                                             
024600          INCLUDE TMDSEDP                                                 
024700     END-EXEC.                                                            
024800/                                                                         
024900     EXEC SQL                                                             
025000          INCLUDE TMDSEAR                                                 
025100     END-EXEC.                                                            
025200/                                                                         
025300     EXEC SQL                                                             
025400          INCLUDE TSTRZN                                                  
025500     END-EXEC.                                                            
025600/                                                                         
025700     EXEC SQL                                                             
025800          INCLUDE SQLCA                                                   
025900     END-EXEC.                                                            
026000                                                                          
026100     EXEC SQL                                                             
026200     DECLARE DEPT-CSR CURSOR FOR                                          
026300         SELECT                                                           
026400             B.DEPT_NBR                                                   
026500           , B.MDSEAR_DESC                                                
026600         FROM                                                             
026700             TMDSEDP A                                                    
026800           , TMDSEAR B                                                    
026900         WHERE                                                            
027000              B.MDSELV_CDE    = 'DPT'                                     
027100         AND B.OPERCO_NBR     = '03'                                      
027200         AND (B.STRT_DTE     <=  CURRENT DATE AND                         
027300                B.END_DTE     >  CURRENT DATE)                            
027400         AND B.MDSEAR_DKEY    =  A.MDSEAR_DKEY                            
027500         AND A.STR_ZN_ID      =  :STRZN-STR-ZN-ID                         
027600         ORDER BY B.DEPT_NBR                                              
027700     END-EXEC.                                                            
027800/                                                                         
027900 LINKAGE SECTION.                                                         
028000                                                                          
028100 01  DFHCOMMAREA.                                                         
028200     05  FILLER                         OCCURS  1 TO 4072 TIMES           
028300                                        DEPENDING ON EIBCALEN.            
028400         10  FILLER                     PIC X.                            
028500                                                                          
028600 EJECT                                                                    
028700 PROCEDURE DIVISION.                                                      
028800                                                                          
028900 0000-MAIN-MODULE.                                                        
029000                                                                          
029100     INITIALIZE DP030-CICS-API-FIELDS.                                    
029200     MOVE +1                      TO DP030-NUMBER-OF-MAPS.                
029300     MOVE PC-CURRENT-MAPSET-NAME  TO DP030-MAPSET-NAME.                   
029400     MOVE PC-CURRENT-MAP-NAME     TO DP030-MAP-NAME (1).                  
029500     SET DP030-RECEIVE-APPL-MAP   TO TRUE.                                
029600     MOVE LENGTH OF CN213AI       TO DP030-MAP-LENGTH (1).                
029700     MOVE 'PROGRAM ENTRY CALL'    TO DP013-MESSAGE-TEXT (1).              
029800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
029900                                                                          
030000     PERFORM 1000-CONTROL-PROCESSING.                                     
030100                                                                          
030200     MOVE 'PROGRAM EXIT CALL'     TO DP013-MESSAGE-TEXT (1).              
030300     PERFORM 0001-CALL-CICS-ARCH-API.                                     
030400                                                                          
030500*----------------------------------------------------------------*        
030600*   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *        
030700*   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *        
030800*----------------------------------------------------------------*        
030900                                                                          
031000 0001-CALL-CICS-ARCH-API.                                                 
031100                                                                          
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
031300                                    DFHCOMMAREA                           
031400                                    DP030-CICS-API-FIELDS                 
031500                                    DP020-STANDARD-COMMAREA               
031600                                    CN213AI.                              
031700                                                                          
031800     IF  DP030-RC-CALL-SUCCESSFUL                                         
031900         CONTINUE                                                         
032000     ELSE                                                                 
032100         SET DP013-NO-ROLLBACK TO TRUE                                    
032200         SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
032300         SET DP013-CICS-ABEND TO TRUE                                     
032400         MOVE 'BEFORE 0000-MAIN-MODULE' TO DP013-PARAGRAPH                
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
032700         MOVE DP030-RETURN-CODE                                           
032800                               TO DP013-MESSAGE-TEXT (3)                  
032900         PERFORM DP013-0000-PROCESS-ABEND                                 
033000     END-IF.                                                              
033100                                                                          
033200*----------------------------------------------------------------*        
033300* PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *        
033400* COURSE OF ACTION IS FOR THIS TRANSACTION.                      *        
033500*                                                                *        
033600* NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *        
033700*  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *        
033800*  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *        
033900*  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *        
034000*----------------------------------------------------------------*        
034100                                                                          
034200 1000-CONTROL-PROCESSING.                                                 
034300                                                                          
034400     EVALUATE TRUE                                                        
034500         WHEN DP020-NEXT-ACT-INITIAL                                      
034600             MOVE EIBTRMID          TO ASC-DTL-TERM-ID                    
034700             MOVE DP020-SRC-TASK-NUMBER                                   
034800                                    TO ASC-DTL-TASK-NUMBER                
034900             MOVE PC-DTL-QUEUE-SEQ  TO ASC-DTL-SEQ                        
035000             PERFORM 1100-PROCESS-INTER-APPL-COMM                         
035100***-----$TEMPLATE NOTE$-------------------------------------------        
035200*** IF THE DATA EXPECTED IS NOT RECEIVED FROM CALLING APPLICATION,        
035300*** SETUP APPROPRIATE MESSAGE AND SEVERITY AND SET APPL-ERROR TO          
035400*** TRUE, THIS WILL RETURN TO CALLING APPLICATION WHEN API CALLED.        
035500***---------------------------------------------------------------        
035600             IF  PS-COMMAREA-NOT-VALID                                    
035700***              MOVE PC-TSYMSG-99901                                     
035800***                            TO  DP020-MSG-NUMBER                       
035900***              SET DP020-MSG-FATAL                                      
036000***                            TO  TRUE                                   
036100                 SET DP020-NEXT-ACT-APPL-ERROR                            
036200                               TO  TRUE                                   
036300             ELSE                                                         
036400                 PERFORM 2050-DELETE-DETAIL-QUEUE                         
036500                 PERFORM 4000-BUILD-INITIAL-PANEL                         
036600             END-IF                                                       
036700                                                                          
036800         WHEN DP020-NEXT-ACT-READ-MAP                                     
036900             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
037000                               TO PV-TOP-ITEM                             
037100             PERFORM 2000-PROCESS-PANEL                                   
037200                                                                          
037300         WHEN DP020-NEXT-ACT-RETURN                                       
037400             MOVE ASC-ITEM-AT-TOP-OF-PANEL                                
037500                               TO PV-TOP-ITEM                             
037600             MOVE ZERO         TO ASC-ITEM-AT-TOP-OF-PANEL                
037700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
037800                                                                          
037900         WHEN OTHER                                                       
038000             SET DP013-LOGIC-ABEND TO TRUE                                
038100             SET DP013-NO-ROLLBACK TO TRUE                                
038200             MOVE '1000-CONTROL-PROCESSING'                               
038300                               TO  DP013-PARAGRAPH                        
038400             MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'            
038500                               TO  DP013-MESSAGE-TEXT(1)                  
038600             PERFORM DP013-0000-PROCESS-ABEND                             
038700     END-EVALUATE.                                                        
038800                                                                          
038900 EJECT                                                                    
039000*----------------------------------------------------------------*        
039100* DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *        
039200* IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *        
039300*----------------------------------------------------------------*        
039400                                                                          
039500 1100-PROCESS-INTER-APPL-COMM.                                            
039600                                                                          
039700     SET PS-COMMAREA-VALID     TO TRUE.                                   
039800     INITIALIZE ASC-SPECIFIC-COMMAREA.                                    
039900     MOVE SPACE                TO ASC-START-ITEM-REQUEST.                 
040000     SET ASC-USE-SELECTION-QUEUE TO TRUE                                  
040100     MOVE CN003-NUMBER-OF-TS-ITEMS                                        
040200                               TO ASC-NUMBER-OF-TS-ITEMS                  
040300     MOVE CN003-SEL-QUEUE-NAME                                            
040400                               TO ASC-SEL-QUEUE-NAME                      
040500     PERFORM 1120-PREPARE-LIST-ENTRY.                                     
040600                                                                          
040700*----------------------------------------------------------------*        
040800*    GET READY TO START AT THE FIRST ITEM ON THE LIST.           *        
040900*----------------------------------------------------------------*        
041000                                                                          
041100 1120-PREPARE-LIST-ENTRY.                                                 
041200                                                                          
041300     MOVE CN007-MAX-ITEMS TO ASC-SEL-SUB.                                 
041400     MOVE ZERO            TO ASC-SEL-ITEM.                                
041500     PERFORM 2140-GET-NEXT-MESSAGE.                                       
041600 EJECT                                                                    
041700*----------------------------------------------------------------*        
041800*    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
041900*    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
042000*----------------------------------------------------------------*        
042100                                                                          
042200 2000-PROCESS-PANEL.                                                      
042300                                                                          
042400     EVALUATE TRUE                                                        
042500         WHEN DP020-SRC-AID = DP016-CLEAR                                 
042600             MOVE ZERO TO ASC-ITEM-AT-TOP-OF-PANEL                        
042700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
042800             PERFORM 3200-RESET-ATTRIBUTES                                
042900                                                                          
043000         WHEN DP020-FK-REFRESH (DP020-SRC-AID)                            
043100             PERFORM 2050-DELETE-DETAIL-QUEUE                             
043200             PERFORM 4000-BUILD-INITIAL-PANEL                             
043300             PERFORM 3200-RESET-ATTRIBUTES                                
043400                                                                          
043500         WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)                     
043600             IF  ASC-USE-SELECTION-QUEUE                                  
043700                 PERFORM 2140-GET-NEXT-MESSAGE                            
043800                 IF PS-LIST-END                                           
043900                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
044000                     SET PS-NO-LIST-END TO TRUE                           
044100                 ELSE                                                     
044200                     PERFORM 2050-DELETE-DETAIL-QUEUE                     
044300                     PERFORM 4000-BUILD-INITIAL-PANEL                     
044400                 END-IF                                                   
044500                 PERFORM 3200-RESET-ATTRIBUTES                            
044600             ELSE                                                         
044700                 MOVE PC-TSYMSG-00070                                     
044800                                  TO DP020-MSG-NUMBER                     
044900                 SET DP020-MSG-INFORMATIONAL TO TRUE                      
045000             END-IF                                                       
045100                                                                          
045200         WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)                     
045300             IF  ASC-USE-SELECTION-QUEUE                                  
045400                 PERFORM 2150-GET-PREV-MESSAGE                            
045500                 IF PS-LIST-END                                           
045600                     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 
045700                     SET PS-NO-LIST-END TO TRUE                           
045800                 ELSE                                                     
045900                     PERFORM 2050-DELETE-DETAIL-QUEUE                     
046000                     PERFORM 4000-BUILD-INITIAL-PANEL                     
046100                 END-IF                                                   
046200                 PERFORM 3200-RESET-ATTRIBUTES                            
046300             ELSE                                                         
046400                 MOVE PC-TSYMSG-00069                                     
046500                                  TO DP020-MSG-NUMBER                     
046600                 SET DP020-MSG-INFORMATIONAL TO TRUE                      
046700             END-IF                                                       
046800                                                                          
046900         WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
047000             MOVE +1 TO PV-TOP-ITEM                                       
047100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
047200             PERFORM 3200-RESET-ATTRIBUTES                                
047300                                                                          
047400         WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
047500             PERFORM 2110-BROWSE-BACKWARD                                 
047600             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
047700             PERFORM 3200-RESET-ATTRIBUTES                                
047800                                                                          
047900         WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
048000             PERFORM 2120-BROWSE-FORWARD                                  
048100             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
048200             PERFORM 3200-RESET-ATTRIBUTES                                
048300                                                                          
048400         WHEN DP020-SRC-AID = DP016-ENTER                                 
048500             PERFORM 2130-CHECK-FOR-JUMP-BROWSE                           
048600             IF  PS-ERROR                                                 
048700                 CONTINUE                                                 
048800             ELSE                                                         
048900                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
049000                 PERFORM 3200-RESET-ATTRIBUTES                            
049100             END-IF                                                       
049200                                                                          
049300         WHEN OTHER                                                       
049400             SET DP013-NO-ROLLBACK TO TRUE                                
049500             SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
049600             SET DP013-CICS-ABEND TO TRUE                                 
049700             MOVE '2100-CHECK-FUNCTION-KEY' TO DP013-PARAGRAPH            
049800             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
049900                                   TO DP013-MESSAGE-TEXT (1)              
050000             PERFORM DP013-0000-PROCESS-ABEND                             
050100                                                                          
050200     END-EVALUATE.                                                        
050300 EJECT                                                                    
050400*----------------------------------------------------------------*        
050500*    DELETE THE TEMPORARY STORAGE QUEUE.                         *        
050600*----------------------------------------------------------------*        
050700                                                                          
050800 2050-DELETE-DETAIL-QUEUE.                                                
050900                                                                          
051000     EXEC CICS                                                            
051100         DELETEQ TS                                                       
051200             QUEUE (ASC-DTL-QUEUE-NAME)                                   
051300             RESP  (PV-CICS-RESP)                                         
051400     END-EXEC.                                                            
051500                                                                          
051600     IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                               
051700        (PV-CICS-RESP = DFHRESP(QIDERR))                                  
051800         CONTINUE                                                         
051900     ELSE                                                                 
052000         SET DP013-CICS-ABEND TO TRUE                                     
052100         SET DP013-NO-ROLLBACK TO TRUE                                    
052200                                                                          
052300         MOVE '2050-DELETE-DETAIL-QUEUE' TO DP013-PARAGRAPH               
052400         MOVE 'ERROR TRYING TO DELETE DETAIL TEMP STORAGE QUEUE'          
052500                        TO DP013-MESSAGE-TEXT (1)                         
052600         PERFORM DP013-0000-PROCESS-ABEND                                 
052700     END-IF.                                                              
052800                                                                          
052900     MOVE +0 TO ASC-HIGHEST-BLOCK-WRITTEN.                                
053000 EJECT                                                                    
053100*----------------------------------------------------------------*        
053200*    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *        
053300*    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *        
053400*    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *        
053500*    MESSAGE.                                                    *        
053600*----------------------------------------------------------------*        
053700                                                                          
053800 2110-BROWSE-BACKWARD.                                                    
053900                                                                          
054000     SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL            
054100         GIVING PV-TOP-ITEM.                                              
054200                                                                          
054300     IF  PV-TOP-ITEM < +1                                                 
054400         MOVE +1              TO PV-TOP-ITEM                              
054500*        -- TOP OF LIST --                                                
054600         MOVE PC-TSYMSG-00069 TO DP020-MSG-NUMBER                         
054700         SET DP020-MSG-INFORMATIONAL TO TRUE                              
054800     END-IF.                                                              
054900                                                                          
055000 EJECT                                                                    
055100                                                                          
055200*----------------------------------------------------------------*        
055300*    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
055400*    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *        
055500*    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *        
055600*----------------------------------------------------------------*        
055700                                                                          
055800 2120-BROWSE-FORWARD.                                                     
055900                                                                          
056000     ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL                      
056100         GIVING PV-TOP-ITEM.                                              
056200     IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                           
056300         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
056400                                   TO PV-TOP-ITEM                         
056500*        -- BOTTOM OF LIST --                                             
056600         MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER                    
056700         SET DP020-MSG-INFORMATIONAL                                      
056800                                   TO TRUE                                
056900     ELSE                                                                 
057000         COMPUTE PV-BOTTOM-ITEM =                                         
057100             PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                         
057200         IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ                    
057300*            -- BOTTOM OF LIST --                                         
057400             MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER                    
057500             SET DP020-MSG-INFORMATIONAL                                  
057600                                   TO TRUE                                
057700         END-IF                                                           
057800     END-IF.                                                              
057900 EJECT                                                                    
058000*----------------------------------------------------------------*        
058100*    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
058200*    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
058300*    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
058400*    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *        
058500*    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
058600*    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
058700*----------------------------------------------------------------*        
058800                                                                          
058900 2130-CHECK-FOR-JUMP-BROWSE.                                              
059000                                                                          
059100     IF ASTRTITL > ZERO                                                   
059200         MOVE ASTRTITI             TO ASC-START-ITEM-REQUEST              
059300     ELSE                                                                 
059400         IF ASTRTITF = DP015-ERASE-EOF                                    
059500             INITIALIZE ASC-START-ITEM-REQUEST                            
059600         END-IF                                                           
059700     END-IF.                                                              
059800                                                                          
059900     SET PS-NO-ERROR               TO TRUE.                               
060000     IF (ASC-START-ITEM-REQUEST = SPACE)                                  
060100         MOVE ASC-ITEM-AT-TOP-OF-PANEL                                    
060200                                   TO PV-TOP-ITEM                         
060300     ELSE                                                                 
060400         MOVE ASC-START-ITEM-REQUEST                                      
060500                                   TO DP010I-UNEDITED-FIELD               
060600         MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS               
060700         MOVE +0                   TO DP010I-MAXIMUM-DECIMALS             
060800         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
060900                                   TO TRUE                                
263455         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263455              USING DP010I-NUMERIC-EDIT-AREA                              
061100                                                                          
061200         IF  NOT DP010I-ERROR-DETECTED                                    
061300             MOVE DP010I-NUMERIC-FIELD                                    
061400                                   TO  ASC-START-ITEM-REQUEST-N           
061500             IF  ASC-START-ITEM-REQUEST-N > ZERO                          
061600                 IF  ASC-START-ITEM-REQUEST-N >                           
061700                                          ASC-NUMBER-OF-ITEMS-READ        
061800                     MOVE SPACE TO ASC-START-ITEM-REQUEST                 
061900                     COMPUTE PV-TOP-ITEM =                                
062000                             ASC-NUMBER-OF-ITEMS-READ                     
062100                             - PC-ITEMS-PER-PANEL + 1                     
062200                     IF  PV-TOP-ITEM < +1                                 
062300                         MOVE +1   TO PV-TOP-ITEM                         
062400                         MOVE PC-TSYMSG-00070                             
062500                                   TO DP020-MSG-NUMBER                    
062600                         SET DP020-MSG-INFORMATIONAL                      
062700                                   TO TRUE                                
062800                     END-IF                                               
062900                 ELSE                                                     
063000                     MOVE ASC-START-ITEM-REQUEST-N                        
063100                                   TO PV-TOP-ITEM                         
063200                     MOVE SPACE    TO ASC-START-ITEM-REQUEST              
063300                     COMPUTE PV-BOTTOM-ITEM =                             
063400                         PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1             
063500                     IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ        
063600*                        -- BOTTOM OF LIST --                             
063700                         MOVE PC-TSYMSG-00070                             
063800                                   TO DP020-MSG-NUMBER                    
063900                         SET DP020-MSG-INFORMATIONAL                      
064000                                   TO TRUE                                
064100                     END-IF                                               
064200                 END-IF                                                   
064300             ELSE                                                         
064400*                --- VALUE OUT OF RANGE ----                              
064500                 MOVE PC-TSYMSG-00101                                     
064600                                   TO DP020-MSG-NUMBER                    
064700                 MOVE -1           TO ASTRTITL                            
064800                 SET DP030-SET-CURSOR-APPL-1                              
064900                                   TO TRUE                                
065000                 MOVE DP015-REVERSE                                       
065100                                   TO ASTRTITH                            
065200                 MOVE DP015-RED    TO ASTRTITC                            
065300                 MOVE DP015-UNP-NUM-BRT-MDT                               
065400                                   TO ASTRTITA                            
065500                 SET DP020-MSG-FATAL                                      
065600                                   TO TRUE                                
065700                 SET PS-ERROR      TO TRUE                                
065800             END-IF                                                       
065900                                                                          
066000         ELSE                                                             
066100             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
066200             MOVE -1               TO ASTRTITL                            
066300             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
066400             MOVE DP015-REVERSE    TO ASTRTITH                            
066500             MOVE DP015-RED        TO ASTRTITC                            
066600             MOVE DP015-UNP-NUM-BRT-MDT                                   
066700                                   TO ASTRTITA                            
066800             SET DP020-MSG-FATAL   TO TRUE                                
066900             SET PS-ERROR          TO TRUE                                
067000         END-IF                                                           
067100     END-IF.                                                              
067200 EJECT                                                                    
067300                                                                          
067400*----------------------------------------------------------------*        
067500*    GET THE NEXT MESSAGE IF A LIST OF MESSAGES IS BEING         *        
067600*    PROCESSED.                                                  *        
067700*----------------------------------------------------------------*        
067800                                                                          
067900 2140-GET-NEXT-MESSAGE.                                                   
068000                                                                          
068100     ADD +1                       TO ASC-SEL-SUB.                         
068200     IF  ASC-SEL-SUB > CN007-MAX-ITEMS                                    
068300         ADD +1                   TO ASC-SEL-ITEM                         
068400         MOVE +1                  TO ASC-SEL-SUB                          
068500     END-IF.                                                              
068600     IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                        
068700         PERFORM 9100-READ-SEL-QUEUE                                      
068800         IF  CN007-ITEM (ASC-SEL-SUB) > ZERO                              
068900             MOVE CN007-STORE-ZONE     (ASC-SEL-SUB)                      
069000                                  TO ASC-KEY-ZONE-ID                      
069100             SET CN007-INQ (ASC-SEL-SUB) TO TRUE                          
069200             PERFORM 9200-REWRITE-SEL-QUEUE                               
069300         ELSE                                                             
069400             SUBTRACT +1 FROM ASC-SEL-SUB                                 
069500             MOVE PC-TSYMSG-00070 TO DP020-MSG-NUMBER                     
069600             SET DP020-MSG-INFORMATIONAL TO TRUE                          
069700             SET PS-LIST-END             TO TRUE                          
069800         END-IF                                                           
069900     ELSE                                                                 
070000         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
070100         MOVE CN007-MAX-ITEMS     TO ASC-SEL-SUB                          
070200         MOVE PC-TSYMSG-00070     TO DP020-MSG-NUMBER                     
070300         SET DP020-MSG-INFORMATIONAL TO TRUE                              
070400         SET PS-LIST-END             TO TRUE                              
070500     END-IF.                                                              
070600                                                                          
070700*----------------------------------------------------------------*        
070800*    GET THE PREVIOUS MESSAGE IF A LIST OF MESSAGES IS BEING     *        
070900*    PROCESSED.                                                  *        
071000*----------------------------------------------------------------*        
071100                                                                          
071200 2150-GET-PREV-MESSAGE.                                                   
071300                                                                          
071400     SUBTRACT +1 FROM ASC-SEL-SUB.                                        
071500     IF  ASC-SEL-SUB < +1                                                 
071600         SUBTRACT +1 FROM ASC-SEL-ITEM                                    
071700         MOVE CN007-MAX-ITEMS     TO ASC-SEL-SUB                          
071800     END-IF.                                                              
071900                                                                          
072000     IF  ASC-SEL-ITEM > ZERO                                              
072100         PERFORM 9100-READ-SEL-QUEUE                                      
072200         MOVE CN007-STORE-ZONE     (ASC-SEL-SUB)                          
072300                                  TO ASC-KEY-ZONE-ID                      
072400     ELSE                                                                 
072500         MOVE +1                  TO ASC-SEL-ITEM                         
072600                                     ASC-SEL-SUB                          
072700         MOVE PC-TSYMSG-00069     TO DP020-MSG-NUMBER                     
072800         SET DP020-MSG-INFORMATIONAL TO TRUE                              
072900         SET PS-LIST-END             TO TRUE                              
073000     END-IF.                                                              
073100                                                                          
073200 EJECT                                                                    
073300*----------------------------------------------------------------*        
073400*    RESET ATTRIBUTES.                                           *        
073500*----------------------------------------------------------------*        
073600                                                                          
073700 3200-RESET-ATTRIBUTES.                                                   
073800                                                                          
073900     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                           
074000     MOVE DP015-GREEN              TO ASTRTITC.                           
074100     MOVE DP015-UNDERLINE          TO ASTRTITH.                           
074200     MOVE -1                       TO ASTRTITL.                           
074300     SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
074400                                                                          
074500 EJECT                                                                    
074600                                                                          
074700*----------------------------------------------------------------*        
074800*    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
074900*    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
075000*    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
075100*    DISPLAY THE PANEL TO USER.                                  *        
075200*----------------------------------------------------------------*        
075300                                                                          
075400 4000-BUILD-INITIAL-PANEL.                                                
075500                                                                          
075600     INITIALIZE                  ASC-BLOCK-ENTRIES (1)                    
075700                                 ASC-BLOCK-ENTRIES (2).                   
075800     MOVE +1                 TO  ASC-BLK-SUB.                             
075900     SET ASC-ITEMS-LEFT-ON-DB                                             
076000                             TO  TRUE.                                    
076100                                                                          
076200     MOVE +0                 TO  ASC-ITEM-SUB                             
076300                                 ASC-NUMBER-OF-ITEMS-READ                 
076400                                 ASC-NUMBER-OF-SELECTED-ITEMS             
076500                                 ASC-HIGHEST-BLOCK-WRITTEN                
076600                                 ASC-START-OF-SELECTED-RANGE              
076700                                 ASC-END-OF-SELECTED-RANGE                
076800                                 ASC-ITEM-AT-TOP-OF-PANEL.                
076900                                                                          
077000     MOVE SPACE              TO  ASC-DESC-LINE-IND                        
077100                                 ASC-CORR-LINE-IND.                       
077200                                                                          
077300     PERFORM 4100-GET-ZONE-INFO.                                          
077400     PERFORM 4010-OPEN-CURSOR.                                            
077500                                                                          
077600     PERFORM 4300-READ-ITEMS-FROM-DB.                                     
077700                                                                          
077800     PERFORM 4020-CLOSE-CURSOR.                                           
077900                                                                          
078000     MOVE +1                 TO  PV-TOP-ITEM.                             
078100*    IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                                  
078200*        SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                            
078300*        SET DP020-MSG-FATAL   TO  TRUE                                   
078400*        MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
078500*    ELSE                                                                 
078600         MOVE -1             TO ASTRTITL                                  
078700         SET DP030-SET-CURSOR-APPL-1 TO TRUE                              
078800         PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                            
078900*    END-IF.                                                              
079000 EJECT                                                                    
079100*----------------------------------------------------------------*        
079200* OPEN THE CURSOR.                                               *        
079300*----------------------------------------------------------------*        
079400                                                                          
079500 4010-OPEN-CURSOR.                                                        
079600                                                                          
079700     EXEC SQL                                                             
079800          OPEN DEPT-CSR                                                   
079900     END-EXEC.                                                            
080000                                                                          
080100     EVALUATE TRUE                                                        
080200         WHEN SQLCODE = ZERO                                              
080300              CONTINUE                                                    
080400         WHEN SQLWARN0 NOT EQUAL SPACE                                    
080500         WHEN SQLCODE NOT EQUAL ZERO                                      
080600              MOVE '4010-OPEN-CURSOR'                                     
080700                           TO  DP013-PARAGRAPH                            
080800              MOVE 'OPEN MESSAGE CURSOR'                                  
080900                           TO  DP013-MESSAGE-TEXT (1)                     
081000              MOVE SQLCA   TO  DP013-SQLCA                                
081100              SET DP013-DB2-ABEND                                         
081200                           TO  TRUE                                       
081300              PERFORM DP013-0000-PROCESS-ABEND                            
081400     END-EVALUATE.                                                        
081500                                                                          
081600 EJECT                                                                    
081700*----------------------------------------------------------------*        
081800* CLOSE THE CURSOR.                                              *        
081900*----------------------------------------------------------------*        
082000                                                                          
082100 4020-CLOSE-CURSOR.                                                       
082200                                                                          
082300     EXEC SQL                                                             
082400         CLOSE DEPT-CSR                                                   
082500     END-EXEC.                                                            
082600                                                                          
082700     EVALUATE TRUE                                                        
082800         WHEN SQLCODE = ZERO                                              
082900              CONTINUE                                                    
083000         WHEN SQLWARN0 NOT EQUAL SPACE                                    
083100         WHEN SQLCODE NOT EQUAL ZERO                                      
083200              MOVE '4020-CLOSE-CURSOR'                                    
083300                           TO  DP013-PARAGRAPH                            
083400              MOVE 'CLOSE MESSAGE CURSOR'                                 
083500                           TO  DP013-MESSAGE-TEXT (1)                     
083600              MOVE SQLCA   TO  DP013-SQLCA                                
083700              SET DP013-DB2-ABEND                                         
083800                           TO  TRUE                                       
083900              PERFORM DP013-0000-PROCESS-ABEND                            
084000     END-EVALUATE.                                                        
084100                                                                          
084200 EJECT                                                                    
084300*----------------------------------------------------------------*        
084400*    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
084500*    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
084600*    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
084700*    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
084800*    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
084900*                                                                *        
085000*    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
085100*    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
085200*    UPON INITIALIZATION.                                        *        
085300*----------------------------------------------------------------*        
085400                                                                          
085500 4100-GET-ZONE-INFO.                                                      
085600                                                                          
085700     MOVE ASC-KEY-ZONE-ID             TO STRZN-STR-ZN-ID                  
085800     EXEC SQL                                                             
085900     SELECT                                                               
086000         STR_ZN_DESC                                                      
086100     INTO                                                                 
086200         :STRZN-STR-ZN-DESC                                               
086300     FROM TSTRZN                                                          
086400     WHERE STR_ZN_ID = :STRZN-STR-ZN-ID                                   
086500     END-EXEC.                                                            
086600                                                                          
086700     EVALUATE TRUE                                                        
086800         WHEN SQLCODE = ZERO                                              
086900             MOVE STRZN-STR-ZN-DESC  TO ASC-STORE-ZONE-DESC               
087000         WHEN SQLCODE = +100                                              
087100             MOVE ALL '?' TO ASC-STORE-ZONE-DESC                          
087200         WHEN SQLCODE = -904                                              
087300             WHEN SQLCODE = -911                                          
087400             WHEN SQLCODE = -913                                          
087500                 PERFORM 9900-SOFT-ABORT                                  
087600             WHEN SQLWARN0 NOT EQUAL SPACE                                
087700             WHEN SQLCODE NOT EQUAL ZERO                                  
087800                  MOVE '4310-FETCH-CURSOR'                                
087900                               TO  DP013-PARAGRAPH                        
088000                  MOVE 'FETCH MESSAGE CURSOR'                             
088100                               TO  DP013-MESSAGE-TEXT (1)                 
088200                  MOVE SQLCA   TO  DP013-SQLCA                            
088300                  SET DP013-DB2-ABEND                                     
088400                               TO  TRUE                                   
088500                  PERFORM DP013-0000-PROCESS-ABEND                        
088600     END-EVALUATE.                                                        
088700                                                                          
088800 4300-READ-ITEMS-FROM-DB.                                                 
088900                                                                          
089000     PERFORM                                                              
089100         VARYING PV-READ-COUNT                                            
089200         FROM 1 BY 1                                                      
089300         UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                            
089400            OR (ASC-NO-ITEMS-LEFT-ON-DB))                                 
089500                                                                          
089600         PERFORM 4310-FETCH                                               
089700                                                                          
089800         IF  ASC-ITEMS-LEFT-ON-DB                                         
089900                                                                          
089901             IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                        
089902                 ADD +1            TO ASC-ITEM-SUB                        
089903             ELSE                                                         
089904                 ADD +1            TO ASC-BLK-SUB                         
089905                 MOVE +1           TO ASC-ITEM-SUB                        
089906                                                                          
089907                 IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY                 
089908                     MOVE +1       TO ASC-BLK-SUB                         
089909                     SET ASC-BLOCK-MODIFIED (1)                           
089910                                   TO TRUE                                
089911                     PERFORM 5100-WRITE-TEMP-STORAGE                      
089912                     MOVE ASC-BLOCK-ENTRIES                               
089913                               (PC-MAX-BLOCKS-IN-ARRAY)                   
089914                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)         
089915                     ADD +1        TO ASC-BLK-SUB                         
089916                     INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)           
089917                     SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)              
089918                                   TO TRUE                                
089919                 END-IF                                                   
089920             END-IF                                                       
089921             ADD +1                TO ASC-NUMBER-OF-ITEMS-READ            
089922             MOVE ASC-NUMBER-OF-ITEMS-READ                                
089923                                   TO ASC-LIST-ITEM-NUMBER                
089924                                      (ASC-BLK-SUB ASC-ITEM-SUB)          
090100* CURSOR FIELDS GO HERE                                                   
090200             MOVE MDSEAR-DEPT-NBR TO ASC-DEPT                             
090300                                       (ASC-BLK-SUB                       
090400                                        ASC-ITEM-SUB)                     
090500             MOVE MDSEAR-DESC     TO ASC-DEPT-DESC                        
090600                                       (ASC-BLK-SUB                       
090700                                        ASC-ITEM-SUB)                     
090710                                                                          
090800         END-IF                                                           
090900     END-PERFORM.                                                         
091000                                                                          
092600                                                                          
092601*    IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                                  
092602         MOVE +1                   TO ASC-BLK-SUB                         
092603         PERFORM 5100-WRITE-TEMP-STORAGE                                  
092604         ADD +1                    TO ASC-BLK-SUB                         
092605         PERFORM 5100-WRITE-TEMP-STORAGE                                  
092606                                                                          
092607         MOVE 1                    TO ASC-BLK-SUB                         
092608                                      PV-BLOCK-NUMBER                     
092609         PERFORM 4410-READ-TEMP-STORAGE                                   
092610         IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                     
092611             MOVE 2                TO ASC-BLK-SUB                         
092612                                      PV-BLOCK-NUMBER                     
092613             PERFORM 4410-READ-TEMP-STORAGE                               
092614         END-IF.                                                          
092615*    END-IF.                                                              
094100/                                                                         
094200 4310-FETCH.                                                              
094300                                                                          
094400     EXEC SQL                                                             
094500          FETCH DEPT-CSR                                                  
094600          INTO                                                            
094700             :MDSEAR-DEPT-NBR                                             
094800           , :MDSEAR-DESC                                                 
094900     END-EXEC                                                             
095000                                                                          
095100     EVALUATE TRUE                                                        
095200         WHEN SQLCODE = ZERO                                              
095300             CONTINUE                                                     
095400         WHEN SQLCODE = +100                                              
095500             SET ASC-NO-ITEMS-LEFT-ON-DB                                  
095600                                   TO  TRUE                               
095700         WHEN SQLCODE = -904                                              
095800         WHEN SQLCODE = -911                                              
095900         WHEN SQLCODE = -913                                              
096000             PERFORM 9900-SOFT-ABORT                                      
096100         WHEN SQLWARN0 NOT EQUAL SPACE                                    
096200         WHEN SQLCODE NOT EQUAL ZERO                                      
096300              MOVE '4310-FETCH-CURSOR'                                    
096400                           TO  DP013-PARAGRAPH                            
096500              MOVE 'FETCH MESSAGE CURSOR'                                 
096600                           TO  DP013-MESSAGE-TEXT (1)                     
096700              MOVE SQLCA   TO  DP013-SQLCA                                
096800              SET DP013-DB2-ABEND                                         
096900                           TO  TRUE                                       
097000              PERFORM DP013-0000-PROCESS-ABEND                            
097100     END-EVALUATE.                                                        
097200/                                                                         
100100*----------------------------------------------------------------*        
100200*    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
100300*----------------------------------------------------------------*        
100400                                                                          
100500 4400-MOVE-COMMAREA-TO-SCREEN.                                            
100600                                                                          
100700     MOVE ASC-KEY-ZONE-ID        TO ASTZONEO.                             
100800     MOVE ASC-STORE-ZONE-DESC    TO AZDESCO.                              
100900                                                                          
101000     IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))                     
101100         AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))                  
101200         OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))                  
101300             DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                     
101400                 GIVING PV-BLOCK-NUMBER                                   
101500                 REMAINDER PV-REMAINDER                                   
101600             IF  PV-REMAINDER > ZERO                                      
101700                 ADD +1 TO PV-BLOCK-NUMBER                                
101800             END-IF                                                       
101900             MOVE +1    TO ASC-BLK-SUB                                    
102000             PERFORM 4410-READ-TEMP-STORAGE                               
102100             ADD +1     TO ASC-BLK-SUB                                    
102200                           PV-BLOCK-NUMBER                                
102300             PERFORM 4410-READ-TEMP-STORAGE                               
102400     END-IF.                                                              
102500     MOVE +1                       TO ASC-BLK-SUB.                        
102600     COMPUTE ASC-ITEM-SUB =                                               
102700         ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))                      
102800         + PC-ITEMS-PER-PANEL).                                           
102900                                                                          
103000     IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                                
103100         ADD +1   TO ASC-BLK-SUB                                          
103200         SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB                    
103300     END-IF.                                                              
103400                                                                          
103500     COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                                   
103600         ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                        
103700     PERFORM 4420-FORMAT-LIST-LINES                                       
103800         VARYING PV-SUB                                                   
103900         FROM PC-ITEMS-PER-PANEL BY -1                                    
104000         UNTIL PV-SUB < +1.                                               
104100                                                                          
104200     COMPUTE ASC-ITEMS-DISPLAYED =                                        
104300         ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).                  
104400     MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL            
104500                                      ASTRTITO.                           
104600     MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                            
104700     MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                            
104800                                                                          
104900     IF  DP020-MSG-NUMBER-X = SPACE                                       
105000         IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ          
105100*            --- MORE ITEMS TO DISPLAY ----                               
105200             MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER                    
105300             SET DP020-MSG-INFORMATIONAL TO TRUE                          
105400         END-IF                                                           
105500     END-IF.                                                              
105600                                                                          
105700*----------------------------------------------------------------*        
105800*    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
105900*----------------------------------------------------------------*        
106000                                                                          
106100 4410-READ-TEMP-STORAGE.                                                  
106200                                                                          
106300     EXEC CICS                                                            
106400         READQ TS                                                         
106500             QUEUE (ASC-DTL-QUEUE-NAME)                                   
106600             INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
106700             ITEM  (PV-BLOCK-NUMBER)                                      
106800             RESP  (PV-CICS-RESP)                                         
106900     END-EXEC.                                                            
107000                                                                          
107100     IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                               
107200       OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                             
107300         CONTINUE                                                         
107400     ELSE                                                                 
107500         SET DP013-CICS-ABEND TO  TRUE                                    
107600         SET DP013-NO-ROLLBACK TO  TRUE                                   
107700         MOVE '4410-READ-TEMP-STORAGE'                                    
107800                               TO  DP013-PARAGRAPH                        
107900         MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'              
108000                               TO  DP013-MESSAGE-TEXT(1)                  
108100         PERFORM DP013-0000-PROCESS-ABEND                                 
108200     END-IF.                                                              
108300                                                                          
108400*    SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.                     
108500                                                                          
108600     IF  PV-CICS-RESP = DFHRESP (ITEMERR)                                 
108700         INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)                       
108800     END-IF.                                                              
108900 EJECT                                                                    
109000*----------------------------------------------------------------*        
109100*    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
109200*----------------------------------------------------------------*        
109300                                                                          
109400 4420-FORMAT-LIST-LINES.                                                  
109500                                                                          
109600     IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ              
109700         SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                        
109800         MOVE SPACE              TO MR-DEPT-DESC (PV-SUB)                 
109900                                    MR-DEPT      (PV-SUB)                 
110000     ELSE                                                                 
110100         IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL                   
110200             MOVE ASC-DEPT        (ASC-BLK-SUB ASC-ITEM-SUB)              
110300                                 TO MR-DEPT (PV-SUB)                      
110400             MOVE ASC-DEPT-DESC   (ASC-BLK-SUB ASC-ITEM-SUB)              
110500                                 TO MR-DEPT-DESC (PV-SUB)                 
110600         END-IF                                                           
110700     END-IF.                                                              
110800     SUBTRACT +1 FROM ASC-ITEM-SUB.                                       
110900     IF  ASC-ITEM-SUB < + 1                                               
111000         MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                          
111100         MOVE +1                 TO ASC-BLK-SUB                           
111200     END-IF.                                                              
111300                                                                          
111400*----------------------------------------------------------------*        
111500*    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
111600*----------------------------------------------------------------*        
111700                                                                          
111800 5100-WRITE-TEMP-STORAGE.                                                 
111900                                                                          
112000     MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                             
112100                               TO  PV-LIST-ITEM-NUMBER.                   
112200     COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                                   
112300         ASC-HIGHEST-BLOCK-WRITTEN + 1.                                   
112400                                                                          
112500     EXEC CICS                                                            
112600         WRITEQ TS                                                        
112700             QUEUE (ASC-DTL-QUEUE-NAME)                                   
112800             FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))                       
112900             ITEM  (PV-HIGHEST-BLOCK-WRITTEN)                             
113000             RESP  (PV-CICS-RESP)                                         
113100     END-EXEC.                                                            
113200                                                                          
113300     IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                              
113400         SET DP013-CICS-ABEND                                             
113500                       TO  TRUE                                           
113600         SET DP013-NO-ROLLBACK                                            
113700                       TO  TRUE                                           
113800         MOVE '5100-WRITE-TEMP-STORAGE'                                   
113900                       TO  DP013-PARAGRAPH                                
114000         MOVE 'ATTEMPTING TO WRITE TEMP STORAGE LIST BLOCK'               
114100                       TO  DP013-MESSAGE-TEXT(1)                          
114200         PERFORM DP013-0000-PROCESS-ABEND                                 
114300     END-IF.                                                              
114400     ADD +1            TO  ASC-HIGHEST-BLOCK-WRITTEN.                     
114500                                                                          
114600 EJECT                                                                    
114700*----------------------------------------------------------------*        
114800* READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
114900*----------------------------------------------------------------*        
115000                                                                          
115100 9100-READ-SEL-QUEUE.                                                     
115200     EXEC CICS                                                            
115300         READQ TS                                                         
115400             QUEUE (ASC-SEL-QUEUE-NAME)                                   
115500             INTO  (CN007-TS-SEL-REC)                                     
115600             ITEM  (ASC-SEL-ITEM)                                         
115700             RESP  (PV-CICS-RESP)                                         
115800     END-EXEC.                                                            
115900                                                                          
116000     IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                               
116100         CONTINUE                                                         
116200     ELSE                                                                 
116300         SET DP013-LOGIC-ABEND                                            
116400             DP013-NO-ROLLBACK     TO TRUE                                
116500         MOVE '9100-READ-SEL-QUEUE'                                       
116600                                   TO DP013-PARAGRAPH                     
116700         MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'           
116800                                   TO DP013-MESSAGE-TEXT (1)              
116900         PERFORM DP013-0000-PROCESS-ABEND                                 
117000     END-IF.                                                              
117100*----------------------------------------------------------------*        
117200* UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *        
117300*----------------------------------------------------------------*        
117400 9200-REWRITE-SEL-QUEUE.                                                  
117500     EXEC CICS                                                            
117600          WRITEQ TS                                                       
117700              QUEUE  (ASC-SEL-QUEUE-NAME)                                 
117800              FROM   (CN007-TS-SEL-REC)                                   
117900              ITEM   (ASC-SEL-ITEM)                                       
118000              RESP   (PV-CICS-RESP)                                       
118100              REWRITE                                                     
118200     END-EXEC.                                                            
118300                                                                          
118400     IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                           
118500         SET DP013-CICS-ABEND                                             
118600             DP013-NO-ROLLBACK     TO TRUE                                
118700         MOVE '9200-REWRITE-SEL-QUEUE'                                    
118800                                   TO DP013-PARAGRAPH                     
118900         MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'           
119000                                   TO DP013-MESSAGE-TEXT (1)              
119100         PERFORM DP013-0000-PROCESS-ABEND                                 
119200     END-IF.                                                              
119300/                                                                         
119400 9900-SOFT-ABORT.                                                         
119500     SET PS-ERROR         TO TRUE.                                        
119600     SET DP020-MSG-FATAL  TO TRUE.                                        
119700     MOVE 'THROUGH SOFT-ABORT PARA'                                       
119800                      TO DP013-MESSAGE-TEXT (1)                           
119900     MOVE PC-TSYMSG-00551 TO DP020-MSG-NUMBER.                            
120000                                                                          
120100     SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                                
120200                                                                          
120300     PERFORM 9901-BACKUP.                                                 
120400     MOVE 'PROGRAM EXIT CALL' TO DP013-MESSAGE-TEXT (1).                  
120500     PERFORM 0001-CALL-CICS-ARCH-API.                                     
120600                                                                          
120700 9901-BACKUP.                                                             
120800     EXEC CICS SYNCPOINT                                                  
120900          ROLLBACK                                                        
121000     END-EXEC.                                                            
121100                                                                          
121200/                                                                         
121300*----------------------------------------------------------------*        
121400*    ABEND PROCESSOR MODULE                                      *        
121500*----------------------------------------------------------------*        
121600                                                                          
121700     COPY DPPD013.                                                        
