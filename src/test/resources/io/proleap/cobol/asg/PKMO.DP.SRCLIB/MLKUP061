       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              MLKUP061.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/03/2004.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *  PARTITION CONTROL UPDATE                                      *        
      *                                                                *        
      *  THIS PROGRAM TAKES THE CONTROL RECORD AND UPDATE THE ACTUAL   *        
      *  PARTITION CONTROL TABLES.                                     *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PARTN-INFO-EXTRACT                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PARTN-CNTL-RPT                                                
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PARTN-INFO-EXTRACT                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PARTN-INFO-REC                  PIC X(80).                           
                                                                                
       FD  PARTN-CNTL-RPT                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 075 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE                     PIC X(075).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  PARTITION INFORMATION DATA EXTRACT                                     
      ******************************************************************        
      *01  ML060-PARTN-INFO-EXTRACT.                                            
           COPY MLRD060.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** REPORT RECORD/LAYOUT BEGIN ***'.                                
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
      *01  DP075O-STANDARD-HEADER-75-COLS.                                      
      *01  DP075O-CNFDL-HDR-075-COLS.                                           
           COPY DPWS075O.                                                       
                                                                                
       01  RP-REPORT-TITLE-1.                                                   
           05  FILLER                      PIC X(22)       VALUE SPACES.        
           05  RP-REPORT-TITLE             PIC X(31)       VALUE                
               'PARTITION CONTROL TABLE REPORT'.                                
           05  FILLER                      PIC X(22)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-1.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(08)       VALUE                
               'LEVEL   '.                                                      
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(11)       VALUE                
               'FISCAL DATE'.                                                   
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)       VALUE                
               'PARTITION'.                                                     
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)       VALUE                
               'GROUP'.                                                         
           05  FILLER                      PIC X(32)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-HEADER-2.                                                  
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(08)      VALUE ALL '-'.        
           05  FILLER                      PIC X(04)       VALUE SPACES.        
           05  FILLER                      PIC X(11)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(09)      VALUE ALL '-'.        
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  FILLER                      PIC X(05)      VALUE ALL '-'.        
           05  FILLER                      PIC X(32)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-DETAIL.                                                    
           05  FILLER                      PIC X(02)       VALUE SPACES.        
           05  RP-CONTROL-LEVEL            PIC X(08)       VALUE SPACES.        
               88  RP-MONTHLY-LEVEL                        VALUE                
                   'MONTHLY '.                                                  
               88  RP-WEEKLY-LEVEL                         VALUE                
                   'WEEKLY  '.                                                  
           05  FILLER                      PIC X(05)       VALUE SPACES.        
           05  RP-FISCAL-DATE              PIC X(10)       VALUE SPACES.        
           05  FILLER                      PIC X(07)       VALUE SPACES.        
           05  RP-PARTITION-NBR            PIC 9(04)       VALUE ZEROES.        
           05  FILLER                      PIC X(06)       VALUE SPACES.        
           05  RP-DEPT-GROUP-CDE           PIC X(01)       VALUE SPACES.        
           05  FILLER                      PIC X(32)       VALUE SPACES.        
                                                                                
       01  RP-REPORT-END.                                                       
           05  FILLER                      PIC X(18)       VALUE SPACES.        
           05  FILLER                      PIC X(39)       VALUE                
               '*****  E N D   O F   R E P O R T  *****'.                       
           05  FILLER                      PIC X(18)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING CONSTANT BEGIN ***'.                                
                                                                                
       01  PC-PROGRAM-CONSTANTS-AREA.                                           
           05  PC-PRINT-LINE-MAX           PIC S9(04)      VALUE +57            
                               COMP.                                            
           05  PC-RETRIES-MAX              PIC S9(04)      VALUE +3             
                               COMP.                                            
           05  PC-NULL-DTE                 PIC X(10)       VALUE                
                                                           '9999-12-31'.        
           05  PC-Y                        PIC X           VALUE 'Y'.           
           05  PC-N                        PIC X           VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING SWITCHES BEGIN ***'.                                
                                                                                
       01  PS-SWITCHES-AREA.                                                    
           05  PS-END-OF-PARTN-INFO        PIC X           VALUE 'N'.           
               88  END-OF-PARTN-INFO                       VALUE 'Y'.           
               88  NOT-END-OF-PARTN-INFO                   VALUE 'N'.           
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING VARIABLE BEGIN ***'.                                
                                                                                
       01  PV-PROGRAM-VARIABLES-AREA.                                           
           05  PV-PAGE-COUNT               PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PV-PRINT-LINE-COUNT         PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ADVANCE-LINE             PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-RETRIES-COUNT            PIC S9(04)      VALUE ZEROES         
                               COMP.                                            
           05  PV-ACTIVE-CARDS             PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-COMMENT-CARDS            PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-READ              PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-RECORD-INSERTED          PIC S9(07)      VALUE ZEROES         
                               COMP-3.                                          
           05  PV-OPER-ATTEMPTED           PIC X(25)       VALUE SPACES.        
           05  PV-PROCESSING-DATE          PIC X(10)       VALUE SPACES.        
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-CC        PIC X(02).                           
               10  PV-PROCESSING-YY        PIC X(02).                           
               10  PV-PROC-DSH1            PIC X(01).                           
               10  PV-PROCESSING-MM        PIC X(02).                           
               10  PV-PROC-DSH2            PIC X(01).                           
               10  PV-PROCESSING-DD        PIC X(02).                           
           05  PV-TODAYS-DATE.                                                  
               10  PV-TODAYS-DATE-YY       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-MM       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-DATE-DD       PIC 9(02)       VALUE ZEROES.        
           05  PV-TODAYS-TIME.                                                  
               10  PV-TODAYS-TIME-HR       PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-MIN      PIC 9(02)       VALUE ZEROES.        
               10  PV-TODAYS-TIME-SEC      PIC 9(02)       VALUE ZEROES.        
               10  FILLER                  PIC 9(02)       VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SQL WORK AREA BEGIN ***'.                                       
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
      *01  DP004-DB2-ERROR-FIELDS.                                              
           COPY DPWS004.                                                        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  CONTROL-CARD-ABEND                          VALUE +4001.         
           88  TABLE-AREA-ABEND                            VALUE +4002.         
           88  NON-SQL-ABEND-CODE                          VALUE +4004.         
           88  LOGICAL-ISSUE-ABEND                         VALUE +4012.         
           88  SQL-ABEND-CODE                              VALUE +4016.         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    MLT_PARTN_CNTL - ML'S PARTITION CONTROL TABLE              *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE END ***'.                                       
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-READ-PARTN-INFO.                                        
                                                                                
           PERFORM B200-PROCESS-PARTN-INFO                                      
               UNTIL END-OF-PARTN-INFO.                                         
                                                                                
           IF PV-RECORD-READ > ZEROES AND PV-RECORD-INSERTED > ZEROES           
               CONTINUE                                                         
           ELSE                                                                 
               SET CONTROL-CARD-ABEND     TO TRUE                               
               DISPLAY ' *** NOTHING TO PROCESS, CHECK INPUT FILE'              
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
           PERFORM B900-FINALIZE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
       B100-HOUSEKEEPING.                                                       
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-TODAYS-DATE-YY TO PV-PROCESSING-YY.                          
           MOVE PV-TODAYS-DATE-MM TO PV-PROCESSING-MM.                          
           MOVE PV-TODAYS-DATE-DD TO PV-PROCESSING-DD.                          
                                                                                
           MOVE '-' TO PV-PROC-DSH1.                                            
           MOVE '-' TO PV-PROC-DSH2.                                            
                                                                                
           IF PV-TODAYS-DATE-YY > '80'                                          
               MOVE '19' TO PV-PROCESSING-CC                                    
           ELSE                                                                 
               MOVE '20' TO PV-PROCESSING-CC                                    
           END-IF.                                                              
                                                                                
           MOVE PV-TODAYS-DATE-MM  TO DP075O-RUN-MONTH.                         
           MOVE PV-TODAYS-DATE-DD  TO DP075O-RUN-DAY.                           
           MOVE PV-TODAYS-DATE-YY  TO DP075O-RUN-YEAR.                          
           MOVE PV-TODAYS-TIME-HR  TO DP075O-RUN-HOUR.                          
           MOVE PV-TODAYS-TIME-MIN TO DP075O-RUN-MINUTE.                        
           MOVE 'MLKUP061'         TO DP075O-PROGRAM-NAME.                      
                                                                                
           OPEN INPUT  PARTN-INFO-EXTRACT                                       
                OUTPUT PARTN-CNTL-RPT.                                          
                                                                                
      ******************************************************************        
      *    PROCESS PARTITION INFO                                               
      ******************************************************************        
       B200-PROCESS-PARTN-INFO.                                                 
                                                                                
           IF ML060-ACTIVE-CARD                                                 
               ADD +1                      TO PV-ACTIVE-CARDS                   
               IF ML060-TM-LVL-CODE NOT = MLT00003-TBL-TM-LVL-CDE               
                   MOVE ZEROES             TO PV-PRINT-LINE-COUNT               
               END-IF                                                           
               MOVE ML060-TM-LVL-CODE      TO MLT00003-TBL-TM-LVL-CDE           
               IF ML060-TM-WEEKLY-LVL-CODE                                      
                   SET RP-WEEKLY-LEVEL         TO TRUE                          
               ELSE                                                             
                   SET RP-MONTHLY-LEVEL        TO TRUE                          
               END-IF                                                           
               MOVE ML060-FSCL-DTE         TO MLT00003-FSCL-DTE                 
                                              RP-FISCAL-DATE                    
               MOVE ML060-PARTN-NBR        TO MLT00003-PARTN-NBR                
                                              RP-PARTITION-NBR                  
               MOVE ML060-DEPT-GP-CDE      TO MLT00003-DEPT-GP-CDE              
                                              RP-DEPT-GROUP-CDE                 
               PERFORM Z200-INSERT-PARTN-CNTL                                   
               PERFORM Z700-PRINT-DETAILS                                       
           ELSE                                                                 
               ADD +1                      TO PV-COMMENT-CARDS                  
           END-IF.                                                              
                                                                                
           PERFORM Z100-READ-PARTN-INFO.                                        
                                                                                
      ******************************************************************        
      *    FINALIZE THE REPORT AND CLOSE OUT FILES                              
      ******************************************************************        
       B900-FINALIZE.                                                           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' **** RECORDS READ:      '                                  
                   PV-RECORD-READ.                                              
           DISPLAY ' **** ACTIVE RECORDS:    '                                  
                   PV-ACTIVE-CARDS.                                             
           DISPLAY ' **** COMMENTED RECORDS: '                                  
                   PV-COMMENT-CARDS.                                            
           DISPLAY ' **** RECORDS INSERTED:  '                                  
                   PV-RECORD-INSERTED.                                          
                                                                                
           PERFORM Z300-COMMIT.                                                 
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-END                                 
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           CLOSE   PARTN-INFO-EXTRACT                                           
                   PARTN-CNTL-RPT.                                              
                                                                                
      ******************************************************************        
      *    READ CONTROL CARD                                                    
      ******************************************************************        
       Z100-READ-PARTN-INFO.                                                    
                                                                                
           ADD +1                      TO PV-RECORD-READ.                       
           READ PARTN-INFO-EXTRACT INTO ML060-PARTN-INFO-EXTRACT                
               AT END SET END-OF-PARTN-INFO TO TRUE.                            
                                                                                
      ******************************************************************        
      *    INSERT ROWS TO PARTITION CONTROL TABLE                               
      ******************************************************************        
       Z200-INSERT-PARTN-CNTL.                                                  
                                                                                
           MOVE 'INSERT PARTN-CNTL ROW' TO PV-OPER-ATTEMPTED.                   
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRIES-COUNT FROM 1 BY 1                             
               UNTIL PV-RETRIES-COUNT > PC-RETRIES-MAX                          
                   OR SQLCODE = +0                                              
               EXEC SQL                                                         
                   INSERT                                                       
                     INTO MLT_PARTN_CNTL                                        
                          (                                                     
                            TBL_TM_LVL_CDE                                      
                           ,FSCL_DTE                                            
                           ,PARTN_NBR                                           
                           ,DEPT_GP_CDE                                         
                          )                                                     
                     VALUES                                                     
                          (                                                     
                            :MLT00003-TBL-TM-LVL-CDE                            
                           ,:MLT00003-FSCL-DTE                                  
                           ,:MLT00003-PARTN-NBR                                 
                           ,:MLT00003-DEPT-GP-CDE                               
                          )                                                     
               END-EXEC                                                         
                                                                                
               IF SQLCODE NOT = +0                                              
                   DISPLAY ' *** INSERT RETRY #'                                
                           PV-RETRIES-COUNT                                     
                           ' FAIL WITH SQLCODE = ' SQLCODE                      
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF SQLCODE = +0                                                      
               ADD +1                      TO PV-RECORD-INSERTED                
           ELSE                                                                 
               DISPLAY ' *** TM-LVL-CDE: '                                      
                       MLT00003-TBL-TM-LVL-CDE                                  
                       ', FSCL-DTE: '                                           
                       MLT00003-FSCL-DTE                                        
                       ', PARTN-NBR: '                                          
                       MLT00003-PARTN-NBR                                       
                       ', GP-CDE: '                                             
                       MLT00003-DEPT-GP-CDE                                     
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    COMMIT ROWS TO PARTITION CONTROL TABLE                               
      ******************************************************************        
       Z300-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'COMMIT INSERTED ROWS' TO PV-OPER-ATTEMPTED                 
               PERFORM Z900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT DETAILS                                             
      ******************************************************************        
       Z700-PRINT-DETAILS.                                                      
                                                                                
           IF PV-PRINT-LINE-COUNT > PC-PRINT-LINE-MAX                           
               OR PV-PRINT-LINE-COUNT = ZEROES                                  
               PERFORM Z750-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-DETAIL                              
               AFTER ADVANCING PV-ADVANCE-LINE LINES.                           
                                                                                
           ADD  PV-ADVANCE-LINE TO PV-PRINT-LINE-COUNT.                         
           MOVE 1               TO PV-ADVANCE-LINE.                             
           MOVE SPACES TO RP-REPORT-DETAIL.                                     
                                                                                
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      ******************************************************************        
       Z750-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP075O-PAGE-NUMBER.                            
           MOVE 1             TO DP075O-REPORT-NUMBER.                          
           WRITE REPORT-LINE FROM DP075O-STANDARD-HEADER-75-COLS                
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-TITLE-1.                            
                                                                                
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-1                            
               AFTER ADVANCING 2 LINES.                                         
           WRITE REPORT-LINE FROM RP-REPORT-HEADER-2.                           
                                                                                
           MOVE 6 TO PV-PRINT-LINE-COUNT.                                       
           MOVE 2 TO PV-ADVANCE-LINE.                                           
                                                                                
      ******************************************************************        
      *    SQL ERROR - ABEND                                                    
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND-CODE         TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MLKUP061'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *    ABEND CALL                                                           
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
