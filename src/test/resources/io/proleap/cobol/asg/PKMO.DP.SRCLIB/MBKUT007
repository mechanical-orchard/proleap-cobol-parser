      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    MBKUT007.                                                 
       AUTHOR.        MIKE FURLICK.                                             
       DATE-WRITTEN.  NOVEMBER 26, 1991.                                        
                                                                                
      *----------------------------------------------------------------*        
      *         REALIGN THE MERCHANDISE BUSINESS STRUCTURE             *        
      *                                                                *        
      *    THIS PROGRAM PROCESSES THE 'MBKUT007' RECORD TYPES FROM     *        
      *    THE VSAM BATCH CONTROL DATASET.                             *        
      *                                                                *        
      *    FOR EACH BATCH CONTROL RECORD, VALIDATE THE RECORD'S        *        
      *    CONTENTS, INSERT THE ROWS, AND UPDATE THE EXISTING ROWS.    *        
      *    FOR THE NEWLY INSERTED ROWS ON MDSEAR, A NEW DKEY MUST BE   *        
      *    OBTAINED AND USED FOR THE INSERTS ON APLAVL, MDSECL,        *        
      *    MDSEDP, MDSECL, AND DPTSHK.                                 *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *   06/17/94   AJR  WR2602 GROUP PRICING                         *        
      *   REMOVED THE PLU-GP-PR-OWNER-ID.  ALL GROUP PRICED ITEMS WILL *        
      *   BE OWNED AT THE LOW.                                         *        
      *----------------------------------------------------------------*        
      *   06/07/96   PATI GREEN    WR4026  VENDOR STYLE MGMT                    
      *                                                                *        
      *   PGM CHANGED TO RETAIN ORIGINAL DKEY WITH THE NEW/ACTIVE DATA,*        
      *   OLD/EXPIRING DATA WILL BE UPDATED WITH A NEWLY ASSIGNED DKEY.*        
      *                                                                *        
      *   FUTURE DATED REALIGNMENTS SHOULD NOT BE PROCESSED UNTIL THEIR*        
      *   NEW-START-DATE HAS BEEN REACHED, TO PREVENT DKEYS ON MBS     *        
      *   TABLES FROM MISMATCHING DKEYS ON VENDOR STYLE TABLES.  FUTURE*        
      *   REALIGNMENT RECORDS WILL REMIAN ON INPUT VSAM FILE UNTIL THE *        
      *   NEW-START-DATE IS REACHED. (CHECKS IN P-3200 AND P-3800.)    *        
      *                                                                *        
      *    NOTE:                                                                
      *    FOLLOWING THE ORIGINAL PROGRAM LOGIC, ALREADY EXPIRED ROWS  *        
      *    ON TMDSEAR, TAPLAVL AND TRSPBAR ARE NOT RETRIEVED/PROCESSED;*        
      *    THESE ROWS RETAIN THEIR ORIGINAL DKEYS AND DATES.           *        
      *----------------------------------------------------------------*        
      *    06/23/98  DOUG DOUGLAS/STRATAGEM         WR#: 6041          *        
      *              - UPDATE DATE PROCESSING WITH DPKUT500            *        
      *----------------------------------------------------------------*        
      *    07/14/06  PAUL KEBER                     IR#: P000900238    *        
      *              - ADD ERROR DISPLAYS TO DETERMINE WHY ABENDING    *        
      *----------------------------------------------------------------*        
C14666*                                                CRQ000000014666          
C14666*    07/10/2012 BRUCE GRAHAM / KFORCE                                     
C14666*               ACCOUNT FOR NEW ATTRIBUTES ADDED TO TABLE                 
C14666*               TDPTSHK. BRING FORWARD FROM DECLARE/SELECT                
C14666*               AND INSERT INTO NEW ROWS.                                 
C14666*----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BATCH-CONTROL-FILE                                            
               ORGANIZATION IS INDEXED                                          
               RECORD KEY IS BCR-KEY                                            
               FILE STATUS IS PV-VSAM-STATUS                                    
               ACCESS IS SEQUENTIAL                                             
               ASSIGN TO INP01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BATCH-CONTROL-FILE.                                                  
                                                                                
       01  BATCH-CONTROL-RECORD.                                                
           05  BCR-KEY               PIC X(30).                                 
           05  BCR-DATA              PIC X(70).                                 
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES            PIC S9(3)  VALUE +3                    
                                                    COMP-3.                     
           05  PC-NINES                  PIC 9(03)  VALUE 999.                  
           05  PC-CO                     PIC X(03)  VALUE 'CO '.                
           05  PC-GMA                    PIC X(03)  VALUE 'GMA'.                
           05  PC-DMA                    PIC X(03)  VALUE 'DMA'.                
           05  PC-DPT                    PIC X(03)  VALUE 'DPT'.                
           05  PC-MCL                    PIC X(03)  VALUE 'MCL'.                
           05  PC-SCL                    PIC X(03)  VALUE 'SCL'.                
           05  PC-COMPANY                PIC X(02)  VALUE '03'.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTINUE-SW            PIC X      VALUE 'Y'.                  
               88  PS-CONTINUE                      VALUE 'Y'.                  
               88  PS-DONT-PROCESS                  VALUE 'N'.                  
           05  PS-DATE-SW                PIC X      VALUE 'N'.                  
               88  PS-VALID-DATE                    VALUE 'Y'.                  
               88  PS-INVALID-DATE                  VALUE 'N'.                  
           05  PS-FUTURE-START-DATE-SW   PIC X      VALUE 'N'.                  
               88  PS-FUTURE-START-DATE             VALUE 'Y'.                  
               88  PS-CURRENT-START-DATE            VALUE 'N'.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SQL-SUB                PIC S9(4)  VALUE ZERO                  
                                                    COMP.                       
           05  PV-NULL-IND               PIC S9(4)  VALUE ZERO                  
                                                    COMP.                       
           05  PV-NULL-IND2              PIC S9(4)  VALUE ZERO                  
                                                    COMP.                       
           05  PV-DB2-COUNT              PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-DELETE-COUNT           PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-UPDATE-COUNT           PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-COUNT           PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-UPDATE-APLAVL-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-DELETE-APLAVL-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-APLAVL-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-UPDATE-RSPBAR-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-DELETE-RSPBAR-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-RSPBAR-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-MDSEDP-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-UPDATE-MDSEDP-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-MDSECL-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-INSERT-DPTSHK-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-UPDATE-DPTSHK-COUNT    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-BATCH-CNTL-COUNT       PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-INSERTS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-DELETES            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-UPDATES            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-APLAVLS-UPDATED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-APLAVLS-DELETED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-APLAVLS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-RSPBARS-UPDATED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-RSPBARS-DELETED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-RSPBARS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-MDSEDPS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-MDSEDPS-UPDATED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-MDSECLS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-DPTSHKS            PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-TOT-DPTSHKS-UPDATED    PIC S9(9)  VALUE ZERO                  
                                                    COMP-3.                     
                                                                                
           05  PV-MDSEAR-HOLD-AREA       PIC X(98)  VALUE SPACE.                
           05  PV-APLAVL-HOLD-AREA       PIC X(28)  VALUE SPACE.                
           05  PV-RSPBAR-HOLD-AREA       PIC X(63)  VALUE SPACE.                
           05  PV-MDSEDP-HOLD-AREA       PIC X(49)  VALUE SPACE.                
           05  PV-MDSECL-HOLD-AREA       PIC X(23)  VALUE SPACE.                
C14666*    05  PV-DPTSHK-HOLD-AREA       PIC X(58)  VALUE SPACE.                
C14666     05  PV-DPTSHK-HOLD-AREA       PIC X(89)  VALUE SPACE.                
                                                                                
           05  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACE.                
           05  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACE.                
           05  PV-VSAM-STATUS            PIC XX     VALUE SPACE.                
               88  PV-GOOD-STATUS                   VALUE '00'.                 
               88  PV-AT-END                        VALUE '10'.                 
               88  PV-NO-RECORDS                    VALUE '23'.                 
               88  PV-VERIFIED                      VALUE '97'.                 
           05  PV-BUILD-MAJ-CL.                                                 
               10  PV-BUILD-MAJ-CL-BYTE1 PIC X.                                 
               10  PV-BUILD-MAJ-CL-BYTE2 PIC X.                                 
           05  PV-BUILD-SUB-CL.                                                 
               10  PV-BUILD-SUB-CL-BYTE1 PIC X.                                 
               10  PV-BUILD-SUB-CL-BYTE2 PIC X.                                 
               10  PV-BUILD-SUB-CL-BYTE3 PIC X.                                 
           05  PV-NEW-SUB-CL.                                                   
               10  PV-NEW-SUB-CL-BYTE1   PIC X.                                 
               10  PV-NEW-SUB-CL-BYTE2   PIC X.                                 
               10  PV-NEW-SUB-CL-BYTE3   PIC X.                                 
           05  PV-OLD-SUB-CL.                                                   
               10  PV-OLD-SUB-CL-BYTE1   PIC X.                                 
               10  PV-OLD-SUB-CL-BYTE2   PIC X.                                 
               10  PV-OLD-SUB-CL-BYTE3   PIC X.                                 
           05  PV-NEW-MAJ-CL.                                                   
               10  PV-NEW-MAJ-CL-BYTE1   PIC X.                                 
               10  PV-NEW-MAJ-CL-BYTE2   PIC X.                                 
           05  PV-OLD-MAJ-CL.                                                   
               10  PV-OLD-MAJ-CL-BYTE1   PIC X.                                 
               10  PV-OLD-MAJ-CL-BYTE2   PIC X.                                 
           05  PV-NEW-DKEY               PIC S9(6)  VALUE +0 COMP-3.            
                                                                                
           05  PV-LIST-SEQ-NBR           PIC S9(15) VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-MAX-SEQ-NBR            PIC S9(15) VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-WORK-SEQ-NBR           PIC S9(15) VALUE ZERO.                 
           05  FILLER  REDEFINES  PV-WORK-SEQ-NBR.                              
               10  PV-WS-OPERCO          PIC 99.                                
               10  PV-CONTROL-DATA.                                             
                   15  PV-WS-GMA         PIC 99.                                
                   15  PV-WS-DMA         PIC 99.                                
                   15  PV-WS-DEPT        PIC 999.                               
                   15  PV-WS-MAJ-CL      PIC 99.                                
                   15  PV-WS-SUB-CL      PIC 999.                               
               10  PV-WS-ZERO            PIC 9.                                 
           05  PV-LEVEL                  PIC X(3)   VALUE SPACES.               
               88  PV-CO-LIT                        VALUE 'CO '.                
               88  PV-GMA-LIT                       VALUE 'GMA'.                
               88  PV-DMA-LIT                       VALUE 'DMA'.                
               88  PV-DPT-LIT                       VALUE 'DPT'.                
               88  PV-MCL-LIT                       VALUE 'MCL'.                
                                                                                
           05  PV-LAST-LEVEL             PIC X(3)   VALUE SPACES.               
           05  PV-LAST-CO-DKEY           PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-LAST-GMA-DKEY          PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-LAST-DMA-DKEY          PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-LAST-DPT-DKEY          PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-LAST-MCL-DKEY          PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-APLAVL-DKEY            PIC S9(6)  VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-APLAVL-START-DATE      PIC X(10).                             
           05  PV-APLAVL-END-DATE        PIC X(10).                             
                                                                                
           05  PV-PARENT-DKEY            PIC S9(15) VALUE ZERO                  
                                                    COMP-3.                     
           05  PV-PARENT-START-DATE      PIC X(10).                             
           05  PV-PARENT-END-DATE        PIC X(10).                             
           05  PV-CURRENT-DATE.                                                 
               10  PV-CD-YY              PIC 99.                                
               10  PV-CD-MM              PIC XX.                                
               10  PV-CD-DD              PIC XX.                                
           05  PV-DB2-WORK-DATE.                                                
               10  PV-WD-CCYY            PIC 9(4).                              
               10  FILLER  REDEFINES  PV-WD-CCYY.                               
                   15  PV-WD-CC          PIC XX.                                
                   15  PV-WD-YY          PIC XX.                                
               10  FILLER                PIC X.                                 
               10  PV-WD-MM              PIC XX.                                
               10  FILLER                PIC X.                                 
               10  PV-WD-DD              PIC XX.                                
                                                                                
       01  PV-HOLD-END-DATE              PIC X(10).                             
       01  PV-DB2-DATE                   PIC X(10).                             
                                                                                
      *    PV-END-DATE-MINUS1 REFLECTS THE EFFECTIVE DATE MINUS ONE             
      *    DAY.  THIS IS USED TO DETERMINE WHETHER TO DELETE OR UPDATE          
      *    THE "OLD" ROWS.  PV-OLD-END-DATE-MINUS1 REFLECTS THE DATE            
      *    USED TO UPDATE THE "OLD" ROWS WITH THE NEW END DATE.                 
                                                                                
       01  PV-END-DATE-MINUS1            PIC X(10).                             
       01  PV-OLD-END-DATE-MINUS1        PIC X(10).                             
                                                                                
       01  PV-DB2-CHANGE-DATE            PIC X(10).                             
                                                                                
       01  ABEND-CODE                    PIC S9(4)  VALUE ZERO                  
                                                    COMP.                       
           88  ABEND-4001                           VALUE 4001.                 
           88  ABEND-4002                           VALUE 4002.                 
           88  ABEND-4003                           VALUE 4003.                 
           88  ABEND-4004                           VALUE 4004.                 
           88  ABEND-4005                           VALUE 4005.                 
           88  ABEND-4006                           VALUE 4006.                 
           88  ABEND-4013                           VALUE 4013.                 
           EJECT                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    RECORD LAYOUT FOR THE BATCH CONTROL FILE                             
      *----------------------------------------------------------------*        
                                                                                
           COPY MBWS018.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE FOR THE DATE VALIDATION ROUTINE                      
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS500.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    MDSEAR DCLGEN                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    APLAVL DCLGEN                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    RSPBAR DCLGEN                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    MDSEDP DCLGEN                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSEDP                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    MDSECL DCLGEN                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMDSECL                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    DEPARTMENT SHRINK                                                    
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE TDPTSHK                                                  
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA DCLGEN                                       
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    ERROR MESSAGE AREA FOR DB2                                           
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    MDSEAR LIST CURSOR. (LIST-SEQ-NBR'S)                                 
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE MDSEAR-LIST CURSOR                                       
                   FOR SELECT                                                   
                               OPERCO_NBR                                       
                            ,  GMA_NBR                                          
                            ,  DMA_NBR                                          
                            ,  DEPT_NBR                                         
                            ,  MAJ_CL_NBR                                       
                            ,  SUB_CL_NBR                                       
                            ,  MDSELV_CDE                                       
                            ,  MDSEAR_DKEY                                      
                            ,  CHG_DTE                                          
                            ,  CRE_DTE                                          
                            ,  STRT_DTE                                         
                            ,  END_DTE                                          
                            ,  GEN_MDSE_TYP_CDE                                 
                            ,  MDSEAR_DESC                                      
                            ,  BYR_NBR                                          
                            ,  LIST_SEQ_NBR                                     
               FROM                                                             
                    TMDSEAR                                                     
                  WHERE                                                         
                            (OPERCO_NBR   =  :MB018-OPERATING-CO-007            
                    AND     (LIST_SEQ_NBR                                       
                               BETWEEN       :PV-LIST-SEQ-NBR                   
                               AND           :PV-MAX-SEQ-NBR)                   
                    AND      END_DTE      >  :MB018-NEW-START-DATE-007)         
               ORDER BY                                                         
                             LIST_SEQ_NBR                                       
           END-EXEC.                                                            
      *        FOR UPDATE OF                                                    
      *                      END_DTE                                            
      *                    , CHG_DTE                                            
                                                                                
      *----------------------------------------------------------------*        
      *    APLAVL LIST CURSOR. (MDSEAR-DKEY'S)                                  
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE APLAVL-LIST CURSOR                                       
                   FOR SELECT                                                   
                               APLSYS_CDE                                       
                            ,  MDSEAR_DKEY                                      
                            ,  MDSEAR_STRT_DTE                                  
                            ,  MDSEAR_END_DTE                                   
                            ,  OPERCO_NBR                                       
               FROM                                                             
                    TAPLAVL                                                     
                  WHERE                                                         
                           (OPERCO_NBR     = :MB018-OPERATING-CO-007            
                    AND     MDSEAR_DKEY    = :MDSEAR-DKEY                       
                    AND     MDSEAR_END_DTE > :MB018-NEW-START-DATE-007)         
               FOR UPDATE OF                                                    
                            MDSEAR_STRT_DTE,                                    
                            MDSEAR_END_DTE                                      
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    RSPBAR LIST CURSOR. (MDSEAR-DKEY'S)                                  
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE RSPBAR-LIST CURSOR                                       
                   FOR SELECT                                                   
                               MDSEAR_DKEY                                      
                            ,  WORKER_NBR                                       
                            ,  WORKER_STRT_DTE                                  
                            ,  WORKER_END_DTE                                   
                            ,  RSPLVL_CDE                                       
                            ,  WORKER_FIR_NAME                                  
                            ,  WORKER_LAST_NAME                                 
                            ,  OPERCO_NBR                                       
               FROM                                                             
                    TRSPBAR                                                     
                  WHERE                                                         
                           (OPERCO_NBR     = :MB018-OPERATING-CO-007            
                    AND     MDSEAR_DKEY    = :MDSEAR-DKEY                       
                    AND     WORKER_END_DTE > :MB018-NEW-START-DATE-007)         
               FOR UPDATE OF                                                    
                            WORKER_END_DTE                                      
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    MDSEDP LIST CURSOR. (MDSEAR-DKEY'S)                                  
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE MDSEDP-LIST CURSOR                                       
                   FOR SELECT                                                   
                               MDSEAR_DKEY                                      
                            ,  CHG_DTE                                          
                            ,  MDSEBG_NBR                                       
                            ,  DPTDIS_CDE                                       
                            ,  ARDEPT_CHG_DTE                                   
                            ,  ARDEPT_DESC                                      
                            ,  ZOUT_EXCL_IND                                    
                            ,  CONS_IND                                         
                            ,  PLAN_FRGT_PCT                                    
                            ,  SKU_LVL_UPD_IND                                  
               FROM                                                             
                    TMDSEDP                                                     
                  WHERE                                                         
                             MDSEAR_DKEY  =  :MDSEAR-DKEY                       
               FOR UPDATE OF                                                    
                            CHG_DTE                                             
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    MDSECL LIST CURSOR. (MDSEAR-DKEY'S)                                  
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE MDSECL-LIST CURSOR                                       
                   FOR SELECT                                                   
                               MDSEAR_DKEY                                      
                            ,  POS_DESC                                         
                            ,  TAX_DFLT_IND                                     
                            ,  PRT_VEND_UPC_IND                                 
               FROM                                                             
                    TMDSECL                                                     
                  WHERE                                                         
                             MDSEAR_DKEY  =  :MDSEAR-DKEY                       
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    DPTSHK LIST CURSOR. (MDSEAR-DKEY'S)                                  
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
               DECLARE DPTSHK-LIST CURSOR                                       
                   FOR SELECT                                                   
                               MDSEAR_DKEY                                      
                            ,  SHRINK_YR_NBR                                    
                            ,  CHG_TMST                                         
                            ,  JAN_SHRINK_PCT                                   
                            ,  FEB_SHRINK_PCT                                   
                            ,  MAR_SHRINK_PCT                                   
                            ,  APR_SHRINK_PCT                                   
                            ,  MAY_SHRINK_PCT                                   
                            ,  JUN_SHRINK_PCT                                   
                            ,  JUL_SHRINK_PCT                                   
                            ,  AUG_SHRINK_PCT                                   
                            ,  SEP_SHRINK_PCT                                   
                            ,  OCT_SHRINK_PCT                                   
                            ,  NOV_SHRINK_PCT                                   
                            ,  DEC_SHRINK_PCT                                   
C14666                      ,  E_BUS_IND                                        
C14666                      ,  SHNK_PCT_CHG_DTE                                 
C14666                      ,  LAST_UPD_BY_USR                                  
               FROM                                                             
                    TDPTSHK                                                     
                  WHERE                                                         
                             MDSEAR_DKEY  =  :MDSEAR-DKEY                       
               FOR UPDATE OF                                                    
                            CHG_TMST                                            
           END-EXEC.                                                            
                                                                                
      /                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-MAINLINE.                                               
           GOBACK.                                                              
       1000-MAINLINE.                                                           
                                                                                
           PERFORM 2000-OPEN-BATCH-CONTROL.                                     
           PERFORM 2500-OBTAIN-MAX-DKEY.                                        
           IF  PV-GOOD-STATUS                                                   
               PERFORM 3000-PROCESS-BATCH-CONTROL                               
                   UNTIL NOT MB018-MBS-REALIGNMENT                              
               PERFORM 6000-CLOSE-BATCH-CONTROL                                 
               PERFORM 9000-DISPLAY-TOTALS                                      
           ELSE                                                                 
               DISPLAY '*******************************'                        
               DISPLAY '*                             *'                        
               DISPLAY '* NO MBS REALIGNMENT RECORDS  *'                        
               DISPLAY '*                             *'                        
               DISPLAY '* FOR PROCESSING IN MBKUT007. *'                        
               DISPLAY '*                             *'                        
               DISPLAY '*   ***NORMAL END OF JOB***   *'                        
               DISPLAY '*******************************'                        
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *                                                                *        
      *    THIS ROUTINE OPENS THE BATCH CONTROL FILE AND POSITIONS IT  *        
      *    TO THE FIRST MBS REALIGNMENT RECORD.                        *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2000-OPEN-BATCH-CONTROL.                                                 
                                                                                
           OPEN I-O BATCH-CONTROL-FILE.                                         
                                                                                
           IF (PV-GOOD-STATUS                                                   
           OR  PV-AT-END                                                        
           OR  PV-VERIFIED)                                                     
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***MBKUT007 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   INVALID STATUS ON THE BATCH *'                      
               DISPLAY '*   CONTROL DATASET - OPEN.     *'                      
               DISPLAY '*                               *'                      
               DISPLAY '* STATUS = '                                            
                       PV-VSAM-STATUS                                           
                       '                  *'                                    
               DISPLAY '*                               *'                      
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               SET ABEND-4005        TO  TRUE                                   
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
           END-IF.                                                              
                                                                                
           MOVE LOW-VALUES           TO  MB018-BCR-KEY.                         
           SET MB018-MBS-REALIGNMENT TO  TRUE.                                  
           MOVE MB018-BCR-KEY-006    TO  BCR-KEY.                               
           START BATCH-CONTROL-FILE                                             
                 KEY IS GREATER THAN BCR-KEY.                                   
                                                                                
           IF  PV-GOOD-STATUS                                                   
               PERFORM 3100-READ-BATCH-CONTROL                                  
           ELSE                                                                 
               IF  PV-NO-RECORDS                                                
                   CONTINUE                                                     
               ELSE                                                             
                   DISPLAY '* * * * * * * * * * * * * * * * *'                  
                   DISPLAY '*    ***MBKUT007 ABEND***       *'                  
                   DISPLAY '*                               *'                  
                   DISPLAY '*   INVALID STATUS ON THE BATCH *'                  
                   DISPLAY '*   CONTROL DATASET - START.    *'                  
                   DISPLAY '*                               *'                  
                   DISPLAY '* STATUS = '                                        
                           PV-VSAM-STATUS                                       
                           '                  *'                                
                   DISPLAY '*                               *'                  
                   DISPLAY '* * * * * * * * * * * * * * * * *'                  
                   SET ABEND-4002        TO  TRUE                               
                   CALL 'ILBOABN0'    USING  ABEND-CODE                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    OBTAIN THE MAXIMUM DATALESS KEY.                            *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       2500-OBTAIN-MAX-DKEY.                                                    
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  SELECT                                                        
                       MAX(MDSEAR_DKEY)                                         
                  INTO                                                          
                       :PV-NEW-DKEY                                             
                  FROM                                                          
                       TMDSEAR                                                  
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '2500-OBTAIN-MAX-DKEY'                              
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'SELECT MAX DKEY'                                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '2500-OBTAIN-MAX-DKEY'                                      
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'SELECT MAX DKEY TRIES EXCEEDED'                            
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    PROCESS THE BATCH CONTROL RECORD, VALIDATE THE DATA, IF     *        
      *    OK, UPDATE MDSEAR.                                          *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       3000-PROCESS-BATCH-CONTROL.                                              
                                                                                
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'.         
           DISPLAY '* CONTROL RECORD:'.                                         
           DISPLAY '* ' MB018-BATCH-CNTL-RECORD.                                
           DISPLAY '*'.                                                         
           ADD 1                     TO  PV-BATCH-CNTL-COUNT.                   
                                                                                
           SET PS-CONTINUE           TO  TRUE.                                  
           PERFORM 3200-VALIDATE-DATA.                                          
           PERFORM 3900-OBTAIN-PARENT-DATES.                                    
                                                                                
           IF  PS-CONTINUE                                                      
               PERFORM 4000-UPDATE-MDSEAR                                       
           ELSE                                                                 
               IF PS-CURRENT-START-DATE                                         
                   MOVE 4008             TO  RETURN-CODE                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 3800-DELETE-BATCH-CONTROL.                                   
           PERFORM 3100-READ-BATCH-CONTROL.                                     
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    HERE WE READ THE BATCH CONTROL FILE.                        *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       3100-READ-BATCH-CONTROL.                                                 
                                                                                
           READ BATCH-CONTROL-FILE                                              
                INTO MB018-BATCH-CNTL-RECORD.                                   
           IF  PV-GOOD-STATUS                                                   
           OR  PV-AT-END                                                        
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***MBKUT007 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   INVALID STATUS ON THE BATCH *'                      
               DISPLAY '*   CONTROL DATASET - READ.     *'                      
               DISPLAY '*                               *'                      
               DISPLAY '* STATUS = '                                            
                       PV-VSAM-STATUS                                           
                       '                  *'                                    
               DISPLAY '*                               *'                      
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               SET ABEND-4002        TO  TRUE                                   
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
           END-IF.                                                              
                                                                                
           IF  PV-AT-END                                                        
               MOVE LOW-VALUE        TO  MB018-BATCH-CNTL-RECORD                
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    ENSURE THE DATA, ALBEIT PREVIOUSLY VALIDATED ON-LINE,       *        
      *    IS OKAY.                                                    *        
      *    FUTURE-START-DATES ARE SET TO NOT PROCESS DURING THIS RUN.  *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       3200-VALIDATE-DATA.                                                      
                                                                                
           IF  MB018-OPERATING-CO-007 NOT NUMERIC                               
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* COMPANY NUMBER NOT NUMERIC = '                        
                       MB018-OPERATING-CO-007 '.'                               
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-BUYER-NUMBER-007 GREATER THAN SPACES                   
               IF  MB018-NEW-BUYER-NUMBER-007 NUMERIC                           
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* BUYER NUMBER NOT NUMERIC = '                      
                       MB018-NEW-BUYER-NUMBER-007 '.'                           
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *    BEGIN THE DATE CHECK                                                 
      *                                                                         
                                                                                
           INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                 
                                                                                
           SET PS-VALID-DATE                TO TRUE.                            
           ACCEPT PV-CURRENT-DATE           FROM DATE.                          
                                                                                
           SET DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                            
           SET DPG52-LK-DTE-GREG            TO TRUE.                            
           MOVE PV-CD-MM                    TO DPG52-GREG-DATE-MO-X.            
           MOVE PV-CD-DD                    TO DPG52-GREG-DATE-DY-X.            
           MOVE PV-CD-YY                    TO DPG52-GREG-DATE-YR-X.            
                                                                                
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                                   
                 USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56.                
                                                                                
           IF DPG54-SEVERE-ERROR OR DPG54-WARNING-ERROR                         
              DISPLAY '* * * * * * * * * * * * * * * * *'                       
              DISPLAY '*    ***MBKUT007-1 ABEND***     *'                       
              DISPLAY '*                               *'                       
              DISPLAY '*                               *'                       
              DISPLAY '*  INVALID RETURN FROM DPKUT500 *'                       
              DISPLAY '* * * * * * * * * * * * * * * * *'                       
              DISPLAY 'ERROR IND.:  '      DPG54-ERROR-IND                      
              DISPLAY 'DATE IND.:   '      DPG54-DATE-VALID-IND                 
              DISPLAY 'ERROR MSG:   '      DPG54-ERROR-MESSAGE                  
              SET PS-INVALID-DATE TO  TRUE                                      
           ELSE                                                                 
              MOVE DPG55-DB2-ISO-DATE-FMT TO PV-DB2-CHANGE-DATE                 
           END-IF.                                                              
           MOVE PV-DB2-CHANGE-DATE        TO PV-DB2-WORK-DATE.                  
                                                                                
           MOVE MB018-NEW-START-DATE-007  TO  PV-DB2-DATE.                      
                                                                                
           ADD 1                          TO  PV-WD-CCYY.                       
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER PV-DB2-WORK-DATE                
               SET PS-INVALID-DATE   TO  TRUE                                   
               DISPLAY '* DATE MORE THAN 1 YEAR IN FUTURE. DATE = '             
                       MB018-NEW-START-DATE-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF  PS-INVALID-DATE                                                  
               SET PS-DONT-PROCESS   TO  TRUE                                   
           ELSE                                                                 
                                                                                
               INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56              
                                                                                
               SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                           
               SET DPG52-LK-DTE-ISO-FMT       TO TRUE                           
               MOVE MB018-NEW-START-DATE-007  TO DPG52-DB2-ISO-DATE-FMT         
                                                                                
               CALL  DP500-KOHLS-CALENDAR-ROUTINE                               
                     USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56             
                                                                                
               IF  DPG54-SEVERE-ERROR OR DPG54-WARNING-ERROR                    
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   SET PS-INVALID-DATE                                          
                                     TO  TRUE                                   
                   DISPLAY '* INVALID DATE. DATE = '                            
                       MB018-NEW-START-DATE-007 '.'                             
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *    IF THE NEW-START-DATE IS IN THE FUTURE (GREATER THAN TODAY),         
      *    DO NOT PROCESS THIS RECORD DURING TODAY'S RUN.                       
      *                                                                         
           SET PS-CURRENT-START-DATE TO TRUE.                                   
           IF  PS-VALID-DATE                                                    
               IF  MB018-NEW-START-DATE-007 GREATER PV-DB2-CHANGE-DATE          
                   SET PS-FUTURE-START-DATE TO TRUE                             
                   SET PS-DONT-PROCESS TO  TRUE                                 
                   DISPLAY '* START DATE HAS NOT BEEN REACHED. DATE = '         
                           MB018-NEW-START-DATE-007 '.'                         
                   DISPLAY '* REALIGN WILL BE PROCESSED ON START DATE.'         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *    IF THE DATE IS OK, BUILD THE END DATE FOR "OLD" ROWS                 
      *                                                                         
           IF  PS-VALID-DATE                                                    
               INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56              
                                                                                
               SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                           
               SET DPG51-DECREMENT-DATE       TO TRUE                           
               MOVE 1                         TO DPG51-INCR-DECR-DAYS-9         
               SET DPG52-LK-DTE-ISO-FMT       TO TRUE                           
               MOVE MB018-NEW-START-DATE-007  TO DPG52-DB2-ISO-DATE-FMT         
                                                                                
               CALL  DP500-KOHLS-CALENDAR-ROUTINE                               
                     USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56             
                                                                                
               IF  DPG54-NO-ERROR                                               
                   MOVE DPG55-DB2-ISO-DATE-FMT TO PV-END-DATE-MINUS1            
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
      *    REASONABILITY CHECK FOR NEW DATA.                                    
      *                                                                         
           IF  MB018-NEW-GMA-007 LESS THAN ZERO                                 
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* INVALID NEW GMA NUMBER = '                            
                       MB018-NEW-GMA-007 '.'                                    
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-DEPT-007 GREATER THAN SPACES                           
           AND MB018-NEW-DMA-007 LESS THAN ZERO)                                
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* NEW DEPT PRESENT WITHOUT DMA.  NEW = '                
                       MB018-NEW-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-MAJ-CL-007 GREATER THAN SPACES                         
           AND MB018-NEW-DEPT-007 LESS THAN ZERO)                               
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* NEW MAJCL PRESENT WITHOUT DEPT. NEW = '               
                       MB018-NEW-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-SUB-CL-007 GREATER THAN SPACES                         
           AND MB018-NEW-MAJ-CL-007 LESS THAN ZERO)                             
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* NEW SUBCL PRESENT WITHOUT MAJCL. NEW = '              
                       MB018-NEW-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
      *                                                                         
      *    REASONABILITY CHECK FOR OLD DATA.                                    
      *                                                                         
           IF  MB018-OLD-GMA-007 LESS THAN ZERO                                 
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* INVALID OLD GMA NUMBER = '                            
                       MB018-OLD-GMA-007 '.'                                    
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-OLD-DEPT-007 GREATER THAN SPACES                           
           AND MB018-OLD-DMA-007 LESS THAN ZERO)                                
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* OLD DEPT PRESENT WITHOUT DMA.  OLD = '                
                       MB018-OLD-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-OLD-MAJ-CL-007 GREATER THAN SPACES                         
           AND MB018-OLD-DEPT-007 LESS THAN ZERO)                               
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* OLD MAJCL PRESENT WITHOUT DEPT. OLD = '               
                       MB018-OLD-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF (MB018-OLD-SUB-CL-007 GREATER THAN SPACES                         
           AND MB018-OLD-MAJ-CL-007 LESS THAN ZERO)                             
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* OLD SUBCL PRESENT WITHOUT MAJCL. OLD = '              
                       MB018-OLD-MERCH-AREA-007 '.'                             
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           IF  PS-CONTINUE                                                      
               PERFORM 3300-VALIDATE-LEVEL                                      
               PERFORM 3400-EXISTANCE-CHECK                                     
           END-IF.                                                              
                                                                                
           IF  PS-CONTINUE                                                      
               PERFORM 3500-CHECK-EFFECTIVE-DATE                                
               IF  PV-DB2-COUNT GREATER THAN ZERO                               
                   SET PS-DONT-PROCESS   TO  TRUE                               
                   DISPLAY                                                      
                       '* MDSE AREA EXPIRES BEFORE EFFECTIVE DATE. '            
                       MB018-NEW-MERCH-AREA-007 '.'                             
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    CHECK THAT THE LEVELS FOR OLD AND NEW MATCH.                *        
      *                                                                *        
      *    PERFORMED BY 3200                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3300-VALIDATE-LEVEL.                                                     
                                                                                
           IF (MB018-NEW-SUB-CL-007 GREATER THAN SPACES                         
           OR  MB018-OLD-SUB-CL-007 GREATER THAN SPACES)                        
               IF (MB018-NEW-SUB-CL-007 GREATER THAN SPACES                     
               AND MB018-OLD-SUB-CL-007 GREATER THAN SPACES)                    
                                                                                
                   MOVE MB018-NEW-SUB-CL-007                                    
                                     TO  PV-NEW-SUB-CL                          
                   MOVE MB018-OLD-SUB-CL-007                                    
                                     TO  PV-OLD-SUB-CL                          
                   MOVE MB018-NEW-MAJ-CL-007                                    
                                     TO  PV-NEW-MAJ-CL                          
                   MOVE MB018-OLD-MAJ-CL-007                                    
                                     TO  PV-OLD-MAJ-CL                          
                                                                                
                   IF ((PV-NEW-SUB-CL-BYTE2 EQUAL                               
                       PV-NEW-MAJ-CL-BYTE1                                      
                   AND PV-NEW-SUB-CL-BYTE1 EQUAL ZERO)                          
                                                                                
                   AND (PV-OLD-SUB-CL-BYTE2 EQUAL                               
                       PV-OLD-MAJ-CL-BYTE1                                      
                   AND PV-OLD-SUB-CL-BYTE1 EQUAL ZERO))                         
                       CONTINUE                                                 
                                                                                
                   ELSE                                                         
                       SET PS-DONT-PROCESS                                      
                                     TO  TRUE                                   
                       DISPLAY '* SUBCL OUT OF RANGE FOR MAJCL. '               
                           MB018-NEW-MAJ-CL-007 '-'                             
                           MB018-NEW-SUB-CL-007 '.'                             
                       DISPLAY '*'                                              
                   END-IF                                                       
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* OLD AND NEW MERCH AREA MUST BE SAME LEVEL'        
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                       DISPLAY '*'                                              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-MAJ-CL-007 GREATER THAN SPACES                         
           OR  MB018-OLD-MAJ-CL-007 GREATER THAN SPACES)                        
               IF (MB018-NEW-MAJ-CL-007 GREATER THAN SPACES                     
               AND MB018-OLD-MAJ-CL-007 GREATER THAN SPACES)                    
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* OLD AND NEW MERCH AREA MUST BE SAME LEVEL'        
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-DEPT-007 GREATER THAN SPACES                           
           OR  MB018-OLD-DEPT-007 GREATER THAN SPACES)                          
               IF (MB018-NEW-DEPT-007 GREATER THAN SPACES                       
               AND MB018-OLD-DEPT-007 GREATER THAN SPACES)                      
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* OLD AND NEW MERCH AREA MUST BE SAME LEVEL'        
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-DMA-007 GREATER THAN SPACES                            
           OR  MB018-OLD-DMA-007 GREATER THAN SPACES)                           
               IF (MB018-NEW-DMA-007 GREATER THAN SPACES                        
               AND MB018-OLD-DMA-007 GREATER THAN SPACES)                       
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* OLD AND NEW MERCH AREA MUST BE SAME LEVEL'        
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MB018-NEW-GMA-007 GREATER THAN SPACES                            
           OR  MB018-OLD-GMA-007 GREATER THAN SPACES)                           
               IF (MB018-NEW-GMA-007 GREATER THAN SPACES                        
               AND MB018-OLD-GMA-007 GREATER THAN SPACES)                       
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY '* OLD AND NEW MERCH AREA MUST BE SAME LEVEL'        
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF (MB018-OLD-SUB-CL-007 GREATER THAN SPACES                         
           AND MB018-OLD-MAJ-CL-007 GREATER THAN SPACES)                        
               IF (MB018-OLD-GMA-007  EQUAL MB018-NEW-GMA-007                   
               AND MB018-OLD-DMA-007  EQUAL MB018-NEW-DMA-007                   
               AND MB018-OLD-DEPT-007 EQUAL MB018-NEW-DEPT-007)                 
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
                   DISPLAY                                                      
                       '* CLASS REALIGHMENT MUST BE SAME GMA, DMA, DPT'         
                           MB018-OLD-MERCH-AREA-007 '-'                         
                           MB018-NEW-MERCH-AREA-007 '.'                         
                   DISPLAY '*'                                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    ENSURE THE OLD DATA EXISTS AND THE NEW DATA DOES NOT.       *        
      *                                                                *        
      *    PERFORMED BY 3200                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3400-EXISTANCE-CHECK.                                                    
      *                                                                         
      *    BUILD THE LIST SEQUENCE NUMBER FOR THE "NEW" DATA.                   
      *                                                                         
           MOVE MB018-NEW-MERCH-AREA-007                                        
                                     TO  PV-CONTROL-DATA.                       
           MOVE ZERO                 TO  PV-WS-ZERO.                            
                                                                                
           IF  MB018-NEW-SUB-CL-007 LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-SUB-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-MAJ-CL-007 LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-MAJ-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-DEPT-007 LESS THAN ZERO                                
               MOVE ZERO             TO  PV-WS-DEPT                             
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-DMA-007  LESS THAN ZERO                                
               MOVE ZERO             TO  PV-WS-DMA                              
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-GMA-007 LESS THAN ZERO                                 
               MOVE ZERO             TO  PV-WS-GMA                              
           END-IF.                                                              
                                                                                
           MOVE PV-WORK-SEQ-NBR      TO  PV-LIST-SEQ-NBR.                       
      *                                                                         
      *    NOW THE MAX SEQUENCE NUMBER.                                         
      *                                                                         
           MOVE PC-NINES             TO  PV-WS-ZERO.                            
           IF  MB018-NEW-SUB-CL-007 LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-SUB-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-MAJ-CL-007 LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-MAJ-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-DEPT-007 LESS THAN ZERO                                
               MOVE PC-NINES         TO  PV-WS-DEPT                             
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-DMA-007  LESS THAN ZERO                                
               MOVE PC-NINES         TO  PV-WS-DMA                              
           END-IF.                                                              
                                                                                
           IF  MB018-NEW-GMA-007 LESS THAN ZERO                                 
               MOVE PC-NINES         TO  PV-WS-GMA                              
           END-IF.                                                              
                                                                                
           MOVE PV-WORK-SEQ-NBR      TO  PV-MAX-SEQ-NBR.                        
                                                                                
           PERFORM 3600-CHECK-EXISTANCE.                                        
                                                                                
           IF  PV-DB2-COUNT GREATER THAN ZERO                                   
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '* NEW MERCHANDISE AREA ALREADY EXISTS'                  
                           MB018-NEW-MERCH-AREA-007 '.'                         
               DISPLAY '*'                                                      
           END-IF.                                                              
      *                                                                         
      *    BUILD THE LIST SEQUENCE NUMBER FOR THE "OLD" DATA.                   
      *                                                                         
           MOVE MB018-OLD-MERCH-AREA-007                                        
                                     TO  PV-CONTROL-DATA.                       
           MOVE MB018-OPERATING-CO-007                                          
                                     TO  PV-WS-OPERCO.                          
           MOVE ZERO                 TO  PV-WS-ZERO.                            
                                                                                
           IF  MB018-OLD-SUB-CL-007 LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-SUB-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-MAJ-CL-007 LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-MAJ-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-DEPT-007   LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-DEPT                             
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-DMA-007    LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-DMA                              
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-GMA-007    LESS THAN ZERO                              
               MOVE ZERO             TO  PV-WS-GMA                              
           END-IF.                                                              
                                                                                
           MOVE PV-WORK-SEQ-NBR      TO  PV-LIST-SEQ-NBR.                       
      *                                                                         
      *    NOW THE MAX SEQUENCE NUMBER.                                         
      *                                                                         
           MOVE PC-NINES             TO  PV-WS-ZERO.                            
           IF  MB018-OLD-SUB-CL-007 LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-SUB-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-MAJ-CL-007 LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-MAJ-CL                           
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-DEPT-007   LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-DEPT                             
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-DMA-007    LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-DMA                              
           END-IF.                                                              
                                                                                
           IF  MB018-OLD-GMA-007    LESS THAN ZERO                              
               MOVE PC-NINES         TO  PV-WS-GMA                              
           END-IF.                                                              
                                                                                
           MOVE PV-WORK-SEQ-NBR      TO  PV-MAX-SEQ-NBR.                        
                                                                                
           PERFORM 3600-CHECK-EXISTANCE.                                        
           IF  PV-DB2-COUNT EQUAL ZERO                                          
               SET PS-DONT-PROCESS TO  TRUE                                     
               DISPLAY '* OLD MERCHANDISE AREA DOES NOT EXIST-'                 
                           MB018-OLD-MERCH-AREA-007 '.'                         
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
           PERFORM 3700-CHECK-PARENT.                                           
           IF  PV-DB2-COUNT EQUAL ZERO                                          
               SET PS-DONT-PROCESS TO  TRUE                                     
               DISPLAY '* PARENT NOT FOUND FOR NEW MERCHANDISE AREA-'           
                           MB018-NEW-MERCH-AREA-007 '.'                         
               DISPLAY '*'                                                      
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    DETERMINE IF THE EFFECTIVE DATE IS PROPER.                  *        
      *    (TO CHECK IF OLD ROW EXPIRES PRIOR TO NEW ROW START DATE.)  *        
      *    PERFORMED BY 3300                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3500-CHECK-EFFECTIVE-DATE.                                               
                                                                                
           MOVE ZERO                 TO  PV-DB2-COUNT.                          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                           COUNT(DISTINCT GMA_NBR)                              
                    INTO                                                        
                           :PV-DB2-COUNT                                        
                    FROM                                                        
                           TMDSEAR                                              
                    WHERE                                                       
                           OPERCO_NBR     = :MB018-OPERATING-CO-007             
                      AND (LIST_SEQ_NBR                                         
                           BETWEEN          :PV-LIST-SEQ-NBR                    
                           AND              :PV-MAX-SEQ-NBR)                    
                      AND (END_DTE                                              
                           BETWEEN          (CURRENT DATE)                      
                           AND              :PV-DB2-DATE)                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '3500-CHECK-EFFECTIVE-DATE'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COUNT GMA'                                         
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3500-CHECK-EFFECTIVE-DATE'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'COUNT RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    CHECK IF THE NEW STRUCTURE ALREADY EXISTS.                  *        
      *                                                                *        
      *    PERFORMED BY 3300                                           *        
      *----------------------------------------------------------------*        
                                                                                
       3600-CHECK-EXISTANCE.                                                    
                                                                                
           MOVE ZERO                 TO  PV-DB2-COUNT.                          
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                    SELECT                                                      
                           COUNT(DISTINCT GMA_NBR)                              
                    INTO                                                        
                           :PV-DB2-COUNT                                        
                    FROM                                                        
                           TMDSEAR                                              
                    WHERE                                                       
                           OPERCO_NBR     = :MB018-OPERATING-CO-007             
                      AND (LIST_SEQ_NBR                                         
                           BETWEEN          :PV-LIST-SEQ-NBR                    
                           AND              :PV-MAX-SEQ-NBR)                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '3600-CHECK-EXISTANCE'                              
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COUNT GMA'                                         
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3600-CHECK-EXISTANCE'                                      
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'COUNT RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *    ENSURE THE PARENT EXISTS.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       3700-CHECK-PARENT.                                                       
                                                                                
           MOVE ZERO                 TO  PV-DB2-COUNT.                          
           MOVE ZERO                 TO  MDSEAR-SUB-CL-NBR.                     
           MOVE MB018-NEW-MAJ-CL-007 TO  MDSEAR-MAJ-CL-NBR.                     
           MOVE MB018-NEW-DEPT-007   TO  MDSEAR-DEPT-NBR.                       
           MOVE MB018-NEW-DMA-007    TO  MDSEAR-DMA-NBR.                        
           MOVE MB018-NEW-GMA-007    TO  MDSEAR-GMA-NBR.                        
                                                                                
           SET PV-MCL-LIT            TO  TRUE.                                  
           IF  MB018-NEW-SUB-CL-007 GREATER THAN ZERO                           
               CONTINUE                                                         
           ELSE                                                                 
               IF  MB018-NEW-MAJ-CL-007 LESS THAN ZERO                          
                   MOVE ZERO         TO  MDSEAR-MAJ-CL-NBR                      
                   MOVE ZERO         TO  MDSEAR-DEPT-NBR                        
                   SET PV-DMA-LIT    TO  TRUE                                   
                   IF  MB018-NEW-DEPT-007 LESS THAN ZERO                        
                       SET PV-GMA-LIT                                           
                                     TO  TRUE                                   
                       MOVE ZERO     TO  MDSEAR-DMA-NBR                         
                       IF  MB018-NEW-DMA-007 LESS THAN ZERO                     
                           MOVE ZERO TO  MDSEAR-GMA-NBR                         
                           SET PV-CO-LIT                                        
                                     TO  TRUE                                   
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                   MOVE ZERO         TO  MDSEAR-MAJ-CL-NBR                      
                   SET PV-DPT-LIT    TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
      *                                                                         
               EXEC SQL                                                         
                    SELECT                                                      
                           COUNT(DISTINCT OPERCO_NBR)                           
                    INTO                                                        
                           :PV-DB2-COUNT                                        
                    FROM                                                        
                           TMDSEAR                                              
                    WHERE                                                       
                           OPERCO_NBR     = :PC-COMPANY                         
                      AND  GMA_NBR        = :MDSEAR-GMA-NBR                     
                      AND  DMA_NBR        = :MDSEAR-DMA-NBR                     
                      AND  DEPT_NBR       = :MDSEAR-DEPT-NBR                    
                      AND  MAJ_CL_NBR     = :MDSEAR-MAJ-CL-NBR                  
                      AND  SUB_CL_NBR     = :MDSEAR-SUB-CL-NBR                  
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                       MOVE '3700-CHECK-PARENT'                                 
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COUNT FOR PARENTAGE'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3700-CHECK-PARENT'                                         
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'COUNT RETRIES EXCEEDED'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    DELETE THE CURRENT BATCH CONTROL RECORD.                  *          
      *    (UNLESS THE CURRENT RECORD HAD A FUTURE-START-DATE.)      *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       3800-DELETE-BATCH-CONTROL.                                               
      *                                                                         
           IF  PS-CONTINUE                                                      
               DISPLAY '* '                                                     
                   PV-INSERT-COUNT ' TMDSEAR INSERTED'                          
               DISPLAY '* '                                                     
                   PV-UPDATE-COUNT ' TMDSEAR UPDATED'                           
               DISPLAY '* '                                                     
                   PV-DELETE-COUNT ' TMDSEAR DELETED'                           
               DISPLAY '* '                                                     
               DISPLAY '* '                                                     
                   PV-INSERT-APLAVL-COUNT ' TAPLAVL INSERTED'                   
               DISPLAY '* '                                                     
                   PV-UPDATE-APLAVL-COUNT ' TAPLAVL UPDATED'                    
               DISPLAY '* '                                                     
                   PV-DELETE-APLAVL-COUNT ' TAPLAVL DELETED'                    
               DISPLAY '* '                                                     
               DISPLAY '* '                                                     
                   PV-INSERT-RSPBAR-COUNT ' TRSPBAR INSERTED'                   
               DISPLAY '* '                                                     
                   PV-UPDATE-RSPBAR-COUNT ' TRSPBAR UPDATED'                    
               DISPLAY '* '                                                     
                   PV-DELETE-RSPBAR-COUNT ' TRSPBAR DELETED'                    
               DISPLAY '* '                                                     
               DISPLAY '* '                                                     
                   PV-INSERT-MDSEDP-COUNT ' TMDSEDP INSERTED'                   
               DISPLAY '* '                                                     
                   PV-UPDATE-MDSEDP-COUNT ' TMDSEDP UPDATED'                    
               DISPLAY '* '                                                     
                   PV-INSERT-MDSECL-COUNT ' TMDSECL INSERTED'                   
               DISPLAY '* '                                                     
                   PV-INSERT-DPTSHK-COUNT ' TDPTSHK INSERTED'                   
               DISPLAY '* '                                                     
                   PV-UPDATE-DPTSHK-COUNT ' TDPTSHK UPDATED'                    
               DISPLAY '* '                                                     
               DISPLAY '* FOR THIS CONTROL RECORD.'                             
               DISPLAY '*'                                                      
                                                                                
               ADD PV-INSERT-COUNT   TO  PV-TOT-INSERTS                         
               ADD PV-UPDATE-COUNT   TO  PV-TOT-UPDATES                         
               ADD PV-DELETE-COUNT   TO  PV-TOT-DELETES                         
               ADD PV-INSERT-APLAVL-COUNT                                       
                                     TO  PV-TOT-APLAVLS                         
               ADD PV-DELETE-APLAVL-COUNT                                       
                                     TO  PV-TOT-APLAVLS-DELETED                 
               ADD PV-UPDATE-APLAVL-COUNT                                       
                                     TO  PV-TOT-APLAVLS-UPDATED                 
               ADD PV-INSERT-RSPBAR-COUNT                                       
                                     TO  PV-TOT-RSPBARS                         
               ADD PV-DELETE-RSPBAR-COUNT                                       
                                     TO  PV-TOT-RSPBARS-DELETED                 
               ADD PV-UPDATE-RSPBAR-COUNT                                       
                                     TO  PV-TOT-RSPBARS-UPDATED                 
               ADD PV-INSERT-MDSEDP-COUNT                                       
                                     TO  PV-TOT-MDSEDPS                         
               ADD PV-UPDATE-MDSEDP-COUNT                                       
                                     TO  PV-TOT-MDSEDPS-UPDATED                 
               ADD PV-INSERT-MDSECL-COUNT                                       
                                     TO  PV-TOT-MDSECLS                         
               ADD PV-INSERT-DPTSHK-COUNT                                       
                                     TO  PV-TOT-DPTSHKS                         
               ADD PV-UPDATE-DPTSHK-COUNT                                       
                                     TO  PV-TOT-DPTSHKS-UPDATED                 
           END-IF.                                                              
      *                                                                         
           MOVE ZERO                 TO  PV-INSERT-COUNT                        
                                         PV-UPDATE-COUNT                        
                                         PV-DELETE-COUNT                        
                                         PV-INSERT-APLAVL-COUNT                 
                                         PV-DELETE-APLAVL-COUNT                 
                                         PV-UPDATE-APLAVL-COUNT                 
                                         PV-INSERT-RSPBAR-COUNT                 
                                         PV-DELETE-RSPBAR-COUNT                 
                                         PV-UPDATE-RSPBAR-COUNT                 
                                         PV-INSERT-MDSEDP-COUNT                 
                                         PV-UPDATE-MDSEDP-COUNT                 
                                         PV-INSERT-MDSECL-COUNT                 
                                         PV-INSERT-DPTSHK-COUNT                 
                                         PV-UPDATE-DPTSHK-COUNT                 
                                                                                
      *                                                                         
      * IF CURRENT-START-DATE, DELETE VSAM RECORD; ELSE                         
      * IF FUTURE-START-DATE, DO NOT DELETE VSAM RECORD.                        
      *                                                                         
           IF PS-CURRENT-START-DATE                                             
               DELETE BATCH-CONTROL-FILE RECORD                                 
               IF  PV-GOOD-STATUS                                               
                   CONTINUE                                                     
               ELSE                                                             
                   DISPLAY '* * * * * * * * * * * * * * * * *'                  
                   DISPLAY '*    ***MBKUT007 ABEND***       *'                  
                   DISPLAY '*                               *'                  
                   DISPLAY '*   INVALID STATUS ON THE BATCH *'                  
                   DISPLAY '*   CONTROL DATASET - DELETE    *'                  
                   DISPLAY '*                               *'                  
                   DISPLAY '* STATUS = '                                        
                           PV-VSAM-STATUS                                       
                           '                  *'                                
                   DISPLAY '*                               *'                  
                   DISPLAY '* * * * * * * * * * * * * * * * *'                  
                   SET ABEND-4003        TO  TRUE                               
                   CALL 'ILBOABN0'    USING  ABEND-CODE                         
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    READ THE PARENT ROW FOR THE NEW DATA.  SAVE THE           *          
      *    DATES TO ENSURE ALL NEWLY ADDED ROWS EXIST WITHIN         *          
      *    THEIR PARENT.  FIRST, DETERMINE WHICH LEVEL WE NEED       *          
      *    AND THEN ISSUE THE SQL.                                   *          
      *                                                              *          
      *    PERFORMED BY 3000                                         *          
      ****************************************************************          
      *                                                                         
       3900-OBTAIN-PARENT-DATES.                                                
      *                                                                         
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                    SELECT STRT_DTE                                             
                         , END_DTE                                              
                         , MDSEAR_DKEY                                          
                    INTO                                                        
                           :PV-PARENT-START-DATE                                
                         , :PV-PARENT-END-DATE                                  
                         , :PV-PARENT-DKEY                                      
                    FROM                                                        
                           TMDSEAR                                              
                    WHERE                                                       
                           OPERCO_NBR     =  :MB018-OPERATING-CO-007            
                      AND  GMA_NBR        =  :MB018-NEW-GMA-007                 
                      AND  DMA_NBR        =  :MDSEAR-DMA-NBR                    
                      AND  DEPT_NBR       =  :MDSEAR-DEPT-NBR                   
                      AND  MAJ_CL_NBR     =  :MDSEAR-MAJ-CL-NBR                 
                      AND  SUB_CL_NBR     =  :MDSEAR-SUB-CL-NBR                 
                      AND  MDSELV_CDE     =  :PV-LEVEL                          
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '3900-OBTAIN-PARENT-DATES'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'SELECT TMDSEAR FOR PARENT DATES'                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3900-OBTAIN-PARENT-DATES'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'SELECT FOR TMDSEAR PARENT DATES EXCEEDED'                  
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           DISPLAY '*  KEYS USED = OPERCO ='                                    
                   MB018-OPERATING-CO-007  ', GMA='                             
                   MB018-NEW-GMA-007       ', DMA='                             
                   MDSEAR-DMA-NBR          ', DPT='                             
                   MDSEAR-DEPT-NBR         ', MCL='                             
                   MDSEAR-MAJ-CL-NBR       ', SCL='                             
                   MDSEAR-SUB-CL-NBR       ', LVL='                             
                   PV-LEVEL.                                                    
                                                                                
           IF  SQLCODE EQUAL ZERO                                               
               DISPLAY '*  PARENT START DATE = ' PV-PARENT-START-DATE           
               DISPLAY '*  PARENT END   DATE = ' PV-PARENT-END-DATE             
           ELSE                                                                 
               SET PS-DONT-PROCESS   TO  TRUE                                   
               DISPLAY '*  PARENT NOT FOUND FOR THIS CONTROL RECORD'            
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN PV-GMA-LIT                                                  
                    MOVE PV-PARENT-DKEY                                         
                                     TO  PV-LAST-GMA-DKEY                       
                                                                                
               WHEN PV-DMA-LIT                                                  
                    MOVE PV-PARENT-DKEY                                         
                                     TO  PV-LAST-DMA-DKEY                       
                                                                                
               WHEN PV-DPT-LIT                                                  
                    MOVE PV-PARENT-DKEY                                         
                                     TO  PV-LAST-DPT-DKEY                       
                                                                                
               WHEN PV-MCL-LIT                                                  
                    MOVE PV-PARENT-DKEY                                         
                                     TO  PV-LAST-MCL-DKEY                       
           END-EVALUATE.                                                        
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    HERE BEGINS THE ACTUAL UPDATE PROCESS.  FIRST, WE MUST    *          
      *    OPEN THE CURSOR.  NEXT, FOR EACH ROW RETRIEVED, CREATE    *          
      *    A "NEW" ROW AND INSERT IT.  THEN, CLOSE THE CURSOR AND    *          
      *    UPDATE THE "OLD" ROWS.  (END DATE = NEW START DATE -1)    *          
      *    (CURSOR OF LIST-SEQ-NBR'S.)                               *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4000-UPDATE-MDSEAR.                                                      
      *                                                                         
           PERFORM 4100-OPEN-CURSOR.                                            
           PERFORM 4200-PROCESS-CURSOR                                          
               UNTIL SQLCODE NOT EQUAL ZERO.                                    
           PERFORM 4600-CLOSE-CURSOR.                                           
           PERFORM 4800-COMMIT.                                                 
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OPEN THE MERCHANDISE LIST CURSOR.                         *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4100-OPEN-CURSOR.                                                        
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  OPEN  MDSEAR-LIST                                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4100-OPEN-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN MDSEAR CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4100-OPEN-CURSOR'                                          
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    FETCH THE CURSOR, BUILD THE NEW ROW, INSERT IT.           *          
      *    PROCESS ALL OTHER TABLES FOR THE CURRENT DKEY:            *          
      *       (FOLLOWING THE ORIGINAL PROGRAM LOGIC, ALREADY EXPIRED *          
      *        ROWS ON TMDSEAR, TAPLAVL AND TRSPBAR ARE NOT RETRIEVED*          
      *        OR PROCESSED; THESE ROWS RETAIN THEIR ORIGINAL DKEYS  *          
      *        AND DATES.)                                           *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4200-PROCESS-CURSOR.                                                     
      *                                                                         
           PERFORM 4300-FETCH-CURSOR.                                           
           IF  SQLCODE EQUAL ZERO                                               
               ADD 1                 TO  PV-NEW-DKEY                            
               MOVE DCLTMDSEAR TO PV-MDSEAR-HOLD-AREA                           
               IF  MDSEAR-STRT-DTE GREATER THAN PV-END-DATE-MINUS1              
                   PERFORM 4750-DELETE-CURRENT-ROW                              
               ELSE                                                             
                   PERFORM 4700-UPDATE-CURRENT-ROW                              
               END-IF                                                           
                                                                                
               MOVE PV-MDSEAR-HOLD-AREA TO DCLTMDSEAR                           
               PERFORM 4400-BUILD-NEW-ROW                                       
               PERFORM 4500-INSERT-NEW-ROW                                      
               PERFORM 5000-PROCESS-OTHER-TABLES                                
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    FETCH THE MERCHANDISE LIST CURSOR.                        *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4300-FETCH-CURSOR.                                                       
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  FETCH                                                         
                        MDSEAR-LIST                                             
                   INTO                                                         
                               :MDSEAR-OPERCO-NBR                               
                            ,  :MDSEAR-GMA-NBR                                  
                            ,  :MDSEAR-DMA-NBR                                  
                            ,  :MDSEAR-DEPT-NBR                                 
                            ,  :MDSEAR-MAJ-CL-NBR                               
                            ,  :MDSEAR-SUB-CL-NBR                               
                            ,  :MDSEAR-MDSELV-CDE                               
                            ,  :MDSEAR-DKEY                                     
                            ,  :MDSEAR-CHG-DTE                                  
                            ,  :MDSEAR-CRE-DTE                                  
                            ,  :MDSEAR-STRT-DTE                                 
                            ,  :MDSEAR-END-DTE                                  
                            ,  :MDSEAR-GEN-MDSE-TYP-CDE                         
                            ,  :MDSEAR-DESC                                     
                            ,  :MDSEAR-BYR-NBR                                  
                            ,  :MDSEAR-LIST-SEQ-NBR                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4300-FETCH-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH MDSEAR CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4300-FETCH-CURSOR'                                         
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH CURSOR RETRIES EXCEEDED'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OVERLAY THE NEW COLUMNS.                                  *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4400-BUILD-NEW-ROW.                                                      
      *                                                                         
           MOVE MB018-OPERATING-CO-007                                          
                                     TO  MDSEAR-OPERCO-NBR.                     
           MOVE MB018-NEW-GMA-007    TO  MDSEAR-GMA-NBR.                        
           MOVE MB018-NEW-START-DATE-007                                        
                                     TO  MDSEAR-CHG-DTE                         
                                         MDSEAR-CRE-DTE.                        
      *                                                                         
           IF  MB018-NEW-START-DATE-007 GREATER MDSEAR-STRT-DTE                 
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  MDSEAR-STRT-DTE                        
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE LESS THAN PV-PARENT-START-DATE                   
               MOVE PV-PARENT-START-DATE                                        
                                     TO  MDSEAR-STRT-DTE                        
           END-IF.                                                              
                                                                                
           IF  MDSEAR-END-DTE GREATER THAN PV-PARENT-END-DATE                   
               MOVE PV-PARENT-END-DATE                                          
                                     TO  MDSEAR-END-DTE                         
           END-IF.                                                              
      *                                                                         
           IF (MB018-NEW-DMA-007 GREATER THAN ZERO                              
           AND MDSEAR-MDSELV-CDE NOT EQUAL PC-GMA)                              
               MOVE MB018-NEW-DMA-007                                           
                                     TO  MDSEAR-DMA-NBR                         
           END-IF.                                                              
      *                                                                         
           IF (MB018-NEW-DEPT-007 GREATER THAN ZERO                             
           AND (MDSEAR-MDSELV-CDE EQUAL PC-DPT OR PC-MCL OR PC-SCL))            
               MOVE MB018-NEW-DEPT-007                                          
                                     TO  MDSEAR-DEPT-NBR                        
           END-IF.                                                              
      *                                                                         
           IF (MB018-NEW-MAJ-CL-007 GREATER THAN ZERO                           
           AND (MDSEAR-MDSELV-CDE EQUAL PC-MCL OR PC-SCL))                      
               MOVE MB018-NEW-MAJ-CL-007                                        
                                     TO  MDSEAR-MAJ-CL-NBR                      
           END-IF.                                                              
      *                                                                         
           IF (MB018-NEW-SUB-CL-007 GREATER THAN ZERO                           
           AND MDSEAR-MDSELV-CDE EQUAL PC-SCL)                                  
               MOVE MB018-NEW-SUB-CL-007                                        
                                     TO  MDSEAR-SUB-CL-NBR                      
           END-IF.                                                              
      *                                                                         
      ****************************************************************          
      **  IF A MAJOR CLASS IS BEING REALIGNED (MAJOR CLASS MOVING   **          
      **  TO A NEW MAJOR CLASS, TAKING ALL SUBSEQUENT SUB CLASSES), **          
      **  ALL SUB CLASS NUMBERS MUST BE CHANGED TO CORRESPOND TO THE**          
      **  NEW MAJOR CLASS RANGE.                                    **          
      ****************************************************************          
           IF (MB018-NEW-MAJ-CL-007 GREATER THAN ZERO)                          
           AND (MB018-NEW-SUB-CL-007 LESS THAN OR EQUAL ZERO)                   
           AND (MDSEAR-MDSELV-CDE EQUAL PC-SCL)                                 
               MOVE MDSEAR-SUB-CL-NBR     TO PV-BUILD-SUB-CL                    
               MOVE MB018-NEW-MAJ-CL-007  TO PV-BUILD-MAJ-CL                    
               MOVE PV-BUILD-MAJ-CL-BYTE1 TO PV-BUILD-SUB-CL-BYTE2              
               MOVE PV-BUILD-SUB-CL       TO MDSEAR-SUB-CL-NBR                  
           END-IF.                                                              
      *                                                                         
           IF  MB018-NEW-BUYER-NUMBER-007 GREATER THAN SPACES                   
               MOVE MB018-NEW-BUYER-NUMBER-007                                  
                                     TO  MDSEAR-BYR-NBR                         
           END-IF.                                                              
      *                                                                         
           MOVE MDSEAR-OPERCO-NBR    TO  PV-WS-OPERCO.                          
           MOVE MDSEAR-GMA-NBR       TO  PV-WS-GMA.                             
           MOVE MDSEAR-DMA-NBR       TO  PV-WS-DMA.                             
           MOVE MDSEAR-DEPT-NBR      TO  PV-WS-DEPT.                            
           MOVE MDSEAR-MAJ-CL-NBR    TO  PV-WS-MAJ-CL.                          
           MOVE MDSEAR-SUB-CL-NBR    TO  PV-WS-SUB-CL.                          
           MOVE ZERO                 TO  PV-WS-ZERO.                            
           MOVE PV-WORK-SEQ-NBR      TO  MDSEAR-LIST-SEQ-NBR.                   
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT THE NEWLY BUILT ROW.                               *          
      *    (WITH ORIG DKEY AND CURRENT CHG/CREATE DATES.)            *          
      *    SAVE THE DKEY FOR THE CURRENT LEVEL.                      *          
      *                                                              *          
      ****************************************************************          
                                                                                
       4500-INSERT-NEW-ROW.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TMDSEAR                                                 
                               (OPERCO_NBR                                      
                            ,   GMA_NBR                                         
                            ,   DMA_NBR                                         
                            ,   DEPT_NBR                                        
                            ,   MAJ_CL_NBR                                      
                            ,   SUB_CL_NBR                                      
                            ,   MDSELV_CDE                                      
                            ,   MDSEAR_DKEY                                     
                            ,   CHG_DTE                                         
                            ,   CRE_DTE                                         
                            ,   STRT_DTE                                        
                            ,   END_DTE                                         
                            ,   GEN_MDSE_TYP_CDE                                
                            ,   MDSEAR_DESC                                     
                            ,   BYR_NBR                                         
                            ,   LIST_SEQ_NBR)                                   
                       VALUES                                                   
                              (:MDSEAR-OPERCO-NBR                               
                            ,  :MDSEAR-GMA-NBR                                  
                            ,  :MDSEAR-DMA-NBR                                  
                            ,  :MDSEAR-DEPT-NBR                                 
                            ,  :MDSEAR-MAJ-CL-NBR                               
                            ,  :MDSEAR-SUB-CL-NBR                               
                            ,  :MDSEAR-MDSELV-CDE                               
                            ,  :MDSEAR-DKEY                                     
                            ,  CURRENT DATE                                     
                            ,  CURRENT DATE                                     
                            ,  :MDSEAR-STRT-DTE                                 
                            ,  :MDSEAR-END-DTE                                  
                            ,  :MDSEAR-GEN-MDSE-TYP-CDE                         
                            ,  :MDSEAR-DESC                                     
                            ,  :MDSEAR-BYR-NBR                                  
                            ,  :MDSEAR-LIST-SEQ-NBR)                            
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4500-INSERT-NEW-ROW'                               
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT MDSEAR ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       DISPLAY 'MDSELV-CDE: ' MDSEAR-MDSELV-CDE             0672
                       DISPLAY 'LIST-SEQ-NBR: ' MDSEAR-LIST-SEQ-NBR         0672
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4500-INSERT-NEW-ROW'                                       
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT NEW ROW RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-INSERT-COUNT.                       
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-CO                               
                    MOVE MDSEAR-DKEY TO  PV-LAST-CO-DKEY                        
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-GMA                              
                    MOVE MDSEAR-DKEY TO  PV-LAST-GMA-DKEY                       
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-DMA                              
                    MOVE MDSEAR-DKEY TO  PV-LAST-DMA-DKEY                       
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-DPT                              
                    MOVE MDSEAR-DKEY TO  PV-LAST-DPT-DKEY                       
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-MCL                              
                    MOVE MDSEAR-DKEY TO  PV-LAST-MCL-DKEY                       
                                                                                
           END-EVALUATE.                                                        
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    CLOSE THE MERCHANDISE LIST CURSOR.                        *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4600-CLOSE-CURSOR.                                                       
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  CLOSE MDSEAR-LIST                                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4600-CLOSE-CURSOR'                                 
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE MDSEAR CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4600-CLOSE-CURSOR'                                         
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE CURSOR RETRIES EXCEEDED'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    UPDATE THE END DATE, CHG DATE, AND ASSIGN NEW DKEY FOR THE*          
      *    CURRENT (EXPIRING) ROW.                                   *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4700-UPDATE-CURRENT-ROW.                                                 
      *                                                                         
           MOVE MDSEAR-STRT-DTE      TO  PV-DB2-WORK-DATE.                      
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER PV-DB2-WORK-DATE                
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
           IF  PV-DB2-WORK-DATE LESS THAN PV-PARENT-START-DATE                  
               MOVE PV-PARENT-START-DATE                                        
                                     TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-CALCULATE-END-DATE.                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  UPDATE                                                        
                         TMDSEAR                                                
                    SET                                                         
                         MDSEAR_DKEY  =  :PV-NEW-DKEY,                          
                         END_DTE      =  :PV-OLD-END-DATE-MINUS1,               
                         CHG_DTE      =  CURRENT DATE                           
                    WHERE                                                       
                         LIST_SEQ_NBR =  :MDSEAR-LIST-SEQ-NBR                   
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4700-UPDATE-CURRENT-ROW'                           
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'UPDATE CURRENT CURSOR'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4700-UPDATE-CURRENT-ROW'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'UPDATE CURRENT ROW RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           ADD 1                     TO  PV-UPDATE-COUNT.                       
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    DELETE THE CURRENT ROW BECAUSE IT IS NOT YET IN EFFECT.   *          
      *    THE START DATE IS GREATER THAN THE NEW EFFECTIVE DATE.    *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4750-DELETE-CURRENT-ROW.                                                 
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  DELETE                                                        
                    FROM                                                        
                         TMDSEAR                                                
                    WHERE                                                       
                         LIST_SEQ_NBR =  :MDSEAR-LIST-SEQ-NBR                   
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4750-DELETE-CURRENT-ROW'                           
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'DELETE CURRENT CURSOR'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4750-DELETE-CURRENT-ROW'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'DELETE CURRENT ROW RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           DISPLAY '* TMDSEAR DELETED -' DCLTMDSEAR.                            
           ADD 1                     TO  PV-DELETE-COUNT.                       
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    WE ARE DONE PROCESSING THIS CONTROL RECORD.  COMMIT THE   *          
      *    DB2 CHANGES.                                              *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       4800-COMMIT.                                                             
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  COMMIT                                                        
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '4800-COMMIT'                                       
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COMMIT - MDSEAR'                                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '4800-COMMIT'                                               
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'COMMIT RETRIES EXCEEDED'                                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    FIVE TABLES ARE PROCESSED FROM THIS PARAGRAPH:              *        
      *    APLAVL IS UPDATED (BY CURSOR) AND HAS ROWS INSERTED,        *        
      *    RSPBAR HAS ROWS INSERTED,                                   *        
      *    MDSEDP HAS ROWS INSERTED/UPDATED,                           *        
      *    MDSECL HAS ROWS INSERTED,                                   *        
      *    DPTSHK HAS ROWS INSERTED/UPDATED.                           *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5000-PROCESS-OTHER-TABLES.                                               
                                                                                
           PERFORM 5100-PROCESS-APLAVL.                                         
           PERFORM 5200-PROCESS-RSPBAR.                                         
           PERFORM 5300-PROCESS-MDSEDP.                                         
           PERFORM 5400-PROCESS-MDSECL.                                         
           PERFORM 5500-PROCESS-DPTSHK.                                         
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    PROCESS THE APPLICATION AVAILABLE TABLE.                  *          
      *    OPEN THE CURSOR FIRST, THEN, FOR EACH ROW RETURNED,       *          
      *    UPDATE THE END DATE, NEXT CREATE A NEW ROW TO BE INSERTED *          
      *    AND INSERT IT.                                            *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5100-PROCESS-APLAVL.                                                     
      *                                                                         
           PERFORM 5110-OPEN-APLAVL-CURSOR.                                     
                                                                                
           PERFORM                                                              
               UNTIL SQLCODE NOT EQUAL ZERO                                     
                   PERFORM 5120-FETCH-APLAVL-CURSOR                             
                   IF  SQLCODE EQUAL ZERO                                       
                       MOVE DCLTAPLAVL TO PV-APLAVL-HOLD-AREA                   
                       MOVE APLAVL-MDSEAR-END-DTE                               
                                     TO  PV-HOLD-END-DATE                       
                       PERFORM 5105-DETERMINE-PROCESS                           
                   END-IF                                                       
           END-PERFORM.                                                         
                                                                                
           PERFORM 5150-CLOSE-APLAVL-CURSOR.                                    
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    SET THE DATALESS KEY BASED UPON THE LEVEL THAT IS BEING   *          
      *    PROCESSED.  PERFORM THE ROUTINE TO OBTAIN THE APLAVL      *          
      *    PARENT DATES.  IF FUTURE APLAVL ROW, DELETE ROW THEN      *          
      *    INSERT NEW FUTURE ROW; ELSE INSERT EXPIRING ROW AND UPDATE*          
      *    CURRENT ROW.                                              *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5105-DETERMINE-PROCESS.                                                  
      *                                                                         
           EVALUATE TRUE                                                        
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-GMA                              
                    MOVE PV-LAST-CO-DKEY                                        
                                     TO  PV-APLAVL-DKEY                         
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-DMA                              
                    MOVE PV-LAST-GMA-DKEY                                       
                                     TO  PV-APLAVL-DKEY                         
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-DPT                              
                    MOVE PV-LAST-DMA-DKEY                                       
                                     TO  PV-APLAVL-DKEY                         
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-MCL                              
                    MOVE PV-LAST-DPT-DKEY                                       
                                     TO  PV-APLAVL-DKEY                         
                                                                                
               WHEN MDSEAR-MDSELV-CDE EQUAL PC-SCL                              
                    MOVE PV-LAST-MCL-DKEY                                       
                                     TO  PV-APLAVL-DKEY                         
           END-EVALUATE.                                                        
                                                                                
           PERFORM 5160-OBTAIN-APLAVL-PARENT.                                   
                                                                                
           IF  APLAVL-MDSEAR-STRT-DTE GREATER THAN PV-END-DATE-MINUS1           
               PERFORM 5135-DELETE-APLAVL                                       
               IF (APLAVL-MDSEAR-END-DTE > PV-APLAVL-START-DATE                 
               AND APLAVL-MDSEAR-STRT-DTE < PV-APLAVL-END-DATE)                 
                   PERFORM 5136-INSERT-APLAVL-FUTURE                            
               END-IF                                                           
           ELSE                                                                 
               PERFORM 5130-INSERT-APLAVL                                       
               IF (APLAVL-MDSEAR-END-DTE > PV-APLAVL-START-DATE                 
               AND APLAVL-MDSEAR-STRT-DTE < PV-APLAVL-END-DATE)                 
                   MOVE PV-APLAVL-HOLD-AREA TO DCLTAPLAVL                       
                   PERFORM 5140-UPDATE-APLAVL                                   
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OPEN THE APPLICATION AVAILABILITY CURSOR.                 *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5110-OPEN-APLAVL-CURSOR.                                                 
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  OPEN APLAVL-LIST                                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5110-OPEN-APLAVL'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN APLAVL CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5110-OPEN-APLAVL-CURSOR'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    FETCH THE APPLICATION AVAILABILITY CURSOR.                *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5120-FETCH-APLAVL-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  FETCH                                                         
                        APLAVL-LIST                                             
                   INTO                                                         
                               :APLAVL-APLSYS-CDE                               
                            ,  :APLAVL-MDSEAR-DKEY                              
                            ,  :APLAVL-MDSEAR-STRT-DTE                          
                            ,  :APLAVL-MDSEAR-END-DTE                           
                            ,  :APLAVL-OPERCO-NBR                               
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5120-FETCH-APLAVL-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH APLAVL CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5120-FETCH-APLAVL-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH CURSOR RETRIES EXCEEDED'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT EXPIRING APLAVL ROW WITH THE END DATE AND NEW DKEY.*          
      *                                                              *          
      ****************************************************************          
                                                                                
       5130-INSERT-APLAVL.                                                      
                                                                                
           MOVE APLAVL-MDSEAR-STRT-DTE                                          
                                     TO  PV-DB2-WORK-DATE.                      
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER PV-DB2-WORK-DATE                
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE GREATER THAN PV-DB2-WORK-DATE                    
               MOVE MDSEAR-STRT-DTE  TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           IF  PV-APLAVL-START-DATE GREATER THAN PV-DB2-WORK-DATE               
               MOVE PV-APLAVL-START-DATE                                        
                                     TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-CALCULATE-END-DATE.                                     
                                                                                
           IF  PV-DB2-WORK-DATE LESS APLAVL-MDSEAR-END-DTE                      
               PERFORM                                                          
                   WITH TEST AFTER                                              
                   VARYING PV-SQL-SUB                                           
                   FROM 1 BY 1                                                  
                   UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                           
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                   EXEC SQL INSERT                                              
                        INTO                                                    
                            TAPLAVL                                             
                                   (APLSYS_CDE                                  
                                ,   MDSEAR_DKEY                                 
                                ,   MDSEAR_STRT_DTE                             
                                ,   MDSEAR_END_DTE                              
                                ,   OPERCO_NBR)                                 
                           VALUES                                               
                                  (:APLAVL-APLSYS-CDE                           
                                ,  :PV-NEW-DKEY                                 
                                ,  :APLAVL-MDSEAR-STRT-DTE                      
                                ,  :PV-OLD-END-DATE-MINUS1                      
                                ,  :APLAVL-OPERCO-NBR)                          
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = 0                                         
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                           CONTINUE                                             
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                           MOVE '5130-INSERT-APLAVL'                            
                                     TO  PV-PARAGRAPH-NAME                      
                           MOVE 'INSERT APLAVL  CURSOR'                         
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                           PERFORM 9900-SQL-ABEND                               
                   END-EVALUATE                                                 
               END-PERFORM                                                      
                                                                                
               IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                            
                   MOVE '5130-INSERT-APLAVL'                                    
                                     TO  PV-PARAGRAPH-NAME                      
                   MOVE 'INSERT APLAVL  ROW RETRIES EXCEEDED'                   
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                   PERFORM 9900-SQL-ABEND                                       
               END-IF                                                           
                                                                                
               ADD 1                 TO  PV-INSERT-APLAVL-COUNT                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    DELETE THE CURRENT ROW BECAUSE IT IS NOT YET IN EFFECT.   *          
      *    THE START DATE IS GREATER THAN THE NEW EFFECTIVE DATE.    *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5135-DELETE-APLAVL.                                                      
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  DELETE                                                        
                    FROM                                                        
                         TAPLAVL                                                
                    WHERE                                                       
                         CURRENT OF APLAVL-LIST                                 
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5135-DELETE-APLAVL'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'DELETE CURRENT APLAVL'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5135-DELETE-APLAVL'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'DELETE CURRENT APLAVL RETRIES EXCEEDED'                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           DISPLAY '* TAPLAVL DELETED -' DCLTAPLAVL.                            
           ADD 1                     TO  PV-DELETE-APLAVL-COUNT.                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT THE NEW APLAVL FUTURE ROW, WITH ORIG DKEY, AND     *          
      *    START/END DATES.                                          *          
      *                                                              *          
      ****************************************************************          
                                                                                
       5136-INSERT-APLAVL-FUTURE.                                               
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER APLAVL-MDSEAR-STRT-DTE          
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE GREATER APLAVL-MDSEAR-STRT-DTE                   
               MOVE MDSEAR-STRT-DTE  TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  PV-APLAVL-START-DATE GREATER APLAVL-MDSEAR-STRT-DTE              
               MOVE PV-APLAVL-START-DATE                                        
                                     TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           MOVE PV-HOLD-END-DATE     TO  APLAVL-MDSEAR-END-DTE.                 
           IF  APLAVL-MDSEAR-END-DTE GREATER MDSEAR-END-DTE                     
               MOVE MDSEAR-END-DTE   TO  APLAVL-MDSEAR-END-DTE                  
           END-IF.                                                              
                                                                                
           IF  APLAVL-MDSEAR-END-DTE GREATER PV-APLAVL-END-DATE                 
               MOVE PV-APLAVL-END-DATE                                          
                                     TO  APLAVL-MDSEAR-END-DTE                  
           END-IF.                                                              
                                                                                
           IF  APLAVL-MDSEAR-END-DTE GREATER APLAVL-MDSEAR-STRT-DTE             
               PERFORM                                                          
                   WITH TEST AFTER                                              
                   VARYING PV-SQL-SUB                                           
                   FROM 1 BY 1                                                  
                   UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                           
                         OR SQLCODE = 0)                                        
                                                                                
                   EXEC SQL INSERT                                              
                        INTO                                                    
                            TAPLAVL                                             
                                   (APLSYS_CDE                                  
                                ,   MDSEAR_DKEY                                 
                                ,   MDSEAR_STRT_DTE                             
                                ,   MDSEAR_END_DTE                              
                                ,   OPERCO_NBR)                                 
                           VALUES                                               
                                  (:APLAVL-APLSYS-CDE                           
                                ,  :APLAVL-MDSEAR-DKEY                          
                                ,  :APLAVL-MDSEAR-STRT-DTE                      
                                ,  :APLAVL-MDSEAR-END-DTE                       
                                ,  :APLAVL-OPERCO-NBR)                          
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = 0                                         
                           CONTINUE                                             
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                           MOVE '5136-INSERT-APLAVL-FUTURE'                     
                                     TO  PV-PARAGRAPH-NAME                      
                           MOVE 'INSERT APLAVL ROW'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                           PERFORM 9900-SQL-ABEND                               
                   END-EVALUATE                                                 
               END-PERFORM                                                      
                                                                                
               IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                            
                   MOVE '5136-INSERT-APLAVL-FUTURE'                             
                                     TO  PV-PARAGRAPH-NAME                      
                   MOVE 'INSERT APLAVL  RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                   PERFORM 9900-SQL-ABEND                                       
               END-IF                                                           
                                                                                
               ADD 1                 TO  PV-INSERT-APLAVL-COUNT                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    UPDATE THE APLAVL ROW, WITH ORIG DKEY & START/END DATE.   *          
      *                                                              *          
      ****************************************************************          
                                                                                
       5140-UPDATE-APLAVL.                                                      
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER APLAVL-MDSEAR-STRT-DTE          
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE GREATER APLAVL-MDSEAR-STRT-DTE                   
               MOVE MDSEAR-STRT-DTE  TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  PV-APLAVL-START-DATE GREATER APLAVL-MDSEAR-STRT-DTE              
               MOVE PV-APLAVL-START-DATE                                        
                                     TO  APLAVL-MDSEAR-STRT-DTE                 
           END-IF.                                                              
                                                                                
           MOVE PV-HOLD-END-DATE     TO  APLAVL-MDSEAR-END-DTE.                 
           IF  APLAVL-MDSEAR-END-DTE GREATER MDSEAR-END-DTE                     
               MOVE MDSEAR-END-DTE   TO  APLAVL-MDSEAR-END-DTE                  
           END-IF.                                                              
                                                                                
           IF  APLAVL-MDSEAR-END-DTE GREATER PV-APLAVL-END-DATE                 
               MOVE PV-APLAVL-END-DATE                                          
                                     TO  APLAVL-MDSEAR-END-DTE                  
           END-IF.                                                              
                                                                                
           IF  APLAVL-MDSEAR-END-DTE GREATER APLAVL-MDSEAR-STRT-DTE             
               PERFORM                                                          
                   WITH TEST AFTER                                              
                   VARYING PV-SQL-SUB                                           
                   FROM 1 BY 1                                                  
                   UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                           
                         OR SQLCODE = 0)                                        
                                                                                
                   EXEC SQL                                                     
                      UPDATE                                                    
                             TAPLAVL                                            
                        SET                                                     
                             MDSEAR_STRT_DTE = :APLAVL-MDSEAR-STRT-DTE,         
                             MDSEAR_END_DTE =  :APLAVL-MDSEAR-END-DTE           
                        WHERE                                                   
                             CURRENT OF APLAVL-LIST                             
                   END-EXEC                                                     
                                                                                
                   EVALUATE TRUE                                                
                       WHEN SQLCODE = -904                                      
                       WHEN SQLCODE = -911                                      
                       WHEN SQLCODE = 0                                         
                           CONTINUE                                             
                       WHEN SQLWARN NOT = SPACES                                
                       WHEN SQLCODE NOT = ZERO                                  
                           MOVE '5140-UPDATE-APLAVL'                            
                                     TO  PV-PARAGRAPH-NAME                      
                           MOVE 'UPDATE APLAVL ROW'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                           PERFORM 9900-SQL-ABEND                               
                   END-EVALUATE                                                 
               END-PERFORM                                                      
                                                                                
               IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                            
                   MOVE '5140-UPDATE-APLAVL'                                    
                                     TO  PV-PARAGRAPH-NAME                      
                   MOVE 'UPDATE APLAVL  RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                   PERFORM 9900-SQL-ABEND                                       
               END-IF                                                           
                                                                                
               ADD 1                 TO  PV-UPDATE-APLAVL-COUNT                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    CLOSE THE APLAVL LIST CURSOR.                             *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5150-CLOSE-APLAVL-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  CLOSE APLAVL-LIST                                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5150-CLOSE-APLAVL-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE APLAVL CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5150-CLOSE-APLAVL-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE APLAVL CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OBTAIN THE PARENT DATES FOR THE ROW BEING PROCESSED.      *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5160-OBTAIN-APLAVL-PARENT.                                               
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                    SELECT                                                      
                           MDSEAR_STRT_DTE                                      
                         , MDSEAR_END_DTE                                       
                    INTO                                                        
                           :PV-APLAVL-START-DATE                                
                         , :PV-APLAVL-END-DATE                                  
                    FROM                                                        
                           TAPLAVL                                              
                    WHERE                                                       
                          (OPERCO_NBR     =  :MB018-OPERATING-CO-007            
                      AND  MDSEAR_DKEY    =  :PV-APLAVL-DKEY                    
                      AND  APLSYS_CDE     =  :APLAVL-APLSYS-CDE                 
                      AND  MDSEAR_END_DTE >  CURRENT DATE)                      
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5160-OBTAIN-APLAVL-PARENT'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'SELECT APLAVL'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5160-OBTAIN-APLAVL-PARENT'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'SELECT APLAVL - PARENT'                                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           IF  SQLCODE = +100                                                   
               MOVE LOW-VALUES       TO  PV-APLAVL-START-DATE                   
                                         PV-APLAVL-END-DATE                     
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    PROCESS THE RESPONSIBILITY AREA TABLE.                    *          
      *    OPEN THE CURSOR FIRST, THEN, FOR EACH ROW RETURNED,       *          
      *    UPDATE THE END DATE, NEXT CREATE A NEW ROW TO BE INSERTED *          
      *    AND INSERT IT.                                            *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5200-PROCESS-RSPBAR.                                                     
      *                                                                         
           PERFORM 5210-OPEN-RSPBAR-CURSOR.                                     
           PERFORM                                                              
               UNTIL SQLCODE NOT EQUAL ZERO                                     
                   PERFORM 5220-FETCH-RSPBAR-CURSOR                             
                   IF  SQLCODE EQUAL ZERO                                       
                       MOVE DCLTRSPBAR TO PV-RSPBAR-HOLD-AREA                   
                       MOVE RSPBAR-WORKER-END-DTE                               
                                     TO  PV-HOLD-END-DATE                       
                                                                                
                       IF  RSPBAR-WORKER-STRT-DTE GREATER THAN                  
                           PV-END-DATE-MINUS1                                   
                           PERFORM 5235-DELETE-RSPBAR                           
                       ELSE                                                     
                           PERFORM 5235-DELETE-RSPBAR                           
                           PERFORM 5230-INSERT-RSPBAR-EXPIRE                    
                       END-IF                                                   
                       MOVE PV-RSPBAR-HOLD-AREA TO DCLTRSPBAR                   
                       PERFORM 5240-INSERT-RSPBAR-NEW                           
                   END-IF                                                       
           END-PERFORM.                                                         
           PERFORM 5250-CLOSE-RSPBAR-CURSOR.                                    
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OPEN THE RESPONSIBILITY AREA CURSOR.                      *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5210-OPEN-RSPBAR-CURSOR.                                                 
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  OPEN RSPBAR-LIST                                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5210-OPEN-RSPBAR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN RSPBAR CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5210-OPEN-RSPBAR-CURSOR'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN CURSOR RETRIES EXCEEDED'                              
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    FETCH THE RESPONSIBILITY AREA CURSOR.                     *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5220-FETCH-RSPBAR-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  FETCH                                                         
                        RSPBAR-LIST                                             
                   INTO                                                         
                               :RSPBAR-MDSEAR-DKEY                              
                            ,  :RSPBAR-WORKER-NBR                               
                            ,  :RSPBAR-WORKER-STRT-DTE                          
                            ,  :RSPBAR-WORKER-END-DTE                           
                            ,  :RSPBAR-RSPLVL-CDE                               
                            ,  :RSPBAR-WORKER-FIR-NAME                          
                            ,  :RSPBAR-WORKER-LAST-NAME                         
                            ,  :RSPBAR-OPERCO-NBR                               
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5220-FETCH-RSPBAR-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH RSPBAR CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5220-FETCH-RSPBAR-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH RSPBAR CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT THE EXPIRING ROW WITH THE END DATE AND NEW DKEY.   *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5230-INSERT-RSPBAR-EXPIRE.                                               
      *                                                                         
           MOVE RSPBAR-WORKER-STRT-DTE                                          
                                     TO  PV-DB2-WORK-DATE.                      
                                                                                
           IF  MB018-NEW-START-DATE-007 GREATER THAN PV-DB2-WORK-DATE           
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE GREATER THAN PV-DB2-WORK-DATE                    
               MOVE MDSEAR-STRT-DTE  TO  PV-DB2-WORK-DATE                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-CALCULATE-END-DATE.                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TRSPBAR                                                 
                               (MDSEAR_DKEY                                     
                            ,   WORKER_NBR                                      
                            ,   WORKER_STRT_DTE                                 
                            ,   WORKER_END_DTE                                  
                            ,   RSPLVL_CDE                                      
                            ,   WORKER_FIR_NAME                                 
                            ,   WORKER_LAST_NAME                                
                            ,   OPERCO_NBR)                                     
                       VALUES                                                   
                              (:PV-NEW-DKEY                                     
                            ,  :RSPBAR-WORKER-NBR                               
                            ,  :RSPBAR-WORKER-STRT-DTE                          
                            ,  :PV-OLD-END-DATE-MINUS1                          
                            ,  :RSPBAR-RSPLVL-CDE                               
                            ,  :RSPBAR-WORKER-FIR-NAME                          
                            ,  :RSPBAR-WORKER-LAST-NAME                         
                            ,  :APLAVL-OPERCO-NBR)                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5230-INSERT-RSPBAR-EXPIRE'                         
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT RSPBAR  CURSOR'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5230-INSERT-RSPBAR-EXPIRE'                                 
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT RSPBAR ROW RETRIES EXCEEDED'                        
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-INSERT-RSPBAR-COUNT.                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    DELETE THE CURRENT ROW BECAUSE IT IS NOT YET IN EFFECT.   *          
      *    THE START DATE IS GREATER THAN THE NEW EFFECTIVE DATE.    *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5235-DELETE-RSPBAR.                                                      
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  DELETE                                                        
                    FROM                                                        
                         TRSPBAR                                                
                    WHERE                                                       
                         CURRENT OF RSPBAR-LIST                                 
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5235-DELETE-RSPBAR'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'DELETE CURRENT RSPBAR'                             
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5235-DELETE-RSPBAR'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'DELETE CURRENT RSPBAR RETRIES EXCEEDED'                    
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           DISPLAY '* TRSPBAR DELETED -' DCLTRSPBAR.                            
           ADD 1                     TO  PV-DELETE-RSPBAR-COUNT.                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT THE NEW RSPBAR ROW.                                *          
      *    (WITH ORIG DKEY AND START/END DATES.)                     *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5240-INSERT-RSPBAR-NEW.                                                  
      *                                                                         
           IF  MB018-NEW-START-DATE-007 GREATER RSPBAR-WORKER-STRT-DTE          
               MOVE MB018-NEW-START-DATE-007                                    
                                     TO  RSPBAR-WORKER-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  MDSEAR-STRT-DTE GREATER RSPBAR-WORKER-STRT-DTE                   
               MOVE MDSEAR-STRT-DTE  TO  RSPBAR-WORKER-STRT-DTE                 
           END-IF.                                                              
                                                                                
           IF  PV-HOLD-END-DATE GREATER THAN MDSEAR-END-DTE                     
               MOVE MDSEAR-END-DTE   TO  RSPBAR-WORKER-END-DTE                  
           ELSE                                                                 
               MOVE PV-HOLD-END-DATE TO  RSPBAR-WORKER-END-DTE                  
           END-IF.                                                              
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
      *                                                                         
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TRSPBAR                                                 
                               (MDSEAR_DKEY                                     
                            ,   WORKER_NBR                                      
                            ,   WORKER_STRT_DTE                                 
                            ,   WORKER_END_DTE                                  
                            ,   RSPLVL_CDE                                      
                            ,   WORKER_FIR_NAME                                 
                            ,   WORKER_LAST_NAME                                
                            ,   OPERCO_NBR)                                     
                       VALUES                                                   
                              (:RSPBAR-MDSEAR-DKEY                              
                            ,  :RSPBAR-WORKER-NBR                               
                            ,  :RSPBAR-WORKER-STRT-DTE                          
                            ,  :RSPBAR-WORKER-END-DTE                           
                            ,  :RSPBAR-RSPLVL-CDE                               
                            ,  :RSPBAR-WORKER-FIR-NAME                          
                            ,  :RSPBAR-WORKER-LAST-NAME                         
                            ,  :APLAVL-OPERCO-NBR)                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5240-INSERT-RSPBAR-NEW'                            
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT RSPBAR ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5240-INSERT-RSPBAR-NEW'                                    
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT RSPBAR  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           ADD 1                     TO  PV-INSERT-RSPBAR-COUNT.                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    CLOSE THE RSPBAR LIST CURSOR.                             *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5250-CLOSE-RSPBAR-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  CLOSE RSPBAR-LIST                                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5250-CLOSE-RSPBAR-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE RSPBAR CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5250-CLOSE-RSPBAR-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE RSPBAR CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    PROCESS THE MERCHANDISE AREA DEPAREMENT TABLE.            *          
      *    OPEN THE CURSOR FIRST, THEN, FOR EACH ROW RETURNED,       *          
      *    INSERT A NEW ROW WITH THE "NEW" DATALESS KEY.             *          
      *    UPDATE CHG-DATE ON CURRENT ROW.                           *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5300-PROCESS-MDSEDP.                                                     
      *                                                                         
           PERFORM 5310-OPEN-MDSEDP-CURSOR.                                     
           PERFORM                                                              
               UNTIL SQLCODE NOT EQUAL ZERO                                     
                   PERFORM 5320-FETCH-MDSEDP-CURSOR                             
                   IF  SQLCODE EQUAL ZERO                                       
                       MOVE DCLTMDSEDP TO PV-MDSEDP-HOLD-AREA                   
                       PERFORM 5340-INSERT-MDSEDP                               
                       MOVE PV-MDSEDP-HOLD-AREA TO DCLTMDSEDP                   
                       PERFORM 5330-UPDATE-MDSEDP                               
                   END-IF                                                       
           END-PERFORM.                                                         
           PERFORM 5350-CLOSE-MDSEDP-CURSOR.                                    
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    OPEN THE MERCHANDISE AREA DEPARTMENT CURSOR.              *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5310-OPEN-MDSEDP-CURSOR.                                                 
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  OPEN MDSEDP-LIST                                              
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5310-OPEN-MDSEDP'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN MDSEDP CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5310-OPEN-MDSEDP-CURSOR'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN MDSEDP CURSOR RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    FETCH THE MERCHANDISE AREA DEPARTMENT CURSOR.             *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5320-FETCH-MDSEDP-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  FETCH                                                         
                        MDSEDP-LIST                                             
                   INTO                                                         
                               :MDSEDP-MDSEAR-DKEY                              
                            ,  :MDSEDP-CHG-DTE                                  
                            ,  :MDSEDP-MDSEBG-NBR                               
                            ,  :MDSEDP-DPTDIS-CDE                               
                            ,  :MDSEDP-ARDEPT-CHG-DTE                           
                            ,  :MDSEDP-ARDEPT-DESC                              
                            ,  :MDSEDP-ZOUT-EXCL-IND                            
                            ,  :MDSEDP-CONS-IND                                 
                            ,  :MDSEDP-PLAN-FRGT-PCT                            
                            ,  :MDSEDP-SKU-LVL-UPD-IND                          
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5320-FETCH-MDSEDP-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH MDSEDP CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5320-FETCH-MDSEDP-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH MDSEDP CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    UPDATE CHG-DATE ON CURRENT MDSEDP ROW WITH CURRENT DATE.  *          
      *                                                              *          
      ****************************************************************          
                                                                                
       5330-UPDATE-MDSEDP.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                    UPDATE                                                      
                        TMDSEDP                                                 
                    SET                                                         
                         CHG_DTE = CURRENT DATE                                 
                    WHERE                                                       
                         CURRENT OF MDSEDP-LIST                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5330-UPDATE-MDSEDP'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'UPDATE MDSEDP ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5330-UPDATE-MDSEDP'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'UPDATE MDSEDP  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-UPDATE-MDSEDP-COUNT.                
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    INSERT THE OLD MDSEDP ROW WITH NEW DKEY & ORIG CHG-DATE.  *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5340-INSERT-MDSEDP.                                                      
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
      *                                                                         
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TMDSEDP                                                 
                               (MDSEAR_DKEY                                     
                            ,   CHG_DTE                                         
                            ,   MDSEBG_NBR                                      
                            ,   DPTDIS_CDE                                      
                            ,   ARDEPT_CHG_DTE                                  
                            ,   ARDEPT_DESC                                     
                            ,   ZOUT_EXCL_IND                                   
                            ,   CONS_IND                                        
                            ,   PLAN_FRGT_PCT                                   
                            ,   SKU_LVL_UPD_IND)                                
                       VALUES                                                   
                              (:PV-NEW-DKEY                                     
                            ,  :MDSEDP-CHG-DTE                                  
                            ,  :MDSEDP-MDSEBG-NBR                               
                            ,  :MDSEDP-DPTDIS-CDE                               
                            ,  :MDSEDP-ARDEPT-CHG-DTE                           
                            ,  :MDSEDP-ARDEPT-DESC                              
                            ,  :MDSEDP-ZOUT-EXCL-IND                            
                            ,  :MDSEDP-CONS-IND                                 
                            ,  :MDSEDP-PLAN-FRGT-PCT                            
                            ,  :MDSEDP-SKU-LVL-UPD-IND)                         
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5340-INSERT-MDSEDP'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT MDSEDP ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5340-INSERT-MDSEDP'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT MDSEDP  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
      *                                                                         
           ADD 1                     TO  PV-INSERT-MDSEDP-COUNT.                
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    CLOSE THE MDSEDP LIST CURSOR.                             *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       5350-CLOSE-MDSEDP-CURSOR.                                                
      *                                                                         
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
      *                                                                         
               EXEC SQL                                                         
                  CLOSE MDSEDP-LIST                                             
               END-EXEC                                                         
      *                                                                         
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5350-CLOSE-MDSEDP-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE MDSEDP CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5350-CLOSE-MDSEDP-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE MDSEDP CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    PROCESS THE MERCHANDISE AREA CLASS TABLE.                   *        
      *    OPEN THE CURSOR FIRST, THEN, FOR EACH ROW RETURNED,         *        
      *    INSERT A NEW ROW WITH THE "NEW" DATALESS KEY.               *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5400-PROCESS-MDSECL.                                                     
                                                                                
           PERFORM 5410-OPEN-MDSECL-CURSOR.                                     
           PERFORM                                                              
               UNTIL SQLCODE NOT EQUAL ZERO                                     
                   PERFORM 5420-FETCH-MDSECL-CURSOR                             
                   IF  SQLCODE EQUAL ZERO                                       
                       PERFORM 5440-INSERT-MDSECL                               
                   END-IF                                                       
           END-PERFORM.                                                         
           PERFORM 5450-CLOSE-MDSECL-CURSOR.                                    
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    OPEN THE MERCHANDISE AREA CLASS CURSOR.                     *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5410-OPEN-MDSECL-CURSOR.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  OPEN MDSECL-LIST                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5410-OPEN-MDSECL'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN MDSECL CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5410-OPEN-MDSECL-CURSOR'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN MDSECL CURSOR RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    FETCH THE MERCHANDISE AREA CLASS CURSOR.                    *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5420-FETCH-MDSECL-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  FETCH                                                         
                        MDSECL-LIST                                             
                   INTO                                                         
                               :MDSECL-MDSEAR-DKEY                              
                            ,  :MDSECL-POS-DESC                                 
                            ,  :MDSECL-TAX-DFLT-IND                             
                            ,  :MDSECL-PRT-VEND-UPC-IND                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5420-FETCH-MDSECL-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH MDSECL CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5420-FETCH-MDSECL-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH MDSECL CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    INSERT THE NEW MDSECL ROW.                                  *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5440-INSERT-MDSECL.                                                      
                                                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TMDSECL                                                 
                               (MDSEAR_DKEY                                     
                            ,   POS_DESC                                        
                            ,   TAX_DFLT_IND                                    
                            ,   PRT_VEND_UPC_IND)                               
                       VALUES                                                   
                              (:PV-NEW-DKEY                                     
                            ,  :MDSECL-POS-DESC                                 
                            ,  :MDSECL-TAX-DFLT-IND                             
                            ,  :MDSECL-PRT-VEND-UPC-IND)                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5440-INSERT-MDSECL'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT MDSECL ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5440-INSERT-MDSECL'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT MDSECL  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-INSERT-MDSECL-COUNT.                
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    CLOSE THE MDSECL LIST CURSOR.                               *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5450-CLOSE-MDSECL-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE MDSECL-LIST                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5450-CLOSE-MDSECL-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE MDSECL CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5450-CLOSE-MDSECL-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE MDSECL CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    PROCESS THE DEPARTMENT SHRINK TABLE.                        *        
      *    OPEN THE CURSOR FIRST, THEN, FOR EACH ROW RETURNED,         *        
      *    INSERT A NEW ROW WITH THE "NEW" DATALESS KEY.               *        
      *    UPDATE TIMESTAMP ON ORIG DKEY ROW.                          *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5500-PROCESS-DPTSHK.                                                     
                                                                                
           PERFORM 5510-OPEN-DPTSHK-CURSOR.                                     
           PERFORM                                                              
               UNTIL SQLCODE NOT EQUAL ZERO                                     
                   PERFORM 5520-FETCH-DPTSHK-CURSOR                             
                   IF  SQLCODE EQUAL ZERO                                       
                       MOVE DCLTDPTSHK TO PV-DPTSHK-HOLD-AREA                   
                       PERFORM 5540-INSERT-DPTSHK                               
                       MOVE PV-DPTSHK-HOLD-AREA TO DCLTDPTSHK                   
                       PERFORM 5530-UPDATE-DPTSHK                               
                   END-IF                                                       
           END-PERFORM.                                                         
           PERFORM 5550-CLOSE-DPTSHK-CURSOR.                                    
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    OPEN THE DEPARTMENT SHRINK CURSOR.                          *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5510-OPEN-DPTSHK-CURSOR.                                                 
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  OPEN DPTSHK-LIST                                              
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5510-OPEN-DPTSHK'                                  
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'OPEN DPTSHK CURSOR'                                
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5510-OPEN-DPTSHK-CURSOR'                                   
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'OPEN DPTSHK CURSOR RETRIES EXCEEDED'                       
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    FETCH THE DEPARTMENT SHRINK CURSOR.                         *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5520-FETCH-DPTSHK-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  FETCH                                                         
                        DPTSHK-LIST                                             
                   INTO                                                         
                               :DPTSHK-MDSEAR-DKEY                              
                             , :DPTSHK-SHRINK-YR-NBR                            
                             , :DPTSHK-CHG-TMST                                 
                             , :DPTSHK-JAN-SHRINK-PCT                           
                             , :DPTSHK-FEB-SHRINK-PCT                           
                             , :DPTSHK-MAR-SHRINK-PCT                           
                             , :DPTSHK-APR-SHRINK-PCT                           
                             , :DPTSHK-MAY-SHRINK-PCT                           
                             , :DPTSHK-JUN-SHRINK-PCT                           
                             , :DPTSHK-JUL-SHRINK-PCT                           
                             , :DPTSHK-AUG-SHRINK-PCT                           
                             , :DPTSHK-SEP-SHRINK-PCT                           
                             , :DPTSHK-OCT-SHRINK-PCT                           
                             , :DPTSHK-NOV-SHRINK-PCT                           
                             , :DPTSHK-DEC-SHRINK-PCT                           
C14666                       , :DPTSHK-E-BUS-IND                                
C14666                       , :DPTSHK-SHNK-PCT-CHG-DTE                         
C14666                       , :DPTSHK-LAST-UPD-BY-USR                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = +100                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5520-FETCH-DPTSHK-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'FETCH DPTSHK CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5520-FETCH-DPTSHK-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'FETCH DPTSHK CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      ****************************************************************          
      *                                                              *          
      *    UPDATE THE CURRENT DPTSHK ROW WITH THE CURRENT TIMESTAMP. *          
      *                                                              *          
      ****************************************************************          
                                                                                
       5530-UPDATE-DPTSHK.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL                                                         
                    UPDATE                                                      
                        TDPTSHK                                                 
                    SET                                                         
                         CHG_TMST = CURRENT TIMESTAMP                           
                    WHERE                                                       
                         CURRENT OF DPTSHK-LIST                                 
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5530-UPDATE-DPTSHK'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'UPDATE DPTSHK ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5530-UPDATE-DPTSHK'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'UPDATE DPTSHK  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-UPDATE-DPTSHK-COUNT.                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *                                                                *        
      *    INSERT THE NEW DPTSHK ROW.                                  *        
      *    (WITH ORIG TIMESTAMP)                                       *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5540-INSERT-DPTSHK.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0)                                        
                                                                                
               EXEC SQL INSERT                                                  
                    INTO                                                        
                        TDPTSHK                                                 
                               (MDSEAR_DKEY                                     
                             ,  SHRINK_YR_NBR                                   
                             ,  CHG_TMST                                        
                             ,  JAN_SHRINK_PCT                                  
                             ,  FEB_SHRINK_PCT                                  
                             ,  MAR_SHRINK_PCT                                  
                             ,  APR_SHRINK_PCT                                  
                             ,  MAY_SHRINK_PCT                                  
                             ,  JUN_SHRINK_PCT                                  
                             ,  JUL_SHRINK_PCT                                  
                             ,  AUG_SHRINK_PCT                                  
                             ,  SEP_SHRINK_PCT                                  
                             ,  OCT_SHRINK_PCT                                  
                             ,  NOV_SHRINK_PCT                                  
C14666**                     ,  DEC_SHRINK_PCT)                                 
C14666                       ,  DEC_SHRINK_PCT                                  
C14666                       ,  E_BUS_IND                                       
C14666                       ,  SHNK_PCT_CHG_DTE                                
C14666                       ,  LAST_UPD_BY_USR)                                
                       VALUES                                                   
                              (:PV-NEW-DKEY                                     
                             , :DPTSHK-SHRINK-YR-NBR                            
                             , :DPTSHK-CHG-TMST                                 
                             , :DPTSHK-JAN-SHRINK-PCT                           
                             , :DPTSHK-FEB-SHRINK-PCT                           
                             , :DPTSHK-MAR-SHRINK-PCT                           
                             , :DPTSHK-APR-SHRINK-PCT                           
                             , :DPTSHK-MAY-SHRINK-PCT                           
                             , :DPTSHK-JUN-SHRINK-PCT                           
                             , :DPTSHK-JUL-SHRINK-PCT                           
                             , :DPTSHK-AUG-SHRINK-PCT                           
                             , :DPTSHK-SEP-SHRINK-PCT                           
                             , :DPTSHK-OCT-SHRINK-PCT                           
                             , :DPTSHK-NOV-SHRINK-PCT                           
C14666*****                  , :DPTSHK-DEC-SHRINK-PCT)                          
C14666                       , :DPTSHK-DEC-SHRINK-PCT                           
C14666                       , :DPTSHK-E-BUS-IND                                
C14666                       , :DPTSHK-SHNK-PCT-CHG-DTE                         
C14666                       , :DPTSHK-LAST-UPD-BY-USR)                         
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = 0                                             
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5540-INSERT-DPTSHK'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'INSERT DPTSHK ROW'                                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5540-INSERT-DPTSHK'                                        
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'INSERT DPTSHK  RETRIES EXCEEDED'                           
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           ADD 1                     TO  PV-INSERT-DPTSHK-COUNT.                
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    CLOSE THE DPTSHK LIST CURSOR.                               *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       5550-CLOSE-DPTSHK-CURSOR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  CLOSE DPTSHK-LIST                                             
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE '5550-CLOSE-DPTSHK-CURSOR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'CLOSE DPTSHK CURSOR'                               
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '5550-CLOSE-DPTSHK-CURSOR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'CLOSE DPTSHK CURSOR RETRIES EXCEEDED'                      
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /                                                                         
      *----------------------------------------------------------------*        
      *                                                                *        
      *    CLOSE THE BATCH CONTROL FILE.                               *        
      *                                                                *        
      *----------------------------------------------------------------*        
                                                                                
       6000-CLOSE-BATCH-CONTROL.                                                
                                                                                
           CLOSE BATCH-CONTROL-FILE.                                            
                                                                                
           IF  PV-GOOD-STATUS                                                   
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               DISPLAY '*    ***MBKUT007 ABEND***       *'                      
               DISPLAY '*                               *'                      
               DISPLAY '*   INVALID STATUS ON THE BATCH *'                      
               DISPLAY '*   CONTROL DATASET - CLOSE     *'                      
               DISPLAY '*                               *'                      
               DISPLAY '* STATUS = '                                            
                       PV-VSAM-STATUS                                           
                       '                  *'                                    
               DISPLAY '*                               *'                      
               DISPLAY '* * * * * * * * * * * * * * * * *'                      
               SET ABEND-4006        TO  TRUE                                   
               CALL 'ILBOABN0'    USING  ABEND-CODE                             
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    CALCULATE THE END DATE FOR THE "OLD" ROW.  IF THE DATE    *          
      *    PASSED TO THIS ROUTINE IS THE EFFECTIVE START DATE FROM   *          
      *    THE CONTROL RECORD, USE PV-END-DATE-MINUS1 BECAUSE IT     *          
      *    HAS ALREADY BEEN CALCULATED.                              *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       7000-CALCULATE-END-DATE.                                                 
      *                                                                         
           IF  MB018-NEW-START-DATE-007 EQUAL PV-DB2-WORK-DATE                  
               MOVE PV-END-DATE-MINUS1                                          
                                     TO  PV-OLD-END-DATE-MINUS1                 
           ELSE                                                                 
               INITIALIZE DPG51, DPG52, DPG53, DPG54, DPG55, DPG56              
                                                                                
               SET DPG51-ACTUAL-CALENDAR-ONLY TO TRUE                           
               SET DPG51-DECREMENT-DATE    TO TRUE                              
               MOVE 1                      TO DPG51-INCR-DECR-DAYS-9            
               SET DPG52-LK-DTE-ISO-FMT    TO TRUE                              
               MOVE PV-DB2-WORK-DATE       TO DPG52-DB2-ISO-DATE-FMT            
                                                                                
               CALL  DP500-KOHLS-CALENDAR-ROUTINE                               
                     USING DPG51, DPG52, DPG53, DPG54, DPG55, DPG56             
                                                                                
               IF  DPG54-NO-ERROR                                               
                   MOVE DPG55-DB2-ISO-DATE-FMT                                  
                                        TO PV-OLD-END-DATE-MINUS1               
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    DISPLAY THE NUMBER OF RECORDS READ/WRITTEN.               *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       9000-DISPLAY-TOTALS.                                                     
      *                                                                         
           DISPLAY '*'.                                                         
           DISPLAY '* * * * * * * * * * * * * * * * * * * * * * * * *'.         
           DISPLAY ' '.                                                         
           DISPLAY '* * * * * * * * * * * * * * *'.                             
           DISPLAY '*                           *'.                             
           DISPLAY '*   **MBKUT007 COUNTS**     *'.                             
           DISPLAY '* '                                                         
                   PV-BATCH-CNTL-COUNT                                          
                   ' RECORDS READ    *'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-INSERTS                                               
                   ' TMDSEAR INSERTED*'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-UPDATES                                               
                   ' TMDSEAR UPDATED *'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-DELETES                                               
                   ' TMDSEAR DELETED *'.                                        
           DISPLAY '*                           *'.                             
           DISPLAY '* '                                                         
                   PV-TOT-APLAVLS                                               
                   ' TAPLAVL INSERTED*'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-APLAVLS-UPDATED                                       
                   ' TAPLAVL UPDATED *'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-APLAVLS-DELETED                                       
                   ' TAPLAVL DELETED *'.                                        
           DISPLAY '*                           *'.                             
           DISPLAY '* '                                                         
                   PV-TOT-RSPBARS                                               
                   ' TRSPBAR INSERTED*'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-RSPBARS-UPDATED                                       
                   ' TRSPBAR UPDATED *'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-RSPBARS-DELETED                                       
                   ' TRSPBAR DELETED *'.                                        
           DISPLAY '*                           *'.                             
           DISPLAY '* '                                                         
                   PV-TOT-MDSEDPS                                               
                   ' TMDSEDP INSERTED*'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-MDSEDPS-UPDATED                                       
                   ' TMDSEDP UPDATED *'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-MDSECLS                                               
                   ' TMDSECL INSERTED*'.                                        
           DISPLAY '*                           *'.                             
           DISPLAY '* '                                                         
                   PV-TOT-DPTSHKS                                               
                   ' TDPTSHK INSERTED*'.                                        
           DISPLAY '* '                                                         
                   PV-TOT-DPTSHKS-UPDATED                                       
                   ' TDPTSHK UPDATED *'.                                        
           DISPLAY '*                           *'.                             
           DISPLAY '* * * * * * * * * * * * * * *'.                             
      *                                                                         
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      *                                                              *          
      ****************************************************************          
      *                                                                         
       9900-SQL-ABEND.                                                          
      *                                                                         
           SET ABEND-4013             TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  MBKUT007'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
      *                                                                         
