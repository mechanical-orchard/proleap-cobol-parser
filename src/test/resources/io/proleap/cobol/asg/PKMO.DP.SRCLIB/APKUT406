       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     APKUT406.                                                
       AUTHOR.         JILL JUEL.                                               
       DATE-WRITTEN.   JUNE 16, 2000.                                           
       DATE-COMPILED.                                                           
      *************************************************************             
      *    COBOL 2                                                *             
      *                                                           *             
      *    MERGE TMIITEM AND TPREQIT UNLOAD FILES                 *             
      *                                                           *             
      * INPUT:                                                    *             
      * 1. UNLOAD OF TMIITEM                                      *             
      * 2. UNLOAD OF TPREQIT                                      *             
      *                                                           *             
      * OUTPUT:                                                   *             
      * 1.  MERGED FILE                                           *             
      *                                                           *             
      *************************************************************             
      * CHANGE LOG:                                               *             
      *                                                           *             
      * 06/16/2000  INITIAL VERSION.                              *             
      * 10/03/2000  JILL JUEL                       WR# 20741     *             
      *       ADDED CODE TO MATCH RECORDS BY PAYT-REQ-LNE-NBR.    *             
      *                                                           *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-TMIITEM-FILE                                            
               ASSIGN TO UT-S-INP01.                                            
                                                                                
           SELECT INPUT-TPREQIT-FILE                                            
               ASSIGN TO UT-S-INP02.                                            
                                                                                
           SELECT OUTPUT-MERGE-FILE                                             
               ASSIGN TO UT-S-OUT01.                                            
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-TMIITEM-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS  F                                                 
           RECORD CONTAINS 23 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-TMIITEM-RECORD         PIC X(23).                              
                                                                                
       FD  INPUT-TPREQIT-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS  F                                                 
           RECORD CONTAINS 20 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INPUT-TPREQIT-RECORD         PIC X(20).                              
                                                                                
       FD  OUTPUT-MERGE-FILE                                                    
           LABEL RECORDS  ARE STANDARD                                          
           RECORDING MODE IS  F                                                 
           DATA RECORD IS  OUTPUT-MERGE-RECORD.                                 
       01  OUTPUT-MERGE-RECORD          PIC X(33).                              
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
       01  WS-SWITCH-AREA.                                                      
           05  WS-TMIITEM-EOF-SW            PIC X(01)  VALUE 'N'.               
               88  TMIITEM-EOF                         VALUE 'Y'.               
               88  NOT-TMIITEM-EOF                     VALUE 'N'.               
           05  WS-TPREQIT-EOF-SW            PIC X(01)  VALUE 'N'.               
               88  TPREQIT-EOF                         VALUE 'Y'.               
               88  NOT-TPREQIT-EOF                     VALUE 'N'.               
                                                                                
       01  WS-PROGRAM-COUNTERS.                                                 
           05  TMIITEM-COUNT                PIC  9(11) VALUE ZEROS.             
           05  TPREQIT-COUNT                PIC  9(11) VALUE ZEROS.             
           05  MERGE-FILE-COUNT             PIC  9(11) VALUE ZEROS.             
           05  SKIP-REC-COUNT               PIC  9(11) VALUE ZEROS.             
                                                                                
       01  WS-TMIITEM.                                                          
           05 MIITEM-PAYT-REQ-SEQ-NBR       PIC  S9(9) COMP.                    
           05 MIITEM-PAYT-REQ-LNE-NBR       PIC  S9(9) COMP.                    
           05 MIITEM-UNT-COST-SEL-CDE       PIC   X(1).                         
           05 MIITEM-PO-NBR                 PIC  S9(9) COMP.                    
           05 MIITEM-VER-NBR                PIC  S9(9) COMP.                    
           05 FILLER                        PIC   X(1).                         
           05 MIITEM-PO-LNE-NBR             PIC  S9(9) COMP.                    
           05 FILLER                        PIC   X(1).                         
                                                                                
       01  WS-TPREQIT.                                                          
           05 PREQIT-PAYT-REQ-SEQ-NBR       PIC  S9(9) COMP.                    
           05 PREQIT-PAYT-REQ-LNE-NBR       PIC  S9(9) COMP.                    
           05 PREQIT-PAY-REQ-LNE-QTY        PIC  S9(9) COMP.                    
           05 PREQIT-QTY-UNT-OF-MSMT        PIC   X(2).                         
           05 PREQIT-UNT-COST-AMT           PIC  S9(7)V9(3) COMP-3.             
                                                                                
      *****************************************************************         
      **  THIS IS THE LAYOUT FOR THE MERGER OF TMIITEM AND TPREQIT   **         
      **  UNLOAD FILES.                                              **         
      *****************************************************************         
           COPY APRD406.                                                        
                                                                                
       01  ABEND-CODE                    PIC S9(4) COMP SYNC                    
                                                   VALUE ZEROS.                 
                                                                                
       01  WS-RETRY-COUNT                PIC S9(4) COMP VALUE ZERO.             
       01  WS-RETRY-MAX                  PIC S9(4) COMP VALUE 3.                
                                                                                
       01  WS-ABEND-FIELDS.                                                     
           05  AA-ABEND-LIT            PIC  X(40)     VALUE                     
                   '*****       ABEND'.                                         
           05  AA-PROGRAM-LIT          PIC  X(40)     VALUE                     
                   '*****   PROGRAM: APKUT406'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC  X(17)     VALUE                     
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC  X(35)     VALUE SPACES.             
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC  X(06)     VALUE '*****'.            
               10  AA-MESSAGE          PIC  X(80)     VALUE SPACES.             
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
           PERFORM B100-INITIALIZATION.                                         
                                                                                
           PERFORM B200-MERGE-FILES                                             
             UNTIL TMIITEM-EOF AND                                              
                   TPREQIT-EOF.                                                 
                                                                                
           PERFORM B300-TERMINATION.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      *************************************************************             
      *  INITIALIZATION ROUTINE                                   *             
      *************************************************************             
       B100-INITIALIZATION.                                                     
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*---------- PROGRAM:  APKUT406 ----------*'                 
           DISPLAY 'SYSTEM DATE ' FUNCTION CURRENT-DATE(1:8)                    
                        '  TIME ' FUNCTION CURRENT-DATE(9:6).                   
                                                                                
           OPEN INPUT INPUT-TMIITEM-FILE                                        
                      INPUT-TPREQIT-FILE                                        
               OUTPUT OUTPUT-MERGE-FILE.                                        
                                                                                
           PERFORM R100-READ-TMIITEM.                                           
           PERFORM R200-READ-TPREQIT.                                           
                                                                                
           IF  TMIITEM-EOF                                                      
               MOVE 'B100-INITIALIZATION'       TO AA-PARAGRAPH-NAME            
               MOVE 'EMPTY TMIITEM INPUT FILE ' TO AA-MESSAGE                   
               PERFORM Z998-ABEND                                               
           END-IF.                                                              
                                                                                
           IF  TPREQIT-EOF                                                      
               MOVE 'B100-INITIALIZATION'       TO AA-PARAGRAPH-NAME            
               MOVE 'EMPTY TPREQIT INPUT FILE ' TO AA-MESSAGE                   
               PERFORM Z998-ABEND                                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      *************************************************************             
      *  MAIN PROCESSING FOR MERGING TMIITEM AND TPREQIT FILES    *             
      *************************************************************             
       B200-MERGE-FILES.                                                        
                                                                                
           IF MIITEM-PAYT-REQ-SEQ-NBR = PREQIT-PAYT-REQ-SEQ-NBR                 
              IF MIITEM-PAYT-REQ-LNE-NBR = PREQIT-PAYT-REQ-LNE-NBR              
                 MOVE MIITEM-PAYT-REQ-SEQ-NBR TO AP406-PAYT-REQ-SEQ-NBR         
                 MOVE MIITEM-PAYT-REQ-LNE-NBR TO AP406-PAYT-REQ-LNE-NBR         
                 MOVE MIITEM-UNT-COST-SEL-CDE TO AP406-UNT-COST-SEL-CDE         
                 MOVE MIITEM-PO-NBR           TO AP406-PO-NBR                   
                 MOVE MIITEM-VER-NBR          TO AP406-VER-NBR                  
                 MOVE MIITEM-PO-LNE-NBR       TO AP406-PO-LNE-NBR               
                 MOVE PREQIT-PAY-REQ-LNE-QTY  TO AP406-PAY-REQ-LNE-QTY          
                 MOVE PREQIT-QTY-UNT-OF-MSMT  TO AP406-QTY-UNT-OF-MSMT          
                 MOVE PREQIT-UNT-COST-AMT     TO AP406-UNT-COST-AMT             
                 PERFORM W100-WRITE-MERGE-FILE                                  
                 PERFORM R100-READ-TMIITEM                                      
                 PERFORM R200-READ-TPREQIT                                      
              ELSE                                                              
                IF MIITEM-PAYT-REQ-LNE-NBR > PREQIT-PAYT-REQ-LNE-NBR            
                   ADD 1 TO SKIP-REC-COUNT                                      
                   PERFORM R200-READ-TPREQIT                                    
                ELSE                                                            
                   ADD 1 TO SKIP-REC-COUNT                                      
                   PERFORM R100-READ-TMIITEM                                    
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
              IF MIITEM-PAYT-REQ-SEQ-NBR > PREQIT-PAYT-REQ-SEQ-NBR              
                 ADD 1 TO SKIP-REC-COUNT                                        
                 PERFORM R200-READ-TPREQIT                                      
              ELSE                                                              
                 ADD 1 TO SKIP-REC-COUNT                                        
                 PERFORM R100-READ-TMIITEM                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      *************************************************************             
      *  TERMINATION ROUTINE                                      *             
      *************************************************************             
       B300-TERMINATION.                                                        
           CLOSE INPUT-TMIITEM-FILE                                             
                 INPUT-TPREQIT-FILE                                             
                 OUTPUT-MERGE-FILE.                                             
                                                                                
           DISPLAY '*****************************************'.                 
           DISPLAY '**        TOTALS FOR APKUT406          **'.                 
           DISPLAY '*****************************************'.                 
           DISPLAY 'TOTAL TMIITEM READ   = ' TMIITEM-COUNT.                     
           DISPLAY 'TOTAL TPREQIT READ   = ' TPREQIT-COUNT.                     
           DISPLAY 'TOTAL MERGE RECORDS  = ' MERGE-FILE-COUNT.                  
           DISPLAY 'SKIPPED RECORDS      = ' SKIP-REC-COUNT.                    
           DISPLAY '*****************************************'.                 
                                                                                
      *************************************************************             
      *  READ TMIITEM FILE                                        *             
      *************************************************************             
       R100-READ-TMIITEM.                                                       
                                                                                
           READ INPUT-TMIITEM-FILE INTO WS-TMIITEM                              
             AT END                                                             
                SET TMIITEM-EOF TO TRUE                                         
                MOVE +999999999 TO MIITEM-PAYT-REQ-SEQ-NBR                      
             NOT AT END                                                         
                ADD 1 TO TMIITEM-COUNT                                          
           END-READ.                                                            
                                                                                
      *************************************************************             
      *  READ TPREQIT FILE                                        *             
      *************************************************************             
       R200-READ-TPREQIT.                                                       
                                                                                
           READ INPUT-TPREQIT-FILE INTO WS-TPREQIT                              
             AT END                                                             
                SET TPREQIT-EOF TO TRUE                                         
                MOVE +999999999 TO PREQIT-PAYT-REQ-SEQ-NBR                      
             NOT AT END                                                         
                ADD 1 TO TPREQIT-COUNT                                          
           END-READ.                                                            
                                                                                
      *************************************************************             
      *  WRITE MERGED FILE                                        *             
      *************************************************************             
       W100-WRITE-MERGE-FILE.                                                   
                                                                                
           WRITE OUTPUT-MERGE-RECORD FROM AP406-TMIITEM-TPREQIT-RECORD.         
           ADD 1 TO MERGE-FILE-COUNT.                                           
                                                                                
      *************************************************************             
      *    ABEND ROUTINE FOR NON DB2 ERRORS                       *             
      *************************************************************             
       Z998-ABEND.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
