       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      AUKUT016.                                       00020044
       AUTHOR.          TOM HANSON.                                     00030000
       DATE-WRITTEN.    JUNE, 2004.                                     00040000
                                                                        00050000
      *----------------------------------------------------------------*00060000
      *            REWARDS REDEMPTION PARAMETER FILE EXTRACT            00070000
      *----------------------------------------------------------------*00080000
      *   OVERVIEW                                                      00090000
      *   - THIS PROGRAM WILL EXTRACT AND FORMAT ALL REWARD EVENT       00100000
      *     RECORDS WHICH ARE CURRENTLY SET AS ACTIVE. DEPENDING ON     00110000
      *     A CONTROL CARD, THE PROGRAM WILL EITHER AUTOMATICALLY       00120000
      *     RUN THE FULL EXTRACT PROCESS, OR IT WILL CHECK FOR ANY      00130000
      *     UPDATES ON THE APPROPRIATE TABLES.  IF IT DOES FIND ANY     00140000
      *     CHANGES HAVE BEEN MADE, IT WILL RUN THE FULL EXTRACT.       00150000
      *     ORIGINAL PROGRAM WAS EPKUT149.                              00160031
      *----------------------------------------------------------------*00170000
      * HISTORY LOG:                                                    00180000
      *----------------------------------------------------------------*00190000
073004* DATE:        07/30/2004 * CHANGES DENOTED BY 073004             00200000
*     * WR/IR#:      P000679532                                         00210000
*     * PROGRAMMER:  THOMAS HANSON                                      00220000
*     * DESCRIPTION: MINOR IMPROVEMENTS TO SQL CURSOR CALLS, AND ADDTL  00230000
*     *              CURSOR LOGIC TO HELP SUPPORT KOHL'S "YES, WE CAN"  00240000
073004*              CUSTOMER POLICY SERVICES. SENDS REDM PARAMS SOONER.00250000
      *----------------------------------------------------------------*00260000
      * DATE:        09/14/2005                                         00270000
      * WR/IR#:      WR28518                                            00280000
      * PROGRAMMER:  SCOTT BARTELT                                      00290000
      * DESCRIPTION: PRIOR TO RETURN AUTOMATION THE DISCOUNT AMOUNT     00300000
      *              FIELD ON THE OUTPUT RECORD WAS ALWAYS POPULATED    00310000
      *              WITH A VALUE BECAUSE INDIVIDUAL REWARDS FOR A PRE- 00320000
      *              DEFINED AMOUNT WERE USED.  SO WHETHER AN AUTH WAS  00330000
      *              REQUIRED OR NOT, THE VALUE OF THE REWARD WAS ALWAYS00340000
      *              KNOWN.                                             00350000
      *              NOW THERE WILL BE ONE REWARD ISSUED WITH ANY VALUE.00360000
      *              IT'S POSSIBLE THAT ONCE THE ACTIVATION PHASE OF AN 00370000
      *              EVENT HAS STARTED THE USERS MAY DECIDE TO TURN OFF 00380000
      *              AUTHORIZATION.  IF THIS OCCURS THE POS OPERATOR    00390000
      *              WILL HAVE TO BE PROMPTED TO ENTER THE VALUE OF THE 00400000
      *              REWARD FROM THE BACK OF THE CARD.                  00410000
      *              A DATABASE VALUE OF '2' IN THE AUTH TYPE WILL      00420000
      *              INDICATE THIS SITUATION IS OCCURING.  THEREFORE,   00430000
      *              THE OUTPUT FILE SHOULD CONTAIN AN AUTH TYPE CODE   00440000
      *              OF '0'(DON'T AUTHORIZE) AND AN AMOUNT FIELD OF     00450000
      *              '00000' WHICH WILL INDICATE TO POS THAT THE PROMPT 00460000
      *              SHOULD BE GIVEN.                                   00470000
      *----------------------------------------------------------------*00480000
      * DATE:        09/14/2005                                         00490000
      * WR/IR#:      WR28518                                            00500000
      * PROGRAMMER:  SCOTT BARTELT                                      00510000
      * DESCRIPTION: WITH RETURN AUTOMATION ALL PAST EVENTS MUST NOW    00520000
      *              BE ON THE FILE.                                    00530000
      *----------------------------------------------------------------*00540000
061306* DATE:        06/13/2006 *CHANGES DENOTED BY 061306              00550000
061306* WR/IR#:      P000000000                                         00560000
061306* PROGRAMMER:  STEVEN NOWAK                                       00570000
061306* DESCRIPTION: UPDATE DATE IN CURSOR                              00580000
062309*----------------------------------------------------------------*00590000
062309* DATE:        06/23/2009 *CHANGES DENOTED BY '062309'            00600000
062309* WR/IR#:      INC000000852057                                    00610000
062309* PROGRAMMER:  THOMAS HANSON                                      00620000
062309* DESCRIPTION: THE OUTPUT FILES APPARENTLY STILL HAD A FILE SIZE  00630000
062309*              RESTRICTION AT POS AND THE ACTIVATE.TXT FILE WENT  00640000
062309*              PAST THE 20K LIMIT CAUSING GPFS AT THE REGISTERS.  00650000
062309*              THIS CHANGE WILL LIMIT THE EVENTS INCLUDED TO THE  00660000
062309*              ONES WITH AN END DATE GREATER THAN CURRENT DATE    00670000
062309*              MINUS 18 MONTHS. PREVIOUSLY WE HAD USED A STATIC   00680000
062309*              HARDCODED DATE OF '2005-11-30'.                    00690000
W36988*----------------------------------------------------------------*00700001
W36988* DATE:        07/28/2009 *CHANGES DENOTED BY 'W36988'            00710007
W36988* WR/IR#:      WR36988                                            00720001
W36988* PROGRAMMER:  THOMAS HANSON                                      00730001
W36988* DESCRIPTION: AS A FOLLOW-UP EFFORT TO THE FILE SIZE LIMIT ISSUE 00740001
W36988*              WE WILL PREVENT THE FILE FROM GETTING PAST THE     00750001
W36988*              EXTRACT PROGRAM IF IT WOULD BE LARGE ENOUGH TO     00760001
W36988*              CAUSE A PROBLEM AT POS. IF WE ARE NEARING THE FILE 00770001
W36988*              RECORD COUNT LIMIT, WE WILL SEND AN EMAIL TO THE   00780001
W36988*              SUPPORT TEAM TO INFORM THEM OF THE POTENTIAL RISK. 00790001
092410*----------------------------------------------------------------*00800028
092410* DATE:        09/24/2010 *CHANGES DENOTED BY '092410'            00810028
092410* WR/IR#:      WR38366                                            00820028
092410* PROGRAMMER:  JACKIE HLEING                                      00830028
092410* DESCRIPTION: CREATE SECOND FILE FOR E-COMM                      00840028
092410*----------------------------------------------------------------*00850028
PK8161* DATE:        06/16/2011 *CHANGES DENOTED BY 'PK8161'            00860029
PK8161* WR/IR#:      PKE000000008161                                    00870029
PK8161* PROGRAMMER:  COGNIZANT                                          00880029
PK8161* DESCRIPTION: TO INCREASE THE FILE RECORD COUNT LIMIT TO 500 FOR 00890029
PK8161*              WARNINGS AND 600 TO ABEND.                         00900029
PK8161*----------------------------------------------------------------*00910029
      * DATE:        07/25/2016 CHG0146560                              00920055
      * WR/IR#:      11082d - Kohl's Cash project - EPKUT148 - old pgm  00930031
      * PROGRAMMER:  Baskaran Srinivasan                                00940031
      * DESCRIPTION: Convert existing DB2 calls to Oracle using the     00950031
      *              DBLinks for XST tables.                            00960031
      *              Combines both Oracle tables and DB2 views.         00970031
      *              Since the Oracle tables are defined in AU instance 00980031
      *              and belong to AU, the program was moved from       00990031
      *              EPKUT148 to AUKUT012.                              01000031
      *              The Job and Procs didn't change.                   01010031
      *----------------------------------------------------------------*01020031
      *----------------------------------------------------------------*01021069
      * DATE:        07/12/2017                                         01022069
      * WR/IR#:      CHG0172687                                         01023073
      * PROGRAMMER:  Vinoth Ramamurthy                                  01024069
      * DESCRIPTION: Business wanted to have the rewards work as it is  01025069
      *              till it expires. Modified RWD_REDM_EVNT_CSR cursor 01026069
      *              to fetch the reward till the redeem end date.      01027071
      *              Changes are tagged in CHG0172687.                  01027173
      *                                                                 01028071
      *----------------------------------------------------------------*01029369
                                                                        01030000
      *----------------------------------------------------------------*01040000
       ENVIRONMENT DIVISION.                                            01050000
      *----------------------------------------------------------------*01060000
       CONFIGURATION SECTION.                                           01070000
       SOURCE-COMPUTER.        IBM-3090.                                01080000
       OBJECT-COMPUTER.        IBM-3090.                                01090000
                                                                        01100000
       INPUT-OUTPUT SECTION.                                            01110000
                                                                        01120000
       FILE-CONTROL.                                                    01130000
                                                                        01140000
           SELECT CTRL-CRD-PGM-LAST-RUN                                 01150000
             FILE STATUS IS FS-CTRL-CRD-PGM-LAST-RUN                    01160000
             ASSIGN TO INP01.                                           01170000
                                                                        01180000
           SELECT CTRL-CRD-IDENT-TYP                                    01190000
             FILE STATUS IS FS-CTRL-CRD-IDENT-TYP                       01200000
             ASSIGN TO INP02.                                           01210000
                                                                        01220000
           SELECT CTRL-CRD-CUR-BTCH-DTE                                 01230000
             FILE STATUS IS FS-CTRL-CRD-CUR-BTCH-DTE                    01240000
             ASSIGN TO INP03.                                           01250000
                                                                        01260000
           SELECT CTRL-CRD-BTCH-FUTR-DTE                                01270000
             FILE STATUS IS FS-CTRL-CRD-BTCH-FUTR-DTE                   01280000
             ASSIGN TO INP04.                                           01290000
                                                                        01300000
           SELECT CTRL-CRD-ADV-OPN-DAYS                                 01310000
             FILE STATUS IS FS-CTRL-CRD-ADV-OPN-DAYS                    01320000
             ASSIGN TO INP05.                                           01330000
                                                                        01340000
           SELECT CONTROL-CARD-ORACLE-LOGIN                             01350031
             ASSIGN TO ORALOGON.                                        01360056
                                                                        01370031
           SELECT RWD-REDM-EXTRCT-FLE                                   01380000
             FILE STATUS IS FS-RWD-REDM-EXTRCT-FLE                      01390000
             ASSIGN TO OUT01.                                           01400000
                                                                        01410000
092410     SELECT RWD-REDM-EXTRCT-FLE-ECOM                              01420027
092410       FILE STATUS IS FS-RWD-REDM-EXTRCT-FLE-ECOM                 01430027
092410       ASSIGN TO OUT02.                                           01440027
                                                                        01450008
           SELECT RWD-REDM-EXTRCT-RPT                                   01460000
             FILE STATUS IS FS-RWD-REDM-EXTRCT-RPT                      01470000
             ASSIGN TO RPT01.                                           01480000
                                                                        01490000
       DATA DIVISION.                                                   01500000
       FILE SECTION.                                                    01510000
                                                                        01520000
       FD  CTRL-CRD-PGM-LAST-RUN                                        01530000
           RECORDING MODE F                                             01540000
           BLOCK CONTAINS 0 RECORDS                                     01550000
           LABEL RECORDS ARE OMITTED                                    01560000
           DATA RECORD IS CTRL-CRD-PGM-LAST-RUN-REC.                    01570000
       01  CTRL-CRD-PGM-LAST-RUN-REC       PIC  X(080).                 01580000
                                                                        01590000
       FD  CTRL-CRD-IDENT-TYP                                           01600000
           RECORDING MODE F                                             01610000
           BLOCK CONTAINS 0 RECORDS                                     01620000
           LABEL RECORDS ARE OMITTED                                    01630000
           DATA RECORD IS CTRL-CRD-IDENT-TYP-REC.                       01640000
       01  CTRL-CRD-IDENT-TYP-REC          PIC  X(080).                 01650000
                                                                        01660000
       FD  CTRL-CRD-CUR-BTCH-DTE                                        01670000
           RECORDING MODE F                                             01680000
           BLOCK CONTAINS 0 RECORDS                                     01690000
           LABEL RECORDS ARE OMITTED                                    01700000
           DATA RECORD IS CTRL-CRD-CUR-BTCH-DTE-REC.                    01710000
       01  CTRL-CRD-CUR-BTCH-DTE-REC       PIC  X(080).                 01720000
                                                                        01730000
       FD  CTRL-CRD-BTCH-FUTR-DTE                                       01740000
           RECORDING MODE F                                             01750000
           BLOCK CONTAINS 0 RECORDS                                     01760000
           LABEL RECORDS ARE OMITTED                                    01770000
           DATA RECORD IS CTRL-CRD-BTCH-FUTR-REC.                       01780000
       01  CTRL-CRD-BTCH-FUTR-REC          PIC  X(080).                 01790000
                                                                        01800000
       FD  CTRL-CRD-ADV-OPN-DAYS                                        01810000
           RECORDING MODE F                                             01820000
           BLOCK CONTAINS 0 RECORDS                                     01830000
           LABEL RECORDS ARE OMITTED                                    01840000
           DATA RECORD IS CTRL-CRD-ADV-OPN-DAYS-REC.                    01850000
       01  CTRL-CRD-ADV-OPN-DAYS-REC       PIC  X(080).                 01860000
                                                                        01870000
       FD  CONTROL-CARD-ORACLE-LOGIN                                    01880031
           RECORDING MODE IS F                                          01890031
           LABEL RECORDS ARE STANDARD                                   01900031
           RECORD CONTAINS 080 CHARACTERS                               01910031
           BLOCK CONTAINS 0 RECORDS                                     01920031
           DATA RECORD IS CONTROL-CARD-ORACLE-LOGIN-RCD.                01930031
                                                                        01940031
       01  CONTROL-CARD-ORACLE-LOGIN-RCD      PIC X(080).               01950031
                                                                        01960031
       FD  RWD-REDM-EXTRCT-FLE                                          01970000
           RECORDING MODE IS V                                          01980000
           LABEL RECORDS ARE STANDARD                                   01990000
           RECORD IS VARYING FROM 1 TO 66                               02000000
           DEPENDING ON PV-RWD-RDM-REC-BYTE-CNT                         02010000
           DATA RECORD IS RWD-REDM-EXTRCT-RECORD.                       02020000
       01  RWD-REDM-EXTRCT-RECORD.                                      02030000
           05  RWD-REDM-EXTRCT-BYTE        PIC  X(001)                  02040000
                  OCCURS 1 TO 66 TIMES                                  02050000
                  DEPENDING ON PV-RWD-RDM-REC-BYTE-CNT.                 02060000
                                                                        02070000
092410 FD  RWD-REDM-EXTRCT-FLE-ECOM                                     02080027
092410     RECORDING MODE IS V                                          02090027
092410     LABEL RECORDS ARE STANDARD                                   02100027
092410     RECORD IS VARYING FROM 1 TO 72                               02110027
092410     DEPENDING ON PV-RWD-RDM-REC-BYTE-CNT-EC                      02120027
092410     DATA RECORD IS RWD-REDM-EXTRCT-RECORD-ECOM.                  02130027
092410 01  RWD-REDM-EXTRCT-RECORD-ECOM.                                 02140027
092410     05  RWD-REDM-EXTRCT-BYTE-ECOM        PIC  X(001)             02150027
092410            OCCURS 1 TO 72 TIMES                                  02160027
092410            DEPENDING ON PV-RWD-RDM-REC-BYTE-CNT-EC.              02170027
                                                                        02180008
       FD  RWD-REDM-EXTRCT-RPT                                          02190000
           RECORDING MODE IS F                                          02200000
           LABEL RECORDS ARE STANDARD                                   02210000
           BLOCK CONTAINS 0 RECORDS                                     02220000
           RECORD CONTAINS 132 CHARACTERS.                              02230000
       01  RWD-REDM-EXTRCT-RPT-REC         PIC  X(132).                 02240000
                                                                        02250000
       WORKING-STORAGE SECTION.                                         02260000
                                                                        02270000
      *----------------------------------------------------------------*02280000
      *  AB- ABEND CODES                                                02290000
      *----------------------------------------------------------------*02300000
       01  AB-ABEND-CODE                   PIC  S9(004) VALUE ZERO      02310000
                                                              COMP SYNC.02320000
           88  ABEND-4003-CTRL-CARD-ERROR               VALUE +4003.    02330000
           88  ABEND-4013-DB2-ERROR                     VALUE +4013.    02340033
           88  ABEND-4013-ORA-ERROR                     VALUE +4013.    02350037
           88  ABEND-4019-CAL-SUB-ERROR                 VALUE +4019.    02360000
W36988     88  ABEND-4444-FORCED-ABEND                  VALUE +4444.    02370001
                                                                        02380000
      *----------------------------------------------------------------*02390031
      *  CC- CONTROL CARD VARIABLES                                     02400031
      *----------------------------------------------------------------*02410031
       01  CC-ORACLE-CONNECTION.                                        02420031
           05  CC-ORACLE-USERID             PIC  X(40) VALUE SPACES.    02430033
               88 CC-AUTO-LOGON                        VALUE '/'.       02440031
           05  CC-ORACLE-PASSWORD           PIC  X(40) VALUE SPACES.    02450033
                                                                        02460031
      *----------------------------------------------------------------*02470000
      *  FS- FILE STATUS CODE VARIABLES                                 02480000
      *----------------------------------------------------------------*02490000
       01  FS-FILE-STATUS-CODES.                                        02500000
           05  FS-CTRL-CRD-PGM-LAST-RUN    PIC  X(002).                 02510000
               88  FS-CTRL-CRD-PGM-LAST-RUN-OK          VALUE '00'.     02520000
               88  FS-CTRL-CRD-PGM-LAST-RUN-EOF         VALUE '10'.     02530000
                                                                        02540000
           05  FS-CTRL-CRD-IDENT-TYP       PIC  X(002).                 02550000
               88  FS-CTRL-CRD-IDENT-TYP-OK             VALUE '00'.     02560000
               88  FS-CTRL-CRD-IDENT-TYP-EOF            VALUE '10'.     02570000
                                                                        02580000
           05  FS-CTRL-CRD-CUR-BTCH-DTE    PIC  X(002).                 02590000
               88  FS-CTRL-CRD-CUR-BTCH-DTE-OK          VALUE '00'.     02600000
               88  FS-CTRL-CRD-CUR-BTCH-DTE-EOF         VALUE '10'.     02610000
                                                                        02620000
           05  FS-CTRL-CRD-BTCH-FUTR-DTE   PIC  X(002).                 02630000
               88  FS-CTRL-CRD-BTCH-FUTR-DTE-OK         VALUE '00'.     02640000
               88  FS-CTRL-CRD-BTCH-FUTR-DTE-EOF        VALUE '10'.     02650000
                                                                        02660000
           05  FS-CTRL-CRD-ADV-OPN-DAYS    PIC  X(002).                 02670000
               88  FS-CTRL-CRD-DAYS-TILL-OPN-OK         VALUE '00'.     02680000
               88  FS-CTRL-CRD-DAYS-TILL-OPN-EOF        VALUE '10'.     02690000
                                                                        02700000
           05  FS-RWD-REDM-EXTRCT-FLE      PIC  X(002).                 02710000
               88  FS-RWD-REDM-EXTRCT-FLE-OK            VALUE '00'.     02720000
               88  FS-RWD-REDM-EXTRCT-FLE-EOF           VALUE '10'.     02730000
                                                                        02740000
092410     05  FS-RWD-REDM-EXTRCT-FLE-ECOM      PIC  X(002).            02750027
092410         88  FS-RWD-REDM-EXTRCT-FLE-OK-EC         VALUE '00'.     02760027
092410         88  FS-RWD-REDM-EXTRCT-FLE-EOF-EC        VALUE '10'.     02770027
                                                                        02780008
           05  FS-RWD-REDM-EXTRCT-RPT      PIC  X(002).                 02790000
               88  FS-RWD-REDM-EXTRCT-RPT-OK            VALUE '00'.     02800000
               88  FS-RWD-REDM-EXTRCT-RPT-EOF           VALUE '10'.     02810000
                                                                        02820000
      *----------------------------------------------------------------*02830000
      *  PC- PROGRAM CONSTANTS                                          02840000
      *----------------------------------------------------------------*02850000
       01  PC-PROGRAM-CONSTANTS.                                        02860000
           05  PC-PROGRAM-NAME             PIC  X(008)                  02870000
                                                VALUE 'AUKUT016'.       02880044
           05  PC-RWD-REDM-FIXED-LENGTH    PIC  9(002)  VALUE 41.       02890000
092410     05  PC-RWD-REDM-FIX-LGTH-ECOM   PIC  9(002)  VALUE 47.       02900027
           05  PC-0-DEPT-SIZE              PIC  9(002)  VALUE 4.        02910000
           05  PC-1-DEPT-SIZE              PIC  9(002)  VALUE 8.        02920000
           05  PC-2-DEPT-SIZE              PIC  9(002)  VALUE 12.       02930000
           05  PC-3-DEPT-SIZE              PIC  9(002)  VALUE 16.       02940000
           05  PC-4-DEPT-SIZE              PIC  9(002)  VALUE 20.       02950000
           05  PC-5-DEPT-SIZE              PIC  9(002)  VALUE 24.       02960000
           05  PC-SQL-MAX-RETRIES          PIC  9(001)  VALUE 5.        02970000
           05  PC-MDSE-MAX-DEPTS           PIC  9(001)  VALUE 5.        02980000
           05  PC-MDSE-ALL-DEPTS           PIC  9(001)  VALUE 2.        02990000
           05  PC-REPORT-NUMBER            PIC  9(001)  VALUE 1.        03000000
           05  PC-LINES-PER-PAGE           PIC  9(002)  VALUE 50.       03010000
           05  PC-LINES-FOR-HDRS           PIC  9(001)  VALUE 9.        03020000
           05  PC-AUTO-LOGON               PIC  X(1)    VALUE '/'.      03030031
W36988                                                                  03040001
W36988*-->   IN ORDER TO AVOID SENDING A FILE THAT IS TOO LARGE FOR     03050001
W36988*-->   THE POS REGISTER APP TO HANDLE, WE WILL BE PREVENTING THE  03060001
W36988*-->   FILE TRANSMISSION DURING THE EXTRACT PROGRAMS THEMSELVES.  03070001
PK8161   05  PC-FILE-COUNT-LIMIT-ABEND        PIC 9(03)  VALUE 600.     03080030
PK8161   05  PC-FILE-COUNT-LIMIT-EMAIL        PIC 9(03)  VALUE 500.     03090030
W36988   05  PC-EMAIL-RETURN-CODE             PIC 9(02)  VALUE 04.      03100001
                                                                        03110000
      *----------------------------------------------------------------*03120000
      *  PS - PROGRAM SWITCHES                                          03130000
      *----------------------------------------------------------------*03140000
       01  PS-PROGRAM-SWITCHES.                                         03150000
           05  PS-END-OF-CNTL-CRD-SW       PIC  X(001).                 03160000
               88  PS-END-OF-CNTL-CRD-FILE              VALUE 'Y'.      03170000
               88  PS-NOT-END-OF-CNTL-CRD-FILE          VALUE 'N'.      03180000
                                                                        03190000
           05  PS-FUTURE-CHG-CRD-SW        PIC  X(001).                 03200000
               88  PS-FUTURE-CHG-FND                    VALUE 'Y'.      03210000
               88  PS-NO-FUTURE-CHG-FND                 VALUE 'N'.      03220000
                                                                        03230000
           05  PS-RWD-ACTV-CSR-SW          PIC  X(001).                 03240000
               88  PS-END-OF-RWD-ACTV-CSR               VALUE 'Y'.      03250000
               88  PS-NOT-END-OF-RWD-ACTV-CSR           VALUE 'N'.      03260000
                                                                        03270000
           05  PS-RWD-REDM-CSR-SW          PIC  X(001).                 03280000
               88  PS-END-OF-RWD-REDM-CSR               VALUE 'Y'.      03290000
               88  PS-NOT-END-OF-RWD-REDM-CSR           VALUE 'N'.      03300000
                                                                        03310000
           05  PS-STR-LOC-CSR-SW           PIC  X(001).                 03320000
               88  PS-END-OF-STR-LOC-CSR                VALUE 'Y'.      03330000
               88  PS-NOT-END-OF-STR-LOC-CSR            VALUE 'N'.      03340000
                                                                        03350000
           05  PS-IE-MDSE-DEPTS-SW         PIC  X(001).                 03360000
               88  PS-END-OF-DEPTS-CSR                  VALUE 'Y'.      03370000
               88  PS-NOT-END-OF-DEPTS-CSR              VALUE 'N'.      03380000
                                                                        03390000
           05  PS-LINE-TERM-FOUND-SW       PIC  X(001).                 03400000
               88  PS-LINE-END-FOUND                    VALUE 'Y'.      03410000
               88  PS-LINE-END-NOT-FOUND                VALUE 'N'.      03420000
                                                                        03430000
W36988     05  PS-SET-EMAIL-RC-FLAG-SW     PIC X(01).                   03440002
W36988         88  PS-SET-EMAIL-RC                      VALUE 'Y'.      03450002
W36988         88  PS-DO-NOT-SET-EMAIL-RC               VALUE 'N'.      03460002
                                                                        03470000
      *----------------------------------------------------------------*03480000
      *  PV- WORKING PROGRAM VARIABLES                                  03490000
      *----------------------------------------------------------------*03500000
       01  PV-PROGRAM-VARIABLES.                                        03510000
                                                                        03520000
         05  PV-RWD-FORMATTING-VARIABLES.                               03530000
             10  PV-RWD-EVNT-ID-HELD       PIC  X(005)  VALUE SPACES.   03540000
             10  PV-RWD-EVNT-ID-CHARS      PIC  X(005)  JUSTIFIED RIGHT.03550000
             10  PV-RWD-DISC-AMT-NMRIC     PIC  9(03)V9(02).            03560000
             10  PV-RWD-DISC-AMT-FINAL     PIC  9(005)  VALUE ZERO.     03570000
             10  PV-MDSE-IE-CDE-INT        PIC  9(001)  VALUE ZERO.     03580000
             10  PV-AUTH-TYP-CDE-FINAL     PIC  9(001)  VALUE ZERO.     03590000
                                                                        03600000
         05  PV-DEPT-PROCESSING-VARIABLES.                              03610000
             10  PV-DEPT-LIST              PIC  X(024)  VALUE SPACES.   03620000
             10  PV-DEPT-FIELD-LENGTH      PIC  9(002)  VALUE ZERO.     03630000
             10  PV-DEPT-VAR-1             PIC  9(004)  VALUE ZERO.     03640000
             10  PV-DEPT-VAR-2             PIC  9(004)  VALUE ZERO.     03650000
             10  PV-DEPT-VAR-3             PIC  9(004)  VALUE ZERO.     03660000
             10  PV-DEPT-VAR-4             PIC  9(004)  VALUE ZERO.     03670000
             10  PV-DEPT-VAR-5             PIC  9(004)  VALUE ZERO.     03680000
             10  PV-ORACLE-USERID-LEN      PIC  S9(4)   VALUE ZEROS     03690031
                                                         COMP SYNC.     03700031
             10  PV-ORACLE-PASSWORD-LEN    PIC  S9(4)   VALUE ZEROS     03710031
                                                         COMP SYNC.     03720031
                                                                        03730000
         05  PV-VAR-REC-VARIABLES.                                      03740000
             10  PV-VAR-LNGTH                PIC  9(003)  VALUE ZERO.   03750011
             10  PV-SQL-SUB                  PIC  9(001)  VALUE ZERO.   03760011
             10  PV-RWD-CHGS-EXIST           PIC  X(001)  VALUE SPACE.  03770011
             10  PV-RWD-RDM-REC-BYTE-CNT     PIC  9(003)  VALUE ZERO.   03780011
092410       10  PV-RWD-RDM-REC-BYTE-CNT-EC  PIC  9(003)  VALUE ZERO.   03790027
             10  PV-WRITE-RECORD-LENGTH      PIC  9(003)  VALUE ZERO.   03800011
                                                                        03810000
         05  PV-DATE-AND-TIME-VARIABLES.                                03820000
             10  PV-CUR-SYS-TMST           PIC  X(026)  VALUE SPACE.    03830000
             10  PV-CUR-SYS-DATE           PIC  X(010)  VALUE SPACE.    03840000
             10  PV-EP111-CC-DATE-PLUS1    PIC  X(010)  VALUE SPACE.    03850000
             10  PV-MOD-REDM-BEG-DTE       PIC  X(010)  VALUE SPACE.    03860000
             10  PV-MOD-REDM-END-DTE       PIC  X(010)  VALUE SPACE.    03870000
             10  PV-OPEN-SOON-DATE         PIC  X(010)  VALUE SPACE.    03880000
             10  PV-CONV-DTE.                                           03890000
                 15  PV-CONV-DTE-MM        PIC  X(002)  VALUE SPACE.    03900000
                 15  PV-CONV-DTE-DD        PIC  X(002)  VALUE SPACE.    03910000
                 15  PV-CONV-DTE-CCYY      PIC  X(004)  VALUE SPACE.    03920000
                                                                        03930000
         05  PV-COUNTER-VARIABLES.                                      03940000
             10  PV-RWD-STR-WRITES         PIC  9(006)  VALUE ZERO.     03950001
092410       10  PV-RWD-STR-WRITES-ECOM    PIC  9(006)  VALUE ZERO.     03960027
             10  PV-DEPTS-CT               PIC  9(001)  VALUE ZERO.     03970000
             10  PV-DEPTS-FOUND            PIC  9(002)  VALUE ZERO.     03980000
             10  PV-PAGE-COUNT             PIC  9(006)  VALUE ZERO.     03990000
             10  PV-LINE-COUNT             PIC  9(006)  VALUE ZERO.     04000000
W36988       10  PV-RWD-EVNTS-COUNT        PIC  9(06)   VALUE ZERO.     04010003
W36988       10  PV-STR-REC-CT-OK          PIC  9(06)   VALUE ZERO.     04020003
                                                                        04030000
         05  PV-NULL-INDICATORS.                                        04040000
             10  PV-RWD-PRE-DYS-IND        PIC S9(004)  VALUE +0 COMP.  04050000
                 88  PV-PRE-DYS-NULL                    VALUE -1.       04060000
             10  PV-RWD-PST-DYS-IND        PIC S9(004)  VALUE +0 COMP.  04070000
                 88  PV-PST-DYS-NULL                    VALUE -1.       04080000
                                                                        04090000
         05  PV-ABEND-VARIABLES.                                        04100000
             10  PV-PARAGRAPH-NAME         PIC  X(030)  VALUE SPACE.    04110000
             10  PV-OPERATION-MSG-1        PIC  X(040)  VALUE SPACE.    04120000
             10  PV-MSG-TEXT               PIC  X(040)  VALUE SPACE.    04130000
             10  PV-DB2-OPERATION          PIC  X(030)  VALUE SPACE.    04140000
             10  PV-ORA-OPERATION          PIC  X(030)  VALUE SPACE.    04150037
                                                                        04160000
      *----------------------------------------------------------------*04170000
      *  RP- REPORT HEADER & DETAIL LINES                               04180000
      *----------------------------------------------------------------*04190000
                                                                        04200000
       01  RP-EP-SYSTEM-ID-LINE.                                        04210000
           05  FILLER                      PIC  X(053)  VALUE SPACES.   04220000
           05  FILLER                      PIC  X(025)                  04230000
                     VALUE 'ENTERPRISE POS MANAGEMENT'.                 04240000
           05  FILLER                      PIC  X(054)  VALUE SPACES.   04250000
                                                                        04260000
       01  RP-EP-TITLE-LINE.                                            04270000
           05  FILLER                      PIC  X(053)  VALUE SPACES.   04280000
           05  FILLER                      PIC  X(026)                  04290000
                     VALUE 'REWARDS REDEMPTION SUMMARY'.                04300000
           05  FILLER                      PIC  X(053)  VALUE SPACES.   04310000
                                                                        04320000
       01  RP-EP-STORE-GROUP-LINE.                                      04330000
           05  FILLER                      PIC  X(002)  VALUE SPACES.   04340000
           05  FILLER                      PIC  X(012)                  04350000
                     VALUE 'STORE LOC #:'.                              04360000
           05  FILLER                      PIC  X(002)  VALUE SPACES.   04370000
           05  RP-STORE-LOC-NBR            PIC  X(005).                 04380000
           05  FILLER                      PIC  X(111)  VALUE SPACES.   04390000
                                                                        04400000
       01  RP-EP-HEADER-LINE-A.                                         04410000
           05  FILLER                      PIC  X(002)  VALUE SPACES.   04420000
           05  FILLER                      PIC  X(008)                  04430000
                                                VALUE 'EVENT-ID'.       04440000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04450000
           05  FILLER                      PIC  X(008)                  04460000
                                                VALUE 'STRT DTE'.       04470000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04480000
           05  FILLER                      PIC  X(008)                  04490000
                                                VALUE 'END DATE'.       04500000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04510000
           05  FILLER                      PIC  X(008)                  04520000
                                                VALUE 'AUTH-TYP'.       04530000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04540000
           05  FILLER                      PIC  X(008)                  04550000
                                                VALUE 'DISC-TYP'.       04560000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04570000
           05  FILLER                      PIC  X(008)                  04580000
                                                VALUE 'DISC-AMT'.       04590000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04600000
           05  FILLER                      PIC  X(008)                  04610000
                                                VALUE 'I-E-CODE'.       04620000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04630000
           05  FILLER                      PIC  X(009)                  04640000
                                                VALUE 'DEPT LIST'.      04650000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04660000
                                                                        04670000
       01  RP-EP-HEADER-LINE-B.                                         04680000
           05  FILLER                      PIC  X(002)  VALUE SPACES.   04690000
           05  FILLER                      PIC  X(008)                  04700000
                                                VALUE '========'.       04710000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04720000
           05  FILLER                      PIC  X(008)                  04730000
                                                VALUE '========'.       04740000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04750000
           05  FILLER                      PIC  X(008)                  04760000
                                                VALUE '========'.       04770000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04780000
           05  FILLER                      PIC  X(008)                  04790000
                                                VALUE '========'.       04800000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04810000
           05  FILLER                      PIC  X(008)                  04820000
                                                VALUE '========'.       04830000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04840000
           05  FILLER                      PIC  X(008)                  04850000
                                                VALUE '========'.       04860000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04870000
           05  FILLER                      PIC  X(008)                  04880000
                                                VALUE '========'.       04890000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04900000
           05  FILLER                      PIC  X(024)                  04910000
                                                VALUE ALL '='.          04920000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   04930000
                                                                        04940000
       01  RP-EP-DETAIL-LINE.                                           04950000
           05  FILLER                      PIC  X(001)  VALUE SPACES.   04960000
           05  RP-EARLY-SEND-FLAG          PIC  X(001)  VALUE SPACES.   04970000
           05  FILLER                      PIC  X(001)  VALUE SPACES.   04980000
           05  RP-EVENT-ID                 PIC  X(005)  VALUE SPACES.   04990000
           05  FILLER                      PIC  X(008)  VALUE SPACES.   05000000
           05  RP-STRT-DTE                 PIC  X(008)  VALUE SPACES.   05010000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   05020000
           05  RP-END-DATE                 PIC  X(008)  VALUE SPACES.   05030000
           05  FILLER                      PIC  X(009)  VALUE SPACES.   05040000
           05  RP-AUTH-TYP                 PIC  9(001)  VALUE ZERO.     05050000
           05  FILLER                      PIC  X(013)  VALUE SPACES.   05060000
           05  RP-DISC-TYP                 PIC  X(001)  VALUE SPACES.   05070000
           05  FILLER                      PIC  X(012)  VALUE SPACES.   05080000
           05  RP-DISC-AMT                 PIC  9(3)V9(2) VALUE ZERO.   05090000
           05  FILLER                      PIC  X(010)  VALUE SPACES.   05100000
           05  RP-IE-TYP-CDE               PIC  9(001)  VALUE ZERO.     05110000
           05  FILLER                      PIC  X(010)  VALUE SPACES.   05120000
           05  RP-DEPT-LIST                PIC  X(024)  VALUE SPACES.   05130000
           05  FILLER                      PIC  X(006)  VALUE SPACES.   05140000
                                                                        05150000
073004 01  RP-EP-EARLY-RWDS-MSG.                                        05160000
*          05  FILLER                      PIC  X(002)  VALUE SPACES.   05170000
*          05  RP-EARLY-SEND-MSG           PIC  X(047)                  05180000
*              VALUE '* : REDM RWD PARAM SENT EARLY TO MATCH ACTV RWD'. 05190000
073004     05  FILLER                      PIC  X(083)  VALUE SPACES.   05200000
                                                                        05210000
       01  WS-MSG-TEXT                     PIC X(512)  VALUE SPACES.    05220032
       01  WS-MAX-SIZE                     PIC S9(09)  VALUE +512 COMP. 05230032
       01  WS-MSG-LENGTH                   PIC S9(09)  VALUE ZERO COMP. 05240032
      *----------------------------------------------------------------*05250000
      *  REDEMPTION RECORD LAYOUT                                       05260000
      *----------------------------------------------------------------*05270000
           COPY EPRD140.                                                05280000
                                                                        05290000
      *----------------------------------------------------------------*05300008
092410*  REDEMPTION RECORD LAYOUT - ECOM                                05310027
092410*----------------------------------------------------------------*05320027
092410     COPY EPRD142.                                                05330027
                                                                        05340008
      *----------------------------------------------------------------*05350000
      *  EPCC149 CONTROL CARD LAYOUT - LAST DATE AUKUT016 WAS RAN       05360044
      *----------------------------------------------------------------*05370000
           COPY EPRD149.                                                05380000
                                                                        05390000
      *----------------------------------------------------------------*05400000
      *  EPCC165 CONTROL CARD LAYOUT - RUN TYPE IDENTIFIER              05410000
      *----------------------------------------------------------------*05420000
           COPY EPRD165.                                                05430000
                                                                        05440000
      *----------------------------------------------------------------*05450000
      *  EPCC111 CONTROL CARD LAYOUT - CURRENT BATCH DATE               05460000
      *----------------------------------------------------------------*05470000
           COPY EPRD111.                                                05480000
                                                                        05490000
      *----------------------------------------------------------------*05500000
      *  EPCC107 CONTROL CARD LAYOUT - NEXT FUTURE BATCH DATE           05510000
      *----------------------------------------------------------------*05520000
           COPY EPRD107.                                                05530000
                                                                        05540000
      *----------------------------------------------------------------*05550000
      *  EPCC045 CONTROL CARD LAYOUT - ADV. DAYS AHEAD OF STORE OPEN    05560000
      *----------------------------------------------------------------*05570000
           COPY EPRD045.                                                05580000
                                                                        05590000
      *----------------------------------------------------------------*05600000
      *  DATE ROUTINE COPYBOOK                                          05610000
      *----------------------------------------------------------------*05620000
           COPY DPWS500.                                                05630000
                                                                        05640000
      *----------------------------------------------------------------*05650000
      *  REPORT HEADER FOR A 132 COLUMN REPORT                          05660000
      *----------------------------------------------------------------*05670000
           COPY DPWS132O.                                               05680000
                                                                        05690000
                                                                        05700000
      *----------------------------------------------------------------*05710000
      *  DB2 MESSAGE AREA                                               05720000
      *----------------------------------------------------------------*05730000
           COPY SQLCA2.                                                 05740034
                                                                        05750000
      *----------------------------------------------------------------*05760000
      *  DB2 COMMUNICATIONS AREA                                        05770000
      *----------------------------------------------------------------*05780000
           EXEC SQL                                                     05790034
             INCLUDE SQLCA                                              05800034
           END-EXEC.                                                    05810034
                                                                        05820000
      *----------------------------------------------------------------*05830031
      * ORACLE DELARE SECTION START                                    *05840031
      *----------------------------------------------------------------*05850031
           EXEC SQL                                                     05860031
              BEGIN DECLARE SECTION                                     05870031
           END-EXEC.                                                    05880031
                                                                        05890031
      *----------------------------------------------------------------*05900031
      * PD- ORA VARIABLES (NON DCLGEN)                                 *05910031
      *----------------------------------------------------------------*05920031
       01  USERNAME                      PIC X(10)  VARYING.            05930031
       01  PASSWD                        PIC X(10)  VARYING.            05940031
                                                                        05950031
      *----------------------------------------------------------------*05960000
      *  DB2 TABLE XST_LOC - STORE LOCATION TABLE                       05970000
      *----------------------------------------------------------------*05980000
           EXEC SQL                                                     05990000
               INCLUDE XST00001                                         06000000
           END-EXEC.                                                    06010000
                                                                        06020000
      *----------------------------------------------------------------*06030000
      *  DB2 TABLE XST_LOC_ATTR - LOCATION ATTRIBUTE TABLE              06040000
      *----------------------------------------------------------------*06050000
           EXEC SQL                                                     06060000
                INCLUDE XST00025                                        06070000
           END-EXEC.                                                    06080000
                                                                        06090000
      *----------------------------------------------------------------*06100000
      *  DB2 TABLE XST_STR_GP_MBSHP - STORE GROUP MEMBERSHIP TABLE      06110000
      *----------------------------------------------------------------*06120000
                                                                        06130000
           EXEC SQL                                                     06140000
               INCLUDE XST00023                                         06150000
           END-EXEC.                                                    06160000
                                                                        06170000
      *----------------------------------------------------------------*06180000
      *  DB2 TABLE AUT_RWD_EVNT - MAIN REWARD EVENT TABLE               06190000
      *----------------------------------------------------------------*06200000
           EXEC SQL                                                     06210000
             INCLUDE AUT00001                                           06220000
           END-EXEC.                                                    06230000
                                                                        06240000
      *----------------------------------------------------------------*06250000
      *  DB2 TABLE AUT_RWD_ISUD - REWARD ISSUED TABLE                   06260000
      *----------------------------------------------------------------*06270000
           EXEC SQL                                                     06280000
             INCLUDE AUT00002                                           06290000
           END-EXEC.                                                    06300000
                                                                        06310000
      *----------------------------------------------------------------*06320000
      *  DB2 TABLE AUT_RWD_REDM - REWARD REDEMPTION TABLE               06330000
      *----------------------------------------------------------------*06340000
           EXEC SQL                                                     06350000
             INCLUDE AUT00004                                           06360000
           END-EXEC.                                                    06370000
                                                                        06380000
      *----------------------------------------------------------------*06390000
      *  DB2 TABLE AUT_RWD_REDM_MDSE - REWARD REDM MERCHANDISE TABLE    06400000
      *----------------------------------------------------------------*06410000
           EXEC SQL                                                     06420000
             INCLUDE AUT00005                                           06430000
           END-EXEC.                                                    06440000
                                                                        06450000
      *----------------------------------------------------------------*06460000
      *  DB2 AREA FOR SYSDUMMY                                          06470000
      *----------------------------------------------------------------*06480000
      **   EXEC SQL                                                     06490042
      **     INCLUDE SYSDUMMY                                           06500042
      **   END-EXEC.                                                    06510042
                                                                        06520000
      *----------------------------------------------------------------*06530031
      * ORACLE END OF DECLARATIVE SECTION                              *06540031
      *----------------------------------------------------------------*06550031
           EXEC SQL                                                     06560031
             END DECLARE SECTION                                        06570031
           END-EXEC.                                                    06580031
                                                                        06590031
      *----------------------------------------------------------------*06600031
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        *06610031
      *----------------------------------------------------------------*06620031
           COPY DPWS004.                                                06630031
                                                                        06640031
      *----------------------------------------------------------------*06650031
      * ORA ABEND COPYBOOK                                             *06660031
      *----------------------------------------------------------------*06670031
           COPY DPWS900.                                                06680031
      *----------------------------------------------------------------*06690000
      *  STORE LOCATION CURSOR                                          06700000
      *  - PULLS IN ALL STORE LOCATIONS OPEN NOW OR WHICH WILL BE       06710000
      *    OPENING IN THE NEAR FUTURE AS DICTATED BY EPCC045.           06720000
      *----------------------------------------------------------------*06730000
           EXEC SQL                                                     06740000
             DECLARE STR_LOC_CSR CURSOR FOR                             06750033
               SELECT LOCN.LOC_NBR                                      06760000
               FROM   XST_LOC      LOCN                                 06770000
                     ,XST_LOC_ATTR ATTR                                 06780000
               WHERE  LOCN.LOC_TYP_CDE = 'DS'                           06790000
                 AND  LOCN.LOC_NBR = ATTR.LOC_NBR                       06800040
                 AND ((LOCN.OPN_DTE <=                                  06810040
                           TO_DATE(:PV-CUR-SYS-DATE,'YYYY-MM-DD')       06820040
                       AND LOCN.CLO_DTE >=                              06830040
                           TO_DATE(:PV-CUR-SYS-DATE,'YYYY-MM-DD'))      06840040
                      OR                                                06850040
                      (LOCN.OPN_DTE <=                                  06860040
                           TO_DATE(:PV-OPEN-SOON-DATE,'YYYY-MM-DD')     06870040
                       AND LOCN.CLO_DTE >=                              06880040
                           TO_DATE(:PV-OPEN-SOON-DATE,'YYYY-MM-DD')))   06890040
                 AND  (ATTR.POS_CAP_LVL_ID = 5 OR                       06900000
                       ATTR.POS_CAP_LVL_ID > 14)                        06910000
               ORDER BY LOCN.LOC_NBR                                    06920043
           END-EXEC.                                                    06930000
                                                                        06940000
      *----------------------------------------------------------------*06950000
      *  REWARD REDEMPTION EVENT CURSOR                                 06960000
      *  - RETURNS THE REWARD REDEMPTION INFORMATION ON ALL REWARDS     06970000
      *    FOR A PARTICULAR STORE LOCATION NUMBER WHICH MEET            06980000
      *    THE DATE CRITERIA SPECIFIED.                                 06990000
      *CHG0172687                                                       06990173
      *Modified the query to fetch the reward till the redeem end date. 06991072
      *Since '=' condition is not pulling the reward if the current date06992072
      *is equal to redm_end_dte, we have reduced the current date by 1  06993072
      *day so that the reward gets pulled.                              06994072
      *----------------------------------------------------------------*07000000
           EXEC SQL                                                     07010000
             DECLARE RWD_REDM_EVNT_CSR CURSOR FOR                       07020033
               SELECT EVNT.RWD_EVNT_ID                                  07030000
                     ,NVL(EVNT.RWD_DISC_AMT,0)                          07040050
                     ,NVL(EVNT.RWD_TYP_CDE,' ')                         07050050
                     ,NVL(REDM.STR_GP_TYP_CDE,0)                        07060050
                     ,NVL(REDM.STR_GP_ID,0)                             07070050
                     ,REDM.REDM_BEG_DTE                                 07080052
                     ,REDM.REDM_END_DTE                                 07090052
                     ,NVL(REDM.PRE_BEG_DAYS_QTY,0)                      07100050
                     ,NVL(REDM.POST_END_DAYS_QTY,0)                     07110050
                     ,NVL(REDM.AUTH_TYP_CDE,0)                          07120050
                     ,NVL(REDM.MDSE_IE_CDE,0)                           07130050
                 FROM  AUT_RWD_EVNT     EVNT                            07140000
                      ,AUT_RWD_REDM     REDM                            07150000
                      ,XST_STR_GP_MBSHP MBSHP                           07160000
                 WHERE EVNT.RWD_EVNT_ID = REDM.RWD_EVNT_ID              07170000
062309             AND REDM_END_DTE    >= ADD_MONTHS(CURRENT_DATE,-18)  07180038
                   AND REDM_BEG_DTE    <  (CURRENT_DATE + 30)           07190041
                   AND EVNT.STAT_CDE IN ('S')                           07200000
                   AND REDM.STAT_CDE IN ('S')                           07210000
                   AND MBSHP.STR_NBR       =:XST00001-LOC-NBR           07220041
                   AND REDM.STR_GP_TYP_CDE = MBSHP.STR_GP_TYP_CDE       07230000
                   AND REDM.STR_GP_ID      = MBSHP.STR_GP_ID            07240041
                   AND MBSHP.EFF_BEG_DTE  <=                            07250041
                             TO_DATE(:PV-CUR-SYS-DATE,'YYYY-MM-DD')     07260068
                   AND (MBSHP.EFF_END_DTE >=                            07270041
                             TO_DATE(:PV-CUR-SYS-DATE,'YYYY-MM-DD')     07280068
                        OR MBSHP.EFF_END_DTE IS NULL)                   07290000
                   AND (EVNT.RET_RUL_DSBL_IND ='N'                      07300062
                           OR                                           07310000
      **                CURRENT_DATE <= ( REDM.REDM_END_DTE             07320039
      **                                + REDM.POST_END_DAYS_QTY DAYS ) 07330039
      * CHG0172687 - START                                              07334073
                       (CURRENT_DATE - 1) <= (REDM.REDM_END_DTE +       07340072
                                                 REDM.POST_END_DAYS_QTY)07350072
                       )                                                07360072
      * CHG0172687 - END                                                07361073
               ORDER BY EVNT.RWD_EVNT_ID                                07370000
                       ,REDM.STR_SELN_HIER_CDE DESC                     07380000
           END-EXEC.                                                    07390000
                                                                        07400000
      *----------------------------------------------------------------*07410000
      *  INCLUDE / EXCLUDE DEPTS CURSOR                                 07420000
      *  - SELECTS ALL DEPARTMENTS EITHER INCLUDED OR EXCLUDED IN THE   07430000
      *    SELECTED SALES REWARD EVENT. MAY RETURN UP TO 5 DEPTS.       07440000
      *----------------------------------------------------------------*07450000
           EXEC SQL                                                     07460000
             DECLARE I_E_MDSE_DEPT_CSR CURSOR FOR                       07470033
               SELECT MDSE.DEPT_NBR                                     07480000
               FROM   AUT_RWD_REDM_MDSE MDSE                            07490000
               WHERE  MDSE.RWD_EVNT_ID    = :AUT00001.RWD-EVNT-ID       07500040
                  AND MDSE.STR_GP_TYP_CDE = :AUT00004.STR-GP-TYP-CDE    07510035
                  AND MDSE.STR_GP_ID      = :AUT00004.STR-GP-ID         07520040
                  AND MDSE.STAT_CDE = 'S'                               07530000
               ORDER BY MDSE.DEPT_NBR                                   07540000
           END-EXEC.                                                    07550000
                                                                        07560000
      *----------------------------------------------------------------*07570000
                                                                        07580000
       LINKAGE SECTION.                                                 07590000
                                                                        07600000
      *----------------------------------------------------------------*07610000
       PROCEDURE DIVISION.                                              07620000
      *----------------------------------------------------------------*07630000
       0000-MAINLINE.                                                   07640000
                                                                        07650000
           PERFORM 1000-INITIALIZATION.                                 07660000
                                                                        07670000
           PERFORM 2000-EVALUATE-JOB-RUN-TYPE.                          07680000
                                                                        07690000
           PERFORM 9999-WRAPUP.                                         07700000
                                                                        07710000
           GOBACK.                                                      07720000
                                                                        07730000
      *0000-EXIT.                                                       07740000
                                                                        07750000
      *----------------------------------------------------------------*07760000
      *  1000-INITIALIZATION.                                           07770000
      *  - OPENS INPUT & OUTPUT FILES, READS THROUGH ALL THE CONTROL    07780000
      *    CARDS, AND SETS UP ALL THE INITIAL TIMESTAMP VALUES.         07790000
      *----------------------------------------------------------------*07800000
       1000-INITIALIZATION.                                             07810000
                                                                        07820000
           DISPLAY '*-----------------------------------*'.             07830000
           DISPLAY '*         AUKUT016 - REWARDS        *'.             07840044
           DISPLAY '*         REDEMPTION EXTRACT        *'.             07850000
           DISPLAY '*-----------------------------------*'.             07860000
           DISPLAY ' '.                                                 07870000
                                                                        07880000
           OPEN INPUT CTRL-CRD-PGM-LAST-RUN                             07890000
                      CTRL-CRD-IDENT-TYP                                07900000
                      CTRL-CRD-CUR-BTCH-DTE                             07910000
                      CTRL-CRD-BTCH-FUTR-DTE                            07920000
                      CTRL-CRD-ADV-OPN-DAYS                             07930031
                      CONTROL-CARD-ORACLE-LOGIN.                        07940031
                                                                        07950000
           OPEN OUTPUT RWD-REDM-EXTRCT-FLE                              07960000
092410                 RWD-REDM-EXTRCT-FLE-ECOM                         07970027
                       RWD-REDM-EXTRCT-RPT.                             07980000
                                                                        07990000
           PERFORM 1050-SETUP-FOR-ORACLE.                               08000047
           PERFORM 1100-GET-CURRENT-SYSTEM-TMST.                        08010000
                                                                        08020000
           PERFORM 1200-READ-PGM-LAST-RUN-CARD.                         08030000
           PERFORM 1300-READ-CUR-BTCH-DTE-CARD.                         08040000
           PERFORM 1400-READ-BTCH-FUTR-DTE-CARD.                        08050000
           PERFORM 1500-READ-IDENT-TYP-CARD.                            08060000
           PERFORM 1600-READ-DAYS-TILL-STR-OPN-CD.                      08070000
                                                                        08080000
           PERFORM 1700-INCR-CUR-BTCH-DTE.                              08090000
           PERFORM 1800-INCR-CUR-OPEN-DTE.                              08100000
                                                                        08110000
           PERFORM 1900-INIT-EP-SELF-REPORTING.                         08120000
                                                                        08130000
      *1000-EXIT.                                                       08140000
                                                                        08150000
      ******************************************************************08160047
      *   1050-SETUP-FOR-ORACLE.                                       *08170047
      *     SET UP TO CONNECT TO THE ORACLE DATABASE.                  *08180047
      ******************************************************************08190047
       1050-SETUP-FOR-ORACLE.                                           08200047
           MOVE '1050-SETUP-FOR-ORACLE          ' TO  PV-PARAGRAPH-NAME.08210047
      *                                                                 08220047
           READ CONTROL-CARD-ORACLE-LOGIN INTO CC-ORACLE-CONNECTION.    08230047
                                                                        08240047
            IF CC-AUTO-LOGON                                            08250047
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'        08260047
               EXEC SQL                                                 08270047
                    CONNECT :PC-AUTO-LOGON                              08280047
               END-EXEC                                                 08290047
            ELSE                                                        08300047
               INITIALIZE PV-ORACLE-USERID-LEN                          08310047
                          PV-ORACLE-PASSWORD-LEN                        08320047
               INSPECT CC-ORACLE-USERID                                 08330047
                       TALLYING PV-ORACLE-USERID-LEN                    08340047
                       FOR CHARACTERS BEFORE INITIAL SPACE              08350047
               INSPECT CC-ORACLE-PASSWORD                               08360047
                       TALLYING PV-ORACLE-PASSWORD-LEN                  08370047
                       FOR CHARACTERS BEFORE INITIAL SPACE              08380047
               MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO    08390047
                       USERNAME-ARR                                     08400047
               MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN             08410047
               MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO    08420047
                       PASSWD-ARR                                       08430047
               MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN               08440047
               MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO    08450047
                    PASSWD-ARR                                          08460047
               MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN               08470047
               MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN)     TO    08480047
                    USERNAME-ARR                                        08490047
               MOVE PV-ORACLE-USERID-LEN    TO USERNAME-LEN             08500047
               MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO    08510047
                    PASSWD-ARR                                          08520047
               MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN               08530047
               DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER '   08540047
                    USERNAME-ARR                                        08550047
               EXEC SQL                                                 08560047
                    CONNECT :USERNAME IDENTIFIED BY :PASSWD             08570047
               END-EXEC                                                 08580047
               INITIALIZE CC-ORACLE-PASSWORD                            08590047
                          PASSWD                                        08600047
            END-IF.                                                     08610047
                                                                        08620047
            IF (SQLCODE NOT = 0)                                        08630047
                MOVE 'CONNECT TO ORACLE DATABASE'                       08640047
                                           TO PV-ORA-OPERATION          08650047
                PERFORM 9900-ORA-ERROR                                  08660047
            END-IF.                                                     08670047
                                                                        08680047
      *1050-EXIT.                                                       08690047
      *----------------------------------------------------------------*08700000
      *  1100-GET-CURRENT-SYSTEM-TMST.                                  08710000
      *  - RETREIVE THE CURRENT SYSTEM TIMESTAMP. THIS WILL BE USED     08720000
      *    LATER TO IDENTIFY ANY UPDATES WHICH WOULD NEED TO TRIGGER A  08730000
      *    NEW EXTRACT CREATION.  IT WILL ALSO BE USED TO UPDATE        08740000
      *    EPCC149 ON PROGRAM EXIT TO MARK THE LAST RUN OF THIS         08750000
      *    PROGRAM.                                                     08760000
      *----------------------------------------------------------------*08770000
       1100-GET-CURRENT-SYSTEM-TMST.                                    08780000
                                                                        08790000
           EXEC SQL                                                     08800035
                ALTER SESSION                                           08810035
                  SET NLS_DATE_FORMAT = 'YYYY-MM-DD.HH24.MI.SS'         08820035
           END-EXEC.                                                    08830035
                                                                        08840035
           EXEC SQL                                                     08850000
               SELECT TO_CHAR(CURRENT_TIMESTAMP,'YYYY-MM-DD HH24:MI:SS')08860049
                  INTO :PV-CUR-SYS-TMST                                 08870035
                  FROM DUAL                                             08880035
           END-EXEC.                                                    08890000
                                                                        08900000
           MOVE PV-CUR-SYS-TMST(1:10) TO PV-CUR-SYS-DATE.               08910000
           DISPLAY '** CURRENT SYSTEM TIMESTAMP = ' PV-CUR-SYS-TMST.    08920000
           DISPLAY ' '.                                                 08930000
                                                                        08940000
           IF SQLCODE NOT EQUAL ZERO                                    08950000
             MOVE 'GET CURRENT TIMESTAMP'         TO PV-DB2-OPERATION   08960000
             MOVE '1100-GET-CURRENT-SYSTEM-TMST'  TO PV-PARAGRAPH-NAME  08970000
             PERFORM 9900-ORA-ERROR                                     08980035
           END-IF.                                                      08990000
                                                                        09000000
      *1100-EXIT.                                                       09010000
                                                                        09020000
      *----------------------------------------------------------------*09030000
      *  1200-READ-PGM-LAST-RUN-CARD.                                   09040000
      *  - READ CONTROL CARD EPCC149. THIS CONTROL CARD CONTAINS THE    09050000
      *    TIMESTAMP FROM THE LAST RUN OF AUKUT016                      09060044
      *----------------------------------------------------------------*09070000
       1200-READ-PGM-LAST-RUN-CARD.                                     09080000
                                                                        09090000
           READ CTRL-CRD-PGM-LAST-RUN                                   09100000
             INTO EP149-CC-CONTROL-CARD                                 09110000
               AT END                                                   09120000
                 SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                   09130000
                                                                        09140000
           IF PS-END-OF-CNTL-CRD-FILE                                   09150000
             MOVE '1200-READ-PGM-LAST-RUN-CARD' TO PV-PARAGRAPH-NAME    09160000
             MOVE 'ATTEMPTING TO READ CTRL CRD' TO PV-OPERATION-MSG-1   09170000
             MOVE 'EMPTY CONTROL CARD !       ' TO PV-MSG-TEXT          09180000
             SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                     09190000
             PERFORM 9010-ABEND                                         09200000
           ELSE                                                         09210000
             DISPLAY '** PROGRAM LAST RUN CONTROL CARD |FS = '          09220000
                         FS-CTRL-CRD-PGM-LAST-RUN ' **'                 09230000
             DISPLAY '** EPCC149     **  =' EP149-CC-RWD-RDM-CUTOFF-TMST09240000
             DISPLAY ' '                                                09250000
           END-IF.                                                      09260000
                                                                        09270000
      *1200-EXIT.                                                       09280000
                                                                        09290000
      *----------------------------------------------------------------*09300000
      * 1300-READ-CUR-BTCH-DTE-CARD.                                    09310000
      *     READ CONTROL CARD EPCC111. THIS CONTROL CARD CONTAINS THE   09320000
      *     CURRENT BATCH DATE.                                         09330000
      *----------------------------------------------------------------*09340000
       1300-READ-CUR-BTCH-DTE-CARD.                                     09350000
                                                                        09360000
           READ CTRL-CRD-CUR-BTCH-DTE                                   09370000
             INTO EP111-CC-CONTROL-CARD                                 09380000
               AT END                                                   09390000
                 SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                   09400000
                                                                        09410000
           IF PS-END-OF-CNTL-CRD-FILE                                   09420000
             MOVE '1300-READ-CUR-BTCH-DTE-CARD' TO PV-PARAGRAPH-NAME    09430000
             MOVE 'ATTEMPTING TO READ CTRL CRD' TO PV-OPERATION-MSG-1   09440000
             MOVE 'EMPTY CONTROL CARD !       ' TO PV-MSG-TEXT          09450000
             SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                     09460000
             PERFORM 9010-ABEND                                         09470000
           ELSE                                                         09480000
             DISPLAY '** CURRENT BATCH DATE CTRL CARD  |FS = '          09490000
                         FS-CTRL-CRD-CUR-BTCH-DTE ' **'                 09500000
             DISPLAY '** EPCC111     **  =' EP111-CC-CRNT-DTE           09510000
             DISPLAY ' '                                                09520000
           END-IF.                                                      09530000
                                                                        09540000
      *1300-EXIT.                                                       09550000
                                                                        09560000
      *----------------------------------------------------------------*09570000
      *  1400-READ-BTCH-FUTR-DTE-CARD.                                  09580000
      *  - READ CONTROL CARD EPCC107. THIS CONTROL CARD CONTAINS THE    09590000
      *    NEXT FUTURE BATCH DATE.                                      09600000
      *----------------------------------------------------------------*09610000
       1400-READ-BTCH-FUTR-DTE-CARD.                                    09620000
                                                                        09630000
           READ CTRL-CRD-BTCH-FUTR-DTE                                  09640000
             INTO EP107-CC-CONTROL-CARD                                 09650000
               AT END                                                   09660000
                 SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                   09670000
                                                                        09680000
           IF PS-END-OF-CNTL-CRD-FILE                                   09690000
             MOVE '1400-READ-BTCH-FUTR-DTE-CARD' TO PV-PARAGRAPH-NAME   09700000
             MOVE 'ATTEMPTING TO READ CTRL CRD'  TO PV-OPERATION-MSG-1  09710000
             MOVE 'EMPTY CONTROL CARD !       '  TO PV-MSG-TEXT         09720000
             SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                     09730000
             PERFORM 9010-ABEND                                         09740000
           ELSE                                                         09750000
             DISPLAY '** FUTURE BATCH DATE CONTROL CRD |FS = '          09760000
                         FS-CTRL-CRD-BTCH-FUTR-DTE ' **'                09770000
             DISPLAY '** EPCC107     **  =' EP107-CC-CRNT-DTE           09780000
             DISPLAY ' '                                                09790000
           END-IF.                                                      09800000
                                                                        09810000
      *1400-EXIT.                                                       09820000
                                                                        09830000
      *----------------------------------------------------------------*09840000
      *  1500-READ-IDENT-TYP-CARD.                                      09850000
      *  - READ CONTROL CARD EPCC149. THIS CONTROL CARD CONTAINS THE    09860000
      *    JOB RUN TYPE IDENTIFIER (CHECK FOR UPDATES OR AUTO-EXTRACT). 09870000
      *----------------------------------------------------------------*09880000
       1500-READ-IDENT-TYP-CARD.                                        09890000
                                                                        09900000
           READ CTRL-CRD-IDENT-TYP                                      09910000
             INTO EP165-CC-CONTROL-CARD                                 09920000
               AT END                                                   09930000
                 SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                   09940000
                                                                        09950000
           IF PS-END-OF-CNTL-CRD-FILE                                   09960000
             MOVE '1500-READ-IDENT-TYP-CARD   ' TO PV-PARAGRAPH-NAME    09970000
             MOVE 'ATTEMPTING TO READ CTRL CRD' TO PV-OPERATION-MSG-1   09980000
             MOVE 'EMPTY CONTROL CARD !       ' TO PV-MSG-TEXT          09990000
             SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                     10000000
             PERFORM 9010-ABEND                                         10010000
           ELSE                                                         10020000
             DISPLAY '** JOB RUN TYPE ID CONTROL CARD  |FS = '          10030000
                         FS-CTRL-CRD-IDENT-TYP ' **'                    10040000
             DISPLAY '** EPCC165     **  =' EP165-CC-JOB-RUN-TYP        10050000
             DISPLAY ' '                                                10060000
           END-IF.                                                      10070000
                                                                        10080000
      *1500-EXIT.                                                       10090000
                                                                        10100000
      *----------------------------------------------------------------*10110000
      *  1600-READ-DAYS-TILL-STR-OPN-CD.                                10120000
      *  - READ CONTROL CARD EPCC045. THIS CONTROL CARD CONTAINS THE    10130000
      *    NUMBER OF DAYS BEFORE A NEW STORE OPENS FOR IT TO BE         10140000
      *    INCLUDED IN THIS PROGRAM'S STORE CURSOR.                     10150000
      *----------------------------------------------------------------*10160000
       1600-READ-DAYS-TILL-STR-OPN-CD.                                  10170000
                                                                        10180000
           READ CTRL-CRD-ADV-OPN-DAYS                                   10190000
             INTO EP045-ADVDAYS-TO-SND-RECORD                           10200000
               AT END                                                   10210000
                 SET PS-END-OF-CNTL-CRD-FILE TO TRUE.                   10220000
                                                                        10230000
           IF PS-END-OF-CNTL-CRD-FILE                                   10240000
             MOVE '1600-READ-DAYS-TILL-STR-OPN-CD' TO PV-PARAGRAPH-NAME 10250000
             MOVE 'ATTEMPTING TO READ CTRL CRD' TO PV-OPERATION-MSG-1   10260000
             MOVE 'EMPTY CONTROL CARD !       ' TO PV-MSG-TEXT          10270000
             SET ABEND-4003-CTRL-CARD-ERROR TO TRUE                     10280000
             PERFORM 9010-ABEND                                         10290000
           ELSE                                                         10300000
             DISPLAY '** STR ADV DYS OPN CONTROL CARD  |FS = '          10310000
                         FS-CTRL-CRD-ADV-OPN-DAYS ' **'                 10320000
             DISPLAY '** EPCC045     **  =' EP045-DAYS-TILL-STORE-OPN   10330000
             DISPLAY ' '                                                10340000
           END-IF.                                                      10350000
                                                                        10360000
      *1600-EXIT.                                                       10370000
                                                                        10380000
      *----------------------------------------------------------------*10390000
      *  1700-INCR-CUR-BTCH-DTE.                                        10400000
      *  - INCREMENT THE BATCH DATE AND RETRIEVE THE FORMAT AS CCYYMMDD.10410000
      *----------------------------------------------------------------*10420000
       1700-INCR-CUR-BTCH-DTE.                                          10430000
                                                                        10440000
           INITIALIZE DPG51 DPG52 DPG53                                 10450000
                      DPG54 DPG55 DPG56.                                10460000
                                                                        10470000
           SET DPG51-FISCAL-454-CALENDAR  TO TRUE.                      10480000
           SET DPG51-INCREMENT-DATE       TO TRUE.                      10490000
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      10500000
           MOVE 1                         TO DPG51-INCR-DECR-DAYS-9.    10510000
           MOVE EP111-CC-CRNT-DTE         TO DPG52-DB2-ISO-DATE-FMT.    10520000
                                                                        10530000
           MOVE 'INCREMENT BATCH DATE BY 1' TO PV-OPERATION-MSG-1.      10540000
           PERFORM 6000-DATE-ROUTINE.                                   10550000
                                                                        10560000
           MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-EP111-CC-DATE-PLUS1.    10570000
           DISPLAY '** CUR BTCH DTE PLUS 1: ' PV-EP111-CC-DATE-PLUS1.   10580000
           DISPLAY ' '.                                                 10590000
                                                                        10600000
      *1700-EXIT.                                                       10610000
                                                                        10620000
      *----------------------------------------------------------       10630000
      *  1800-INCR-CUR-OPEN-DTE                                         10640000
      *  - ADDS THE NUMBER OF DAYS TO TODAY'S DATE FOR AN               10650000
      *    UPCOMING STORE OPENING DATE ALLOWANCE. USED IN               10660000
      *    THE STORE CURSOR TO ALLOW ADVANCE STORE OPENINGS             10670000
      *    TO BE INCLUDED IN THE REWARDS PROCESSING.                    10680000
      *----------------------------------------------------------       10690000
       1800-INCR-CUR-OPEN-DTE.                                          10700000
                                                                        10710000
           INITIALIZE DPG51 DPG52 DPG53                                 10720000
                      DPG54 DPG55 DPG56.                                10730000
                                                                        10740000
           MOVE EP111-CC-CRNT-DTE            TO DPG52-LK-DATE-INPUT.    10750000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   10760000
           SET  DPG52-LK-DTE-ISO-FMT         TO TRUE.                   10770000
           SET  DPG51-INCREMENT-DATE         TO TRUE.                   10780000
           MOVE EP045-DAYS-TILL-STORE-OPN    TO DPG51-INCR-DECR-DAYS-9. 10790000
           PERFORM 6000-DATE-ROUTINE.                                   10800000
           MOVE DPG55-DB2-ISO-DATE-FMT TO PV-OPEN-SOON-DATE             10810000
           DISPLAY '** ADVANCE ON OPENING DATE : ' PV-OPEN-SOON-DATE.   10820000
           DISPLAY ' '.                                                 10830000
                                                                        10840000
      *1800-EXIT.                                                       10850000
                                                                        10860000
      *----------------------------------------------------------------*10870000
      *  1900-INIT-EP-SELF-REPORTING.                                   10880000
      *  - SETS UP THE HEADER FOR A REPORT WHICH WILL LOG THE           10890000
      *    REWARD EVENTS EACH STORE RECEIVES.                           10900000
      *    REPORTING IS FOR SUPPORT PURPOSES ONLY.                      10910000
      *----------------------------------------------------------------*10920000
       1900-INIT-EP-SELF-REPORTING.                                     10930000
                                                                        10940000
      *    //MOVE DEFAULT INFORMATION INTO THE STD HEADER LINES         10950000
           MOVE PC-PROGRAM-NAME       TO DP132O-PROGRAM-NAME.           10960000
           MOVE PC-REPORT-NUMBER      TO DP132O-REPORT-NUMBER.          10970000
           MOVE PV-CUR-SYS-TMST(15:2) TO DP132O-RUN-MINUTE.             10980000
           MOVE PV-CUR-SYS-TMST(12:2) TO DP132O-RUN-HOUR.               10990000
           MOVE PV-CUR-SYS-TMST(9:2)  TO DP132O-RUN-DAY.                11000000
           MOVE PV-CUR-SYS-TMST(6:2)  TO DP132O-RUN-MONTH.              11010000
           MOVE PV-CUR-SYS-TMST(1:4)  TO DP132O-RUN-YEAR.               11020000
                                                                        11030000
      *1900-EXIT.                                                       11040000
                                                                        11050000
      *----------------------------------------------------------------*11060000
      *  2000-EVALUATE-JOB-RUN-TYPE.                                    11070000
      *  - CHECK IF THE REDEMPTION EXTRACT SHOULD BE PERFORMED AND IF   11080000
      *    IT SHOULD, PERFORM THE NECESSARY ROUTINES AND LOGIC.         11090000
      *----------------------------------------------------------------*11100000
       2000-EVALUATE-JOB-RUN-TYPE.                                      11110000
                                                                        11120000
           DISPLAY 'EVALUATING RUN MODE. . . = ' EP165-CC-JOB-RUN-TYP.  11130000
           EVALUATE TRUE                                                11140000
                                                                        11150000
             WHEN EP165-CC-CHK-FOR-UPDATES                              11160000
               PERFORM 2100-CHK-FOR-RWD-EVENT-CHANGES                   11170000
               IF PS-FUTURE-CHG-FND                                     11180000
                 DISPLAY '** CHANGES FOUND, PERFORMING EXTRACT **'      11190000
                 PERFORM 2200-AUTO-EXTRACT                              11200000
               ELSE                                                     11210000
                 DISPLAY '** NO CHANGES FOUND, EXITING PROGRAM **'      11220000
               END-IF                                                   11230000
                                                                        11240000
             WHEN EP165-CC-AUTO-EXTRACT                                 11250000
               DISPLAY '** PERFORMING SCHEDULED EXTRACT **'             11260000
               PERFORM 2200-AUTO-EXTRACT                                11270000
                                                                        11280000
             WHEN OTHER                                                 11290000
               MOVE '2000-EVALUATE-RUN-TYPE '  TO PV-PARAGRAPH-NAME     11300000
               MOVE 'INVALID VALUE IN CTRL CD' TO PV-MSG-TEXT           11310000
               SET ABEND-4003-CTRL-CARD-ERROR  TO TRUE                  11320000
               PERFORM 9010-ABEND                                       11330000
                                                                        11340000
           END-EVALUATE.                                                11350000
                                                                        11360000
      *2000-EXIT.                                                       11370000
                                                                        11380000
      *----------------------------------------------------------------*11390000
      *  2100-CHK-FOR-RWD-EVENT-CHANGES.                                11400000
      *  - CHECK IF ANY CHANGES HAVE OCCURRED TO THE AUT_RWD_EVNT,      11410000
      *    AUT_RWD_REDM, OR THE AUT_RWD_REDM_MDSE TABLES SINCE THE      11420000
      *    LAST TIME THIS PROGRAM RAN.                                  11430000
      *    THIS PARA MODIFIED TO FIT THE ORACLE NEEDS.                  11440050
      *----------------------------------------------------------------*11450000
       2100-CHK-FOR-RWD-EVENT-CHANGES.                                  11460000
                                                                        11470000
           PERFORM                                                      11480000
               WITH TEST AFTER                                          11490000
               VARYING PV-SQL-SUB                                       11500000
               FROM 1 BY 1                                              11510000
               UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES             11520000
                         OR  SQLCODE = +0                               11530000
                         OR  SQLCODE = +100                             11540045
                         OR  SQLCODE = +1403)                           11550045
                                                                        11560000
               EXEC SQL                                                 11570000
      **        SELECT '1'                                              11580042
      **         INTO :PV-RWD-CHGS-EXIST                                11590042
      **         FROM SYSIBM.SYSDUMMY1 X                                11600042
      **        WHERE EXISTS                                            11610042
      **          (                                                     11620042
                   SELECT '1'                                           11630000
                     INTO :PV-RWD-CHGS-EXIST                            11640042
                   FROM AUT_RWD_EVNT                                    11650000
      **           WHERE X.IBMREQD = X.IBMREQD                          11660042
      **             AND LAST_UPD_TMST BETWEEN                          11670042
                   WHERE LAST_UPD_TMST BETWEEN                          11680042
      ***             :EP149-CC-RWD-RDM-CUTOFF-TMST AND :PV-CUR-SYS-TMST11690040
                      TO_TIMESTAMP(:EP149-CC-RWD-RDM-CUTOFF-TMST,       11700040
                                   'YYYY-MM-DD HH24:MI:SS.FF') AND      11710040
                      TO_TIMESTAMP(:PV-CUR-SYS-TMST,                    11720040
                                   'YYYY-MM-DD HH24:MI:SS.FF')          11730040
                                                                        11740000
                   UNION                                                11750000
                                                                        11760000
                   SELECT '1'                                           11770000
                   FROM AUT_RWD_REDM                                    11780000
      ***          WHERE X.IBMREQD = X.IBMREQD                          11790042
      ***           AND LAST_UPD_TMST BETWEEN                           11800042
                   WHERE LAST_UPD_TMST BETWEEN                          11810042
      ***             :EP149-CC-RWD-RDM-CUTOFF-TMST AND :PV-CUR-SYS-TMST11820040
                      TO_TIMESTAMP(:EP149-CC-RWD-RDM-CUTOFF-TMST,       11830040
                                   'YYYY-MM-DD HH24:MI:SS.FF') AND      11840040
                      TO_TIMESTAMP(:PV-CUR-SYS-TMST,                    11850040
                                   'YYYY-MM-DD HH24:MI:SS.FF')          11860040
                                                                        11870000
                   UNION                                                11880000
                                                                        11890000
                   SELECT '1'                                           11900000
                   FROM AUT_RWD_REDM_MDSE                               11910000
      ***          WHERE X.IBMREQD = X.IBMREQD                          11920042
      ***            AND LAST_UPD_TMST BETWEEN                          11930042
                   WHERE LAST_UPD_TMST BETWEEN                          11940042
      ***             :EP149-CC-RWD-RDM-CUTOFF-TMST AND :PV-CUR-SYS-TMST11950040
                      TO_TIMESTAMP(:EP149-CC-RWD-RDM-CUTOFF-TMST,       11960040
                                   'YYYY-MM-DD HH24:MI:SS.FF') AND      11970040
                      TO_TIMESTAMP(:PV-CUR-SYS-TMST,                    11980040
                                   'YYYY-MM-DD HH24:MI:SS.FF')          11990040
      ***         )                                                     12000042
                                                                        12010000
               END-EXEC                                                 12020000
                                                                        12030000
               EVALUATE TRUE                                            12040000
                 WHEN SQLCODE = -904                                    12050000
                 WHEN SQLCODE = -911                                    12060000
                 WHEN SQLCODE = -913                                    12070000
                   CONTINUE                                             12080000
                                                                        12090000
                 WHEN SQLCODE = +0                                      12100000
                   SET PS-FUTURE-CHG-FND TO TRUE                        12110000
                                                                        12120000
                 WHEN SQLCODE = +100                                    12130000
                 WHEN SQLCODE = +1403                                   12140045
                   SET PS-NO-FUTURE-CHG-FND TO TRUE                     12150000
                                                                        12160000
                 WHEN OTHER                                             12170000
                   MOVE '2100-CHK-FOR-RWD-CHANGES' TO PV-PARAGRAPH-NAME 12180000
                   MOVE 'FETCH FUTURE CHANGES' TO PV-DB2-OPERATION      12190000
                   PERFORM 9900-ORA-ERROR                               12200034
               END-EVALUATE                                             12210000
           END-PERFORM.                                                 12220000
                                                                        12230000
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     12240000
             MOVE '2100-CHK-FOR-RWD-EVENT-CHANGES' TO PV-PARAGRAPH-NAME 12250000
             MOVE 'SELECT RETRIES EXCEEDED' TO PV-DB2-OPERATION         12260000
             PERFORM 9900-ORA-ERROR                                     12270034
           END-IF.                                                      12280000
                                                                        12290000
      *2100-EXIT.                                                       12300000
                                                                        12310000
      *----------------------------------------------------------------*12320000
      *  2200-AUTO-EXTRACT.                                             12330000
      *  - AUTOMATIC EXTRACT FOR THE REWARDS REDEMPTION EXTRACT.        12340000
      *    OPENS A VALID STORE CURSOR AND PROCESSES REWARD EVENTS       12350000
      *    APPROPRIATE FOR EACH STORE LOCATION.                         12360000
      *----------------------------------------------------------------*12370000
       2200-AUTO-EXTRACT.                                               12380000
                                                                        12390000
           PERFORM 2300-OPEN-STR-LOCN-CSR.                              12400000
           SET PS-NOT-END-OF-STR-LOC-CSR TO TRUE.                       12410000
                                                                        12420000
           PERFORM UNTIL PS-END-OF-STR-LOC-CSR                          12430000
                                                                        12440000
             PERFORM 2400-FETCH-STR-LOC-CSR                             12450000
             IF PS-NOT-END-OF-STR-LOC-CSR                               12460000
                                                                        12470000
               INITIALIZE PV-PAGE-COUNT                                 12480000
                          PV-LINE-COUNT                                 12490000
               MOVE XST00001-LOC-NBR TO RP-STORE-LOC-NBR                12500000
               PERFORM 7000-WRITE-REPORT-HEADERS                        12510000
                                                                        12520002
W36988         INITIALIZE PV-RWD-EVNTS-COUNT                            12530002
                                                                        12540000
               PERFORM 2500-OPEN-RWD-REDM-CSR                           12550000
073004         INITIALIZE PV-RWD-EVNT-ID-HELD                           12560000
073004         SET PS-NOT-END-OF-RWD-REDM-CSR TO TRUE                   12570000
               PERFORM 2600-PROCESS-RWD-REDM-CSR                        12580000
                       UNTIL PS-END-OF-RWD-REDM-CSR                     12590000
W36988                                                                  12600005
W36988        EVALUATE TRUE                                             12610002
W36988           WHEN PV-RWD-EVNTS-COUNT >= PC-FILE-COUNT-LIMIT-ABEND   12620002
W36988              DISPLAY '** PASSED ABEND LIMIT THRESHOLD !!    **'  12630002
W36988              DISPLAY '** ABENDING DUE TO EVENT RECORD COUNT **'  12640002
W36988              DISPLAY '** STORE LOCATION NBR: ' XST00001-LOC-NBR  12650006
W36988              DISPLAY '** REWARD EVENT COUNT: ' PV-RWD-EVNTS-COUNT12660006
W36988              MOVE '1000-INITIALIZATION'     TO PV-PARAGRAPH-NAME 12670002
W36988              MOVE 'DO NOT SEND FILE TO STR' TO PV-OPERATION-MSG-112680002
W36988              MOVE 'WILL CAUSE REGS TO GPF!' TO PV-MSG-TEXT       12690002
W36988              SET ABEND-4444-FORCED-ABEND TO TRUE                 12700002
W36988              PERFORM 9010-ABEND                                  12710002
W36988           WHEN PV-RWD-EVNTS-COUNT >= PC-FILE-COUNT-LIMIT-EMAIL   12720002
W36988              DISPLAY '** EMAIL FOR STORE: ' XST00001-LOC-NBR     12730006
W36988              ADD +1 TO PV-STR-REC-CT-OK                          12740004
W36988              SET PS-SET-EMAIL-RC TO TRUE                         12750002
W36988           WHEN OTHER                                             12760002
W36988              ADD +1 TO PV-STR-REC-CT-OK                          12770003
W36988        END-EVALUATE                                              12780002
                                                                        12790000
               PERFORM 2700-CLOSE-RWD-REDM-CSR                          12800000
               MOVE SPACES TO RWD-REDM-EXTRCT-RPT-REC                   12810000
               WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE     12820000
073004         MOVE RP-EP-EARLY-RWDS-MSG TO RWD-REDM-EXTRCT-RPT-REC     12830000
*              WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE     12840000
073004         INITIALIZE RWD-REDM-EXTRCT-RPT-REC                       12850000
               SET PS-NOT-END-OF-RWD-REDM-CSR TO TRUE                   12860000
                                                                        12870000
             END-IF                                                     12880000
                                                                        12890000
           END-PERFORM.                                                 12900000
                                                                        12910000
           PERFORM 2800-CLOSE-STR-LOC-CSR.                              12920000
                                                                        12930000
      *2200-EXIT.                                                       12940000
                                                                        12950000
      *----------------------------------------------------------------*12960000
      *  2300-OPEN-STR-LOCN-CSR.                                        12970000
      *  - OPENS THE STORE LOCATION CURSOR FOR PROCESSING.              12980000
      *----------------------------------------------------------------*12990000
       2300-OPEN-STR-LOCN-CSR.                                          13000000
                                                                        13010000
           EXEC SQL                                                     13020000
             OPEN STR_LOC_CSR                                           13030033
           END-EXEC.                                                    13040000
                                                                        13050000
           IF SQLCODE NOT EQUAL ZERO                                    13060000
             MOVE 'OPENING STR LOC CSR   '        TO PV-DB2-OPERATION   13070000
             MOVE '2300-OPEN-STR-LOCN-CSR'        TO PV-PARAGRAPH-NAME  13080000
             PERFORM 9020-SQL-ABEND-ROUTINE                             13090000
           END-IF.                                                      13100000
                                                                        13110000
      *2300-EXIT.                                                       13120000
                                                                        13130000
      *----------------------------------------------------------------*13140000
      *  2400-FETCH-STR-LOC-CSR.                                        13150000
      *  - FETCHES THE NEXT STORE LOCATION FOUND IN THE CURSOR.         13160000
      *----------------------------------------------------------------*13170000
       2400-FETCH-STR-LOC-CSR.                                          13180000
                                                                        13190000
           PERFORM                                                      13200000
               WITH TEST AFTER                                          13210000
               VARYING PV-SQL-SUB                                       13220000
               FROM 1 BY 1                                              13230000
               UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES             13240000
                         OR  SQLCODE = +0                               13250000
                         OR  SQLCODE = +100                             13260045
                         OR  SQLCODE = +1403)                           13270045
                                                                        13280000
               EXEC SQL                                                 13290000
                 FETCH STR_LOC_CSR                                      13300033
                   INTO :XST00001-LOC-NBR                               13310000
               END-EXEC                                                 13320000
                                                                        13330000
               EVALUATE TRUE                                            13340000
                 WHEN SQLCODE = +0                                      13350000
                   CONTINUE                                             13360000
                 WHEN SQLCODE = +100                                    13370000
                 WHEN SQLCODE = +1403                                   13380000
                   SET PS-END-OF-STR-LOC-CSR      TO TRUE               13390000
                 WHEN OTHER                                             13400000
                   SET PS-END-OF-STR-LOC-CSR      TO TRUE               13410000
                   MOVE 'FETCHING STR GP CSR'     TO PV-DB2-OPERATION   13420000
                   MOVE '2400-FETCH-STR-GP-CSR'   TO PV-PARAGRAPH-NAME  13430000
                   PERFORM 9020-SQL-ABEND-ROUTINE                       13440000
               END-EVALUATE                                             13450000
           END-PERFORM.                                                 13460000
                                                                        13470000
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     13480000
             MOVE '2400-FETCH-STR-LOC-CSR ' TO PV-PARAGRAPH-NAME        13490000
             MOVE 'SELECT RETRIES EXCEEDED' TO PV-DB2-OPERATION         13500000
             PERFORM 9020-SQL-ABEND-ROUTINE                             13510000
           END-IF.                                                      13520000
                                                                        13530000
      *2400-EXIT.                                                       13540000
                                                                        13550000
      *----------------------------------------------------------------*13560000
      *  2500-OPEN-RWD-REDM-CSR.                                        13570000
      *  - OPENS THE REWARD EVENTS REDEMPTION EXTRACT CURSOR            13580000
      *    RELEVANT ON A SPECIFIED STORE LOCATION NUMBER.               13590000
      *----------------------------------------------------------------*13600000
       2500-OPEN-RWD-REDM-CSR.                                          13610000
                                                                        13620000
           EXEC SQL                                                     13630000
             OPEN RWD_REDM_EVNT_CSR                                     13640033
           END-EXEC.                                                    13650000
                                                                        13660000
           IF SQLCODE NOT EQUAL ZERO                                    13670000
             MOVE 'OPENING RWD REDM CSR'          TO PV-ORA-OPERATION   13680051
             MOVE '2500-OPEN-RWD-REDM-CSR'        TO PV-PARAGRAPH-NAME  13690000
             PERFORM 9900-ORA-ERROR                                     13700051
           END-IF.                                                      13710000
                                                                        13720000
      *2500-EXIT.                                                       13730000
                                                                        13740000
      *----------------------------------------------------------------*13750000
      *  2600-PROCESS-RWD-REDM-CSR.                                     13760000
      *  - FOR EACH STORE, THIS PARAGRAPH WILL PROCESS THE ISSUED       13770000
      *    REWARDS REDEMPTION CURSOR, MAKING CALLS TO FORMAT THE        13780000
      *    LOCAL VARIABLES AND TO EVENTUALLY PRINT OUT THE RECORD.      13790000
      *----------------------------------------------------------------*13800000
       2600-PROCESS-RWD-REDM-CSR.                                       13810000
                                                                        13820000
           PERFORM 2610-FETCH-RWD-REDM-CSR.                             13830000
                                                                        13840000
      *    //WHEN ONE OF THE FOLLOWING FIELDS IS NULL. . .              13850000
           IF PV-PRE-DYS-NULL                                           13860000
              MOVE 0 TO PRE-BEG-DAYS-QTY OF AUT00004                    13870035
           END-IF.                                                      13880000
           IF PV-PST-DYS-NULL                                           13890000
              MOVE 0 TO POST-END-DAYS-QTY OF AUT00004                   13900035
           END-IF.                                                      13910000
                                                                        13920000
           IF PS-NOT-END-OF-RWD-REDM-CSR                                13930000
             IF PV-RWD-EVNT-ID-HELD IS NOT EQUAL TO                     13940035
                                       RWD-EVNT-ID OF AUT00001          13950035
                                                                        13960000
               PERFORM 2620-PROCESS-EVENT-ID-FIELD                      13970000
               PERFORM 2630-PROCESS-BEG-AND-END-DATES                   13980000
               PERFORM 2640-PROCESS-AUTH-TYP-CHGS                       13990000
                                                                        14000000
               MOVE ZERO TO PV-DEPTS-FOUND                              14010000
               SET PS-NOT-END-OF-DEPTS-CSR TO TRUE                      14020000
                                                                        14030000
               IF MDSE-IE-CDE OF AUT00004 EQUAL TO PC-MDSE-ALL-DEPTS    14040035
                 SET PS-END-OF-DEPTS-CSR TO TRUE                        14050000
               ELSE                                                     14060000
                 PERFORM 2660-OPEN-I-E-MDSE-DEPT-CSR                    14070000
               END-IF                                                   14080000
                                                                        14090000
               INITIALIZE PV-DEPT-VAR-1 PV-DEPT-VAR-2                   14100000
                          PV-DEPT-VAR-3 PV-DEPT-VAR-4 PV-DEPT-VAR-5     14110000
                                                                        14120000
               PERFORM VARYING PV-DEPTS-CT FROM 1 BY 1                  14130000
               UNTIL (PV-DEPTS-CT GREATER THAN PC-MDSE-MAX-DEPTS        14140000
                      OR PS-END-OF-DEPTS-CSR)                           14150000
                                                                        14160000
                  PERFORM 2670-FETCH-I-E-MDSE-DEPT-CSR                  14170000
                                                                        14180000
                  IF SQLCODE = ZERO                                     14190000
                    ADD +1 TO PV-DEPTS-FOUND                            14200000
                    EVALUATE PV-DEPTS-CT                                14210000
                      WHEN 1                                            14220000
                        MOVE DEPT-NBR OF AUT00005 TO PV-DEPT-VAR-1      14230035
                      WHEN 2                                            14240000
                        MOVE DEPT-NBR OF AUT00005 TO PV-DEPT-VAR-2      14250035
                      WHEN 3                                            14260000
                        MOVE DEPT-NBR OF AUT00005 TO PV-DEPT-VAR-3      14270035
                      WHEN 4                                            14280000
                        MOVE DEPT-NBR OF AUT00005 TO PV-DEPT-VAR-4      14290035
                      WHEN 5                                            14300000
                        MOVE DEPT-NBR OF AUT00005 TO PV-DEPT-VAR-5      14310035
                    END-EVALUATE                                        14320000
                                                                        14330000
                  END-IF                                                14340000
                                                                        14350000
               END-PERFORM                                              14360000
                                                                        14370000
               IF MDSE-IE-CDE OF AUT00004 NOT EQUAL TO PC-MDSE-ALL-DEPTS14380035
                 PERFORM 2680-CLOSE-I-E-MDSE-DEPT-CSR                   14390000
               END-IF                                                   14400000
                                                                        14410000
               PERFORM 2685-EVALUATE-DEPTS-FOUND                        14420000
                                                                        14430000
               PERFORM 2690-BUILD-RWD-REDM-RECORD                       14440000
               PERFORM 8000-WRITE-RWD-REDM-RECORD                       14450000
               PERFORM 2900-BUILD-AND-WRITE-RPT-REC                     14460000
             END-IF                                                     14470000
           END-IF.                                                      14480000
                                                                        14490000
           MOVE RWD-EVNT-ID OF AUT00001 TO PV-RWD-EVNT-ID-HELD.         14500035
                                                                        14510000
      *2600-EXIT.                                                       14520000
                                                                        14530000
      *----------------------------------------------------------------*14540000
      *  2610-FETCH-RWD-REDM-CSR.                                       14550000
      *  - FETCHES THE REWARD EVENT INFORMATION INTO LOCAL VARIABLES.   14560000
      *----------------------------------------------------------------*14570000
       2610-FETCH-RWD-REDM-CSR.                                         14580000
                                                                        14590000
           INITIALIZE AUT00001                                          14600035
                      AUT00004.                                         14610035
           PERFORM                                                      14620000
               WITH TEST AFTER                                          14630000
               VARYING PV-SQL-SUB                                       14640000
               FROM 1 BY 1                                              14650000
               UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES             14660000
                         OR  SQLCODE = +0                               14670000
                         OR  SQLCODE = +100                             14680045
                         OR  SQLCODE = +1403)                           14690045
                                                                        14700000
               EXEC SQL                                                 14710000
                 FETCH RWD_REDM_EVNT_CSR                                14720033
                   INTO :AUT00001.RWD-EVNT-ID                           14730035
                      , :AUT00001.RWD-DISC-AMT                          14740035
                      , :AUT00001.RWD-TYP-CDE                           14750035
                      , :AUT00004.STR-GP-TYP-CDE                        14760035
                      , :AUT00004.STR-GP-ID                             14770035
                      , :AUT00004.REDM-BEG-DTE                          14780035
                      , :AUT00004.REDM-END-DTE                          14790035
                      , :AUT00004.PRE-BEG-DAYS-QTY :PV-RWD-PRE-DYS-IND  14800035
                      , :AUT00004.POST-END-DAYS-QTY :PV-RWD-PST-DYS-IND 14810035
                      , :AUT00004.AUTH-TYP-CDE                          14820035
                      , :AUT00004.MDSE-IE-CDE                           14830035
               END-EXEC                                                 14840000
                                                                        14850000
               EVALUATE TRUE                                            14860000
                 WHEN SQLCODE = +0                                      14870000
                   CONTINUE                                             14880054
                 WHEN SQLCODE = +100                                    14890000
                 WHEN SQLCODE = +1403                                   14900000
                   SET PS-END-OF-RWD-REDM-CSR     TO TRUE               14910000
                 WHEN OTHER                                             14920000
                   SET PS-END-OF-RWD-REDM-CSR     TO TRUE               14930000
                   MOVE 'FETCHING RWD REDM CSR'   TO PV-ORA-OPERATION   14940051
                   MOVE '2610-FETCH-RWD-REDM-CSR' TO PV-PARAGRAPH-NAME  14950000
                   PERFORM 9900-ORA-ERROR                               14960051
               END-EVALUATE                                             14970000
           END-PERFORM.                                                 14980000
                                                                        14990000
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     15000000
             MOVE '2610-FETCH-RWD-REDM-CSR' TO PV-PARAGRAPH-NAME        15010000
             MOVE 'SELECT RETRIES EXCEEDED' TO PV-ORA-OPERATION         15020051
             PERFORM 9900-ORA-ERROR                                     15030051
           END-IF.                                                      15040000
                                                                        15050000
      *2610-EXIT.                                                       15060000
                                                                        15070000
      *----------------------------------------------------------------*15080000
      *  2620-PROCESS-EVENT-ID-FIELD                                    15090000
      *  - FORMATS THE EVENT ID FIELD FROM LEFT JUSTIFIED PIC X(5)      15100000
      *    INTO A LEADING ZEROES EDITED ALPHANUMERIC FIELD.             15110000
      *    NECESSARY DUE TO THE WAY THE FIELD IS STORE IN THE DB.       15120000
      *----------------------------------------------------------------*15130000
       2620-PROCESS-EVENT-ID-FIELD.                                     15140000
                                                                        15150000
      *    //MAY PROCESS EITHER NUMERIC OR ALPHANUMERIC EVNT IDS        15160000
           UNSTRING RWD-EVNT-ID OF AUT00001 DELIMITED BY SPACES         15170035
                    INTO PV-RWD-EVNT-ID-CHARS                           15180000
           END-UNSTRING.                                                15190000
                                                                        15200000
           INSPECT PV-RWD-EVNT-ID-CHARS REPLACING ALL " " BY "0".       15210000
           MOVE PV-RWD-EVNT-ID-CHARS TO EP140-RWD-REDM-EVNT-ID.         15220000
092410     MOVE PV-RWD-EVNT-ID-CHARS TO EP142-RWD-REDM-EVNT-ID.         15230027
                                                                        15240000
      *2620-EXIT.                                                       15250000
                                                                        15260000
      *----------------------------------------------------------------*15270000
      *  2630-PROCESS-BEG-AND-END-DATES.                                15280000
      *  - EACH REWARD EVENT HAS SPECIFIED START AND END DATES          15290000
      *    WHICH MAY BE ALTERED FOR CERTAIN STORES OR DISTRICTS.        15300000
      *    IN EACH REWARD EVENT RECORD IS A PRE-OPEN AND POST-CLOSE     15310000
      *    NUMERIC FIELD WHICH WOULD BE THE NUMBER OF DAYS TO ADJUST    15320000
      *    HOW SOON THE AWARDS WOULD BEGIN ACTIVATING, OR HOW MUCH      15330000
      *    LONGER THEY WOULD ACTIVATE AT SPECIFIED LOCATIONS.           15340000
      *----------------------------------------------------------------*15350000
       2630-PROCESS-BEG-AND-END-DATES.                                  15360000
                                                                        15370000
      *    //COPY REDM BEG AND END DATES TO MOD IN CASE NO CHANGES EXIST15380000
           MOVE REDM-BEG-DTE OF AUT00004 TO PV-MOD-REDM-BEG-DTE.        15390035
           MOVE REDM-END-DTE OF AUT00004 TO PV-MOD-REDM-END-DTE.        15400035
                                                                        15410000
           IF PRE-BEG-DAYS-QTY OF AUT00004 NOT EQUAL TO ZERO            15420035
      *      //DECREMENT ISSUED BEG DATE BY X DAYS                      15430000
             INITIALIZE DPG51 DPG52 DPG53                               15440000
                        DPG54 DPG55 DPG56                               15450000
                                                                        15460000
             SET DPG51-FISCAL-454-CALENDAR  TO TRUE                     15470000
             SET DPG51-DECREMENT-DATE       TO TRUE                     15480000
             SET DPG52-LK-DTE-ISO-FMT       TO TRUE                     15490000
             MOVE PRE-BEG-DAYS-QTY OF AUT00004 TO DPG51-INCR-DECR-DAYS-915500035
             MOVE REDM-BEG-DTE     OF AUT00004 TO DPG52-DB2-ISO-DATE-FMT15510035
                                                                        15520000
             MOVE 'DECREMENT BEG ISSUED DATE' TO PV-OPERATION-MSG-1     15530000
             PERFORM 6000-DATE-ROUTINE                                  15540000
             MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-MOD-REDM-BEG-DTE      15550000
                                                                        15560000
           END-IF.                                                      15570000
                                                                        15580000
           IF POST-END-DAYS-QTY OF AUT00004 NOT EQUAL TO ZERO           15590035
      *      //INCREMENT ISSUED END DATE BY X DAYS                      15600000
             INITIALIZE DPG51 DPG52 DPG53                               15610000
                        DPG54 DPG55 DPG56                               15620000
                                                                        15630000
             SET DPG51-FISCAL-454-CALENDAR  TO TRUE                     15640000
             SET DPG51-INCREMENT-DATE       TO TRUE                     15650000
             SET DPG52-LK-DTE-ISO-FMT       TO TRUE                     15660000
             MOVE POST-END-DAYS-QTY OF AUT00004                         15670035
                                            TO DPG51-INCR-DECR-DAYS-9   15680035
             MOVE REDM-END-DTE OF AUT00004  TO DPG52-DB2-ISO-DATE-FMT   15690035
                                                                        15700000
             MOVE 'INCREMENT END ISSUED DATE' TO PV-OPERATION-MSG-1     15710000
             PERFORM 6000-DATE-ROUTINE                                  15720000
             MOVE DPG55-DB2-ISO-DATE-FMT    TO PV-MOD-REDM-END-DTE      15730000
                                                                        15740000
           END-IF.                                                      15750000
                                                                        15760000
      *2630-EXIT.                                                       15770000
                                                                        15780000
      *----------------------------------------------------------------*15790000
      *  2640-PROCESS-AUTH-TYP-CHGS.                                    15800000
      *  -                                                              15810000
      *----------------------------------------------------------------*15820000
       2640-PROCESS-AUTH-TYP-CHGS.                                      15830000
                                                                        15840000
           EVALUATE AUTH-TYP-CDE OF AUT00004                            15850035
                                                                        15860000
             WHEN 1                                                     15870000
               MOVE AUTH-TYP-CDE OF AUT00004 TO PV-AUTH-TYP-CDE-FINAL   15880035
               MOVE ZEROES TO PV-RWD-DISC-AMT-NMRIC                     15890000
                                                                        15900000
             WHEN 2                                                     15910000
               MOVE ZEROES TO PV-AUTH-TYP-CDE-FINAL                     15920000
               MOVE ZEROES TO PV-RWD-DISC-AMT-NMRIC                     15930000
                                                                        15940000
             WHEN OTHER                                                 15950000
               MOVE AUTH-TYP-CDE OF AUT00004 TO PV-AUTH-TYP-CDE-FINAL   15960035
               MOVE RWD-DISC-AMT OF AUT00001 TO PV-RWD-DISC-AMT-NMRIC   15970035
                                                                        15980000
           END-EVALUATE.                                                15990000
                                                                        16000000
      *2640-EXIT.                                                       16010000
                                                                        16020000
      *----------------------------------------------------------------*16030000
      *  2660-OPEN-I-E-MDSE-DEPT-CSR.                                   16040000
      *  - OPENS THE INCLUDE / EXCLUDE DEPARTMENTS CURSOR.              16050000
      *----------------------------------------------------------------*16060000
       2660-OPEN-I-E-MDSE-DEPT-CSR.                                     16070000
                                                                        16080000
           EXEC SQL                                                     16090000
             OPEN I_E_MDSE_DEPT_CSR                                     16100033
           END-EXEC.                                                    16110000
                                                                        16120000
           IF SQLCODE NOT EQUAL ZERO                                    16130000
             MOVE 'OPENING I/E MDSE DEPT CSR'     TO PV-ORA-OPERATION   16140051
             MOVE '2660-OPEN-I-E-MDSE-DEPT-CSR'   TO PV-PARAGRAPH-NAME  16150000
             PERFORM 9900-ORA-ERROR                                     16160051
           END-IF.                                                      16170000
                                                                        16180000
      *2660-EXIT.                                                       16190000
                                                                        16200000
      *----------------------------------------------------------------*16210000
      *  2670-FETCH-I-E-MDSE-DEPT-CSR.                                  16220000
      *  - FETCHES A DEPARTMENT NUMBER AS IT RELATES TO A               16230000
      *    SPECIFIC REWARD EVENT AND STORE LOCATION.                    16240000
      *----------------------------------------------------------------*16250000
       2670-FETCH-I-E-MDSE-DEPT-CSR.                                    16260000
                                                                        16270000
           PERFORM                                                      16280000
               WITH TEST AFTER                                          16290000
               VARYING PV-SQL-SUB                                       16300000
               FROM 1 BY 1                                              16310000
               UNTIL (PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES             16320000
                         OR  SQLCODE = +0                               16330000
                         OR  SQLCODE = +100                             16340045
                         OR  SQLCODE = +1403)                           16350045
                                                                        16360000
               EXEC SQL                                                 16370000
                 FETCH I_E_MDSE_DEPT_CSR                                16380033
                   INTO :AUT00005.DEPT-NBR                              16390035
               END-EXEC                                                 16400000
                                                                        16410000
               EVALUATE TRUE                                            16420000
                 WHEN SQLCODE = +0                                      16430000
                   CONTINUE                                             16440000
                 WHEN SQLCODE = +100                                    16450000
                 WHEN SQLCODE = +1403                                   16460000
                   SET PS-END-OF-DEPTS-CSR        TO TRUE               16470000
                 WHEN OTHER                                             16480000
                   SET PS-END-OF-RWD-REDM-CSR     TO TRUE               16490000
                   MOVE 'FETCHING IE DEPTS CSR'   TO PV-ORA-OPERATION   16500051
                   MOVE '2670-FETCH-IE-DEPTS-CSR' TO PV-PARAGRAPH-NAME  16510000
                   PERFORM 9900-ORA-ERROR                               16520051
               END-EVALUATE                                             16530000
           END-PERFORM.                                                 16540000
                                                                        16550000
           IF PV-SQL-SUB GREATER PC-SQL-MAX-RETRIES                     16560000
             MOVE '2670-FETCH-IE-DEPTS-CSR' TO PV-PARAGRAPH-NAME        16570000
             MOVE 'SELECT RETRIES EXCEEDED' TO PV-ORA-OPERATION         16580051
             PERFORM 9900-ORA-ERROR                                     16590051
           END-IF.                                                      16600000
                                                                        16610000
      *2670-EXIT.                                                       16620000
                                                                        16630000
      *----------------------------------------------------------------*16640000
      *  2680-CLOSE-I-E-MDSE-DEPT-CSR.                                  16650000
      *  - CLOSES THE INCLUDE / EXCLUDE DEPARTMENT LIST CURSOR.         16660000
      *----------------------------------------------------------------*16670000
       2680-CLOSE-I-E-MDSE-DEPT-CSR.                                    16680000
                                                                        16690000
           EXEC SQL                                                     16700000
             CLOSE I_E_MDSE_DEPT_CSR                                    16710033
           END-EXEC.                                                    16720000
                                                                        16730000
           IF SQLCODE NOT EQUAL ZERO                                    16740000
             MOVE 'CLOSING MDSE DEPT CSR'        TO PV-ORA-OPERATION    16750051
             MOVE '2680-CLOSE-I-E-MDSE-DEPT-CSR' TO PV-PARAGRAPH-NAME   16760000
             PERFORM 9900-ORA-ERROR                                     16770051
           END-IF.                                                      16780000
                                                                        16790000
      *2680-EXIT.                                                       16800000
                                                                        16810000
      *----------------------------------------------------------------*16820000
      *  2685-EVALUATE-DEPTS-FOUND.                                     16830000
      *  - BUILDS THE DEPT-LIST & CALCULATES THE LIST'S LENGTH.         16840000
      *----------------------------------------------------------------*16850000
       2685-EVALUATE-DEPTS-FOUND.                                       16860000
                                                                        16870000
           INITIALIZE PV-DEPT-LIST.                                     16880000
           EVALUATE PV-DEPTS-FOUND                                      16890000
             WHEN 0                                                     16900000
               STRING ',,,,' DELIMITED BY SIZE                          16910000
               INTO PV-DEPT-LIST                                        16920000
               END-STRING                                               16930000
               MOVE PC-0-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              16940000
             WHEN 1                                                     16950000
               STRING PV-DEPT-VAR-1 ',,,,' DELIMITED BY SIZE            16960000
               INTO PV-DEPT-LIST                                        16970000
               END-STRING                                               16980000
               MOVE PC-1-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              16990000
             WHEN 2                                                     17000000
               STRING PV-DEPT-VAR-1 ',' PV-DEPT-VAR-2 ',,,'             17010000
                      DELIMITED BY SIZE                                 17020000
               INTO PV-DEPT-LIST                                        17030000
               END-STRING                                               17040000
               MOVE PC-2-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              17050000
             WHEN 3                                                     17060000
               STRING PV-DEPT-VAR-1 ',' PV-DEPT-VAR-2 ','               17070000
                      PV-DEPT-VAR-3 ',,' DELIMITED BY SIZE              17080000
               INTO PV-DEPT-LIST                                        17090000
               END-STRING                                               17100000
               MOVE PC-3-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              17110000
             WHEN 4                                                     17120000
               STRING PV-DEPT-VAR-1 ',' PV-DEPT-VAR-2 ','               17130000
                      PV-DEPT-VAR-3 ',' PV-DEPT-VAR-4 ','               17140000
                      DELIMITED BY SIZE                                 17150000
               INTO PV-DEPT-LIST                                        17160000
               END-STRING                                               17170000
               MOVE PC-4-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              17180000
             WHEN 5                                                     17190000
               STRING PV-DEPT-VAR-1 ',' PV-DEPT-VAR-2 ','               17200000
                      PV-DEPT-VAR-3 ',' PV-DEPT-VAR-4 ','               17210000
                      PV-DEPT-VAR-5                                     17220000
                      DELIMITED BY SIZE                                 17230000
               INTO PV-DEPT-LIST                                        17240000
               END-STRING                                               17250000
               MOVE PC-5-DEPT-SIZE TO PV-DEPT-FIELD-LENGTH              17260000
           END-EVALUATE.                                                17270000
                                                                        17280000
      *2685-EXIT.                                                       17290000
                                                                        17300000
      *----------------------------------------------------------------*17310000
      *  2690-BUILD-RWD-REDM-RECORD                                     17320000
      *  - BUILDS THE REWARDS REDEMPTION PARAMETER EXTRACT RECORD.      17330000
      *    THE VARIABLE LENGTH PORTION OF THIS RECORD LAYOUT CONTAINS   17340000
      *    THE LIST OF APPLICABLE DEPARTMENTS AFFECTED BY THE INCLUDE   17350000
      *    EXCLUDE FLAG. THE EXTRACT RECORD IS COMMA DELIMITED AND EACH 17360000
      *    LINE ENDS WITH A SEMICOLON (';').                            17370000
      *----------------------------------------------------------------*17380000
       2690-BUILD-RWD-REDM-RECORD.                                      17390000
                                                                        17400000
092410*    //COPY EACH FIELD OVER TO THE LAYOUT IN EPRD140 & EPRD142    17410027
           MOVE XST00001-LOC-NBR         TO EP140-RWD-REDM-STR-LOC.     17420000
092410     MOVE XST00001-LOC-NBR         TO EP142-RWD-REDM-STR-LOC.     17430027
                                                                        17440000
           MOVE PV-MOD-REDM-BEG-DTE(1:4) TO PV-CONV-DTE-CCYY.           17450000
           MOVE PV-MOD-REDM-BEG-DTE(6:2) TO PV-CONV-DTE-MM.             17460000
           MOVE PV-MOD-REDM-BEG-DTE(9:2) TO PV-CONV-DTE-DD.             17470000
           MOVE PV-CONV-DTE              TO EP140-RWD-REDM-BEG-DTE.     17480000
092410     MOVE PV-CONV-DTE              TO EP142-RWD-REDM-BEG-DTE.     17490027
                                                                        17500000
           MOVE PV-MOD-REDM-END-DTE(1:4) TO PV-CONV-DTE-CCYY.           17510000
           MOVE PV-MOD-REDM-END-DTE(6:2) TO PV-CONV-DTE-MM.             17520000
           MOVE PV-MOD-REDM-END-DTE(9:2) TO PV-CONV-DTE-DD.             17530000
           MOVE PV-CONV-DTE              TO EP140-RWD-REDM-END-DTE.     17540000
092410     MOVE PV-CONV-DTE              TO EP142-RWD-REDM-END-DTE.     17550027
                                                                        17560000
           MOVE PV-AUTH-TYP-CDE-FINAL    TO EP140-RWD-REDM-AUTH-TYP-CDE.17570000
           MOVE RWD-TYP-CDE OF AUT00001  TO EP140-RWD-REDM-DISC-TYP-CDE.17580035
092410     MOVE PV-AUTH-TYP-CDE-FINAL    TO EP142-RWD-REDM-AUTH-TYP-CDE.17590027
092410     MOVE RWD-TYP-CDE  OF AUT00001 TO EP142-RWD-REDM-DISC-TYP-CDE.17600035
                                                                        17610000
092410     MOVE PRE-BEG-DAYS-QTY OF AUT00004 TO                         17620035
092410                                      EP142-RWD-REDM-PRE-DAYS-QTY 17630027
092410     MOVE POST-END-DAYS-QTY OF AUT00004 TO                        17640035
092410                                     EP142-RWD-REDM-POST-DAYS-QTY 17650027
                                                                        17660013
           IF RWD-TYP-CDE OF AUT00001 EQUAL TO 'P'                      17670035
             STRING '000'                                               17680000
                    PV-RWD-DISC-AMT-NMRIC(2:2) DELIMITED BY SIZE        17690000
               INTO PV-RWD-DISC-AMT-FINAL                               17700000
             END-STRING                                                 17710000
             MOVE PV-RWD-DISC-AMT-FINAL  TO EP140-RWD-REDM-DISC-AMT     17720000
092410       MOVE PV-RWD-DISC-AMT-FINAL  TO EP142-RWD-REDM-DISC-AMT     17730027
           ELSE                                                         17740000
             STRING PV-RWD-DISC-AMT-NMRIC(1:3)                          17750000
                    PV-RWD-DISC-AMT-NMRIC(4:2) DELIMITED BY SIZE        17760000
               INTO PV-RWD-DISC-AMT-FINAL                               17770000
             END-STRING                                                 17780000
             MOVE PV-RWD-DISC-AMT-FINAL  TO EP140-RWD-REDM-DISC-AMT     17790000
092410       MOVE PV-RWD-DISC-AMT-FINAL  TO EP142-RWD-REDM-DISC-AMT     17800027
           END-IF.                                                      17810000
                                                                        17820000
      *    //IF THE MDSE CDE ON MVS IS 2, CHANGE IT TO 0 FOR POS        17830000
           MOVE MDSE-IE-CDE OF AUT00004 TO PV-MDSE-IE-CDE-INT.          17840035
           IF MDSE-IE-CDE OF AUT00004 EQUAL TO PC-MDSE-ALL-DEPTS        17850035
             MOVE 0 TO PV-MDSE-IE-CDE-INT                               17860000
           END-IF.                                                      17870000
                                                                        17880000
           MOVE PV-MDSE-IE-CDE-INT       TO EP140-RWD-REDM-I-E-TYP-CDE. 17890000
092410     MOVE PV-MDSE-IE-CDE-INT       TO EP142-RWD-REDM-I-E-TYP-CDE. 17900027
                                                                        17910000
           STRING PV-DEPT-LIST(1:PV-DEPT-FIELD-LENGTH) ';'              17920000
                  DELIMITED BY SIZE                                     17930000
                  INTO EP140-RWD-REDM-EXTR-VAR                          17940000
           END-STRING.                                                  17950000
                                                                        17960000
092410     STRING PV-DEPT-LIST(1:PV-DEPT-FIELD-LENGTH) ';'              17970027
092410            DELIMITED BY SIZE                                     17980027
092410            INTO EP142-RWD-REDM-EXTR-VAR                          17990027
092410     END-STRING.                                                  18000027
                                                                        18010012
      *2690-EXIT.                                                       18020000
                                                                        18030000
      *----------------------------------------------------------------*18040000
      *  2700-CLOSE-RWD-REDM-CSR.                                       18050000
      *  - CLOSES THE REWARDS REDEMPTION EXTRACT CURSOR.                18060000
      *----------------------------------------------------------------*18070000
       2700-CLOSE-RWD-REDM-CSR.                                         18080000
                                                                        18090000
           EXEC SQL                                                     18100000
             CLOSE RWD_REDM_EVNT_CSR                                    18110033
           END-EXEC.                                                    18120000
                                                                        18130000
           IF SQLCODE NOT EQUAL ZERO                                    18140000
             MOVE 'CLOSING RWD REDM CSR'          TO PV-ORA-OPERATION   18150051
             MOVE '2700-CLOSE-RWD-REDM-CSR'       TO PV-PARAGRAPH-NAME  18160000
             PERFORM 9900-ORA-ERROR                                     18170051
           END-IF.                                                      18180000
                                                                        18190000
      *2700-EXIT.                                                       18200000
                                                                        18210000
      *----------------------------------------------------------------*18220000
      *  2800-CLOSE-STR-LOC-CSR.                                        18230000
      *  - CLOSES THE STORE LOCATION CURSOR.                            18240000
      *----------------------------------------------------------------*18250000
       2800-CLOSE-STR-LOC-CSR.                                          18260000
                                                                        18270000
           EXEC SQL                                                     18280000
             CLOSE STR_LOC_CSR                                          18290033
           END-EXEC.                                                    18300000
                                                                        18310000
           IF SQLCODE NOT EQUAL ZERO                                    18320000
             MOVE 'CLOSING STR LOC CSR'         TO PV-ORA-OPERATION     18330051
             MOVE '2800-CLOSE-STR-LOC-CSR'      TO PV-PARAGRAPH-NAME    18340000
             PERFORM 9900-ORA-ERROR                                     18350051
           END-IF.                                                      18360000
                                                                        18370000
      *2800-EXIT.                                                       18380000
                                                                        18390000
      *----------------------------------------------------------------*18400000
      *  2900-BUILD-AND-WRITE-RPT-REC.                                  18410000
      *  - BUILDS A REPORT RECORD FOR A SPECIFIC REWARD EVENT ID        18420000
      *    AS IT RELATES TO A SPECIFIC STORE LOCATION NUMBER.           18430000
      *    THIS REPORTING IS SOLELY FOR SUPPORT PURPOSES.               18440000
      *----------------------------------------------------------------*18450000
       2900-BUILD-AND-WRITE-RPT-REC.                                    18460000
                                                                        18470000
      *    //CREATE A REPORT FORMATTED VERSION OF THE CUR RWD REC       18480000
           MOVE RWD-EVNT-ID OF AUT00001    TO RP-EVENT-ID.              18490035
           MOVE AUTH-TYP-CDE OF AUT00004    TO RP-AUTH-TYP.             18500035
           MOVE MDSE-IE-CDE  OF AUT00004    TO RP-IE-TYP-CDE.           18510035
           MOVE RWD-DISC-AMT OF AUT00001   TO RP-DISC-AMT.              18520035
           MOVE RWD-TYP-CDE  OF AUT00001   TO RP-DISC-TYP.              18530035
           MOVE PV-DEPT-LIST             TO RP-DEPT-LIST.               18540000
                                                                        18550000
           MOVE PV-MOD-REDM-BEG-DTE(1:4) TO PV-CONV-DTE-CCYY.           18560000
           MOVE PV-MOD-REDM-BEG-DTE(6:2) TO PV-CONV-DTE-MM.             18570000
           MOVE PV-MOD-REDM-BEG-DTE(9:2) TO PV-CONV-DTE-DD.             18580000
           MOVE PV-CONV-DTE              TO RP-STRT-DTE.                18590000
                                                                        18600000
           MOVE PV-MOD-REDM-END-DTE(1:4) TO PV-CONV-DTE-CCYY.           18610000
           MOVE PV-MOD-REDM-END-DTE(6:2) TO PV-CONV-DTE-MM.             18620000
           MOVE PV-MOD-REDM-END-DTE(9:2) TO PV-CONV-DTE-DD.             18630000
           MOVE PV-CONV-DTE              TO RP-END-DATE.                18640000
                                                                        18650000
           IF PV-LINE-COUNT >= PC-LINES-PER-PAGE                        18660000
073004       MOVE SPACES TO RWD-REDM-EXTRCT-RPT-REC                     18670000
*            WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE       18680000
*            MOVE RP-EP-EARLY-RWDS-MSG TO RWD-REDM-EXTRCT-RPT-REC       18690000
*            WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE       18700000
073004       INITIALIZE RWD-REDM-EXTRCT-RPT-REC                         18710000
                                                                        18720000
             INITIALIZE PV-LINE-COUNT                                   18730000
             PERFORM 7000-WRITE-REPORT-HEADERS                          18740000
           END-IF.                                                      18750000
                                                                        18760000
           MOVE RP-EP-DETAIL-LINE TO RWD-REDM-EXTRCT-RPT-REC.           18770000
           WRITE RWD-REDM-EXTRCT-RPT-REC.                               18780000
           INITIALIZE RP-EP-DETAIL-LINE.                                18790000
                                                                        18800000
      *2900-EXIT.                                                       18810000
                                                                        18820000
      *----------------------------------------------------------------*18830000
      *  6000-DATE-ROUTINE.                                             18840000
      *  - PERFORMS A CALL TO DPKUT500, KOHLS CALENDAR DATE ROUTINE.    18850000
      *----------------------------------------------------------------*18860000
       6000-DATE-ROUTINE.                                               18870000
                                                                        18880000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    18890000
                                                   DPG54 DPG55 DPG56.   18900000
                                                                        18910000
           IF DPG54-SEVERE-ERROR OR DPG54-DATE-INVALID                  18920000
             MOVE '6000-DATE-ROUTINE'     TO PV-PARAGRAPH-NAME          18930000
             MOVE DPG54-ERROR-MESSAGE     TO PV-MSG-TEXT                18940000
             SET ABEND-4019-CAL-SUB-ERROR TO TRUE                       18950000
             PERFORM 9010-ABEND                                         18960000
           END-IF.                                                      18970000
                                                                        18980000
      *6000-EXIT.                                                       18990000
                                                                        19000000
      *----------------------------------------------------------------*19010000
      *  7000-WRITE-REPORT-HEADERS.                                     19020000
      *  - WRITES REPORT HEADERS OUT TO THE FLAT FILE AS A MEANS TO     19030000
      *    SEPARATE EACH STORE'S REWARD EVENTS INFORMATION.             19040000
      *----------------------------------------------------------------*19050000
       7000-WRITE-REPORT-HEADERS.                                       19060000
                                                                        19070000
           ADD +1 TO PV-PAGE-COUNT.                                     19080000
                                                                        19090000
      *    //PRINT THE KOHLS STD HEADER LINE                            19100000
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    19110000
           MOVE DP132O-STANDRD-HEADER-132-COLS                          19120000
                              TO RWD-REDM-EXTRCT-RPT-REC.               19130000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING PAGE.          19140000
                                                                        19150000
      *    //PRINT THE REPORT HEADER LINES                              19160000
           MOVE RP-EP-SYSTEM-ID-LINE TO RWD-REDM-EXTRCT-RPT-REC.        19170000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19180000
                                                                        19190000
           MOVE SPACES TO RWD-REDM-EXTRCT-RPT-REC.                      19200000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19210000
                                                                        19220000
           MOVE RP-EP-TITLE-LINE TO RWD-REDM-EXTRCT-RPT-REC.            19230000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19240000
                                                                        19250000
           MOVE SPACES TO RWD-REDM-EXTRCT-RPT-REC.                      19260000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19270000
                                                                        19280000
           MOVE RP-EP-STORE-GROUP-LINE TO RWD-REDM-EXTRCT-RPT-REC.      19290000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19300000
                                                                        19310000
           MOVE RP-EP-HEADER-LINE-A TO RWD-REDM-EXTRCT-RPT-REC.         19320000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19330000
                                                                        19340000
           MOVE RP-EP-HEADER-LINE-B TO RWD-REDM-EXTRCT-RPT-REC.         19350000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19360000
                                                                        19370000
           MOVE SPACES TO RWD-REDM-EXTRCT-RPT-REC.                      19380000
           WRITE RWD-REDM-EXTRCT-RPT-REC AFTER ADVANCING 1 LINE.        19390000
                                                                        19400000
           MOVE PC-LINES-FOR-HDRS TO PV-LINE-COUNT.                     19410000
                                                                        19420000
      *7000-EXIT.                                                       19430000
                                                                        19440000
      *----------------------------------------------------------------*19450000
      *  8000-WRITE-RWD-REDM-RECORD.                                    19460000
      *  - WRITE OUT THE VARIABLE LENGTH REWARD REDEMPTION RECORD.      19470000
      *----------------------------------------------------------------*19480000
       8000-WRITE-RWD-REDM-RECORD.                                      19490000
                                                                        19500000
092410*-----------------------------------------------------------------19510027
092410*     STORE RECORD LOGIC                                          19520027
092410*-----------------------------------------------------------------19530027
      *    //DETERMINE VARIABLE RECORD LENGTH                           19540000
           INITIALIZE PV-VAR-LNGTH.                                     19550000
           SET PS-LINE-END-NOT-FOUND TO TRUE.                           19560000
           PERFORM VARYING PV-VAR-LNGTH FROM 1 BY 1                     19570000
                   UNTIL ( PS-LINE-END-FOUND )                          19580000
            IF EP140-RWD-REDM-EXTR-VAR-TABLE (PV-VAR-LNGTH) EQUAL TO ';'19590000
                SET PS-LINE-END-FOUND TO TRUE                           19600000
                SUBTRACT 1 FROM PV-VAR-LNGTH                            19610000
            END-IF                                                      19620023
           END-PERFORM.                                                 19630000
                                                                        19640000
      *    //SUB-SELECT EXTRACT RECORD UP TO CALCULATED LENGTH          19650000
           INITIALIZE PV-WRITE-RECORD-LENGTH.                           19660000
           ADD PC-RWD-REDM-FIXED-LENGTH  TO PV-WRITE-RECORD-LENGTH.     19670000
           ADD PV-VAR-LNGTH              TO PV-WRITE-RECORD-LENGTH.     19680000
           MOVE PV-WRITE-RECORD-LENGTH   TO PV-RWD-RDM-REC-BYTE-CNT.    19690000
           MOVE EP140-RWD-REDM-EXTRCT-REC(1:PV-RWD-RDM-REC-BYTE-CNT)    19700000
                                         TO RWD-REDM-EXTRCT-RECORD.     19710000
                                                                        19720000
      *    //WRITE & CLEAR THE EXTRACT RECORD LAYOUT                    19730000
           WRITE RWD-REDM-EXTRCT-RECORD.                                19740000
           MOVE SPACES TO RWD-REDM-EXTRCT-RECORD.                       19750000
           INITIALIZE EP140-RWD-REDM-EXTRCT-REC.                        19760000
                                                                        19770000
      *    //INCREMENT RECORDS WRITTEN                                  19780000
           ADD 1 TO PV-RWD-STR-WRITES.                                  19790001
W36988     ADD 1 TO PV-RWD-EVNTS-COUNT.                                 19800002
                                                                        19810000
092410*-----------------------------------------------------------------19820027
092410*     ECOM RECORD LOGIC                                           19830027
092410*-----------------------------------------------------------------19840027
092410*    //DETERMINE VARIABLE RECORD LENGTH                           19850027
092410     INITIALIZE PV-VAR-LNGTH.                                     19860027
092410     SET PS-LINE-END-NOT-FOUND TO TRUE.                           19870027
092410     PERFORM VARYING PV-VAR-LNGTH FROM 1 BY 1                     19880027
092410             UNTIL ( PS-LINE-END-FOUND )                          19890027
092410      IF EP142-RWD-REDM-EXTR-VAR-TABLE (PV-VAR-LNGTH) EQUAL TO ';'19900027
092410          SET PS-LINE-END-FOUND TO TRUE                           19910027
092410          SUBTRACT 1 FROM PV-VAR-LNGTH                            19920027
092410       END-IF                                                     19930027
092410     END-PERFORM.                                                 19940027
                                                                        19950009
092410*   //SUB-SELECT EXTRACT RECORD UP TO CALCULATED LENGTH           19960027
092410     INITIALIZE PV-WRITE-RECORD-LENGTH.                           19970027
092410     ADD PC-RWD-REDM-FIX-LGTH-ECOM TO PV-WRITE-RECORD-LENGTH.     19980027
092410     ADD PV-VAR-LNGTH              TO PV-WRITE-RECORD-LENGTH.     19990027
092410     MOVE PV-WRITE-RECORD-LENGTH   TO PV-RWD-RDM-REC-BYTE-CNT-EC. 20000027
092410     MOVE EP142-RWD-REDM-EXTRCT-REC(1:PV-RWD-RDM-REC-BYTE-CNT-EC) 20010027
092410                                   TO RWD-REDM-EXTRCT-RECORD-ECOM.20020027
                                                                        20030009
092410*    //WRITE & CLEAR THE EXTRACT RECORD LAYOUT                    20040027
092410     WRITE RWD-REDM-EXTRCT-RECORD-ECOM.                           20050027
092410     MOVE SPACES TO RWD-REDM-EXTRCT-RECORD-ECOM.                  20060027
092410     INITIALIZE EP142-RWD-REDM-EXTRCT-REC.                        20070027
                                                                        20080009
092410*    //INCREMENT RECORDS WRITTEN                                  20090027
092410     ADD 1 TO PV-RWD-STR-WRITES-ECOM.                             20100027
                                                                        20110009
      *8000-EXIT.                                                       20120009
                                                                        20130000
      *----------------------------------------------------------------*20140000
      *  9010-ABEND                                                     20150000
      *  - ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                   20160000
      *----------------------------------------------------------------*20170000
       9010-ABEND.                                                      20180000
                                                                        20190000
           DISPLAY ' '.                                                 20200000
           DISPLAY '** ABEND           **'.                             20210000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          20220000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        20230000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       20240000
           DISPLAY '** MESSAGE         ** = ' PV-MSG-TEXT.              20250000
                                                                        20260000
           CALL 'ILBOABN0' USING AB-ABEND-CODE.                         20270000
                                                                        20280000
      *9010-EXIT.                                                       20290000
                                                                        20300000
      *----------------------------------------------------------------*20310000
      *  9020-SQL-ABEND-ROUTINE.                                        20320000
      *  - SQL ABEND ROUTINE.                                           20330000
      *----------------------------------------------------------------*20340000
       9020-SQL-ABEND-ROUTINE.                                          20350000
                                                                        20360000
           DISPLAY ' '.                                                 20370000
           DISPLAY '** ABEND     **'.                                   20380000
           DISPLAY '** PROGRAM   ** = ' PC-PROGRAM-NAME.                20390000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              20400000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               20410000
                                                                        20420000
           COPY DPPD004.                                                20430000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            20440000
           CALL 'ILBOABN0' USING AB-ABEND-CODE.                         20450000
                                                                        20460000
      *9020-EXIT.                                                       20470000
                                                                        20480000
      ***************************************************************** 20490032
      *   9900-ORA-ERROR.                                             * 20500032
      *     ORACLE ABEND ROUTINE.                                     * 20510032
      ***************************************************************** 20520032
       9900-ORA-ERROR.                                                  20530032
                                                                        20540032
           MOVE SQLCODE    TO SQLCODE2.                                 20550032
           MOVE SQLERRD(3) TO SQLERRD3.                                 20560032
           DISPLAY '** ABEND **       ** = SQLERROR'.                   20570032
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.           20580032
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.         20590032
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  20600032
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  20610032
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   20620032
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  20630032
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                20640032
           DISPLAY '** OPERATION ** = ' PV-ORA-OPERATION.               20650032
                                                                        20660032
           MOVE SPACES TO WS-MSG-TEXT.                                  20670032
      *    GET FULL TEXT OF ERROR MESSAGE                               20680032
                                                                        20690032
           CALL 'SQLGLM' USING WS-MSG-TEXT,                             20700032
                               WS-MAX-SIZE,                             20710032
                               WS-MSG-LENGTH.                           20720032
           DISPLAY 'MESSAGE TEXT      ** = ' WS-MSG-TEXT.               20730032
                                                                        20740032
           EXEC SQL                                                     20750032
                ROLLBACK WORK RELEASE                                   20760032
           END-EXEC.                                                    20770032
                                                                        20780032
           SET ABEND-4013-ORA-ERROR  TO TRUE.                           20790036
                                                                        20800032
           CALL 'ILBOABN0' USING AB-ABEND-CODE.                         20810032
                                                                        20820032
      *9900-EXIT.                                                       20830032
      *----------------------------------------------------------------*20840000
      *  9999-WRAPUP.                                                   20850000
      *  - END OF PROGRAM ROUTINE. CLOSES ALL FILES AND DISPLAYS        20860000
      *    ANY DATA FROM THE PROGRAM.                                   20870000
      *----------------------------------------------------------------*20880000
       9999-WRAPUP.                                                     20890000
                                                                        20900000
      *    //UPDATE THE PROGRAM LAST RUN TIMESTAMP CONTROL CARD         20910000
           CLOSE CTRL-CRD-PGM-LAST-RUN.                                 20920000
           OPEN OUTPUT CTRL-CRD-PGM-LAST-RUN.                           20930000
           MOVE PV-CUR-SYS-TMST TO EP149-CC-RWD-RDM-CUTOFF-TMST.        20940000
           WRITE CTRL-CRD-PGM-LAST-RUN-REC FROM EP149-CC-CONTROL-CARD.  20950000
                                                                        20960000
      *    //CLOSE ALL OPEN INPUT / OUTPUT FILES                        20970000
           CLOSE CTRL-CRD-PGM-LAST-RUN                                  20980000
                 CTRL-CRD-IDENT-TYP                                     20990000
                 CTRL-CRD-CUR-BTCH-DTE                                  21000000
                 CTRL-CRD-BTCH-FUTR-DTE                                 21010000
                 CTRL-CRD-ADV-OPN-DAYS                                  21020000
                 CONTROL-CARD-ORACLE-LOGIN                              21030031
                 RWD-REDM-EXTRCT-FLE                                    21040000
092410           RWD-REDM-EXTRCT-FLE-ECOM                               21050027
                 RWD-REDM-EXTRCT-RPT.                                   21060000
                                                                        21070002
W36988     IF PS-SET-EMAIL-RC                                           21080002
W36988        DISPLAY '** PASSED EMAIL THRESHOLD, SETTING RC04 **'      21090004
W36988        MOVE PC-EMAIL-RETURN-CODE TO RETURN-CODE                  21100002
W36988     END-IF                                                       21110002
                                                                        21120000
           DISPLAY ' '.                                                 21130000
           DISPLAY '*********************************************'.     21140000
           DISPLAY 'PROGRAM NAME: ' PC-PROGRAM-NAME.                    21150000
           DISPLAY '*********************************************'.     21160000
W36998     DISPLAY 'NUMBER OF STORES W/OK FILE CT: ' PV-STR-REC-CT-OK.  21170003
           DISPLAY 'NUMBER OF REWARD RECS WRITTEN: ' PV-RWD-STR-WRITES. 21180003
092410     DISPLAY 'NUMBER OF REWARD RECS WRITTEN ECOM: '               21190027
092410                                                PV-RWD-STR-WRITES.21200027
           DISPLAY '*********************************************'.     21210000
                                                                        21220000
      *9999-EXIT.                                                       21230000
                                                                        21240000
      *----------------------------------------------------------------*21250005
