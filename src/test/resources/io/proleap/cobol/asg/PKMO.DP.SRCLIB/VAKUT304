       IDENTIFICATION DIVISION.                                         00010000
W34517 PROGRAM-ID.    VAKUT304.                                         00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  09-01-2004.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
W34517****IMPORTANT*****************************************************00061000
W34517* THIS IS A CLONE OF THE INKUT304 CREATED FOR THE VA PROJECT     *00062000
W34517* THE MAJOR CHANGES ARE .....                                    *00063000
      ******************************************************************00064000
      ******************************************************************00070000
      *PURPOSE: AT A VENDOR LEVEL, THE PROGRAM PRORATES THE SALES BASE  00080000
      *         FILE SHRINK, SALES, AND ANY EST INV RETAIL DOLLARS WHICH00081000
      *         MIGHT RESIDE ON THE SALES BASE CUM FILE.                00082000
      *                                                                 00083000
      *         THE CURRENT TWKSHNK TABLE DOES NOT BREAK SALES/SHRINK AT00084000
      *         A VENDOR LEVEL. WE NEED TO PRORATE THE DOLLARS ACROSS   00085000
      *         THE DEPT, SUB CLASS, STORE, AND BRAND VENDOR KEY.       00086000
      *                                                                 00087000
      *         SALES/SHRINK TO BE PRORATED IS REPRESENTED BY AN BRAND  00088000
      *         VENDOR KEY EQUAL TO ZEROS.                              00089000
      *                                                                 00090000
W27710*         SALES/SHRINK WHERE TINSCLS OR MLT_MNLY_SUM TABLE ENTRIES00091000
W27710*         DO NOT EXIST HAVE BEEN SORT OUT IN STEP 120 OF INK410   00092000
W27710*         TO BE SORTED BACK IN AFTER THIS STEP. (INSR191 - VEND 0)00093000
W27710*                                                                 00094000
      *CHANGE HISTORY:                                                  00095000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00096000
      * ------- ---------- ----------- -------------------------------- 00097000
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVENTORY ADJUSTMENT    00098000
W20445*                                (CREATION...)                    00099000
W27710* W27710  01-29-2005 ROD JANSSEN MISSING ESTIMATED STORE SALES &  00100000
W27710*                                SHRINK DOLLARS FOR SALES BASE    00110000
W27710*                                REPORTING. WHY?                  00120000
W28582* W28582  01-24-2006 CLARK SCHWENN VENDOR/BRAND CHANGES - REPLACE 00130000
W28582* W28582  01-24-2006             ENT ID W/ LWST ID (DATALESS KEY) 00140000
W34517* W34517  10-14-2008 IN-VA INVENTORY STREAM SPLIT                 00141000
W33304* W33304  04-29-2009 AMANDA      COMMENT OUT LINE WHERE SAVE AND  00142001
W33304*                    JEFFORDS    SHNK VARIABLE ARE SET EQUAL.     00143001
      ******************************************************************00150000
                                                                        00160000
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.        IBM-3090.                                00190000
       OBJECT-COMPUTER.        IBM-3090.                                00200000
       INPUT-OUTPUT SECTION.                                            00210000
       FILE-CONTROL.                                                    00220000
                                                                        00230000
           SELECT SLS-CUM-FILE-BVKEY-IN                                 00240000
               ASSIGN TO INP01.                                         00250000
                                                                        00260000
           SELECT SLS-CUM-FILE-SHNK-IN                                  00270000
               ASSIGN TO INP02.                                         00280000
                                                                        00290000
           SELECT SLS-CUM-FILE-PRORATE-OUT                              00300000
               ASSIGN TO OUT01.                                         00310000
                                                                        00320000
       DATA DIVISION.                                                   00330000
       FILE SECTION.                                                    00340000
                                                                        00350000
       FD  SLS-CUM-FILE-BVKEY-IN                                        00360000
           LABEL RECORDS ARE OMITTED                                    00370000
           RECORDING MODE IS F                                          00380000
           BLOCK CONTAINS 0 RECORDS.                                    00390000
       01  SLS-CUM-REC-BVKEY-IN     PIC X(80).                          00400000
                                                                        00410000
       FD  SLS-CUM-FILE-SHNK-IN                                         00420000
           LABEL RECORDS ARE OMITTED                                    00430000
           RECORDING MODE IS F                                          00440000
           BLOCK CONTAINS 0 RECORDS.                                    00450000
       01  SLS-CUM-REC-SHNK-IN      PIC X(80).                          00460000
                                                                        00470000
       FD  SLS-CUM-FILE-PRORATE-OUT                                     00480000
           LABEL RECORDS ARE OMITTED                                    00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  SLS-CUM-REC-PRORATE-OUT  PIC X(80).                          00520000
                                                                        00530000
      /*****************************************************************00540000
       WORKING-STORAGE SECTION.                                         00550000
      ******************************************************************00560000
                                                                        00570000
       01  ABEND-CODE               PIC S9(04) VALUE ZERO COMP SYNC.    00580000
                                                                        00590000
       01  PC-PROGRAM-CONSTANTS.                                        00600000
W34517     05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'VAKUT304'.     00610000
           05  PC-TABLE-MAX            PIC S9(15) COMP-3 VALUE 9999.    00620000
                                                                        00630000
       01  PV-PROGRAM-VARIABLES.                                        00640000
           05  PV-BVKEY-NDX            PIC S9(15) COMP-3 VALUE ZEROS.   00650000
           05  PV-CUM-FILE-BVKEY-READ  PIC S9(15) COMP-3 VALUE ZEROS.   00660000
           05  PV-CUM-FILE-SHNK-READ   PIC S9(15) COMP-3 VALUE ZEROS.   00670000
           05  PV-WRITE-RECS           PIC S9(15) COMP-3 VALUE ZEROS.   00680000
           05  PV-PRORATE-WRITTEN      PIC S9(15) COMP-3 VALUE ZEROS.   00690000
           05  PV-DISPLAY-AMOUNT    PIC ZZZZZZZZZZZZZZ9  VALUE ZEROS.   00700000
           05  PV-SAVE-KEY.                                             00710000
               10  PV-DEPT-NBR-SAVE    PIC X(03)  VALUE ZEROS.          00720000
               10  PV-SUB-CL-NBR-SAVE  PIC X(02)  VALUE ZEROS.          00730000
               10  PV-STR-NBR-SAVE     PIC S9(04) COMP VALUE ZEROS.     00740000
W27710     05  PV-SHNK-KEY.                                             00750000
W27710         10  PV-DEPT-NBR-SHNK    PIC X(03)  VALUE ZEROS.          00760000
W27710         10  PV-SUB-CL-NBR-SHNK  PIC X(02)  VALUE ZEROS.          00770000
W27710         10  PV-STR-NBR-SHNK     PIC S9(04) COMP VALUE ZEROS.     00780000
           05  PV-OPN-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00790000
           05  PV-OPN-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00791000
           05  PV-CUM-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00792000
           05  PV-CUM-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00793000
           05  PV-NXT-SLS-RTL-AMT      PIC S9(11)V99 COMP-3 VALUE ZERO. 00794000
           05  PV-NXT-SHNK-RTL-AMT     PIC S9(11)V99 COMP-3 VALUE ZERO. 00795000
           05  PV-ESTIMATED-INV-ADJ-AMT PIC S9(11)V99 COMP-3 VALUE ZERO.00796000
           05  PV-OPN-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00797000
           05  PV-OPN-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00797100
           05  PV-CUM-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00797200
           05  PV-CUM-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00797300
           05  PV-NXT-SLS-RTL-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00797400
           05  PV-NXT-SHNK-RTL-AMT-ACC PIC S9(11)V99 COMP-3 VALUE ZERO. 00797500
           05  PV-EST-INV-ADJ-AMT-ACC  PIC S9(11)V99 COMP-3 VALUE ZERO. 00797600
           05  PV-WORK-AMOUNT          PIC S9(11)V99 COMP-3 VALUE ZERO. 00797700
           05  PV-BVKEY-PERCENT        PIC S9(04)V9(07)                 00797800
                                                     COMP-3 VALUE ZERO. 00797900
                                                                        00798000
       01  PS-PROGRAM-SWITCHES.                                         00799000
           05  PS-SLS-CUM-SHNK-DONE-SW   PIC X(01)   VALUE 'N'.         00800000
               88  PS-SLS-CUM-SHNK-DONE              VALUE 'Y'.         00810000
           05  PS-SLS-CUM-BVKEY-DONE-SW  PIC X(01)   VALUE 'N'.         00820000
               88  PS-SLS-CUM-BVKEY-DONE             VALUE 'Y'.         00830000
           05  PS-SHRINK-FOUND-SW        PIC X(01)   VALUE 'N'.         00840000
               88  PS-SHRINK-FOUND                   VALUE 'Y'.         00850000
           05  PS-WRITE-ADJUST-SW        PIC X(01)   VALUE 'N'.         00860000
               88  PS-WRITE-ADJUSTMENT               VALUE 'Y'.         00870000
           05  PS-FIRST-TIME-SW          PIC X(01)   VALUE 'N'.         00880000
               88  FIRST-TIME-THRU                   VALUE 'N'.         00890000
               88  CONTINUED-PROCESSING              VALUE 'Y'.         00900000
                                                                        00910000
       01  TB-BVKEY-TABLE.                                              00920000
           05 TB-BVKEY-ELEMENTS       OCCURS 9999 TIMES.                00930000
              10 TB-DEPT-NBR          PIC X(03) VALUE SPACES.           00940000
              10 TB-SUB-CL-NBR        PIC X(02) VALUE SPACES.           00950000
              10 TB-ML-LWST-ID        PIC S9(9) COMP VALUE ZEROS.       00960000
              10 TB-STR-NBR           PIC S9(4) COMP VALUE ZEROS.       00970000
              10 TB-OPN-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 00980000
              10 TB-OPN-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 00990000
              10 TB-CUM-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 01000000
              10 TB-CUM-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 01010000
              10 TB-NXT-SLS-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE ZEROS. 01020000
              10 TB-NXT-SHNK-RTL-AMT  PIC S9(11)V99 COMP-3 VALUE ZEROS. 01021000
              10 TB-ESTIMATED-INV-ADJ-AMT PIC S9(11)V99                 01022000
                                                    COMP-3 VALUE ZEROS. 01023000
                                                                        01024000
      /**************************************************************** 01025000
      * COPYBOOK - SALES BASE CUM FILE RECORD DESCRIPTION               01026000
      ******************************************************************01027000
           COPY INRD023.                                                01028000
                                                                        01029000
      /**************************************************************** 01030000
      * COPYBOOK - SHRINK LEVEL SALES BASE CUM FILE RECORD DESCRIPTION  01040000
      ***************************************************************** 01050000
           COPY INRD023 REPLACING IN023 BY INSHK.                       01060000
                                                                        01070000
      /**************************************************************** 01080000
      * COPYBOOK - PRORATED SALES BASE CUM FILE RECORD DESCRIPTION      01090000
      ***************************************************************** 01100000
           COPY INRD023 REPLACING IN023 BY PRRTE.                       01110000
                                                                        01120000
      /*****************************************************************01130000
      *FLOW:    THE INK410 JOB CREATES THE SALES BASE WORK FILES USING  01140000
      *         TABLES MLT_MNLY_SUM, TINSCLS, AND TWKSHNK. THE          01150000
      *         MLT_MNLY_SUM AND TINSCLS TABLES BREAK SALES AND SHRINK  01160000
      *         DOWN TO THE STORE/VENDOR LEVEL. TABLE TWKSHNK BREAKS    01170000
      *         SALES AND SHRINK DOWN TO THE SUB CLASS/STORE LEVEL.     01180000
      *                                                                 01190000
      *         SHRINK TABLE DOLLARS WILL HAVE ZEROS FOR AN B/V KEY.    01200000
      *         THESE ROWS WILL BE PRORATED AT A DEPT, SUB CLASS, STORE 01210000
      *         AND B/V KEY LEVEL USING THE SALES AND SHRINK DOLLARS    01220000
      *         FOUND IN THE SALES BASE VENDOR LEVEL FILE.              01230000
      *                                                                 01240000
      *         FORMULA:                                                01250000
      *        *TOTAL UP SALES AND SHRINK AT THE DEPT, SUB CLASS, STORE,01260000
      *         AND BRAND VENDOR KEY.                                   01270000
      *        *BREAK TIME, DEVELOP THE B/V KEY LEVEL PERCENT BASED ON  01280000
      *         THE B/V KEY SALES AND SHRINK DOLLARS DIVIDED BY THE     01290000
      *         TOTAL SALES AND SHRINK DOLLARS.                         01300000
      *        *APPLY THE PERCENT TO THE SHRINK TABLE DOLLARS FOR THE   01310000
      *         DEPT, SUB CLASS, AND STORE.                             01320000
      *        *ACCUMULATE THE ADJUSTED DOLLARS. ANY REMAINDER IS ADDED 01330000
      *         TO THE LAST B/V KEY FOR THE DEPT, SUB CLASS, STORE, AND 01340000
      *         BRAND VENDOR KEY.                                       01350000
      *                                                                 01360000
      *         INPUTS:                                                 01370000
      *         INP01 - CUM SALES BASE AT A VENDOR/BRAND LEVEL.         01380000
      *         INP02 - CUM SALES BASE AT THE SHRINK LEVEL (B/V KEY= 0) 01390000
      *         OUT01 - ALL VENDOR/BRAND LEVEL RECORDS AND ANY PRORATED 01400000
      *                 DOLLAR RECORDS.                                 01410000
      *                                                                 01420000
      ******************************************************************01430000
      ******************************************************************01440000
       PROCEDURE DIVISION.                                              01450000
      ******************************************************************01460000
                                                                        01470000
W34517 0000-MAINLINE-VAKUT304.                                          01480000
                                                                        01490000
           PERFORM 1000-INITIALIZATION.                                 01500000
                                                                        01510000
           PERFORM 2000-PRORATION-MAIN-LOOP                             01520000
               UNTIL PS-SLS-CUM-BVKEY-DONE.                             01530000
                                                                        01540000
           PERFORM 9000-END-OF-JOB-TASKS.                               01550000
                                                                        01560000
       9999-EXIT-PROGRAM.                                               01570000
           STOP RUN.                                                    01580000
                                                                        01590000
      /*****************************************************************01600000
      * 1000-INITIALIZATION.                                            01610000
      ******************************************************************01620000
       1000-INITIALIZATION.                                             01630000
                                                                        01640000
           DISPLAY '******************************************'.        01650000
W34517     DISPLAY '*  START OF VAKUT304                      '.        01660000
           DISPLAY '******************************************'.        01670000
           DISPLAY ' '.                                                 01680000
                                                                        01690000
           OPEN INPUT  SLS-CUM-FILE-BVKEY-IN                            01700000
                INPUT  SLS-CUM-FILE-SHNK-IN                             01710000
           OPEN OUTPUT SLS-CUM-FILE-PRORATE-OUT.                        01720000
                                                                        01730000
           INITIALIZE TB-BVKEY-TABLE.                                   01740000
W27710     MOVE ZEROS               TO PV-BVKEY-NDX.                    01750000
           MOVE ZEROS               TO PV-OPN-SLS-RTL-AMT.              01760000
           MOVE ZEROS               TO PV-OPN-SHNK-RTL-AMT.             01760100
           MOVE ZEROS               TO PV-CUM-SLS-RTL-AMT.              01760200
           MOVE ZEROS               TO PV-CUM-SHNK-RTL-AMT.             01760300
           MOVE ZEROS               TO PV-NXT-SLS-RTL-AMT.              01760400
           MOVE ZEROS               TO PV-NXT-SHNK-RTL-AMT.             01760500
           MOVE ZEROS               TO PV-ESTIMATED-INV-ADJ-AMT.        01760600
           MOVE 'N'                 TO PS-SLS-CUM-SHNK-DONE-SW.         01760700
           MOVE 'N'                 TO PS-SLS-CUM-BVKEY-DONE-SW.        01760800
           MOVE 'N'                 TO PS-SHRINK-FOUND-SW.              01760900
           MOVE 'N'                 TO PS-FIRST-TIME-SW.                01761000
                                                                        01762000
           PERFORM 7000-READ-CUM-FILE-BVKEY.                            01763000
                                                                        01764000
           PERFORM 7100-READ-CUM-FILE-SHNK.                             01765000
                                                                        01766000
      /*****************************************************************01767000
      * 2000-PRORATION-MAIN-LOOP.                                       01768000
      *     LOOP THROUGH THE VEN/BR LEVEL SALES BASE FILE TO ACCUMULATE 01769000
      *   TOTAL SALES RETAIL, TOTAL SHRINK RETAIL, AND ESTIMATED        01770000
      *   INVENTORY RETAIL AMOUNTS AT THE DEPARTMENT, SUB CLASS, STORE, 01780000
      *   AND VEN/BR LEVEL.                                             01790000
      *     ADD THE VEN/BR LEVEL SALES BASE DOLLARS TO A TABLE TO BE    01800000
      *   USED IN THE PRORATION PROCESS.                                01810000
      *     AT A BREAK, PRORATE THE SHRINK RETAIL FOUND ON THE SHRINK   01820000
      *   LEVEL SALES BASE FILE TO THE VEN/BRS FOR THE DEPARTMENT, SUB  01830000
      *   CLASS, STORE, AND VEN/BR.                                     01840000
      *     RESET FOR THE NEXT BREAK.                                   01850000
      *     WRITE ANY VEN/BR LEVEL SALES BASE TO THE OUTPUT FILE.       01860000
      *     READ THE NEXT VEN/BR LEVEL SALES BASE RECORD.               01870000
      ******************************************************************01880000
       2000-PRORATION-MAIN-LOOP.                                        01890000
                                                                        01900000
           IF FIRST-TIME-THRU                                           01910000
               MOVE IN023-DEPT-NBR   TO PV-DEPT-NBR-SAVE                01920000
               MOVE IN023-SUB-CL-NBR TO PV-SUB-CL-NBR-SAVE              01930000
               MOVE IN023-STR-NBR    TO PV-STR-NBR-SAVE                 01940000
W33304*        COMMENTED LINE BELOW                                     01941001
W27710*        MOVE PV-SAVE-KEY      TO PV-SHNK-KEY                     01950001
               MOVE 'Y'              TO PS-FIRST-TIME-SW                01960000
           END-IF.                                                      01970000
                                                                        01971000
           IF IN023-DEPT-NBR EQUAL PV-DEPT-NBR-SAVE     AND             01972000
              IN023-SUB-CL-NBR EQUAL PV-SUB-CL-NBR-SAVE AND             01973000
              IN023-STR-NBR EQUAL PV-STR-NBR-SAVE                       01974000
               PERFORM 3000-ACCUM-SLS-CUM-BVKEY                         01975000
                                                                        01976000
               MOVE IN023-SALES-BASE-CUM-REC                            01977000
                                   TO PRRTE-SALES-BASE-CUM-REC          01978000
               PERFORM 7200-WRITE-EXTRACT-REC                           01979000
               ADD 1               TO PV-WRITE-RECS                     01980000
                                                                        01990000
           ELSE                                                         02000000
               MOVE 'N'            TO PS-SHRINK-FOUND-SW                02010000
               PERFORM 3100-PRORATE-SHRINK                              02020000
                   UNTIL PS-SHRINK-FOUND                                02030000
                                                                        02030100
               INITIALIZE TB-BVKEY-TABLE                                02030200
W27710         MOVE ZEROS          TO PV-BVKEY-NDX                      02030300
               MOVE ZEROS          TO PV-OPN-SLS-RTL-AMT                02030400
               MOVE ZEROS          TO PV-OPN-SHNK-RTL-AMT               02030500
               MOVE ZEROS          TO PV-CUM-SLS-RTL-AMT                02030600
               MOVE ZEROS          TO PV-CUM-SHNK-RTL-AMT               02030700
               MOVE ZEROS          TO PV-NXT-SLS-RTL-AMT                02030800
               MOVE ZEROS          TO PV-NXT-SHNK-RTL-AMT               02030900
               MOVE ZEROS          TO PV-ESTIMATED-INV-ADJ-AMT          02031000
                                                                        02031100
               PERFORM 3000-ACCUM-SLS-CUM-BVKEY                         02031200
                                                                        02031300
               MOVE IN023-SALES-BASE-CUM-REC                            02031400
                                   TO PRRTE-SALES-BASE-CUM-REC          02031500
               PERFORM 7200-WRITE-EXTRACT-REC                           02031600
               ADD 1               TO PV-WRITE-RECS                     02031700
                                                                        02031800
               MOVE IN023-DEPT-NBR   TO PV-DEPT-NBR-SAVE                02031900
               MOVE IN023-SUB-CL-NBR TO PV-SUB-CL-NBR-SAVE              02032000
               MOVE IN023-STR-NBR    TO PV-STR-NBR-SAVE                 02033000
                                                                        02034000
           END-IF.                                                      02035000
                                                                        02036000
           PERFORM 7000-READ-CUM-FILE-BVKEY.                            02037000
                                                                        02038000
      /**************************************************************** 02039000
      * 3000-ACCUM-SLS-CUM-BVKEY.                                       02040000
      *     ACCUMULATE TOTAL SALES RETAIL, TOTAL SHRINK RETAIL, AND     02050000
      *   ESTIMATED INVENTORY RETAIL AMOUNTS AT THE DEPARTMENT,         02060000
      *   STORE, AND VEN/BR LEVEL.                                      02070000
      *     MOVE ALL VEN/BR LEVEL SALES BASE INFORMATION TO A WORKING   02080000
      *   STORAGE VEN/BR TABLE. THE VEN/BR TABLE WILL BE USED IN THE    02090000
      *   PRORATION PROCESS.                                            02100000
      ***************************************************************** 02110000
       3000-ACCUM-SLS-CUM-BVKEY.                                        02120000
                                                                        02130000
W27710**** MOVE ZEROS                  TO PV-BVKEY-NDX.                 02140000
           PERFORM 4100-SAVE-VENDOR-INFORMATION.                        02150000
                                                                        02160000
           ADD IN023-OPN-SLS-RTL-AMT   TO PV-OPN-SLS-RTL-AMT.           02170000
           ADD IN023-OPN-SHNK-RTL-AMT  TO PV-OPN-SHNK-RTL-AMT.          02180000
           ADD IN023-CUM-SLS-RTL-AMT   TO PV-CUM-SLS-RTL-AMT.           02190000
           ADD IN023-CUM-SHNK-RTL-AMT  TO PV-CUM-SHNK-RTL-AMT.          02200000
           ADD IN023-NXT-SLS-RTL-AMT   TO PV-NXT-SLS-RTL-AMT.           02210000
           ADD IN023-NXT-SHNK-RTL-AMT  TO PV-NXT-SHNK-RTL-AMT.          02220000
           ADD IN023-ESTIMATED-INV-ADJ-AMT                              02230000
                                       TO PV-ESTIMATED-INV-ADJ-AMT.     02240000
                                                                        02250000
      ***************************************************************** 02260000
      * 3100-PRORATE-SHRINK.                                            02270000
      *     USE THE WORKING STORAGE VENDOR TABLE WHICH CONTAINS THE     02270100
      * VENDOR LEVEL SALES BASE DOLLARS FOR THE DEPARTMENT, SUB CLASS,  02270200
      * STORE, AND VENDOR TO DEVELOP THE PRORATED SHRINK LEVEL SALES    02270300
      * BASE DOLLARS.                                                   02270400
      *     READ THE NEXT SHRINK LEVEL SALES BASE RECORD.               02270500
      *                                                                 02270600
      * ** NOTE - WE ONLY PROCESS SHRINK LEVEL SALES BASE IN THIS       02270700
      *    MODULE WHERE A MATCH EXISTS AT A DEPARTMENT, SUB CLASS, AND  02270800
      *    STORE LEVEL. WE HAVE STRIPPED OUT THE NON-MATCH SHRINK LEVEL 02270900
      *    SALES BASE USING "SORT" OUTSIDE THIS COBOL PROGRAM. THE      02271000
      *    STRIPPED OUT DOLLARS ARE INCLUDED IN THE NEXT STEP OF THE    02271100
      *    EXECUTING JOB.                                               02271200
      ***************************************************************** 02271300
       3100-PRORATE-SHRINK.                                             02271400
                                                                        02271500
      *** PRORATE ANY SALES AND SHRINK DOLLARS....                      02271600
W27710     IF PV-SHNK-KEY IS EQUAL TO PV-SAVE-KEY                       02271700
               MOVE ZEROS         TO PV-BVKEY-NDX                       02271800
               MOVE ZEROS         TO PV-OPN-SLS-RTL-AMT-ACC             02271900
               MOVE ZEROS         TO PV-OPN-SHNK-RTL-AMT-ACC            02272000
               MOVE ZEROS         TO PV-CUM-SLS-RTL-AMT-ACC             02273000
               MOVE ZEROS         TO PV-CUM-SHNK-RTL-AMT-ACC            02274000
               MOVE ZEROS         TO PV-NXT-SLS-RTL-AMT-ACC             02275000
               MOVE ZEROS         TO PV-NXT-SHNK-RTL-AMT-ACC            02276000
               MOVE ZEROS         TO PV-EST-INV-ADJ-AMT-ACC             02277000
               PERFORM 4000-CALC-SALES-BASE                             02278000
                 VARYING PV-BVKEY-NDX FROM 1 BY 1                       02279000
                   UNTIL PV-BVKEY-NDX GREATER PC-TABLE-MAX              02280000
               PERFORM 5100-APPLY-ANY-REMAINDERS                        02281000
               MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02282000
W27710     ELSE                                                         02283000
W27710*** A CUM FILE SHRINK RECORD DOES NOT EXIST FOR THE SAVE KEY      02284000
W27710     IF PV-SHNK-KEY IS GREATER THAN PV-SAVE-KEY                   02285000
W27710         MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02286000
W27710     ELSE                                                         02286100
W27710*** END OF FILE - CUM FILE SHRINK...                              02286200
W27710     IF PS-SLS-CUM-SHNK-DONE                                      02286300
W27710         MOVE 'Y'           TO PS-SHRINK-FOUND-SW                 02286400
W27710     ELSE                                                         02286500
W27710*** READ THE CUM FILE SHRINK FOR THE NEXT RECORD.                 02286600
W27710     IF PV-SHNK-KEY IS LESS THAN PV-SAVE-KEY                      02286700
               PERFORM 7100-READ-CUM-FILE-SHNK                          02286800
               MOVE INSHK-DEPT-NBR   TO PV-DEPT-NBR-SHNK                02286900
               MOVE INSHK-SUB-CL-NBR TO PV-SUB-CL-NBR-SHNK              02287000
               MOVE INSHK-STR-NBR    TO PV-STR-NBR-SHNK                 02287100
           END-IF.                                                      02287200
                                                                        02287300
      /**************************************************************** 02287400
      * 4000-CALC-SALES-BASE.                                           02287500
      ***************************************************************** 02287600
       4000-CALC-SALES-BASE.                                            02287700
                                                                        02287800
           IF TB-DEPT-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES   AND         02287900
              TB-SUB-CL-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES AND         02288000
              TB-ML-LWST-ID (PV-BVKEY-NDX) NOT EQUAL ZEROS  AND         02289000
              TB-STR-NBR (PV-BVKEY-NDX) NOT EQUAL ZEROS                 02290000
               PERFORM 5000-CALC-SHRINK-SLS-BASE                        02300000
           ELSE                                                         02310000
               MOVE 999999          TO PV-BVKEY-NDX                     02320000
           END-IF.                                                      02330000
                                                                        02340000
      ***************************************************************** 02350000
      * 4100-SAVE-VENDOR-INFORMATION.                                   02360000
      ***************************************************************** 02370000
       4100-SAVE-VENDOR-INFORMATION.                                    02380000
                                                                        02390000
           ADD 1                    TO PV-BVKEY-NDX.                    02400000
                                                                        02410000
      *** MAXIMUM ENTIES IN THE TABLE? ABEND THE PROGRAM....            02420000
           IF PV-BVKEY-NDX IS GREATER THAN PC-TABLE-MAX                 02420100
               MOVE +4013           TO ABEND-CODE                       02420200
               DISPLAY '** ABEND **'                                    02420300
W34517         DISPLAY '** PROGRAM   =  VAKUT304'                       02420400
               DISPLAY '** PARAGRAPH = 4100-SAVE-VENDOR-INFO'           02420500
               DISPLAY '** OPERATION = WORK STOR TABLE LOAD'            02420600
               CALL 'ILBOABN0'      USING ABEND-CODE                    02420700
           END-IF.                                                      02420800
                                                                        02420900
      *** LOOK FOR AN EMPTY ROW IN THE TABLE....                        02421000
           IF TB-DEPT-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES   AND         02422000
              TB-SUB-CL-NBR (PV-BVKEY-NDX) NOT EQUAL SPACES AND         02422100
              TB-ML-LWST-ID (PV-BVKEY-NDX) NOT EQUAL ZEROS  AND         02422200
              TB-STR-NBR (PV-BVKEY-NDX) NOT EQUAL ZEROS                 02422300
               GO TO 4100-SAVE-VENDOR-INFORMATION.                      02422400
                                                                        02422500
      *** ADD THE RECORD TO THE TABLE....                               02422600
           MOVE IN023-DEPT-NBR   TO TB-DEPT-NBR (PV-BVKEY-NDX).         02422700
           MOVE IN023-SUB-CL-NBR TO TB-SUB-CL-NBR (PV-BVKEY-NDX).       02422800
           MOVE IN023-ML-LWST-ID TO TB-ML-LWST-ID (PV-BVKEY-NDX).       02422900
           MOVE IN023-STR-NBR    TO TB-STR-NBR (PV-BVKEY-NDX).          02423000
           MOVE IN023-OPN-SLS-RTL-AMT                                   02424000
                           TO TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX).        02425000
           MOVE IN023-OPN-SHNK-RTL-AMT                                  02426000
                           TO TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX).       02427000
           MOVE IN023-CUM-SLS-RTL-AMT                                   02428000
                           TO TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX).        02429000
           MOVE IN023-CUM-SHNK-RTL-AMT                                  02430000
                           TO TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX).       02440000
           MOVE IN023-NXT-SLS-RTL-AMT                                   02450000
                           TO TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX).        02460000
           MOVE IN023-NXT-SHNK-RTL-AMT                                  02470000
                           TO TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX).       02480000
           MOVE IN023-ESTIMATED-INV-ADJ-AMT                             02490000
                        TO TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX).     02500000
                                                                        02510000
      /**************************************************************** 02520000
      * 5000-CALC-SHRINK-SLS-BASE.                                      02530000
      ***************************************************************** 02540000
       5000-CALC-SHRINK-SLS-BASE.                                       02550000
                                                                        02560000
           INITIALIZE PRRTE-SALES-BASE-CUM-REC.                         02570000
                                                                        02580000
           MOVE TB-DEPT-NBR (PV-BVKEY-NDX)  TO PRRTE-DEPT-NBR.          02590000
           MOVE TB-SUB-CL-NBR (PV-BVKEY-NDX)TO PRRTE-SUB-CL-NBR.        02600000
           MOVE TB-ML-LWST-ID (PV-BVKEY-NDX) TO PRRTE-ML-LWST-ID.       02610000
           MOVE TB-STR-NBR (PV-BVKEY-NDX)   TO PRRTE-STR-NBR.           02620000
                                                                        02630000
      *** PRORATE OPEN SALES RETAIL AMOUNT.....                         02640000
           IF TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02650000
              PV-OPN-SLS-RTL-AMT EQUAL ZEROS OR                         02660000
W27710        INSHK-OPN-SLS-RTL-AMT EQUAL ZEROS                         02670000
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02680000
           ELSE                                                         02690000
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02700000
                                  = TB-OPN-SLS-RTL-AMT (PV-BVKEY-NDX)   02710000
                                  / PV-OPN-SLS-RTL-AMT                  02720000
           END-IF.                                                      02730000
                                                                        02740000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02750000
                                          * INSHK-OPN-SLS-RTL-AMT.      02760000
           ADD PV-WORK-AMOUNT     TO PV-OPN-SLS-RTL-AMT-ACC.            02770000
           MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SLS-RTL-AMT.             02780000
                                                                        02790000
      *** PRORATE OPEN SHRINK RETAIL AMOUNT.....                        02800000
           IF TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02810000
              PV-OPN-SHNK-RTL-AMT EQUAL ZEROS  OR                       02820000
W27710        INSHK-OPN-SHNK-RTL-AMT EQUAL ZEROS                        02830000
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02840000
           ELSE                                                         02850000
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02860000
                                  = TB-OPN-SHNK-RTL-AMT (PV-BVKEY-NDX)  02861000
                                  / PV-OPN-SHNK-RTL-AMT                 02862000
           END-IF.                                                      02863000
                                                                        02864000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02865000
                                          * INSHK-OPN-SHNK-RTL-AMT.     02866000
           ADD PV-WORK-AMOUNT     TO PV-OPN-SHNK-RTL-AMT-ACC.           02867000
           MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SHNK-RTL-AMT.            02868000
                                                                        02869000
      *** PRORATE CUMM SALES RETAIL AMOUNT.....                         02870000
           IF TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02870100
              PV-CUM-SLS-RTL-AMT EQUAL ZEROS OR                         02870200
W27710        INSHK-CUM-SLS-RTL-AMT EQUAL ZEROS                         02870300
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02870400
           ELSE                                                         02870500
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02870600
                                  = TB-CUM-SLS-RTL-AMT (PV-BVKEY-NDX)   02870700
                                  / PV-CUM-SLS-RTL-AMT                  02870800
           END-IF.                                                      02870900
                                                                        02871000
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02871100
                                          * INSHK-CUM-SLS-RTL-AMT.      02871200
           ADD PV-WORK-AMOUNT     TO PV-CUM-SLS-RTL-AMT-ACC.            02871300
           MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SLS-RTL-AMT.             02871400
                                                                        02871500
      *** PRORATE CUMM SHRINK RETAIL AMOUNT.....                        02871600
           IF TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02871700
              PV-CUM-SHNK-RTL-AMT EQUAL ZEROS OR                        02871800
W27710        INSHK-CUM-SHNK-RTL-AMT EQUAL ZEROS                        02871900
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02872000
           ELSE                                                         02872100
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02872200
                                  = TB-CUM-SHNK-RTL-AMT (PV-BVKEY-NDX)  02872300
                                  / PV-CUM-SHNK-RTL-AMT                 02872400
           END-IF.                                                      02872500
                                                                        02872600
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02872700
                                          * INSHK-CUM-SHNK-RTL-AMT.     02872800
           ADD PV-WORK-AMOUNT     TO PV-CUM-SHNK-RTL-AMT-ACC.           02872900
           MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SHNK-RTL-AMT.            02873000
                                                                        02873100
      *** PRORATE NEXT SALES RETAIL AMOUNT.....                         02873200
           IF TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR          02873300
              PV-NXT-SLS-RTL-AMT EQUAL ZEROS OR                         02873400
W27710        INSHK-NXT-SLS-RTL-AMT EQUAL ZEROS                         02873500
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02873600
           ELSE                                                         02873700
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02873800
                                  = TB-NXT-SLS-RTL-AMT (PV-BVKEY-NDX)   02873900
                                  / PV-NXT-SLS-RTL-AMT                  02874000
           END-IF.                                                      02874100
                                                                        02874200
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02874300
                                          * INSHK-NXT-SLS-RTL-AMT.      02874400
           ADD PV-WORK-AMOUNT     TO PV-NXT-SLS-RTL-AMT-ACC.            02874500
           MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SLS-RTL-AMT.             02874600
                                                                        02874700
      *** PRORATE NEXT SHRINK RETAIL AMOUNT.....                        02874800
           IF TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR         02874900
              PV-NXT-SHNK-RTL-AMT EQUAL ZEROS OR                        02875000
W27710        INSHK-NXT-SHNK-RTL-AMT EQUAL ZEROS                        02875100
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02875200
           ELSE                                                         02875300
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02875400
                                  = TB-NXT-SHNK-RTL-AMT (PV-BVKEY-NDX)  02875500
                                  / PV-NXT-SHNK-RTL-AMT                 02875600
           END-IF.                                                      02875700
                                                                        02875800
           COMPUTE PV-WORK-AMOUNT ROUNDED = PV-BVKEY-PERCENT            02875900
                                          * INSHK-NXT-SHNK-RTL-AMT.     02876000
           ADD PV-WORK-AMOUNT     TO PV-NXT-SHNK-RTL-AMT-ACC.           02876100
           MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SHNK-RTL-AMT.            02876200
                                                                        02876300
      *** PRORATE ESTIMATED INVENTORY ADJUSTMENT.....                   02876400
           IF TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX) EQUAL ZEROS OR    02876500
              PV-ESTIMATED-INV-ADJ-AMT EQUAL ZEROS OR                   02876600
W27710        INSHK-ESTIMATED-INV-ADJ-AMT EQUAL ZEROS                   02876700
               MOVE ZEROS         TO PV-BVKEY-PERCENT                   02876800
           ELSE                                                         02876900
               COMPUTE PV-BVKEY-PERCENT ROUNDED                         02877000
                             = TB-ESTIMATED-INV-ADJ-AMT (PV-BVKEY-NDX)  02877100
                             / PV-ESTIMATED-INV-ADJ-AMT                 02877200
           END-IF.                                                      02877300
                                                                        02877400
           COMPUTE PV-WORK-AMOUNT ROUNDED                               02877500
                                  = PV-BVKEY-PERCENT                    02877600
                                  * INSHK-ESTIMATED-INV-ADJ-AMT.        02877700
           ADD PV-WORK-AMOUNT     TO PV-EST-INV-ADJ-AMT-ACC             02877800
           MOVE PV-WORK-AMOUNT    TO PRRTE-ESTIMATED-INV-ADJ-AMT.       02877900
                                                                        02878000
      *** WRITE THE PRORATE SALES/SHRINK RECORD...                      02878100
           PERFORM 7200-WRITE-EXTRACT-REC.                              02878200
           ADD 1                    TO PV-WRITE-RECS.                   02878300
           ADD 1                    TO PV-PRORATE-WRITTEN.              02878400
                                                                        02878500
      ***************************************************************** 02878600
      * 5100-APPLY-ANY-REMAINDERS.                                      02878700
      ***************************************************************** 02878800
       5100-APPLY-ANY-REMAINDERS.                                       02878900
                                                                        02879000
           MOVE 'N'               TO PS-WRITE-ADJUST-SW.                02879100
                                                                        02879200
      *** APPLY ANY REMAINING OPEN SALES RETAIL AMOUNT.....             02879300
           IF PV-OPN-SLS-RTL-AMT-ACC EQUAL INSHK-OPN-SLS-RTL-AMT        02879400
               MOVE ZEROS             TO PRRTE-OPN-SLS-RTL-AMT          02879500
           ELSE                                                         02879600
               COMPUTE PV-WORK-AMOUNT = INSHK-OPN-SLS-RTL-AMT           02879700
                                      - PV-OPN-SLS-RTL-AMT-ACC          02879800
W27710*        COMPUTE PV-WORK-AMOUNT = PV-OPN-SLS-RTL-AMT-ACC          02879900
W27710*                               + PV-WORK-AMOUNT                  02880000
               MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SLS-RTL-AMT          02881000
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02882000
           END-IF.                                                      02883000
                                                                        02884000
      *** APPLY ANY REMAINING OPEN SHRINK RETAIL AMOUNT.....            02885000
           IF PV-OPN-SHNK-RTL-AMT-ACC EQUAL INSHK-OPN-SHNK-RTL-AMT      02885100
               MOVE ZEROS             TO PRRTE-OPN-SHNK-RTL-AMT         02885200
           ELSE                                                         02885300
               COMPUTE PV-WORK-AMOUNT = INSHK-OPN-SHNK-RTL-AMT          02885400
                                      - PV-OPN-SHNK-RTL-AMT-ACC         02885500
W27710*        COMPUTE PV-WORK-AMOUNT = PV-OPN-SHNK-RTL-AMT-ACC         02885600
W27710*                               + PV-WORK-AMOUNT                  02885700
               MOVE PV-WORK-AMOUNT    TO PRRTE-OPN-SHNK-RTL-AMT         02885800
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02885900
           END-IF.                                                      02886000
                                                                        02886100
      *** APPLY ANY REMAINING CUMM SALES RETAIL AMOUNT.....             02886200
           IF PV-CUM-SLS-RTL-AMT-ACC EQUAL INSHK-CUM-SLS-RTL-AMT        02886300
               MOVE ZEROS             TO PRRTE-CUM-SLS-RTL-AMT          02886400
           ELSE                                                         02886500
               COMPUTE PV-WORK-AMOUNT = INSHK-CUM-SLS-RTL-AMT           02886600
                                      - PV-CUM-SLS-RTL-AMT-ACC          02886700
W27710*        COMPUTE PV-WORK-AMOUNT = PV-CUM-SLS-RTL-AMT-ACC          02886800
W27710*                               + PV-WORK-AMOUNT                  02886900
               MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SLS-RTL-AMT          02887000
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02887100
           END-IF.                                                      02887200
                                                                        02887300
      *** APPLY ANY REMAINING CUMM SHRINK RETAIL AMOUNT.....            02887400
           IF PV-CUM-SHNK-RTL-AMT-ACC EQUAL INSHK-CUM-SHNK-RTL-AMT      02887500
               MOVE ZEROS             TO PRRTE-CUM-SHNK-RTL-AMT         02887600
           ELSE                                                         02887700
               COMPUTE PV-WORK-AMOUNT = INSHK-CUM-SHNK-RTL-AMT          02887800
                                      - PV-CUM-SHNK-RTL-AMT-ACC         02887900
W27710*        COMPUTE PV-WORK-AMOUNT = PV-CUM-SHNK-RTL-AMT-ACC         02888000
W27710*                               + PV-WORK-AMOUNT                  02889000
               MOVE PV-WORK-AMOUNT    TO PRRTE-CUM-SHNK-RTL-AMT         02890000
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02890100
           END-IF.                                                      02890200
                                                                        02890300
      *** APPLY ANY REMAINING NEXT SALES RETAIL AMOUNT.....             02890400
           IF PV-NXT-SLS-RTL-AMT-ACC EQUAL INSHK-NXT-SLS-RTL-AMT        02890500
               MOVE ZEROS             TO PRRTE-NXT-SLS-RTL-AMT          02890600
           ELSE                                                         02890700
               COMPUTE PV-WORK-AMOUNT = INSHK-NXT-SLS-RTL-AMT           02890800
                                      - PV-NXT-SLS-RTL-AMT-ACC          02890900
W27710*        COMPUTE PV-WORK-AMOUNT = PV-NXT-SLS-RTL-AMT-ACC          02891000
W27710*                               + PV-WORK-AMOUNT                  02892000
               MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SLS-RTL-AMT          02892100
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02892200
           END-IF.                                                      02892300
                                                                        02892400
      *** APPLY ANY REMAINING NEXT SHRINK RETAIL AMOUNT.....            02892500
           IF PV-NXT-SHNK-RTL-AMT-ACC EQUAL INSHK-NXT-SHNK-RTL-AMT      02892600
               MOVE ZEROS             TO PRRTE-NXT-SHNK-RTL-AMT         02892700
           ELSE                                                         02892800
               COMPUTE PV-WORK-AMOUNT = INSHK-NXT-SHNK-RTL-AMT          02892900
                                      - PV-NXT-SHNK-RTL-AMT-ACC         02893000
W27710*        COMPUTE PV-WORK-AMOUNT = PV-NXT-SHNK-RTL-AMT-ACC         02894000
W27710*                               + PV-WORK-AMOUNT                  02895000
               MOVE PV-WORK-AMOUNT    TO PRRTE-NXT-SHNK-RTL-AMT         02895100
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02895200
           END-IF.                                                      02895300
                                                                        02895400
      *** APPLY ANY REMAINING ESTIMATED INVENTORY ADJUSTMENTS....       02895500
           IF PV-EST-INV-ADJ-AMT-ACC EQUAL INSHK-ESTIMATED-INV-ADJ-AMT  02895600
               MOVE ZEROS             TO PRRTE-ESTIMATED-INV-ADJ-AMT    02895700
           ELSE                                                         02895800
               COMPUTE PV-WORK-AMOUNT = INSHK-ESTIMATED-INV-ADJ-AMT     02895900
                                      - PV-EST-INV-ADJ-AMT-ACC          02896000
W27710*        COMPUTE PV-WORK-AMOUNT = PV-EST-INV-ADJ-AMT-ACC          02897000
W27710*                               + PV-WORK-AMOUNT                  02897100
               MOVE PV-WORK-AMOUNT    TO PRRTE-ESTIMATED-INV-ADJ-AMT    02897200
               MOVE 'Y'               TO PS-WRITE-ADJUST-SW             02897300
           END-IF.                                                      02897400
                                                                        02897500
      *** WRITE THE PRORATE SALES/SHRINK RECORD...                      02897600
           IF PS-WRITE-ADJUSTMENT                                       02897700
               PERFORM 7200-WRITE-EXTRACT-REC                           02897800
               ADD 1                TO PV-WRITE-RECS                    02897900
               ADD 1                TO PV-PRORATE-WRITTEN               02898000
           END-IF.                                                      02899000
                                                                        02900000
      /**************************************************************** 02900100
      * 7000-READ-CUM-FILE-BVKEY. READ THE VENDOR LVL SALES CUM FILE.   02900200
      ***************************************************************** 02900300
       7000-READ-CUM-FILE-BVKEY.                                        02900400
                                                                        02900500
           READ SLS-CUM-FILE-BVKEY-IN NEXT                              02900600
             INTO IN023-SALES-BASE-CUM-REC                              02900700
               AT END  MOVE 'Y' TO PS-SLS-CUM-BVKEY-DONE-SW             02900800
               NOT AT END  ADD 1 TO PV-CUM-FILE-BVKEY-READ              02900900
           END-READ.                                                    02901000
                                                                        02901100
      ***************************************************************** 02901200
      * 7100-READ-CUM-FILE-SHNK. READ THE SHRINK LVL SALES CUM FILE.    02901300
      ***************************************************************** 02901400
       7100-READ-CUM-FILE-SHNK.                                         02901500
                                                                        02901600
           READ SLS-CUM-FILE-SHNK-IN NEXT                               02901700
             INTO INSHK-SALES-BASE-CUM-REC                              02901800
W27710         AT END  INITIALIZE INSHK-SALES-BASE-CUM-REC              02901900
                       MOVE 'Y' TO PS-SLS-CUM-SHNK-DONE-SW              02902000
               NOT AT END  ADD 1 TO PV-CUM-FILE-SHNK-READ               02902100
           END-READ.                                                    02902200
                                                                        02902300
      /*****************************************************************02902400
      * 7200-WRITE-EXTRACT-REC.                                         02902500
      ******************************************************************02902600
       7200-WRITE-EXTRACT-REC.                                          02902700
                                                                        02902800
           WRITE SLS-CUM-REC-PRORATE-OUT FROM PRRTE-SALES-BASE-CUM-REC. 02902900
                                                                        02903000
      /*****************************************************************02904000
      * 9000-END-OF-JOB-TASKS. FINAL PROCESSING FOR THE PROGRAM.        02905000
      ******************************************************************02906000
       9000-END-OF-JOB-TASKS.                                           02907000
                                                                        02908000
           IF PV-CUM-FILE-BVKEY-READ IS GREATER THAN ZEROS              02909000
               MOVE 'N'              TO PS-SHRINK-FOUND-SW              02910000
               PERFORM 3100-PRORATE-SHRINK                              02920000
                   UNTIL PS-SHRINK-FOUND                                02930000
           END-IF.                                                      02940000
                                                                        02950000
           DISPLAY '******************************************'.        02960000
           DISPLAY '** RECORD COUNT SUMMARY **'.                        02970000
                                                                        02980000
           MOVE PV-CUM-FILE-BVKEY-READ  TO PV-DISPLAY-AMOUNT.           02990000
           DISPLAY '* READ SALES BASE - VENDOR LVL: '                   03000000
                                           PV-DISPLAY-AMOUNT.           03010000
           MOVE PV-CUM-FILE-SHNK-READ   TO PV-DISPLAY-AMOUNT.           03020000
           DISPLAY '*      SALES BASE - SHRINK LVL: '                   03030000
                                           PV-DISPLAY-AMOUNT.           03040000
           MOVE PV-WRITE-RECS           TO PV-DISPLAY-AMOUNT.           03050000
           DISPLAY '* WRITE TOTAL RECORDS         : '                   03060000
                                           PV-DISPLAY-AMOUNT.           03070000
           MOVE PV-PRORATE-WRITTEN      TO PV-DISPLAY-AMOUNT.           03080000
           DISPLAY '*       PRORTED SLS BASE DOLRS: '                   03090000
                                           PV-DISPLAY-AMOUNT.           03100000
                                                                        03110000
           DISPLAY ' '.                                                 03120000
           DISPLAY '******************************************'.        03130000
W34517     DISPLAY '*  END OF VAKUT304                        '.        03140000
           DISPLAY '******************************************'.        03150000
                                                                        03160000
           CLOSE SLS-CUM-FILE-BVKEY-IN                                  03170000
                 SLS-CUM-FILE-SHNK-IN                                   03180000
                 SLS-CUM-FILE-PRORATE-OUT.                              03190000
                                                                        03200000
