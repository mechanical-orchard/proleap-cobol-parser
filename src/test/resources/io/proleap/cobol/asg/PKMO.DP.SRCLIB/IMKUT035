      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.             IMKUT035.                                00050000
       AUTHOR.                 D. THEEL                                 00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            9/19/2002.                               00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      *                                                                 00110000
      *-----------------------------------------------------------------00120000
      *    CREATE ITEM DOMAIN UPC EXTRACT RECORDS                       00130000
      *-----------------------------------------------------------------00140000
      *  THE PURPOSE OF THIS PROGRAM IS TO READ IN THE BMC UNLOAD ITEM  00150000
      *  UPC EXTRACT FILE, INCLUDING ALL ACTIVE INTERNAL, VENDOR, AND   00160000
      *  RENUMBERED UPCS, GET SIZE DESCRIPTION, NRF COLOR DESCRIPTION,  00160100
      *  AND NRF SHORT COLOR DESCRIPTION.  WRITE OUT ITEM UPC DOMAIN    00160200
      *  EXTRACT RECORD.                                                00160300
      *-----------------------------------------------------------------00160400
      *    CHG          DATE        DESCRIPTION                         00160500
      *    --------   --------     ---------------------------------    00160600
      *-----------------------------------------------------------------00161200
                                                                        00161300
      ******************************************************************00161400
       ENVIRONMENT DIVISION.                                            00161500
      ******************************************************************00161600
                                                                        00161700
       CONFIGURATION SECTION.                                           00161800
                                                                        00161900
       SOURCE-COMPUTER.        IBM-3090.                                00162000
       OBJECT-COMPUTER.        IBM-3090.                                00163000
                                                                        00164000
       INPUT-OUTPUT SECTION.                                            00165000
                                                                        00166000
       FILE-CONTROL.                                                    00167000
                                                                        00168000
           SELECT UPC-UNLOAD-IN                                         00169000
               ASSIGN TO INP01.                                         00170000
                                                                        00180000
           SELECT UPC-EXTRACT-OUT                                       00202000
               ASSIGN TO OUT01.                                         00203000
                                                                        00204000
                                                                        00205000
      ******************************************************************00206000
       DATA DIVISION.                                                   00207000
      ******************************************************************00208000
                                                                        00209000
       FILE SECTION.                                                    00210000
                                                                        00220000
       FD  UPC-UNLOAD-IN                                                00230000
           RECORDING MODE IS F                                          00240000
           BLOCK CONTAINS 0 RECORDS                                     00250000
           LABEL RECORDS ARE STANDARD.                                  00260000
       01  UPC-UNLOAD-IN-REC                  PIC X(69).                00270000
                                                                        00280000
                                                                        00350000
       FD  UPC-EXTRACT-OUT                                              00360000
           RECORDING MODE IS F                                          00370000
           BLOCK CONTAINS 0 RECORDS                                     00380000
           LABEL RECORDS ARE STANDARD.                                  00390000
       01  UPC-EXTRACT-OUT-REC                PIC X(250).               00400000
                                                                        00410000
      ******************************************************************00420000
       WORKING-STORAGE SECTION.                                         00430000
      ******************************************************************00440000
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00450000
                                                          COMP SYNC.    00460000
       01  PC-PROGRAM-CONSTANTS.                                        00470000
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUT035'. 00480000
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2.         00490000
                                                                        00500000
       01  PS-PROGRAM-SWITCHES.                                         00510000
           05  PS-TUPC-UNLOAD-DONE-SW         PIC X(01)    VALUE 'N'.   00511000
               88  PS-TUPC-UNLOAD-DONE                     VALUE 'Y'.   00512000
               88  PS-TUPC-UNLOAD-NOT-DONE                 VALUE 'N'.   00513000
           05  PS-SQL-ERROR-SW                PIC X(01)    VALUE 'N'.   00517000
               88  PS-SQL-ERROR                            VALUE 'Y'.   00518000
               88  PS-NO-SQL-ERROR                         VALUE 'N'.   00519000
                                                                        00520000
       01  PV-PROGRAM-VARIABLES.                                        00530000
           05  PV-UPC-RECS-WRITTEN            PIC S9(09) VALUE +0  COMP.00540000
           05  PV-UPC-RECS-READ               PIC S9(09) VALUE +0  COMP.00550000
           05  PV-TSKXREF-UNLOAD-READ         PIC S9(09) VALUE +0  COMP.00560000
           05  PV-TSIZE-SELECT-CTR            PIC S9(09) VALUE +0  COMP.00561004
           05  PV-TUPC-UNLOAD-DROPPED         PIC S9(09) VALUE +0  COMP.00570000
           05  PV-PARAGRAPH-NAME              PIC  X(30)  VALUE SPACE.  00580000
           05  PV-DB2-OPERATION               PIC  X(30)  VALUE SPACE.  00590000
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE ZERO    00600000
                                                          COMP SYNC.    00610000
           05  PV-INTR-UPC-ID-TEN             PIC S9(10) VALUE +0 COMP. 00611000
           05  PV-DISPLAY-INTERNAL            PIC  9(10) VALUE ZERO.    00612000
           05  PV-PREV-SIZE-KOHLS-DESC        PIC  X(10) VALUE SPACES.  00612107
           05  PV-PREV-KOHLS-SIZE-CDE  PIC  S9(09)  COMP VALUE 999999.  00612204
                                                                        00613000
      *--------------------------------------------------               00614000
      *  COPYBOOK FOR BMC UPC UNLOAD INPUT FILE.                        00615000
      *--------------------------------------------------               00616000
           COPY IMRD054.                                                00617000
                                                                        00618000
      *--------------------------------------------------               00619000
      *  RECORD LAYOUT FOR THE ITEM DOMAIN TSKXREF UNLOAD INPUT FILE    00620000
      *--------------------------------------------------               00630000
           COPY IMRD062.                                                00640000
                                                                        00650000
      *--------------------------------------------------               00660000
      *  COPYBOOK FOR ITEM DOMAIN UPC EXTRACT OUTPUT FILE.              00670000
      *--------------------------------------------------               00680000
           COPY IMRD055.                                                00690000
                                                                        00700000
      *--------------------------------------------------               00710000
      *  COLOR TABLE                                                    00720000
      *--------------------------------------------------               00730000
           EXEC SQL                                                     00740000
               INCLUDE TCOLOR                                           00750000
           END-EXEC.                                                    00760000
                                                                        00770000
      *--------------------------------------------------               00780000
      *  SIZE TABLE                                                     00790000
      *--------------------------------------------------               00800000
           EXEC SQL                                                     00810000
               INCLUDE TSIZE                                            00820000
           END-EXEC.                                                    00830000
                                                                        00840000
      *--------------------------------------------------               00850000
      *  COMMUNICATION AREA FOR DB2                                     00860000
      *--------------------------------------------------               00870000
           EXEC SQL                                                     00880000
               INCLUDE SQLCA                                            00890000
           END-EXEC.                                                    00900000
                                                                        00910000
      *--------------------------------------------------               00920000
      *  DB2 SQL ERROR ROUTINE                                          00930000
      *--------------------------------------------------               00940000
           COPY DPWS004.                                                00950000
                                                                        00960000
       PROCEDURE DIVISION.                                              00970000
      *-----------------------------------------------------------------00980000
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                          00990000
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. 01000000
      *-----------------------------------------------------------------01010000
       0000-MAINLINE.                                                   01020000
                                                                        01030000
           PERFORM 0100-HOUSEKEEPING.                                   01040000
                                                                        01050000
           PERFORM 1000-PROCESS-INPUT-FILES                             01060000
               UNTIL PS-TUPC-UNLOAD-DONE.                               01070000
                                                                        01090000
           PERFORM 9000-EOJ.                                            01100000
           GOBACK.                                                      01110000
                                                                        01120000
      *-----------------------------------------------------------------01130000
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE              01140000
      *-----------------------------------------------------------------01150000
       0100-HOUSEKEEPING.                                               01160000
                                                                        01170000
           OPEN INPUT UPC-UNLOAD-IN                                     01180000
                OUTPUT UPC-EXTRACT-OUT.                                 01200000
                                                                        01210000
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01220000
                                                                        01220305
                                                                        01230000
           IF PS-TUPC-UNLOAD-DONE                                       01240000
              DISPLAY '*** '                                            01250000
              DISPLAY '*** IMKUT035 - NO INPUT UPC RECS FOUND'          01260000
              DISPLAY '*** '                                            01270000
              MOVE +4003 TO ABEND-CODE                                  01280000
              PERFORM 9999-ABEND                                        01290000
           END-IF.                                                      01300000
                                                                        01310000
                                                                        01361400
      *-----------------------------------------------------------------01361500
      *  PROCESS ALL INPUT RECORDS AND FORMAT ITEM UPC DOMAIN EXTRACT   01361600
      *  RECORDS UNTIL END OF INPUT FILE.                               01361700
      *-----------------------------------------------------------------01361800
       1000-PROCESS-INPUT-FILES.                                        01361900
      *                                                                 01362000
           PERFORM 1100-CREATE-UPC-OUTPUT-REC.                          01364002
           PERFORM 6000-READ-UPC-UNLOAD-FILE.                           01365002
                                                                        01370800
      *-----------------------------------------------------------------01370900
      *  FORMAT AND WRITE A UPC DOMAIN EXTRACT RECORD.                  01371000
      *-----------------------------------------------------------------01372000
       1100-CREATE-UPC-OUTPUT-REC.                                      01373000
                                                                        01374000
           MOVE IM054-INTR-UPC-ID       TO PV-INTR-UPC-ID-TEN.          01375000
           MOVE PV-INTR-UPC-ID-TEN      TO PV-DISPLAY-INTERNAL.         01376000
           IF IM054-KOHLS-SIZE-CDE =  PV-PREV-KOHLS-SIZE-CDE            01376101
               MOVE PV-PREV-SIZE-KOHLS-DESC TO IM055-KOHLS-SIZE-DESC    01376307
           ELSE                                                         01376401
               PERFORM 1300-GET-SIZE-DESC                               01377001
               ADD 1 TO PV-TSIZE-SELECT-CTR                             01377104
               MOVE SIZE-KOHLS-DESC      TO IM055-KOHLS-SIZE-DESC       01378003
               MOVE IM054-KOHLS-SIZE-CDE TO PV-PREV-KOHLS-SIZE-CDE      01378103
           END-IF.                                                      01378201
                                                                        01379000
           MOVE IM054-KOHLS-SIZE-CDE    TO IM055-KOHLS-SIZE-CDE.        01411006
           MOVE IM054-UPC-ID            TO IM055-UPC-ID.                01420000
           MOVE IM054-INTR-UPC-ID       TO IM055-INTR-UPC-ID.           01430000
           MOVE IM054-UPC-TYP-CDE       TO IM055-UPC-TYP-CDE.           01440000
           MOVE IM054-UPC-NBR           TO IM055-UPC-NBR.               01450000
           MOVE IM054-EDI-ORD-IND       TO IM055-EDI-ORD-IND.           01460000
           MOVE IM054-PREPK-IND         TO IM055-PREPK-IND.             01470000
           MOVE IM054-CERTD-IND         TO IM055-CERTD-IND.             01480000
           MOVE IM054-CERTD-DTE         TO IM055-CERTD-DTE.             01490000
           MOVE IM054-UPC-SRC-CDE       TO IM055-UPC-SRC-CDE.           01500000
           MOVE IM054-KOHLS-SIZE-CDE    TO IM055-KOHLS-SIZE-CDE.        01510000
           MOVE IM054-NRF-CLOR-CDE      TO IM055-NRF-CLOR-CDE.          01520000
           MOVE IM054-CSTM-CLOR-DESC    TO IM055-CSTM-CLOR-DESC.        01530000
           MOVE IM054-WT-UM-CDE         TO IM055-WT-UM-CDE.             01540000
           MOVE IM054-WT-UM-QTY         TO IM055-WT-UM-QTY.             01550000
           MOVE IM054-SIZE-UM-CDE       TO IM055-SIZE-UM-CDE.           01560000
           MOVE IM054-WD-UM-QTY         TO IM055-WD-UM-QTY.             01570000
           MOVE IM054-HT-UM-QTY         TO IM055-HT-UM-QTY.             01580000
           MOVE IM054-DPTH-UM-QTY       TO IM055-DPTH-UM-QTY.           01590000
           MOVE SPACES                  TO IM055-ITEM-UPC-FILLER.       01600000
                                                                        01610000
      ***  WRITE UPC OUTPUT RECORD                                      01620000
           PERFORM 7000-WRITE-UPC-OUTPUT-REC.                           01630000
                                                                        01640000
      *-----------------------------------------------------------------01650000
      * GET THE SIZE DESCRIPTION FROM TSIZE BASED ON THE KOHLS SIZE CDE 01660000
      * FROM THE INPUT FILE.                                            01670000
      *-----------------------------------------------------------------01680000
       1300-GET-SIZE-DESC.                                              01690000
                                                                        01700000
           INITIALIZE DCLTSIZE.                                         01710000
                                                                        01720000
           PERFORM WITH TEST AFTER                                      01730000
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       01740000
                 UNTIL SQLCODE = +0                                     01750000
                    OR SQLCODE = +100                                   01760000
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  01770000
               EXEC SQL                                                 01780000
                   SELECT KOHLS_DESC                                    01790000
                     INTO :SIZE-KOHLS-DESC                              01800000
                     FROM TSIZE                                         01810000
                    WHERE KOHLS_SIZE_CDE = :IM054-KOHLS-SIZE-CDE        01820000
                    WITH UR                                             01830000
               END-EXEC                                                 01840000
                                                                        01850000
               EVALUATE TRUE                                            01860000
                   WHEN SQLCODE = -904                                  01870000
                   WHEN SQLCODE = -913                                  01880000
                       CONTINUE                                         01890000
                   WHEN SQLCODE = ZERO                                  01900000
                       MOVE SIZE-KOHLS-DESC TO PV-PREV-SIZE-KOHLS-DESC  01910007
                   WHEN SQLCODE = +100                                  01920000
                       MOVE SPACES TO SIZE-KOHLS-DESC                   01930000
                   WHEN OTHER                                           01940000
                       DISPLAY '** '                                    01950000
                               PC-PGM-NAME                              01960000
                               ' - INTR UPC ID: '                       01970000
                               PV-DISPLAY-INTERNAL                      01980000
                       MOVE '1300-GET-SIZE-DESC' TO PV-PARAGRAPH-NAME   01990000
                       MOVE 'SELECT FROM TSIZE'  TO PV-DB2-OPERATION    02000000
                       PERFORM 9998-SQL-ERROR                           02010000
               END-EVALUATE                                             02020000
           END-PERFORM.                                                 02030000
                                                                        02040000
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02050000
               DISPLAY '** '                                            02060000
                       PC-PGM-NAME                                      02070000
                       ' - INTR UPC ID: '                               02080000
                       PV-DISPLAY-INTERNAL                              02090000
               MOVE '1300-GET-SIZE-DESC' TO PV-PARAGRAPH-NAME           02100000
               MOVE 'SELECT FROM TSIZE'  TO PV-DB2-OPERATION            02110000
               PERFORM 9998-SQL-ERROR                                   02120000
           END-IF.                                                      02130000
                                                                        02140000
      *-----------------------------------------------------------------02473000
      * READ THE UPC UNLOAD INPUT FILE                                  02474000
      *-----------------------------------------------------------------02475000
       6000-READ-UPC-UNLOAD-FILE.                                       02476000
                                                                        02477000
           INITIALIZE IM054-ITEM-UPC-UNLD-REC.                          02478000
                                                                        02479000
           READ UPC-UNLOAD-IN INTO IM054-ITEM-UPC-UNLD-REC              02480000
              AT END                                                    02490000
                 SET PS-TUPC-UNLOAD-DONE TO TRUE                        02500000
              NOT AT END                                                02510000
                 ADD +1   TO PV-UPC-RECS-READ.                          02520000
                                                                        02530000
                                                                        02650000
      *-----------------------------------------------------------------02660000
      * WRITE THE UPC ITEM DOMAIN OUTPUT FILE.                          02670000
      *-----------------------------------------------------------------02680000
       7000-WRITE-UPC-OUTPUT-REC.                                       02690000
                                                                        02700000
           WRITE UPC-EXTRACT-OUT-REC FROM   IM055-ITEM-UPC-DOMAIN-REC.  02710000
                                                                        02720000
           ADD +1 TO PV-UPC-RECS-WRITTEN.                               02730000
                                                                        02740007
      *-----------------------------------------------------------------02880000
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.    02890000
      *-----------------------------------------------------------------02900000
       9000-EOJ.                                                        02910000
                                                                        02920000
                                                                        02970000
           DISPLAY ' '.                                                 02971000
           DISPLAY '============================================='.     02972000
           DISPLAY ' '.                                                 02973000
           DISPLAY ' ******  IMKUT035 PROCESSING SUMMARY  ******  '.    02974000
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                        02975000
           DISPLAY '============================================='.     02975100
                                                                        02975200
                                                                        02975300
           CLOSE UPC-UNLOAD-IN                                          02975400
                 UPC-EXTRACT-OUT.                                       02975600
                                                                        02975700
      *-----------------------------------------------------------------02975800
      * DISPLAY PROCESSED RECORDS COUNTS                                02975900
      *-----------------------------------------------------------------02976000
       9100-DISPLAY-PROCESSING-INFO.                                    02977000
                                                                        02978000
           DISPLAY '    UPC RECORDS READ    = '                         02980004
                 PV-UPC-RECS-READ.                                      02990000
           DISPLAY '    TSIZE SELECTS       = '                         02991004
                 PV-TSIZE-SELECT-CTR                                    02992004
           DISPLAY '    UPC RECORDS WRITTEN = '                         03000004
                 PV-UPC-RECS-WRITTEN.                                   03010000
           DISPLAY ' '.                                                 03060000
      *-----------------------------------------------------------------03070000
      * 9800-SQL-ABEND - DISPLAY DB2 ABEND INFO                         03080000
      *-----------------------------------------------------------------03090000
       9998-SQL-ERROR.                                                  03100000
                                                                        03110000
           SET PS-SQL-ERROR TO TRUE.                                    03120000
                                                                        03130000
           DISPLAY '** '                                                03140000
                   PC-PGM-NAME                                          03150000
                   ' ABEND IN PARAGRAPH '                               03160000
                   PV-PARAGRAPH-NAME.                                   03170000
           DISPLAY '** '                                                03180000
                   PC-PGM-NAME                                          03190000
                   ' OPERATION = ' PV-DB2-OPERATION.                    03200000
                                                                        03210000
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     03220000
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               03230000
           COPY DPPD004.                                                03240000
                                                                        03250000
           EXEC SQL                                                     03260000
               ROLLBACK                                                 03270000
           END-EXEC.                                                    03280000
                                                                        03290000
           MOVE +4013 TO ABEND-CODE                                     03300000
           PERFORM 9999-ABEND.                                          03310000
                                                                        03320000
      *-----------------------------------------------------------------03330000
      * 9999-ABEND - FORCES PROGRAM ABEND WITH RETURN CODE              03340000
      *-----------------------------------------------------------------03350000
       9999-ABEND.                                                      03360000
                                                                        03370000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03380000
                                                                        03390000
