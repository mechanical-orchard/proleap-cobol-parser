      *-----------------------*                                         12/09/94
00002  IDENTIFICATION DIVISION.                                         INKCS004
00003 *-----------------------*                                            LV005
00004  PROGRAM-ID.    INKCS004.                                         INKCS004
00005  AUTHOR.        M. FURLICK - STRATAGEM                            INKCS004
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS004
00007  DATE-WRITTEN.  08/93.                                            INKCS004
00008  DATE-COMPILED.                                                   INKCS004
00009                                                                   INKCS004
00010 *---------------------------------------------------------------* INKCS004
00011 *    I004 - PHYSICAL INVENTORY INVALID DATA DETAIL LIST         * INKCS004
00012 *                                                               * INKCS004
00013 *    THIS PROGRAM GENERATES A LIST OF THE LINES WITHIN AN AREA  * INKCS004
00014 *    FOR THE SELECTED STORE, THAT STILL HAVE ERRORS.  THIS LIST * INKCS004
00015 *    IS USED BY CORPORATE PERSONNEL ONLY.                       * INKCS004
00016 *---------------------------------------------------------------*    CL**4
00017 *                       CHANGE LOG                              *    CL**4
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
00019 * 03/94  M. BLAZICH                                             *    CL**4
00020 *                                                               *    CL**4
00021 *    ADDED INV-LINE-NBR AND SCAN-LINE-NBR TO COMMAREA           *    CL**4
00022 *    NOTE THAT COMMAREA NOW 2000 BYTES                          *    CL**4
00023 *---------------------------------------------------------------*    CL**4
W21732* 10/24/02 CHANGED BY IGSI      WR# 21732                       *    CL**4
W21732*                                                               *    CL**4
W21732*    CHANGED THE PROGRAM TO DISPLAY THE NON-INTELLIGENT SKU     *    CL**4
W21732*    INSTEAD OF DEPT/CLASS/SEQ NUMBER.                          *    CL**4
00023 *                                                                    CL**4
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
00023 *---------------------------------------------------------------*    CL**4
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
263126*---------------------------------------------------------------*   CL*17 
1022RM* CHANGED 10/26/2022    BY BOB MCASEY      WR: CHG0277188                 
1022RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1022RM* THE CICS ARCHITECTURE API.                                              
1022RM*---------------------------------------------------------------*   CL*17 
00024 /                                                                 INKCS004
00025  ENVIRONMENT DIVISION.                                            INKCS004
00026                                                                   INKCS004
00027  DATA DIVISION.                                                   INKCS004
00028                                                                   INKCS004
00029  WORKING-STORAGE SECTION.                                         INKCS004
00030                                                                   INKCS004
00031  01  PC-PROGRAM-CONSTANTS.                                        INKCS004
00032      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS004
00033                                                     'IN004A  '.   INKCS004
00034      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS004
00035                                                     'INKM004 '.   INKCS004
00036      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS004
00037                                                     'INKCS004'.   INKCS004
00038      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS004
00039      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS004
00040      05  PC-SEL-QUEUE-SEQ2              PIC  9(01)  VALUE 3.      INKCS004
00041      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS004
00042      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13     INKCS004
00043                                                     COMP SYNC.    INKCS004
00044      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS004
00045                                                     COMP SYNC.    INKCS004
00046      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS004
00047                                                     +1000000      INKCS004
00048                                                     COMP-3.       INKCS004
00049      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS004
00050                                                     COMP SYNC.    INKCS004
00051      05  PC-MAX-RETRIES                 PIC S9(03)  VALUE 3       INKCS004
00052                                                     COMP-3.       INKCS004
00053      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  INKCS004
00054      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  INKCS004
00055      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  INKCS004
00056      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  INKCS004
00057      05  PC-Y                           PIC  X(01)  VALUE 'Y'.    INKCS004
00058      05  PC-COMPLETE                    PIC  X(08)  VALUE         INKCS004
00059                                                    'COMPLETE'.    INKCS004
00060      05  PC-8999                        PIC  9(05)  VALUE 8999.   INKCS004
00061      05  PC-10000                       PIC  9(05)  VALUE 10000.  INKCS004
00062                                                                   INKCS004
00063      05  PC-INTER-APPL-CODES.                                     INKCS004
00064          10  PC-INTER-APPL-CRIT-SPEC                              INKCS004
00065                                   PIC  X(01)  VALUE '1'.          INKCS004
00066          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.          INKCS004
00067                                                                   INKCS004
00068      05  PC-TSYMSG-NUMBERS.                                       INKCS004
00069          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS004
00070          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS004
00071          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS004
00072          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS004
00073          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS004
00074          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS004
00075          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS004
00076          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS004
00077          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS004
00078          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS004
00079          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS004
00080          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS004
00081          10  PC-TSYMSG-00148            PIC  9(05)  VALUE 00148.  INKCS004
00082          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS004
00083          10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  INKCS004
00084 *                                                                 INKCS004
00085  01  PS-PROGRAM-SWITCHES.                                         INKCS004
00086      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS004
00087          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS004
00088          88  PS-ERROR                               VALUE 'N'.    INKCS004
00089      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    INKCS004
00090          88  PS-COMMAREA-VALID                      VALUE 'Y'.    INKCS004
00091          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    INKCS004
00092      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS004
00093          88  PS-DONE                                VALUE 'Y'.    INKCS004
00094          88  PS-NOT-DONE                            VALUE 'N'.    INKCS004
00095 *                                                                 INKCS004
00096  01  PV-PROGRAM-VARIABLES.                                        INKCS004
W26600     05  PV-STORE-NBR-X       PIC X(04)  VALUE SPACES.            INKCS004
W26600     05  PV-STORE-NBR-9 REDEFINES                                 INKCS004
W26600         PV-STORE-NBR-X       PIC 9(04).                          INKCS004
00097      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS004
00098                                                     COMP SYNC.    INKCS004
00099      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS004
00100                                                     COMP SYNC.    INKCS004
00101      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS004
00102                                                     COMP-3.       INKCS004
00103      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS004
00104                                                     COMP-3.       INKCS004
00105      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS004
00106                                                     COMP-3.       INKCS004
00107      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS004
00108                                                     COMP-3.       INKCS004
00109      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS004
00110                                                     COMP-3.       INKCS004
00111      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS004
00112                                                     COMP-3.       INKCS004
00113      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS004
00114                                                     COMP-3.       INKCS004
00115      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS004
00116                                                     COMP SYNC.    INKCS004
00117      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS004
00118                                                     COMP SYNC.    INKCS004
00119      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS004
00120                                                     COMP SYNC.    INKCS004
00121      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS004
00122                                                     COMP SYNC.    INKCS004
00123      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS004
00124                                                     COMP SYNC.    INKCS004
00125      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS004
00126                                                     COMP SYNC.    INKCS004
00127      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS004
00128                                                     COMP SYNC.    INKCS004
00129      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS004
00130                                                     COMP SYNC.    INKCS004
00131      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS004
00132                                                     COMP SYNC.    INKCS004
00133      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS004
00134                                                     COMP SYNC.    INKCS004
00135      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS004
00136                                                     COMP SYNC.    INKCS004
00137      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS004
00138                                                     COMP SYNC.    INKCS004
00139      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS004
00140                                                     COMP SYNC.    INKCS004
00141      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS004
00142                                                     COMP SYNC.    INKCS004
00143      05  PV-SQL-SUB                     PIC S9(04)  VALUE +0      INKCS004
00144                                                     COMP SYNC.    INKCS004
00145      05  PV-RETRY-COUNTER               PIC S9(04)  VALUE +0      INKCS004
00146                                                     COMP SYNC.    INKCS004
00145      05  PV-SKU                         PIC 9(08).                INKCS004
00147 *----------------------------------------------------------------*INKCS004
00148 *   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *INKCS004
00149 *   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE            *INKCS004
00150 *   APPLICATION-SPECIFIC COMMAREA.                               *INKCS004
00151 *----------------------------------------------------------------*INKCS004
00152  01  LQW-LIST-QUEUE-WORK-AREA.                                    INKCS004
00153      05  LQW-BLOCK-MODIFIED             PIC  X(01)    VALUE SPACE.INKCS004
00154      05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES           INKCS004
00155                                         INDEXED BY LQW-IDX.       INKCS004
00156          10  LQW-LIST-ITEM-NUMBER       PIC S9(09)    COMP-3.     INKCS004
00157          10  LQW-TEMP-SELECTED-SW       PIC  X(01).               INKCS004
00158          10  LQW-SELECTED-SW            PIC  X(01).               INKCS004
00159              88  LQW-SELECT                           VALUE 'S'.  INKCS004
00160              88  LQW-DESELECT                         VALUE ' '.  INKCS004
00161          10  LQW-ACTION                 PIC  X(03).               INKCS004
00162          10  LQW-INV-LINE-NBR           PIC S9(08)    COMP.          CL**3
00163          10  LQW-SCAN-LINE-NBR          PIC S9(08)    COMP.          CL**3
W21732         10  LQW-SKU                    PIC  X(08).               INKCS004
00167          10  LQW-UPC                    PIC S9(15)    COMP-3.     INKCS004
00168          10  LQW-INV-QTY                PIC S9(07)    COMP-3.     INKCS004
00169          10  LQW-INV-PR-AMT             PIC S9(07)V99 COMP-3.     INKCS004
00170          10  LQW-SKU-COLOR              PIC  X(01).               INKCS004
00171          10  LQW-PR-AMT-COLOR           PIC  X(01).               INKCS004
00172          10  LQW-QTY-COLOR              PIC  X(01).               INKCS004
00173          10  LQW-INVALID-ITM-IND        PIC  X(01).               INKCS004
00174          10  LQW-INVALID-REC-IND        PIC  X(01).               INKCS004
00175                                                                   INKCS004
00176      EJECT                                                        INKCS004
00177 *---------------------------------------------------------------* INKCS004
00178 *  RECORD LAYOUT FOR THE LINE SELECTION TEMP STORAGE QUEUE.     * INKCS004
00179 *---------------------------------------------------------------* INKCS004
00180                                                                   INKCS004
00181      COPY INWS003.                                                   CL**5
00182                                                                   INKCS004
00183      EJECT                                                        INKCS004
00184 *---------------------------------------------------------------* INKCS004
00185 *    MAP LAYOUT                                                 * INKCS004
00186 *---------------------------------------------------------------* INKCS004
00187                                                                   INKCS004
W26600     COPY INKM004.                                                INKCS004
00189 *                                                                 INKCS004
00190  01  MR-OCCURS-LAYOUT                   REDEFINES  IN004AO.       INKCS004
W26600     05  FILLER                         PIC X(94).                INKCS004
00192      05  MR-SCROLL-AREA                 OCCURS 13 TIMES.          INKCS004
00193          10  MR-SELECTL                 PIC S9(04) COMP.          INKCS004
00194          10  MR-SELECTA                 PIC  X(01).               INKCS004
00195          10  MR-SELECTC                 PIC  X(01).               INKCS004
00196          10  MR-SELECTH                 PIC  X(01).               INKCS004
00197          10  MR-SELECTI                 PIC  X(01).               INKCS004
00198                                                                   INKCS004
00199          10  MR-ACTIONL                 PIC S9(04) COMP.          INKCS004
00200          10  MR-ACTIONA                 PIC  X(01).               INKCS004
00201          10  MR-ACTIONC                 PIC  X(01).               INKCS004
00202          10  MR-ACTIONH                 PIC  X(01).               INKCS004
00203          10  MR-ACTION                  PIC  X(03).               INKCS004
00204                                                                   INKCS004
00205          10  MR-LINEL                   PIC S9(04) COMP.          INKCS004
00206          10  MR-LINEA                   PIC  X(01).               INKCS004
00207          10  MR-LINEC                   PIC  X(01).               INKCS004
00208          10  MR-LINEH                   PIC  X(01).               INKCS004
00209          10  MR-LINE-X.                                           INKCS004
00210              15  MR-LINE                PIC ZZZZZZ9.              INKCS004
00211                                                                   INKCS004
W21732         10  MR-SKUL                    PIC S9(04) COMP.          INKCS004
W21732         10  MR-SKUA                    PIC  X(01).               INKCS004
W21732         10  MR-SKUC                    PIC  X(01).               INKCS004
W21732         10  MR-SKUH                    PIC  X(01).               INKCS004
W21732         10  MR-SKU                     PIC  X(08).               INKCS004
00229                                                                   INKCS004
00230          10  MR-UPCL                    PIC S9(04) COMP.          INKCS004
00231          10  MR-UPCA                    PIC  X(01).               INKCS004
00232          10  MR-UPCC                    PIC  X(01).               INKCS004
00233          10  MR-UPCH                    PIC  X(01).               INKCS004
00234          10  MR-UPC-X.                                            INKCS004
00235              15  MR-UPC                 PIC Z(14)9.               INKCS004
00236                                                                   INKCS004
00237          10  MR-INV-QTYL                PIC S9(04) COMP.          INKCS004
00238          10  MR-INV-QTYA                PIC  X(01).               INKCS004
00239          10  MR-INV-QTYC                PIC  X(01).               INKCS004
00240          10  MR-INV-QTYH                PIC  X(01).               INKCS004
00241          10  MR-INV-QTY-X.                                        INKCS004
00242              15  MR-INV-QTY             PIC  Z(6)9.               INKCS004
00243                                                                   INKCS004
00244          10  MR-INV-PR-AMTL             PIC S9(04) COMP.          INKCS004
00245          10  MR-INV-PR-AMTA             PIC  X(01).               INKCS004
00246          10  MR-INV-PR-AMTC             PIC  X(01).               INKCS004
00247          10  MR-INV-PR-AMTH             PIC  X(01).               INKCS004
00248          10  MR-INV-PR-AMT-X.                                     INKCS004
00249              15  MR-INV-PR-AMT          PIC  Z(7).99.             INKCS004
00250 /                                                                 INKCS004
00251 *---------------------------------------------------------------* INKCS004
00252 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS004
00253 *---------------------------------------------------------------* INKCS004
00254                                                                   INKCS004
00255      COPY DPWS015.                                                INKCS004
00256                                                                   INKCS004
00257 *---------------------------------------------------------------* INKCS004
00258 *    FUNCTION KEYS COPYBOOK.                                    * INKCS004
00259 *---------------------------------------------------------------* INKCS004
00260                                                                   INKCS004
00261      COPY DPWS016.                                                INKCS004
00262                                                                   INKCS004
00263 *---------------------------------------------------------------* INKCS004
00264 *    NUMERIC EDIT LAYOUT.                                       * INKCS004
00265 *---------------------------------------------------------------* INKCS004
00266                                                                   INKCS004
00267      COPY DPWS010I.                                               INKCS004
00268                                                                   INKCS004
00269 *---------------------------------------------------------------* INKCS004
00270 *    AREA TSQB LAYOUT.                                          * INKCS004
00271 *---------------------------------------------------------------* INKCS004
00272                                                                   INKCS004
00273      COPY INWS002.                                                INKCS004
00274 /                                                                 INKCS004
00275 *---------------------------------------------------------------* INKCS004
00276 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS004
00277 *---------------------------------------------------------------* INKCS004
00278                                                                   INKCS004
00279      COPY DPWS030.                                                INKCS004
1022RM     COPY DPWS930.                                                INKCS004
00280 /                                                                 INKCS004
00281 *---------------------------------------------------------------* INKCS004
00282 *    STANDARD COMMAREA.                                         * INKCS004
00283 *---------------------------------------------------------------* INKCS004
00284                                                                   INKCS004
00285      COPY DPWS020.                                                INKCS004
00286      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS004
00287                                                                   INKCS004
00288          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS004
W26600     COPY INWS001.                                                INKCS004
00290 *---------------------------------------------------------------* INKCS004
00291 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS004
00292 *---------------------------------------------------------------* INKCS004
00293                                                                   INKCS004
00294          10  ASC-SPECIFIC-COMMAREA.                               INKCS004
00295              15  ASC-LIST-KEYS.                                   INKCS004
W26600                 20  ASC-KEY-STORE-NBR               PIC  X(04).  INKCS004
00297                  20  ASC-KEY-AREA-NBR                PIC S9(08)   INKCS004
00298                                                      COMP.        INKCS004
00299                  20  ASC-KEY-SCAN-LINE-NBR           PIC S9(08)      CL**2
00300                                                      COMP.        INKCS004
00301              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS004
00302                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS004
00303                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS004
00304                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS004
00305                                                                   INKCS004
00306                  20  ASC-ITEM-ARRAY.                              INKCS004
00307                      25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.        INKCS004
00308                          30  ASC-LIST-ITEM-NUMBER                 INKCS004
00309                                                      PIC S9(09)   INKCS004
00310                                                      COMP-3.      INKCS004
00311                          30  ASC-TEMP-SELECTED-SW    PIC  X(01).  INKCS004
00312                              88  ASC-TEMP-NO-SELECT  VALUE ' '.   INKCS004
00313                              88  ASC-TEMP-SELECT     VALUE 'S'.   INKCS004
00314                              88  ASC-TEMP-BEGIN      VALUE 'B'.   INKCS004
00315                              88  ASC-TEMP-END        VALUE 'E'.   INKCS004
00316                          30  ASC-SELECTED-SW         PIC  X(01).  INKCS004
00317                              88  ASC-NO-SELECT       VALUE ' '.   INKCS004
00318                              88  ASC-SELECT          VALUE 'S'.   INKCS004
00319                              88  ASC-BEGIN           VALUE 'B'.   INKCS004
00320                              88  ASC-END             VALUE 'E'.   INKCS004
00321                                                                   INKCS004
00322                          30  ASC-ACTION              PIC  X(03).  INKCS004
00323                          30  ASC-INV-LINE-NBR        PIC S9(08)      CL**2
00324                                                           COMP.      CL**2
00325                          30  ASC-SCAN-LINE-NBR       PIC S9(08)      CL**2
00326                                                           COMP.   INKCS004
W21732                         30  ASC-SKU                 PIC  X(08).  INKCS004
00330                          30  ASC-UPC                 PIC S9(15)   INKCS004
00331                                                           COMP-3. INKCS004
00332                          30  ASC-INV-QTY             PIC S9(07)   INKCS004
00333                                                           COMP-3. INKCS004
00334                          30  ASC-INV-PR-AMT          PIC S9(07)V99INKCS004
00335                                                           COMP-3. INKCS004
00336                          30  ASC-SKU-COLOR           PIC  X(01).  INKCS004
00337                          30  ASC-PR-AMT-COLOR        PIC  X(01).  INKCS004
00338                          30  ASC-QTY-COLOR           PIC  X(01).  INKCS004
00339                          30  ASC-INVALID-ITM-IND     PIC  X(01).  INKCS004
00340                          30  ASC-INVALID-REC-IND     PIC  X(01).  INKCS004
00341                                                                   INKCS004
00342              15  ASC-BLK-SUB                         PIC S9(04)   INKCS004
00343                                                           COMP    INKCS004
00344                                                           SYNC.   INKCS004
00345              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS004
00346                                                           COMP    INKCS004
00347                                                           SYNC.   INKCS004
00348              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS004
00349                                                           COMP-3. INKCS004
00350              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS004
00351                                                           COMP-3. INKCS004
00352              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS004
00353                                                           COMP-3. INKCS004
00354              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS004
00355                                                           COMP-3. INKCS004
00356              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS004
00357                                                           COMP-3. INKCS004
00358              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS004
00359                                                           COMP    INKCS004
00360                                                           SYNC.   INKCS004
00361              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS004
00362                                                           COMP-3. INKCS004
00363              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS004
00364                                                           COMP-3. INKCS004
00365              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS004
00366                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS004
00367                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS004
00368              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS004
00369                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS004
00370                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS004
00371              15  ASC-LIST-QUEUE-NAME.                             INKCS004
00372                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS004
00373                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS004
00374                                                           COMP-3. INKCS004
00375                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS004
00376              15  ASC-SEL-QUEUE-NAME.                              INKCS004
00377                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS004
00378                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS004
00379                                                           COMP-3. INKCS004
00380                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS004
00381              15  ASC-SEL-QUEUE-NAME2.                             INKCS004
00382                  20  ASC-SEL-TERM-ID2                PIC  X(04).  INKCS004
00383                  20  ASC-SEL-TASK-NUMBER2            PIC S9(05)   INKCS004
00384                                                           COMP-3. INKCS004
00385                  20  ASC-SEL-SEQ2                    PIC  9(01).  INKCS004
00386                                                                   INKCS004
00387              15  ASC-INT-APPL-FORMAT-IND             PIC  X(01).  INKCS004
00388              15  ASC-USE-SELECTION-QUEUE-IND                      INKCS004
00389                                            PIC  X(01).            INKCS004
00390                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. INKCS004
00391              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.    INKCS004
00392              15  ASC-SEL-SUB               PIC S9(04)  COMP.      INKCS004
00393              15  ASC-SEL-ITEM              PIC S9(04)  COMP.      INKCS004
00394                                                                   INKCS004
00395              15  ASC-CURSOR-SW                       PIC  X(01).  INKCS004
00396                  88  ASC-NO-CURSOR                   VALUE 'Z'.   INKCS004
00397                  88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.   INKCS004
00398                  88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.   INKCS004
00399                  88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.   INKCS004
00400              15  ASC-START-ITEM-REQUEST.                          INKCS004
00401                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS004
00402              15  ASC-HOLD-INTER-APPL                 PIC X(200).  INKCS004
00403 /                                                                 INKCS004
00404 *---------------------------------------------------------------* INKCS004
00405 *    ABEND PROCESSING COPYBOOK.                                 * INKCS004
00406 *---------------------------------------------------------------* INKCS004
00407                                                                   INKCS004
00408      COPY DPWS013.                                                INKCS004
00409                                                                   INKCS004
00410 /                                                                 INKCS004
00411 *---------------------------------------------------------------* INKCS004
00412 *    DB2 AREA FOR TINVDTL                                       * INKCS004
00413 *---------------------------------------------------------------* INKCS004
00414      EXEC SQL                                                     INKCS004
00415           INCLUDE TINVDTL                                         INKCS004
00416      END-EXEC.                                                    INKCS004
00417 *---------------------------------------------------------------* INKCS004
00418 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS004
00419 *---------------------------------------------------------------* INKCS004
00420      EXEC SQL                                                     INKCS004
00421           INCLUDE SQLCA                                           INKCS004
00422      END-EXEC.                                                    INKCS004
00423                                                                   INKCS004
00424 *---------------------------------------------------------------* INKCS004
00425 *    TINVDTL CURSOR DECLARE.                                    * INKCS004
00426 *---------------------------------------------------------------* INKCS004
00427                                                                   INKCS004
00428      EXEC SQL                                                     INKCS004
00429           DECLARE LINE-CSR CURSOR                                 INKCS004
00430           FOR SELECT                                              INKCS004
00431                 REC_UPC_NBR                                       INKCS004
W21732              , REC_SKU_NBR                                       INKCS004
00435               , REC_QTY                                           INKCS004
00436               , REC_PR_AMT                                        INKCS004
00437               , CORR_UPC_NBR                                      INKCS004
W21732              , CORR_SKU_NBR                                      INKCS004
00441               , CORR_QTY                                          INKCS004
00442               , CORR_PR_AMT                                       INKCS004
00443               , INV_LINE_NBR                                         CL**2
00444               , SCAN_LINE_NBR                                        CL**2
00445               , INVALID_ITM_IND                                   INKCS004
00446               , INVALID_REC_IND                                   INKCS004
00447           FROM                                                    INKCS004
00448                 TINVDTL                                           INKCS004
00449           WHERE                                                   INKCS004
W26600                LOC_NBR    =   :INVDTL-LOC-NBR                    INKCS004
00451             AND                                                   INKCS004
W21732                AREA_NBR     =   :INVDTL-AREA-NBR                 INKCS004
00453             AND                                                   INKCS004
W21732                SCAN_LINE_NBR >  :INVDTL-SCAN-LINE-NBR               CL**2
00455           ORDER BY                                                INKCS004
00456                 SCAN_LINE_NBR                                     INKCS004
00457      END-EXEC.                                                    INKCS004
00458                                                                   INKCS004
00459 /                                                                 INKCS004
00460  LINKAGE SECTION.                                                 INKCS004
00461                                                                   INKCS004
00462  01  DFHCOMMAREA.                                                 INKCS004
00463      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS004
00464                                         DEPENDING ON EIBCALEN.    INKCS004
00465          10  FILLER                     PIC X.                    INKCS004
00466                                                                   INKCS004
00467  PROCEDURE DIVISION.                                              INKCS004
00468 **--------------------------------------------------------------**INKCS004
00469 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS004
00470 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS004
00471 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS004
00472 **--------------------------------------------------------------**INKCS004
00473                                                                   INKCS004
00474  0000-MAIN-MODULE.                                                INKCS004
00475                                                                   INKCS004
00476      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS004
00477      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS004
00478      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS004
00479      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS004
00480      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS004
00481                                                                   INKCS004
00482      MOVE LENGTH OF IN004AI        TO DP030-MAP-LENGTH (1).       INKCS004
00483      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS004
00484      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS004
00485                                                                   INKCS004
00486      PERFORM 1000-CONTROL-PROCESSING                              INKCS004
00487                                                                   INKCS004
00488      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS004
00489      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS004
00490                                                                   INKCS004
00491 *----------------------------------------------------------------*INKCS004
00492 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS004
00493 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS004
00494 *----------------------------------------------------------------*INKCS004
00495                                                                   INKCS004
00496  0001-CALL-CICS-ARCH-API.                                         INKCS004
00497                                                                   INKCS004
1022RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS004
1022RM     CALL DP930-CICS-ARCH-API                                     INKCS002
1022RM                     USING DFHEIBLK                                       
00499                            DFHCOMMAREA                            INKCS004
00500                            DP030-CICS-API-FIELDS                  INKCS004
00501                            DP020-STANDARD-COMMAREA                INKCS004
00502                            IN004AI.                               INKCS004
00503                                                                   INKCS004
00504      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS004
00505          CONTINUE                                                 INKCS004
00506      ELSE                                                         INKCS004
00507          SET DP013-NO-ROLLBACK                                    INKCS004
00508              DP013-XCTL-DISPLAY-RESTART                           INKCS004
00509              DP013-CICS-ABEND      TO TRUE                        INKCS004
00510          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS004
00511                                    TO DP013-PARAGRAPH             INKCS004
1022RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS004
1022RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS004
1022RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1022RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00514          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS004
00515          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
00516      END-IF.                                                      INKCS004
00517                                                                   INKCS004
00518 *----------------------------------------------------------------*INKCS004
00519 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS004
00520 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS004
00521 *                                                                *INKCS004
00522 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS004
00523 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS004
00524 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS004
00525 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS004
00526 *----------------------------------------------------------------*INKCS004
00527                                                                   INKCS004
00528  1000-CONTROL-PROCESSING.                                         INKCS004
00529                                                                   INKCS004
00530      EVALUATE TRUE                                                INKCS004
00531          WHEN DP020-NEXT-ACT-INITIAL                              INKCS004
00532               PERFORM 1100-FIND-OBJECT-KEYS                       INKCS004
00533               IF  PS-ERROR                                        INKCS004
00534                   CONTINUE                                        INKCS004
00535               ELSE                                                INKCS004
00536                   MOVE INTER-APPL-COMM-AREA                       INKCS004
00537                                    TO  ASC-HOLD-INTER-APPL        INKCS004
00538                   MOVE EIBTRMID    TO ASC-LIST-TERM-ID            INKCS004
00539                                       ASC-SEL-TERM-ID2            INKCS004
00540                   MOVE DP020-SRC-TASK-NUMBER                      INKCS004
00541                                    TO ASC-LIST-TASK-NUMBER        INKCS004
00542                                       ASC-SEL-TASK-NUMBER2        INKCS004
00543                   MOVE PC-LIST-QUEUE-SEQ                          INKCS004
00544                                    TO ASC-LIST-SEQ                INKCS004
00545                   MOVE PC-SEL-QUEUE-SEQ2                          INKCS004
00546                                    TO ASC-SEL-SEQ2                INKCS004
00547                                                                   INKCS004
00548                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS004
00549                   IF  SQLCODE = +100                              INKCS004
00550                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE       INKCS004
00551                       MOVE PC-TSYMSG-00148                        INKCS004
00552                                    TO  DP020-MSG-NUMBER           INKCS004
00553                       SET DP020-MSG-INFORMATIONAL TO TRUE         INKCS004
00554                   END-IF                                          INKCS004
00555                   IF  ASC-NUMBER-OF-ITEMS-READ = ZERO             INKCS004
00556                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE       INKCS004
00557                   END-IF                                          INKCS004
00558               END-IF                                              INKCS004
00559                                                                   INKCS004
00560          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS004
00561               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS004
00562                                    TO PV-TOP-ITEM                 INKCS004
00563               PERFORM 2000-PROCESS-PANEL                          INKCS004
00564                                                                   INKCS004
00565          WHEN DP020-NEXT-ACT-RETURN                               INKCS004
00566               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS004
00567                                    TO PV-TOP-ITEM                 INKCS004
00568               MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS004
00569               MOVE ASC-HOLD-INTER-APPL                            INKCS004
00570                                    TO INTER-APPL-COMM-AREA        INKCS004
00571               PERFORM 6000-REFRESH-PANEL-FROM-TEMP                INKCS004
00572               PERFORM 3000-EDIT-DATA-IN-COMMAREA                  INKCS004
00573               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS004
00574                                                                   INKCS004
00575          WHEN OTHER                                               INKCS004
00576               SET DP013-LOGIC-ABEND TO TRUE                       INKCS004
00577               SET DP013-NO-ROLLBACK TO TRUE                       INKCS004
00578               MOVE '1000-CONTROL-PROCESSING'                      INKCS004
00579                                    TO DP013-PARAGRAPH             INKCS004
00580               MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'   INKCS004
00581                                    TO DP013-MESSAGE-TEXT(1)       INKCS004
00582               PERFORM DP013-0000-PROCESS-ABEND                    INKCS004
00583      END-EVALUATE.                                                INKCS004
00584                                                                   INKCS004
00585 *----------------------------------------------------------------*INKCS004
00586 *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE    *INKCS004
00587 *  INTER-APPLICATION FORMAT CODE.                                *INKCS004
00588 *----------------------------------------------------------------*INKCS004
00589                                                                   INKCS004
00590  1100-FIND-OBJECT-KEYS.                                           INKCS004
00591                                                                   INKCS004
00592      INITIALIZE ASC-SPECIFIC-COMMAREA.                            INKCS004
00593      MOVE DP020-INT-APPL-FORMAT-IND                               INKCS004
00594                                    TO  ASC-INT-APPL-FORMAT-IND.   INKCS004
00595                                                                   INKCS004
00596      EVALUATE DP020-INT-APPL-FORMAT-IND                           INKCS004
00597          WHEN PC-INTER-APPL-CRIT-SPEC                             INKCS004
00598               IF (IN001-STORE-NBR NUMERIC                         INKCS004
00599               AND IN001-AREA-NBR NUMERIC)                         INKCS004
00600                   MOVE IN001-STORE-NBR                            INKCS004
00601                                    TO  ASC-KEY-STORE-NBR          INKCS004
00602                   MOVE IN001-AREA-NBR                             INKCS004
00603                                    TO  ASC-KEY-AREA-NBR           INKCS004
00604               ELSE                                                INKCS004
00605                   MOVE PC-TSYMSG-00148                            INKCS004
00606                                    TO  DP020-MSG-NUMBER           INKCS004
00607                   SET DP020-MSG-FATAL                             INKCS004
00608 *                     DP020-NEXT-ACT-APPL-ERROR                   INKCS004
00609                       DP020-FK-RETURN (DP020-SRC-AID)             INKCS004
00610                       PS-ERROR                                    INKCS004
00611                                    TO  TRUE                       INKCS004
00612               END-IF                                              INKCS004
00613                                                                   INKCS004
00614          WHEN PC-INTER-APPL-LIST                                  INKCS004
00615               SET ASC-USE-SELECTION-QUEUE TO TRUE                 INKCS004
00616               MOVE IN001-NUMBER-OF-SELECTED-ITEMS                 INKCS004
00617                                    TO ASC-NUMBER-OF-SELECTED-ITEMSINKCS004
00618               MOVE IN001-NUMBER-OF-TS-ITEMS                       INKCS004
00619                                    TO  ASC-NUMBER-OF-TS-ITEMS     INKCS004
00620               MOVE IN001-SEL-QUEUE-NAME                           INKCS004
00621                                    TO  ASC-SEL-QUEUE-NAME         INKCS004
00622               PERFORM 1120-PREPARE-LIST-ENTRY                     INKCS004
00623                                                                   INKCS004
00624          WHEN OTHER                                               INKCS004
00625               MOVE PC-TSYMSG-00148 TO  DP020-MSG-NUMBER           INKCS004
00626               SET DP020-MSG-FATAL                                 INKCS004
00627 *                 DP020-NEXT-ACT-APPL-ERROR                       INKCS004
00628                   DP020-FK-RETURN (DP020-SRC-AID)                 INKCS004
00629                   PS-ERROR                                        INKCS004
00630                                    TO TRUE                        INKCS004
00631      END-EVALUATE.                                                INKCS004
00632                                                                   INKCS004
00633 *----------------------------------------------------------------*INKCS004
00634 * ENTRY IS FROM A LIST.  GET THE MESSAGE NUMBER FROM THE TEMP    *INKCS004
00635 * STORAGE QUEUE PASSED FROM THE LIST.                            *INKCS004
00636 *----------------------------------------------------------------*INKCS004
00637                                                                   INKCS004
00638  1120-PREPARE-LIST-ENTRY.                                         INKCS004
00639                                                                   INKCS004
00640      MOVE IN002-MAX-ITEMS          TO ASC-SEL-SUB.                INKCS004
00641      MOVE ZERO                     TO ASC-SEL-ITEM.               INKCS004
00642      PERFORM 2101-GET-NEXT-AREA.                                  INKCS004
00643                                                                   INKCS004
00644 /                                                                 INKCS004
00645 *----------------------------------------------------------------*INKCS004
00646 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS004
00647 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS004
00648 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS004
00649 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS004
00650 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS004
00651 *----------------------------------------------------------------*INKCS004
00652                                                                   INKCS004
00653  2000-PROCESS-PANEL.                                              INKCS004
00654                                                                   INKCS004
00655      IF  IN001-STORE-NBR NOT NUMERIC                              INKCS004
00656          MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA         INKCS004
00657      END-IF.                                                      INKCS004
00658                                                                   INKCS004
00659      EVALUATE TRUE                                                INKCS004
00660          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS004
00661              MOVE ASC-HOLD-INTER-APPL                             INKCS004
00662                                    TO INTER-APPL-COMM-AREA        INKCS004
00663              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS004
00664              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS004
00665              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00666                                                                   INKCS004
00667          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS004
00668              MOVE ASC-HOLD-INTER-APPL                             INKCS004
00669                                    TO INTER-APPL-COMM-AREA        INKCS004
00670              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS004
00671              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS004
00672                                                                   INKCS004
00673          WHEN DP020-FK-DESELECT(DP020-SRC-AID)                    INKCS004
00674              PERFORM 2140-DESELECT-ALL-ITEMS                      INKCS004
00675              PERFORM 3200-RESET-ATTRIBUTES                        INKCS004
00676              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00677                                                                   INKCS004
00678          WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                  INKCS004
00679              PERFORM 2150-SELECT-ALL-ITEMS                        INKCS004
00680              PERFORM 3200-RESET-ATTRIBUTES                        INKCS004
00681              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00682                                                                   INKCS004
00683          WHEN OTHER                                               INKCS004
00684               PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                INKCS004
00685                   PERFORM 3000-EDIT-DATA-IN-COMMAREA              INKCS004
00686                   IF  PS-NO-ERROR                                 INKCS004
00687                       SET DP030-CONTINUE-GO                       INKCS004
00688                                  TO TRUE                          INKCS004
00689                       PERFORM 2100-CHECK-FUNCTION-KEY             INKCS004
00690                   ELSE                                            INKCS004
00691                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN        INKCS004
00692                       SET DP030-OVERRIDE-GO                       INKCS004
00693                                  TO TRUE                          INKCS004
00694                   END-IF                                          INKCS004
00695      END-EVALUATE.                                                INKCS004
00696  EJECT                                                            INKCS004
00697 *----------------------------------------------------------------*INKCS004
00698 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS004
00699 *----------------------------------------------------------------*INKCS004
00700  2050-DELETE-LIST-QUEUE.                                          INKCS004
00701                                                                   INKCS004
00702      EXEC CICS                                                    INKCS004
00703          DELETEQ TS                                               INKCS004
00704              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
00705              RESP  (PV-CICS-RESP)                                 INKCS004
00706      END-EXEC.                                                    INKCS004
00707                                                                   INKCS004
00708      IF (PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS004
00709      OR  PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS004
00710          CONTINUE                                                 INKCS004
00711      ELSE                                                         INKCS004
00712          SET DP013-CICS-ABEND      TO TRUE                        INKCS004
00713          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS004
00714                                                                   INKCS004
00715          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS004
00716                                    TO DP013-PARAGRAPH             INKCS004
00717          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS004
00718                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
00719          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
00720      END-IF.                                                      INKCS004
00721                                                                   INKCS004
00722      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS004
00723 /                                                                 INKCS004
00724 *----------------------------------------------------------------*INKCS004
00725 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS004
00726 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS004
00727 * CICS ARCHITECTURE API.                                         *INKCS004
00728 *----------------------------------------------------------------*INKCS004
00729                                                                   INKCS004
00730  2100-CHECK-FUNCTION-KEY.                                         INKCS004
00731                                                                   INKCS004
00732      EVALUATE TRUE                                                INKCS004
00733          WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                  INKCS004
00734              IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              INKCS004
00735                  MOVE ASC-NUMBER-OF-SELECTED-ITEMS                INKCS004
00736                                 TO IN003-SELECTED-ITEMS           INKCS004
00737                  PERFORM 5000-PREPARE-SEL-QUEUE                   INKCS004
00738              ELSE                                                 INKCS004
00739                  SET DP030-OVERRIDE-GO                            INKCS004
00740                                    TO TRUE                        INKCS004
00741                  MOVE PC-TSYMSG-00057                             INKCS004
00742                                    TO DP020-MSG-NUMBER            INKCS004
00743                  SET DP020-MSG-WARNING                            INKCS004
00744                                    TO TRUE                        INKCS004
00745              END-IF                                               INKCS004
00746                                                                   INKCS004
00747          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS004
00748              MOVE +1               TO PV-TOP-ITEM                 INKCS004
00749              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00750                                                                   INKCS004
00751          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS004
00752              PERFORM 2110-BROWSE-BACKWARD                         INKCS004
00753              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00754                                                                   INKCS004
00755          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS004
00756              PERFORM 2120-BROWSE-FORWARD                          INKCS004
00757              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00758                                                                   INKCS004
00759          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             INKCS004
00760               IF  ASC-USE-SELECTION-QUEUE                         INKCS004
00761                   PERFORM 2101-GET-NEXT-AREA                      INKCS004
00762                   PERFORM 3200-RESET-ATTRIBUTES                   INKCS004
00763                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS004
00764               ELSE                                                INKCS004
00765 *                 --- BOTTOM OF LIST ---                          INKCS004
00766                   MOVE PC-TSYMSG-00070                            INKCS004
00767                                    TO DP020-MSG-NUMBER            INKCS004
00768                   SET DP020-MSG-INFORMATIONAL                     INKCS004
00769                                    TO TRUE                        INKCS004
00770                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            INKCS004
00771               END-IF                                              INKCS004
00772                                                                   INKCS004
00773          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)             INKCS004
00774               IF  ASC-USE-SELECTION-QUEUE                         INKCS004
00775                   PERFORM 2102-GET-PREV-AREA                      INKCS004
00776                   PERFORM 3200-RESET-ATTRIBUTES                   INKCS004
00777                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS004
00778               ELSE                                                INKCS004
00779 *                 --- TOP OF LIST ---                             INKCS004
00780                   MOVE PC-TSYMSG-00069                            INKCS004
00781                                    TO DP020-MSG-NUMBER            INKCS004
00782                   SET DP020-MSG-INFORMATIONAL                     INKCS004
00783                                    TO TRUE                        INKCS004
00784                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            INKCS004
00785               END-IF                                              INKCS004
00786                                                                   INKCS004
00787          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS004
00788              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS004
00789              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS004
00790                                                                   INKCS004
00791          WHEN OTHER                                               INKCS004
00792              SET DP013-NO-ROLLBACK                                INKCS004
00793                  DP013-XCTL-DISPLAY-RESTART                       INKCS004
00794                  DP013-CICS-ABEND  TO TRUE                        INKCS004
00795              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS004
00796                                    TO DP013-PARAGRAPH             INKCS004
00797              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS004
00798                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
00799              PERFORM DP013-0000-PROCESS-ABEND                     INKCS004
00800      END-EVALUATE.                                                INKCS004
00801                                                                   INKCS004
00802 *----------------------------------------------------------------*INKCS004
00803 *    RETRIEVE THE NEXT KEY FOR PROCESSING.                       *INKCS004
00804 *----------------------------------------------------------------*INKCS004
00805                                                                   INKCS004
00806  2101-GET-NEXT-AREA.                                              INKCS004
00807                                                                   INKCS004
00808      ADD +1                        TO  ASC-SEL-SUB.               INKCS004
00809      IF  ASC-SEL-SUB > IN002-MAX-ITEMS                            INKCS004
00810          ADD +1                    TO  ASC-SEL-ITEM               INKCS004
00811          MOVE +1                   TO  ASC-SEL-SUB                INKCS004
00812      END-IF.                                                      INKCS004
00813                                                                   INKCS004
00814      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                INKCS004
00815          PERFORM 9100-READ-SEL-QUEUE                              INKCS004
00816          IF  IN002-ITEM (ASC-SEL-SUB) > ZERO                      INKCS004
00817              MOVE IN002-AREA-NBR (ASC-SEL-SUB)                    INKCS004
00818                                    TO  ASC-KEY-AREA-NBR           INKCS004
00819              SET IN002-INQ (ASC-SEL-SUB)                          INKCS004
00820                                    TO  TRUE                       INKCS004
00821              PERFORM 9200-REWRITE-LIST-QUEUE                      INKCS004
00822          ELSE                                                     INKCS004
00823              SUBTRACT +1 FROM ASC-SEL-SUB                         INKCS004
00824 *            --- BOTTOM OF LIST ---                               INKCS004
00825              MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER           INKCS004
00826              SET DP020-MSG-INFORMATIONAL                          INKCS004
00827                                    TO  TRUE                       INKCS004
00828          END-IF                                                   INKCS004
00829      ELSE                                                         INKCS004
00830          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS004
00831          MOVE IN002-MAX-ITEMS      TO  ASC-SEL-SUB                INKCS004
00832          MOVE PC-TSYMSG-00070      TO  DP020-MSG-NUMBER           INKCS004
00833          SET DP020-MSG-INFORMATIONAL                              INKCS004
00834                                    TO  TRUE                       INKCS004
00835      END-IF.                                                      INKCS004
00836                                                                   INKCS004
00837 *----------------------------------------------------------------*INKCS004
00838 *  GET THE MESSAGE NUMBER (KEY) OF THE PREVIOUS SELECTED         *INKCS004
00839 *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.           *INKCS004
00840 *----------------------------------------------------------------*INKCS004
00841                                                                   INKCS004
00842  2102-GET-PREV-AREA.                                              INKCS004
00843                                                                   INKCS004
00844      SUBTRACT +1 FROM ASC-SEL-SUB.                                INKCS004
00845      IF  ASC-SEL-SUB < +1                                         INKCS004
00846          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS004
00847          MOVE IN002-MAX-ITEMS      TO ASC-SEL-SUB                 INKCS004
00848      END-IF.                                                      INKCS004
00849                                                                   INKCS004
00850      IF  ASC-SEL-ITEM > ZERO                                      INKCS004
00851          PERFORM 9100-READ-SEL-QUEUE                              INKCS004
00852          MOVE IN002-AREA-NBR (ASC-SEL-SUB)                        INKCS004
00853                                    TO ASC-KEY-AREA-NBR            INKCS004
00854      ELSE                                                         INKCS004
00855          MOVE +1                   TO ASC-SEL-ITEM                INKCS004
00856                                       ASC-SEL-SUB                 INKCS004
00857 *        --- TOP OF LIST ---                                      INKCS004
00858          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS004
00859          SET DP020-MSG-INFORMATIONAL                              INKCS004
00860                                    TO TRUE                        INKCS004
00861      END-IF.                                                      INKCS004
00862                                                                   INKCS004
00863 *----------------------------------------------------------------*INKCS004
00864 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS004
00865 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS004
00866 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS004
00867 *    MESSAGE.                                                    *INKCS004
00868 *----------------------------------------------------------------*INKCS004
00869                                                                   INKCS004
00870  2110-BROWSE-BACKWARD.                                            INKCS004
00871                                                                   INKCS004
00872      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS004
00873          GIVING PV-TOP-ITEM.                                      INKCS004
00874                                                                   INKCS004
00875      IF  ((PV-TOP-ITEM < +1)                                      INKCS004
00876        OR (PV-TOP-ITEM = +1))                                     INKCS004
00877          MOVE +1                   TO PV-TOP-ITEM                 INKCS004
00878 *        -- TOP OF LIST --                                        INKCS004
00879          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS004
00880          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS004
00881      END-IF.                                                      INKCS004
00882                                                                   INKCS004
00883 /                                                                 INKCS004
00884 *----------------------------------------------------------------*INKCS004
00885 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS004
00886 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS004
00887 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS004
00888 *----------------------------------------------------------------*INKCS004
00889                                                                   INKCS004
00890  2120-BROWSE-FORWARD.                                             INKCS004
00891                                                                   INKCS004
00892      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS004
00893          GIVING PV-TOP-ITEM.                                      INKCS004
00894      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS004
00895          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS004
00896                                    TO PV-TOP-ITEM                 INKCS004
00897 *        -- BOTTOM OF LIST --                                     INKCS004
00898          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS004
00899          SET DP020-MSG-INFORMATIONAL                              INKCS004
00900                                    TO TRUE                        INKCS004
00901      ELSE                                                         INKCS004
00902          COMPUTE PV-BOTTOM-ITEM =                                 INKCS004
00903              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS004
00904          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS004
00905 *            -- BOTTOM OF LIST --                                 INKCS004
00906              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS004
00907              SET DP020-MSG-INFORMATIONAL                          INKCS004
00908                                    TO TRUE                        INKCS004
00909          END-IF                                                   INKCS004
00910      END-IF.                                                      INKCS004
00911 /                                                                 INKCS004
00912 *----------------------------------------------------------------*INKCS004
00913 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS004
00914 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS004
00915 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS004
00916 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS004
00917 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS004
00918 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS004
00919 *----------------------------------------------------------------*INKCS004
00920                                                                   INKCS004
00921  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS004
00922                                                                   INKCS004
00923      SET PS-NO-ERROR               TO TRUE.                       INKCS004
00924      IF (ASC-START-ITEM-REQUEST = SPACE)                          INKCS004
00925          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS004
00926                                    TO PV-TOP-ITEM                 INKCS004
00927      ELSE                                                         INKCS004
00928          MOVE ASC-START-ITEM-REQUEST                              INKCS004
00929                                    TO DP010I-UNEDITED-FIELD       INKCS004
00930          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS004
00931          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS004
00932          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS004
00933                                    TO TRUE                        INKCS004
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00935                                                                   INKCS004
00936          IF  NOT DP010I-ERROR-DETECTED                            INKCS004
00937              MOVE DP010I-NUMERIC-FIELD                            INKCS004
00938                                    TO  ASC-START-ITEM-REQUEST-N   INKCS004
00939              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS004
00940                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS004
00941                                           ASC-NUMBER-OF-ITEMS-READINKCS004
00942                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS004
00943                      COMPUTE PV-TOP-ITEM =                        INKCS004
00944                              ASC-NUMBER-OF-ITEMS-READ             INKCS004
00945                              - PC-ITEMS-PER-PANEL + 1             INKCS004
00946                      IF  PV-TOP-ITEM < +1                         INKCS004
00947                          MOVE +1   TO PV-TOP-ITEM                 INKCS004
00948                          MOVE PC-TSYMSG-00070                     INKCS004
00949                                    TO DP020-MSG-NUMBER            INKCS004
00950                          SET DP020-MSG-INFORMATIONAL              INKCS004
00951                                    TO TRUE                        INKCS004
00952                      END-IF                                       INKCS004
00953                  ELSE                                             INKCS004
00954                      MOVE ASC-START-ITEM-REQUEST-N                INKCS004
00955                                    TO PV-TOP-ITEM                 INKCS004
00956                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS004
00957                      COMPUTE PV-BOTTOM-ITEM =                     INKCS004
00958                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS004
00959                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS004
00960 *                        -- BOTTOM OF LIST --                     INKCS004
00961                          MOVE PC-TSYMSG-00070                     INKCS004
00962                                    TO DP020-MSG-NUMBER            INKCS004
00963                          SET DP020-MSG-INFORMATIONAL              INKCS004
00964                                    TO TRUE                        INKCS004
00965                      END-IF                                       INKCS004
00966                  END-IF                                           INKCS004
00967              ELSE                                                 INKCS004
00968 *                --- VALUE OUT OF RANGE ----                      INKCS004
00969                  MOVE PC-TSYMSG-00101                             INKCS004
00970                                    TO DP020-MSG-NUMBER            INKCS004
00971                  MOVE -1           TO ASTRTITL                    INKCS004
00972                  SET DP030-SET-CURSOR-APPL-1                      INKCS004
00973                                    TO TRUE                        INKCS004
00974                  MOVE DP015-REVERSE                               INKCS004
00975                                    TO ASTRTITH                    INKCS004
00976                  MOVE DP015-RED    TO ASTRTITC                    INKCS004
00977                  MOVE DP015-UNP-NUM-BRT-OFF                       INKCS004
00978                                    TO ASTRTITA                    INKCS004
00979                  SET DP020-MSG-FATAL                              INKCS004
00980                                    TO TRUE                        INKCS004
00981                  SET PS-ERROR      TO TRUE                        INKCS004
00982              END-IF                                               INKCS004
00983                                                                   INKCS004
00984          ELSE                                                     INKCS004
00985              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS004
00986              MOVE -1               TO ASTRTITL                    INKCS004
00987              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  INKCS004
00988              MOVE DP015-REVERSE    TO ASTRTITH                    INKCS004
00989              MOVE DP015-RED        TO ASTRTITC                    INKCS004
00990              MOVE DP015-UNP-NUM-BRT-OFF                           INKCS004
00991                                    TO ASTRTITA                    INKCS004
00992              SET DP020-MSG-FATAL   TO TRUE                        INKCS004
00993              SET PS-ERROR          TO TRUE                        INKCS004
00994          END-IF                                                   INKCS004
00995      END-IF.                                                      INKCS004
00996 /                                                                 INKCS004
00997 *----------------------------------------------------------------*INKCS004
00998 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *INKCS004
00999 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *INKCS004
01000 *----------------------------------------------------------------*INKCS004
01001                                                                   INKCS004
01002  2140-DESELECT-ALL-ITEMS.                                         INKCS004
01003                                                                   INKCS004
01004      MOVE +1                       TO ASC-BLK-SUB.                INKCS004
01005                                                                   INKCS004
01006      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS004
01007          VARYING ASC-ITEM-SUB                                     INKCS004
01008          FROM 1 BY 1                                              INKCS004
01009          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01010                                                                   INKCS004
01011      ADD +1                        TO ASC-BLK-SUB.                INKCS004
01012                                                                   INKCS004
01013      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS004
01014          VARYING ASC-ITEM-SUB                                     INKCS004
01015          FROM 1 BY 1                                              INKCS004
01016          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01017                                                                   INKCS004
01018      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS004
01019                                    TO PV-LIST-ITEM-NUMBER.        INKCS004
01020                                                                   INKCS004
01021      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS004
01022          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS004
01023                                                                   INKCS004
01024      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS004
01025         (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)      INKCS004
01026          MOVE +1                   TO ASC-BLK-SUB                 INKCS004
01027                                                                   INKCS004
01028          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS004
01029                                                                   INKCS004
01030          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS004
01031                                                                   INKCS004
01032          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS004
01033              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS004
01034                                                                   INKCS004
01035          PERFORM                                                  INKCS004
01036              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS004
01037                                                                   INKCS004
01038              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01039                                                                   INKCS004
01040              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS004
01041                  VARYING ASC-ITEM-SUB FROM 1 BY 1                 INKCS004
01042                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01043                                                                   INKCS004
01044              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01045                                                                   INKCS004
01046              ADD +1                TO PV-BLOCK-NUMBER             INKCS004
01047                                                                   INKCS004
01048          END-PERFORM                                              INKCS004
01049                                                                   INKCS004
01050          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS004
01051              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS004
01052                                                                   INKCS004
01053          PERFORM                                                  INKCS004
01054              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS004
01055                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS004
01056                                                                   INKCS004
01057              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS004
01058                                    TO PV-BLOCK-NUMBER             INKCS004
01059                                                                   INKCS004
01060              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01061                                                                   INKCS004
01062              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS004
01063                  VARYING ASC-ITEM-SUB                             INKCS004
01064                  FROM 1 BY 1                                      INKCS004
01065                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01066                                                                   INKCS004
01067              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01068                                                                   INKCS004
01069              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS004
01070                                                                   INKCS004
01071          END-PERFORM                                              INKCS004
01072                                                                   INKCS004
01073          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS004
01074                                    TO PV-BLOCK-NUMBER             INKCS004
01075                                                                   INKCS004
01076          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
01077      END-IF.                                                      INKCS004
01078                                                                   INKCS004
01079      MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS. INKCS004
01080      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.INKCS004
01081      MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.  INKCS004
01082                                                                   INKCS004
01083      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS004
01084                                                                   INKCS004
01085 /                                                                 INKCS004
01086 *----------------------------------------------------------------*INKCS004
01087 *    MARK EACH ITEM AS BEING NOT SELECTED.                       *INKCS004
01088 *----------------------------------------------------------------*INKCS004
01089                                                                   INKCS004
01090  2141-CLEAR-SELECTED-INDS.                                        INKCS004
01091                                                                   INKCS004
01092      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS004
01093              NOT EQUAL ZERO                                       INKCS004
01094          IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS004
01095              SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS004
01096                  ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
01097                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS004
01098                                    TO TRUE                        INKCS004
01099          END-IF                                                   INKCS004
01100      END-IF.                                                      INKCS004
01101                                                                   INKCS004
01102 /                                                                 INKCS004
01103 *----------------------------------------------------------------*INKCS004
01104 *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *INKCS004
01105 *  FOR ALL ITEMS.                                                *INKCS004
01106 *----------------------------------------------------------------*INKCS004
01107                                                                   INKCS004
01108  2150-SELECT-ALL-ITEMS.                                           INKCS004
01109                                                                   INKCS004
01110      MOVE +1                       TO ASC-BLK-SUB.                INKCS004
01111                                                                   INKCS004
01112      PERFORM 2151-SET-SELECTED-INDS                               INKCS004
01113          VARYING ASC-ITEM-SUB                                     INKCS004
01114          FROM 1 BY 1                                              INKCS004
01115          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01116                                                                   INKCS004
01117      ADD +1                        TO ASC-BLK-SUB.                INKCS004
01118                                                                   INKCS004
01119      PERFORM 2151-SET-SELECTED-INDS                               INKCS004
01120          VARYING ASC-ITEM-SUB                                     INKCS004
01121          FROM 1 BY 1                                              INKCS004
01122          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01123                                                                   INKCS004
01124      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS004
01125                                    TO PV-LIST-ITEM-NUMBER.        INKCS004
01126                                                                   INKCS004
01127      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS004
01128          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS004
01129                                                                   INKCS004
01130      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS004
01131         (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)    INKCS004
01132          MOVE +1                   TO ASC-BLK-SUB                 INKCS004
01133                                                                   INKCS004
01134          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS004
01135                                                                   INKCS004
01136          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS004
01137                                                                   INKCS004
01138          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS004
01139              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS004
01140                                                                   INKCS004
01141          PERFORM                                                  INKCS004
01142              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS004
01143                                                                   INKCS004
01144              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01145                                                                   INKCS004
01146              PERFORM 2151-SET-SELECTED-INDS                       INKCS004
01147                  VARYING ASC-ITEM-SUB                             INKCS004
01148                  FROM 1 BY 1                                      INKCS004
01149                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01150                                                                   INKCS004
01151              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01152                                                                   INKCS004
01153              ADD +1                TO PV-BLOCK-NUMBER             INKCS004
01154                                                                   INKCS004
01155          END-PERFORM                                              INKCS004
01156                                                                   INKCS004
01157          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS004
01158              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS004
01159                                                                   INKCS004
01160          PERFORM                                                  INKCS004
01161              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS004
01162                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS004
01163                                                                   INKCS004
01164              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS004
01165                                    TO PV-BLOCK-NUMBER             INKCS004
01166                                                                   INKCS004
01167              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01168                                                                   INKCS004
01169              PERFORM 2151-SET-SELECTED-INDS                       INKCS004
01170                  VARYING ASC-ITEM-SUB                             INKCS004
01171                  FROM 1 BY 1                                      INKCS004
01172                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01173                                                                   INKCS004
01174              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01175                                                                   INKCS004
01176              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS004
01177                                                                   INKCS004
01178          END-PERFORM                                              INKCS004
01179                                                                   INKCS004
01180          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS004
01181                                    TO PV-BLOCK-NUMBER             INKCS004
01182                                                                   INKCS004
01183          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
01184      END-IF.                                                      INKCS004
01185                                                                   INKCS004
01186                                                                   INKCS004
01187      MOVE ASC-NUMBER-OF-ITEMS-READ TO                             INKCS004
01188                        ASC-NUMBER-OF-SELECTED-ITEMS.              INKCS004
01189                                                                   INKCS004
01190      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS004
01191                                                                   INKCS004
01192 /                                                                 INKCS004
01193 *----------------------------------------------------------------*INKCS004
01194 *    MARK EACH ITEM AS BEING SELECTED.                           *INKCS004
01195 *----------------------------------------------------------------*INKCS004
01196                                                                   INKCS004
01197  2151-SET-SELECTED-INDS.                                          INKCS004
01198                                                                   INKCS004
01199      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS004
01200              NOT EQUAL ZERO                                       INKCS004
01201          IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS004
01202              SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS004
01203                  ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)        INKCS004
01204                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS004
01205                                TO  TRUE                           INKCS004
01206          END-IF                                                   INKCS004
01207      END-IF.                                                      INKCS004
01208                                                                   INKCS004
01209 *----------------------------------------------------------------*INKCS004
01210 *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *INKCS004
01211 *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *INKCS004
01212 *----------------------------------------------------------------*INKCS004
01213                                                                   INKCS004
01214  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS004
01215                                                                   INKCS004
01216      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS004
01217      MOVE +1                       TO PV-SUB.                     INKCS004
01218                                                                   INKCS004
01219      IF  ASTRTITL > ZERO                                          INKCS004
01220          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST          INKCS004
01221      ELSE                                                         INKCS004
01222          IF  ASTRTITF EQUAL DP015-ERASE-EOF                       INKCS004
01223              INITIALIZE ASC-START-ITEM-REQUEST                    INKCS004
01224          END-IF                                                   INKCS004
01225      END-IF.                                                      INKCS004
01226                                                                   INKCS004
01227      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS004
01228                                                                   INKCS004
01229      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          INKCS004
01230          VARYING PV-SUB                                           INKCS004
01231          FROM 1 BY 1                                              INKCS004
01232          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      INKCS004
01233                                                                   INKCS004
01234 ******************************************************************INKCS004
01235 ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**INKCS004
01236 ******************************************************************INKCS004
01237                                                                   INKCS004
01238  2210-MOVE-LINES-TO-COMMAREA.                                     INKCS004
01239                                                                   INKCS004
01240      IF  MR-SELECTL (PV-SUB) > ZERO                               INKCS004
01241          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS004
01242          MOVE MR-SELECTI (PV-SUB)                                 INKCS004
01243                                    TO ASC-TEMP-SELECTED-SW        INKCS004
01244                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS004
01245      ELSE                                                         INKCS004
01246          IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                INKCS004
01247                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE     INKCS004
01248                  MOVE SPACE        TO ASC-TEMP-SELECTED-SW        INKCS004
01249                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS004
01250          END-IF                                                   INKCS004
01251      END-IF.                                                      INKCS004
01252                                                                   INKCS004
01253      ADD +1                        TO ASC-ITEM-SUB.               INKCS004
01254      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS004
01255          ADD +1                    TO ASC-BLK-SUB                 INKCS004
01256          MOVE +1                   TO ASC-ITEM-SUB                INKCS004
01257      END-IF.                                                      INKCS004
01258                                                                   INKCS004
01259 *----------------------------------------------------------------*INKCS004
01260 *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *INKCS004
01261 *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *INKCS004
01262 *----------------------------------------------------------------*INKCS004
01263                                                                   INKCS004
01264  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS004
01265                                                                   INKCS004
01266      SET PS-NO-ERROR               TO TRUE.                       INKCS004
01267      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS004
01268      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS004
01269 **                                                                INKCS004
01270 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS004
01271 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS004
01272 **                                                                INKCS004
01273      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS004
01274          ASC-ITEMS-DISPLAYED - 1.                                 INKCS004
01275      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS004
01276      PERFORM 3300-EDIT-SELECTIONS                                 INKCS004
01277          VARYING PV-SUB                                           INKCS004
01278          FROM PV-SUB BY -1                                        INKCS004
01279          UNTIL PV-SUB < +1                                        INKCS004
01280                                                                   INKCS004
01281      IF  PS-NO-ERROR                                              INKCS004
01282          PERFORM 3400-CHECK-RANGE-SEQUENCE                        INKCS004
01283      END-IF.                                                      INKCS004
01284                                                                   INKCS004
01285      IF  ((PS-NO-ERROR)                                           INKCS004
01286        AND (ASC-START-OF-SELECTED-RANGE > ZERO)                   INKCS004
01287        AND (ASC-END-OF-SELECTED-RANGE > ZERO))                    INKCS004
01288              PERFORM 3500-MARK-RANGE-AS-SELECTED                  INKCS004
01289      END-IF.                                                      INKCS004
01290 /                                                                 INKCS004
01291 *----------------------------------------------------------------*INKCS004
01292 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS004
01293 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS004
01294 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS004
01295 *                                                                *INKCS004
01296 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS004
01297 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS004
01298 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS004
01299 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS004
01300 *----------------------------------------------------------------*INKCS004
01301                                                                   INKCS004
01302  3100-SET-SUBSCRIPTS.                                             INKCS004
01303                                                                   INKCS004
01304      COMPUTE ASC-ITEM-SUB =                                       INKCS004
01305          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS004
01306                                                                   INKCS004
01307      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS004
01308          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS004
01309                                                                   INKCS004
01310          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS004
01311      ELSE                                                         INKCS004
01312          MOVE +1                     TO ASC-BLK-SUB               INKCS004
01313      END-IF.                                                      INKCS004
01314                                                                   INKCS004
01315 *----------------------------------------------------------------*INKCS004
01316 *  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *INKCS004
01317 *----------------------------------------------------------------*INKCS004
01318                                                                   INKCS004
01319  3200-RESET-ATTRIBUTES.                                           INKCS004
01320                                                                   INKCS004
01321      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS004
01322      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS004
01323      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS004
01324                                                                   INKCS004
01325      PERFORM                                                      INKCS004
01326          VARYING PV-SUB                                           INKCS004
01327          FROM 1 BY 1                                              INKCS004
01328          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        INKCS004
01329          MOVE DP015-UNP-ALP-NOR-OFF                               INKCS004
01330                                    TO MR-SELECTA (PV-SUB)         INKCS004
01331          MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)         INKCS004
01332          MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)         INKCS004
01333      END-PERFORM.                                                 INKCS004
01334                                                                   INKCS004
01335 *----------------------------------------------------------------*INKCS004
01336 *  EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS     *INKCS004
01337 *  WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'    *INKCS004
01338 *  IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR     *INKCS004
01339 *  WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.              *INKCS004
01340 *----------------------------------------------------------------*INKCS004
01341                                                                   INKCS004
01342  3300-EDIT-SELECTIONS.                                            INKCS004
01343                                                                   INKCS004
01344      EVALUATE TRUE                                                INKCS004
01345          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS004
01346              = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS004
01347                  CONTINUE                                         INKCS004
01348                                                                   INKCS004
01349          WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS004
01350              IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS004
01351                  ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS   INKCS004
01352                  MOVE ASC-TEMP-SELECTED-SW                        INKCS004
01353                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01354                                 TO ASC-SELECTED-SW                INKCS004
01355                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01356              END-IF                                               INKCS004
01357                                                                   INKCS004
01358          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS004
01359                  = SPACE                                          INKCS004
01360              EVALUATE TRUE                                        INKCS004
01361                  WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS004
01362                      SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMSINKCS004
01363                      MOVE SPACE TO ASC-SELECTED-SW                INKCS004
01364                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01365                  WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS004
01366                      MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE    INKCS004
01367                      MOVE SPACE TO ASC-SELECTED-SW                INKCS004
01368                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01369                  WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS004
01370                      MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE      INKCS004
01371                      MOVE SPACE TO ASC-SELECTED-SW                INKCS004
01372                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01373              END-EVALUATE                                         INKCS004
01374                                                                   INKCS004
01375          WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS004
01376              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS004
01377                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS004
01378              END-IF                                               INKCS004
01379              PERFORM 3310-EDIT-RANGE                              INKCS004
01380                                                                   INKCS004
01381          WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)             INKCS004
01382              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS004
01383                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS004
01384              END-IF                                               INKCS004
01385              PERFORM 3310-EDIT-RANGE                              INKCS004
01386                                                                   INKCS004
01387          WHEN OTHER                                               INKCS004
01388              MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER           INKCS004
01389              SET DP020-MSG-FATAL    TO TRUE                       INKCS004
01390              MOVE -1                TO MR-SELECTL (PV-SUB)        INKCS004
01391              SET DP030-SET-CURSOR-APPL-1                          INKCS004
01392                                     TO TRUE                       INKCS004
01393              MOVE DP015-RED         TO MR-SELECTC (PV-SUB)        INKCS004
01394              MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)        INKCS004
01395              SET PS-ERROR TO TRUE                                 INKCS004
01396              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS004
01397                  SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS     INKCS004
01398                  MOVE SPACE         TO ASC-SELECTED-SW            INKCS004
01399                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS004
01400              END-IF                                               INKCS004
01401                                                                   INKCS004
01402      END-EVALUATE.                                                INKCS004
01403                                                                   INKCS004
01404      SUBTRACT +1             FROM PV-ITEM-IN-USE.                 INKCS004
01405      SUBTRACT +1 FROM ASC-ITEM-SUB.                               INKCS004
01406      IF  ASC-ITEM-SUB < 1                                         INKCS004
01407          SUBTRACT +1 FROM ASC-BLK-SUB                             INKCS004
01408          MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                INKCS004
01409      END-IF.                                                      INKCS004
01410                                                                   INKCS004
01411 *----------------------------------------------------------------*INKCS004
01412 *  IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A       *INKCS004
01413 *  RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1     *INKCS004
01414 *  ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) *INKCS004
01415 *  THE ENDING.                                                   *INKCS004
01416 *----------------------------------------------------------------*INKCS004
01417                                                                   INKCS004
01418  3310-EDIT-RANGE.                                                 INKCS004
01419                                                                   INKCS004
01420      IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                INKCS004
01421          IF  ASC-START-OF-SELECTED-RANGE = ZERO                   INKCS004
01422              MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)INKCS004
01423                                    TO ASC-SELECTED-SW             INKCS004
01424                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS004
01425              MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE INKCS004
01426          ELSE                                                     INKCS004
01427              IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE     INKCS004
01428                  CONTINUE                                         INKCS004
01429              ELSE                                                 INKCS004
01430 *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --        INKCS004
01431                  MOVE PC-TSYMSG-00054                             INKCS004
01432                                    TO DP020-MSG-NUMBER            INKCS004
01433                  SET DP020-MSG-FATAL                              INKCS004
01434                                    TO TRUE                        INKCS004
01435                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS004
01436                  SET DP030-SET-CURSOR-APPL-1                      INKCS004
01437                                    TO TRUE                        INKCS004
01438                  MOVE DP015-RED    TO MR-SELECTC (PV-SUB)         INKCS004
01439                  MOVE DP015-UNDERLINE                             INKCS004
01440                                    TO MR-SELECTH (PV-SUB)         INKCS004
01441                  SET PS-ERROR      TO TRUE                        INKCS004
01442              END-IF                                               INKCS004
01443          END-IF                                                   INKCS004
01444      ELSE                                                         INKCS004
01445          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS004
01446              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01447                                    TO ASC-SELECTED-SW             INKCS004
01448                                      (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS004
01449              MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE   INKCS004
01450          ELSE                                                     INKCS004
01451              IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE       INKCS004
01452                  CONTINUE                                         INKCS004
01453              ELSE                                                 INKCS004
01454 *                -- ONLY ONE END-OF-RANGE ALLOWED --              INKCS004
01455                  MOVE PC-TSYMSG-00055                             INKCS004
01456                                    TO DP020-MSG-NUMBER            INKCS004
01457                  SET DP020-MSG-FATAL                              INKCS004
01458                                    TO TRUE                        INKCS004
01459                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS004
01460                  SET DP030-SET-CURSOR-APPL-1                      INKCS004
01461                                    TO TRUE                        INKCS004
01462                  SET PS-ERROR      TO TRUE                        INKCS004
01463              END-IF                                               INKCS004
01464          END-IF                                                   INKCS004
01465      END-IF.                                                      INKCS004
01466                                                                   INKCS004
01467 *----------------------------------------------------------------*INKCS004
01468 *  IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,     *INKCS004
01469 *  MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.   *INKCS004
01470 *----------------------------------------------------------------*INKCS004
01471                                                                   INKCS004
01472  3400-CHECK-RANGE-SEQUENCE.                                       INKCS004
01473                                                                   INKCS004
01474      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS004
01475        AND  ASC-END-OF-SELECTED-RANGE > ZERO                      INKCS004
01476        AND  ASC-START-OF-SELECTED-RANGE >                         INKCS004
01477                                          ASC-END-OF-SELECTED-RANGEINKCS004
01478 *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                INKCS004
01479          MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                 INKCS004
01480          SET DP020-MSG-FATAL TO TRUE                              INKCS004
01481          SET PS-ERROR TO TRUE                                     INKCS004
01482          PERFORM 3410-POSITION-AT-ERROR                           INKCS004
01483      END-IF.                                                      INKCS004
01484                                                                   INKCS004
01485 *----------------------------------------------------------------*INKCS004
01486 *  BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE     *INKCS004
01487 *  PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR  *INKCS004
01488 *  SHOULD BE POSITIONED ON A RANGE ERROR.                        *INKCS004
01489 *----------------------------------------------------------------*INKCS004
01490                                                                   INKCS004
01491  3410-POSITION-AT-ERROR.                                          INKCS004
01492                                                                   INKCS004
01493      SET DP030-SET-CURSOR-APPL-1                                  INKCS004
01494                                    TO TRUE.                       INKCS004
01495      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS004
01496                     - PV-TOP-ITEM + 1.                            INKCS004
01497      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS004
01498          CONTINUE                                                 INKCS004
01499      ELSE                                                         INKCS004
01500          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS004
01501          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS004
01502          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS004
01503      END-IF.                                                      INKCS004
01504 *                                                                 INKCS004
01505      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS004
01506                     - PV-TOP-ITEM + 1.                            INKCS004
01507      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS004
01508          CONTINUE                                                 INKCS004
01509      ELSE                                                         INKCS004
01510          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS004
01511          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS004
01512          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS004
01513      END-IF.                                                      INKCS004
01514 /                                                                 INKCS004
01515 *----------------------------------------------------------------*INKCS004
01516 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *INKCS004
01517 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *INKCS004
01518 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *INKCS004
01519 *----------------------------------------------------------------*INKCS004
01520                                                                   INKCS004
01521  3500-MARK-RANGE-AS-SELECTED.                                     INKCS004
01522                                                                   INKCS004
01523      MOVE +1                       TO  ASC-BLK-SUB.               INKCS004
01524      PERFORM 3510-MARK-SELECTED-INDS                              INKCS004
01525          VARYING ASC-ITEM-SUB                                     INKCS004
01526          FROM 1 BY 1                                              INKCS004
01527          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01528                                                                   INKCS004
01529      ADD +1                        TO ASC-BLK-SUB.                INKCS004
01530      PERFORM 3510-MARK-SELECTED-INDS                              INKCS004
01531          VARYING ASC-ITEM-SUB                                     INKCS004
01532          FROM 1 BY 1                                              INKCS004
01533          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS004
01534                                                                   INKCS004
01535      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS004
01536                                    TO  PV-LIST-ITEM-NUMBER.       INKCS004
01537      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS004
01538          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         INKCS004
01539                PC-ITEMS-PER-PANEL).                               INKCS004
01540                                                                   INKCS004
01541      MOVE ASC-START-OF-SELECTED-RANGE                             INKCS004
01542                                    TO PV-START-OF-SELECTED-RANGE. INKCS004
01543      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            INKCS004
01544          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  INKCS004
01545                PC-ITEMS-PER-PANEL).                               INKCS004
01546                                                                   INKCS004
01547      MOVE ASC-END-OF-SELECTED-RANGE                               INKCS004
01548                                    TO  PV-END-OF-SELECTED-RANGE.  INKCS004
01549      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             INKCS004
01550          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    INKCS004
01551                PC-ITEMS-PER-PANEL).                               INKCS004
01552      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             INKCS004
01553          PV-FIRST-BLOCK-IN-ARRAY + 1.                             INKCS004
01554                                                                   INKCS004
01555      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    INKCS004
01556         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        INKCS004
01557          MOVE +1                   TO ASC-BLK-SUB                 INKCS004
01558          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS004
01559          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS004
01560              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS004
01561                                                                   INKCS004
01562          PERFORM                                                  INKCS004
01563              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      INKCS004
01564                    PV-PREV-BLOCK-IN-ARRAY                         INKCS004
01565              MOVE PV-FIRST-BLOCK-IN-RANGE                         INKCS004
01566                                    TO PV-BLOCK-NUMBER             INKCS004
01567              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01568              PERFORM 3510-MARK-SELECTED-INDS                      INKCS004
01569                  VARYING ASC-ITEM-SUB                             INKCS004
01570                  FROM 1 BY 1                                      INKCS004
01571                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01572              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01573              ADD +1                TO PV-FIRST-BLOCK-IN-RANGE     INKCS004
01574          END-PERFORM                                              INKCS004
01575                                                                   INKCS004
01576          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS004
01577              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS004
01578          PERFORM                                                  INKCS004
01579              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS004
01580                    PV-LAST-BLOCK-IN-RANGE                         INKCS004
01581              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS004
01582                                    TO PV-BLOCK-NUMBER             INKCS004
01583              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01584              PERFORM 3510-MARK-SELECTED-INDS                      INKCS004
01585                  VARYING ASC-ITEM-SUB                             INKCS004
01586                  FROM 1 BY 1                                      INKCS004
01587                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS004
01588              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01589              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS004
01590          END-PERFORM                                              INKCS004
01591                                                                   INKCS004
01592          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS004
01593                                    TO PV-BLOCK-NUMBER             INKCS004
01594          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
01595      END-IF.                                                      INKCS004
01596                                                                   INKCS004
01597      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE INKCS004
01598                                       ASC-END-OF-SELECTED-RANGE.  INKCS004
01599 /                                                                 INKCS004
01600 *----------------------------------------------------------------*INKCS004
01601 * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *INKCS004
01602 *----------------------------------------------------------------*INKCS004
01603                                                                   INKCS004
01604  3510-MARK-SELECTED-INDS.                                         INKCS004
01605                                                                   INKCS004
01606      IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS004
01607                                 ASC-ITEM-SUB) >                   INKCS004
01608          ASC-START-OF-SELECTED-RANGE)                             INKCS004
01609      OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS004
01610                                 ASC-ITEM-SUB) =                   INKCS004
01611          ASC-START-OF-SELECTED-RANGE)                             INKCS004
01612                                                                   INKCS004
01613          IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS004
01614                                     ASC-ITEM-SUB) <               INKCS004
01615              ASC-END-OF-SELECTED-RANGE)                           INKCS004
01616          OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS004
01617                                     ASC-ITEM-SUB) =               INKCS004
01618              ASC-END-OF-SELECTED-RANGE)                           INKCS004
01619                                                                   INKCS004
01620              IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS004
01621                  SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS004
01622                      ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS004
01623                                TO TRUE                            INKCS004
01624                  SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)              INKCS004
01625                                TO TRUE                            INKCS004
01626                  ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS    INKCS004
01627              END-IF                                               INKCS004
01628          END-IF                                                   INKCS004
01629      END-IF.                                                      INKCS004
01630 /                                                                 INKCS004
01631 *----------------------------------------------------------------*INKCS004
01632 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS004
01633 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS004
01634 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS004
01635 *    DISPLAY THE PANEL TO USER.                                  *INKCS004
01636 *----------------------------------------------------------------*INKCS004
01637                                                                   INKCS004
01638  4000-BUILD-INITIAL-PANEL.                                        INKCS004
01639                                                                   INKCS004
01640      PERFORM 5120-DELETE-ASC-LISTQ.                               INKCS004
01641      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS004
01642                                  ASC-BLOCK-ENTRIES (2).           INKCS004
01643      MOVE +1                 TO ASC-BLK-SUB.                      INKCS004
01644      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS004
01645                              TO TRUE.                             INKCS004
01646                                                                   INKCS004
01647      MOVE +0                 TO ASC-ITEM-SUB                      INKCS004
01648                                 ASC-NUMBER-OF-ITEMS-READ          INKCS004
01649                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS004
01650                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS004
01651                                 ASC-START-OF-SELECTED-RANGE       INKCS004
01652                                 ASC-END-OF-SELECTED-RANGE         INKCS004
01653                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS004
01654      PERFORM 4100-PREPARE-CURSOR.                                 INKCS004
01655                                                                   INKCS004
01656      PERFORM 4010-OPEN-CURSOR.                                    INKCS004
01657                                                                   INKCS004
01658      PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS004
01659                                                                   INKCS004
01660      PERFORM 4020-CLOSE-CURSOR.                                   INKCS004
01661                                                                   INKCS004
01662      MOVE +1                   TO  PV-TOP-ITEM.                   INKCS004
01663      MOVE SPACE                TO  ASC-START-ITEM-REQUEST.        INKCS004
01664      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS004
01665 *        SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                    INKCS004
01666          SET DP020-MSG-FATAL   TO  TRUE                           INKCS004
01667          SET DP020-FK-RETURN (DP020-SRC-AID) TO TRUE              INKCS004
01668          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS004
01669      ELSE                                                         INKCS004
01670          MOVE -1               TO MR-SELECTL (1)                  INKCS004
01671          SET DP030-SET-CURSOR-APPL-1                              INKCS004
01672                                TO TRUE                            INKCS004
01673          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS004
01674      END-IF.                                                      INKCS004
01675 /                                                                 INKCS004
01676 *----------------------------------------------------------------*INKCS004
01677 * OPEN THE CURSOR.                                               *INKCS004
01678 *----------------------------------------------------------------*INKCS004
01679                                                                   INKCS004
01680  4010-OPEN-CURSOR.                                                INKCS004
01681                                                                   INKCS004
01682      PERFORM                                                      INKCS004
01683          WITH TEST AFTER                                          INKCS004
01684          VARYING PV-RETRY-COUNTER                                 INKCS004
01685          FROM 1 BY 1                                              INKCS004
01686          UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               INKCS004
01687                    OR (SQLCODE = ZERO))                           INKCS004
01688                                                                   INKCS004
W26600     MOVE ASC-KEY-STORE-NBR      TO PV-STORE-NBR-X                        
W26600     MOVE PV-STORE-NBR-9         TO INVDTL-LOC-NBR                        
W21732     MOVE ASC-KEY-AREA-NBR       TO INVDTL-AREA-NBR                       
W21732     MOVE ASC-KEY-SCAN-LINE-NBR  TO INVDTL-SCAN-LINE-NBR                  
W21732                                                                          
01689          EXEC SQL                                                 INKCS004
01690               OPEN LINE-CSR                                       INKCS004
01691          END-EXEC                                                 INKCS004
01692                                                                   INKCS004
01693          EVALUATE TRUE                                            INKCS004
01694              WHEN SQLCODE = ZERO                                  INKCS004
01695              WHEN SQLCODE = -904                                  INKCS004
01696              WHEN SQLCODE = -913                                  INKCS004
01697                  CONTINUE                                         INKCS004
01698              WHEN SQLWARN0 NOT = SPACES                           INKCS004
01699              WHEN SQLCODE < ZERO                                  INKCS004
01700              WHEN SQLCODE > ZERO                                  INKCS004
01701                  MOVE '4010-OPEN-CURSOR'                          INKCS004
01702                                    TO DP013-PARAGRAPH             INKCS004
01703                  MOVE 'OPEN LINE CURSOR - '                       INKCS004
01704                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
01705                  MOVE SQLCA        TO DP013-SQLCA                 INKCS004
01706                  MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)    INKCS004
01707                  SET DP013-DB2-ABEND                              INKCS004
01708                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS004
01709                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS004
01710          END-EVALUATE                                             INKCS004
01711      END-PERFORM.                                                 INKCS004
01712                                                                   INKCS004
01713      IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        INKCS004
01714          MOVE '4010-OPEN-CURSOR'   TO DP013-PARAGRAPH             INKCS004
01715          MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO OPEN CSR'   INKCS004
01716                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
01717          MOVE SQLCA                TO DP013-SQLCA                 INKCS004
01718          MOVE 'TSYMSG  '           TO DP013-DB2-TABLE-NAME (1)    INKCS004
01719          SET DP013-DB2-ABEND                                      INKCS004
01720              DP013-XCTL-DISPLAY-RESTART                           INKCS004
01721                                    TO TRUE                        INKCS004
01722          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
01723      END-IF.                                                      INKCS004
01724                                                                   INKCS004
01725 /                                                                 INKCS004
01726 *----------------------------------------------------------------*INKCS004
01727 *                                                                *INKCS004
01728 * CLOSE THE LINE CURSOR.                                         *INKCS004
01729 *                                                                *INKCS004
01730 *----------------------------------------------------------------*INKCS004
01731                                                                   INKCS004
01732  4020-CLOSE-CURSOR.                                               INKCS004
01733                                                                   INKCS004
01734      EXEC SQL                                                     INKCS004
01735           CLOSE LINE-CSR                                          INKCS004
01736      END-EXEC.                                                    INKCS004
01737                                                                   INKCS004
01738      EVALUATE TRUE                                                INKCS004
01739          WHEN SQLCODE = ZERO                                      INKCS004
01740              CONTINUE                                             INKCS004
01741          WHEN SQLWARN0 NOT = SPACES                               INKCS004
01742          WHEN SQLCODE < ZERO                                      INKCS004
01743          WHEN SQLCODE > ZERO                                      INKCS004
01744              MOVE '4020-CLOSE-CURSOR'                             INKCS004
01745                                TO DP013-PARAGRAPH                 INKCS004
01746              MOVE 'CLOSE LINE CURSOR '                            INKCS004
01747                                TO DP013-MESSAGE-TEXT (1)          INKCS004
01748              MOVE SQLCA        TO DP013-SQLCA                     INKCS004
01749              MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS004
01750              SET DP013-DB2-ABEND                                  INKCS004
01751                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS004
01752              PERFORM DP013-0000-PROCESS-ABEND                     INKCS004
01753      END-EVALUATE.                                                INKCS004
01754 /                                                                 INKCS004
01755 *----------------------------------------------------------------*INKCS004
01756 *    SET UP THE HOST VARIABLES.                                  *INKCS004
01757 *----------------------------------------------------------------*INKCS004
01758                                                                   INKCS004
01759  4100-PREPARE-CURSOR.                                             INKCS004
01760                                                                   INKCS004
01761      MOVE IN001-STORE-NBR      TO  ASC-KEY-STORE-NBR.             INKCS004
01762      IF  ASC-USE-SELECTION-QUEUE                                  INKCS004
01763          CONTINUE                                                 INKCS004
01764      ELSE                                                         INKCS004
01765          MOVE IN001-AREA-NBR   TO  ASC-KEY-AREA-NBR               INKCS004
01766      END-IF.                                                      INKCS004
01767      MOVE ZERO                 TO  ASC-KEY-SCAN-LINE-NBR.            CL**2
01768                                                                   INKCS004
01769 /                                                                 INKCS004
01770 *----------------------------------------------------------------*INKCS004
01771 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS004
01772 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS004
01773 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS004
01774 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS004
01775 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS004
01776 *                                                                *INKCS004
01777 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS004
01778 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS004
01779 *    UPON INITIALIZATION.                                        *INKCS004
01780 *----------------------------------------------------------------*INKCS004
01781                                                                   INKCS004
01782  4300-READ-ITEMS-FROM-DB.                                         INKCS004
01783                                                                   INKCS004
01784      PERFORM                                                      INKCS004
01785          VARYING PV-READ-COUNT                                    INKCS004
01786          FROM 1 BY 1                                              INKCS004
01787          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS004
01788             OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS004
01789                                                                   INKCS004
01790          PERFORM 4310-FETCH                                       INKCS004
01791                                                                   INKCS004
01792          IF (INVDTL-INVALID-ITM-IND EQUAL PC-Y                    INKCS004
01793          OR  INVDTL-INVALID-REC-IND EQUAL PC-Y)                   INKCS004
01794                                                                   INKCS004
01795            IF  ASC-ITEMS-LEFT-ON-DB                               INKCS004
01796              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                INKCS004
01797                  ADD +1        TO ASC-ITEM-SUB                    INKCS004
01798              ELSE                                                 INKCS004
01799                  ADD +1        TO ASC-BLK-SUB                     INKCS004
01800                  MOVE +1       TO ASC-ITEM-SUB                    INKCS004
01801                                                                   INKCS004
01802                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         INKCS004
01803                      MOVE +1   TO ASC-BLK-SUB                     INKCS004
01804                      SET ASC-BLOCK-MODIFIED (1)                   INKCS004
01805                                TO TRUE                            INKCS004
01806                      PERFORM 5100-WRITE-TEMP-STORAGE              INKCS004
01807                      MOVE ASC-BLOCK-ENTRIES                       INKCS004
01808                                (PC-MAX-BLOCKS-IN-ARRAY)           INKCS004
01809                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)  INKCS004
01810                      ADD +1    TO ASC-BLK-SUB                     INKCS004
01811                      INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    INKCS004
01812                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      INKCS004
01813                                TO TRUE                            INKCS004
01814                  END-IF                                           INKCS004
01815              END-IF                                               INKCS004
01816                                                                   INKCS004
01817              ADD +1            TO ASC-NUMBER-OF-ITEMS-READ        INKCS004
01818              MOVE ASC-NUMBER-OF-ITEMS-READ                        INKCS004
01819                                    TO ASC-LIST-ITEM-NUMBER        INKCS004
01820                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS004
01821              MOVE INVDTL-INV-LINE-NBR                                CL**2
01822                                    TO  ASC-INV-LINE-NBR              CL**2
01823                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01824              MOVE INVDTL-SCAN-LINE-NBR                               CL**2
01825                                    TO  ASC-SCAN-LINE-NBR             CL**2
01826                                        (ASC-BLK-SUB ASC-ITEM-SUB)    CL**2
W21732             IF  INVDTL-REC-SKU-NBR  GREATER THAN ZERO            INKCS004
W21732                 MOVE INVDTL-REC-SKU-NBR TO PV-SKU                INKCS004
W21732                 MOVE PV-SKU       TO  ASC-SKU                    INKCS004
W21732                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
W21732             END-IF                                               INKCS004
01838              MOVE INVDTL-REC-UPC-NBR                              INKCS004
01839                                    TO  ASC-UPC                    INKCS004
01840                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01841              MOVE INVDTL-REC-QTY   TO  ASC-INV-QTY                INKCS004
01842                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01843              MOVE INVDTL-REC-PR-AMT TO ASC-INV-PR-AMT             INKCS004
01844                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01845                                                                   INKCS004
01846              MOVE INVDTL-INVALID-ITM-IND                          INKCS004
01847                                    TO  ASC-INVALID-ITM-IND        INKCS004
01848                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01849              MOVE INVDTL-INVALID-REC-IND                          INKCS004
01850                                    TO  ASC-INVALID-REC-IND        INKCS004
01851                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01852                                                                   INKCS004
01853              IF  INVDTL-INVALID-ITM-IND EQUAL PC-Y                INKCS004
01854                  MOVE DP015-RED    TO  ASC-SKU-COLOR              INKCS004
01855                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01856              ELSE                                                 INKCS004
01857                  MOVE DP015-BLUE   TO  ASC-SKU-COLOR              INKCS004
01858                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01859              END-IF                                               INKCS004
01860                                                                   INKCS004
01861              IF (INVDTL-REC-QTY  EQUAL ZERO                       INKCS004
01862              AND INVDTL-CORR-QTY EQUAL ZERO                       INKCS004
01863              AND INVDTL-INVALID-REC-IND EQUAL PC-Y)               INKCS004
01864                  MOVE DP015-RED    TO  ASC-QTY-COLOR              INKCS004
01865                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01866              ELSE                                                 INKCS004
01867                  MOVE DP015-BLUE   TO  ASC-QTY-COLOR              INKCS004
01868                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01869              END-IF                                               INKCS004
01870                                                                   INKCS004
01871              IF (INVDTL-INVALID-REC-IND EQUAL PC-Y                INKCS004
01872              AND INVDTL-REC-PR-AMT  EQUAL ZERO                    INKCS004
01873              AND INVDTL-CORR-PR-AMT EQUAL ZERO)                   INKCS004
01874                  MOVE DP015-RED    TO  ASC-PR-AMT-COLOR           INKCS004
01875                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01876              ELSE                                                 INKCS004
01877                  MOVE DP015-BLUE   TO  ASC-PR-AMT-COLOR           INKCS004
01878                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
01879              END-IF                                               INKCS004
01880            END-IF                                                 INKCS004
01881          END-IF                                                   INKCS004
01882      END-PERFORM.                                                 INKCS004
01883                                                                   INKCS004
01884      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS004
01885          MOVE +1               TO ASC-BLK-SUB                     INKCS004
01886          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS004
01887          ADD +1                TO ASC-BLK-SUB                     INKCS004
01888          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS004
01889                                                                   INKCS004
01890          MOVE 1                TO ASC-BLK-SUB                     INKCS004
01891                                   PV-BLOCK-NUMBER                 INKCS004
01892          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
01893          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS004
01894              MOVE 2            TO ASC-BLK-SUB                     INKCS004
01895                                   PV-BLOCK-NUMBER                 INKCS004
01896              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01897          END-IF                                                   INKCS004
01898      END-IF.                                                      INKCS004
01899                                                                   INKCS004
01900      MOVE INVDTL-SCAN-LINE-NBR TO  ASC-KEY-SCAN-LINE-NBR.            CL**3
01901 /                                                                 INKCS004
01902 *----------------------------------------------------------------*INKCS004
01903 *    FETCH THE CURSOR.                                           *INKCS004
01904 *----------------------------------------------------------------*INKCS004
01905                                                                   INKCS004
01906  4310-FETCH.                                                      INKCS004
01907                                                                   INKCS004
                                                                                
01908      EXEC SQL                                                     INKCS004
01909           FETCH                                                   INKCS004
01910                 LINE-CSR                                          INKCS004
01911           INTO                                                    INKCS004
01912                :INVDTL-REC-UPC-NBR                                INKCS004
W21732             , :INVDTL-REC-SKU-NBR                                INKCS004
01916              , :INVDTL-REC-QTY                                    INKCS004
01917              , :INVDTL-REC-PR-AMT                                 INKCS004
01918              , :INVDTL-CORR-UPC-NBR                               INKCS004
W21732             , :INVDTL-CORR-SKU-NBR                               INKCS004
01922              , :INVDTL-CORR-QTY                                   INKCS004
01923              , :INVDTL-CORR-PR-AMT                                INKCS004
01924              , :INVDTL-INV-LINE-NBR                                  CL**2
01925              , :INVDTL-SCAN-LINE-NBR                                 CL**2
01926              , :INVDTL-INVALID-ITM-IND                            INKCS004
01927              , :INVDTL-INVALID-REC-IND                            INKCS004
01928      END-EXEC.                                                    INKCS004
01929                                                                   INKCS004
01930      EVALUATE TRUE                                                INKCS004
01931          WHEN SQLCODE = ZERO                                      INKCS004
01932          WHEN SQLCODE = +100                                      INKCS004
01933               CONTINUE                                            INKCS004
01934          WHEN SQLWARN0 NOT = SPACE                                INKCS004
01935          WHEN SQLCODE NOT = ZERO                                  INKCS004
01936               MOVE '4310-FETCH-CURSOR'                            INKCS004
01937                                TO DP013-PARAGRAPH                 INKCS004
01938               MOVE 'FETCH LINE CURSOR'                            INKCS004
01939                                TO DP013-MESSAGE-TEXT (1)          INKCS004
01940               MOVE SQLCA       TO DP013-SQLCA                     INKCS004
01941               SET DP013-DB2-ABEND                                 INKCS004
01942                                TO TRUE                            INKCS004
01943               PERFORM DP013-0000-PROCESS-ABEND                    INKCS004
01944      END-EVALUATE.                                                INKCS004
01945                                                                   INKCS004
01946      IF  SQLCODE EQUAL +100                                       INKCS004
01947          SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS004
01948                                    TO TRUE                        INKCS004
01949      END-IF.                                                      INKCS004
01950 /                                                                 INKCS004
01951 *----------------------------------------------------------------*INKCS004
01952 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS004
01953 *----------------------------------------------------------------*INKCS004
01954                                                                   INKCS004
01955  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS004
01956                                                                   INKCS004
W26600     MOVE IN001-STORE-NBR          TO  ASTRNBRO.                  INKCS004
01957      MOVE IN001-STORE-NAME         TO  ASTRNAMO.                  INKCS004
01958      MOVE ASC-KEY-AREA-NBR         TO  AAREAO.                    INKCS004
01959                                                                   INKCS004
01960      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS004
01961          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS004
01962          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS004
01963              MOVE +1    TO ASC-BLK-SUB                            INKCS004
01964              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01965              ADD +1     TO ASC-BLK-SUB                            INKCS004
01966              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS004
01967              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS004
01968                  GIVING PV-BLOCK-NUMBER                           INKCS004
01969                  REMAINDER PV-REMAINDER                           INKCS004
01970              IF  PV-REMAINDER > ZERO                              INKCS004
01971                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS004
01972              END-IF                                               INKCS004
01973              MOVE +1    TO ASC-BLK-SUB                            INKCS004
01974              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01975              ADD +1     TO ASC-BLK-SUB                            INKCS004
01976                            PV-BLOCK-NUMBER                        INKCS004
01977              PERFORM 4410-READ-TEMP-STORAGE                       INKCS004
01978      END-IF.                                                      INKCS004
01979                                                                   INKCS004
01980      MOVE +1            TO ASC-BLK-SUB.                           INKCS004
01981      COMPUTE ASC-ITEM-SUB =                                       INKCS004
01982          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS004
01983          + PC-ITEMS-PER-PANEL).                                   INKCS004
01984                                                                   INKCS004
01985      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS004
01986          ADD +1         TO ASC-BLK-SUB                            INKCS004
01987          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS004
01988      END-IF.                                                      INKCS004
01989                                                                   INKCS004
01990      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS004
01991          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS004
01992      PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                        INKCS004
01993      PERFORM 4420-FORMAT-LIST-LINES                               INKCS004
01994          VARYING PV-SUB                                           INKCS004
01995          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS004
01996          UNTIL PV-SUB < +1.                                       INKCS004
01997                                                                   INKCS004
01998      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS004
01999          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS004
02000      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL.   INKCS004
02001      IF  PS-NO-ERROR                                              INKCS004
02002          MOVE PV-TOP-ITEM          TO ASTRTITO                    INKCS004
02003      END-IF.                                                      INKCS004
02004      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS004
02005      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS004
02006      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS004
02007                                    TO ASELITO.                    INKCS004
02008      MOVE SPACE                    TO  ASC-START-ITEM-REQUEST.    INKCS004
02009                                                                   INKCS004
02010      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS004
02011          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS004
02012 *            --- MORE ITEMS TO DISPLAY ----                       INKCS004
02013              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS004
02014              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS004
02015          END-IF                                                   INKCS004
02016      END-IF.                                                      INKCS004
02017                                                                   INKCS004
02018 *----------------------------------------------------------------*INKCS004
02019 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS004
02020 *----------------------------------------------------------------*INKCS004
02021                                                                   INKCS004
02022  4410-READ-TEMP-STORAGE.                                          INKCS004
02023                                                                   INKCS004
02024      EXEC CICS                                                    INKCS004
02025          READQ TS                                                 INKCS004
02026              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02027              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS004
02028              ITEM  (PV-BLOCK-NUMBER)                              INKCS004
02029              RESP  (PV-CICS-RESP)                                 INKCS004
02030      END-EXEC.                                                    INKCS004
02031                                                                   INKCS004
02032      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS004
02033        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS004
02034          CONTINUE                                                 INKCS004
02035      ELSE                                                         INKCS004
02036          SET DP013-CICS-ABEND     TO TRUE                         INKCS004
02037          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS004
02038          MOVE '4410-READ-TEMP-STORAGE'                            INKCS004
02039                                   TO DP013-PARAGRAPH              INKCS004
02040          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS004
02041                                   TO DP013-MESSAGE-TEXT(1)        INKCS004
02042          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02043      END-IF.                                                      INKCS004
02044                                                                   INKCS004
02045      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS004
02046                                                                   INKCS004
02047      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS004
02048          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS004
02049      END-IF.                                                      INKCS004
02050 /                                                                 INKCS004
02051 *----------------------------------------------------------------*INKCS004
02052 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS004
02053 *----------------------------------------------------------------*INKCS004
02054                                                                   INKCS004
02055  4420-FORMAT-LIST-LINES.                                          INKCS004
02056                                                                   INKCS004
02057      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS004
02058          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS004
02059          MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA      (PV-SUB)      INKCS004
02060          MOVE SPACE              TO MR-SELECTI      (PV-SUB)      INKCS004
02061                                     MR-ACTION       (PV-SUB)      INKCS004
02062                                     MR-LINE-X       (PV-SUB)      INKCS004
W21732                                    MR-SKU          (PV-SUB)      INKCS004
02066                                     MR-UPC-X        (PV-SUB)      INKCS004
02067                                     MR-INV-QTY-X    (PV-SUB)      INKCS004
02068                                     MR-INV-PR-AMT-X (PV-SUB)      INKCS004
02069      ELSE                                                         INKCS004
02070          IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS004
02071                                                    > SPACE        INKCS004
02072              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS004
02073                                  TO MR-SELECTI (PV-SUB)           INKCS004
02074          ELSE                                                     INKCS004
02075              MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS004
02076                                  TO MR-SELECTI (PV-SUB)           INKCS004
02077          END-IF                                                   INKCS004
02078                                                                   INKCS004
02079          IF  DP030-SET-CURSOR-TRAILER                             INKCS004
02080              IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)INKCS004
02081                  > SPACE)                                         INKCS004
02082                OR (PV-SUB = 1))                                   INKCS004
02083                 SET DP030-SET-CURSOR-APPL-1 TO TRUE               INKCS004
02084                 MOVE -1          TO MR-SELECTL (PV-SUB)           INKCS004
02085              END-IF                                               INKCS004
02086          END-IF                                                   INKCS004
02087                                                                   INKCS004
02088          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS004
02089              MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS004
02090                                  TO MR-ACTION (PV-SUB)            INKCS004
02091              MOVE ASC-SCAN-LINE-NBR (ASC-BLK-SUB ASC-ITEM-SUB)       CL**2
02092                                  TO MR-LINE          (PV-SUB)     INKCS004
W21732             MOVE ASC-SKU           (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
W21732                                 TO MR-SKU           (PV-SUB)     INKCS004
02099              MOVE ASC-UPC           (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02100                                  TO MR-UPC           (PV-SUB)     INKCS004
02101              MOVE ASC-INV-QTY       (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02102                                  TO MR-INV-QTY       (PV-SUB)     INKCS004
02103              MOVE ASC-INV-PR-AMT    (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02104                                  TO MR-INV-PR-AMT    (PV-SUB)     INKCS004
02105                                                                   INKCS004
W21732             MOVE ASC-SKU-COLOR     (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
W21732                                 TO MR-SKUC          (PV-SUB)     INKCS004
02112              MOVE ASC-SKU-COLOR     (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02113                                  TO MR-UPCC          (PV-SUB)     INKCS004
02114              MOVE ASC-QTY-COLOR     (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02115                                  TO MR-INV-QTYC      (PV-SUB)     INKCS004
02116              MOVE ASC-PR-AMT-COLOR  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS004
02117                                  TO MR-INV-PR-AMTC   (PV-SUB)     INKCS004
02118          END-IF                                                   INKCS004
02119      END-IF.                                                      INKCS004
02120                                                                   INKCS004
02121      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS004
02122      IF  ASC-ITEM-SUB < + 1                                       INKCS004
02123          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS004
02124          MOVE +1                 TO ASC-BLK-SUB                   INKCS004
02125      END-IF.                                                      INKCS004
02126                                                                   INKCS004
02127 ******************************************************************INKCS004
02128 ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **INKCS004
02129 ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **INKCS004
02130 ** ISSUE A WARNING.                                             **INKCS004
02131 ******************************************************************INKCS004
02132                                                                   INKCS004
02133  4430-CHECK-FOR-BEGIN-AND-END.                                    INKCS004
02134                                                                   INKCS004
02135      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS004
02136          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS004
02137 *            -- NO END-OF-RANGE SPECFIED ---                      INKCS004
02138              MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER            INKCS004
02139              SET DP020-MSG-WARNING TO TRUE                        INKCS004
02140              PERFORM 4431-POSITION-AT-WARNING                     INKCS004
02141          END-IF                                                   INKCS004
02142      ELSE                                                         INKCS004
02143          IF  ASC-END-OF-SELECTED-RANGE > ZERO                     INKCS004
02144 *            -- NO BEGINNING-OF-RANGE SPECFIED ---                INKCS004
02145              MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER            INKCS004
02146              SET DP020-MSG-WARNING TO TRUE                        INKCS004
02147              PERFORM 4431-POSITION-AT-WARNING                     INKCS004
02148          END-IF                                                   INKCS004
02149      END-IF.                                                      INKCS004
02150 ******************************************************************INKCS004
02151 ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **INKCS004
02152 ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **INKCS004
02153 ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **INKCS004
02154 ** POSITION THE CURSOR THERE.                                   **INKCS004
02155 ******************************************************************INKCS004
02156                                                                   INKCS004
02157  4431-POSITION-AT-WARNING.                                        INKCS004
02158                                                                   INKCS004
02159      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS004
02160                     - PV-TOP-ITEM + 1.                            INKCS004
02161      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS004
02162          CONTINUE                                                 INKCS004
02163      ELSE                                                         INKCS004
02164          SET DP030-SET-CURSOR-APPL-1                              INKCS004
02165                                    TO TRUE                        INKCS004
02166          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS004
02167          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS004
02168          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS004
02169      END-IF.                                                      INKCS004
02170                                                                   INKCS004
02171      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS004
02172                     - PV-TOP-ITEM + 1.                            INKCS004
02173      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS004
02174          CONTINUE                                                 INKCS004
02175      ELSE                                                         INKCS004
02176          SET DP030-SET-CURSOR-APPL-1                              INKCS004
02177                                    TO TRUE                        INKCS004
02178          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS004
02179          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS004
02180          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS004
02181      END-IF.                                                      INKCS004
02182 /                                                                 INKCS004
02183 *----------------------------------------------------------------*INKCS004
02184 * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *INKCS004
02185 * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *INKCS004
02186 * FOR THE SELECTED ITEMS QUEUE.                                  *INKCS004
02187 *                                                                *INKCS004
02188 * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *INKCS004
02189 * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *INKCS004
02190 * THE ARRAY IS FULL.                                             *INKCS004
02191 *----------------------------------------------------------------*INKCS004
02192                                                                   INKCS004
02193  5000-PREPARE-SEL-QUEUE.                                          INKCS004
02194                                                                   INKCS004
02195      MOVE +1                   TO  ASC-BLK-SUB.                   INKCS004
02196      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS004
02197      ADD +1                    TO  ASC-BLK-SUB.                   INKCS004
02198      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS004
02199                                                                   INKCS004
02200      MOVE ASC-KEY-AREA-NBR     TO  IN001-AREA-NBR.                INKCS004
02201      MOVE ASC-SEL-QUEUE-NAME2  TO  IN001-SEL-QUEUE-NAME.          INKCS004
02202      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS004
02203                                TO  IN001-NUMBER-OF-SELECTED-ITEMS.INKCS004
02204      PERFORM 6010-DELETE-SEL-QUEUE.                               INKCS004
02205                                                                   INKCS004
02206      SET ASC-UPDATE-TSQ        TO  TRUE.                          INKCS004
02207                                                                   INKCS004
02208      SET IN003-IDX             TO  1.                             INKCS004
02209      MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.       INKCS004
02210      INITIALIZE IN003-SELECTED-ITEM-ARRAY.                        INKCS004
02211                                                                   INKCS004
02212      PERFORM 5200-FILL-SEL-QUEUE-RECORD                           INKCS004
02213              VARYING PV-ITEM                                      INKCS004
02214              FROM 1 BY 1                                          INKCS004
02215              UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     INKCS004
02216                                                                   INKCS004
02217      IF  IN003-IDX > 1                                            INKCS004
02218          PERFORM 5205-WRITE-SEL-QUEUE                             INKCS004
02219          ADD +1                TO IN001-NUMBER-OF-TS-ITEMS        INKCS004
02220      END-IF.                                                      INKCS004
02221 /                                                                 INKCS004
02222 *----------------------------------------------------------------*INKCS004
02223 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS004
02224 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS004
02225 *----------------------------------------------------------------*INKCS004
02226                                                                   INKCS004
02227  5100-WRITE-TEMP-STORAGE.                                         INKCS004
02228                                                                   INKCS004
02229      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS004
02230                                    TO PV-LIST-ITEM-NUMBER.        INKCS004
02231      COMPUTE PV-BLOCK-NUMBER =                                    INKCS004
02232          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS004
02233                                                                   INKCS004
02234      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS004
02235      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS004
02236          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS004
02237              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS004
02238          END-IF                                                   INKCS004
02239      ELSE                                                         INKCS004
02240          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS004
02241          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS004
02242          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS004
02243          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS004
02244      END-IF.                                                      INKCS004
02245                                                                   INKCS004
02246      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS004
02247                                                                   INKCS004
02248 ******************************************************************INKCS004
02249 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS004
02250 ** SPECIFIC COMMAREA.                                           **INKCS004
02251 ******************************************************************INKCS004
02252                                                                   INKCS004
02253  5105-REWRITE-ASC-LISTQ.                                          INKCS004
02254                                                                   INKCS004
02255      EXEC CICS                                                    INKCS004
02256          WRITEQ TS                                                INKCS004
02257              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02258              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS004
02259              ITEM  (PV-BLOCK-NUMBER)                              INKCS004
02260              REWRITE                                              INKCS004
02261              RESP  (PV-CICS-RESP)                                 INKCS004
02262      END-EXEC.                                                    INKCS004
02263                                                                   INKCS004
02264      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS004
02265          CONTINUE                                                 INKCS004
02266      ELSE                                                         INKCS004
02267          SET DP013-CICS-ABEND            TO TRUE                  INKCS004
02268          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS004
02269          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS004
02270          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS004
02271                                          TO DP013-MESSAGE-TEXT(1) INKCS004
02272          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02273      END-IF.                                                      INKCS004
02274                                                                   INKCS004
02275 ******************************************************************INKCS004
02276 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS004
02277 ** SPECIFIC COMMAREA.                                           **INKCS004
02278 ******************************************************************INKCS004
02279                                                                   INKCS004
02280  5110-WRITE-ASC-LISTQ.                                            INKCS004
02281                                                                   INKCS004
02282      EXEC CICS                                                    INKCS004
02283          WRITEQ TS                                                INKCS004
02284              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02285              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS004
02286              ITEM  (PV-BLOCK-NUMBER)                              INKCS004
02287              RESP  (PV-CICS-RESP)                                 INKCS004
02288      END-EXEC.                                                    INKCS004
02289      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS004
02290          SET DP013-CICS-ABEND      TO TRUE                        INKCS004
02291          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS004
02292          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS004
02293                                    TO DP013-PARAGRAPH             INKCS004
02294          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS004
02295                                    TO DP013-MESSAGE-TEXT(1)       INKCS004
02296          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02297      END-IF.                                                      INKCS004
02298                                                                   INKCS004
02299 ******************************************************************INKCS004
02300 ** DELETE THE LIST TEMP STORAGE QUEUE.                          **INKCS004
02301 ******************************************************************INKCS004
02302                                                                   INKCS004
02303  5120-DELETE-ASC-LISTQ.                                           INKCS004
02304                                                                   INKCS004
02305      EXEC CICS                                                    INKCS004
02306           DELETEQ TS                                              INKCS004
02307              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02308              RESP  (PV-CICS-RESP)                                 INKCS004
02309      END-EXEC.                                                    INKCS004
02310                                                                   INKCS004
02311      IF (PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS004
02312      OR  PV-CICS-RESP = DFHRESP (QIDERR))                         INKCS004
02313          CONTINUE                                                 INKCS004
02314      ELSE                                                         INKCS004
02315          SET DP013-CICS-ABEND      TO TRUE                        INKCS004
02316          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS004
02317          MOVE '5120-DELETE-ASC-LISTQ'                             INKCS004
02318                                    TO DP013-PARAGRAPH             INKCS004
02319          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS004
02320                                    TO DP013-MESSAGE-TEXT(1)       INKCS004
02321          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02322      END-IF.                                                      INKCS004
02323 /                                                                 INKCS004
02324 *----------------------------------------------------------------*INKCS004
02325 * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *INKCS004
02326 * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *INKCS004
02327 *----------------------------------------------------------------*INKCS004
02328                                                                   INKCS004
02329  5200-FILL-SEL-QUEUE-RECORD.                                      INKCS004
02330                                                                   INKCS004
02331      PERFORM 6112-READ-LIST-QUEUE.                                INKCS004
02332                                                                   INKCS004
02333      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS004
02334          PERFORM                                                  INKCS004
02335          VARYING LQW-IDX                                          INKCS004
02336          FROM 1 BY 1                                              INKCS004
02337          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS004
02338            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS004
02339                                                                   INKCS004
02340              IF  LQW-SELECT (LQW-IDX)                             INKCS004
02341                  MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)              INKCS004
02342                                    TO IN003-ITEM (IN003-IDX)      INKCS004
02343                  MOVE LQW-INV-LINE-NBR  (LQW-IDX)                    CL**2
02344                                    TO IN003-INV-LINE-NBR             CL**2
02345                                         (IN003-IDX)               INKCS004
02346                  MOVE LQW-SCAN-LINE-NBR  (LQW-IDX)                   CL**2
02347                                    TO IN003-SCAN-LINE-NBR            CL**2
02348                                         (IN003-IDX)                  CL**2
02349                  MOVE SPACE        TO IN003-ACTION-SW (IN003-IDX) INKCS004
02350                  SET IN003-IDX UP BY 1                            INKCS004
02351                  IF  IN003-IDX > IN003-MAX-ITEMS                  INKCS004
02352                      PERFORM 5205-WRITE-SEL-QUEUE                 INKCS004
02353                      ADD +1        TO IN001-NUMBER-OF-TS-ITEMS    INKCS004
02354                      INITIALIZE IN003-SELECTED-ITEM-ARRAY         INKCS004
02355                      SET IN003-IDX TO  1                          INKCS004
02356                  END-IF                                           INKCS004
02357              END-IF                                               INKCS004
02358          END-PERFORM                                              INKCS004
02359      END-IF.                                                      INKCS004
02360                                                                   INKCS004
02361 *----------------------------------------------------------------*INKCS004
02362 *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *INKCS004
02363 *----------------------------------------------------------------*INKCS004
02364                                                                   INKCS004
02365  5205-WRITE-SEL-QUEUE.                                            INKCS004
02366                                                                   INKCS004
02367      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS004
02368                                    TO IN003-SELECTED-ITEMS.       INKCS004
02369                                                                   INKCS004
02370      EXEC CICS                                                    INKCS004
02371           WRITEQ TS                                               INKCS004
02372               QUEUE (ASC-SEL-QUEUE-NAME2)                         INKCS004
02373               FROM  (IN003-SELECTED-LINES)                        INKCS004
02374               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS004
02375               RESP  (PV-CICS-RESP)                                INKCS004
02376      END-EXEC.                                                    INKCS004
02377                                                                   INKCS004
02378      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS004
02379          CONTINUE                                                 INKCS004
02380      ELSE                                                         INKCS004
02381          SET DP013-CICS-ABEND      TO TRUE                        INKCS004
02382          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS004
02383          MOVE '5205-WRITE-SEL-QUEUE'                              INKCS004
02384                                    TO DP013-PARAGRAPH             INKCS004
02385          MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'          INKCS004
02386                                    TO DP013-MESSAGE-TEXT(1)       INKCS004
02387          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02388      END-IF.                                                      INKCS004
02389 /                                                                 INKCS004
02390 *----------------------------------------------------------------*INKCS004
02391 *  WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,  *INKCS004
02392 *  SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT  *INKCS004
02393 *  THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO     *INKCS004
02394 *  THE USER.                                                     *INKCS004
02395 *----------------------------------------------------------------*INKCS004
02396                                                                   INKCS004
02397  6000-REFRESH-PANEL-FROM-TEMP.                                    INKCS004
02398                                                                   INKCS004
02399      IF  ASC-UPDATE-TSQ                                           INKCS004
02400          SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS004
02401          MOVE ZERO                 TO PV-SAVE-ITEM                INKCS004
02402                                       PV-SEL-QUEUE-ITEM           INKCS004
02403          MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        INKCS004
02404                                    TO PV-HOLD-SELECTED-ITEMS      INKCS004
02405          PERFORM 6100-UPDATE-TSQ                                  INKCS004
02406              UNTIL PV-ITEMS-PROCESSED >                           INKCS004
02407                                      ASC-NUMBER-OF-SELECTED-ITEMS INKCS004
02408          PERFORM 6010-DELETE-SEL-QUEUE                            INKCS004
02409          INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS004
02410                     ASC-BLOCK-ENTRIES (2)                         INKCS004
02411                                                                   INKCS004
02412          DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                 INKCS004
02413                 GIVING PV-BLOCK-NUMBER                            INKCS004
02414                 REMAINDER PV-REMAINDER                            INKCS004
02415          IF  PV-REMAINDER > ZERO                                  INKCS004
02416              ADD +1     TO PV-BLOCK-NUMBER                        INKCS004
02417          END-IF                                                   INKCS004
02418          MOVE +1        TO ASC-BLK-SUB                            INKCS004
02419          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
02420          ADD +1         TO ASC-BLK-SUB                            INKCS004
02421                            PV-BLOCK-NUMBER                        INKCS004
02422          PERFORM 4410-READ-TEMP-STORAGE                           INKCS004
02423      END-IF.                                                      INKCS004
02424                                                                   INKCS004
02425 *----------------------------------------------------------------*INKCS004
02426 *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *INKCS004
02427 *----------------------------------------------------------------*INKCS004
02428                                                                   INKCS004
02429  6010-DELETE-SEL-QUEUE.                                           INKCS004
02430                                                                   INKCS004
02431      EXEC CICS                                                    INKCS004
02432          DELETEQ TS                                               INKCS004
02433              QUEUE (ASC-SEL-QUEUE-NAME2)                          INKCS004
02434              RESP  (PV-CICS-RESP)                                 INKCS004
02435      END-EXEC.                                                    INKCS004
02436                                                                   INKCS004
02437      IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                      INKCS004
02438         (PV-CICS-RESP = DFHRESP(QIDERR)))                         INKCS004
02439          CONTINUE                                                 INKCS004
02440      ELSE                                                         INKCS004
02441          SET DP013-CICS-ABEND TO TRUE                             INKCS004
02442          SET DP013-NO-ROLLBACK TO TRUE                            INKCS004
02443          MOVE '6010-DELETE-SEL-QUEUE'                             INKCS004
02444                         TO DP013-PARAGRAPH                        INKCS004
02445          MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'         INKCS004
02446                         TO DP013-MESSAGE-TEXT (1)                 INKCS004
02447          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02448      END-IF.                                                      INKCS004
02449                                                                   INKCS004
02450 *----------------------------------------------------------------*INKCS004
02451 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *INKCS004
02452 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *INKCS004
02453 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *INKCS004
02454 *    THE TEMP QUEUE.                                             *INKCS004
02455 *----------------------------------------------------------------*INKCS004
02456                                                                   INKCS004
02457  6100-UPDATE-TSQ.                                                 INKCS004
02458                                                                   INKCS004
02459      ADD 1                     TO  PV-SEL-QUEUE-ITEM.             INKCS004
02460      PERFORM 6105-READ-SEL-QUEUE.                                 INKCS004
02461      PERFORM                                                      INKCS004
02462          VARYING IN003-IDX                                        INKCS004
02463          FROM 1 BY 1                                              INKCS004
02464          UNTIL ((PV-ITEMS-PROCESSED >                             INKCS004
02465                               PV-HOLD-SELECTED-ITEMS)             INKCS004
02466          OR (IN003-IDX GREATER THAN IN003-MAX-ITEMS))             INKCS004
02467          IF  IN003-ACTION-SW (IN003-IDX) > SPACE                  INKCS004
02468              PERFORM 6110-PROCESS-ITEMS                           INKCS004
02469          END-IF                                                   INKCS004
02470          ADD +1                TO PV-ITEMS-PROCESSED              INKCS004
02471      END-PERFORM.                                                 INKCS004
02472      PERFORM 6111-WRITE-LIST-QUEUE.                               INKCS004
02473 *----------------------------------------------------------------*INKCS004
02474 *    READ THE LIST QUEUE RECORD.                                 *INKCS004
02475 *----------------------------------------------------------------*INKCS004
02476                                                                   INKCS004
02477  6105-READ-SEL-QUEUE.                                             INKCS004
02478                                                                   INKCS004
02479      EXEC CICS                                                    INKCS004
02480           READQ TS                                                INKCS004
02481               QUEUE (ASC-SEL-QUEUE-NAME2)                         INKCS004
02482               INTO  (IN003-SELECTED-LINES)                        INKCS004
02483               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS004
02484               RESP  (PV-CICS-RESP)                                INKCS004
02485      END-EXEC.                                                    INKCS004
02486                                                                   INKCS004
02487      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS004
02488          CONTINUE                                                 INKCS004
02489      ELSE                                                         INKCS004
02490          SET DP013-CICS-ABEND      TO  TRUE                       INKCS004
02491          SET DP013-NO-ROLLBACK     TO  TRUE                       INKCS004
02492          MOVE '6105-READ-SEL-QUEUE'                               INKCS004
02493                                    TO  DP013-PARAGRAPH            INKCS004
02494          MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'           INKCS004
02495                                    TO  DP013-MESSAGE-TEXT(1)      INKCS004
02496          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02497      END-IF.                                                      INKCS004
02498 /                                                                 INKCS004
02499 *----------------------------------------------------------------*INKCS004
02500 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*INKCS004
02501 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*INKCS004
02502 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *INKCS004
02503 *----------------------------------------------------------------*INKCS004
02504                                                                   INKCS004
02505  6110-PROCESS-ITEMS.                                              INKCS004
02506                                                                   INKCS004
02507      COMPUTE PV-ITEM = 1 + ((IN003-ITEM (IN003-IDX) - 1)          INKCS004
02508                      / PC-ITEMS-PER-PANEL).                       INKCS004
02509                                                                   INKCS004
02510      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                           INKCS004
02511          IF  PV-SAVE-ITEM NOT EQUAL ZERO                          INKCS004
02512              PERFORM 6111-WRITE-LIST-QUEUE                        INKCS004
02513          END-IF                                                   INKCS004
02514          MOVE PV-ITEM          TO PV-SAVE-ITEM                    INKCS004
02515                                                                   INKCS004
02516          PERFORM 6112-READ-LIST-QUEUE                             INKCS004
02517          IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)               INKCS004
02518              CONTINUE                                             INKCS004
02519          END-IF                                                   INKCS004
02520      END-IF.                                                      INKCS004
02521                                                                   INKCS004
02522      COMPUTE PV-SUB = (IN003-ITEM (IN003-IDX)                     INKCS004
02523                     - LQW-LIST-ITEM-NUMBER (1)) + 1.              INKCS004
02524      SET LQW-DESELECT (PV-SUB) TO    TRUE                         INKCS004
02525      MOVE LQW-SELECTED-SW (PV-SUB)                                INKCS004
02526                                TO LQW-TEMP-SELECTED-SW (PV-SUB).  INKCS004
02527      EVALUATE TRUE                                                INKCS004
02528          WHEN IN003-INQ (IN003-IDX)                               INKCS004
02529               MOVE PC-INQ      TO LQW-ACTION (PV-SUB)             INKCS004
02530                                                                   INKCS004
02531          WHEN IN003-UPD (IN003-IDX)                               INKCS004
02532               MOVE PC-UPD      TO LQW-ACTION (PV-SUB)             INKCS004
02533                                                                   INKCS004
02534          WHEN IN003-DEL (IN003-IDX)                               INKCS004
02535               MOVE PC-DEL      TO LQW-ACTION (PV-SUB)             INKCS004
02536                                                                   INKCS004
02537          WHEN IN003-ERR (IN003-IDX)                               INKCS004
02538               MOVE PC-ERR      TO LQW-ACTION (PV-SUB)             INKCS004
02539      END-EVALUATE.                                                INKCS004
02540                                                                   INKCS004
02541      SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                INKCS004
02542                                                                   INKCS004
02543 *----------------------------------------------------------------*INKCS004
02544 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS004
02545 *----------------------------------------------------------------*INKCS004
02546                                                                   INKCS004
02547  6111-WRITE-LIST-QUEUE.                                           INKCS004
02548                                                                   INKCS004
02549      EXEC CICS                                                    INKCS004
02550          WRITEQ TS                                                INKCS004
02551              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02552              FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS004
02553              ITEM  (PV-SAVE-ITEM)                                 INKCS004
02554              REWRITE                                              INKCS004
02555              RESP  (PV-CICS-RESP)                                 INKCS004
02556      END-EXEC.                                                    INKCS004
02557                                                                   INKCS004
02558      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS004
02559          CONTINUE                                                 INKCS004
02560      ELSE                                                         INKCS004
02561          SET DP013-CICS-ABEND  TO  TRUE                           INKCS004
02562          SET DP013-NO-ROLLBACK TO  TRUE                           INKCS004
02563          MOVE '6111-WRITE-LIST-QUEUE'                             INKCS004
02564                                TO  DP013-PARAGRAPH                INKCS004
02565          MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS004
02566                                TO  DP013-MESSAGE-TEXT(1)          INKCS004
02567          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02568      END-IF.                                                      INKCS004
02569 /                                                                 INKCS004
02570 *----------------------------------------------------------------*INKCS004
02571 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS004
02572 *----------------------------------------------------------------*INKCS004
02573                                                                   INKCS004
02574  6112-READ-LIST-QUEUE.                                            INKCS004
02575                                                                   INKCS004
02576      EXEC CICS                                                    INKCS004
02577          READQ TS                                                 INKCS004
02578              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS004
02579              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS004
02580              ITEM  (PV-ITEM)                                      INKCS004
02581              RESP  (PV-CICS-RESP)                                 INKCS004
02582      END-EXEC.                                                    INKCS004
02583                                                                   INKCS004
02584      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS004
02585          CONTINUE                                                 INKCS004
02586      ELSE                                                         INKCS004
02587          SET DP013-CICS-ABEND        TO TRUE                      INKCS004
02588          SET DP013-NO-ROLLBACK       TO TRUE                      INKCS004
02589          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS004
02590          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS004
02591                                      TO DP013-MESSAGE-TEXT(1)     INKCS004
02592          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02593      END-IF.                                                      INKCS004
02594                                                                   INKCS004
02595 /                                                                 INKCS004
02596 *----------------------------------------------------------------*INKCS004
02597 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS004
02598 *----------------------------------------------------------------*INKCS004
02599                                                                   INKCS004
02600  9100-READ-SEL-QUEUE.                                             INKCS004
02601                                                                   INKCS004
02602      EXEC CICS                                                    INKCS004
02603          READQ TS                                                 INKCS004
02604              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS004
02605              INTO  (IN002-SELECTED-AREAS)                         INKCS004
02606              ITEM  (ASC-SEL-ITEM)                                 INKCS004
02607              RESP  (PV-CICS-RESP)                                 INKCS004
02608      END-EXEC.                                                    INKCS004
02609                                                                   INKCS004
02610      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS004
02611          CONTINUE                                                 INKCS004
02612      ELSE                                                         INKCS004
02613          SET DP013-LOGIC-ABEND                                    INKCS004
02614              DP013-NO-ROLLBACK     TO TRUE                        INKCS004
02615          MOVE '9100-READ-SEL-QUEUE'                               INKCS004
02616                                    TO DP013-PARAGRAPH             INKCS004
02617          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   INKCS004
02618                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
02619          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02620      END-IF.                                                      INKCS004
02621                                                                   INKCS004
02622 *----------------------------------------------------------------*INKCS004
02623 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS004
02624 *----------------------------------------------------------------*INKCS004
02625                                                                   INKCS004
02626  9200-REWRITE-LIST-QUEUE.                                         INKCS004
02627                                                                   INKCS004
02628      EXEC CICS                                                    INKCS004
02629           WRITEQ TS                                               INKCS004
02630               QUEUE  (ASC-SEL-QUEUE-NAME)                         INKCS004
02631               FROM   (IN002-SELECTED-AREAS)                       INKCS004
02632               ITEM   (ASC-SEL-ITEM)                               INKCS004
02633               RESP   (PV-CICS-RESP)                               INKCS004
02634               REWRITE                                             INKCS004
02635      END-EXEC.                                                    INKCS004
02636                                                                   INKCS004
02637      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                   INKCS004
02638          SET DP013-CICS-ABEND                                     INKCS004
02639              DP013-NO-ROLLBACK     TO TRUE                        INKCS004
02640          MOVE '9200-REWRITE-LIST-QUEUE'                           INKCS004
02641                                    TO DP013-PARAGRAPH             INKCS004
02642          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   INKCS004
02643                                    TO DP013-MESSAGE-TEXT (1)      INKCS004
02644          PERFORM DP013-0000-PROCESS-ABEND                         INKCS004
02645      END-IF.                                                      INKCS004
02646 /                                                                 INKCS004
02647 *----------------------------------------------------------------*INKCS004
02648 *    ABEND PROCESSOR MODULE                                      *INKCS004
02649 *----------------------------------------------------------------*INKCS004
02650                                                                   INKCS004
02651      COPY DPPD013.                                                INKCS004
