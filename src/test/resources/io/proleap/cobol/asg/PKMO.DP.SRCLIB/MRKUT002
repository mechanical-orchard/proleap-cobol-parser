000100***************************************************************** 02/14/96
000200 IDENTIFICATION DIVISION.                                         MRKUT002
000300*****************************************************************    LV001
000400                                                                  MRKUT002
000500 PROGRAM-ID.             MRKUT002.                                MRKUT002
000600 AUTHOR.                 CHRISTINE FOLEY.                         MRKUT002
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                 MRKUT002
000800 DATE-WRITTEN            10-XX-92.                                MRKUT002
000900 DATE-COMPILED.                                                   MRKUT002
001000                                                                  MRKUT002
001100***************************************************************** MRKUT002
001200*          UPDATE INFOREM CONTROL FILE                          * MRKUT002
001300*                                                               * MRKUT002
001400*  THIS PROGRAM WILL UPDATE THE RUN MODE FIELD ON THE CONTROL   * MRKUT002
001500*  FILE SYSTEM RECORD.                                          * MRKUT002
001600***************************************************************** MRKUT002
001700*  CHANGE MADE:                                                 * MRKUT002
001800*                                                               * MRKUT002
001900***************************************************************** MRKUT002
002000* CHANGED 02/08/2001 BY LINDA FRIESS                              MRKUT002
002100*   PM00390820                                                    MRKUT002
002200*         MODIFIED PARAGRAPH D000 TO HARD-CODE BPN'S IF WEEK      MRKUT002
002300*         NUMBER IS 53 OR 1-4.                                    MRKUT002
002500***************************************************************** MRKUT002
002510* CHANGED 01/30/2003 BY DONALD TOMLINSON                          MRKUT002
002520*   WR 241611                                                     MRKUT002
002530*         CHANGED HOW CONTROL CARD IS BUILT FOR FIELD S-TNOW-DN.  MRKUT002
002540*         FIELD WILL NOW BE GENERATED FROM CONTROL CARD WHICH     MRKUT002
002550*         WILL ONLY NEED TO BE DONE IN JOB MRK115 AND NOT IN      MRKUT002
002551*         JOB MRK015 WHICH WILL NO LONGER NEED TO RUN.            MRKUT002
002500***************************************************************** MRKUT002
002510* CHANGED 09/30/2005 BY LINDA FRIESS                              MRKUT002
002520*   WR 28751                                                      MRKUT002
002530*         PUT IN CHANGES ABOVE AS WELL AS MOVING PARA. D000 TO    MRKUT002
002540*         EXECUTE FOR RUN-CYCLE X.  IN D000, CHANGED DATE LOGIC   MRKUT002
002540*         TO LOOK AT CURRENT WEEK INSTEAD OF ONE WEEK AGO, WHEN   MRKUT002
002540*         CALCULATING BPN'S.                                      MRKUT002
002560***************************************************************** MRKUT002
002600 ENVIRONMENT DIVISION.                                            MRKUT002
002700***************************************************************** MRKUT002
002800                                                                  MRKUT002
002900 CONFIGURATION SECTION.                                           MRKUT002
003000                                                                  MRKUT002
003100 SOURCE-COMPUTER.        IBM-3090.                                MRKUT002
003200 OBJECT-COMPUTER.        IBM-3090.                                MRKUT002
003300                                                                  MRKUT002
003400 INPUT-OUTPUT SECTION.                                            MRKUT002
003500                                                                  MRKUT002
003600 FILE-CONTROL.                                                    MRKUT002
003700*                                                                 MRKUT002
003800     SELECT ICCTL-CNTL-FILE     ASSIGN        TO INP01            MRKUT002
003900                                ORGANIZATION  IS INDEXED          MRKUT002
004000                                ACCESS        IS RANDOM           MRKUT002
004100                                RECORD KEY    IS ICCTL-KEY        MRKUT002
004200                                FILE STATUS   IS PV-VSAM-STATUS.  MRKUT002
004300     SELECT DATE-FILE           ASSIGN        TO INP02.           MRKUT002
004400*                                                                 MRKUT002
004500***************************************************************** MRKUT002
004600 DATA DIVISION.                                                   MRKUT002
004700***************************************************************** MRKUT002
004800*                                                                 MRKUT002
004900 FILE SECTION.                                                    MRKUT002
005000*                                                                 MRKUT002
005100 FD  ICCTL-CNTL-FILE.                                             MRKUT002
005200 01  ICCTL-CNTL-REC.                                              MRKUT002
005300     05  ICCTL-KEY                   PIC X(21).                   MRKUT002
005400     05  FILLER                      PIC X(379).                  MRKUT002
005500*                                                                 MRKUT002
005600 FD  DATE-FILE                                                    MRKUT002
005700     RECORDING MODE IS F                                          MRKUT002
005800     LABEL RECORDS ARE STANDARD                                   MRKUT002
005900     RECORD CONTAINS 80 CHARACTERS                                MRKUT002
006000     BLOCK CONTAINS 0 RECORDS.                                    MRKUT002
006100 01  DATE-REC                        PIC X(80).                   MRKUT002
006200*                                                                 MRKUT002
006300 WORKING-STORAGE SECTION.                                         MRKUT002
006400*                                                                 MRKUT002
006500 01  FILLER.                                                      MRKUT002
006600     05  FILLER                      PIC  X(36)    VALUE          MRKUT002
006700         ' ***** WORKING STORAGE PGM=MRKUT002 '.                  MRKUT002
006800     05  PGM-COMPILED                PIC  X(20)    VALUE SPACE.   MRKUT002
006900     05  FILLER                      PIC  X(10)    VALUE          MRKUT002
007000         ' LOCATION='.                                            MRKUT002
007100     05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.   MRKUT002
007200     05  FILLER                      PIC  X(06)    VALUE          MRKUT002
007300         ' *****'.                                                MRKUT002
007400 EJECT                                                            MRKUT002
007500 01  PROGRAM-SWITCHES.                                            MRKUT002
007600     05  PS-END-OF-SYSTEM-REC        PIC  X(01)    VALUE 'N'.     MRKUT002
007700         88  END-OF-SYSTEM-REC                     VALUE 'Y'.     MRKUT002
007800     05  PS-END-OF-DATE-REC          PIC  X(01)    VALUE 'N'.     MRKUT002
007900         88  END-OF-DATE-REC                       VALUE 'Y'.     MRKUT002
008000*                                                                 MRKUT002
008100/                                                                 MRKUT002
008200 01  PC-CONSTANTS.                                                MRKUT002
008300     05  PC-PLUS-1                   PIC S9(04)    VALUE +1       MRKUT002
008400                                                   COMP SYNC.     MRKUT002
008500     05  PC-PLUS-2                   PIC S9(04)    VALUE +2       MRKUT002
008600                                                   COMP SYNC.     MRKUT002
008700     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100     MRKUT002
008800                                                   COMP SYNC.     MRKUT002
008900     05  PC-1                        PIC S9(03)    VALUE +1       MRKUT002
009000                                                   COMP-3.        MRKUT002
009100     05  PC-2                        PIC S9(03)    VALUE +2       MRKUT002
009200                                                   COMP-3.        MRKUT002
009300     05  PC-3                        PIC S9(03)    VALUE +3       MRKUT002
009400                                                   COMP-3.        MRKUT002
009500     05  PC-4                        PIC S9(03)    VALUE +4       MRKUT002
                                                         COMP-3.                
009600     05  PC-5                        PIC  X(01)    VALUE '5'.     MRKUT002
009700     05  PC-9                        PIC  X(01)    VALUE '9'.     MRKUT002
009800     05  PC-00                       PIC  X(02)    VALUE '00'.    MRKUT002
009900     05  PC-01                       PIC  X(02)    VALUE '01'.    MRKUT002
010000     05  PC-04                       PIC  X(02)    VALUE '04'.    MRKUT002
010100     05  PC-07                       PIC  9(02)    VALUE   7.     MRKUT002
010200     05  PC-19                       PIC  X(02)    VALUE '19'.    MRKUT002
010300     05  PC-20                       PIC  X(02)    VALUE '20'.    MRKUT002
010400     05  PC-49                       PIC  9(02)    VALUE 49.      MRKUT002
010500     05  PC-50                       PIC  9(02)    VALUE 50.      MRKUT002
010600     05  PC-51                       PIC  9(02)    VALUE 51.      MRKUT002
010700     05  PC-52                       PIC  9(02)    VALUE 52.      MRKUT002
010800     05  PC-WEEK-53                  PIC  9(02)    VALUE 53.      MRKUT002
010900     05  PC-WEEK-1                   PIC  9(02)    VALUE 1.       MRKUT002
011000     05  PC-WEEK-2                   PIC  9(02)    VALUE 2.       MRKUT002
011100     05  PC-WEEK-3                   PIC  9(02)    VALUE 3.       MRKUT002
011200     05  PC-WEEK-4                   PIC  9(02)    VALUE 4.       MRKUT002
011300     05  PC-A                        PIC  X(01)    VALUE 'A'.     MRKUT002
011400     05  PC-D                        PIC  X(01)    VALUE 'D'.     MRKUT002
011500     05  PC-M                        PIC  X(01)    VALUE 'M'.     MRKUT002
011600     05  PC-N                        PIC  X(01)    VALUE 'N'.     MRKUT002
011700     05  PC-P                        PIC  X(01)    VALUE 'P'.     MRKUT002
011800     05  PC-U                        PIC  X(01)    VALUE 'U'.     MRKUT002
011900     05  PC-W                        PIC  X(01)    VALUE 'W'.     MRKUT002
012000     05  PC-X                        PIC  X(01)    VALUE 'X'.     MRKUT002
012100     05  PC-Y                        PIC  X(01)    VALUE 'Y'.     MRKUT002
012200     05  PC-F2                       PIC  X(02)    VALUE 'F2'.    MRKUT002
012300     05  PC-PR                       PIC  X(02)    VALUE 'PR'.    MRKUT002
012400     05  PC-TR                       PIC  X(02)    VALUE 'TR'.    MRKUT002
012500     05  PC-RCD-TYPE                 PIC  X(03)    VALUE 'SYS'.   MRKUT002
012600     05  PC-DECR-DAYS                PIC  X(05)    VALUE '00004'. MRKUT002
012700     05  PC-DECR-7-DAYS              PIC  X(05)    VALUE '00007'. MRKUT002
012800     05  PC-DECR-21-DAYS             PIC  X(05)    VALUE '00021'. MRKUT002
012900     05  PC-DASH                     PIC  X(01)    VALUE '-'.     MRKUT002
013000     05  PC-SLASH                    PIC  X(01)    VALUE '/'.     MRKUT002
013100     05  PC-PGM-DPKUT500             PIC  X(08)    VALUE          MRKUT002
013200                                                   'DPKUT500'.    MRKUT002
013300     05  PC-ASTERISK                 PIC  X(30)    VALUE          MRKUT002
013400         '*****************************'.                         MRKUT002
013500 EJECT                                                            MRKUT002
013600*                                                                 MRKUT002
013700 01  PV-NULL-INDICATORS.                                          MRKUT002
013800     05  PV-DTL-ORD-QTY-UM-CDE-NULL  PIC S9(04)    VALUE +0       MRKUT002
013900                                                   COMP SYNC.     MRKUT002
014000*                                                                 MRKUT002
014100 01  PV-ACCUMULATOR-FIELDS.                                       MRKUT002
014200     05  PV-SYSTEM-INPUT-CNT         PIC S9(03)    VALUE ZERO     MRKUT002
014300                                                   COMP-3.        MRKUT002
014400     05  PV-PARM-INPUT-CNT           PIC S9(03)    VALUE ZERO     MRKUT002
014500                                                   COMP-3.        MRKUT002
014600     05  PV-SYSTEM-OUTPUT-CNT        PIC S9(04)    VALUE ZERO     MRKUT002
014700                                                   COMP-3.        MRKUT002
014800 EJECT                                                            MRKUT002
014900*                                                                 MRKUT002
015000 01  PV-MISC-FIELDS.                                              MRKUT002
015100     05  PV-ABEND-CODE               PIC S9(04)    VALUE ZERO     MRKUT002
015200                                                   COMP SYNC.     MRKUT002
015300     05  PV-RBA                      PIC S9(09)    VALUE ZERO     MRKUT002
015400                                                   COMP SYNC.     MRKUT002
015500     05  PV-VSAM-STATUS              PIC  X(02) VALUE SPACE.      MRKUT002
015600         88  PV-VSAM-SUCCESSFUL                 VALUE '00'.       MRKUT002
015700         88  PV-VSAM-NOT-FOUND                  VALUE '23'.       MRKUT002
015800     05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.  MRKUT002
015900     05  PV-TODAYS-DAY               PIC  9(01)    VALUE ZERO.    MRKUT002
016000     05  PV-CURRENT-DATE.                                         MRKUT002
016100         10  PV-CURRENT-DATE-CC      PIC  9(02)    VALUE ZERO.    MRKUT002
016200         10  PV-CURRENT-DATE-YR      PIC  9(02)    VALUE ZERO.    MRKUT002
016300         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
016400         10  PV-CURRENT-DATE-MO      PIC  9(02)    VALUE ZERO.    MRKUT002
016500         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
016600         10  PV-CURRENT-DATE-DY      PIC  9(02)    VALUE ZERO.    MRKUT002
016700     05  PV-DATE-RANGE-MAX.                                       MRKUT002
016800         10  PV-LAST-FP-DATE-CC      PIC  9(02)    VALUE ZERO.    MRKUT002
016900         10  PV-LAST-FP-DATE-YR      PIC  9(02)    VALUE ZERO.    MRKUT002
017000         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
017100         10  PV-LAST-FP-DATE-MO      PIC  9(02)    VALUE ZERO.    MRKUT002
017200         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
017300         10  PV-LAST-FP-DATE-DY      PIC  9(02)    VALUE ZERO.    MRKUT002
017400     05  PV-MMDDYY.                                               MRKUT002
017500         10  PV-MM                   PIC  9(02)    VALUE ZERO.    MRKUT002
017600         10  PV-DD                   PIC  9(02)    VALUE ZERO.    MRKUT002
017700         10  PV-YY                   PIC  9(02)    VALUE ZERO.    MRKUT002
017800     05  PV-YYMMDD                   PIC  9(06)    VALUE ZERO.    MRKUT002
017900     05  PV-TODAYS-TIME.                                          MRKUT002
018000         10  PV-TODAYS-TIME-HH       PIC  9(02)    VALUE ZERO.    MRKUT002
018100         10  PV-TODAYS-TIME-MM       PIC  9(02)    VALUE ZERO.    MRKUT002
018200         10  PV-TODAYS-TIME-SS       PIC  9(02)    VALUE ZERO.    MRKUT002
018300         10  FILLER                  PIC  9(02)    VALUE ZERO.    MRKUT002
018400     05  PV-SUBSCRIPTS.                                           MRKUT002
018500         10  PV-S-CYCLE-SUB          PIC  9(01)    VALUE ZERO.    MRKUT002
018600     05  PV-BPN-WEEK                 PIC  9(02)    VALUE ZERO.    MRKUT002
018700     05  PV-BPN-MONTH                PIC  9(02)    VALUE ZERO.    MRKUT002
018800     05  PV-CURRENT-FIRST-DAY.                                    MRKUT002
018900         10  PV-CURR-FP-DATE-CC      PIC  9(02)    VALUE ZERO.    MRKUT002
019000         10  PV-CURR-FP-DATE-YR      PIC  9(02)    VALUE ZERO.    MRKUT002
019100         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
019200         10  PV-CURR-FP-DATE-MO      PIC  9(02)    VALUE ZERO.    MRKUT002
019300         10  FILLER                  PIC  X(01)    VALUE '-'.     MRKUT002
019400         10  PV-CURR-FP-DATE-DY      PIC  9(02)    VALUE ZERO.    MRKUT002
019500     05  PV-CURRENT-PERIOD           PIC  9(02)    VALUE ZERO.    MRKUT002
019600 01  DATE-RECORD.                                                 MRKUT002
019700     05  DATE-RECORD-CC              PIC  X(02)    VALUE SPACES.  MRKUT002
019800     05  DATE-RECORD-YR              PIC  X(02)    VALUE SPACES.  MRKUT002
019900     05  FILLER                      PIC  X(01)    VALUE SPACES.  MRKUT002
020000     05  DATE-RECORD-MO              PIC  X(02)    VALUE SPACES.  MRKUT002
020100     05  FILLER                      PIC  X(01)    VALUE SPACES.  MRKUT002
020200     05  DATE-RECORD-DY              PIC  X(02)    VALUE SPACES.  MRKUT002
020300 EJECT                                                            MRKUT002
020400*                                                                 MRKUT002
020500     COPY DPWS500.                                                MRKUT002
020600 EJECT                                                            MRKUT002
020700     COPY MRRD001I.                                               MRKUT002
020800 EJECT                                                            MRKUT002
020900 01  SYSTEM-RECORD.                                               MRKUT002
021000     COPY ICI005C.                                                MRKUT002
021100 EJECT                                                            MRKUT002
021200*                                                                 MRKUT002
021300***************************************************************** MRKUT002
021400*PROCEDURE DIVISION.                                              MRKUT002
021500***************************************************************** MRKUT002
021600 PROCEDURE DIVISION.                                              MRKUT002
021700*                                                                 MRKUT002
021800 A000-MAINLINE-PROCESSING.                                        MRKUT002
021900*                                                                 MRKUT002
022000     PERFORM A100-INITIALIZATION.                                 MRKUT002
022100*                                                                 MRKUT002
022200     PERFORM B000-PROCESS-SYSTEM-RECORD.                          MRKUT002
022300*                                                                 MRKUT002
022400     PERFORM Y100-END-ROUTINE.                                    MRKUT002
022500*                                                                 MRKUT002
022600     GOBACK.                                                      MRKUT002
022700*                                                                 MRKUT002
022800 EJECT                                                            MRKUT002
022900******************************************************************MRKUT002
023000** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                 MRKUT002
023100** CALL CALENDAR ROUTINES TO RETRIEVE VALUES NEEDED IN PROGRAM    MRKUT002
023200******************************************************************MRKUT002
023300 A100-INITIALIZATION.                                             MRKUT002
023400*                                                                 MRKUT002
023500     OPEN    I-O ICCTL-CNTL-FILE                                  MRKUT002
023600           INPUT DATE-FILE.                                       MRKUT002
023700*                                                                 MRKUT002
023800     IF  PV-VSAM-SUCCESSFUL                                       MRKUT002
023900        CONTINUE                                                  MRKUT002
024000     ELSE                                                         MRKUT002
024100        IF PV-VSAM-NOT-FOUND                                      MRKUT002
024200           DISPLAY '** ABEND **'                                  MRKUT002
024300           DISPLAY '** PROGRAM    = MRKUT002'                     MRKUT002
024400           DISPLAY '** PARAGRAPH  = R000-READ-SYSTEM-RECORD'      MRKUT002
024500           DISPLAY '** MESSAGE    = '                             MRKUT002
024600                   'FILE NOT FOUND,  STATUS: ' PV-VSAM-STATUS     MRKUT002
024700           MOVE +4005           TO PV-ABEND-CODE                  MRKUT002
024800           CALL 'ILBOABN0'      USING PV-ABEND-CODE               MRKUT002
024900        ELSE                                                      MRKUT002
025000           DISPLAY '** ABEND **'                                  MRKUT002
025100           DISPLAY '** PROGRAM    = MRKUT002'                     MRKUT002
025200           DISPLAY '** PARAGRAPH  = R000-READ-SYSTEM-RECORD'      MRKUT002
025300           DISPLAY '** MESSAGE    = '                             MRKUT002
025400                   'BAD VSAM READ, STATUS = ' PV-VSAM-STATUS      MRKUT002
025500           MOVE +4005           TO PV-ABEND-CODE                  MRKUT002
025600           CALL 'ILBOABN0'      USING PV-ABEND-CODE               MRKUT002
025700        END-IF                                                    MRKUT002
025800     END-IF.                                                      MRKUT002
025900*                                                                 MRKUT002
026000     INITIALIZE SYSTEM-RECORD.                                    MRKUT002
026100*                                                                 MRKUT002
026200     PERFORM R100-READ-DATE-RECORD.                               MRKUT002
026300     ACCEPT PARAMETER-INPUT-RECORD.                               MRKUT002
026400*                                                                 MRKUT002
026500     MOVE DATE-RECORD-CC      TO PV-CURRENT-DATE-CC.              MRKUT002
026600     MOVE DATE-RECORD-YR      TO PV-CURRENT-DATE-YR.              MRKUT002
026700     MOVE DATE-RECORD-MO      TO PV-CURRENT-DATE-MO.              MRKUT002
026800     MOVE DATE-RECORD-DY      TO PV-CURRENT-DATE-DY.              MRKUT002
026900     SET DPG52-LK-DTE-ISO-FMT TO TRUE.                            MRKUT002
027000     MOVE PV-CURRENT-DATE   TO DPG52-LK-DATE-INPUT.               MRKUT002
027100     MOVE PC-F2               TO DPG51-CALENDAR-TYPE.             MRKUT002
027200     PERFORM E000-CALENDAR-ROUTINE.                               MRKUT002
027300*                                                                 MRKUT002
027400*                                                                 MRKUT002
027500 EJECT                                                            MRKUT002
027600******************************************************************MRKUT002
027700*  THIS IS THE MAIN CONTROLLING PARAGRAPH FOR PROCESSING THE     *MRKUT002
027800*  SYSTEM RECORD.                                                *MRKUT002
027900*                                                                *MRKUT002
028000*  IF RUN-TYPE IS X, RESET THE RUN TYPE & BPN'S                  *MRKUT002
028500*                                                                *MRKUT002
028300*  IF RUN-TYPE IS ANY OTHER VALUE (M, W, OR D), RESET THE        *MRKUT002
028300*  RUN TYPE, TNOW_WN AND TNOW_DN                                 *MRKUT002
028600*                                                                *MRKUT002
028700******************************************************************MRKUT002
028800 B000-PROCESS-SYSTEM-RECORD.                                      MRKUT002
028900*                                                                 MRKUT002
029000     MOVE MR001I-S-SYS-ID    TO   S-SYS-ID.                       MRKUT002
029100     MOVE PC-RCD-TYPE        TO   S-RCD-TYPE.                     MRKUT002
029200     MOVE LOW-VALUES         TO   S-RCD-ID.                       MRKUT002
029300     MOVE S-KEY              TO   ICCTL-KEY.                      MRKUT002
029400*                                                                 MRKUT002
029500     PERFORM R000-READ-SYSTEM-RECORD.                             MRKUT002
029600*                                                                 MRKUT002
029700*    DISPLAY 'SYSTEM ID:' MR001I-S-SYS-ID.                        MRKUT002
029800*    DISPLAY 'RUN CYCLE:' MR001I-S-RUN-CYCLE.                     MRKUT002
029900*    DISPLAY 'RUN TYPE: ' MR001I-S-RUN-TYPE.                      MRKUT002
030000*                                                                 MRKUT002
030100     IF MR001I-S-RUN-CYCLE = PC-X                                 MRKUT002
030200         MOVE MR001I-S-RUN-TYPE      TO S-RUN-TYPE                MRKUT002
031500         PERFORM D000-SALES-REVISION-RUN                          MRKUT002
030300     ELSE                                                         MRKUT002
030400**         S-RUN-CYCLE = W, M, OR D                               MRKUT002
030600         MOVE MR001I-S-RUN-TYPE      TO S-RUN-TYPE                MRKUT002
                                                                                
030700         IF DPG56-FISCAL-WEEK-NBR EQUAL PC-WEEK-53                MRKUT002
030800             MOVE PC-52              TO S-TNOW-WN                 MRKUT002
030900         ELSE                                                     MRKUT002
031000             MOVE DPG56-FISCAL-WEEK-NBR                           MRKUT002
031100                                     TO S-TNOW-WN                 MRKUT002
031200         END-IF                                                   MRKUT002
031300                                                                  MRKUT002
031400         MOVE MR001I-S-DAY           TO S-TNOW-DN                 MRKUT002
032700     END-IF.                                                      MRKUT002
032800*                                                                 MRKUT002
032900     PERFORM W000-WRITE-SYSTEM-RECORD.                            MRKUT002
033000*                                                                 MRKUT002
033100 EJECT                                                            MRKUT002
033200******************************************************************MRKUT002
033300*  MOVE VALUES TO OUTPUT FOR A SALES REVISION RUN (TYPE-CD = 3)  *MRKUT002
033400******************************************************************MRKUT002
033500 D000-SALES-REVISION-RUN.                                         MRKUT002
033600*                                                                 MRKUT002
033700     MOVE PC-1                     TO PV-S-CYCLE-SUB.             MRKUT002
033800                                                                  MRKUT002
033900     IF S-DDU-WKS(PV-S-CYCLE-SUB) > PC-07                         MRKUT002
034000        MOVE PC-07                 TO S-DDU-WKS(PV-S-CYCLE-SUB)   MRKUT002
034100     END-IF.                                                      MRKUT002
034200*                                                                 MRKUT002
034300     MOVE PC-2                     TO PV-S-CYCLE-SUB.             MRKUT002
034400                                                                  MRKUT002
034500     IF S-DDU-WKS(PV-S-CYCLE-SUB) > PC-07                         MRKUT002
034600        MOVE PC-07                 TO S-DDU-WKS(PV-S-CYCLE-SUB)   MRKUT002
034700     END-IF.                                                      MRKUT002
034800*                                                                 MRKUT002
034900     MOVE PC-3                     TO PV-S-CYCLE-SUB.             MRKUT002
035000                                                                  MRKUT002
035100     IF S-DDU-WKS(PV-S-CYCLE-SUB) > PC-07                         MRKUT002
035200        MOVE PC-07                 TO S-DDU-WKS(PV-S-CYCLE-SUB)   MRKUT002
035300     END-IF.                                                      MRKUT002
035400**************************                                        MRKUT002
035500     MOVE MR001I-S-RUN-TYPE       TO S-RUN-TYPE.                  MRKUT002
035600*                                                                 MRKUT002
035700***  HARD CODE BPN'S IF WEEK NUMBER IS 53 OR 1-4                  MRKUT002
035800*                                                                 MRKUT002
035900     IF (DPG56-FISCAL-WEEK-NBR = PC-WEEK-53) OR                   MRKUT002
036000        (DPG56-FISCAL-WEEK-NBR = PC-WEEK-1)                       MRKUT002
036100        MOVE PC-1  TO S-BPN (PC-PLUS-1)                                   
036200        MOVE PC-50 TO S-BPN (PC-PLUS-2)                                   
036300     ELSE                                                                 
036400     IF DPG56-FISCAL-WEEK-NBR EQUAL PC-WEEK-2                     MRKUT002
036500        MOVE PC-2  TO S-BPN (PC-PLUS-1)                                   
036600        MOVE PC-51 TO S-BPN (PC-PLUS-2)                                   
036700     ELSE                                                                 
036800     IF DPG56-FISCAL-WEEK-NBR EQUAL PC-WEEK-3                     MRKUT002
036900        MOVE PC-3  TO S-BPN (PC-PLUS-1)                                   
037000        MOVE PC-52 TO S-BPN (PC-PLUS-2)                                   
037100     ELSE                                                                 
037200     IF DPG56-FISCAL-WEEK-NBR EQUAL PC-WEEK-4                     MRKUT002
037300        MOVE PC-4  TO S-BPN (PC-PLUS-1)                                   
037400        MOVE PC-1  TO S-BPN (PC-PLUS-2)                                   
037500     ELSE                                                                 
037600*                                                                 MRKUT002
037700***  CALCULATE BPN'S IF WEEK NUMBER BETWEEN 5 AND 52              MRKUT002
037800*                                                                 MRKUT002
037900*  GET CURRENT WEEK FOR UCYCLE 9                                          
038000        MOVE PC-1                    TO PV-S-CYCLE-SUB            MRKUT002
038900*                                                                         
039000        MOVE DPG56-FISCAL-WEEK-NBR   TO PV-BPN-WEEK               MRKUT002
039100        MOVE PV-BPN-WEEK             TO S-BPN(PV-S-CYCLE-SUB)     MRKUT002
039200*                                                                 MRKUT002
039300*  GET 4 WEEKS AGO FOR UCYCLE 5                                           
039400        MOVE PC-2                    TO PV-S-CYCLE-SUB            MRKUT002
040200*                                                                         
              COMPUTE PV-BPN-WEEK = DPG56-FISCAL-WEEK-NBR - 3                   
040200*                                                                         
040400        MOVE PV-BPN-WEEK             TO S-BPN(PV-S-CYCLE-SUB)     MRKUT002
040500     END-IF                                                               
040600     END-IF                                                               
040700     END-IF                                                               
040800     END-IF.                                                              
040900*                                                                 MRKUT002
041000***  MOVE PERIOD NUMBER TO BPN FOR UCYCLE 4                       MRKUT002
041100*                                                                 MRKUT002
041800     MOVE PC-3                    TO PV-S-CYCLE-SUB.              MRKUT002
041900     MOVE DPG56-FISCAL-PERIOD-NBR TO S-BPN(PV-S-CYCLE-SUB).       MRKUT002
042000*                                                                 MRKUT002
042100**   IF  MR001I-S-RUN-CYCLE = PC-M                                MRKUT002
042200**       MOVE PC-A                 TO S-DDRUNSW(PV-S-CYCLE-SUB)   MRKUT002
042300**   ELSE                                                         MRKUT002
042400**       MOVE PC-N                 TO S-DDRUNSW(PV-S-CYCLE-SUB)   MRKUT002
042500**   END-IF.                                                      MRKUT002
042600*                                                                 MRKUT002
042700******************************************************************MRKUT002
042800*    THIS SECTION CALLS THE CALENDAR ROUTINE                     *MRKUT002
042900******************************************************************MRKUT002
043000 E000-CALENDAR-ROUTINE.                                           MRKUT002
043100*                                                                 MRKUT002
043200*    DISPLAY 'E000-CALENDAR-ROUTINE'.                             MRKUT002
043300*                                                                 MRKUT002
043400     CALL PC-PGM-DPKUT500 USING DPG51 DPG52                       MRKUT002
043500                                DPG53 DPG54                       MRKUT002
043600                                DPG55 DPG56.                      MRKUT002
043700     EVALUATE TRUE                                                MRKUT002
043800     WHEN DPG54-SEVERE-ERROR                                      MRKUT002
043900         DISPLAY 'SEVERE ERROR IN CALENDAR ROUTINE'               MRKUT002
044000         DISPLAY DPG54-ERROR-MESSAGE                              MRKUT002
044100         CALL 'ILBOABN0'        USING PV-ABEND-CODE               MRKUT002
044200     WHEN DPG54-WARNING-ERROR                                     MRKUT002
044300         DISPLAY 'WARNING ERROR IN CALENDAR ROUTINE'              MRKUT002
044400         DISPLAY DPG54-ERROR-MESSAGE                              MRKUT002
044500     WHEN DPG54-NO-ERROR                                          MRKUT002
044600         CONTINUE                                                 MRKUT002
044700     END-EVALUATE.                                                MRKUT002
044800*                                                                *MRKUT002
044900******************************************************************MRKUT002
045000* THIS SECTION READS THE VSAM SYSTEM RECORD                      *MRKUT002
045100******************************************************************MRKUT002
045200 R000-READ-SYSTEM-RECORD.                                         MRKUT002
045300*                                                                 MRKUT002
045400     READ ICCTL-CNTL-FILE INTO  SYSTEM-RECORD                     MRKUT002
045500                                KEY IS ICCTL-KEY.                 MRKUT002
045600*                                                                 MRKUT002
045700     IF  PV-VSAM-SUCCESSFUL                                       MRKUT002
045800        CONTINUE                                                  MRKUT002
045900     ELSE                                                         MRKUT002
046000        IF PV-VSAM-NOT-FOUND                                      MRKUT002
046100           DISPLAY '** ABEND **'                                  MRKUT002
046200           DISPLAY '** PROGRAM    = MRKUT002'                     MRKUT002
046300           DISPLAY '** PARAGRAPH  = R000-READ-SYSTEM-RECORD'      MRKUT002
046400           DISPLAY '** MESSAGE    = '                             MRKUT002
046500                   'RECORD NOT FOUND,STATUS: ' PV-VSAM-STATUS     MRKUT002
046600           MOVE +4005           TO PV-ABEND-CODE                  MRKUT002
046700           CALL 'ILBOABN0'      USING PV-ABEND-CODE               MRKUT002
046800        ELSE                                                      MRKUT002
046900           DISPLAY '** ABEND **'                                  MRKUT002
047000           DISPLAY '** PROGRAM    = MRKUT002'                     MRKUT002
047100           DISPLAY '** PARAGRAPH  = R000-READ-SYSTEM-RECORD'      MRKUT002
047200           DISPLAY '** MESSAGE    = '                             MRKUT002
047300                   'BAD VSAM READ, STATUS = ' PV-VSAM-STATUS      MRKUT002
047400           MOVE +4005           TO PV-ABEND-CODE                  MRKUT002
047500           CALL 'ILBOABN0'      USING PV-ABEND-CODE               MRKUT002
047600        END-IF                                                    MRKUT002
047700     END-IF.                                                      MRKUT002
047800*                                                                 MRKUT002
047900     ADD PC-PLUS-1 TO PV-SYSTEM-INPUT-CNT.                        MRKUT002
048000*                                                                 MRKUT002
048100 EJECT                                                            MRKUT002
048200******************************************************************MRKUT002
048300* THIS SECTION READS THE PARAMETER RECORD                        *MRKUT002
048400******************************************************************MRKUT002
048500 R100-READ-DATE-RECORD.                                           MRKUT002
048600*                                                                 MRKUT002
048700     READ DATE-FILE       INTO  DATE-RECORD                       MRKUT002
048800          AT END                                                  MRKUT002
048900             MOVE PC-Y      TO  PS-END-OF-DATE-REC.               MRKUT002
049000*                                                                 MRKUT002
049100 EJECT                                                            MRKUT002
049200******************************************************************MRKUT002
049300*     WRITES THE REVISED SYSTEM RECORD.                          *MRKUT002
049400******************************************************************MRKUT002
049500 W000-WRITE-SYSTEM-RECORD.                                        MRKUT002
049600*                                                                 MRKUT002
049700     MOVE 'W000'                  TO PGM-LOCATION.                MRKUT002
049800     MOVE 'W000-WRITE-SYSTEM-RECORD   '                           MRKUT002
049900                                  TO PV-ABEND-PARAGRAPH.          MRKUT002
050000*                                                                 MRKUT002
050100     REWRITE ICCTL-CNTL-REC       FROM SYSTEM-RECORD.             MRKUT002
050200*                                                                 MRKUT002
050300     IF (PV-VSAM-SUCCESSFUL                                       MRKUT002
050400     OR  PV-VSAM-NOT-FOUND)                                       MRKUT002
050500        CONTINUE                                                  MRKUT002
050600     ELSE                                                         MRKUT002
050700        DISPLAY '** ABEND **'                                     MRKUT002
050800        DISPLAY '** PROGRAM    = MRKUT002'                        MRKUT002
050900        DISPLAY '** PARAGRAPH  = W000-WRITE-SYSTEM-RECORD'        MRKUT002
051000        DISPLAY '** MESSAGE    = '                                MRKUT002
051100                'BAD VSAM WRITE,STATUS = ' PV-VSAM-STATUS         MRKUT002
051200        MOVE +4005              TO PV-ABEND-CODE                  MRKUT002
051300        CALL 'ILBOABN0'         USING PV-ABEND-CODE               MRKUT002
051400     END-IF.                                                      MRKUT002
051500*                                                                 MRKUT002
051600     ADD PC-PLUS-1 TO PV-SYSTEM-OUTPUT-CNT.                       MRKUT002
051700*                                                                 MRKUT002
051800 EJECT                                                            MRKUT002
051900******************************************************************MRKUT002
052000*   CLOSES FILES AND DISPLAYS MESSAGES.                          *MRKUT002
052100******************************************************************MRKUT002
052200 Y100-END-ROUTINE.                                                MRKUT002
052300*                                                                 MRKUT002
052400     CLOSE  ICCTL-CNTL-FILE.                                      MRKUT002
052500*                                                                 MRKUT002
052600 EJECT                                                            MRKUT002
052700******************************************************************MRKUT002
