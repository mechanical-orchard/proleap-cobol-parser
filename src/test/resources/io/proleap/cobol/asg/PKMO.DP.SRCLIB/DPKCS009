      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    DPKCS009.                                                 
       AUTHOR.        MIKE FURLICK.                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03-19-92.                                                 
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *    Z009 - TRANSACTION LIST PROGRAM.                           *         
      *                                                               *         
      *    THIS PROGRAM GENERATES A LIST OF TRANSACTIONS WITH THEIR   *         
      *    ASSOCIATED PROGRAM, MNEMONIC, AND TITLES.  THE USER MAY    *         
      *    ADD, UPDATE, OR DELETED FROM SELECTIONS ON THIS LIST.  THE *         
      *    APPROPRIATE PROGRAM IS INVOKED WHEN THE OPERATOR SELECTS   *         
      *    ONE OR MORE ITEMS AND PRESSES A FUNCTION KEY.  NO SELECTION*         
      *    IS NECESSARY TO ADD A NEW TRANSACTION.                     *         
      *                                                               *         
      *    02/19/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.      *         
      *                       1) MODIFIED THE MAP (DPKM009) TO INCLUDE*         
      *                          A NEW FIELD TITLED 'UPD VSAM',       *         
      *                          IMMEDIATELY FOLLOWING THE            *         
      *                          'PANEL TITLE' COLUMN.                *         
      *                       2) DISPLAY THE NEW FIELD ADDED TO THE   *         
      *                          MAP FROM THE TMNEMONIC-UPD-VSAM-IND. *         
      *                       3) ACCEPT UPDATE VSAM INDICATOR FROM THE*         
      *                          CRITERIA SPEC AND QUERY USING THE NEW*         
      *                          INDICATOR, IF ENTERED ON THE CRITERIA*         
      *                          SPEC.                                *         
      *                       4) ACCEPT THE PROGRAM NAME FROM THE     *         
      *                          CRITERIA SPEC AND QUERY USING THE    *         
      *                          PROGRAM NAME IF ENTERED.             *         
      *                                                               *         
      *    10/28/96  TKMA235  ADD LOGIC FOR PURGE/ARCHIVE/RETRIEVAL.  *         
      *              SUPPORT "DATA RETRIEVAL" SCREENS (WHICH ARE      *         
      *              EXISTING INQUIRY SCREENS, ACCESSING              *         
      *              ARCHIVE/HISTORY VERSIONS OF APPLICABLE DB2 TBLS).*         
      *              THE "PACKAGE CODE" INDICATES WHETHER TO ACCESS   *         
      *              THE REGULAR PRODUCTION VERSION OF THE DB2 TABLES *         
      *              OR THE ARCHIVE/HISTORY VERSION OF THE DB2 TABLES.*         
      *              1) MODIFIED THE MAP (DPKM009) TO INCLUDE A NEW   *         
      *                 FIELD TITLED 'PKG', BETWEEN THE 'TRAN' AND    *         
      *                 'PANEL TITLE' COLUMNS.                        *         
      *              2) DISPLAY THE NEW FIELD ADDED TO THE            *         
      *                 MAP FROM THE TMNEMONIC-DB2-PKG-CDE.           *         
      *              3) ACCEPT DB2 PKG IND FROM THE CRITERIA SPEC,    *         
      *                 AND QUERY USING THE NEW INDICATOR,            *         
      *                 IF ENTERED ON THE CRITERIA SPEC.              *         
      *              MADE BY: CHERI MASTEL               WR #: 3965   *         
      *    09/09/99  TKMA464  ADDED ACCURATE LOCATION INDICATOR       *         
      *              MADE BY: JEFF ROUBIK                WR #:10808   *         
      *                                                               *         
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023      *         
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE  *         
262023*  MAKING THE CALL DYNAMIC VS STATIC                            *         
      *---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-NAVIGATOR-TRAN-ID           PIC X(04)    VALUE 'Z008'.        
           05  PC-MENU-TRAN-ID                PIC X(04)    VALUE 'Z007'.        
           05  PC-MAXIMUM-COMMAREA-LENGTH     PIC S9(04)   VALUE +4072          
                                                           COMP SYNC.           
           05  PC-CURRENT-MAP-NAME            PIC X(08)    VALUE                
           'DP009A  '.                                                          
           05  PC-CURRENT-MAPSET-NAME         PIC X(08)    VALUE                
           'DPKM009 '.                                                          
           05  PC-CURRENT-PROGRAM-NAME        PIC X(08)    VALUE                
           'DPKCS009'.                                                          
           05  PC-BEGINNING                   PIC X(01)    VALUE 'B'.           
           05  PC-ENDING                      PIC X(01)    VALUE 'E'.           
           05  PC-SELECTED                    PIC X(01)    VALUE 'S'.           
           05  PC-ITEMS-PER-PANEL             PIC S9(04)   VALUE +14            
                                                           COMP SYNC.           
           05  PC-ITEMS-PER-BLOCK             PIC S9(04)   VALUE +21            
                                                           COMP SYNC.           
           05  PC-MAX-ITEMS-TO-BE-READ        PIC S9(04)   VALUE +100           
                                                           COMP SYNC.           
           05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-MAX-WR-ITEMS                PIC S9(03)   VALUE +100           
                                                           COMP-3.              
           05  PC-MAX-ITEMS                   PIC S9(09)   VALUE                
                                                           +1000000             
                                                           COMP-3.              
           05  PC-BLOCK-LENGTH                PIC S9(04)   VALUE +1261          
                                                           COMP SYNC.           
           05  PC-FIELD-LENGTH                PIC S9(04)   VALUE +5             
                                                           COMP SYNC.           
           05  PC-ERASE-EOF-COMP              PIC S9(04)   VALUE +128           
                                                           COMP SYNC.           
           05  FILLER REDEFINES PC-ERASE-EOF-COMP.                              
               10  FILLER                     PIC X(01).                        
               10  PC-ERASE-EOF               PIC X(01).                        
           05  PC-MAX-RETRIES                 PIC S9(3)    VALUE 3              
                                                           COMP-3.              
           05  PC-L                           PIC X(1)     VALUE 'L'.           
           05  PC-M                           PIC X(1)     VALUE 'M'.           
           05  PC-D                           PIC X(1)     VALUE 'D'.           
           05  PC-Z                           PIC X(1)     VALUE 'Z'.           
           05  PC-COMPANY                     PIC X(2)     VALUE '03'.          
           05  PC-INQ                         PIC X(3)     VALUE 'INQ'.         
           05  PC-UPD                         PIC X(3)     VALUE 'UPD'.         
           05  PC-DEL                         PIC X(3)     VALUE 'DEL'.         
           05  PC-ERR                         PIC X(3)     VALUE 'ERR'.         
           05  PC-P                           PIC X(1)     VALUE 'P'.           
           05  PC-A                           PIC X(1)     VALUE 'A'.           
           05  PC-ASTERISK                    PIC X(1)     VALUE '*'.           
           05  PC-SPACE                       PIC X(1)     VALUE SPACE.         
           05  PC-Y                           PIC X(1)     VALUE 'Y'.           
           05  PC-N                           PIC X(1)     VALUE 'N'.           
      *                                                                         
       01  PC-TSYMSG-NUMBERS.                                                   
           05  PC-TSYMSG-00005                PIC 9(5)    VALUE 00005.          
           05  PC-TSYMSG-00008                PIC 9(5)    VALUE 00008.          
           05  PC-TSYMSG-00045                PIC 9(5)    VALUE 00045.          
           05  PC-TSYMSG-00050                PIC 9(5)    VALUE 00050.          
           05  PC-TSYMSG-00051                PIC 9(5)    VALUE 00051.          
           05  PC-TSYMSG-00052                PIC 9(5)    VALUE 00052.          
           05  PC-TSYMSG-00053                PIC 9(5)    VALUE 00053.          
           05  PC-TSYMSG-00054                PIC 9(5)    VALUE 00054.          
           05  PC-TSYMSG-00055                PIC 9(5)    VALUE 00055.          
           05  PC-TSYMSG-00057                PIC 9(5)    VALUE 00057.          
           05  PC-TSYMSG-00069                PIC 9(5)    VALUE 00069.          
           05  PC-TSYMSG-00070                PIC 9(5)    VALUE 00070.          
           05  PC-TSYMSG-00101                PIC 9(5)    VALUE 00101.          
           05  PC-TSYMSG-00146                PIC 9(5)    VALUE 00146.          
           05  PC-TSYMSG-00207                PIC 9(5)    VALUE 00207.          
           05  PC-TSYMSG-99901                PIC 9(5)    VALUE 99901.          
      *                                                                         
       01  PM-PROGRAM-MESSAGES.                                                 
           05  PM-PROGRAM-MESSAGE-9           PIC X(78)    VALUE                
           'ERROR TRYING TO REWRITE  TEMP STORAGE QUEUE - RESP '.               
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-9.                           
               10  FILLER                     PIC X(51).                        
               10  PM-RESP-9                  PIC Z(03)9-.                      
               10  FILLER                     PIC X(22).                        
           05  PM-PROGRAM-MESSAGE-10          PIC X(78)    VALUE                
           'ERROR TRYING TO WRITE TO TEMP STORAGE QUEUE - RESP '.               
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-10.                          
               10  FILLER                     PIC X(51).                        
               10  PM-RESP-10                 PIC Z(03)9-.                      
               10  FILLER                     PIC X(22).                        
           05  PM-PROGRAM-MESSAGE-11          PIC X(78)    VALUE                
           'LOGIC ERROR IN TRYING TO WRITE TO TEMP STORAGE QUEUE - BLOCK        
      -    '     '.                                                             
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-11.                          
               10  FILLER                     PIC X(61).                        
               10  PM-BLOCK-NUMBER            PIC Z(03)9-.                      
               10  FILLER                     PIC X(12).                        
           05  PM-PROGRAM-MESSAGE-12          PIC X(78)    VALUE                
           'INVALID NEXT APPLICATION ACTIVITY RECEIVED'.                        
           05  PM-PROGRAM-MESSAGE-23          PIC X(78)    VALUE                
           'ERROR TRYING TO DELETE TEMP STORAGE QUEUE - RESP '.                 
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-23.                          
               10  FILLER                     PIC X(49).                        
               10  PM-RESP-23                 PIC Z(03)9-.                      
               10  FILLER                     PIC X(24).                        
           05  PM-PROGRAM-MESSAGE-24          PIC X(78)    VALUE                
           'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE - RESP '.              
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-24.                          
               10  FILLER                     PIC X(52).                        
               10  PM-RESP-24                 PIC Z(03)9-.                      
               10  FILLER                     PIC X(21).                        
           05  PM-PROGRAM-MESSAGE-28          PIC X(78)    VALUE                
           'ERROR OCCURRED DURING TS QUEUE PROCESSING'.                         
      *                                                                         
       01  PS-PROGRAM-SUBSCRIPTS.                                               
           05  PS-SUB                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PS-ITEM-SUB                    PIC S9(09)   VALUE +0             
                                                           COMP-3.              
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MAP-SEND-TYPE               PIC X(01)    VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET                VALUE '1'.           
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN          VALUE '2'.           
               88  PS-SEND-DATA-ONLY-FRSET                 VALUE '3'.           
               88  PS-SEND-DATA-ONLY                       VALUE '4'.           
               88  PS-SEND-DATA-ONLY-ERASEAUP              VALUE '5'.           
               88  PS-SEND-MAP-ONLY-FRSET                  VALUE '6'.           
           05  PS-ERROR-SW                    PIC X(01)    VALUE 'N'.           
               88  PS-ERROR                                VALUE 'Y'.           
               88  PS-NO-ERROR                             VALUE 'N'.           
           05  PS-INTER-APPL-VALID-SW         PIC X(01)    VALUE 'N'.           
               88  PS-INTER-APPL-VALID                     VALUE 'Y'.           
               88  PS-INTER-APPL-NOT-VALID                 VALUE 'N'.           
           05  PS-SAME-ITEMS-DISPLAYED-SW     PIC X(01)    VALUE 'N'.           
               88  PS-SAME-ITEMS-DISPLAYED                 VALUE 'Y'.           
               88  PS-DIFFERENT-ITEMS-DISPLAYED            VALUE 'N'.           
           05  PS-CURSOR-POSITIONED-SW        PIC X(01)    VALUE 'N'.           
               88  PS-CURSOR-POSITIONED                    VALUE 'Y'.           
               88  PS-CURSOR-NOT-POSITIONED                VALUE 'N'.           
           05  PS-VALID-SELECTIONS-SW         PIC X(01)    VALUE 'Y'.           
               88  PS-VALID-SELECTIONS                     VALUE 'Y'.           
               88  PS-INVALID-SELECTIONS                   VALUE 'N'.           
           05  PS-VALID-BEG-ITEM-SW           PIC X(01)    VALUE 'N'.           
               88  PS-VALID-BEG-ITEM                       VALUE 'Y'.           
               88  PS-INVALID-BEG-ITEM                     VALUE 'N'.           
           05  PS-TOP-ITEM-IN-ARRAY-SW        PIC X(01)    VALUE 'N'.           
               88  PS-TOP-ITEM-IN-ARRAY                    VALUE 'Y'.           
               88  PS-TOP-ITEM-NOT-IN-ARRAY                VALUE 'N'.           
           05  PS-BOTTOM-ITEM-IN-ARRAY-SW     PIC X(01)    VALUE 'N'.           
               88  PS-BOTTOM-ITEM-IN-ARRAY                 VALUE 'Y'.           
               88  PS-BOTTOM-ITEM-NOT-IN-ARRAY             VALUE 'N'.           
           05  PS-TOP-ITEM-IN-TEMP-SW         PIC X(01)    VALUE 'N'.           
               88  PS-TOP-ITEM-IN-TEMP                     VALUE 'Y'.           
               88  PS-TOP-ITEM-NOT-IN-TEMP                 VALUE 'N'.           
           05  PS-BOTTOM-ITEM-IN-TEMP-SW      PIC X(01)    VALUE 'N'.           
               88  PS-BOTTOM-ITEM-IN-TEMP                  VALUE 'Y'.           
               88  PS-BOTTOM-ITEM-NOT-IN-TEMP              VALUE 'N'.           
           05  PS-LAST-BLOCK-IN-ARRAY-SW      PIC X(01)    VALUE 'N'.           
               88  PS-LAST-BLOCK-IN-ARRAY                  VALUE 'Y'.           
               88  PS-LAST-BLOCK-NOT-IN-ARRAY              VALUE 'N'.           
           05  PS-SELECTION-FOUND-SW          PIC X(01)    VALUE 'N'.           
               88  PS-SELECTION-FOUND                      VALUE 'Y'.           
               88  PS-NO-SELECTION-FOUND                   VALUE 'N'.           
           05  PS-WARNING-SW                  PIC X(01)    VALUE 'N'.           
               88  PS-WARNING                              VALUE 'Y'.           
               88  PS-NO-WARNING                           VALUE 'N'.           
           05  PS-INFORMATIONAL-SW            PIC X(01)    VALUE 'N'.           
               88  PS-INFORMATIONAL                        VALUE 'Y'.           
               88  PS-NOT-INFORMATIONAL                    VALUE 'N'.           
           05  PS-PROCESSING-SW               PIC X(01)    VALUE 'N'.           
               88  PS-CONTINUE                             VALUE 'Y'.           
               88  PS-DONT-PROCESS                         VALUE 'N'.           
           05  PS-PROCESS-ITEM-SW             PIC X(01)    VALUE 'N'.           
               88  PS-END-OF-ITEMS                         VALUE 'Y'.           
               88  PS-NOT-END                              VALUE 'N'.           
           05  PS-HISTORY-SELECT-SW           PIC X(01)    VALUE 'N'.           
               88  PS-HISTORY-SELECTED                     VALUE 'Y'.           
               88  PS-DIDNT-SELECT-HISTORY                 VALUE 'N'.           
           05  PS-COMMAREA-SW                 PIC X(01)    VALUE 'N'.           
               88  PS-COMMAREA-VALID                       VALUE 'Y'.           
               88  PS-COMMAREA-NOT-VALID                   VALUE 'N'.           
           05  PS-LIST-ITEMS-SW               PIC X(01)    VALUE 'N'.           
               88  PS-LIST-ITEMS-FOUND                     VALUE 'Y'.           
               88  PS-NO-LIST-ITEMS-FOUND                  VALUE 'N'.           
           05  PS-USE-ITEM-SE                 PIC X(01)    VALUE 'N'.           
               88  PS-USE-ITEM                             VALUE 'Y'.           
               88  PS-DONT-USE-ITEM                        VALUE 'N'.           
           EJECT                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE               PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-HIGHEST-ITEM-TO-BE-READ     PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-ENDING-ITEM-ON-PANEL        PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-ITEM-IN-USE                 PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-TOP-ITEM                    PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-BOTTOM-ITEM                 PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-MAX-ITEMS-TO-BE-READ        PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-FIRST-ITEM-IN-ARRAY         PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-LAST-ITEM-IN-ARRAY          PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-LAST-ITEM-IN-TEMP           PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-BEGINNING-ITEM              PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)   VALUE +0             
                                                           COMP-3.              
           05  PV-BLOCK-NUMBER                PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-RESP                        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-ITEM                        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-TSQ-B-ITEM                  PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-SAVE-ITEM                   PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-LIST-ITEM-NUMBER            PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-START-OF-SELECTED-RANGE     PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-END-OF-SELECTED-RANGE       PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-ITEMS-READ                  PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
           05  PV-MESSAGE                     PIC X(78)    VALUE SPACES.        
           05  PV-SQL-SUB                     PIC S9(4)    COMP.                
           05  PV-RETRY-COUNTER               PIC S9(3)    COMP-3.              
           05  PV-BEGITMI.                                                      
               10  PV-BEGITMI-N               PIC 9(7).                         
           05  PV-DISPLAY7                    PIC ZZZ,ZZ9.                      
           05  PV-BG-QUEUE-NAME.                                                
               10  PV-BG-TERM-ID              PIC X(4).                         
               10  PV-BG-TASK-NUMBER          PIC S9(5)    COMP-3.              
               10  PV-BG-SEQ-NUMBER           PIC  9(1).                        
                                                                                
       01  PV-DB2-SELECTION-CRITERIA.                                           
           05  PV-MNEMONIC-CODE-LOW           PIC X(8).                         
           05  PV-MNEMONIC-CODE-HIGH          PIC X(8).                         
           05  PV-PGM-NAME-LOW                PIC X(8).                         
           05  PV-PGM-NAME-HIGH               PIC X(8).                         
           05  PV-TRAN-LOW                    PIC X(4).                         
           05  PV-TRAN-HIGH                   PIC X(4).                         
           05  PV-UPD-VSAM-IND-LOW            PIC X(1).                         
           05  PV-UPD-VSAM-IND-HIGH           PIC X(1).                         
           05  PV-DB2-PKG-CDE-1               PIC X(1).                         
           05  PV-DB2-PKG-CDE-2               PIC X(1).                         
           05  PV-LIST-CRITERION              PIC X(1).                         
           05  PV-MENU-CRITERION              PIC X(1).                         
           05  PV-DETAIL-CRITERION            PIC X(1).                         
           05  PV-ACCURATE-LOC-REQ-1          PIC X(1).                         
           05  PV-ACCURATE-LOC-REQ-2          PIC X(1).                         
                                                                                
       01  PV-DB2-COMPARE-DATE                PIC X(10)    VALUE                
                                                           '1900-01-01'.        
       01  PV-DB2-WORK-DATE.                                                    
           05  PV-DB2-WD-CCYY                 PIC X(4).                         
           05  FILLER                         PIC X        VALUE '-'.           
           05  PV-DB2-WD-MM                   PIC XX.                           
           05  FILLER                         PIC X        VALUE '-'.           
           05  PV-DB2-WD-DD                   PIC XX.                           
                                                                                
       01  PV-TEMP-WORK-AREA.                                                   
           05  PV-BLOCK-MODIFIED              PIC X.                            
           05  PV-TW-ITEM-ENTRIES OCCURS 21 TIMES                               
                                  INDEXED BY PV-TW-IDX.                         
               10  PV-TW-LIST-ITEM-NUMBER     PIC S9(5)    COMP-3.              
               10  PV-TW-SELECTED-SW          PIC X.                            
                   88  PV-TW-SELECT                        VALUE 'S'.           
                   88  PV-TW-DESELECT                      VALUE ' '.           
               10  PV-TW-ACTION               PIC X(3).                         
               10  PV-TW-MNEMONIC             PIC X(8).                         
               10  PV-TW-PROGRAM              PIC X(8).                         
               10  PV-TW-TRAN-ID              PIC X(4).                         
               10  PV-TW-DB2-PKG-CDE          PIC X(1).                         
               10  PV-TW-TITLE                PIC X(30).                        
               10  PV-TW-UPD-VSAM-IND         PIC X(1).                         
               10  PV-TW-ACRT-LOC-REQR-IND    PIC X(1).                         
                                                                                
      *---------------------------------------------------------------*         
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005   *         
      *---------------------------------------------------------------*         
           COPY DPWS006.                                                        
                                                                                
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    STANDARD COMMAREA.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *---------------------------------------------------------------*         
      *    INTER-APPLICATION COMMAREA.                                *         
      *---------------------------------------------------------------*         
                                                                                
               10  INTER-APPL-COMM-AREA       PIC X(200).                       
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      *---------------------------------------------------------------*         
      *    SPECIFIC COMMMAREA FOR DPKCS009.                           *         
      *---------------------------------------------------------------*         
               10  DP009-SPECIFIC-COMMAREA.                                     
                   15  DP009-APPL-CURSOR-POSITION          PIC S9(04)           
                                                           COMP.                
                                                                                
                   15  DP009-BLOCK-ENTRIES OCCURS 2 TIMES.                      
                       20  DP009-BLOCK-MODIFIED-SW         PIC X(01).           
                           88  DP009-BLOCK-NOT-MODIFIED    VALUE 'N'.           
                           88  DP009-BLOCK-MODIFIED        VALUE 'Y'.           
                                                                                
                       20  DP009-ITEM-ARRAY.                                    
                           25  DP009-ITEM-ENTRIES OCCURS 21 TIMES.              
                               30  DP009-LIST-ITEM-NUMBER                       
                                                           PIC S9(05)           
                                                               COMP-3.          
                               30  DP009-SELECTED-SW       PIC X(01).           
                                   88  DP009-NO-SELECT     VALUE ' '.           
                                   88  DP009-SELECT        VALUE 'S'.           
                                   88  DP009-BEGIN         VALUE 'B'.           
                                   88  DP009-END           VALUE 'E'.           
                               30  DP009-ACTION            PIC X(3).            
                               30  DP009-MNEMONIC          PIC X(8).            
                               30  DP009-PROGRAM           PIC X(8).            
                               30  DP009-TRAN              PIC X(4).            
                               30  DP009-DB2-PKG-CDE       PIC X(1).            
                               30  DP009-TITLE             PIC X(30).           
                               30  DP009-UPD-VSAM-IND      PIC X(1).            
                               30  DP009-ACRT-LOC-REQR-IND PIC X(1).            
                                                                                
                   15  DP009-BLK-SUB                       PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  DP009-ITEM-SUB                      PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  DP009-NUMBER-OF-ITEMS-READ          PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-NUMBER-OF-SELECTED-ITEMS      PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-ITEM-AT-TOP-OF-PANEL          PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-ITEM-AT-BOT-OF-PANEL          PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-HIGHEST-BLOCK-WRITTEN         PIC S9(04)           
                                                               COMP             
                                                               SYNC.            
                   15  DP009-START-OF-SELECTED-RANGE       PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-END-OF-SELECTED-RANGE         PIC S9(05)           
                                                               COMP-3.          
                   15  DP009-ITEMS-LEFT-INDICATOR          PIC X.               
                       88  DP009-ITEMS-LEFT-ON-DB            VALUE 'Y'.         
                       88  DP009-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.         
                   15  DP009-TEMP-QUEUE-UPDATE-SW          PIC X.               
                       88  DP009-UPDATE-TSQ                  VALUE 'Y'.         
                       88  DP009-NO-TSQ-UPDATE               VALUE 'N'.         
                   15  DP009-TEMP-QUEUE-NAME.                                   
                       20  DP009-TERM-ID                   PIC X(4).            
                       20  DP009-TASK-NUMBER               PIC S9(7)            
                                                               COMP-3.          
                   15  DP009-TSQB-QUEUE-NAME               PIC X(8).            
                   15  DP009-LAST-MNEMONIC                 PIC X(8).            
                   15  DP009-HOLD-MNEMONIC                 PIC X(8).            
                   15  DP009-SAVE-MNEMONIC                 PIC X(8).            
                   15  DP009-SAVE-PROGRAM                  PIC X(8).            
                   15  DP009-SAVE-TRAN                     PIC X(4).            
                   15  DP009-SAVE-UPD-VSAM-IND             PIC X(1).            
                   15  DP009-SAVE-DB2-PKG-CDE              PIC X(1).            
                   15  DP009-SAVE-ACRT-LOC-REQR-IND        PIC X(1).            
                   15  DP009-INT-APPL-FORMAT-IND           PIC X.               
                                                                                
      *            15  DP009-DB2-CURRENT-DATE.                                  
      *                20  DP009-DC-CC                     PIC XX.              
      *                20  DP009-DC-YY                     PIC XX.              
      *                20  FILLER                          PIC X.               
      *                    88  DP009-DC-DASH1              VALUE '-'.           
      *                20  DP009-DC-MM                     PIC XX.              
      *                20  FILLER                          PIC X.               
      *                    88  DP009-DC-DASH2              VALUE '-'.           
      *                20  DP009-DC-DD                     PIC XX.              
                                                                                
                   15  DP009-SAVE-INTER-APPL-COMMAREA      PIC X(200).          
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    MNEMONIC SELECTION TSQ B QUEUE RECORD LAYOUT.              *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS002.                                                        
                                                                                
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ABEND PROCESSING COPYBOOK.                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS013.                                                        
                                                                                
           EJECT                                                                
      *---------------------------------------------------------------*         
      *    ATTRIBUTE SETTINGS COPYBOOK.                               *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    CURRENT DATE AND TIME COPYBOOK.                            *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS017.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    TOGGLE FUNCTION KEY LINES COPYBOOK.                        *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS009.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    FUNCTION KEYS COPYBOOK.                                    *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    DPKUT045 COPYBOOK.                                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS045.                                                        
                                                                                
      *---------------------------------------------------------------*         
      *    NUMERIC EDIT LAYOUT.                                       *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPWS010I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    MESSAGE RECORD LAYOUT                                      *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPRD507I.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD ATTENTION IDENTIFIER LIST                         *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DFHAID.                                                         
                                                                                
      *---------------------------------------------------------------*         
      *    STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST      *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DFHBMSCA.                                                       
                                                                                
      *---------------------------------------------------------------*         
      *    MAP LAYOUT                                                 *         
      *---------------------------------------------------------------*         
                                                                                
           COPY DPKM009.                                                        
       01  MD-MAP-DATA-AREA  REDEFINES DP009AO PIC X(2000).                     
      *                                                                         
       01  DPKM009-OCCURS-LAYOUT  REDEFINES  MD-MAP-DATA-AREA.                  
           05  FILLER                PIC X(146).                                
           05  DPKM009-SCROLL-AREA OCCURS 14 TIMES.                             
               10  DPKM009-SELECTL   PIC S9(4) COMP.                            
               10  DPKM009-SELECTA   PIC X.                                     
               10  DPKM009-SELECTC   PIC X.                                     
               10  DPKM009-SELECTH   PIC X.                                     
               10  DPKM009-SELECTI   PIC X.                                     
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-ACTION    PIC X(3).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-MNEMONIC  PIC X(8).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-PROGRAM   PIC X(8).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-TRAN      PIC X(4).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-DB2-PKG-CDE                                          
                                     PIC X(1).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-TITLE     PIC X(30).                                 
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-UPD-VSAM-IND                                         
                                     PIC X(1).                                  
                                                                                
               10  FILLER            PIC X(5).                                  
               10  DPKM009-ACRT-LOC-REQR-IND                                    
                                     PIC X(1).                                  
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR TMNEMONIC                                     *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE TMNMNC                                                  
           END-EXEC.                                                            
      *---------------------------------------------------------------*         
      *    DB2 AREA FOR COMMUNICATIONS                                *         
      *---------------------------------------------------------------*         
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------*         
      *    TMNEMONIC CURSOR.                                          *         
      *---------------------------------------------------------------*         
                                                                                
           EXEC SQL                                                             
                DECLARE MNEMONIC-CURSOR CURSOR                                  
                FOR SELECT                                                      
                      MNEMONIC_CODE                                             
                    , TRANS_ID_CODE                                             
                    , DB2_PKG_CDE                                               
                    , PANEL_TITLE_NAME                                          
                    , PGM_NAME                                                  
                    , UPD_VSAM_IND                                              
                    , ACRT_LOC_REQR_IND                                         
                FROM                                                            
                      TMNEMONIC                                                 
                WHERE                                                           
                     (MNEMONIC_CODE  BETWEEN                                    
                                          :PV-MNEMONIC-CODE-LOW                 
                                     AND  :PV-MNEMONIC-CODE-HIGH)               
                 AND (PGM_NAME       BETWEEN                                    
                                          :PV-PGM-NAME-LOW                      
                                     AND  :PV-PGM-NAME-HIGH)                    
                 AND (TRANS_ID_CODE  BETWEEN                                    
                                          :PV-TRAN-LOW                          
                                     AND  :PV-TRAN-HIGH)                        
                 AND (UPD_VSAM_IND   BETWEEN                                    
                                          :PV-UPD-VSAM-IND-LOW                  
                                     AND  :PV-UPD-VSAM-IND-HIGH)                
                 AND (DB2_PKG_CDE    =    :PV-DB2-PKG-CDE-1  OR                 
                      DB2_PKG_CDE    =    :PV-DB2-PKG-CDE-2)                    
                                                                                
                 AND (SCRN_TYP_CDE   LIKE :PV-LIST-CRITERION                    
                  OR  SCRN_TYP_CDE   LIKE :PV-MENU-CRITERION                    
                  OR  SCRN_TYP_CDE   LIKE :PV-DETAIL-CRITERION)                 
                                                                                
                 AND (ACRT_LOC_REQR_IND = :PV-ACCURATE-LOC-REQ-1 OR             
                      ACRT_LOC_REQR_IND = :PV-ACCURATE-LOC-REQ-2)               
                ORDER BY                                                        
                      MNEMONIC_CODE                                             
           END-EXEC.                                                            
                                                                                
                                                                                
       EJECT                                                                    
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
                                                                                
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THIS TRANSACTION IF THE COMMAREA IS VALID.          *        
      *----------------------------------------------------------------*        
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
           IF DP020-SRC-RETURN-W-TRAN-ID                                        
               PERFORM B100-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *                                                                *        
      *    PERFORMED BY A100-MAIN-MODULE.                              *        
      *----------------------------------------------------------------*        
                                                                                
       B100-CONTROL-PROCESSING.                                                 
                                                                                
           SET PS-CONTINUE           TO  TRUE.                                  
           MOVE LENGTH OF DP009-BLOCK-ENTRIES (1)                               
                                     TO  PC-BLOCK-LENGTH.                       
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   PERFORM C100-VALIDATE-COMMAREA                               
                   IF  PS-COMMAREA-NOT-VALID                                    
                       MOVE PC-TSYMSG-99901                                     
                                     TO  DP020-MSG-NUMBER                       
                       SET DP020-MSG-FATAL                                      
                                     TO  TRUE                                   
                       MOVE DP016-PF3                                           
                                     TO  DP020-SRC-AID                          
                       PERFORM DP015-1000-START-NAVIGATOR                       
                   ELSE                                                         
                       INITIALIZE DP009-SPECIFIC-COMMAREA                       
                       MOVE INTER-APPL-COMM-AREA                                
                                     TO  DP009-SAVE-INTER-APPL-COMMAREA         
                       MOVE DP020-INT-APPL-FORMAT-IND                           
                                     TO  DP009-INT-APPL-FORMAT-IND              
                                                                                
                       IF  DP020-NAV-KEY-DATA GREATER THAN SPACES               
                           MOVE DP020-NAV-KEY-DATA                              
                                     TO  DP009-SAVE-MNEMONIC                    
                           MOVE '2'  TO  DP009-INT-APPL-FORMAT-IND              
                       ELSE                                                     
                           MOVE DP055-MNEMONIC                                  
                                     TO  DP009-SAVE-MNEMONIC                    
                       END-IF                                                   
                                                                                
                       MOVE DP055-PROG                                          
                                     TO  DP009-SAVE-PROGRAM                     
                       MOVE DP055-TRAN                                          
                                     TO  DP009-SAVE-TRAN                        
                       MOVE DP055-UPD-VSAM-IND                                  
                                     TO  DP009-SAVE-UPD-VSAM-IND                
                                                                                
                       MOVE DP055-DB2-PKG-CDE                                   
                                     TO  DP009-SAVE-DB2-PKG-CDE                 
                       MOVE DP055-ACCURATE-LOCATION-REQ                         
                                     TO  DP009-SAVE-ACRT-LOC-REQR-IND           
                                                                                
                       SET PS-CONTINUE                                          
                                     TO  TRUE                                   
                       SET PS-SEND-MAP-N-DATA-FRSET                             
                                     TO  TRUE                                   
                       SET DP020-NEXT-ACT-READ-MAP                              
                                     TO  TRUE                                   
                       PERFORM D100-SETUP-INIT-PANEL                            
                       IF  PS-NO-LIST-ITEMS-FOUND                               
                           MOVE DP016-PF3                                       
                                     TO  DP020-SRC-AID                          
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       END-IF                                                   
                   END-IF                                                       
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM C300-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                     TO  TRUE                                   
                   PERFORM C400-REFRESH-PANEL-FROM-TEMP                         
                   PERFORM G100-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
                   MOVE DP020-MSG-MESSAGE TO PV-MESSAGE                         
                   IF DP020-NAV-MNEMONIC = SPACES                               
                       MOVE SPACES   TO  ANAVCMDO                               
                       MOVE DP015-UNDERLINE                                     
                                     TO  ANAVCMDH                               
                       MOVE DP015-GREEN                                         
                                     TO  ANAVCMDC                               
                   ELSE                                                         
                       MOVE DP020-NAV-MNEMONIC                                  
                                     TO  ANAVCMDO                               
                       MOVE DP015-REVERSE                                       
                                     TO  ANAVCMDH                               
                       MOVE DP015-RED                                           
                                     TO  ANAVCMDC                               
                   END-IF                                                       
                                                                                
                   PERFORM C400-REFRESH-PANEL-FROM-TEMP                         
                   MOVE -1           TO  ANAVCMDL                               
                   MOVE PV-MESSAGE   TO  DP020-MSG-MESSAGE                      
                                                                                
                   SET DP020-MSG-FATAL                                          
                                     TO  TRUE                                   
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                     TO  TRUE                                   
                   SET PS-CURSOR-POSITIONED                                     
                                     TO  TRUE                                   
                   PERFORM G100-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-NO-ROLLBACK TO TRUE                                
                   MOVE 'B100-CONTROL-PROCESSING'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE PM-PROGRAM-MESSAGE-12                                   
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ENSURE THAT THIS PROGRAM WAS ENTERED ACCORDING TO HOYLE.    *        
      *    THIS HAS NOW BECOME SOMEWHAT OBSOLETE WITH THE USE OF THE   *        
      *    ENHANCEMENTS TO THE ARCHITECTURE.  A NEW COLUMN IS USED TO  *        
      *    DETERMINE WHETHER A PROGRAM HAS GLOBAL ACCESS OR NOT.  IF   *        
      *    NOT, IT CAN ONLY BE ENTERED THROUGH A CORRECT PATH.         *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       C100-VALIDATE-COMMAREA.                                                  
                                                                                
           SET PS-COMMAREA-VALID     TO  TRUE.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RECEIVE THE MAP AND PERFORM THE APPROPRIATE PARAGRAPH BASED *        
      *    ON WHAT FUNCTION KEY WAS SELECTED.                          *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       C300-PROCESS-PANEL.                                                      
                                                                                
           PERFORM DP007-0000-GET-PANEL.                                        
           SET PS-SEND-DATA-ONLY-FRSET                                          
                                     TO TRUE.                                   
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   SET PS-SEND-MAP-N-DATA-FRSET TO TRUE                         
                   SET PS-SAME-ITEMS-DISPLAYED  TO TRUE                         
                   PERFORM E100-EDIT-SELECTIONS                                 
                   PERFORM G100-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-SRC-AID = DP016-PF5                                   
                   MOVE SPACES       TO  ANAVCMDO                               
                                         ANAVKEYO                               
                   PERFORM Z050-DELETE-TEMP-STORAGE                             
                   PERFORM D100-SETUP-INIT-PANEL                                
                                                                                
               WHEN DP020-SRC-AID = DP016-PA2                                   
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP017-0000-GET-CURR-DATE-TIME                        
                   SET PS-SEND-DATA-ONLY                                        
                                     TO  TRUE                                   
                   MOVE -1           TO  ANAVCMDL                               
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN OTHER                                                       
                   PERFORM D200-RECORD-SELECTIONS                               
                   IF PS-VALID-SELECTIONS                                       
                       PERFORM D300-CHECK-FUNCTION-KEY                          
                   ELSE                                                         
                       SET PS-SAME-ITEMS-DISPLAYED TO TRUE                      
                       PERFORM G100-DISPLAY-PANEL                               
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,*        
      *    SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.      *        
      *    EDIT THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL *        
      *    TO THE USER.                                                *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *----------------------------------------------------------------*        
                                                                                
       C400-REFRESH-PANEL-FROM-TEMP.                                            
                                                                                
      *    MOVE DP009-INT-APPL-FORMAT-IND                                       
      *                              TO  DP020-INT-APPL-FORMAT-IND.             
           IF  DP009-NO-TSQ-UPDATE                                              
               MOVE DP009-SAVE-INTER-APPL-COMMAREA                              
                                     TO  INTER-APPL-COMM-AREA                   
           ELSE                                                                 
               IF  DP055-NUMBER-PROCESSED-ITEMS NUMERIC                         
                   MOVE ZERO         TO  PV-SAVE-ITEM                           
                                         PV-TSQ-B-ITEM                          
                   SET PS-NOT-END    TO TRUE                                    
                   PERFORM C500-UPDATE-TSQ                                      
                       UNTIL PS-END-OF-ITEMS                                    
                   PERFORM X900-DELETE-LIST-QUEUE                               
                   MOVE SPACES       TO  DP055-TSQB-QUEUE-NAME                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM D400-REFRESH-BLOCK-ARRAY.                                    
           PERFORM E100-EDIT-SELECTIONS.                                        
                                                                                
           SET PS-DIFFERENT-ITEMS-DISPLAYED TO TRUE.                            
           MOVE DP009-TSQB-QUEUE-NAME                                           
                                     TO  DP055-TSQB-QUEUE-NAME.                 
                                                                                
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------------*        
      *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *        
      *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *        
      *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *        
      *    THE TEMP QUEUE.                                             *        
      *                                                                *        
      *    PERFORMED BY C400                                           *        
      *----------------------------------------------------------------*        
                                                                                
       C500-UPDATE-TSQ.                                                         
                                                                                
           ADD 1                     TO  PV-TSQ-B-ITEM.                         
           PERFORM X600-READ-LIST-QUEUE.                                        
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               SET DP002-IDX         TO  1                                      
               PERFORM C600-PROCESS-ITEMS                                       
                   UNTIL DP002-IDX GREATER THAN DP002-MAX-ITEMS                 
                   OR    DP002-KEY-DATA (DP002-IDX) EQUAL SPACE                 
               PERFORM X550-WRITE-TS-QUEUE                                      
           ELSE                                                                 
               SET PS-END-OF-ITEMS   TO  TRUE.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*        
      *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*        
      *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *        
      *                                                                *        
      *    PERFORMED BY C500                                           *        
      *----------------------------------------------------------------*        
                                                                                
       C600-PROCESS-ITEMS.                                                      
                                                                                
           COMPUTE PV-ITEM = 1 + ((DP002-ITEM (DP002-IDX) - 1)                  
                           / PC-ITEMS-PER-BLOCK).                               
                                                                                
           IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                                   
               IF  PV-SAVE-ITEM NOT EQUAL ZERO                                  
                   PERFORM X550-WRITE-TS-QUEUE                                  
               END-IF                                                           
               MOVE PV-ITEM          TO  PV-SAVE-ITEM                           
                                                                                
               PERFORM X300-READ-TS-QUEUE                                       
               IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                            
                   SET PS-END-OF-ITEMS TO TRUE                                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-NOT-END                                                       
               COMPUTE PS-SUB = (DP002-ITEM (DP002-IDX)                         
                              - PV-TW-LIST-ITEM-NUMBER (1)) + 1                 
               SET PV-TW-DESELECT (PS-SUB)                                      
                                     TO  TRUE                                   
               EVALUATE TRUE                                                    
                   WHEN DP002-INQ (DP002-IDX)                                   
                        MOVE PC-INQ  TO  PV-TW-ACTION (PS-SUB)                  
                                                                                
                   WHEN DP002-UPD (DP002-IDX)                                   
                        MOVE PC-UPD  TO  PV-TW-ACTION (PS-SUB)                  
                                                                                
                   WHEN DP002-DEL (DP002-IDX)                                   
                        MOVE PC-DEL  TO  PV-TW-ACTION (PS-SUB)                  
                                                                                
                   WHEN DP002-ERR (DP002-IDX)                                   
                        MOVE PC-ERR  TO  PV-TW-ACTION (PS-SUB)                  
               END-EVALUATE                                                     
                                                                                
               SUBTRACT 1          FROM  DP055-NUMBER-PROCESSED-ITEMS           
                                         DP009-NUMBER-OF-SELECTED-ITEMS         
                                                                                
               IF  DP055-NUMBER-PROCESSED-ITEMS EQUAL ZERO                      
                   SET PS-END-OF-ITEMS TO TRUE                                  
                   SET DP002-IDX     TO  DP002-MAX-ITEMS                        
               END-IF                                                           
                                                                                
               SET DP002-IDX      UP BY  1                                      
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *        
      *    INITIALIZE ALL COUNTERS AND FLAGS.                          *        
      *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *        
      *    DISPLAY THE PANEL TO USER.                                  *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       D100-SETUP-INIT-PANEL.                                                   
                                                                                
           INITIALIZE                  DP009-BLOCK-ENTRIES (1).                 
           INITIALIZE                  DP009-BLOCK-ENTRIES (2).                 
           MOVE LOW-VALUES         TO  DP009-LAST-MNEMONIC.                     
           MOVE SPACES             TO  DP009-TSQB-QUEUE-NAME.                   
           MOVE +1                 TO  DP009-BLK-SUB.                           
           MOVE PC-ITEMS-PER-PANEL TO  PV-HIGHEST-ITEM-TO-BE-READ.              
           SET DP009-ITEMS-LEFT-ON-DB                                           
                                   TO  TRUE.                                    
                                                                                
           MOVE +0                 TO  DP009-ITEM-SUB                           
                                       DP009-NUMBER-OF-ITEMS-READ               
                                       DP009-NUMBER-OF-SELECTED-ITEMS           
                                       DP009-HIGHEST-BLOCK-WRITTEN              
                                       DP009-START-OF-SELECTED-RANGE            
                                       DP009-END-OF-SELECTED-RANGE.             
                                                                                
      *    PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
      *    MOVE DP017-EDITED-DATE-YYYYMMDD                                      
      *                              TO  DP009-DB2-CURRENT-DATE.                
      *    SET DP009-DC-DASH1        TO  TRUE.                                  
      *    SET DP009-DC-DASH2        TO  TRUE.                                  
                                                                                
           IF  DP009-INT-APPL-FORMAT-IND EQUAL '2'                              
               PERFORM D150-PREPARE-CURSOR                                      
           ELSE                                                                 
               MOVE LOW-VALUES       TO  PV-MNEMONIC-CODE-LOW                   
               MOVE HIGH-VALUES      TO  PV-MNEMONIC-CODE-HIGH                  
           END-IF.                                                              
                                                                                
           PERFORM L100-OPEN-CURSOR.                                            
                                                                                
           PERFORM H100-READ-ITEMS-FROM-DB.                                     
           SET PS-DIFFERENT-ITEMS-DISPLAYED                                     
                                   TO  TRUE.                                    
                                                                                
           MOVE +1                 TO  DP009-ITEM-AT-TOP-OF-PANEL.              
                                                                                
           IF  PS-NO-LIST-ITEMS-FOUND                                           
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM G100-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    SET UP THE HOST VARIABLES.                                  *        
      *                                                                *        
      *    PERFORMED BY D100                                           *        
      *----------------------------------------------------------------*        
                                                                                
       D150-PREPARE-CURSOR.                                                     
                                                                                
           IF  DP009-SAVE-MNEMONIC GREATER THAN SPACES                          
               MOVE DP009-SAVE-MNEMONIC                                         
                                     TO  PV-MNEMONIC-CODE-LOW                   
                                         PV-MNEMONIC-CODE-HIGH                  
               INSPECT PV-MNEMONIC-CODE-LOW                                     
                       REPLACING ALL '*' BY LOW-VALUES                          
               INSPECT PV-MNEMONIC-CODE-HIGH                                    
                       REPLACING ALL '*' BY HIGH-VALUES                         
           ELSE                                                                 
               MOVE LOW-VALUES       TO  PV-MNEMONIC-CODE-LOW                   
               MOVE HIGH-VALUES      TO  PV-MNEMONIC-CODE-HIGH                  
           END-IF.                                                              
                                                                                
           IF  DP009-SAVE-PROGRAM  GREATER THAN SPACES                          
               MOVE DP009-SAVE-PROGRAM                                          
                                     TO  PV-PGM-NAME-LOW                        
                                         PV-PGM-NAME-HIGH                       
               INSPECT PV-PGM-NAME-LOW                                          
                       REPLACING ALL '*' BY LOW-VALUES                          
               INSPECT PV-PGM-NAME-HIGH                                         
                       REPLACING ALL '*' BY HIGH-VALUES                         
           ELSE                                                                 
               MOVE LOW-VALUES       TO  PV-PGM-NAME-LOW                        
               MOVE HIGH-VALUES      TO  PV-PGM-NAME-HIGH                       
           END-IF.                                                              
                                                                                
           IF  DP009-SAVE-TRAN     GREATER THAN SPACES                          
               MOVE DP009-SAVE-TRAN  TO  PV-TRAN-LOW                            
                                         PV-TRAN-HIGH                           
               INSPECT PV-TRAN-LOW                                              
                       REPLACING ALL '*' BY LOW-VALUES                          
               INSPECT PV-TRAN-HIGH                                             
                       REPLACING ALL '*' BY HIGH-VALUES                         
           ELSE                                                                 
               MOVE LOW-VALUES       TO  PV-TRAN-LOW                            
               MOVE HIGH-VALUES      TO  PV-TRAN-HIGH                           
           END-IF.                                                              
                                                                                
           IF  DP009-SAVE-UPD-VSAM-IND                                          
                                   GREATER THAN SPACES                          
               MOVE DP009-SAVE-UPD-VSAM-IND                                     
                                     TO  PV-UPD-VSAM-IND-LOW                    
                                         PV-UPD-VSAM-IND-HIGH                   
               INSPECT PV-UPD-VSAM-IND-LOW                                      
                       REPLACING ALL '*' BY LOW-VALUES                          
               INSPECT PV-UPD-VSAM-IND-HIGH                                     
                       REPLACING ALL '*' BY HIGH-VALUES                         
           ELSE                                                                 
               MOVE LOW-VALUES       TO  PV-UPD-VSAM-IND-LOW                    
               MOVE HIGH-VALUES      TO  PV-UPD-VSAM-IND-HIGH                   
           END-IF.                                                              
                                                                                
           IF  DP009-SAVE-DB2-PKG-CDE    GREATER THAN SPACES                    
               IF DP009-SAVE-DB2-PKG-CDE = PC-ASTERISK                          
                   MOVE PC-P         TO  PV-DB2-PKG-CDE-1                       
                   MOVE PC-A         TO  PV-DB2-PKG-CDE-2                       
               ELSE                                                             
                   MOVE DP009-SAVE-DB2-PKG-CDE                                  
                                     TO  PV-DB2-PKG-CDE-1                       
                                         PV-DB2-PKG-CDE-2                       
               END-IF                                                           
           ELSE                                                                 
               MOVE PC-P             TO  PV-DB2-PKG-CDE-1                       
               MOVE PC-A             TO  PV-DB2-PKG-CDE-2                       
           END-IF.                                                              
                                                                                
           IF  DP055-LIST-CRITERION NOT GREATER THAN SPACE                      
               IF  DP055-MENU-CRITERION GREATER THAN SPACE                      
                   MOVE PC-M         TO  PV-LIST-CRITERION                      
                                         PV-MENU-CRITERION                      
                   IF  DP055-DETAIL-CRITERION GREATER THAN SPACE                
                       MOVE PC-D     TO  PV-DETAIL-CRITERION                    
                   ELSE                                                         
                       MOVE PC-M     TO  PV-LIST-CRITERION                      
                   END-IF                                                       
               ELSE                                                             
                   IF  DP055-DETAIL-CRITERION GREATER THAN SPACE                
                       MOVE PC-D     TO  PV-LIST-CRITERION                      
                                         PV-MENU-CRITERION                      
                                         PV-DETAIL-CRITERION                    
                   ELSE                                                         
                       MOVE PC-L     TO  PV-LIST-CRITERION                      
                       MOVE PC-M     TO  PV-MENU-CRITERION                      
                       MOVE PC-D     TO  PV-DETAIL-CRITERION                    
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               MOVE PC-L             TO  PV-LIST-CRITERION                      
               IF  DP055-MENU-CRITERION NOT GREATER THAN SPACE                  
                   MOVE PC-L         TO  PV-MENU-CRITERION                      
               ELSE                                                             
                   MOVE PC-M         TO  PV-MENU-CRITERION                      
               END-IF                                                           
                                                                                
               IF  DP055-DETAIL-CRITERION NOT GREATER THAN SPACE                
                   MOVE PC-L         TO  PV-DETAIL-CRITERION                    
               ELSE                                                             
                   MOVE PC-D         TO  PV-DETAIL-CRITERION                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  DP009-SAVE-ACRT-LOC-REQR-IND > SPACES                            
               IF DP009-SAVE-ACRT-LOC-REQR-IND = PC-ASTERISK                    
                   MOVE PC-Y         TO  PV-ACCURATE-LOC-REQ-1                  
                   MOVE PC-N         TO  PV-ACCURATE-LOC-REQ-2                  
               ELSE                                                             
                   MOVE DP009-SAVE-ACRT-LOC-REQR-IND                            
                                     TO  PV-ACCURATE-LOC-REQ-1                  
                                         PV-ACCURATE-LOC-REQ-2                  
               END-IF                                                           
           ELSE                                                                 
               MOVE PC-Y             TO  PV-ACCURATE-LOC-REQ-1                  
               MOVE PC-N             TO  PV-ACCURATE-LOC-REQ-2                  
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HIT THE DESELECT KEY, OR A KEY WHICH INVOLVES   *        
      *    LEAVING THIS TRANSACTION AND GOING TO ANOTHER, SET A FLAG   *        
      *    INDICATING THAT ALL SELECTIONS ARE VALID.  IF ANY OTHER KEY *        
      *    IS HIT, MOVE THE SELECTIONS MADE TO THE COMMAREA, AND EDIT  *        
      *    THOSE SELECTIONS.                                           *        
      *                                                                *        
      *    IF THE USER ENTERED SOMETHING IN THE COMMAND LINE OR HIT    *        
      *    AN INVALID FUNCTION KEY, DO NOT EDIT THE SELECTIONS.        *        
      *                                                                *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       D200-RECORD-SELECTIONS.                                                  
                                                                                
           SET DP009-NO-TSQ-UPDATE   TO  TRUE.                                  
                                                                                
           IF (DP020-FK-DESELECT(DP020-SRC-AID))   OR                           
              (DP020-FK-SELECT-ALL(DP020-SRC-AID)) OR                           
              (DP020-FK-GO-TO-TRAN(DP020-SRC-AID)) OR                           
              (DP020-FK-RETURN(DP020-SRC-AID))                                  
               SET PS-VALID-SELECTIONS TO TRUE                                  
           ELSE                                                                 
               PERFORM E200-MOVE-SELECTS-TO-COMMAREA                            
               IF (DP020-SRC-AID          = DP016-ENTER) AND                    
                  (DP020-NAV-MNEMONIC NOT = SPACES)      AND                    
                  (DP020-NAV-MNEMONIC NOT = LOW-VALUES)                         
                   SET PS-VALID-SELECTIONS TO TRUE                              
               ELSE                                                             
                   PERFORM E100-EDIT-SELECTIONS                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PERFORM THE APPROPRIATE PARAGRAPH BASED ON WHICH FUNCTION   *        
      *    KEY HAD BEEN SELECTED BY THE USER.                          *        
      *                                                                *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       D300-CHECK-FUNCTION-KEY.                                                 
                                                                                
           EVALUATE TRUE                                                        
      *                                                                         
      *    FOR THE ADD FUNCTION (PF13) NO SELECTED ITEMS REQUIRED.              
      *                                                                         
               WHEN DP020-FK-GO-TO-TRAN(DP020-SRC-AID)                          
                    PERFORM E300-NAVIGATE                                       
                                                                                
               WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                          
                   IF  PS-CONTINUE                                              
                       IF  DP020-SRC-AID EQUAL DP016-PF13                       
                           PERFORM E300-NAVIGATE                                
                       ELSE                                                     
                           IF  DP009-NUMBER-OF-SELECTED-ITEMS > ZERO            
                               MOVE SPACES TO DP055-MNEMONIC                    
                               PERFORM X100-PREPARE-LIST-QUEUE                  
                                                                                
                               IF  PS-CONTINUE                                  
                                   PERFORM E300-NAVIGATE                        
                               ELSE                                             
                                   SET DP013-LOGIC-ABEND TO TRUE                
                                   SET DP013-NO-ROLLBACK TO TRUE                
                                   MOVE 'D300-CHECK-FUNCTION-KEY'               
                                     TO  DP013-PARAGRAPH                        
                                   MOVE PM-PROGRAM-MESSAGE-28                   
                                     TO  DP013-MESSAGE-TEXT(1)                  
                                   PERFORM DP013-0000-PROCESS-ABEND             
                               END-IF                                           
                                                                                
                           ELSE                                                 
                                                                                
                               MOVE PC-TSYMSG-00057                             
                                     TO  DP020-MSG-NUMBER                       
                               SET DP020-MSG-WARNING                            
                                     TO TRUE                                    
                               PERFORM G100-DISPLAY-PANEL                       
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
                       PERFORM G100-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN DP020-FK-RETURN(DP020-SRC-AID)                              
                    PERFORM X900-DELETE-LIST-QUEUE                              
                    MOVE DP009-HOLD-MNEMONIC                                    
                                     TO  DP055-MNEMONIC                         
                    PERFORM E300-NAVIGATE                                       
                                                                                
               WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                          
                    PERFORM E400-DISPLAY-TOP-OF-LIST                            
                                                                                
               WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                           
                    PERFORM E500-BROWSE-BACKWARD                                
                                                                                
               WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                           
                    PERFORM E600-BROWSE-FORWARD                                 
                                                                                
               WHEN DP020-FK-DESELECT(DP020-SRC-AID)                            
                    PERFORM E700-DESELECT-ALL-ITEMS                             
                                                                                
               WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                          
                    PERFORM E900-SELECT-ALL-ITEMS                               
                                                                                
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   IF (DP020-NAV-MNEMONIC = LOW-VALUES) OR                      
                      (DP020-NAV-MNEMONIC = SPACES)                             
                       PERFORM E800-CHECK-FOR-JUMP-BROWSE                       
                   ELSE                                                         
                       PERFORM E300-NAVIGATE                                    
                   END-IF                                                       
                                                                                
               WHEN OTHER                                                       
                   MOVE PC-TSYMSG-00045                                         
                                     TO  DP020-MSG-NUMBER                       
                   SET DP020-MSG-FATAL                                          
                                     TO  TRUE                                   
                   PERFORM G100-DISPLAY-PANEL                                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION,*        
      *    READ TEMPORARY STORAGE FILLING IN BOTH BLOCKS IN THE ARRAY  *        
      *    WITH THE ITEMS THAT WERE AT THE TOP OF THE LIST WHEN WE     *        
      *    HAD LEFT PREVIOUSLY.                                        *        
      *                                                                *        
      *    PERFORMED BY C400-REFRESH-PANEL-FROM-TEMP.                  *        
      *----------------------------------------------------------------*        
                                                                                
       D400-REFRESH-BLOCK-ARRAY.                                                
                                                                                
           COMPUTE PV-TEMP-BLOCK-NUMBER =                                       
               1 + ((DP009-LIST-ITEM-NUMBER(1 1) - 1) /                         
                     PC-ITEMS-PER-BLOCK).                                       
                                                                                
           MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                        
           MOVE +1                   TO DP009-BLK-SUB.                          
                                                                                
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
           ADD +1 TO PV-BLOCK-NUMBER.                                           
                                                                                
           IF (PV-BLOCK-NUMBER < DP009-HIGHEST-BLOCK-WRITTEN) OR                
              (PV-BLOCK-NUMBER = DP009-HIGHEST-BLOCK-WRITTEN)                   
               ADD +1 TO DP009-BLK-SUB                                          
                                                                                
               PERFORM Z100-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    EDIT EACH SELECTION ENTRY.  IF EACH ENTRY IS CONSIDERED     *        
      *    VALID, CHECK IF A BEGINNING AND ENDING ENTRY WAS ENTERED,   *        
      *    AND CHECK THE FOLLOWING.                                    *        
      *                                                                *        
      *    1) IF THERE IS A BEGINNING RANGE, THERE MUST BE AN ENDING.  *        
      *    2) IF THERE IS A ENDING RANGE, THERE MUST BE AN BEGINNING.  *        
      *    3) BEGINNING RANGE MUST PRECEDE ENDING RANGE.               *        
      *                                                                *        
      *    IF A VALID BEGINNING AND ENDING RANGE HAVE BEEN SELECTED,   *        
      *    MARK ALL ITEMS IN THAT RANGE AS BEING SELECTED.             *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *    PERFORMED BY C400-REFRESH-PANEL-FROM-TEMP.                  *        
      *    PERFORMED BY D200-RECORD-SELECTIONS.                        *        
      *----------------------------------------------------------------*        
                                                                                
       E100-EDIT-SELECTIONS.                                                    
                                                                                
           SET PS-VALID-SELECTIONS TO TRUE.                                     
                                                                                
           COMPUTE PV-ENDING-ITEM-ON-PANEL =                                    
               (DP009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.           
           MOVE DP009-ITEM-AT-TOP-OF-PANEL                                      
                                     TO  PV-ITEM-IN-USE.                        
           MOVE +1                   TO  PS-SUB.                                
                                                                                
           PERFORM F100-EDIT                                                    
               UNTIL (PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL)                 
                  OR (PV-ITEM-IN-USE > DP009-NUMBER-OF-ITEMS-READ).             
                                                                                
           IF  PS-VALID-SELECTIONS                                              
               IF  DP009-START-OF-SELECTED-RANGE GREATER ZERO                   
                   IF  DP009-END-OF-SELECTED-RANGE GREATER                      
                       DP009-START-OF-SELECTED-RANGE                            
                       PERFORM F200-MARK-RANGE-AS-SELECTED                      
                   ELSE                                                         
                                                                                
                       IF  DP009-END-OF-SELECTED-RANGE = +0                     
                           SET PS-CURSOR-POSITIONED                             
                                     TO  TRUE                                   
                           SET PS-WARNING                                       
                                     TO  TRUE                                   
                           MOVE PC-TSYMSG-00051                                 
                                     TO  DP020-MSG-NUMBER                       
                           IF  NOT DP020-NEXT-ACT-ERROR                         
                               SET DP020-MSG-WARNING TO TRUE                    
                           END-IF                                               
                       ELSE                                                     
                                                                                
                           SET PS-CURSOR-POSITIONED                             
                                     TO  TRUE                                   
                           SET PS-INVALID-SELECTIONS                            
                                     TO  TRUE                                   
                           SET DP020-MSG-FATAL                                  
                                     TO  TRUE                                   
                           MOVE PC-TSYMSG-00052                                 
                                     TO  DP020-MSG-NUMBER                       
                           IF  NOT DP020-NEXT-ACT-ERROR                         
                               MOVE DP009-ITEM-AT-TOP-OF-PANEL                  
                                     TO  PV-ITEM-IN-USE                         
                               MOVE +1                                          
                                     TO  PS-SUB                                 
                                                                                
                               PERFORM                                          
                                   UNTIL (PV-ITEM-IN-USE >                      
                                          PV-ENDING-ITEM-ON-PANEL)              
                                      OR (PV-ITEM-IN-USE >                      
                                          DP009-NUMBER-OF-ITEMS-READ)           
                                                                                
                                   PERFORM K100-SET-SUBSCRIPTS                  
                                   IF (DP009-BEGIN(DP009-BLK-SUB                
                                                   DP009-ITEM-SUB))             
                                   OR (DP009-END(DP009-BLK-SUB                  
                                                 DP009-ITEM-SUB))               
                                       MOVE -1                                  
                                          TO  DPKM009-SELECTL(PS-SUB)           
                                       MOVE PV-ENDING-ITEM-ON-PANEL             
                                          TO  PV-ITEM-IN-USE                    
                                   END-IF                                       
                                   ADD +1 TO  PS-SUB                            
                                   ADD +1 TO  PV-ITEM-IN-USE                    
                               END-PERFORM                                      
                                                                                
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                                                                                
                   IF DP009-END-OF-SELECTED-RANGE > +0                          
                       SET PS-CURSOR-POSITIONED TO TRUE                         
                       SET PS-WARNING           TO TRUE                         
                       MOVE PC-TSYMSG-00053                                     
                                     TO  DP020-MSG-NUMBER                       
                       IF NOT DP020-NEXT-ACT-ERROR                              
                           SET DP020-MSG-WARNING TO TRUE                        
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  DP020-MSG-SEVERITY-IND GREATER THAN SPACES                       
               SET PS-DONT-PROCESS   TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MOVE THE SELECTIONS MADE FROM THE CURRENT PAGE INTO         *        
      *    THE COMMAREA.                                               *        
      *                                                                *        
      *    PERFORMED BY D200-RECORD-SELECTIONS.                        *        
      *----------------------------------------------------------------*        
                                                                                
       E200-MOVE-SELECTS-TO-COMMAREA.                                           
                                                                                
           COMPUTE PV-ENDING-ITEM-ON-PANEL =                                    
               (DP009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.           
                                                                                
           MOVE +1                         TO PS-SUB.                           
           MOVE DP009-ITEM-AT-TOP-OF-PANEL TO PV-ITEM-IN-USE.                   
                                                                                
           PERFORM F300-CHECK-SELECTIONS                                        
               UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS A KEY THAT REQUIRES US TO GO TO ANOTHER    *        
      *    TRANSACTION, WRITE OUT THE LAST 2 BLOCKS FROM THE ARRAY TO  *        
      *    TEMPORARY STORAGE, SET UP THE INTER APPLICATION COMMAREA,   *        
      *    AND START THE NAVIGATOR.                                    *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E300-NAVIGATE.                                                           
                                                                                
           MOVE +1 TO DP009-BLK-SUB.                                            
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           ADD +1 TO DP009-BLK-SUB.                                             
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE INTER-APPL-COMM-AREA TO  DP009-SAVE-INTER-APPL-COMMAREA.        
                                                                                
           PERFORM DP015-1000-START-NAVIGATOR.                                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE 1ST PAGE KEY, COMPUTE THE TOP ITEM     *        
      *    NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM          *        
      *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS *        
      *    TO BE READ.                                                 *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E400-DISPLAY-TOP-OF-LIST.                                                
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ > +0                                  
               MOVE +1 TO PV-TOP-ITEM                                           
               PERFORM F500-BROWSE                                              
           ELSE                                                                 
               SET PS-SAME-ITEMS-DISPLAYED TO TRUE                              
               PERFORM G100-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE BACKARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM     *        
      *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS *        
      *    TO BE READ.                                                 *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E500-BROWSE-BACKWARD.                                                    
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ > +0                                  
               COMPUTE PV-TOP-ITEM =                                            
                   DP009-ITEM-AT-TOP-OF-PANEL - PC-ITEMS-PER-PANEL              
               PERFORM F500-BROWSE                                              
           ELSE                                                                 
               SET PS-SAME-ITEMS-DISPLAYED TO TRUE                              
               PERFORM G100-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *        
      *    ITEM NUMBER FOR THE NEW PAGE AND DETERMINE IF THIS ITEM     *        
      *    IS CURRENTLY IN THE ARRAY, OR IN TEMPORARY STORAGE OR NEEDS *        
      *    TO BE READ.                                                 *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E600-BROWSE-FORWARD.                                                     
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ > +0                                  
               COMPUTE PV-TOP-ITEM =                                            
                   DP009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL              
               PERFORM F500-BROWSE                                              
           ELSE                                                                 
               SET PS-SAME-ITEMS-DISPLAYED TO TRUE                              
               PERFORM G100-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *        
      *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E700-DESELECT-ALL-ITEMS.                                                 
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ > +0                                  
               MOVE +1               TO  DP009-BLK-SUB                          
                                                                                
               PERFORM F600-CLEAR-SELECTED-INDS                                 
                   VARYING DP009-ITEM-SUB FROM 1 BY 1                           
                   UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                    
                                                                                
               ADD +1                TO  DP009-BLK-SUB                          
                                                                                
               PERFORM F600-CLEAR-SELECTED-INDS                                 
                   VARYING DP009-ITEM-SUB FROM 1 BY 1                           
                   UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                    
                                                                                
               MOVE DP009-LIST-ITEM-NUMBER(1 1) TO PV-LIST-ITEM-NUMBER          
                                                                                
               COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                
                   1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK)         
                                                                                
               IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                         
                  (DP009-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)        
                   MOVE +1 TO DP009-BLK-SUB                                     
                                                                                
                   PERFORM Z150-WRITE-TEMP-STORAGE                              
                                                                                
                   MOVE +1 TO PV-BLOCK-NUMBER                                   
                                                                                
                   COMPUTE PV-PREV-BLOCK-IN-ARRAY =                             
                       PV-FIRST-BLOCK-IN-ARRAY - 1                              
                                                                                
                   PERFORM                                                      
                       UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY           
                                                                                
                       PERFORM Z100-READ-TEMP-STORAGE                           
                                                                                
                       PERFORM F600-CLEAR-SELECTED-INDS                         
                           VARYING DP009-ITEM-SUB FROM 1 BY 1                   
                           UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK            
                                                                                
                       PERFORM Z150-WRITE-TEMP-STORAGE                          
                                                                                
                       ADD +1 TO PV-BLOCK-NUMBER                                
                                                                                
                   END-PERFORM                                                  
                                                                                
                   COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             
                       PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY         
                                                                                
                   PERFORM                                                      
                       UNTIL PV-NEXT-BLOCK-IN-ARRAY >                           
                             DP009-HIGHEST-BLOCK-WRITTEN                        
                                                                                
                       MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER           
                                                                                
                       PERFORM Z100-READ-TEMP-STORAGE                           
                                                                                
                       PERFORM F600-CLEAR-SELECTED-INDS                         
                           VARYING DP009-ITEM-SUB FROM 1 BY 1                   
                           UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK            
                                                                                
                       PERFORM Z150-WRITE-TEMP-STORAGE                          
                                                                                
                       ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                         
                                                                                
                   END-PERFORM                                                  
                                                                                
                   MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER              
                                                                                
                   PERFORM Z100-READ-TEMP-STORAGE                               
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
           MOVE +0 TO DP009-NUMBER-OF-SELECTED-ITEMS.                           
           MOVE +0 TO DP009-START-OF-SELECTED-RANGE.                            
           MOVE +0 TO DP009-END-OF-SELECTED-RANGE.                              
                                                                                
           SET PS-SAME-ITEMS-DISPLAYED TO TRUE.                                 
                                                                                
           PERFORM G100-DISPLAY-PANEL.                                          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *        
      *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *        
      *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *        
      *    RATHER THAN HITTING PF8 TO PAGE FORWARD TO THAT POINT.      *        
      *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *        
      *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E800-CHECK-FOR-JUMP-BROWSE.                                              
                                                                                
           SET PS-VALID-BEG-ITEM TO TRUE.                                       
                                                                                
           IF (ASTRTITA                   = PC-ERASE-EOF) OR                    
              (DP009-NUMBER-OF-ITEMS-READ = +0)                                 
               SET PS-SAME-ITEMS-DISPLAYED TO TRUE                              
               PERFORM G100-DISPLAY-PANEL                                       
           ELSE                                                                 
                                                                                
               MOVE ASTRTITI         TO  DP010I-UNEDITED-FIELD                  
               MOVE PC-FIELD-LENGTH  TO  DP010I-MAXIMUM-DIGITS                  
               MOVE +0               TO  DP010I-MAXIMUM-DECIMALS                
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
                                                                                
               IF  NOT DP010I-ERROR-DETECTED                                    
                   MOVE DP010I-NUMERIC-FIELD                                    
                                     TO  PV-BEGITMI-N                           
                   MOVE PV-BEGITMI-N TO  PV-BEGINNING-ITEM                      
                                                                                
                   IF (PV-BEGINNING-ITEM >  0)          AND                     
                      (PV-BEGINNING-ITEM <  PC-MAX-ITEMS)                       
                       MOVE PV-BEGINNING-ITEM TO PV-TOP-ITEM                    
                                                                                
                       COMPUTE PV-MAX-ITEMS-TO-BE-READ =                        
                           ((DP009-NUMBER-OF-ITEMS-READ +                       
                             PC-MAX-ITEMS-TO-BE-READ)   -                       
                             PC-ITEMS-PER-PANEL)        + 1                     
                                                                                
                       IF PV-TOP-ITEM > PV-MAX-ITEMS-TO-BE-READ                 
                           MOVE PV-MAX-ITEMS-TO-BE-READ                         
                                     TO  PV-TOP-ITEM                            
                           MOVE -1   TO  ASTRTITL                               
                                                                                
                           IF DP009-NO-ITEMS-LEFT-ON-DB                         
                               MOVE PC-TSYMSG-00070                             
                                     TO  DP020-MSG-NUMBER                       
                           ELSE                                                 
                               MOVE PC-TSYMSG-00207                             
                                     TO  DP020-MSG-NUMBER                       
                           END-IF                                               
                                                                                
                           SET PS-CURSOR-POSITIONED                             
                                     TO  TRUE                                   
                           SET DP020-MSG-INFORMATIONAL                          
                                     TO  TRUE                                   
                       END-IF                                                   
                       PERFORM F500-BROWSE                                      
                   ELSE                                                         
                       MOVE PC-TSYMSG-00101                                     
                                     TO  DP020-MSG-NUMBER                       
                       MOVE -1       TO  ASTRTITL                               
                       MOVE DP015-REVERSE                                       
                                     TO  ASTRTITH                               
                       MOVE DP015-RED                                           
                                     TO  ASTRTITC                               
                       MOVE DP015-UNP-NUM-BRT-MDT                               
                                     TO  ASTRTITA                               
                       SET PS-CURSOR-POSITIONED                                 
                                     TO  TRUE                                   
                       SET DP020-MSG-FATAL                                      
                                     TO  TRUE                                   
                       SET PS-INVALID-BEG-ITEM                                  
                                     TO  TRUE                                   
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   MOVE PC-TSYMSG-00008                                         
                                     TO  DP020-MSG-NUMBER                       
                   MOVE -1           TO  ASTRTITL                               
                   MOVE DP015-REVERSE                                           
                                     TO  ASTRTITH                               
                   MOVE DP015-RED    TO  ASTRTITC                               
                   MOVE DP015-UNP-NUM-BRT-MDT                                   
                                     TO  ASTRTITA                               
                   SET PS-CURSOR-POSITIONED                                     
                                     TO  TRUE                                   
                   SET DP020-MSG-FATAL                                          
                                     TO  TRUE                                   
                   SET PS-INVALID-BEG-ITEM                                      
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PS-INVALID-BEG-ITEM                                              
               SET PS-SAME-ITEMS-DISPLAYED TO TRUE                              
               PERFORM DP017-0000-GET-CURR-DATE-TIME                            
               PERFORM I200-FORMAT-LIST-LINES                                   
               PERFORM I300-FORMAT-NR-SELECTED                                  
               PERFORM DP005-0000-FORMAT-MSG-LINES                              
               PERFORM DP008-0000-WRITE-PANEL                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE USER HITS THE SELECT KEY, SET THE SELECTION SWITCH   *        
      *    FOR ALL ITEMS.                                              *        
      *                                                                *        
      *    PERFORMED BY D300-CHECK-FUNCTION-KEY.                       *        
      *----------------------------------------------------------------*        
                                                                                
       E900-SELECT-ALL-ITEMS.                                                   
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ > +0                                  
               MOVE +1 TO DP009-BLK-SUB                                         
                                                                                
               PERFORM F700-SET-SELECTED-INDS                                   
                   VARYING DP009-ITEM-SUB FROM 1 BY 1                           
                   UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                    
                                                                                
               ADD +1 TO DP009-BLK-SUB                                          
                                                                                
               PERFORM F700-SET-SELECTED-INDS                                   
                   VARYING DP009-ITEM-SUB FROM 1 BY 1                           
                   UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                    
                                                                                
               MOVE DP009-LIST-ITEM-NUMBER(1 1) TO PV-LIST-ITEM-NUMBER          
                                                                                
               COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                
                   1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK)         
                                                                                
               IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                         
                  (DP009-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)        
                   MOVE +1 TO DP009-BLK-SUB                                     
                                                                                
                   PERFORM Z150-WRITE-TEMP-STORAGE                              
                                                                                
                   MOVE +1 TO PV-BLOCK-NUMBER                                   
                                                                                
                   COMPUTE PV-PREV-BLOCK-IN-ARRAY =                             
                       PV-FIRST-BLOCK-IN-ARRAY - 1                              
                                                                                
                   PERFORM                                                      
                       UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY           
                                                                                
                       PERFORM Z100-READ-TEMP-STORAGE                           
                                                                                
                       PERFORM F700-SET-SELECTED-INDS                           
                           VARYING DP009-ITEM-SUB FROM 1 BY 1                   
                           UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK            
                                                                                
                       PERFORM Z150-WRITE-TEMP-STORAGE                          
                                                                                
                       ADD +1 TO PV-BLOCK-NUMBER                                
                                                                                
                   END-PERFORM                                                  
                                                                                
                   COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             
                       PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY         
                                                                                
                   PERFORM                                                      
                       UNTIL PV-NEXT-BLOCK-IN-ARRAY >                           
                             DP009-HIGHEST-BLOCK-WRITTEN                        
                                                                                
                       MOVE PV-NEXT-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER           
                                                                                
                       PERFORM Z100-READ-TEMP-STORAGE                           
                                                                                
                       PERFORM F700-SET-SELECTED-INDS                           
                           VARYING DP009-ITEM-SUB FROM 1 BY 1                   
                           UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK            
                                                                                
                       PERFORM Z150-WRITE-TEMP-STORAGE                          
                                                                                
                       ADD +1 TO PV-NEXT-BLOCK-IN-ARRAY                         
                                                                                
                   END-PERFORM                                                  
                                                                                
                   MOVE PV-FIRST-BLOCK-IN-ARRAY TO PV-BLOCK-NUMBER              
                                                                                
                   PERFORM Z100-READ-TEMP-STORAGE                               
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
           MOVE DP009-NUMBER-OF-ITEMS-READ TO                                   
                             DP009-NUMBER-OF-SELECTED-ITEMS.                    
                                                                                
           SET PS-SAME-ITEMS-DISPLAYED TO TRUE.                                 
                                                                                
           PERFORM G100-DISPLAY-PANEL.                                          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    EDIT THE SELECTION ENTRIES.                                 *        
      *                                                                *        
      *    1) VALID ENTRIES ARE 'S' 'B' OR 'E'.                        *        
      *    2) THERE CAN ONLY BE 1 BEGINNING AND 1 ENDING RANGE.        *        
      *                                                                *        
      *    PERFORMED BY E100-EDIT-SELECTIONS.                          *        
      *----------------------------------------------------------------*        
                                                                                
       F100-EDIT.                                                               
                                                                                
           PERFORM K100-SET-SUBSCRIPTS.                                         
                                                                                
           IF NOT (DP009-NO-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)) AND           
              NOT (DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB))                  
                                                                                
               IF  DP009-BEGIN(DP009-BLK-SUB DP009-ITEM-SUB)                    
                   IF  DP009-START-OF-SELECTED-RANGE                            
                       NOT EQUAL PV-ITEM-IN-USE                                 
                                                                                
                       IF  DP009-START-OF-SELECTED-RANGE = +0                   
                           MOVE PV-ITEM-IN-USE                                  
                                     TO  DP009-START-OF-SELECTED-RANGE          
                       ELSE                                                     
                           IF PS-VALID-SELECTIONS                               
                               MOVE PC-TSYMSG-00054                             
                                     TO  DP020-MSG-NUMBER                       
                               IF  NOT DP020-NEXT-ACT-ERROR                     
                                   MOVE -1                                      
                                     TO  DPKM009-SELECTL(PS-SUB)                
                               END-IF                                           
                           END-IF                                               
                                                                                
                           SET DP020-MSG-FATAL                                  
                                     TO  TRUE                                   
                           SET PS-INVALID-SELECTIONS                            
                                     TO  TRUE                                   
                           SET PS-CURSOR-POSITIONED                             
                                     TO  TRUE                                   
                       END-IF                                                   
                   END-IF                                                       
               ELSE                                                             
                                                                                
                   IF  DP009-END(DP009-BLK-SUB DP009-ITEM-SUB)                  
                       IF  DP009-END-OF-SELECTED-RANGE                          
                           NOT EQUAL PV-ITEM-IN-USE                             
                                                                                
                           IF DP009-END-OF-SELECTED-RANGE = +0                  
                               MOVE PV-ITEM-IN-USE                              
                                     TO  DP009-END-OF-SELECTED-RANGE            
                           ELSE                                                 
                                                                                
                               IF  PS-VALID-SELECTIONS                          
                                   MOVE PC-TSYMSG-00055                         
                                     TO  DP020-MSG-NUMBER                       
                                                                                
                                   IF  NOT DP020-NEXT-ACT-ERROR                 
                                       MOVE -1                                  
                                          TO  DPKM009-SELECTL(PS-SUB)           
                                   END-IF                                       
                               END-IF                                           
                               SET PS-INVALID-SELECTIONS                        
                                          TO  TRUE                              
                               SET PS-CURSOR-POSITIONED                         
                                          TO  TRUE                              
                               SET DP020-MSG-FATAL                              
                                          TO  TRUE                              
                           END-IF                                               
                       END-IF                                                   
                   ELSE                                                         
                                                                                
                       IF  PS-VALID-SELECTIONS                                  
                           MOVE PC-TSYMSG-00005                                 
                                     TO  DP020-MSG-NUMBER                       
                           IF  NOT DP020-NEXT-ACT-ERROR                         
                               MOVE -1                                          
                                     TO  DPKM009-SELECTL(PS-SUB)                
                           END-IF                                               
                       END-IF                                                   
                       SET PS-INVALID-SELECTIONS                                
                                     TO  TRUE                                   
                       SET PS-CURSOR-POSITIONED                                 
                                     TO  TRUE                                   
                       SET DP020-MSG-FATAL                                      
                                     TO  TRUE                                   
                       MOVE DP015-RED                                           
                                     TO  DPKM009-SELECTC(PS-SUB)                
                       MOVE DP015-REVERSE                                       
                                     TO  DPKM009-SELECTH(PS-SUB)                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD +1                    TO  PV-ITEM-IN-USE.                        
           ADD +1                    TO  PS-SUB.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *        
      *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *        
      *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *        
      *                                                                *        
      *    PERFORMED BY E100-EDIT-SELECTIONS.                          *        
      *----------------------------------------------------------------*        
                                                                                
       F200-MARK-RANGE-AS-SELECTED.                                             
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM G200-MARK-SELECTED-INDS                                      
               VARYING DP009-ITEM-SUB FROM 1 BY 1                               
               UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK.                       
                                                                                
           ADD +1 TO DP009-BLK-SUB.                                             
           PERFORM G200-MARK-SELECTED-INDS                                      
               VARYING DP009-ITEM-SUB FROM 1 BY 1                               
               UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK.                       
                                                                                
           MOVE DP009-LIST-ITEM-NUMBER(1 1)                                     
                                     TO  PV-LIST-ITEM-NUMBER.                   
           COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                                    
               1 + ((PV-LIST-ITEM-NUMBER - 1) /                                 
                     PC-ITEMS-PER-BLOCK).                                       
                                                                                
           MOVE DP009-START-OF-SELECTED-RANGE                                   
                                     TO  PV-START-OF-SELECTED-RANGE.            
           COMPUTE PV-FIRST-BLOCK-IN-RANGE =                                    
               1 + ((PV-START-OF-SELECTED-RANGE - 1) /                          
                     PC-ITEMS-PER-BLOCK).                                       
                                                                                
           MOVE DP009-END-OF-SELECTED-RANGE                                     
                                     TO  PV-END-OF-SELECTED-RANGE.              
           COMPUTE PV-LAST-BLOCK-IN-RANGE =                                     
               1 + ((PV-END-OF-SELECTED-RANGE - 1) /                            
                     PC-ITEMS-PER-BLOCK).                                       
           COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                     
               PV-FIRST-BLOCK-IN-ARRAY + 1.                                     
                                                                                
           IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR            
              (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)                
               MOVE +1               TO  DP009-BLK-SUB                          
               PERFORM Z150-WRITE-TEMP-STORAGE                                  
               COMPUTE PV-PREV-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY - 1                                  
                                                                                
               PERFORM                                                          
                   UNTIL PV-FIRST-BLOCK-IN-RANGE >                              
                         PV-PREV-BLOCK-IN-ARRAY                                 
                   MOVE PV-FIRST-BLOCK-IN-RANGE                                 
                                     TO  PV-BLOCK-NUMBER                        
                   PERFORM Z100-READ-TEMP-STORAGE                               
                   PERFORM G200-MARK-SELECTED-INDS                              
                       VARYING DP009-ITEM-SUB FROM 1 BY 1                       
                       UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                
                   PERFORM Z150-WRITE-TEMP-STORAGE                              
                   ADD +1            TO PV-FIRST-BLOCK-IN-RANGE                 
               END-PERFORM                                                      
                                                                                
               COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                                 
                   PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY             
               PERFORM                                                          
                   UNTIL PV-NEXT-BLOCK-IN-ARRAY >                               
                         PV-LAST-BLOCK-IN-RANGE                                 
                   MOVE PV-NEXT-BLOCK-IN-ARRAY                                  
                                     TO  PV-BLOCK-NUMBER                        
                   PERFORM Z100-READ-TEMP-STORAGE                               
                   PERFORM G200-MARK-SELECTED-INDS                              
                       VARYING DP009-ITEM-SUB FROM 1 BY 1                       
                       UNTIL DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                
                   PERFORM Z150-WRITE-TEMP-STORAGE                              
                   ADD +1            TO  PV-NEXT-BLOCK-IN-ARRAY                 
               END-PERFORM                                                      
                                                                                
               MOVE PV-FIRST-BLOCK-IN-ARRAY                                     
                                     TO  PV-BLOCK-NUMBER                        
               PERFORM Z100-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
           MOVE +0                   TO  DP009-START-OF-SELECTED-RANGE          
                                         DP009-END-OF-SELECTED-RANGE.           
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CHECK EACH ENTRY TO DETERMINE IF THE SELECTION CRITERIA IN  *        
      *    THE MAP DIFFERS FROM THAT IN THE ARRAY.  IF THEY DO DIFFER, *        
      *    AND THE ARRAY ENTRY SHOWS THAT THE ITEM WAS PREVIOUSLY      *        
      *    SELECTED, SUBTRACT 1 FROM THE SELECTED COUNTER.  IF THEY DO *        
      *    DIFFER, AND THE ARRAY ENTRY SHOWS THAT THIS ITEM WAS MARKED *        
      *    AS EITHER THE BEGINNING OR ENDING SELECTION POINT, ZERO OUT *        
      *    THE STARTING RANGE AND/OR ENDING RANGE.  IF THE ENTRY ON    *        
      *    THE MAP SHOWS THAT THIS ITEM WAS SELECTED, ADD 1 TO THE     *        
      *    SELECTED COUNTER.                                           *        
      *                                                                *        
      *    PERFORMED BY E200-MOVE-SELECTS-TO-COMMAREA.                 *        
      *----------------------------------------------------------------*        
                                                                                
       F300-CHECK-SELECTIONS.                                                   
                                                                                
           IF (PV-ITEM-IN-USE < DP009-NUMBER-OF-ITEMS-READ) OR                  
              (PV-ITEM-IN-USE = DP009-NUMBER-OF-ITEMS-READ)                     
               PERFORM K100-SET-SUBSCRIPTS                                      
                                                                                
               IF  DP009-SELECTED-SW(DP009-BLK-SUB DP009-ITEM-SUB)              
                   NOT EQUAL DPKM009-SELECTI(PS-SUB)                            
                   SET DP009-BLOCK-MODIFIED(DP009-BLK-SUB)                      
                                     TO  TRUE                                   
                   IF  DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)               
                       SUBTRACT +1 FROM DP009-NUMBER-OF-SELECTED-ITEMS          
                   ELSE                                                         
                                                                                
                       IF  DP009-BEGIN(DP009-BLK-SUB DP009-ITEM-SUB)            
                           IF  DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB             
                                                     DP009-ITEM-SUB) =          
                               DP009-START-OF-SELECTED-RANGE                    
                               MOVE +0                                          
                                     TO  DP009-START-OF-SELECTED-RANGE          
                           END-IF                                               
                       ELSE                                                     
                                                                                
                           IF  DP009-END(DP009-BLK-SUB DP009-ITEM-SUB)          
                               IF  DP009-LIST-ITEM-NUMBER                       
                                       (DP009-BLK-SUB DP009-ITEM-SUB)           
                               =  DP009-END-OF-SELECTED-RANGE                   
                                   MOVE +0                                      
                                     TO DP009-END-OF-SELECTED-RANGE             
                               END-IF                                           
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF DPKM009-SELECTI(PS-SUB) = PC-SELECTED                     
                       ADD +1        TO DP009-NUMBER-OF-SELECTED-ITEMS          
                       MOVE DP009-MNEMONIC (DP009-BLK-SUB                       
                                            DP009-ITEM-SUB)                     
                                     TO  DP009-HOLD-MNEMONIC                    
                   END-IF                                                       
                                                                                
                   IF (DPKM009-SELECTA(PS-SUB) = PC-ERASE-EOF) OR               
                      (DPKM009-SELECTI(PS-SUB) = SPACES)                        
                       MOVE SPACES   TO                                         
                            DP009-SELECTED-SW(DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                   ELSE                                                         
                       MOVE DPKM009-SELECTI(PS-SUB) TO                          
                            DP009-SELECTED-SW(DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD +1                    TO  PV-ITEM-IN-USE.                        
           ADD +1                    TO  PS-SUB.                                
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CALCULATE THE NEW TOP AND BOTTOM ITEM NUMBERS BASED ON WHERE*        
      *    THE USER SPECIFIED TO GO ON THE LIST AND DETERMINE WHERE    *        
      *    THESE ITEMS CAN BE FOUND (I.E. ARRAY, TEMPORARY STORAGE).   *        
      *                                                                *        
      *    PERFORMED BY E400-DISPLAY-TOP-OF-LIST.                      *        
      *    PERFORMED BY E500-BROWSE-BACKWARD.                          *        
      *    PERFORMED BY E600-BROWSE-FORWARD.                           *        
      *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                    *        
      *----------------------------------------------------------------*        
                                                                                
       F500-BROWSE.                                                             
                                                                                
           IF  PV-TOP-ITEM < +1                                                 
               MOVE +1               TO  PV-TOP-ITEM                            
               IF  PS-CURSOR-NOT-POSITIONED                                     
                   MOVE PC-TSYMSG-00069                                         
                                     TO  DP020-MSG-NUMBER                       
                   SET DP020-MSG-INFORMATIONAL                                  
                                     TO  TRUE                                   
                   SET PS-INFORMATIONAL                                         
                                     TO  TRUE                                   
                   SET PS-CURSOR-POSITIONED                                     
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  DP009-NO-ITEMS-LEFT-ON-DB                                        
               IF  PV-TOP-ITEM > DP009-NUMBER-OF-ITEMS-READ                     
                   IF  DP020-SRC-AID EQUAL DP016-ENTER                          
                       MOVE DP009-NUMBER-OF-ITEMS-READ                          
                                     TO  PV-TOP-ITEM                            
                   ELSE                                                         
                       MOVE DP009-ITEM-AT-TOP-OF-PANEL                          
                                     TO  PV-TOP-ITEM                            
                   END-IF                                                       
                                                                                
                   IF  PS-CURSOR-NOT-POSITIONED                                 
                       MOVE -1       TO  ANAVCMDL                               
                       SET DP020-MSG-INFORMATIONAL                              
                                     TO  TRUE                                   
                       SET PS-CURSOR-POSITIONED                                 
                                     TO  TRUE                                   
                       IF  DP020-FK-NEXT-PAGE(DP020-SRC-AID)                    
                           MOVE PC-TSYMSG-00070                                 
                                     TO  DP020-MSG-NUMBER                       
                           SET PS-INFORMATIONAL                                 
                                     TO  TRUE                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           COMPUTE PV-BOTTOM-ITEM =                                             
               (PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1.                          
                                                                                
           IF  DP009-NO-ITEMS-LEFT-ON-DB                                        
               IF  PV-BOTTOM-ITEM > DP009-NUMBER-OF-ITEMS-READ                  
                   MOVE DP009-NUMBER-OF-ITEMS-READ                              
                                     TO  PV-BOTTOM-ITEM                         
                   IF  PS-CURSOR-NOT-POSITIONED                                 
                       IF  PV-TOP-ITEM NOT = +1                                 
                           IF  DP020-FK-NEXT-PAGE(DP020-SRC-AID)                
                               MOVE PC-TSYMSG-00070                             
                                     TO  DP020-MSG-NUMBER                       
                               SET PS-INFORMATIONAL                             
                                     TO  TRUE                                   
                               SET DP020-MSG-INFORMATIONAL                      
                                     TO  TRUE                                   
                               SET PS-CURSOR-POSITIONED                         
                                     TO  TRUE                                   
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM G300-DETERMINE-WHAT-IS-WHERE.                                
                                                                                
           IF  PS-TOP-ITEM-IN-ARRAY                                             
               IF  PS-BOTTOM-ITEM-NOT-IN-ARRAY                                  
                   IF  PS-BOTTOM-ITEM-IN-TEMP                                   
                       PERFORM G500-SCROLL-FORWARD                              
                   ELSE                                                         
                       IF  PS-LAST-BLOCK-NOT-IN-ARRAY                           
                           PERFORM G500-SCROLL-FORWARD                          
                       END-IF                                                   
                       PERFORM G400-READ-ITEMS-INTO-ARRAY                       
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
                                                                                
               IF  PS-BOTTOM-ITEM-IN-ARRAY                                      
                   PERFORM G600-SCROLL-BACKWARD                                 
               ELSE                                                             
                   IF  PS-TOP-ITEM-IN-TEMP                                      
                       PERFORM G700-GET-TOP-ITEMS-BLOCK                         
                       PERFORM G300-DETERMINE-WHAT-IS-WHERE                     
                       IF  PS-BOTTOM-ITEM-NOT-IN-ARRAY                          
                           PERFORM G400-READ-ITEMS-INTO-ARRAY                   
                       END-IF                                                   
                   ELSE                                                         
                       IF  PS-LAST-BLOCK-NOT-IN-ARRAY                           
                           PERFORM G800-GET-LAST-BLOCK-IN-TEMP                  
                       END-IF                                                   
                       PERFORM G400-READ-ITEMS-INTO-ARRAY                       
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  PV-TOP-ITEM = DP009-ITEM-AT-TOP-OF-PANEL                         
               SET PS-SAME-ITEMS-DISPLAYED                                      
                                     TO  TRUE                                   
           ELSE                                                                 
               SET PS-DIFFERENT-ITEMS-DISPLAYED                                 
                                     TO  TRUE                                   
               MOVE PV-TOP-ITEM      TO  DP009-ITEM-AT-TOP-OF-PANEL             
           END-IF.                                                              
                                                                                
           PERFORM G100-DISPLAY-PANEL.                                          
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING NOT SELECTED.                       *        
      *                                                                *        
      *    PERFORMED BY E700-DESELECT-ALL-ITEMS.                       *        
      *----------------------------------------------------------------*        
                                                                                
       F600-CLEAR-SELECTED-INDS.                                                
                                                                                
           IF  DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB DP009-ITEM-SUB)             
                   NOT EQUAL ZERO                                               
               IF  NOT DP009-NO-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)            
                   SET DP009-NO-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)            
                                     TO  TRUE                                   
                   SET DP009-BLOCK-MODIFIED(DP009-BLK-SUB)                      
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM AS BEING SELECTED.                           *        
      *                                                                *        
      *    PERFORMED BY E900-SELECT-ALL-ITEMS.                         *        
      *----------------------------------------------------------------*        
                                                                                
       F700-SET-SELECTED-INDS.                                                  
                                                                                
           IF  DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB DP009-ITEM-SUB)             
                   NOT EQUAL ZERO                                               
               IF  NOT DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)               
                   SET DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)               
                                     TO  TRUE                                   
                   SET DP009-BLOCK-MODIFIED(DP009-BLK-SUB)                      
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FILL THE MAP WITH THE NECESSARY DATA AND WRITE IT OUT.      *        
      *                                                                *        
      *    PERFORMED BY B100-CONTROL-PROCESSING.                       *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *    PERFORMED BY C400-REFRESH-PANEL-FROM-TEMP.                  *        
      *    PERFORMED BY D100-SETUP-INIT-PANEL.                         *        
      *    PERFORMED BY E400-DISPLAY-TOP-OF-LIST.                      *        
      *    PERFORMED BY E500-BROWSE-BACKWARD.                          *        
      *    PERFORMED BY E600-BROWSE-FORWARD.                           *        
      *    PERFORMED BY E700-DESELECT-ALL-ITEMS.                       *        
      *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                    *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G100-DISPLAY-PANEL.                                                      
                                                                                
           PERFORM DP017-0000-GET-CURR-DATE-TIME.                               
           MOVE DP017-EDITED-TIME-HMS                                           
                                     TO  ATIMEO.                                
                                                                                
           IF PS-SEND-MAP-N-DATA-FRSET                                          
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
               PERFORM DP009-0000-LOAD-FK-LINES                                 
           END-IF.                                                              
                                                                                
           PERFORM H200-FORMAT-PANEL-BODY.                                      
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    MARK EACH ITEM THAT FALLS WITHIN THE BEGINNING AND ENDING   *        
      *    RANGE AS BEING SELECTED.                                    *        
      *                                                                *        
      *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                   *        
      *----------------------------------------------------------------*        
                                                                                
       G200-MARK-SELECTED-INDS.                                                 
                                                                                
           IF (DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB                             
                                      DP009-ITEM-SUB) >                         
               DP009-START-OF-SELECTED-RANGE)                                   
           OR (DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB                             
                                      DP009-ITEM-SUB) =                         
               DP009-START-OF-SELECTED-RANGE)                                   
                                                                                
               IF (DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB                         
                                          DP009-ITEM-SUB) <                     
                   DP009-END-OF-SELECTED-RANGE)                                 
               OR (DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB                         
                                          DP009-ITEM-SUB) =                     
                   DP009-END-OF-SELECTED-RANGE)                                 
                                                                                
                   IF NOT DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB)            
                       SET DP009-SELECT(DP009-BLK-SUB                           
                                        DP009-ITEM-SUB)                         
                                     TO  TRUE                                   
                       SET DP009-BLOCK-MODIFIED(DP009-BLK-SUB)                  
                                     TO  TRUE                                   
                       ADD +1        TO DP009-NUMBER-OF-SELECTED-ITEMS          
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE WHETHER THE TOP ITEM AND BOTTOM ITEM CAN BE FOUND *        
      *    IN THE ARRAY, IN TEMPORARY STORAGE, OR NEITHER.             *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G300-DETERMINE-WHAT-IS-WHERE.                                            
                                                                                
           MOVE DP009-LIST-ITEM-NUMBER(1 1)                                     
                                     TO  PV-FIRST-ITEM-IN-ARRAY.                
           COMPUTE PV-LAST-ITEM-IN-ARRAY =                                      
               (PV-FIRST-ITEM-IN-ARRAY +                                        
               (PC-MAX-BLOCKS-IN-ARRAY * PC-ITEMS-PER-BLOCK)) - 1.              
           IF  PV-LAST-ITEM-IN-ARRAY > DP009-NUMBER-OF-ITEMS-READ               
               MOVE DP009-NUMBER-OF-ITEMS-READ                                  
                                     TO  PV-LAST-ITEM-IN-ARRAY                  
           END-IF.                                                              
                                                                                
           COMPUTE PV-LAST-ITEM-IN-TEMP =                                       
               DP009-HIGHEST-BLOCK-WRITTEN * PC-ITEMS-PER-BLOCK.                
           IF  PV-LAST-ITEM-IN-TEMP > DP009-NUMBER-OF-ITEMS-READ                
               MOVE DP009-NUMBER-OF-ITEMS-READ                                  
                                     TO  PV-LAST-ITEM-IN-TEMP                   
           END-IF.                                                              
                                                                                
           IF (PV-TOP-ITEM > PV-FIRST-ITEM-IN-ARRAY)                            
           OR (PV-TOP-ITEM = PV-FIRST-ITEM-IN-ARRAY)                            
               IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-ARRAY)                         
               OR (PV-TOP-ITEM = PV-LAST-ITEM-IN-ARRAY)                         
                   SET PS-TOP-ITEM-IN-ARRAY                                     
                                     TO  TRUE                                   
               ELSE                                                             
                   SET PS-TOP-ITEM-NOT-IN-ARRAY                                 
                                     TO  TRUE                                   
               END-IF                                                           
           ELSE                                                                 
               SET PS-TOP-ITEM-NOT-IN-ARRAY                                     
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
           IF (PV-BOTTOM-ITEM > PV-FIRST-ITEM-IN-ARRAY)                         
           OR (PV-BOTTOM-ITEM = PV-FIRST-ITEM-IN-ARRAY)                         
               IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-ARRAY)                      
               OR (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-ARRAY)                      
                   SET PS-BOTTOM-ITEM-IN-ARRAY                                  
                                     TO  TRUE                                   
               ELSE                                                             
                   SET PS-BOTTOM-ITEM-NOT-IN-ARRAY                              
                                     TO  TRUE                                   
               END-IF                                                           
           ELSE                                                                 
               SET PS-BOTTOM-ITEM-NOT-IN-ARRAY                                  
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
           IF (PV-TOP-ITEM < PV-LAST-ITEM-IN-TEMP)                              
           OR (PV-TOP-ITEM = PV-LAST-ITEM-IN-TEMP)                              
               SET PS-TOP-ITEM-IN-TEMP                                          
                                     TO  TRUE                                   
           ELSE                                                                 
               SET PS-TOP-ITEM-NOT-IN-TEMP                                      
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
           IF (PV-BOTTOM-ITEM < PV-LAST-ITEM-IN-TEMP)                           
           OR (PV-BOTTOM-ITEM = PV-LAST-ITEM-IN-TEMP)                           
               SET PS-BOTTOM-ITEM-IN-TEMP                                       
                                     TO  TRUE                                   
           ELSE                                                                 
               SET PS-BOTTOM-ITEM-NOT-IN-TEMP                                   
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
           IF PV-LAST-ITEM-IN-TEMP > PV-LAST-ITEM-IN-ARRAY                      
               SET PS-LAST-BLOCK-NOT-IN-ARRAY                                   
                                     TO  TRUE                                   
           ELSE                                                                 
               SET PS-LAST-BLOCK-IN-ARRAY                                       
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE SPOT IN THE ARRAY WHERE THE LAST ITEM FROM    *        
      *    THE DATA BASE WAS ADDED SO WE KNOW WHERE TO ADD THE NEW     *        
      *    ITEMS.  USE THE SAVED UPC KEY TO START THE BROWSE.  H600    *        
      *    MUST BE PERFORMED TO POSITION TO THE FIRST UPC TO PROCESS.  *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G400-READ-ITEMS-INTO-ARRAY.                                              
                                                                                
           COMPUTE PS-ITEM-SUB =                                                
               (DP009-NUMBER-OF-ITEMS-READ -                                    
                DP009-LIST-ITEM-NUMBER(1 1)) + 1.                               
                                                                                
           MOVE PS-ITEM-SUB TO DP009-ITEM-SUB.                                  
                                                                                
           IF  DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                              
               SUBTRACT PC-ITEMS-PER-BLOCK                                      
                                   FROM  DP009-ITEM-SUB                         
               MOVE PC-MAX-BLOCKS-IN-ARRAY                                      
                                     TO  DP009-BLK-SUB                          
           ELSE                                                                 
               MOVE +1               TO  DP009-BLK-SUB                          
           END-IF.                                                              
                                                                                
           MOVE PV-BOTTOM-ITEM       TO  PV-HIGHEST-ITEM-TO-BE-READ.            
                                                                                
           PERFORM L100-OPEN-CURSOR.                                            
           PERFORM H100-READ-ITEMS-FROM-DB.                                     
           IF  PV-ITEMS-READ EQUAL ZERO                                         
               MOVE DP009-ITEM-AT-TOP-OF-PANEL                                  
                                     TO  PV-TOP-ITEM                            
           END-IF.                                                              
                                                                                
           IF  DP009-NO-ITEMS-LEFT-ON-DB                                        
               IF  PV-TOP-ITEM > DP009-NUMBER-OF-ITEMS-READ                     
                   MOVE DP009-NUMBER-OF-ITEMS-READ                              
                                     TO  PV-TOP-ITEM                            
               END-IF                                                           
               IF  DP020-FK-NEXT-PAGE(DP020-SRC-AID)                            
                   MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                   
                   SET DP020-MSG-INFORMATIONAL                                  
                                     TO  TRUE                                   
                   SET PS-INFORMATIONAL                                         
                                     TO  TRUE                                   
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF THE TOP ITEM WE NEED IS IN THE ARRAY, BUT THE BOTTOM     *        
      *    ITEM WE NEED IS IN TEMPORARY STORAGE OR HAS NOT BEEN READ   *        
      *    YET, WRITE OUT THE 1ST BLOCK IN THE ARRAY TO TEMPORARY      *        
      *    STORAGE.  MOVE THE 2ND BLOCK IN THE ARRAY TO THE 1ST BLOCK  *        
      *    AND READ THE BLOCK NEEDED INTO THE 2ND BLOCK OF THE ARRAY.  *        
      *    THE TOP ITEM WILL BE LOCATED IN THE 1ST BLOCK, AND THE      *        
      *    BOTTOM ITEM WILL EITHER BE LOCATED IN THE 2ND BLOCK OR      *        
      *    WILL BE PLACED IN THE 2ND BLOCK.                            *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G500-SCROLL-FORWARD.                                                     
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE DP009-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY) TO                  
                DP009-BLOCK-ENTRIES(DP009-BLK-SUB).                             
                                                                                
           COMPUTE PV-TEMP-BLOCK-NUMBER =                                       
               1 + ((PV-BOTTOM-ITEM - 1) / PC-ITEMS-PER-BLOCK).                 
                                                                                
           MOVE PV-TEMP-BLOCK-NUMBER TO  PV-BLOCK-NUMBER.                       
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF TOP ITEM WE NEED IS NOT IN THE ARRAY, BUT THE BOTTOM     *        
      *    ITEM IS, WRITE BOTH BLOCKS TO TEMPORARY STORAGE.  MOVE      *        
      *    THE 1ST BLOCK IN THE ARRAY TO THE 2ND BLOCK OF THE ARRAY.   *        
      *    READ THE BLOCK THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK*        
      *    OF THE ARRAY.                                               *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G600-SCROLL-BACKWARD.                                                    
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           ADD +1 TO DP009-BLK-SUB.                                             
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           MOVE DP009-BLOCK-ENTRIES(DP009-BLK-SUB) TO                           
                DP009-BLOCK-ENTRIES(PC-MAX-BLOCKS-IN-ARRAY).                    
                                                                                
           COMPUTE PV-TEMP-BLOCK-NUMBER =                                       
               1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).                    
                                                                                
           MOVE PV-TEMP-BLOCK-NUMBER TO PV-BLOCK-NUMBER.                        
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE     *        
      *    ARRAY, BUT THE TOP ITEM IS IN TEMPORARY STORAGE, WRITE BOTH *        
      *    BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ THE BLOCK   *        
      *    THAT CONTAINS THE TOP ITEM INTO THE 1ST BLOCK OF THE ARRAY. *        
      *    IF THERE IS ANOTHER BLOCK IN TEMPORARY STORAGE FOLLOWING    *        
      *    THE ONE JUST READ, READ THAT ONE INTO THE 2ND BLOCK OF THE  *        
      *    ARRAY, OTHERWISE INITIALIZE THE 2ND BLOCK OF THE ARRAY.     *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G700-GET-TOP-ITEMS-BLOCK.                                                
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           COMPUTE PV-TEMP-BLOCK-NUMBER =                                       
               1 + ((PV-TOP-ITEM - 1) / PC-ITEMS-PER-BLOCK).                    
                                                                                
           MOVE PV-TEMP-BLOCK-NUMBER TO  PV-BLOCK-NUMBER.                       
           MOVE +1                   TO  DP009-BLK-SUB.                         
                                                                                
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
           ADD +1                    TO  PV-BLOCK-NUMBER.                       
           ADD +1                    TO  DP009-BLK-SUB.                         
                                                                                
           IF (PV-BLOCK-NUMBER < DP009-HIGHEST-BLOCK-WRITTEN) OR                
              (PV-BLOCK-NUMBER = DP009-HIGHEST-BLOCK-WRITTEN)                   
               PERFORM Z100-READ-TEMP-STORAGE                                   
           ELSE                                                                 
               PERFORM I100-INITIALIZE-BLOCK                                    
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF BOTH THE TOP AND BOTTOM ITEMS WE NEED ARE NOT IN THE     *        
      *    ARRAY AND BOTH ITEMS ARE NOT IN TEMPORARY STORAGE (THE      *        
      *    ITEMS HAVE NOT BEEN READ FROM THE DATA BASE YET), AND THE   *        
      *    LAST ITEM READ FROM THE DATA BASE IS IN TEMPORARY STORAGE,  *        
      *    WRITE BOTH BLOCKS IN THE ARRAY TO TEMPORARY STORAGE.  READ  *        
      *    THE HIGHEST BLOCK IN TEMPORARY STORAGE INTO THE 1ST BLOCK   *        
      *    OF THE ARRAY, AND INITIALIZE THE 2ND BLOCK IN THE ARRAY.    *        
      *    THE 2ND BLOCK WILL BE USED FOR THE NEW ITEMS TO BE READ.    *        
      *    THE 1ST BLOCK WILL NOW CONTAIN THE ITEMS THAT WERE LAST     *        
      *    READ FROM THE DATA BASE.                                    *        
      *                                                                *        
      *    PERFORMED BY F500-BROWSE.                                   *        
      *----------------------------------------------------------------*        
                                                                                
       G800-GET-LAST-BLOCK-IN-TEMP.                                             
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE DP009-HIGHEST-BLOCK-WRITTEN                                     
                                     TO  PV-BLOCK-NUMBER.                       
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM I100-INITIALIZE-BLOCK.                                       
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *        
      *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *        
      *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *        
      *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *        
      *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *        
      *                                                                *        
      *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *        
      *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *        
      *    UPON INITIALIZATION.                                        *        
      *                                                                *        
      *    PERFORMED BY D100-SETUP-INIT-PANEL.                         *        
      *    PERFORMED BY G400-READ-ITEMS-INTO-ARRAY.                    *        
      *----------------------------------------------------------------*        
                                                                                
       H100-READ-ITEMS-FROM-DB.                                                 
                                                                                
           PERFORM                                                              
               UNTIL (DP009-NO-ITEMS-LEFT-ON-DB)                                
      *         OR   (DP009-NUMBER-OF-ITEMS-READ =                              
      *               PV-HIGHEST-ITEM-TO-BE-READ)                               
                                                                                
               PERFORM L200-FETCH                                               
                                                                                
               IF  DP009-ITEMS-LEFT-ON-DB                                       
                   IF  DP009-ITEM-SUB < PC-ITEMS-PER-BLOCK                      
                       ADD +1            TO  DP009-ITEM-SUB                     
                   ELSE                                                         
                       ADD +1            TO  DP009-BLK-SUB                      
                       MOVE +1           TO  DP009-ITEM-SUB                     
                                                                                
                       IF  DP009-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY               
                           MOVE +1       TO  DP009-BLK-SUB                      
                           SET DP009-BLOCK-MODIFIED (1)                         
                                     TO  TRUE                                   
                           PERFORM Z150-WRITE-TEMP-STORAGE                      
                           MOVE DP009-BLOCK-ENTRIES                             
                                     (PC-MAX-BLOCKS-IN-ARRAY)                   
                           TO   DP009-BLOCK-ENTRIES(DP009-BLK-SUB)              
                           ADD +1        TO  DP009-BLK-SUB                      
                           PERFORM I100-INITIALIZE-BLOCK                        
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   ADD 1             TO  DP009-NUMBER-OF-ITEMS-READ             
                                         PV-ITEMS-READ                          
                   MOVE DP009-NUMBER-OF-ITEMS-READ                              
                                     TO  DP009-LIST-ITEM-NUMBER                 
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-MNEMONIC-CODE                                 
                                     TO  DP009-MNEMONIC                         
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                     TO  DP009-TRAN                             
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-DB2-PKG-CDE                                   
                                     TO  DP009-DB2-PKG-CDE                      
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-PANEL-TITLE-NAME                              
                                     TO  DP009-TITLE                            
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-PGM-NAME                                      
                                     TO  DP009-PROGRAM                          
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-UPD-VSAM-IND                                  
                                     TO  DP009-UPD-VSAM-IND                     
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
                                                                                
                   MOVE TMNEMONIC-ACRT-LOC-REQR-IND                             
                                     TO  DP009-ACRT-LOC-REQR-IND                
                                             (DP009-BLK-SUB                     
                                              DP009-ITEM-SUB)                   
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           IF  DP009-NUMBER-OF-ITEMS-READ EQUAL ZERO                            
               SET PS-NO-LIST-ITEMS-FOUND                                       
                                     TO  TRUE                                   
               SET DP020-MSG-FATAL   TO  TRUE                                   
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
           ELSE                                                                 
               SET PS-LIST-ITEMS-FOUND                                          
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE 1                    TO  DP009-BLK-SUB                          
                                         PV-BLOCK-NUMBER.                       
           PERFORM Z100-READ-TEMP-STORAGE.                                      
                                                                                
           IF  DP009-HIGHEST-BLOCK-WRITTEN GREATER THAN 1                       
               MOVE 2                TO  DP009-BLK-SUB                          
                                         PV-BLOCK-NUMBER                        
               PERFORM Z100-READ-TEMP-STORAGE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT THE MAP AND DETERMINE THE CURSOR POSITION.           *        
      *                                                                *        
      *    PERFORMED BY G100-DISPLAY-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       H200-FORMAT-PANEL-BODY.                                                  
                                                                                
           PERFORM I700-FORMAT-FROM-TO-LINE.                                    
           PERFORM I200-FORMAT-LIST-LINES.                                      
           PERFORM I300-FORMAT-NR-SELECTED.                                     
                                                                                
           IF NOT DP020-NEXT-ACT-ERROR                                          
               PERFORM I800-SET-CURSOR-POSITION                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    INITIALIZE A BLOCK IN THE ARRAY.                            *        
      *                                                                *        
      *    PERFORMED BY D100-SETUP-INIT-PANEL.                         *        
      *    PERFORMED BY G700-GET-TOP-ITEMS-BLOCK.                      *        
      *    PERFORMED BY G800-GET-LAST-BLOCK-IN-TEMP.                   *        
      *    PERFORMED BY H100-READ-ITEMS-FROM-DB.                       *        
      *----------------------------------------------------------------*        
                                                                                
       I100-INITIALIZE-BLOCK.                                                   
                                                                                
           INITIALIZE  DP009-BLOCK-ENTRIES (DP009-BLK-SUB).                     
           SET DP009-BLOCK-NOT-MODIFIED(DP009-BLK-SUB) TO TRUE.                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *        
      *                                                                *        
      *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                    *        
      *    PERFORMED BY H200-FORMAT-PANEL-BODY.                        *        
      *----------------------------------------------------------------*        
                                                                                
       I200-FORMAT-LIST-LINES.                                                  
                                                                                
           COMPUTE PV-ENDING-ITEM-ON-PANEL =                                    
               (DP009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.           
                                                                                
           MOVE DP009-ITEM-AT-TOP-OF-PANEL                                      
                                     TO  PV-ITEM-IN-USE.                        
           MOVE +1                   TO  PS-SUB.                                
                                                                                
           PERFORM J100-PROCESS-ITEMS                                           
               UNTIL PV-ITEM-IN-USE > PV-ENDING-ITEM-ON-PANEL.                  
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT THE FIELD WHICH WILL SHOW THE NUMBER OF ITEMS THAT   *        
      *    WERE SELECTED BY THE USER.                                  *        
      *                                                                *        
      *    PERFORMED BY E800-CHECK-FOR-JUMP-BROWSE.                    *        
      *    PERFORMED BY H200-FORMAT-PANEL-BODY.                        *        
      *----------------------------------------------------------------*        
                                                                                
       I300-FORMAT-NR-SELECTED.                                                 
                                                                                
           MOVE DP009-NUMBER-OF-SELECTED-ITEMS                                  
                                     TO  ASELITO.                               
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT THE LINE THAT WILL SHOW THE TOTAL NUMBER OF ITEMS WE *        
      *    HAVE ON OUR LIST AND SHOW THE NUMBERS ASSOCIATED WITH THE   *        
      *    FIRST AND LAST ITEMS ON CURRENT PAGE.  THIS WILL HELP THE   *        
      *    USER DETERMINE HOW FAR THEY ARE INTO THE LIST.              *        
      *                                                                *        
      *    PERFORMED BY H200-FORMAT-PANEL-BODY.                        *        
      *----------------------------------------------------------------*        
                                                                                
       I700-FORMAT-FROM-TO-LINE.                                                
                                                                                
           MOVE DP009-ITEM-AT-TOP-OF-PANEL                                      
                                     TO  ASTRTITO.                              
                                                                                
           MOVE DP015-GREEN          TO  ASTRTITC.                              
           MOVE DP015-UNDERLINE      TO  ASTRTITH.                              
           MOVE DP015-UNP-NUM-BRT-MDT                                           
                                     TO  ASTRTITA.                              
                                                                                
           COMPUTE PV-ENDING-ITEM-ON-PANEL =                                    
               (DP009-ITEM-AT-TOP-OF-PANEL + PC-ITEMS-PER-PANEL) - 1.           
                                                                                
           IF PV-ENDING-ITEM-ON-PANEL < DP009-NUMBER-OF-ITEMS-READ              
               MOVE PV-ENDING-ITEM-ON-PANEL                                     
                                     TO  AENDITO                                
           ELSE                                                                 
               MOVE DP009-NUMBER-OF-ITEMS-READ                                  
                                     TO  AENDITO                                
           END-IF.                                                              
                                                                                
           MOVE DP009-NUMBER-OF-ITEMS-READ                                      
                                     TO  ATOTITO.                               
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    IF NO ITEMS WERE READ FROM THE DATA BASE, POINT THE CURSOR  *        
      *    TO THE COMMAND LINE, OTHERWISE POINT THE CURSOR TO ONE OF   *        
      *    THE SELECTION ENTRIES.                                      *        
      *                                                                *        
      *    PERFORMED BY H200-FORMAT-PANEL-BODY.                        *        
      *----------------------------------------------------------------*        
                                                                                
       I800-SET-CURSOR-POSITION.                                                
                                                                                
           IF DP009-NUMBER-OF-ITEMS-READ = +0                                   
           AND DP020-MSG-NUMBER NOT NUMERIC                                     
               MOVE -1               TO  ANAVCMDL                               
               MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER                       
               SET DP020-MSG-WARNING TO  TRUE                                   
           ELSE                                                                 
                                                                                
               IF (PS-CURSOR-NOT-POSITIONED) OR                                 
                  (PS-WARNING)               OR                                 
                  (PS-INFORMATIONAL)                                            
                   PERFORM J200-FIND-LAST-SELECTION                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PROCESS EACH ITEM IN THE ARRAY AND MOVE IT TO THE MAP.      *        
      *    FILL THE MAP UNTIL WE HAVE REACHED THE MAXIMUM NUMBER OF    *        
      *    MAP ENTRIES.                                                *        
      *                                                                *        
      *    PERFORMED BY I200-FORMAT-LIST-LINES.                        *        
      *----------------------------------------------------------------*        
                                                                                
       J100-PROCESS-ITEMS.                                                      
                                                                                
           IF (PV-ITEM-IN-USE < DP009-NUMBER-OF-ITEMS-READ) OR                  
              (PV-ITEM-IN-USE = DP009-NUMBER-OF-ITEMS-READ)                     
               PERFORM K100-SET-SUBSCRIPTS                                      
               MOVE DP015-UNP-ALP-BRT-MDT                                       
                                     TO  DPKM009-SELECTA(PS-SUB)                
                                                                                
               IF (DP009-SELECT(DP009-BLK-SUB DP009-ITEM-SUB))                  
               OR (DP009-NO-SELECT(DP009-BLK-SUB DP009-ITEM-SUB))               
                   MOVE DP015-UNDERLINE TO DPKM009-SELECTH(PS-SUB)              
                   MOVE DP015-GREEN     TO DPKM009-SELECTC(PS-SUB)              
               ELSE                                                             
                                                                                
                   IF (DP009-BEGIN(DP009-BLK-SUB DP009-ITEM-SUB))               
                   OR (DP009-END(DP009-BLK-SUB DP009-ITEM-SUB))                 
                       MOVE DP015-UNDERLINE   TO DPKM009-SELECTH(PS-SUB)        
                       MOVE DP015-GREEN       TO DPKM009-SELECTC(PS-SUB)        
                       IF (DP020-MSG-NUMBER  =    PC-TSYMSG-00051               
                                               OR PC-TSYMSG-00053               
                          )                                                     
      *                    UNPAIRED RANGE CHARACTER, SET WARNING                
      *                    ATTRIBUTES FOR RANGE CHARACTER:                      
                           MOVE DP015-REVERSE TO DPKM009-SELECTH(PS-SUB)        
                           MOVE DP015-YELLOW  TO DPKM009-SELECTC(PS-SUB)        
                       END-IF                                                   
                                                                                
                       IF (    DP020-MSG-NUMBER  = PC-TSYMSG-00054              
                           AND DP009-BEGIN( DP009-BLK-SUB                       
                                            DP009-ITEM-SUB   )                  
                          )                                                     
      *                    TWO OR MORE START-OF-RANGE CHARACTERS, SET           
      *                    ATTRIBUTES FOR START-OF-RANGE CHARACTERS:            
                           MOVE DP015-REVERSE TO DPKM009-SELECTH(PS-SUB)        
                           MOVE DP015-RED     TO DPKM009-SELECTC(PS-SUB)        
                       END-IF                                                   
                                                                                
                       IF (    DP020-MSG-NUMBER  = PC-TSYMSG-00055              
                           AND DP009-END( DP009-BLK-SUB                         
                                          DP009-ITEM-SUB   )                    
                          )                                                     
      *                    TWO OR MORE END-OF-RANGE CHARACTERS, SET             
      *                    ATTRIBUTES FOR END-OF-RANGE CHARACTERS:              
                           MOVE DP015-REVERSE TO DPKM009-SELECTH(PS-SUB)        
                           MOVE DP015-RED     TO DPKM009-SELECTC(PS-SUB)        
                       END-IF                                                   
                                                                                
                       IF  DP020-MSG-NUMBER  =  PC-TSYMSG-00052                 
      *                    END-OF-RANGE PRECEDES START OF RANGE, SET            
      *                    ATTRIBUTES OF RANGE CHARACTERS:                      
                           MOVE DP015-REVERSE TO DPKM009-SELECTH(PS-SUB)        
                           MOVE DP015-RED     TO DPKM009-SELECTC(PS-SUB)        
                       END-IF                                                   
                   END-IF                                                       
                                                                                
               END-IF                                                           
                                                                                
               MOVE DP009-SELECTED-SW(DP009-BLK-SUB DP009-ITEM-SUB)             
                                     TO  DPKM009-SELECTI(PS-SUB)                
                                                                                
               IF (PS-SEND-MAP-N-DATA-FRSET)                                    
               OR (PS-DIFFERENT-ITEMS-DISPLAYED)                                
                                                                                
                  MOVE DP009-ACTION (DP009-BLK-SUB DP009-ITEM-SUB)              
                                     TO  DPKM009-ACTION       (PS-SUB)          
                  IF  DPKM009-ACTION (PS-SUB) EQUAL PC-ERR                      
                      MOVE DP015-RED TO  DPKM009-SELECTC (PS-SUB)               
                      MOVE DP015-REVERSE                                        
                                     TO  DPKM009-SELECTH (PS-SUB)               
                      MOVE -1        TO  DPKM009-SELECTL (PS-SUB)               
                      SET PS-CURSOR-POSITIONED                                  
                                     TO  TRUE                                   
                      MOVE SPACE     TO  DP009-ACTION                           
                                        (DP009-BLK-SUB DP009-ITEM-SUB)          
                      SET DP009-BLOCK-MODIFIED (DP009-BLK-SUB)                  
                                     TO  TRUE                                   
                  END-IF                                                        
                                                                                
                  MOVE DP009-MNEMONIC (DP009-BLK-SUB DP009-ITEM-SUB)            
                                     TO  DPKM009-MNEMONIC     (PS-SUB)          
                                                                                
                  MOVE DP009-PROGRAM  (DP009-BLK-SUB DP009-ITEM-SUB)            
                                     TO  DPKM009-PROGRAM      (PS-SUB)          
                                                                                
                  MOVE DP009-TRAN (DP009-BLK-SUB DP009-ITEM-SUB)                
                                     TO  DPKM009-TRAN         (PS-SUB)          
                                                                                
                  MOVE DP009-DB2-PKG-CDE                                        
                                  (DP009-BLK-SUB DP009-ITEM-SUB)                
                                     TO  DPKM009-DB2-PKG-CDE  (PS-SUB)          
                                                                                
                  MOVE DP009-TITLE      (DP009-BLK-SUB DP009-ITEM-SUB)          
                                     TO  DPKM009-TITLE        (PS-SUB)          
                                                                                
                  MOVE DP009-UPD-VSAM-IND                                       
                                        (DP009-BLK-SUB DP009-ITEM-SUB)          
                                     TO  DPKM009-UPD-VSAM-IND (PS-SUB)          
                                                                                
                  MOVE DP009-ACRT-LOC-REQR-IND                                  
                                        (DP009-BLK-SUB DP009-ITEM-SUB)          
                                     TO  DPKM009-ACRT-LOC-REQR-IND              
                                                              (PS-SUB)          
                                                                                
               END-IF                                                           
                                                                                
           ELSE                                                                 
                                                                                
               IF (PS-SEND-MAP-N-DATA-FRSET)     OR                             
                  (PS-DIFFERENT-ITEMS-DISPLAYED)                                
                   MOVE SPACES       TO  DPKM009-SELECTI      (PS-SUB)          
                                         DPKM009-ACTION       (PS-SUB)          
                                         DPKM009-MNEMONIC     (PS-SUB)          
                                         DPKM009-PROGRAM      (PS-SUB)          
                                         DPKM009-TRAN         (PS-SUB)          
                                         DPKM009-DB2-PKG-CDE                    
                                                              (PS-SUB)          
                                         DPKM009-TITLE        (PS-SUB)          
                                         DPKM009-UPD-VSAM-IND (PS-SUB)          
                                         DPKM009-ACRT-LOC-REQR-IND              
                                                              (PS-SUB)          
                   MOVE DP015-ASK-DRK-OFF                                       
                                     TO  DPKM009-SELECTA(PS-SUB)                
               END-IF                                                           
                                                                                
           END-IF.                                                              
                                                                                
           ADD +1 TO PV-ITEM-IN-USE.                                            
           ADD +1 TO PS-SUB.                                                    
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FIND THE LAST SELECTION ENTRY IN THE MAP THAT IS NOT        *        
      *    SPACES OR LOW VALUES AND POINT THE CURSOR TO THIS ENTRY.    *        
      *    IF ALL ENTRIES ARE SPACES OR LOW VALUES, POINT THE CURSOR   *        
      *    TO THE 1ST SELECTION ENTRY IN THE MAP.                      *        
      *                                                                *        
      *    PERFORMED BY I800-SET-CURSOR-POSITION.                      *        
      *----------------------------------------------------------------*        
                                                                                
       J200-FIND-LAST-SELECTION.                                                
                                                                                
           MOVE PC-ITEMS-PER-PANEL TO PS-SUB.                                   
                                                                                
           SET PS-NO-SELECTION-FOUND TO TRUE.                                   
                                                                                
           PERFORM                                                              
               UNTIL (PS-SUB = +1)                                              
                  OR (PS-SELECTION-FOUND)                                       
                                                                                
               IF (DPKM009-SELECTI(PS-SUB) NOT EQUAL SPACES                     
               AND DPKM009-SELECTI(PS-SUB) NOT EQUAL LOW-VALUE)                 
                   SET PS-SELECTION-FOUND                                       
                                     TO  TRUE                                   
               ELSE                                                             
                   SUBTRACT +1     FROM  PS-SUB                                 
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           MOVE -1                 TO  DPKM009-SELECTL(PS-SUB).                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *        
      *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*        
      *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *        
      *                                                                *        
      *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *        
      *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *        
      *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *        
      *    US IN THE 2ND BLOCK OF THE ARRAY.                           *        
      *                                                                *        
      *    PERFORMED BY E100-EDIT-SELECTIONS.                          *        
      *    PERFORMED BY F100-EDIT.                                     *        
      *    PERFORMED BY F300-CHECK-SELECTIONS.                         *        
      *    PERFORMED BY J100-PROCESS-ITEMS.                            *        
      *----------------------------------------------------------------*        
                                                                                
       K100-SET-SUBSCRIPTS.                                                     
                                                                                
           COMPUTE PS-ITEM-SUB =                                                
               (PV-ITEM-IN-USE - DP009-LIST-ITEM-NUMBER(1 1)) + 1.              
                                                                                
           MOVE PS-ITEM-SUB TO DP009-ITEM-SUB.                                  
                                                                                
           IF DP009-ITEM-SUB > PC-ITEMS-PER-BLOCK                               
               SUBTRACT PC-ITEMS-PER-BLOCK FROM DP009-ITEM-SUB                  
                                                                                
               MOVE PC-MAX-BLOCKS-IN-ARRAY TO DP009-BLK-SUB                     
           ELSE                                                                 
               MOVE +1                     TO DP009-BLK-SUB                     
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    OPEN THE TMNEMONIC CURSOR.                                  *        
      *                                                                *        
      *    PERFORMED BY D100-SETUP-INITIAL-PANEL                       *        
      *                 G400-READ-ITEMS-INTO-ARRAY.                    *        
      *----------------------------------------------------------------*        
                                                                                
       L100-OPEN-CURSOR.                                                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
                                                                                
               EXEC SQL                                                         
                    OPEN MNEMONIC-CURSOR                                        
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE 'L100-OPEN-CURSOR'                                 
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'OPEN MNEMONIC CURSOR'                             
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE 'L100-OPEN-CURSOR'                                          
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR OPEN CURSOR'                      
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FETCH THE MNEMONIC CURSOR.                                  *        
      *                                                                *        
      *    PERFORMED BY H100                                           *        
      *----------------------------------------------------------------*        
                                                                                
       L200-FETCH.                                                              
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                     
                  OR SQLCODE = 0                                                
                  OR SQLCODE = +100                                             
                                                                                
               EXEC SQL                                                         
                    FETCH MNEMONIC-CURSOR                                       
                    INTO                                                        
                          :TMNEMONIC-MNEMONIC-CODE                              
                        , :TMNEMONIC-TRANS-ID-CODE                              
                        , :TMNEMONIC-DB2-PKG-CDE                                
                        , :TMNEMONIC-PANEL-TITLE-NAME                           
                        , :TMNEMONIC-PGM-NAME                                   
                        , :TMNEMONIC-UPD-VSAM-IND                               
                        , :TMNEMONIC-ACRT-LOC-REQR-IND                          
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = +100                                          
                        CONTINUE                                                
                   WHEN SQLWARN0 NOT EQUAL SPACE                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                        MOVE 'L200-FETCH-CURSOR'                                
                                     TO  DP013-PARAGRAPH                        
                        MOVE 'FETCH MNEMONIC CURSOR'                            
                                     TO  DP013-MESSAGE-TEXT (1)                 
                        MOVE SQLCA   TO  DP013-SQLCA                            
                        SET DP013-DB2-ABEND                                     
                                     TO  TRUE                                   
                        PERFORM DP013-0000-PROCESS-ABEND                        
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                           
               MOVE 'L200-FETCH-CURSOR'                                         
                                     TO  DP013-PARAGRAPH                        
               MOVE 'MAX RETRIES EXCEEDED FOR FETCH CURSOR'                     
                                     TO  DP013-MESSAGE-TEXT (1)                 
               MOVE SQLCA            TO  DP013-SQLCA                            
               SET DP013-DB2-ABEND   TO  TRUE                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           IF  SQLCODE EQUAL +100                                               
               SET DP009-NO-ITEMS-LEFT-ON-DB                                    
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    PREPARE THE TEMPORARY STORAGE QUEUE "B".  MOVE ALL SELECTED *        
      *    ITEMS FROM TEMP STORAGE QUEUE "A" (SCROLL QUEUE) TO THE     *        
      *    SELECTED ITEM LAYOUT FOR QUEUE "B".                         *        
      *                                                                *        
      *    FIRST, PREPARE QUEUE "B" NAME, THEN DELETE THE QUEUE, NEXT  *        
      *    LOAD THE QUEUE "B" RECORD.  WRITE A QUEUE "B" RECORD WHEN   *        
      *    THE MAXIMUM ENTRIES ARE FULL.                               *        
      *                                                                *        
      *    PERFORMED BY D300                                           *        
      *----------------------------------------------------------------*        
                                                                                
       X100-PREPARE-LIST-QUEUE.                                                 
                                                                                
           MOVE +1                   TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
           ADD +1                    TO  DP009-BLK-SUB.                         
           PERFORM Z150-WRITE-TEMP-STORAGE.                                     
                                                                                
           MOVE DP009-TERM-ID        TO  PV-BG-TERM-ID.                         
           MOVE DP009-TASK-NUMBER    TO  PV-BG-TASK-NUMBER.                     
           MOVE 1                    TO  PV-BG-SEQ-NUMBER.                      
                                                                                
           MOVE PV-BG-QUEUE-NAME     TO  DP055-TSQB-QUEUE-NAME                  
                                         DP009-TSQB-QUEUE-NAME.                 
           MOVE LENGTH OF DP002-ARCH-MAINT-TSQB-REC                             
                                     TO  DP055-TSQB-QUEUE-LENGTH.               
           PERFORM X900-DELETE-LIST-QUEUE.                                      
                                                                                
           SET DP009-UPDATE-TSQ      TO  TRUE.                                  
                                                                                
           SET DP002-IDX             TO  1.                                     
           INITIALIZE DP002-ARCH-MAINT-TSQB-REC.                                
                                                                                
           PERFORM X200-PROCESS-TS-QUEUE                                        
                   VARYING PV-ITEM                                              
                   FROM 1 BY 1                                                  
                   UNTIL PV-ITEM GREATER DP009-HIGHEST-BLOCK-WRITTEN.           
                                                                                
           PERFORM X500-WRITE-LIST-QUEUE.                                       
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ A TS QUEUE RECORD, MOVE SELECTED RECORDS TO THE        *        
      *    APPLICATIONS SYSTEM LIST QUEUE RECORD.                      *        
      *                                                                *        
      *    PERFORMED BY X100                                           *        
      *----------------------------------------------------------------*        
                                                                                
       X200-PROCESS-TS-QUEUE.                                                   
                                                                                
           PERFORM X300-READ-TS-QUEUE.                                          
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               PERFORM                                                          
               VARYING PV-TW-IDX                                                
               FROM 1 BY 1                                                      
               UNTIL PV-TW-IDX GREATER THAN PC-ITEMS-PER-BLOCK                  
                 OR  PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX) EQUAL ZERO              
                                                                                
                   IF  PV-TW-SELECT (PV-TW-IDX)                                 
                       MOVE PV-TW-MNEMONIC (PV-TW-IDX)                          
                                     TO  DP002-MNEMONIC                         
                                              (DP002-IDX)                       
                       MOVE PV-TW-LIST-ITEM-NUMBER (PV-TW-IDX)                  
                                     TO  DP002-ITEM                             
                                              (DP002-IDX)                       
                                                                                
                       SET DP002-IDX UP BY 1                                    
                       IF  DP002-IDX GREATER PC-MAX-WR-ITEMS                    
                           PERFORM X500-WRITE-LIST-QUEUE                        
                       END-IF                                                   
                                                                                
                   END-IF                                                       
                                                                                
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*        
      *                                                                *        
      *    PERFORMED BY C500, X200                                     *        
      *----------------------------------------------------------------*        
                                                                                
       X300-READ-TS-QUEUE.                                                      
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE(DP009-TEMP-QUEUE-NAME)                                     
               INTO(PV-TEMP-WORK-AREA)                                          
               LENGTH(PC-BLOCK-LENGTH)                                          
               ITEM(PV-ITEM)                                                    
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE THE STORE SELECTION RECORD (QUEUE "B").               *        
      *                                                                *        
      *    PERFORMED BY X100                                           *        
      *----------------------------------------------------------------*        
                                                                                
       X500-WRITE-LIST-QUEUE.                                                   
                                                                                
           IF  DP002-IDX GREATER THAN 1                                         
               MOVE PC-MAX-WR-ITEMS  TO  DP002-MAX-ITEMS                        
               MOVE DP009-NUMBER-OF-SELECTED-ITEMS                              
                                     TO  DP002-SELECTED-ITEMS                   
                                                                                
               EVALUATE TRUE                                                    
                                                                                
                   WHEN DP020-SRC-AID EQUAL DP016-PF14                          
                       SET DP002-INQUIRY                                        
                                     TO    TRUE                                 
                                                                                
                   WHEN DP020-SRC-AID EQUAL DP016-PF15                          
                        SET DP002-UPDATE                                        
                                     TO  TRUE                                   
                                                                                
                   WHEN DP020-SRC-AID EQUAL DP016-PF16                          
                        SET DP002-DELETE                                        
                                     TO  TRUE                                   
                                                                                
               END-EVALUATE                                                     
                                                                                
               EXEC CICS                                                        
                    WRITEQ TS                                                   
                    QUEUE(DP009-TSQB-QUEUE-NAME)                                
                    FROM(DP002-ARCH-MAINT-TSQB-REC)                             
                    LENGTH(DP055-TSQB-QUEUE-LENGTH)                             
                    ITEM(PV-TSQ-B-ITEM)                                         
                    RESP(PV-RESP)                                               
               END-EXEC                                                         
                                                                                
               IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                            
                   SET PS-DONT-PROCESS                                          
                                     TO  TRUE                                   
               END-IF                                                           
                                                                                
               INITIALIZE DP002-ARCH-MAINT-TSQB-REC                             
               SET DP002-IDX         TO  1                                      
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *        
      *                                                                *        
      *    PERFORMED BY C500, C600                                     *        
      *----------------------------------------------------------------*        
                                                                                
       X550-WRITE-TS-QUEUE.                                                     
                                                                                
           EXEC CICS                                                            
               WRITEQ TS                                                        
               REWRITE                                                          
               QUEUE(DP009-TEMP-QUEUE-NAME)                                     
               FROM(PV-TEMP-WORK-AREA)                                          
               LENGTH(PC-BLOCK-LENGTH)                                          
               ITEM(PV-SAVE-ITEM)                                               
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE LIST QUEUE RECORD.                                 *        
      *                                                                *        
      *    PERFORMED BY C500                                           *        
      *----------------------------------------------------------------*        
                                                                                
       X600-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
                READQ TS                                                        
                QUEUE(DP009-TSQB-QUEUE-NAME)                                    
                INTO(DP002-ARCH-MAINT-TSQB-REC)                                 
                LENGTH(DP055-TSQB-QUEUE-LENGTH)                                 
                ITEM(PV-TSQ-B-ITEM)                                             
                RESP(PV-RESP)                                                   
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DELETE THE STORE SELECTION STORAGE QUEUE ("B").             *        
      *                                                                *        
      *    PERFORMED BY X100                                           *        
      *----------------------------------------------------------------*        
                                                                                
       X900-DELETE-LIST-QUEUE.                                                  
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
               QUEUE(DP009-TSQB-QUEUE-NAME)                                     
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    DELETE THE TEMPORARY STORAGE QUEUE.                         *        
      *                                                                *        
      *    PERFORMED BY C300-PROCESS-PANEL.                            *        
      *----------------------------------------------------------------*        
                                                                                
       Z050-DELETE-TEMP-STORAGE.                                                
                                                                                
           EXEC CICS                                                            
               DELETEQ TS                                                       
               QUEUE(DP009-TEMP-QUEUE-NAME)                                     
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
           IF (PV-RESP NOT = DFHRESP(NORMAL)) AND                               
              (PV-RESP NOT = DFHRESP(QIDERR))                                   
               MOVE PV-RESP TO PM-RESP-23                                       
                                                                                
               SET DP013-LOGIC-ABEND TO TRUE                                    
               SET DP013-NO-ROLLBACK TO TRUE                                    
                                                                                
               MOVE 'Z050-DELETE-TEMP-STORAGE' TO DP013-PARAGRAPH               
               MOVE PM-PROGRAM-MESSAGE-23      TO DP013-MESSAGE-TEXT(1)         
                                                                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           MOVE +0 TO DP009-HIGHEST-BLOCK-WRITTEN.                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *        
      *                                                                *        
      *    PERFORMED BY D400-REFRESH-BLOCK-ARRAY.                      *        
      *    PERFORMED BY E700-DESELECT-ALL-ITEMS.                       *        
      *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                   *        
      *    PERFORMED BY G500-SCROLL-FORWARD.                           *        
      *    PERFORMED BY G600-SCROLL-BACKWARD.                          *        
      *    PERFORMED BY G700-GET-TOP-ITEMS-BLOCK.                      *        
      *    PERFORMED BY G800-GET-LAST-BLOCK-IN-TEMP.                   *        
      *----------------------------------------------------------------*        
                                                                                
       Z100-READ-TEMP-STORAGE.                                                  
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE(DP009-TEMP-QUEUE-NAME)                                     
               INTO(DP009-BLOCK-ENTRIES(DP009-BLK-SUB))                         
               LENGTH(PC-BLOCK-LENGTH)                                          
               ITEM(PV-BLOCK-NUMBER)                                            
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
           IF PV-RESP NOT = DFHRESP(NORMAL)                                     
               MOVE PV-RESP          TO  PM-RESP-24                             
               EXEC CICS                                                        
                    DELETEQ TS                                                  
                    QUEUE(DP009-TEMP-QUEUE-NAME)                                
                    RESP(PV-RESP)                                               
               END-EXEC                                                         
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE 'Z100-READ-TEMP-STORAGE'                                    
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-24                                       
                                     TO  DP013-MESSAGE-TEXT(1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           SET DP009-BLOCK-NOT-MODIFIED(DP009-BLK-SUB) TO TRUE.                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *        
      *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *        
      *                                                                *        
      *    PERFORMED BY E300-NAVIGATE.                                 *        
      *    PERFORMED BY E700-DESELECT-ALL-ITEMS.                       *        
      *    PERFORMED BY F200-MARK-RANGE-AS-SELECTED.                   *        
      *    PERFORMED BY G500-SCROLL-FORWARD.                           *        
      *    PERFORMED BY G600-SCROLL-BACKWARD.                          *        
      *    PERFORMED BY G700-GET-TOP-ITEMS-BLOCK.                      *        
      *    PERFORMED BY G800-GET-LAST-BLOCK-IN-TEMP.                   *        
      *    PERFORMED BY H100-READ-ITEMS-FROM-DB.                       *        
      *----------------------------------------------------------------*        
                                                                                
       Z150-WRITE-TEMP-STORAGE.                                                 
                                                                                
           IF DP009-HIGHEST-BLOCK-WRITTEN = +0                                  
               MOVE EIBTASKN         TO  DP020-SRC-TASK-NUMBER                  
               MOVE EIBTASKN         TO  DP009-TASK-NUMBER                      
               MOVE EIBTRMID         TO  DP009-TERM-ID                          
           END-IF.                                                              
                                                                                
           MOVE DP009-LIST-ITEM-NUMBER(DP009-BLK-SUB 1)                         
                                     TO  PV-LIST-ITEM-NUMBER.                   
           COMPUTE PV-BLOCK-NUMBER =                                            
               1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-BLOCK).            
                                                                                
           IF (PV-BLOCK-NUMBER < DP009-HIGHEST-BLOCK-WRITTEN)                   
           OR (PV-BLOCK-NUMBER = DP009-HIGHEST-BLOCK-WRITTEN)                   
               IF  DP009-BLOCK-MODIFIED(DP009-BLK-SUB)                          
                   EXEC CICS                                                    
                       WRITEQ TS                                                
                       QUEUE(DP009-TEMP-QUEUE-NAME)                             
                       FROM(DP009-BLOCK-ENTRIES(DP009-BLK-SUB))                 
                       LENGTH(PC-BLOCK-LENGTH)                                  
                       ITEM(PV-BLOCK-NUMBER)                                    
                       REWRITE                                                  
                       RESP(PV-RESP)                                            
                   END-EXEC                                                     
                                                                                
                   IF  PV-RESP NOT = DFHRESP(NORMAL)                            
                       MOVE PV-RESP  TO  PM-RESP-9                              
                       EXEC CICS                                                
                           DELETEQ TS                                           
                           QUEUE(DP009-TEMP-QUEUE-NAME)                         
                           RESP(PV-RESP)                                        
                       END-EXEC                                                 
                                                                                
                       SET DP013-LOGIC-ABEND                                    
                                     TO  TRUE                                   
                       SET DP013-NO-ROLLBACK                                    
                                     TO  TRUE                                   
                       MOVE 'Z150-WRITE-TEMP-STORAGE'                           
                                     TO  DP013-PARAGRAPH                        
                       MOVE PM-PROGRAM-MESSAGE-9                                
                                     TO  DP013-MESSAGE-TEXT(1)                  
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
                                                                                
               COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                               
                   DP009-HIGHEST-BLOCK-WRITTEN + 1                              
               IF  PV-BLOCK-NUMBER = PV-HIGHEST-BLOCK-WRITTEN                   
                   EXEC CICS                                                    
                       WRITEQ TS                                                
                       QUEUE(DP009-TEMP-QUEUE-NAME)                             
                       FROM(DP009-BLOCK-ENTRIES(DP009-BLK-SUB))                 
                       LENGTH(PC-BLOCK-LENGTH)                                  
                       ITEM(PV-BLOCK-NUMBER)                                    
                       RESP(PV-RESP)                                            
                   END-EXEC                                                     
                                                                                
                   IF  PV-RESP NOT = DFHRESP(NORMAL)                            
                       MOVE PV-RESP  TO  PM-RESP-10                             
                       EXEC CICS                                                
                           DELETEQ TS                                           
                           QUEUE(DP009-TEMP-QUEUE-NAME)                         
                           RESP(PV-RESP)                                        
                       END-EXEC                                                 
                                                                                
                       SET DP013-LOGIC-ABEND                                    
                                     TO  TRUE                                   
                       SET DP013-NO-ROLLBACK                                    
                                     TO  TRUE                                   
                       MOVE 'Z150-WRITE-TEMP-STORAGE'                           
                                     TO  DP013-PARAGRAPH                        
                       MOVE PM-PROGRAM-MESSAGE-10                               
                                     TO  DP013-MESSAGE-TEXT(1)                  
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
                   ADD +1            TO  DP009-HIGHEST-BLOCK-WRITTEN            
               ELSE                                                             
                   SET DP013-LOGIC-ABEND                                        
                                     TO  TRUE                                   
                   SET DP013-NO-ROLLBACK                                        
                                     TO  TRUE                                   
                   MOVE 'Z150-WRITE-TEMP-STORAGE'                               
                                     TO  DP013-PARAGRAPH                        
                   MOVE PV-BLOCK-NUMBER                                         
                                     TO  PM-BLOCK-NUMBER                        
                   MOVE PM-PROGRAM-MESSAGE-11                                   
                                     TO  DP013-MESSAGE-TEXT(1)                  
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           SET DP009-BLOCK-NOT-MODIFIED(DP009-BLK-SUB) TO TRUE.                 
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FILL IN SCREEN HEADER MODULE                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD022.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    RETURN TO CICS MODULE                                       *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD003.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    FORMAT MESSAGE LINES MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD005.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    GET PANEL MODULE                                            *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD007.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    WRITE PANEL MODULE                                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD008.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    TOGGLE THE FUNCTION KEY LINE MODULE                         *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD009.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    CHECK COMMUNICATION AREA MODULE                             *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD015.                                                        
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE TIME AND DATE MODULE                                 *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD017.                                                        
       EJECT                                                                    
