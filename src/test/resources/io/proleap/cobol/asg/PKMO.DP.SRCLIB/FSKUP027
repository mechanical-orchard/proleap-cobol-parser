       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             FSKUP027.                                        
       AUTHOR.                 BETTY DORST                                      
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            AUG 1998.                                        
      ******************************************************************        
      *   THIS PROGRAM WILL CALL THE DATE ROUTINE AND DECREMENT CURRENT         
      *  BEGINNING OF WEEK DATE (FROM CONTROL CARD) 7 WEEKS TO CREATE           
      *  WEEKLY SALES PURGE DATE.  THIS DATE FILE IS USED FOR THE DBK           
      *  DELETE/REORG OF THE WEEKLY SALES TABLE.                                
      *                                                                         
      * 05/03/99  ROLLIN KRING - REMOVED REFERENCE TO DPG51-SYSTEM-DATE*        
      *       WHICH IS NO LONGER SUPPORTED BY THE CALENDAR ROUTINE.    *        
      *                                           (WR# 10647)          *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SSKSTWKS-PURGE-DATE-FILE                                      
                           ASSIGN TO OUT01.                                     
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SSKSTWKS-PURGE-DATE-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SSKSTWKS-PURGE-DATE-RECORD     PIC X(80).                            
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
           COPY DPWS500.                                                        
      *                                                                         
       01  CR-CONTROL-RECORD.                                                   
           05 CC-ISO-FMT-DATE.                                                  
              10 CC-ISO-FMT-CCYY     PIC X(4).                                  
              10 CC-ISO-FMT-DASH1    PIC X(1).                                  
              10 CC-ISO-FMT-MM       PIC X(2).                                  
              10 CC-ISO-FMT-DASH1    PIC X(1).                                  
              10 CC-ISO-FMT-DD       PIC X(2).                                  
           05 FILLER                 PIC X(70).                                 
      *                                                                         
       01  WS-SSKSTWKS-PURGE-DATE-RECORD.                                       
           05  WS-TICK-MARK1           PIC X(1)      VALUE SPACES.              
           05  WS-PURGE-FSCL-BOW-DTE   PIC X(10).                               
           05  WS-TICK-MARK2           PIC X(1)      VALUE SPACES.              
           05  FILLER                  PIC X(68)     VALUE SPACES.              
                                                                                
       01  ABEND-CODE                  PIC S9(4)           VALUE ZEROS.         
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PURGE-BOW-DTE.                                                
               10  PC-PURGE-BOW-CCYY    PIC X(4).                               
               10  FILLER               PIC X(1).                               
               10  PC-PURGE-BOW-MM      PIC X(2).                               
               10  FILLER               PIC X(1).                               
               10  PC-PURGE-BOW-DD      PIC X(2).                               
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
      ******************************************************************        
      * 0000-MAINLINE                                                           
      ******************************************************************        
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 5000-FIND-PURGE-DATE.                                        
           PERFORM 9999-EOJ-ROUTINE.                                            
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * OPEN OUTPUT FILE AN ACCEPT SYSTEM DATE.                                 
      ******************************************************************        
        1000-INITIALIZATION.                                                    
           OPEN OUTPUT SSKSTWKS-PURGE-DATE-FILE.                                
           ACCEPT CR-CONTROL-RECORD FROM SYSIN.                                 
           PERFORM 2000-EDIT-CONTROL-CARD.                                      
                                                                                
      *                                                                         
      ******************************************************************        
      * CONTROL CARD IS CHECKED FOR VALID DATE.  MUST BE IN CCYY-MM-DD *        
      * FORMAT AND BE A SUNDAY DATE.                                   *        
      ******************************************************************        
       2000-EDIT-CONTROL-CARD.                                                  
           DISPLAY SPACES.                                                      
           DISPLAY 'CC-ISO-FMT-DATE = ' CC-ISO-FMT-DATE.                        
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-454-CALENDAR                                        
               DPG51-DO-NOT-INCR-DECR-DATE                                      
               DPG52-LK-DTE-ISO-FMT      TO TRUE.                               
           MOVE CC-ISO-FMT-DATE          TO DPG52-LK-DATE-INPUT.                
                                                                                
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                                   
              USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                        
                                                                                
           EVALUATE TRUE                                                        
               WHEN DPG54-WARNING-ERROR                                         
               WHEN DPG54-SEVERE-ERROR                                          
               WHEN DPG54-DATE-INVALID                                          
                  DISPLAY '** ABEND **'                                         
                  DISPLAY '** PGM = FSKUP027'                                   
                  DISPLAY '** PARAGRAPH = 2000-EDIT-CONTROL-CARD'               
                  DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'             
                  DISPLAY '** USING CONTROL CARD DATE AS INPUT.'                
                  DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE             
                  DISPLAY '** DPG51 = ' DPG51                                   
                  DISPLAY '** DPG52 = ' DPG52                                   
                  DISPLAY '** DPG53 = ' DPG53                                   
                  DISPLAY '** DPG54 = ' DPG54                                   
                  MOVE +4003 TO ABEND-CODE                                      
                  CALL 'ILBOABN0' USING ABEND-CODE                              
               WHEN OTHER                                                       
                  CONTINUE                                                      
           END-EVALUATE.                                                        
      *                                                                         
           DISPLAY 'DAY OF WEEK RETURNED = ' DPG55-ALPHA-WEEK-DAY.              
           IF DPG55-ALPHA-WEEK-DAY-SUNDAY                                       
             CONTINUE                                                           
           ELSE                                                                 
             DISPLAY '** ABEND **'                                              
             DISPLAY '** PGM = FSKUP027'                                        
             DISPLAY '** PARAGRAPH = 2000-EDIT-CONTROL-CARD'                    
             DISPLAY '** CONTROL CARD DATE MUST BE A SUNDAY DATE.'              
             MOVE +4003 TO ABEND-CODE                                           
           END-IF.                                                              
                                                                                
        5000-FIND-PURGE-DATE.                                                   
      ******************************************************************        
      * DECREMENT BEGINNING OF WEEK SUNDAY DATE (CCYY-MM-DD) BY 49     *        
      * DAYS (7 WEEKS) TO DETERMINE PURGE DATE SINCE WE ONLY WEEK 7    *        
      * WEEKS OF INFORMATION ON TSKSTWKS.                              *        
      ******************************************************************        
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-FISCAL-JAN-DEC-F2                                          
               DPG51-DECREMENT-DATE                                             
               DPG52-LK-DTE-ISO-FMT     TO TRUE.                                
           MOVE 49 TO DPG51-INCR-DECR-DAYS-9.                                   
           MOVE CC-ISO-FMT-DATE        TO DPG52-LK-DATE-INPUT.                  
           CALL  DP500-KOHLS-CALENDAR-ROUTINE                                   
              USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                        
           MOVE DPG55-1ST-WKDY-DB2ISO-FMT TO PC-PURGE-BOW-DTE.                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'PURGE FSCL BOW DTE = ' PC-PURGE-BOW-DTE.                    
                                                                                
           PERFORM 3000-WRITE-PURGE-RECORD.                                     
                                                                                
      ******************************************************************        
      * WRITE TSKSTWKS PURGE DATE TO A FILE                                     
      ******************************************************************        
       3000-WRITE-PURGE-RECORD.                                                 
           INITIALIZE WS-SSKSTWKS-PURGE-DATE-RECORD.                            
           MOVE QUOTE TO WS-TICK-MARK1                                          
                         WS-TICK-MARK2.                                         
           MOVE PC-PURGE-BOW-DTE  TO WS-PURGE-FSCL-BOW-DTE.                     
           WRITE SSKSTWKS-PURGE-DATE-RECORD                                     
             FROM WS-SSKSTWKS-PURGE-DATE-RECORD.                                
                                                                                
      ******************************************************************        
      * CLOSE OUTPUT FILE                                                       
      ******************************************************************        
       9999-EOJ-ROUTINE.                                                        
           CLOSE SSKSTWKS-PURGE-DATE-FILE.                                      
