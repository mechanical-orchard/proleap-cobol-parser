00001  IDENTIFICATION DIVISION.                                         12/27/93
00002  PROGRAM-ID.              INKRP103.                               INKRP103
00003  AUTHOR.                  MARC BLAZICH - GREENBRIER & RUSSEL.        LV021
00004  INSTALLATION.            KOHLS DEPARTMENT STORES.                   CL**1
00005  DATE-WRITTEN.            09/15/93.                                  CL**1
00006  DATE-COMPILED.                                                      CL**1
00007 ******************************************************************   CL*13
00008 *  THIS PROGRAM DISPLAYS ALL STORES THAT HAVEN'T CORRECTED THEIR     CL*13
00009 *  INVENTORY ERRORS BY THEIR INVENTORY CUTOFF DATE.                  CL*13
00010 ******************************************************************   CL*13
      ******************************************************************        
      *  CHANGED: 04/22/98     JEFF HARTIG     WR# 6041                *        
      *                                                                *        
      *  PGM CHANGED FOR Y2K COMPLIANCE. UPDATE PIVOT YEAR TO '80',    *        
      *  UPDATE CONTROL CARD TO INCLUDE CENTURY (MMDDCCYY)             *        
W26600*                                                                *        
W26600******************************************************************        
W26600*  W26600  06/  /2004  PHYSICAL INVENTORY STORE EXPANSION        *        
W26600*                      M JOHNSON                                 *        
W26600*                                                                *        
      ******************************************************************        
W26006*  W26006  02/24/2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.*        
W26006*                      ERIN SEARL                                *        
28545 *                                                                         
28545 * 08/10/2006 B. GRAHAM - STRATAGEM      WR# W28545                        
28545 *            DEPARTMENT LEVEL INVENTORY PROJECT                           
28545 *            JOIN TINVPAR TO TINCNTL                                      
W33304* 08-27-2008  PRAVEEN      HOLDING INVENTORY LEDGER OPEN - HILO           
      ******************************************************************        
00011                                                                      CL*13
00012  ENVIRONMENT DIVISION.                                               CL**1
00013  CONFIGURATION SECTION.                                              CL**1
00014  INPUT-OUTPUT SECTION.                                               CL**1
00015  FILE-CONTROL.                                                       CL**1
00016                                                                      CL**1
00017      SELECT CONTROL-CARD-FILE                                        CL*16
00018          ASSIGN TO INP01.                                            CL*16
00019                                                                      CL*16
00020      SELECT LATE-CORRECTIONS-REPORT                                  CL**1
00021          ASSIGN TO RPT01.                                            CL**1
00022                                                                      CL**1
00023  DATA DIVISION.                                                      CL**1
00024  FILE SECTION.                                                       CL**1
00025                                                                      CL**1
00026  FD  CONTROL-CARD-FILE                                               CL*16
00027      LABEL RECORDS ARE OMITTED                                       CL*16
00028      RECORDING MODE IS F                                             CL*16
00029      BLOCK CONTAINS 0 RECORDS.                                       CL*16
00030  01  CONTROL-CARD-REC        PIC X(080).                             CL*16
00031                                                                      CL*16
00032  FD  LATE-CORRECTIONS-REPORT                                         CL**1
00033      LABEL RECORDS ARE OMITTED                                       CL**1
00034      RECORD CONTAINS 133 CHARACTERS                                  CL**1
00035      RECORDING  MODE IS F                                            CL**1
00036      DATA RECORD IS REPORT-LINE.                                     CL**1
00037  01  REPORT-LINE.                                                    CL**1
00038      05  REPORT-LINE-CC    PIC X(001).                               CL**1
00039      05  REPORT-LINE-DATA  PIC X(132).                               CL**1
00040      05  FILLER REDEFINES  REPORT-LINE-DATA.                         CL**1
00041          10  FILLER        PIC X(131).                               CL**1
00042          10  REPORT-HEADER-FONT-INDICATOR                            CL**1
00043                            PIC X(001).                               CL**1
00044                                                                      CL**1
00045  EJECT                                                               CL**1
00046  WORKING-STORAGE SECTION.                                            CL**1
00047                                                                      CL**1
00048  01  CC-CONTROL-CARD.                                                CL*16
00049      05  CC-DATE.                                                    CL*16
00050          10  CC-MM               PIC X(02).                          CL*16
00051              88  CC-MM-VALID         VALUE '01' THRU '12'.           CL*17
00052          10  CC-DD               PIC X(02).                          CL*16
00053              88  CC-DD-VALID         VALUE '01' THRU '31'.           CL*17
00054          10  CC-YY               PIC X(04).                          CL*19
00055      05  FILLER                  PIC X(72).                          CL*16
00056                                                                      CL*16
00057 *****************************************************************    CL**1
00058 *  SQL ABEND ROUTINE WORKING STORAGE                                 CL**1
00059 *****************************************************************    CL**1
00060                                                                      CL**1
00061      COPY DPWS004.                                                   CL**1
00062                                                                      CL**1
00063 ******************************************************************   CL**1
00064 *  HEADER LINE                                                       CL**1
00065 ******************************************************************   CL**1
00066      COPY DPWS133O.                                                  CL**1
00067                                                                      CL**1
00068  01  PS-SWITCHES.                                                    CL**1
00069      05  END-OF-TINVPAR-SW           PIC X       VALUE 'N'.          CL**1
00070          88  END-OF-TINVPAR                      VALUE 'Y'.          CL**1
00071                                                                      CL**1
00072  01  PC-PROGRAM-CONSTANTS.                                           CL**1
00073      05  PC-LINE-MAX                 PIC S9(03)  VALUE +57.          CL**1
00074      05  PC-ALL-STORES-OK-MSG.                                       CL**2
00075          10  FILLER                  PIC X(48)   VALUE               CL**9
00076          'ALL STORES HAVE COMPLETED THEIR INVENTORY CORREC'.         CL**9
00077          10  FILLER                  PIC X(49)   VALUE               CL**9
00078          'TIONS PRIOR TO THEIR RESPECTIVE TARGET DATES.   '.         CL**9
00079                                                                      CL**9
00080  01  PV-PROGRAM-VARIABLES.                                           CL**2
00081      05  PV-OPER-ATTEMPTED               PIC X(25).                  CL**2
00082      05  PV-PAGE-COUNT                   PIC 9(5)    VALUE 0.        CL**2
00083      05  PV-LINE-COUNT                   PIC 9(2)    VALUE 0.        CL**2
00084      05  PV-PROCESSING-DATE              PIC X(10).                  CL*17
00085      05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.        CL*17
00087          10  PV-PROCESSING-YY            PIC X(04).                  CL*17
00088          10  PV-PROC-SL1                 PIC X(01).                  CL*17
00089          10  PV-PROCESSING-MM            PIC X(02).                  CL*17
00090          10  PV-PROC-SL2                 PIC X(01).                  CL*17
00091          10  PV-PROCESSING-DD            PIC X(02).                  CL*17
00092      05  PV-TARGET-DATE                  PIC X(10).                  CL*17
00093      05  PV-TARGET-DATE-RDF REDEFINES PV-TARGET-DATE.                CL*17
00094          10  FILLER                      PIC X(02).                  CL*17
00095          10  PV-TARGET-YY                PIC X(02).                  CL*17
00096          10  FILLER                      PIC X(01).                  CL*17
00097          10  PV-TARGET-MM                PIC X(02).                  CL*17
00098          10  FILLER                      PIC X(01).                  CL*17
00099          10  PV-TARGET-DD                PIC X(02).                  CL*17
00100      05  PV-TODAYS-DATE.                                             CL**7
00101          10  PV-YY                       PIC 9(02)   VALUE ZERO.     CL**2
00102          10  PV-MM                       PIC 9(02)   VALUE ZERO.     CL**2
00103          10  PV-DD                       PIC 9(02)   VALUE ZERO.     CL**2
00104      05  PV-TODAYS-TIME.                                             CL**2
00105          10  PV-HR                       PIC 9(02)   VALUE ZERO.     CL**2
00106          10  PV-MIN                      PIC 9(02)   VALUE ZERO.     CL**2
00107          10  PV-SEC                      PIC 9(02)   VALUE ZERO.     CL**2
00108          10  FILLER                      PIC 9(02)   VALUE ZERO.     CL**2
W26600     05  PV-LOC-NBR                      PIC 9(04)   VALUE ZEROS.         
00109                                                                      CL**5
00110  01  RH-REPORT-HEADING-ONE.                                          CL**1
00111      05  FILLER                      PIC X(45) VALUE SPACES.         CL**6
00112      05  FILLER                      PIC X(88) VALUE                 CL**6
00113          'STORES LATE IN MAKING INVENTORY CORRECTIONS'.              CL**1
00114                                                                      CL**1
00115  01  RH-REPORT-HEADING-TWO.                                          CL**1
00116      05  FILLER                      PIC X(05)  VALUE '0'.           CL**1
00117      05  FILLER                      PIC X(33)  VALUE 'STORE'.       CL**7
00118      05  FILLER                      PIC X(95)  VALUE                CL**7
00119         'TARGET CORRECTION DATE'.                                    CL*11
00120                                                                      CL**1
00121  01  DL-DETAIL-LINE.                                                 CL**1
00122      05  FILLER                      PIC X(05)  VALUE '0'.           CL**1
00123      05  DL-DATA.                                                    CL**9
W26600         10  DL-STORE-NBR            PIC X(04).                      CL**9
00125          10  FILLER                  PIC X(03)  VALUE ' - '.         CL**9
00126          10  DL-STORE-NAME           PIC X(25).                      CL**9
00127          10  FILLER                  PIC X(01).                      CL*10
00128          10  DL-TARGET-DATE.                                         CL**9
00129              15  DL-TARGET-MM        PIC X(02).                      CL**9
00130              15  FILLER              PIC X(01)  VALUE '-'.           CL**9
00131              15  DL-TARGET-DD        PIC X(02).                      CL**9
00132              15  FILLER              PIC X(01)  VALUE '-'.           CL**9
00133              15  DL-TARGET-YY        PIC X(02).                      CL**9
W26600         10  FILLER                  PIC X(87)  VALUE SPACES.        CL**9
00135                                                                      CL**1
00136  01  RE-END-OF-REPORT-MARKER.                                        CL**1
00137      05  FILLER                      PIC X     VALUE '0'.            CL**2
00138      05  FILLER                      PIC X(51) VALUE ALL '*'.        CL**1
00139      05  FILLER                      PIC X(29) VALUE                 CL**1
00140          '  E N D   O F   R E P O R T  '.                            CL**1
00141      05  FILLER                      PIC X(51) VALUE ALL '*'.        CL**1
00142                                                                      CL**1
00143                                                                      CL**1
00144  01  ABEND-CODE                          PIC S9(04)  VALUE +0        CL**1
00145                                                      COMP SYNC.      CL**1
00146      88  ABEND-4003                                  VALUE +4003.    CL**1
00147  EJECT                                                               CL**1
00148      EXEC SQL                                                        CL**1
00149          INCLUDE SQLCA                                               CL**1
00150      END-EXEC.                                                       CL**1
00151                                                                      CL**1
00152      EXEC SQL                                                        CL**1
W26600         INCLUDE XMT00001                                                 
00154      END-EXEC.                                                       CL**1
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
28545 *                                                                 01380000
00155                                                                      CL**1
00156      EXEC SQL                                                        CL**1
00157          INCLUDE TINVPAR                                             CL**1
00158      END-EXEC.                                                       CL**1
00159                                                                      CL**1
00160      EXEC SQL                                                        CL**1
00161          INCLUDE TINVDTL                                             CL**1
00162      END-EXEC.                                                       CL**1
00163                                                                      CL**1
00164                                                                      CL*13
00165 **   FIND ALL STORES THAT HAVE UN-CORRECTED INVENTORY ERRORS         CL*13
00166 **   AND THEIR INVENTORY CUTOFF DATE HAS ALREADY EXPIRED.            CL*13
00167                                                                      CL*13
00168      EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                    CL**1
00169                                                                      CL*13
W26600         SELECT LOC_NBR                                              CL**1
00171                ,ACTL_INV_DTE                                         CL**7
28545                ,A.CORR_DAY_QTY                                       CL**7
00173 *-->            ** DERIVED CUTOFF DATE                               CL*17
28545                ,DATE(DAYS(ACTL_INV_DTE) + A.CORR_DAY_QTY)            CL**7
00175                                                                      CL*14
00176            FROM TINVPAR A                                            CL**1
28545                ,TINCNTL C                                                 
00177                                                                      CL*13
00178           WHERE ACTL_INV_RECD_DTE < :PV-PROCESSING-DATE              CL*17
00179             AND ACTL_INV_DTE      < :PV-PROCESSING-DATE              CL*17
00180             AND ACTL_UNIT_BK_DTE  = '9999-09-09'                     CL**7
00181 *-->        ** IS DERIVED CUTOFF DATE LESS THAN OR EQUAL TO TODAY?   CL*17
28545             AND ((DAYS(ACTL_INV_DTE) + A.CORR_DAY_QTY) <=            CL*17
00183                   DAYS(:PV-PROCESSING-DATE))                         CL*18
28545             AND A.INV_ID = C.INV_ID                                       
W33304            AND A.LOC_INV_STAT_CDE = 'IN'                                 
28545             AND C.ACTV_IND = 'Y'                                          
00184 *-->        **  FIND IF ANY UNCORRECTED DETAIL EXISTS                CL*13
00185             AND EXISTS                                               CL**1
00186                 (SELECT 1 FROM TINVDTL B                             CL*15
00187                   WHERE B.LOC_NBR         = A.LOC_NBR                CL**1
00188                     AND B.INVALID_REC_IND = 'Y')                     CL**1
W26600       ORDER BY A.LOC_NBR                                            CL**1
00190                                                                      CL**1
00191      END-EXEC.                                                       CL**1
00192                                                                      CL**1
00193  PROCEDURE DIVISION.                                                 CL**1
00194                                                                      CL**1
00195  A100-MAIN-MODULE.                                                   CL**1
00196 ******************************************************************   CL**1
00197 *  MAIN PROCESSING.                                                  CL*21
00198 ******************************************************************   CL**1
00199                                                                      CL**1
00200      PERFORM B100-HOUSEKEEPING.                                      CL**1
00201                                                                      CL**1
00202      PERFORM Z100-FETCH-TINVPAR.                                     CL**3
00203                                                                      CL**1
00204      IF END-OF-TINVPAR                                               CL**1
00205          MOVE PC-ALL-STORES-OK-MSG TO DL-DATA                        CL**9
00206          MOVE DL-DETAIL-LINE       TO REPORT-LINE                    CL**2
00207          PERFORM Z800-WRITE-REPORT-LINE                              CL**2
00208      ELSE                                                            CL**1
00209          PERFORM B200-PROCESS-TINVPAR                                CL**1
00210              UNTIL END-OF-TINVPAR                                    CL**1
00211      END-IF.                                                         CL**1
00212                                                                      CL**1
00213      PERFORM B300-EOJ.                                               CL**1
00214                                                                      CL**1
00215      STOP RUN.                                                       CL**1
00216 *                                                                    CL**1
00217  B100-HOUSEKEEPING.                                                  CL**1
00218 ******************************************************************   CL**1
00219 * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET         CL**1
00220 * THE REPORT NAME AND NUMBER.                                        CL**1
00221 ******************************************************************   CL**1
00222                                                                      CL**1
00223      ACCEPT PV-TODAYS-DATE FROM DATE.                                CL**1
00224      ACCEPT PV-TODAYS-TIME FROM TIME.                                CL**1
00225      MOVE PV-MM           TO DP133O-RUN-MONTH.                       CL**1
00226      MOVE PV-DD           TO DP133O-RUN-DAY.                         CL**1
00227      MOVE PV-YY           TO DP133O-RUN-YEAR.                        CL**1
00228      MOVE PV-HR           TO DP133O-RUN-HOUR.                        CL**1
00229      MOVE PV-MIN          TO DP133O-RUN-MINUTE.                      CL**1
00230      MOVE 'INKRP103'      TO DP133O-PROGRAM-NAME.                    CL**1
00231                                                                      CL**1
00232      OPEN  INPUT CONTROL-CARD-FILE                                   CL*16
00233           OUTPUT LATE-CORRECTIONS-REPORT.                            CL*16
00234                                                                      CL*16
00235      READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                     CL*16
00236         AT END                                                       CL*16
00237            DISPLAY 'INKRP103 - NO CONTROL CARD PRESENT'              CL*16
00238            MOVE +4019                  TO ABEND-CODE                 CL*17
00239            CALL 'ILBOABN0'  USING ABEND-CODE.                        CL*17
00240                                                                      CL*17
00241      IF CC-MM-VALID   AND                                            CL*17
00242         CC-DD-VALID   AND                                            CL*17
00243         CC-YY NUMERIC                                                CL*17
00244          CONTINUE                                                    CL*17
00245      ELSE                                                            CL*17
00246          DISPLAY '** INKRP103 ABEND **'                              CL*17
00247          DISPLAY '** CARDIN DATE INVALID ' CC-DATE                   CL*17
00248          MOVE +4019                  TO ABEND-CODE                   CL*17
00249          CALL 'ILBOABN0' USING ABEND-CODE                            CL*17
00250      END-IF                                                          CL*17
00251                                                                      CL*17
00252      MOVE CC-MM    TO PV-PROCESSING-MM                               CL*17
00253      MOVE CC-DD    TO PV-PROCESSING-DD                               CL*17
00254      MOVE CC-YY    TO PV-PROCESSING-YY                               CL*17
00255                                                                      CL*19
00261      MOVE '-'      TO PV-PROC-SL1                                    CL*17
00262                       PV-PROC-SL2.                                   CL*17
00263                                                                      CL*17
00264      DISPLAY 'INKRP103 - PROCESSING DATE = ' PV-PROCESSING-DATE.     CL*19
00265                                                                      CL*19
00266      EXEC SQL                                                        CL**1
00267          OPEN TINVPAR_CURSOR                                         CL**1
00268      END-EXEC.                                                       CL**1
00269                                                                      CL**1
00270      IF SQLCODE NOT = +0                                             CL**1
00271          MOVE 'OPEN TINVPAR_CURSOR' TO PV-OPER-ATTEMPTED             CL**1
00272          PERFORM Z900-SQL-ABEND                                      CL**1
00273      END-IF.                                                         CL**1
00274                                                                      CL**1
00275      PERFORM Z700-PRINT-HEADINGS.                                    CL**4
00276                                                                      CL**4
00277  B200-PROCESS-TINVPAR.                                               CL**1
00278                                                                      CL**1
00279      IF PV-LINE-COUNT > PC-LINE-MAX                                  CL**1
00280          PERFORM Z700-PRINT-HEADINGS                                 CL**2
00281      END-IF.                                                         CL**1
00282                                                                      CL**1
00283      PERFORM Z200-SELECT-XMTLOC.                                     CL**1
00284                                                                      CL**1
00285      INITIALIZE DL-DETAIL-LINE.                                      CL**1
00286                                                                      CL**1
W26600     MOVE INVPAR-LOC-NBR       TO PV-LOC-NBR                         CL**7
W26600     MOVE PV-LOC-NBR           TO DL-STORE-NBR                       CL**7
00288      MOVE XMT00001-LOC-NM      TO DL-STORE-NAME                      CL**7
00289                                                                      CL**7
00290      MOVE PV-TARGET-MM         TO DL-TARGET-MM                       CL**7
00291      MOVE PV-TARGET-DD         TO DL-TARGET-DD                       CL*12
00292      MOVE PV-TARGET-YY         TO DL-TARGET-YY                       CL**7
00293                                                                      CL**1
00294      MOVE DL-DETAIL-LINE         TO REPORT-LINE.                     CL**2
00295                                                                      CL**1
00296      PERFORM Z800-WRITE-REPORT-LINE.                                 CL**2
00297                                                                      CL**1
00298      PERFORM Z100-FETCH-TINVPAR.                                     CL**1
00299 *                                                                    CL**1
00300  B300-EOJ.                                                           CL**1
00301 ******************************************************************   CL**1
00302 *    CLOSE THE FILES.                                                CL**1
00303 ******************************************************************   CL**1
00304                                                                      CL**1
00305      IF PV-LINE-COUNT > PC-LINE-MAX                                  CL**1
00306          PERFORM Z700-PRINT-HEADINGS                                 CL**2
00307      END-IF.                                                         CL**1
00308                                                                      CL**1
00309      MOVE RE-END-OF-REPORT-MARKER TO REPORT-LINE.                    CL**2
00310      PERFORM Z800-WRITE-REPORT-LINE.                                 CL**4
00311                                                                      CL**4
00312      CLOSE   LATE-CORRECTIONS-REPORT                                 CL*16
00313              CONTROL-CARD-FILE.                                      CL*19
00314                                                                      CL**1
00315  Z100-FETCH-TINVPAR.                                                 CL**1
00316 *                                                                    CL**1
00317      EXEC SQL FETCH TINVPAR_CURSOR                                   CL**1
W26600        INTO :INVPAR-LOC-NBR                                         CL**1
00319             ,:INVPAR-ACTL-INV-DTE                                    CL**7
00320             ,:INVPAR-CORR-DAY-QTY                                    CL**7
00321             ,:PV-TARGET-DATE                                         CL**7
00322      END-EXEC.                                                       CL**1
00323                                                                      CL**1
00324      EVALUATE SQLCODE                                                CL**1
00325          WHEN +0                                                     CL**1
00326              CONTINUE                                                CL**1
00327          WHEN +100                                                   CL**1
00328              SET END-OF-TINVPAR TO TRUE                              CL**1
00329          WHEN OTHER                                                  CL**1
00330              MOVE 'FETCH TINVPAR_CURSOR'                             CL**1
00331                                 TO  PV-OPER-ATTEMPTED                CL**1
00332              PERFORM Z900-SQL-ABEND                                  CL**1
00333      END-EVALUATE.                                                   CL**1
00334                                                                      CL**1
00335  Z200-SELECT-XMTLOC.                                                 CL**1
00336                                                                      CL**1
00337      EXEC SQL                                                        CL**1
W26600       SELECT   LOC_NM                                               CL**1
W26600         INTO  :XMT00001-LOC-NM                                      CL**1
W26600         FROM   XMT_LOC                                              CL**1
W26600        WHERE   LOC_NBR = :INVPAR-LOC-NBR                            CL**1
00342      END-EXEC.                                                       CL**1
00343                                                                      CL**1
00344      EVALUATE SQLCODE                                                CL**1
00345          WHEN +0                                                     CL**1
00346              CONTINUE                                                CL**1
00347          WHEN OTHER                                                  CL**1
W26600             MOVE 'SELECT XMT_LOC '                                  CL**1
00349                                 TO  PV-OPER-ATTEMPTED                CL**1
00350              PERFORM Z900-SQL-ABEND                                  CL**1
00351      END-EVALUATE.                                                   CL**1
00352                                                                      CL**1
00353 *                                                                    CL**1
00354  Z700-PRINT-HEADINGS.                                                CL**2
00355 ******************************************************************   CL**1
00356 *    PRINT THE REPORT HEADINGS                                       CL**1
00357 ******************************************************************   CL**1
00358                                                                      CL**1
00359      ADD 1 TO PV-PAGE-COUNT.                                         CL**2
00360      MOVE PV-PAGE-COUNT TO DP133O-PAGE-NUMBER.                       CL**3
00361      MOVE 1             TO DP133O-REPORT-NUMBER.                     CL**1
00362      MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.             CL**1
00363      MOVE '1'           TO REPORT-LINE-CC                            CL**1
00364      MOVE ' '           TO REPORT-HEADER-FONT-INDICATOR              CL**1
00365                                                                      CL**1
00366      PERFORM Z800-WRITE-REPORT-LINE.                                 CL**2
00367                                                                      CL**1
00368      MOVE RH-REPORT-HEADING-ONE TO REPORT-LINE.                      CL**1
00369      PERFORM Z800-WRITE-REPORT-LINE.                                 CL**2
00370                                                                      CL**1
00371      MOVE RH-REPORT-HEADING-TWO TO REPORT-LINE.                      CL**2
00372      PERFORM Z800-WRITE-REPORT-LINE.                                 CL**2
00373                                                                      CL**2
00374      MOVE 4 TO PV-LINE-COUNT.                                        CL**2
00375                                                                      CL**2
00376  Z800-WRITE-REPORT-LINE.                                             CL**2
00377                                                                      CL**2
00378      WRITE REPORT-LINE.                                              CL**2
00379      EVALUATE  REPORT-LINE-CC                                        CL**2
00380          WHEN ' '                                                    CL**2
00381             ADD 1 TO PV-LINE-COUNT                                   CL**2
00382          WHEN '0'                                                    CL**2
00383             ADD 2 TO PV-LINE-COUNT                                   CL**2
00384          WHEN '-'                                                    CL**2
00385             ADD 3 TO PV-LINE-COUNT                                   CL**2
00386      END-EVALUATE.                                                   CL**2
00387                                                                      CL**2
00388      EJECT                                                           CL**1
00389  Z900-SQL-ABEND.                                                     CL**1
00390                                                                      CL**1
00391      MOVE +4013                 TO ABEND-CODE.                       CL**1
00392      DISPLAY '**  ABEND     **'.                                     CL**1
00393      DISPLAY '**  PROGRAM   **  =  INKRP103'.                        CL**2
00394      DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED               CL**1
00395                                                                      CL**1
00396 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE            CL**1
00397 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.           CL**1
00398                                                                      CL**1
00399      COPY DPPD004.                                                   CL**1
00400                                                                      CL**1
00401      CALL 'ILBOABN0'  USING ABEND-CODE.                              CL**1
