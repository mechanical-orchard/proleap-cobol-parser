       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT176.                                       
       AUTHOR.                  JULIE SEIDLER.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            11/13/98.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *          REFORMAT CENTRAL STOCK INVENTORY FILE                          
      *                                                                *        
      *  REFORMAT THE CENTRAL STOCK INVENTORY FILE (CERD010) INTO               
      *  THE PHYSICAL INVENTORY FILE FORMAT  (INWS018).                *        
      *                                                                *        
      ******************************************************************        
      ******************************************************************        
      *  CHANGED:   /  /       J. SEIDLER      WR# XXXX                *        
      *                                                                *        
      *  IN JAN 99, GRANTOSA STORE (056) WILL BASE ITS PHY COUNT ON    *        
      *  TRANSFER INFORMATION. WE WILL RECEIVE AND PROCESS THIS TRANSFER        
      *  FILE LIKE THE CENTRAL STOCK DATA FILE.                                 
      *                                                                *        
      *  CHANGED: 02/02/99     M. KLAMIK       WR# XXXX                *        
      *     UPDATE INWS018 COPYBOOK WITH NEW TIMESTAMP FIELDS.         *        
      *                                                                *        
      *  CHANGED: 09/01/99     P. KEBER        WR# 20362               *        
      *     CHANGE PROGRAM TO CREATE A NEW OUTPUT FILE HEADER RECORD   *        
      *     FOR EACH STORE BEING PROCESSED.  THE HARDCODED STORE       *        
      *     NUMBER CHECK IS REPLACED BY SELECTING TYPE CS OR RV STORES *        
      *     WITH A FINANCIAL BOOK DATE OF 9999-09-09 ON TINVPAR.  A    *        
      *     NEW ERROR REPORT WILL LIST DROPPED INVALID TRANSACTIONS.   *        
      *                                                                *        
21015 *  CHANGED: 01/17/2001   R.JANSSEN       WR# 21015               *        
  |   *     PROBLEM - STORE 812 DID NOT CREATE AN EXCEPTION REPORT,    *        
  |   *               INKRP178, WHY????*                                        
  |   *     SOLUTION - ADJUST THE DEFAULT AREA NUMBER TO REFLECT       *        
21015 *               100,000 INSTEAD OF 10,000.                       *        
      *                                                                *        
22000 *  CHANGED: 04/06/2001   S.JACKSON       WR# 22000               *        
22000 *     ADD CONDITION TO Z200-READ-XMTLOC-TINVPAR TO ALLOW         *        
22000 *     PROCESSING OF STORE 873 (STORE TYPE 'DS'). PGM CAN THEN    *        
22000 *     HANDLE E-COMMERCE DATA THRU 'CENTRAL STOCK' FILE FORMAT.   *        
      *                                                                *        
23511 *  CHANGED: 06/16/2002   R.JANSSEN       WR# 23511               *        
23511 *     AUTOMATE THE CS/RTV PROCESS.                                        
W21732*                                                                *        
W21732*  CHANGED: 11/14/2002  J.S.NAVIS RAJA,IGSI       WR# 21732      *        
W21732*  DEPT/CLASS/SEQ ARE REPLACED WITH 8 DIGIT NON-INTELLIGENT SKU. *        
W21732*                                                                *        
25248 *  CHANGED: 10/06/2003  ROD JANSSEN               WR# 25248      *        
25248 *     UPC CAPABILITIES                                           *        
25248 *                                                                *        
W26006*  CHANGED: 02/24/2005  ERIN SEARL                WR# 26006      *        
W26006*     REMOVE REFERENCES TO OBSOLETE STORE TABLE.                 *        
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
W33304*  CHANGED: 10/23/2008  SATHISH/INFOSYS           WR# 33304      *        
W33304*     HOLDING INVENTORY LEDGER OPEN (HILO).                      *        
W26006******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CENTRAL-STOCK-INV                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PHYSICAL-INV-CE                                               
               ASSIGN TO OUT01.                                                 
                                                                                
23511      SELECT STORE-CONTROL-CARD                                            
23511          ASSIGN TO OUT02.                                                 
23511                                                                           
           SELECT ERROR-REPORT-FILE                                             
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CENTRAL-STOCK-INV                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 175 CHARACTERS                                       
           DATA RECORD IS CENTRAL-STOCK-INV-REC.                                
       01  CENTRAL-STOCK-INV-REC PIC X(175).                                    
                                                                                
       FD  PHYSICAL-INV-CE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 58 CHARACTERS                                        
           DATA RECORD IS PHYSICAL-INV-CE-REC.                                  
       01  PHYSICAL-INV-CE-REC    PIC X(58).                                    
                                                                                
23511  FD  STORE-CONTROL-CARD                                                   
  |        RECORDING MODE IS F                                                  
  |        LABEL RECORDS ARE OMITTED                                            
  |        RECORD CONTAINS 80 CHARACTERS                                        
  |        DATA RECORD IS STORE-CONTROL-CARD-REC.                               
  |    01  STORE-CONTROL-CARD-REC PIC X(80).                                    
23511                                                                           
       FD  ERROR-REPORT-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ERROR-REPORT-LINE       PIC X(132).                                  
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
23511  01  CC-CONTROL-CARD.                                                     
25248      05  CC-STORE-NBR            PIC X(04)      VALUE ZERO.               
25248      05  FILLER                  PIC X(76)      VALUE SPACES.             
23511                                                                           
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ERROR-COUNT          PIC S9(09)      VALUE +0  COMP.          
           05  PV-INPUT-RECORD-NBR     PIC S9(09)      VALUE +0  COMP.          
           05  PV-DB2-NUM              PIC S9(04)      VALUE +0  COMP.          
           05  PV-SQL-SUB              PIC S9(04)      VALUE +0  COMP.          
21015      05  PV-DEFAULT-AREA         PIC 9(6)        VALUE 100000.            
21015 **** 05  PV-DEFAULT-AREA         PIC 9(6)        VALUE 010000.            
           05  PV-CALC-QTY             PIC 9(9)        VALUE ZERO.              
           05  PV-STORE-HOLD           PIC X(4)        VALUE SPACE.             
           05  PV-PROGRAM-NAME         PIC X(8)        VALUE 'INKUT176'.        
           05  PV-PARAGRAPH-NAME       PIC X(25)       VALUE SPACE.             
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)       VALUE SPACES.            
           05  PV-ABEND-MESSAGE        PIC X(50)       VALUE SPACE.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES          PIC S9(04)      VALUE +3  COMP.          
SR3403     05  PC-MAX-INV-PRICE        PIC 9(5)V99     VALUE 99999.99.          
W21732     05  PC-ZERO-SKU             PIC X(08)     VALUE '00000000'.          
           05  PC-MAX-LINES            PIC 9(04)       VALUE 0025.              
           05  PC-OPERCO               PIC X(02)       VALUE '03'.              
           05  PC-FIN-BK-DATE          PIC X(10)       VALUE                    
                   '9999-09-09'.                                                
21015      05  PC-START-AREA           PIC 9(6)        VALUE 100000.            
21015 **** 05  PC-START-AREA           PIC 9(6)        VALUE 010000.            
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-LINE-COUNT           PIC S9(03)      VALUE +099.              
           05  PC-LINE-MAX             PIC S9(03)      VALUE +60.               
           05  PC-ADVANCE              PIC S9(03)      VALUE ZERO.              
           05  PC-PAGE                 PIC S9(05)      VALUE ZERO.              
                                                                                
       01  PS-SWITCHES.                                                         
           05  END-OF-CEN-STK-INV-SW   PIC X           VALUE 'N'.               
               88  END-OF-CEN-STK                      VALUE 'Y'.               
           05  PS-PROCESS-STORE-SW     PIC X           VALUE ' '.               
               88  PS-PROCESS-STORE                    VALUE 'Y'.               
               88  PS-DO-NOT-PROCESS-STORE             VALUE 'N'.               
                                                                                
       01  ABEND-CODE                  PIC S9(04)      VALUE +0                 
                                                       COMP SYNC.               
                                                                                
      *****************************************************************         
      *  CENTRAL STOCK INVENTORY DATA                                           
      *****************************************************************         
           COPY CERD010.                                                        
                                                                                
      *****************************************************************         
      *  PHYSICAL INVENTORY DATA                                                
      *****************************************************************         
           COPY INWS018.                                                        
                                                                                
      *****************************************************************         
      *  STANDARD KOHLS HEADER (132 COLUMNS)                                    
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
       01  RD-REPORT-TITLE-LINE.                                        MLKRP217
           05  FILLER                  PIC  X(37)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(57)    VALUE              MLKRP217
           'REFORMAT CE TRANSFER / RTV SHEET INVENTORIES ERROR REPORT'.         
           05  FILLER                  PIC  X(38)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE1.                                         MLKRP217
           05  FILLER                  PIC  X(05)    VALUE 'STORE'.     MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(07)    VALUE 'SKU NBR'.   MLKRP217
           05  FILLER                  PIC  X(06)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(08)    VALUE 'RECORD #'.  MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE 'ERROR'.     MLKRP217
           05  FILLER                  PIC  X(95)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-HDR-LINE2.                                         MLKRP217
           05  FILLER                  PIC  X(05)    VALUE '-----'.     MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(10)    VALUE '----------'.MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(08)    VALUE '--------'.  MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(05)    VALUE '-----'.     MLKRP217
           05  FILLER                  PIC  X(45)    VALUE              MLKRP217
               '---------------------------------------------'.                 
           05  FILLER                  PIC  X(15)    VALUE              MLKRP217
               '---------------'.                                               
           05  FILLER                  PIC  X(40)    VALUE SPACES.      MLKRP217
                                                                                
       01  RD-REPORT-ERROR-LINE.                                        MLKRP217
           05  FILLER                  PIC  X(01)    VALUE SPACES.      MLKRP217
           05  RD-RPT-ERROR-STORE      PIC  X(03)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(04)    VALUE SPACES.      MLKRP217
W21732     05  RD-RPT-ERROR-SKU        PIC  X(08)    VALUE SPACES.              
W21732     05  FILLER                  PIC  X(04)    VALUE SPACES.              
           05  RD-RPT-ERROR-REC-NBR    PIC  Z(09)    VALUE ZERO.        MLKRP217
           05  FILLER                  PIC  X(03)    VALUE SPACES.      MLKRP217
           05  RD-RPT-ERROR-MESSAGE    PIC  X(50)    VALUE SPACES.      MLKRP217
           05  FILLER                  PIC  X(50)    VALUE SPACES.      MLKRP217
                                                                        MLKRP217
       01  RD-REPORT-ERRORS.                                            MLKRP217
           05  RD-RPT-UNIT-PRICE-ERR.                                   MLKRP217
               10  FILLER              PIC  X(15)    VALUE              MLKRP217
                   'UNIT PRICE OF $'.                                           
SR3403         10  RD-RPT-UNIT-PRICE   PIC  Z(5)9.99- VALUE ZERO.       MLKRP217
               10  FILLER              PIC  X(25)    VALUE              MLKRP217
SR3403             ' IS GREATER THAN 7 DIGITS'.                                 
           05  RD-RPT-ZERO-SKU-ERR.                                     MLKRP217
W21732         10  FILLER              PIC  X(11)    VALUE              MLKRP217
W21732             'SKU IS ZERO'.                                               
                                                                        MLKRP217
       01  RD-REPORT-NO-ERRORS.                                         MLKRP217
           05  FILLER                  PIC  X(35)    VALUE              MLKRP217
               '*** NO ERRORS FOUND IN THIS RUN ***'.                   MLKRP217
                                                                                
      *****************************************************************         
      * ERROR MESSAGE AREA FOR DB2                                              
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
25248 *****************************************************************         
  |   * XMT_LOC DCLGEN                                                          
  |   *****************************************************************         
  |        EXEC SQL                                                             
  |           INCLUDE XMT00001                                                  
  |        END-EXEC.                                                            
25248                                                                           
      *****************************************************************         
      * TINVPAR DCLGEN                                                          
      *****************************************************************         
           EXEC SQL                                                             
              INCLUDE TINVPAR                                                   
           END-EXEC.                                                            
                                                                                
P28545*****************************************************************         
P28545* TINCNTL DCLGEN                                                          
P28545*****************************************************************         
P28545     EXEC SQL                                                             
P28545        INCLUDE TINCNTL                                                   
P28545     END-EXEC.                                                            
P28545                                                                          
      *****************************************************************         
      * SQL COMM AREA DCLGEN                                                    
      *****************************************************************         
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN RPOCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PROCESS-INPUT THRU B200-EXIT                            
               UNTIL END-OF-CEN-STK.                                            
                                                                                
           PERFORM B300-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      *                                                                         
       B100-HOUSEKEEPING.                                                       
                                                                                
           OPEN INPUT CENTRAL-STOCK-INV                                         
                OUTPUT PHYSICAL-INV-CE                                          
23511                  STORE-CONTROL-CARD                                       
                       ERROR-REPORT-FILE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE (3:2) TO DP132O-RUN-YEAR.         MLKRP800
           MOVE FUNCTION CURRENT-DATE (5:2) TO DP132O-RUN-MONTH.        MLKRP800
           MOVE FUNCTION CURRENT-DATE (7:2) TO DP132O-RUN-DAY.          MLKRP800
           MOVE FUNCTION CURRENT-DATE (9:2) TO DP132O-RUN-HOUR.         MLKRP800
           MOVE FUNCTION CURRENT-DATE (11:2) TO DP132O-RUN-MINUTE.      MLKRP800
           MOVE 'INKUT176' TO DP132O-PROGRAM-NAME.                              
           MOVE  1         TO DP132O-REPORT-NUMBER.                             
                                                                                
           PERFORM Z100-READ-CEN-STK.                                           
                                                                                
           IF END-OF-CEN-STK                                                    
               MOVE 'INPUT CENTRAL STOCK INVENTORY FILE IS EMPTY'               
                   TO  PV-ABEND-MESSAGE                                         
               MOVE 'B100-HOUSEKEEPING' TO PV-PARAGRAPH-NAME                    
               MOVE +4003 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
       B200-PROCESS-INPUT.                                                      
                                                                                
           IF CE010-CNTRL-STK-ID NOT = PV-STORE-HOLD                            
               MOVE CE010-CNTRL-STK-ID TO PV-STORE-HOLD                         
25248          MOVE CE010-CNTRL-STK-ID      TO XMT00001-LOC-NBR                 
               PERFORM Z200-READ-XMTLOC-TINVPAR                                 
               IF PS-PROCESS-STORE                                              
                   PERFORM Z903-WRITE-HEADER-REC                                
23511              PERFORM Z910-WRITE-STR-SLCT-CCARD                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF PS-DO-NOT-PROCESS-STORE                                           
               GO TO B200-CONTINUE.                                             
                                                                                
           ADD +1 TO PV-INPUT-RECORD-NBR.                                       
                                                                                
           IF CE010-CUR-UNIT-RTL-AMT > PC-MAX-INV-PRICE                         
               MOVE CE010-CUR-UNIT-RTL-AMT TO RD-RPT-UNIT-PRICE                 
               MOVE RD-RPT-UNIT-PRICE-ERR  TO RD-RPT-ERROR-MESSAGE              
               ADD 1 TO PV-ERROR-COUNT                                          
               PERFORM C100-INVALID-DATA                                        
               GO TO B200-CONTINUE.                                             
                                                                                
W21732     IF CE010-SKU-NBR EQUAL PC-ZERO-SKU                                   
               MOVE RD-RPT-ZERO-SKU-ERR  TO RD-RPT-ERROR-MESSAGE                
               ADD 1 TO PV-ERROR-COUNT                                          
               PERFORM C100-INVALID-DATA                                        
               GO TO B200-CONTINUE.                                             
                                                                                
25248      MOVE CE010-CNTRL-STK-ID      TO IN018-STORE-NUMBER.                  
25248 **** MOVE CE010-CNTRL-STK-ID(2:3) TO IN018-STORE-NUMBER.                  
                                                                                
W21732     MOVE CE010-SKU-NBR          TO IN018-SKU.                            
                                                                                
           MOVE CE010-CUR-UNIT-RTL-AMT TO IN018-PRICE.                          
           MOVE CE010-AVAILABLE        TO IN018-QUANTITY.                       
                                                                                
           MOVE ZERO                   TO IN018-UPC-CODE.                       
           MOVE SPACE                  TO IN018-INVALID-INDICATOR.              
           MOVE SPACE                  TO IN018-BLANK-SHEET-INDICATOR.          
           MOVE ZERO                   TO IN018-SCAN-DTE.                       
           MOVE ZERO                   TO IN018-SCAN-TIMESTAMP.                 
                                                                                
           IF CE010-CUR-UNIT-RTL-AMT EQUAL ZERO                                 
               DISPLAY '------- W A R N I N G ----------------------'           
               DISPLAY 'ERROR STORE NBR: ' CE010-CNTRL-STK-ID(2:3)              
W21732                 ' SKU NBR: ' CE010-SKU-NBR ' RECORD NBR: '               
                       PV-INPUT-RECORD-NBR                                      
               DISPLAY 'UNIT PRICE IS ZERO'                                     
               DISPLAY ' '.                                                     
                                                                                
           IF CE010-AVAILABLE EQUAL ZERO                                        
               DISPLAY '------- W A R N I N G ----------------------'           
               DISPLAY 'ERROR STORE NBR: ' CE010-CNTRL-STK-ID(2:3)              
W21732                 ' SKU NBR: ' CE010-SKU-NBR ' RECORD NBR: '               
                       PV-INPUT-RECORD-NBR                                      
               DISPLAY 'UNIT QTY IS ZERO'                                       
               DISPLAY ' '.                                                     
                                                                                
      *    -------------------------------------------------------              
      *    THE PHYSCIAL INVENTORY FILE ALLOWS FOR ONLY A 4 BYTE                 
      *    QUANTITY FIELD. WHEN CENTRAL STOCK QUANTITY IS GREATER               
      *    THAN 999, MULTIPLE RECORDS MUST BE WRITTEN.                          
      *    -------------------------------------------------------              
SR3403     IF CE010-AVAILABLE > 999                                             
               MOVE CE010-AVAILABLE TO PV-CALC-QTY                              
               PERFORM Z902-WRITE-MULTIPLE-OUTPUT-REC                           
SR3403             UNTIL PV-CALC-QTY NOT > 999                                  
           ELSE                                                                 
               MOVE CE010-AVAILABLE TO IN018-QUANTITY                           
               PERFORM Z900-WRITE-OUTPUT-REC.                                   
                                                                                
       B200-CONTINUE.                                                           
                                                                                
           PERFORM Z100-READ-CEN-STK.                                           
                                                                                
       B200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    SHEET INVENTORYS ALLOW ONLY 25 LINES PER SHEET(AREA)                 
      ******************************************************************        
       B210-DETERMINE-LINE-AREA.                                                
                                                                                
           ADD +1                      TO IN018-LINE-NUMBER.                    
                                                                                
           IF IN018-LINE-NUMBER > 25                                            
               MOVE 1                  TO IN018-LINE-NUMBER                     
               COMPUTE PV-DEFAULT-AREA = 1 + PV-DEFAULT-AREA.                   
                                                                                
           MOVE PV-DEFAULT-AREA        TO IN018-AREA-NUMBER.                    
                                                                                
       B300-EOJ.                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
                                                                                
           IF PV-ERROR-COUNT EQUAL ZERO                                         
               MOVE 1 TO DP132O-PAGE-NUMBER                                     
               WRITE ERROR-REPORT-LINE FROM                                     
                   DP132O-STANDRD-HEADER-132-COLS                               
                   AFTER ADVANCING PAGE                                         
               MOVE 1 TO PC-ADVANCE                                             
               MOVE RD-REPORT-TITLE-LINE TO ERROR-REPORT-LINE                   
               PERFORM Z800-WRITE-REPORT-LINE                                   
               MOVE 2 TO PC-ADVANCE                                             
               MOVE RD-REPORT-NO-ERRORS TO ERROR-REPORT-LINE                    
               PERFORM Z800-WRITE-REPORT-LINE                                   
           END-IF.                                                              
                                                                                
           CLOSE CENTRAL-STOCK-INV                                              
                 PHYSICAL-INV-CE                                                
23511            STORE-CONTROL-CARD                                             
                 ERROR-REPORT-FILE.                                             
                                                                                
      ****************************************************************          
      * WRITE AN ERROR REPORT LINE AFTER CHECKING TO CREATE HEADINGS            
      ****************************************************************          
       C100-INVALID-DATA.                                                       
                                                                                
           IF PC-LINE-COUNT > PC-LINE-MAX                                       
               PERFORM D100-CREATE-HEADINGS                                     
           END-IF.                                                              
                                                                                
           MOVE CE010-CNTRL-STK-ID(2:3) TO RD-RPT-ERROR-STORE.                  
W21732     MOVE CE010-SKU-NBR           TO RD-RPT-ERROR-SKU.                    
           MOVE PV-INPUT-RECORD-NBR     TO RD-RPT-ERROR-REC-NBR.                
           MOVE RD-REPORT-ERROR-LINE TO ERROR-REPORT-LINE.                      
           MOVE 2 TO PC-ADVANCE.                                                
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
      ****************************************************************          
      * THIS ROUTINE WILL PRODUCE THE REPORT                                    
      * HEADING IF AN ERROR REPORT IS PRODUCED.                                 
      ****************************************************************          
       D100-CREATE-HEADINGS.                                                    
                                                                                
           ADD 1 TO PC-PAGE.                                                    
           MOVE PC-PAGE TO DP132O-PAGE-NUMBER.                                  
           WRITE ERROR-REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS          
               AFTER ADVANCING PAGE.                                            
           MOVE 1 TO PC-LINE-COUNT.                                             
           MOVE 1 TO PC-ADVANCE                                                 
           MOVE RD-REPORT-TITLE-LINE TO ERROR-REPORT-LINE.                      
           PERFORM Z800-WRITE-REPORT-LINE.                                      
           MOVE 2 TO PC-ADVANCE.                                                
           MOVE RD-REPORT-HDR-LINE1 TO ERROR-REPORT-LINE.                       
           PERFORM Z800-WRITE-REPORT-LINE.                                      
           MOVE 1 TO PC-ADVANCE.                                                
           MOVE RD-REPORT-HDR-LINE2 TO ERROR-REPORT-LINE.                       
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
      ******************************************************************        
      *    READ INPUT CENTRAL STOCK FILE                                        
      ******************************************************************        
                                                                                
       Z100-READ-CEN-STK.                                                       
           READ CENTRAL-STOCK-INV                                               
               INTO CE010-SKU-RECORD                                            
               AT END SET END-OF-CEN-STK TO TRUE.                               
                                                                                
      ******************************************************************        
      *    DETERMINE IF THE STORE BEING PROCESSED HAS A TINVPAR                 
      *    FINANCIAL BOOK DATE OF 9999-09-09                                    
      ******************************************************************        
       Z200-READ-XMTLOC-TINVPAR.                                                
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB > PC-MAX-RETRIES                               
                         OR SQLCODE = 0                                         
                         OR SQLCODE = +100)                                     
                                                                                
               EXEC SQL                                                         
                  SELECT  1                                                     
                  INTO :PV-DB2-NUM                                              
25248              FROM XMT_LOC ST,                                             
P28545                  TINVPAR IP,                                             
P28545                  TINCNTL X                                               
25248             WHERE ST.LOC_NBR    = :XMT00001-LOC-NBR    AND                
25248                   ST.LOC_NBR    = IP.LOC_NBR           AND                
                        IP.ACTL_FIN_BK_DTE = :PC-FIN-BK-DATE AND                
W33304                  IP.LOC_INV_STAT_CDE = 'IN'           AND                
P28545                  X.ACTV_IND = 'Y'                     AND                
P28545                  IP.INV_ID = X.INV_ID                 AND                
25248                   ( ( ST.LOC_TYP_CDE IN ('CS','RV') )                     
22000                   OR                                                      
25248                     ( ST.LOC_TYP_CDE =   'DS'  AND                        
25248                       ST.E_BUS_IND = 'Y' ) )                              
P28545            WITH UR                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                       CONTINUE                                                 
                   WHEN SQLCODE = 0                                             
                       SET PS-PROCESS-STORE TO TRUE                             
                   WHEN SQLCODE = +100                                          
                       SET PS-DO-NOT-PROCESS-STORE TO TRUE                      
                   WHEN SQLWARN NOT = SPACES                                    
                   WHEN SQLCODE NOT = ZERO                                      
                       MOVE 'Z200-READ-XMTLOC-TINVPAR'                          
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'READING XMTLOC AND TINVPAR TABLES'                 
                                     TO  PV-DB2-OPER-ATTEMPTED                  
                       PERFORM Z999-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
      *                                                                         
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE 'Z200-READ-XMTLOC-TINVPAR'                                  
                                     TO  PV-PARAGRAPH-NAME                      
               MOVE 'READ RETRIES EXCEEDED'                                     
                                     TO  PV-DB2-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ****************************************************************  10160000
      * Z800-WRITE-REPORT-LINE.  ERROR REPORT LINES WRITTEN             10170000
      ****************************************************************  10180000
       Z800-WRITE-REPORT-LINE.                                          10190000
                                                                        10200000
           WRITE ERROR-REPORT-LINE                                      10210000
               AFTER ADVANCING PC-ADVANCE.                              10220000
           ADD PC-ADVANCE TO PC-LINE-COUNT.                             10230000
                                                                                
      ******************************************************************        
      *    WRITE SINGLE OUTPUT RECORD                                           
      ******************************************************************        
                                                                                
       Z900-WRITE-OUTPUT-REC.                                                   
                                                                                
           PERFORM B210-DETERMINE-LINE-AREA.                                    
           WRITE PHYSICAL-INV-CE-REC                                            
               FROM IN018-INVENTORY-COUNT-RECORD.                               
                                                                                
      ******************************************************************        
      *    WRITE MULTIPLE OUTPUT RECORDS                                        
      ******************************************************************        
                                                                                
       Z902-WRITE-MULTIPLE-OUTPUT-REC.                                          
                                                                                
SR3403     MOVE  999 TO IN018-QUANTITY.                                         
           PERFORM B210-DETERMINE-LINE-AREA.                                    
                                                                                
           WRITE PHYSICAL-INV-CE-REC                                            
               FROM IN018-INVENTORY-COUNT-RECORD.                               
                                                                                
SR3403     COMPUTE PV-CALC-QTY = PV-CALC-QTY - 999.                             
                                                                                
           IF PV-CALC-QTY = 0                                                   
               CONTINUE                                                         
           ELSE                                                                 
SR3403         IF PV-CALC-QTY > 999                                             
                   CONTINUE                                                     
               ELSE                                                             
                   MOVE PV-CALC-QTY TO IN018-QUANTITY                           
                   PERFORM B210-DETERMINE-LINE-AREA                             
                   WRITE PHYSICAL-INV-CE-REC                                    
                       FROM IN018-INVENTORY-COUNT-RECORD.                       
                                                                                
      ******************************************************************        
      *    WRITE HEADER RECORD                                                  
      ******************************************************************        
                                                                                
       Z903-WRITE-HEADER-REC.                                                   
                                                                                
           MOVE SPACES TO IN018-INVENTORY-HEADER-RECORD.                        
           SET IN018-HDR-RECORD TO TRUE.                                        
           SET IN018-HDR-TOTALS-NOT-INCL TO TRUE.                               
25248      MOVE PV-STORE-HOLD      TO IN018-HDR-STORE.                          
25248 **** MOVE PV-STORE-HOLD(2:3) TO IN018-HDR-STORE.                          
                                                                                
           WRITE PHYSICAL-INV-CE-REC                                            
               FROM IN018-INVENTORY-HEADER-RECORD.                              
                                                                                
           INITIALIZE IN018-INVENTORY-COUNT-RECORD.                             
           MOVE ZERO TO IN018-LINE-NUMBER                                       
                        PV-INPUT-RECORD-NBR.                                    
           MOVE PC-START-AREA TO PV-DEFAULT-AREA.                               
                                                                                
23511 ****************************************************************          
  |   * Z910-WRITE-STR-SLCT-CCARD                                               
  |   *   THE ROUTINE WRITE THE STORE NUMBER TO A CONTROL CARAD FILE            
  |   * WHICH IS USED IN INK307 TO DETERMINE WHICH STORES WE ARE                
  |   * PROCESSING. THE RECORDS ARE MIGRATED TO COPYBOOK INCC020.               
  |   ****************************************************************          
  |    Z910-WRITE-STR-SLCT-CCARD.                                               
  |   *** BUILD THE CONTROL CARD FOR INK307.....                                
  |                                                                             
23511      MOVE SPACES                  TO CC-CONTROL-CARD.                     
25248      MOVE PV-STORE-HOLD           TO CC-STORE-NBR.                        
23511                                                                           
  |        WRITE STORE-CONTROL-CARD-REC FROM CC-CONTROL-CARD.                   
23511                                                                           
      ****************************************************************          
      *    THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.        *          
      ****************************************************************          
       Z999-SQL-ABEND.                                                          
      *                                                                         
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT176'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
      *                                                                         
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
      *                                                                         
           COPY DPPD004.                                                        
      *                                                                         
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                               
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PV-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  ' PV-ABEND-MESSAGE.                                     
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
