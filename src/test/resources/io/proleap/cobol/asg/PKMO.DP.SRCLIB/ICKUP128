       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.      ICKUP128.                                       00020000
       AUTHOR.          DAVID WELLER.                                   00030000
       DATE-WRITTEN.    MARCH 2008.                                     00040000
                                                                        00050000
      *----------------------------------------------------------------*00060000
      *     RF HA ADSET SIGN DATA LAST PROCESSING TIMESTAMP UPDATE      00061000
      *----------------------------------------------------------------*00062000
      *  OVERVIEW                                                       00063000
      *  - THIS PROGRAM WILL EITHER INSERT OR UPDATE A RECORD IN THE    00064000
      *    ICT_CNT_EXTR_PRC TABLE WITH THE CURRENT TIMESTAMP. THIS      00065001
      *    TIMESTAMP IS USED IN THE ADSET SIGN DELTA PROCESS TO FIGURE  00066000
      *    OUT WHICH RECORDS HAVE CHANGED SINCE THIS LAST RUN TIME.     00067000
      *----------------------------------------------------------------*00068000
      * HISTORY LOG:                                                    00069000
      *----------------------------------------------------------------*00069100
      * DATE:                                                           00069200
      * WR/IR#:                                                         00069300
      * PROGRAMMER:                                                     00069400
      * DESCRIPTION:                                                    00069500
      *                                                                 00069600
      *----------------------------------------------------------------*00069700
       ENVIRONMENT DIVISION.                                            00069800
      *----------------------------------------------------------------*00069900
       CONFIGURATION SECTION.                                           00070000
       SOURCE-COMPUTER.        IBM-3090.                                00080000
       OBJECT-COMPUTER.        IBM-3090.                                00090000
                                                                        00100000
       INPUT-OUTPUT SECTION.                                            00110000
                                                                        00120000
       FILE-CONTROL.                                                    00130000
                                                                        00140000
           SELECT TODAYS-PROCESS-DATE-FILE                              00150000
                  ASSIGN TO INP01.                                      00160000
                                                                        00170000
      *----------------------------------------------------------------*00171000
       DATA DIVISION.                                                   00172000
      *----------------------------------------------------------------*00173000
       FILE SECTION.                                                    00174000
                                                                        00175000
       FD  TODAYS-PROCESS-DATE-FILE                                     00176000
           RECORDING MODE F                                             00177000
           LABEL RECORDS ARE OMITTED                                    00178000
           DATA RECORD IS TODAYS-PROCESS-DATE-RECORD.                   00179000
       01  TODAYS-PROCESS-DATE-RECORD      PIC X(080).                  00180000
                                                                        00190000
      *----------------------------------------------------------------*00200000
       WORKING-STORAGE SECTION.                                         00210000
      *----------------------------------------------------------------*00220000
                                                                        00230000
      *----------------------------------------------------------------*00240000
      *  PS- PROGRAM SWITCHES                                           00250000
      *----------------------------------------------------------------*00260000
       01  PS-PROGRAM-SWITCHES.                                         00270000
           05  PS-TODAYS-DATE-EXISTS-SW        PIC X(01)   VALUE 'N'.   00280000
               88  PS-TODAYS-DATE-ALREADY-EXISTS           VALUE 'Y'.   00290000
               88  PS-TODAYS-DATE-DOES-NOT-EXIST           VALUE 'N'.   00300000
                                                                        00310000
      *----------------------------------------------------------------*00320000
      *  DT- DATE / TIMESTAMP                                           00330000
      *----------------------------------------------------------------*00340000
       01  DT-TODAYS-PROCESS-DATE-FORMAT.                               00350000
           05  FILLER                          PIC X(23).               00360000
           05  DT-TIMESTAMP.                                            00370000
               10  DT-TODAY-CCYY-MM-DD         PIC X(10).               00380000
               10  DT-HH-MM-SS-NNNNNN          PIC X(16).               00390000
                                                                        00400000
      *----------------------------------------------------------------*00410000
      *  PC- PROGRAM CONSTANTS                                          00420000
      *----------------------------------------------------------------*00430000
       01  PC-PROGRAM-CONSTANTS.                                        00440000
           05  PC-CURRENT-PROGRAM-NAME         PIC X(08)                00450000
                                               VALUE 'ICKUP128'.        00460000
           05  PC-MAX-RETRIES                  PIC 9(01) VALUE 5.       00470000
           05  PC-EXTRACT-TYPE-CODE            PIC X(03) VALUE 'EXT'.   00471002
                                                                        00480000
      *----------------------------------------------------------------*00490000
      *  PA- PROGRAM ACCUMULATORS (COUNTERS)                            00500000
      *----------------------------------------------------------------*00510000
       01  PA-PROGRAM-COUNTERS.                                         00520000
           05  PA-RECORD-INSERTS               PIC 9(09) VALUE ZEROS.   00530000
           05  PA-RECORD-UPDATES               PIC 9(09) VALUE ZEROS.   00540000
                                                                        00550000
      *----------------------------------------------------------------*00560000
      *  DB2 COPYBOOKS                                                  00570000
      *----------------------------------------------------------------*00580000
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        00590000
           COPY DPWS004.                                                00600000
      ***DB2 ABEND COPYBOOK                                             00610000
           COPY DPWS900.                                                00620000
      ***DB2 MESSAGE AREA                                               00630000
           COPY SQLCA2.                                                 00640000
      ***KOHLS CALENDAR ROUTINE                                         00650000
           COPY DPWS500.                                                00660000
                                                                        00670000
      *----------------------------------------------------------------*00680000
      *  DB2 TABLE INCLUSIONS                                           00690000
      *----------------------------------------------------------------*00700000
      ***DB2 COMMUNICATIONS AREA                                        00710000
           EXEC SQL                                                     00720000
                INCLUDE SQLCA                                           00730000
           END-EXEC.                                                    00740000
      ***TABLE ICT_CNT_EXTR_PRC                                         00750001
           EXEC SQL                                                     00760000
                INCLUDE ICT00007                                        00770001
           END-EXEC.                                                    00780000
                                                                        00790000
      *----------------------------------------------------------------*00800000
      *  ERROR HANDLING                                                 00810000
      *----------------------------------------------------------------*00820000
       01  PV-MISC-ABEND-FIELDS.                                        00830000
           05  PV-PARAGRAPH-NAME               PIC X(30) VALUE SPACE.   00840000
           05  PV-OPERATION-MSG-1              PIC X(40) VALUE SPACE.   00850000
           05  PV-OPERATION-MSG-2              PIC X(40) VALUE SPACE.   00860000
           05  PV-DB2-OPERATION                PIC X(30) VALUE SPACE.   00870000
           05  PV-SQL-SUB                      PIC 9(01) VALUE ZERO.    00880000
                                                                        00890000
       01  ABEND-CODE                          PIC S9(4) VALUE ZEROS    00900000
                                                         COMP SYNC.     00910000
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           00920000
                                                                        00930000
                                                                        00940000
      ******************************************************************00950000
       PROCEDURE DIVISION.                                              00960000
      ******************************************************************00970000
       0000-MAINLINE.                                                   00980000
                                                                        00990000
           PERFORM 1000-INITIALIZATION.                                 01000000
                                                                        01010000
           PERFORM 2000-MAIN-PROCESSING.                                01020000
                                                                        01030000
           PERFORM 9999-WRAPUP.                                         01040000
                                                                        01050000
           GOBACK.                                                      01060000
                                                                        01070000
      *0000-EXIT.                                                       01080000
       EJECT.                                                           01090000
                                                                        01100000
      ******************************************************************01110000
      *  1000-INITIALIZATION.                                           01120000
      *  - SIMPLY OPEN THE INPUT DATE/TIMESTAMP FILE AND READ THE DATA. 01130000
      ******************************************************************01140000
       1000-INITIALIZATION.                                             01150000
                                                                        01160000
           OPEN INPUT TODAYS-PROCESS-DATE-FILE.                         01170000
                                                                        01180000
           READ TODAYS-PROCESS-DATE-FILE                                01190000
                INTO DT-TODAYS-PROCESS-DATE-FORMAT.                     01200000
                                                                        01210000
           CLOSE TODAYS-PROCESS-DATE-FILE.                              01220000
                                                                        01230000
      *1000-EXIT.                                                       01240000
       EJECT.                                                           01250000
                                                                        01260000
      ******************************************************************01270000
      *  2000-MAIN-PROCESSING.                                          01280000
      *  - IF AN INSERT FAILS, ATTEMPT TO UPDATE TODAY'S PROCESS DATE.  01290000
      ******************************************************************01300000
       2000-MAIN-PROCESSING.                                            01310000
                                                                        01320000
           PERFORM 2100-ATTEMPT-INSERT.                                 01330000
                                                                        01340000
           IF PS-TODAYS-DATE-ALREADY-EXISTS                             01350000
              PERFORM 2200-ATTEMPT-UPDATE                               01360000
           END-IF.                                                      01370000
                                                                        01380000
      *2000-EXIT.                                                       01390000
       EJECT.                                                           01400000
                                                                        01410000
      ***************************************************************** 01420000
      *  2100-ATTEMPT-INSERT.                                           01430000
      *  - INSERT TIMESTAMP ROW INTO TABLE ICT_CNT_EXTR_PRC.            01440001
      ***************************************************************** 01450000
       2100-ATTEMPT-INSERT.                                             01460000
                                                                        01470000
           MOVE DT-TODAY-CCYY-MM-DD TO ICT00007-EXTR-PRC-DTE.           01480001
           MOVE DT-TIMESTAMP        TO ICT00007-EXTR-PRC-TMST.          01490001
                                                                        01500000
           MOVE '2100-ATTEMPT-INSERT'          TO PV-PARAGRAPH-NAME.    01510000
           MOVE 'INSERT TO ICT_CNT_EXTR_PRC' TO PV-DB2-OPERATION.       01520001
                                                                        01530000
           PERFORM                                                      01540000
             WITH TEST AFTER                                            01550000
             VARYING PV-SQL-SUB FROM 1 BY 1                             01560000
             UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                   01570000
                     OR SQLCODE = +0 OR SQLCODE = -803)                 01580000
                                                                        01590000
               EXEC SQL                                                 01600000
                    INSERT INTO ICT_CNT_EXTR_PRC                        01610001
                           ( EXTR_PRC_TYP_CDE                           01611001
                            ,EXTR_PRC_DTE                               01620001
                            ,EXTR_PRC_TMST)                             01630001
                    VALUES ( :PC-EXTRACT-TYPE-CODE                      01640002
                            ,:ICT00007-EXTR-PRC-DTE                     01641001
                            ,:ICT00007-EXTR-PRC-TMST)                   01650001
               END-EXEC                                                 01660000
                                                                        01670000
               EVALUATE TRUE                                            01680000
                 WHEN SQLCODE = -904                                    01690000
                 WHEN SQLCODE = -911                                    01700000
                 WHEN SQLCODE = -913                                    01710000
                   CONTINUE                                             01720000
                                                                        01730000
                 WHEN SQLCODE = +0                                      01740000
                   ADD +1                      TO PA-RECORD-INSERTS     01750000
                                                                        01760000
                 WHEN SQLCODE = -803                                    01770000
                   SET PS-TODAYS-DATE-ALREADY-EXISTS TO TRUE            01780000
                                                                        01790000
                 WHEN OTHER                                             01800000
                   DISPLAY 'TRYING TO INSERT VALUES: '                  01810000
                   DISPLAY 'PROC DTE: ' DT-TODAY-CCYY-MM-DD             01820000
                   DISPLAY 'TIMESTMP: ' DT-TIMESTAMP                    01830000
                   PERFORM 9999-DISPLAY-STATISTICS                      01840000
                   PERFORM 9999-ABEND-ROUTINE                           01850000
               END-EVALUATE                                             01860000
           END-PERFORM.                                                 01870000
                                                                        01880000
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         01890000
              DISPLAY 'DB2 CONTENTION WHILE...'                         01900000
              DISPLAY 'TRYING TO INSERT VALUES: '                       01910000
              DISPLAY 'PROC DTE: ' DT-TODAY-CCYY-MM-DD                  01920000
              DISPLAY 'TIMESTMP: ' DT-TIMESTAMP                         01930000
              PERFORM 9999-DISPLAY-STATISTICS                           01940000
              PERFORM 9999-ABEND-ROUTINE                                01950000
           END-IF.                                                      01960000
                                                                        01970000
      *2100-EXIT.                                                       01980000
       EJECT.                                                           01990000
                                                                        02000000
      ***************************************************************** 02010000
      *  2200-ATTEMPT-UPDATE.                                           02020000
      *  - UPDATE TIMESTAMP IN TABLE ICT_CNT_EXTR_PRC                   02030001
      ***************************************************************** 02040000
       2200-ATTEMPT-UPDATE.                                             02050000
                                                                        02060000
           MOVE '2200-ATTEMPT-UPDATE'          TO PV-PARAGRAPH-NAME.    02070000
           MOVE 'UPDATE ON ICT_CNT_EXTR_PRC' TO PV-DB2-OPERATION.       02080001
                                                                        02090000
           PERFORM                                                      02100000
             WITH TEST AFTER                                            02110000
             VARYING PV-SQL-SUB FROM 1 BY 1                             02120000
             UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                   02130000
                     OR SQLCODE = +0 OR SQLCODE = -803)                 02131000
                                                                        02132000
              EXEC SQL                                                  02133000
                UPDATE ICT_CNT_EXTR_PRC                                 02134001
                SET    EXTR_PRC_TMST      = :ICT00007-EXTR-PRC-TMST     02135001
                WHERE  EXTR_PRC_DTE       = :ICT00007-EXTR-PRC-DTE      02136001
                  AND EXTR_PRC_TYP_CDE    = :PC-EXTRACT-TYPE-CODE       02136102
              END-EXEC                                                  02137000
                                                                        02138000
               EVALUATE TRUE                                            02139000
                 WHEN SQLCODE = -904                                    02140000
                 WHEN SQLCODE = -911                                    02150000
                 WHEN SQLCODE = -913                                    02160000
                   CONTINUE                                             02170000
                                                                        02180000
                 WHEN SQLCODE = +0                                      02190000
                   ADD +1                      TO PA-RECORD-UPDATES     02200000
                                                                        02210000
                 WHEN OTHER                                             02220000
                   DISPLAY 'TRYING TO UPDATE VALUES: '                  02230000
                   DISPLAY 'PROC DTE: ' DT-TODAY-CCYY-MM-DD             02240000
                   DISPLAY 'TIMESTMP: ' DT-TIMESTAMP                    02250000
                   PERFORM 9999-DISPLAY-STATISTICS                      02260000
                   PERFORM 9999-ABEND-ROUTINE                           02270000
               END-EVALUATE                                             02280000
           END-PERFORM.                                                 02290000
                                                                        02300000
           IF PV-SQL-SUB GREATER PC-MAX-RETRIES                         02310000
              DISPLAY 'DB2 CONTENTION WHILE...'                         02320000
              DISPLAY 'TRYING TO UPDATE VALUES: '                       02330000
              DISPLAY 'PROC DTE: ' DT-TODAY-CCYY-MM-DD                  02330100
              DISPLAY 'TIMESTMP: ' DT-TIMESTAMP                         02330200
              PERFORM 9999-DISPLAY-STATISTICS                           02330300
              PERFORM 9999-ABEND-ROUTINE                                02330400
           END-IF.                                                      02330500
                                                                        02330600
      *2200-EXIT.                                                       02330700
       EJECT.                                                           02330800
                                                                        02330900
      ****************************************************************  02331000
      *  9999-WRAPUP.                                                   02332000
      *  -                                                              02333000
      ****************************************************************  02334000
       9999-WRAPUP.                                                     02335000
                                                                        02336000
           PERFORM 9999-COMMIT-DATA.                                    02337000
                                                                        02338000
           PERFORM 9999-DISPLAY-STATISTICS.                             02339000
                                                                        02340000
      *9999-EXIT.                                                       02350000
       EJECT.                                                           02360000
                                                                        02370000
      ****************************************************************  02380000
      *  9999-COMMIT-DATA                                               02390000
      *  -                                                              02400000
      ****************************************************************  02410000
       9999-COMMIT-DATA.                                                02420000
                                                                        02430000
           MOVE '9999-COMMIT-DATA' TO PV-PARAGRAPH-NAME.                02440000
           MOVE 'COMMIT'           TO PV-DB2-OPERATION.                 02450000
                                                                        02460000
           EXEC SQL                                                     02470000
                COMMIT                                                  02480000
           END-EXEC.                                                    02490000
                                                                        02500000
           EVALUATE TRUE                                                02510000
              WHEN SQLCODE = +0                                         02520000
                   CONTINUE                                             02530000
              WHEN OTHER                                                02540000
                   PERFORM 9999-DISPLAY-STATISTICS                      02550000
                   PERFORM 9999-ABEND-ROUTINE                           02560000
           END-EVALUATE.                                                02570000
                                                                        02580000
      *9999-EXIT.                                                       02590000
       EJECT.                                                           02600000
                                                                        02610000
      ****************************************************************  02620000
      *  9999-DISPLAY-STATISTICS.                                       02630000
      *  -                                                              02640000
      ****************************************************************  02650000
       9999-DISPLAY-STATISTICS.                                         02660000
                                                                        02670000
           DISPLAY '*************************************************'. 02680000
           DISPLAY '* PROGRAM............: ' PC-CURRENT-PROGRAM-NAME.   02690000
           DISPLAY '* PROCESSING DATE....: ' DT-TODAY-CCYY-MM-DD.       02700000
           DISPLAY '*************************************************'. 02710000
           DISPLAY '* TABLE:  ICT_CNT_EXTR_PRC'.                        02711003
           DISPLAY '* CURRENT TIMESTAMP..: ' DT-TIMESTAMP.              02720000
           DISPLAY '* RECORD INSERT COUNT: ' PA-RECORD-INSERTS.         02730000
           DISPLAY '* RECORD UPDATE COUNT: ' PA-RECORD-UPDATES.         02740000
           DISPLAY '*************************************************'. 02750000
                                                                        02760000
      *9999-EXIT.                                                       02770000
       EJECT.                                                           02780000
                                                                        02790000
      ******************************************************************02800000
      *  9999-ABEND-ROUTINE.                                            02810000
      *  -                                                              02820000
      ******************************************************************02830000
       9999-ABEND-ROUTINE.                                              02840000
                                                                        02850000
           DISPLAY '** ABEND     **'.                                   02860000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        02870000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              02880000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               02890000
      ******************************************************************02900000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     02910000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      02920000
      ******************************************************************02930000
           COPY DPPD004.                                                02940000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            02950000
           CALL 'ILBOABN0' USING ABEND-CODE.                            02960000
                                                                        02970000
      *9999-EXIT.                                                       02980000
       EJECT                                                            02990000
                                                                        03000000
      ******************************************************************03010000
