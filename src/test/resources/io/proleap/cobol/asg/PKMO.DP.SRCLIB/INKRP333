       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.             INKRP333.                                00020000
       AUTHOR.                 THERESE RAMSEYER.                        00030000
       INSTALLATION.           KOHLS.                                   00040000
       DATE-WRITTEN.           SEPTEMBER 2009.                          00050000
       DATE-COMPILED.                                                   00060000
                                                                        00070000
      ***************************************************************** 00080000
      *    THIS PROGRAM PRODUCES THE STORE SHORTAGE REPORT            * 00090000
      *    AND IS A CLONE OF THE ORIGINAL INKRP145 WITH A FEW TWEAKS. * 00100000
      *                                                               * 00110000
      *    A CONTROL CARD IS REQUIRED IN THE FOLLOWING FORMAT:        * 00120000
      *                                                               * 00130000
      *    BYTE 1       :    'S' -OR-  SPACE                          * 00131000
      *    BYTE 2       :    FILLER                                   * 00132000
      *    BYTES 3-12   :    BEG DATE OF RANGE CCYY-MM-DD             * 00133000
      *    BYTE 13      :    FILLER                                   * 00134000
      *    BYTES 14-23  :    END DATE OF RANGE CCYY-MM-DD             * 00135000
      *    BYTE 24      :    FILLER                                   * 00136000
      *    BYTES 25-29  :    'FLASH'  -OR- 'FINAL'                    * 00137000
      *                                                               * 00138000
      *    AN 'S' WILL SUPPRESS STORE TOTALS.                         * 00139000
      *                                                               * 00140000
      ***************************************************************** 00150000
      * W33304  09-29-2009 THERESE RAMSEYER                             00160000
      *                    ORIGINAL IMPLEMENTATION                      00170000
      ***************************************************************** 00180000
W34259* W34259  11-01-2013 COGNIZANT                                    00181000
W34259*                    MODIFIED PROGRAM TO INCORPORATE THE BELOW    00182000
W34259*                    CHANGES TO THE REPORT.                       00183000
W34259*                   -ADDED GMM, DMM AND BUYER DETAILS TO SUBTITLE,00184000
W34259*                   -ADDED CLASS DESCRIPTION COLUMN TO THE REPORT,00185000
W34259*                   -REMOVED SHORTAGE COST COLUMN,                00186000
W34259*                   -CHANGED THE HEADING OF SHORTAGE RETAIL AND   00187000
W34259*                    SHRINK % TO TY SHORTAGE RETAIL & TY SHRINK %,00188000
W34259*                   -ROUNDED OFF EACH CLASS DOLLAR VALUES BEFORE  00189000
W34259*                    DISPLAY.                                     00189100
W34259*                   -SHRINK % WAS CALCULATED BASED ON DECIMAL     00189200
W34259*                    VALUES AND ROUNDED BEFORE DISPLAYING         00189300
W34259*                    AS REQUESTED BY BUSINESS.                    00189400
W34259*                   -DEPARTMENT, STORE AND COMPANY TOTALS ARE     00189500
W34259*                    CALCULATED BASED ON DECIMAL VALUES AND       00189600
W34259*                    ROUNDED BEFORE DISPLAYING AS REQUESTED       00189700
W34259*                    BY BUSINESS.                                 00189800
W34259***************************************************************** 00189900
       ENVIRONMENT DIVISION.                                            00190000
                                                                        00200000
       CONFIGURATION SECTION.                                           00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
       OBJECT-COMPUTER.        IBM-3090.                                00230000
                                                                        00240000
       INPUT-OUTPUT SECTION.                                            00250000
       FILE-CONTROL.                                                    00260000
           SELECT STORE-SHORT-FILE           ASSIGN TO INP01.           00270000
           SELECT CONTROL-FILE               ASSIGN TO INP02.           00280000
           SELECT DATE-CONTROL               ASSIGN TO INP03.           00290000
           SELECT PRINT-FILE                 ASSIGN TO RPT01.           00300000
                                                                        00310000
       DATA DIVISION.                                                   00320000
                                                                        00330000
       FILE SECTION.                                                    00340000
                                                                        00350000
       FD  STORE-SHORT-FILE                                             00360000
           RECORDING MODE IS F                                          00370000
           LABEL RECORDS ARE STANDARD                                   00380000
           BLOCK CONTAINS 0 RECORDS                                     00390000
      *    RECORD CONTAINS 94 CHARACTERS                                00400000
           DATA RECORD IS SHORT-REC.                                    00410000
W34259 01  SHORT-REC                   PIC X(155).                      00420000
                                                                        00430000
       FD  CONTROL-FILE                                                 00440000
           RECORDING MODE IS F                                          00450000
           LABEL RECORDS ARE STANDARD                                   00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           RECORD CONTAINS 80 CHARACTERS                                00480000
           DATA RECORD IS CONTROL-REC.                                  00490000
       01  CONTROL-REC                 PIC X(80).                       00500000
                                                                        00510000
       FD  DATE-CONTROL                                                 00520000
           RECORDING MODE IS F                                          00530000
           LABEL RECORDS ARE STANDARD                                   00540000
           BLOCK CONTAINS 0 RECORDS                                     00550000
           RECORD CONTAINS 80 CHARACTERS                                00560000
           DATA RECORD IS DATE-REC.                                     00570000
       01  DATE-REC                    PIC X(80).                       00580000
                                                                        00590000
       FD  PRINT-FILE                                                   00600000
           RECORDING MODE IS F                                          00610000
           LABEL RECORDS ARE STANDARD                                   00620000
           BLOCK CONTAINS 0 RECORDS                                     00630000
           RECORD CONTAINS 132 CHARACTERS                               00640000
           DATA RECORD IS PRINT-REC.                                    00650000
       01  PRINT-REC                   PIC X(132).                      00660000
                                                                        00670000
                                                                        00680000
       WORKING-STORAGE SECTION.                                         00690000
       01  CONTROL-CARD.                                                00700000
           05  CC-CONTROL-IND         PIC  X(16).                       00710000
               88  CC-LOSS-PREV-REPORT      VALUE ' LOSS PREVENTION'.   00720000
               88  CC-STORE-DIST-REPORT     VALUE 'STR DISTRIBUTION'.   00730000
                                                                        00740000
       01  DATE-CONTROL-CC.                                             00750000
           05  CC-PRINT-TOTAL-STR-LN  PIC  X(01)   VALUE SPACES.        00760000
           05  CC-FILLER              PIC  X(01)   VALUE SPACES.        00770000
           05  CC-BEG-DATE            PIC  X(10)   VALUE SPACES.        00780000
           05  CC-FILLER              PIC  X(01)   VALUE SPACES.        00790000
           05  CC-END-DATE            PIC  X(10)   VALUE SPACES.        00800000
           05  CC-FILLER              PIC  X(01)   VALUE SPACES.        00801000
           05  CC-RPT-TYPE            PIC  X(05)   VALUE SPACES.        00810000
               88  CC-RPT-TYPE-FLASH        VALUE 'FLASH'.              00820000
               88  CC-RPT-TYPE-FINAL        VALUE 'FINAL'.              00830000
           05  CC-FILLER              PIC  X(51)   VALUE SPACES.        00840000
                                                                        00850000
       01  PV-PROGRAM-VARIABLE.                                         00860000
           05  PV-STORE-NBR           PIC  9(05)   VALUE ZEROES.        00870000
           05  PV-STORE-NBRX  REDEFINES                                 00880000
               PV-STORE-NBR           PIC  X(05).                       00890000
           05  PV-SHRINK-PERCENT      PIC  S9(3)V99 COMP-3 VALUE +0.    00900000
           05  PV-HALF-PAGES          PIC 9(10)            VALUE ZEROES.00910000
           05  PV-REMAINDER-PAGES     PIC 9(10)            VALUE ZEROES.00920000
           05  PV-OPERATION-MSG-1     PIC X(30)     VALUE SPACES.       00921000
           05  PV-OPERATION-MSG-2     PIC X(30)     VALUE SPACES.       00922000
           05  PV-PARAGRAPH-NAME      PIC X(30)     VALUE SPACES.       00923000
                                                                        00930000
       01  PS-PROGRAM-SWITCHES.                                         00940000
           05  PS-PHYS-CNT-STORE-SW   PIC  X(03)           VALUE 'YES'. 00950000
               88  PS-PROCESS-STORE                        VALUE 'YES'. 00960000
               88  PS-BYPASS-STORE                         VALUE 'NO '. 00970000
           05  PS-PHYS-CNT-LOC-SW     PIC  X               VALUE 'Y'.   00980000
               88  PHYS-CNT-LOC                            VALUE 'Y'.   00990000
               88  NOT-PHYS-CNT-LOC                        VALUE 'N'.   01000000
           05  PS-CODE-BREAK-SW     PIC  X                 VALUE 'N'.   01010000
               88  PS-CODE-BREAK-YES                       VALUE 'Y'.   01020000
               88  PS-CODE-BREAK-NO                        VALUE 'N'.   01030000
                                                                        01040000
       01  PC-PROGRAM-CONSTANTS.                                        01050000
           05  PC-TWO                 PIC 9(02)   VALUE 2.              01060000
           05  PC-S                   PIC X(01)   VALUE 'S'.            01070000
           05  PC-FLASH               PIC X(05)   VALUE 'FLASH'.        01071000
           05  PC-FINAL               PIC X(05)   VALUE 'FINAL'.        01072000
           05  PC-DPKUT500            PIC X(08)   VALUE                 01080000
                                                          'DPKUT500'.   01090000
           05  PC-PROGRAM-NAME        PIC X(08)   VALUE                 01100000
                                                          'INKRP333'.   01110000
                                                                        01120000
       01  MV-MISC-VARIABLES.                                           01130000
           05  MV-ABEND-CODE          PIC  9(02)    COMP   VALUE 0.     01140000
           05  MV-PRINT-REC-SAVE      PIC  X(132)          VALUE ' '.   01150000
           05  MV-STRIPPED-STORE.                                       01160000
               10  MV-JUST-STORE      PIC  X(03).                       01170000
               10  FILLER             PIC  X(01).                       01180000
               10  FILLER             PIC  X(01).                       01190000
           05  MV-NUMB-LINES          PIC  9(02)           VALUE 0.     01200000
           05  MV-LINE-COUNT          PIC  9(02)    COMP-3 VALUE 61.    01210000
           05  MV-PAGE-COUNT          PIC  9(04)    COMP-3 VALUE 0.     01220000
W33304     05  MV-PREV-FSCL-MN-DATE   PIC  X(10)           VALUE SPACES.01230000
W33304     05  MV-PREV-CYCLE-NBR      PIC S9(9)     COMP   VALUE ZERO.  01240000
           05  MV-PREV-DEPARTMENT     PIC  X(03)           VALUE ' '.   01250000
           05  MV-PREV-STORE          PIC  X(05)           VALUE ' '.   01260000
W26600     05  MV-DPT-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.    01270000
0200RM     05  MV-DPT-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.    01280000
0200RM     05  MV-DPT-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01290000
           05  MV-DPT-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.    01300000
0200RM     05  MV-DPT-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.    01310000
0200RM     05  MV-DPT-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01320000
           05  MV-DPT-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.    01330000
           05  MV-CYC-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.    01340000
           05  MV-CYC-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.    01350000
           05  MV-CYC-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01360000
           05  MV-CYC-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.    01370000
           05  MV-CYC-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.    01380000
           05  MV-CYC-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01390000
           05  MV-CYC-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.    01400000
           05  MV-MTH-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.    01410000
           05  MV-MTH-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.    01420000
           05  MV-MTH-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01430000
           05  MV-MTH-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.    01440000
           05  MV-MTH-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.    01450000
           05  MV-MTH-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01460000
           05  MV-MTH-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.    01470000
           05  MV-STR-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.    01480000
           05  MV-STR-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.    01490000
           05  MV-STR-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01500000
           05  MV-STR-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.    01510000
           05  MV-STR-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.    01520000
           05  MV-STR-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01530000
           05  MV-STR-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.    01540000
           05  MV-TOT-BOOK-INV        PIC S9(13)V99 COMP-3 VALUE +0.    01550000
           05  MV-TOT-SHRINK-ALLOW    PIC S9(13)V99 COMP-3 VALUE +0.    01560000
           05  MV-TOT-TOTAL-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01570000
           05  MV-TOT-TOTAL-COST      PIC S9(13)V99 COMP-3 VALUE +0.    01580000
           05  MV-TOT-PHY-INV-RETAIL  PIC S9(13)V99 COMP-3 VALUE +0.    01590000
           05  MV-TOT-SHORT-RETAIL    PIC S9(13)V99 COMP-3 VALUE +0.    01600000
           05  MV-TOT-TOTAL-SALES     PIC S9(13)V99 COMP-3 VALUE +0.    01610000
W34259     05  MV-TOTDPT-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.    01620000
W34259     05  MV-TOTDPT-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.    01630000
W34259     05  MV-TOTDPT-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01640000
W34259     05  MV-TOTDPT-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.    01650000
W34259     05  MV-TOTDPT-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.    01660000
W34259     05  MV-TOTDPT-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.    01661000
W34259     05  MV-TOTDPT-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.    01662000
W34259     05  MV-TOTSTR-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.    01663000
W34259     05  MV-TOTSTR-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.    01664000
W34259     05  MV-TOTSTR-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01665000
W34259     05  MV-TOTSTR-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.    01666000
W34259     05  MV-TOTSTR-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.    01667000
W34259     05  MV-TOTSTR-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.    01668000
W34259     05  MV-TOTSTR-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.    01669000
W34259     05  MV-TOTCYC-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.    01669100
W34259     05  MV-TOTCYC-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.    01669200
W34259     05  MV-TOTCYC-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01669300
W34259     05  MV-TOTCYC-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.    01669400
W34259     05  MV-TOTCYC-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.    01669500
W34259     05  MV-TOTCYC-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.    01669600
W34259     05  MV-TOTCYC-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.    01669700
W34259     05  MV-TOTMTH-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.    01669800
W34259     05  MV-TOTMTH-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.    01669900
W34259     05  MV-TOTMTH-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01670000
W34259     05  MV-TOTMTH-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.    01670100
W34259     05  MV-TOTMTH-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.    01670200
W34259     05  MV-TOTMTH-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.    01670300
W34259     05  MV-TOTMTH-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.    01670400
W34259     05  MV-TOTRND-BOOK-INV        PIC S9(13) COMP-3 VALUE +0.    01670500
W34259     05  MV-TOTRND-SHRINK-ALLOW    PIC S9(13) COMP-3 VALUE +0.    01670600
W34259     05  MV-TOTRND-TOTAL-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01670700
W34259     05  MV-TOTRND-TOTAL-COST      PIC S9(13) COMP-3 VALUE +0.    01670800
W34259     05  MV-TOTRND-PHY-INV-RETAIL  PIC S9(13) COMP-3 VALUE +0.    01670900
W34259     05  MV-TOTRND-TY-SHORT-RETAIL PIC S9(13) COMP-3 VALUE +0.    01671000
W34259     05  MV-TOTRND-TOTAL-SALES     PIC S9(13) COMP-3 VALUE +0.    01671100
W34259     05  MV-RND-BOOK-INV           PIC S9(13) COMP-3 VALUE +0.    01671200
W34259     05  MV-RND-SHRINK-ALLOW       PIC S9(13) COMP-3 VALUE +0.    01671300
W34259     05  MV-RND-TOTAL-RETAIL       PIC S9(13) COMP-3 VALUE +0.    01671400
W34259     05  MV-RND-TOTAL-COST         PIC S9(13) COMP-3 VALUE +0.    01671500
W34259     05  MV-RND-PHY-INV-RETAIL     PIC S9(13) COMP-3 VALUE +0.    01671600
W34259     05  MV-RND-TY-SHORT-RETAIL    PIC S9(13) COMP-3 VALUE +0.    01671700
W34259     05  MV-RND-TOTAL-SALES        PIC S9(13) COMP-3 VALUE +0.    01671800
       01  CL-CLASS-LEVEL-PRINT.                                        01672000
           05  FILLER                 PIC X(01)            VALUE ' '.   01680000
           05  CL-CLASS-NUMBER        PIC X(02).                        01690000
W34259     05  FILLER                  PIC X(03)            VALUE ' '.  01700000
W34259     05  CL-CLASS-DESCRIPTION    PIC X(25).                       01710000
W34259     05  CL-DEPT-BOOK-INV        PIC ZZ,ZZZ,ZZZ,ZZZ-.             01711000
W34259     05  CL-DEPT-SHRINK-ALLOW    PIC ZZ,ZZZ,ZZZ,ZZZ-.             01720000
W34259     05  CL-DEPT-TOTAL-RETAIL    PIC ZZ,ZZZ,ZZZ,ZZZ-.             01730000
W34259     05  CL-DEPT-TOTAL-SALES     PIC ZZ,ZZZ,ZZZ,ZZZ-.             01740000
W34259     05  CL-DEPT-PHY-INV-RETAIL  PIC ZZ,ZZZ,ZZZ,ZZZ-.             01750000
W34259     05  CL-DEPT-TY-SHORT-RETAIL PIC ZZ,ZZZ,ZZZ,ZZZ-.             01770000
W34259     05  FILLER                  PIC X(03)            VALUE ' '.  01780000
W34259     05  CL-TY-SHRINK-PERCENT    PIC ZZZ.99-.                     01790000
       01  SH-SECOND-HEADING-LINE.                                      01810000
           05  FILLER                 PIC X(134)           VALUE ' '.   01820000
       01  SH-SECOND-HEADING-LINE1.                                     01830000
           05  FILLER                 PIC X(49)            VALUE ' '.   01840000
           05  FILLER                 PIC X(45)            VALUE        01850000
                 'TOTAL PHYSICAL INVENTORY SHORTAGE REPORT '.           01860000
           05  FILLER                 PIC X(40)            VALUE ' '.   01870000
       01  SH-SECOND-HEADING-LINE2.                                     01880000
           05  FILLER                 PIC X(43)            VALUE ' '.   01890000
           05  SH-STORE.                                                01900000
               10  FILLER             PIC X(06)            VALUE        01910000
                 'STORE '.                                              01920000
               10  SH-STORE-NUMBER    PIC X(06)            VALUE ' '.   01930000
           05  FILLER                 PIC X(39)            VALUE        01940000
                 'PHYSICAL INVENTORY SHORTAGE REPORT '.                 01950000
           05  FILLER                 PIC X(45)            VALUE ' '.   01960000
       01  SH-CYCLE-MONTH-HEADER.                                       01970000
           05  FILLER                 PIC X(134)           VALUE ' '.   01980000
       01  SH-CYCLE-MONTH-HEADER1.                                      01990000
           05  FILLER                 PIC X(07)            VALUE        02000000
                                                           ' MONTH '.   02010000
           05  SH-REPORT-MON          PIC X(03)            VALUE ' '.   02020000
           05  FILLER                 PIC X(50)            VALUE ' '.   02030000
           05  SH-REPORT-MONTH        PIC X(09)            VALUE ' '.   02040000
           05  FILLER                 PIC X(55)            VALUE ' '.   02050000
       01  SH-CYCLE-MONTH-HEADER2.                                      02060000
           05  FILLER                 PIC X(07)            VALUE        02070000
                                                           ' CYCLE '.   02080000
           05  SH-CYCLE-NO            PIC 9(05)            VALUE 0.     02090000
           05  FILLER                 PIC X(43)            VALUE ' '.   02100000
           05  SH-CYCLE-NAME          PIC X(20)            VALUE ' '.   02110000
           05  FILLER                 PIC X(64)            VALUE ' '.   02120000
       01  SH-CYCLE-MONTH-HEADER3.                                      02130000
           05  FILLER                 PIC X(07)            VALUE        02140000
                                                           ' CYCLE '.   02150000
           05  SH-CYCLE-NUM           PIC 9(05)            VALUE 0.     02160000
           05  FILLER                 PIC X(43)            VALUE ' '.   02170000
           05  SH-CYCLE-NAM           PIC X(20)            VALUE ' '.   02180000
           05  FILLER                 PIC X(64)            VALUE ' '.   02190000
       01  TH-THIRD-HEADING-LINE.                                       02200000
W34259     05  FILLER                 PIC X(09)            VALUE ' '.   02210000
W34259     05  FILLER                 PIC X(52)            VALUE        02220000
W34259         'CLASS                      BOOK           SHRINKAGE '.  02230000
W34259     05  FILLER                 PIC X(45)            VALUE        02240000
W34259           '        TOTAL            TOTAL     PHY. INV. '.       02250000
W34259     05  FILLER                 PIC X(26)            VALUE        02260000
W34259           '   TY SHORTAGE  TY SHRINK'.                           02270000
       01  FH-FOURTH-HEADING-LINE.                                      02280000
W34259     05  FILLER                 PIC X(51)            VALUE        02290000
W34259           ' CLASS   DESCRIPTION              INVENTORY'.         02300000
W34259     05  FILLER                 PIC X(54)            VALUE        02310000
W34259         'ALLOWANCE         RETAIL           SALES      RETAIL'.  02320000
W34259     05  FILLER                 PIC X(24)            VALUE        02330000
W34259           '       RETAIL         %'.                             02340000
       01  FH-FIFTH-HEADING-LINE.                                       02350000
           05  FILLER                 PIC X(06)            VALUE ' '.   02360000
           05  FILLER                 PIC X(07)            VALUE        02370000
                 'STORE: '.                                             02380000
           05  FH-STORE-NUMBER        PIC X(05).                        02390000
           05  FILLER                 PIC X(02)            VALUE ' '.   02400000
           05  FH-STORE-NAME          PIC X(10).                        02410000
W34259     05  FILLER                 PIC X(07)            VALUE        02411000
W34259           '  GMM: '.                                             02412000
W34259     05  FH-GMM-NUMBER          PIC 9(03).                        02413000
W34259     05  FILLER                 PIC X(07)            VALUE        02414000
W34259           '  DMM: '.                                             02415000
W34259     05  FH-DMM-NUMBER          PIC 9(03).                        02416000
W34259     05  FILLER                 PIC X(09)            VALUE        02417000
W34259           '  BUYER: '.                                           02418000
W34259     05  FH-BUYER-NUMBER        PIC 9(03).                        02419000
           05  FILLER                 PIC X(14)            VALUE        02420000
                 '  DEPARTMENT: '.                                      02430000
           05  FH-DEPARTMENT-NUMBER   PIC X(03).                        02440000
W34259     05  FILLER                 PIC X(19)            VALUE        02441000
W34259           '  DEPARTMENT NAME: '.                                 02442000
           05  FH-DEPARTMENT-NAME     PIC X(20).                        02443000
W34259     05  FILLER                 PIC X(17)            VALUE ' '.   02444000
       01  DT-DEPARTMENT-TOTAL-LINE.                                    02480000
           05  FILLER                 PIC X(18)            VALUE        02490000
                 '  ***  TOTAL DEPT '.                                  02500000
           05  DT-DEPARTMENT-NUMBER   PIC X(03).                        02510000
           05  FILLER                 PIC X(05)            VALUE        02520000
                 '  ***'.                                               02530000
           05  FILLER                 PIC X(106)           VALUE ' '.   02540000
       01  ST-STORE-TOTAL-LINE.                                         02550000
           05  FILLER                 PIC X(19)            VALUE        02560000
                 '  ***  TOTAL STORE '.                                 02570000
           05  ST-STORE-NUMBER        PIC X(05).                        02580000
           05  FILLER                 PIC X(05)            VALUE        02590000
                 '  ***'.                                               02600000
           05  FILLER                 PIC X(105)           VALUE ' '.   02610000
       01  ST-CYCLE-TOTAL-LINE.                                         02620000
           05  FILLER                 PIC X(19)            VALUE        02630000
                 '  ***  TOTAL CYCLE '.                                 02640000
           05  ST-CYCLE-NUMBER        PIC 9(05)            VALUE ZEROES.02650000
           05  FILLER                 PIC X(05)            VALUE        02660000
                 '  ***'.                                               02670000
           05  FILLER                 PIC X(105)           VALUE ' '.   02680000
       01  ST-MONTH-TOTAL-LINE.                                         02690000
           05  FILLER                 PIC X(24)            VALUE        02700000
                 '  ***  TOTAL MONTH   ***'.                            02710000
           05  FILLER                 PIC X(108)           VALUE ' '.   02720000
       01  BP-BLANK-PAGE-DETAIL.                                        02730000
           05  FILLER                 PIC X(46)            VALUE SPACES.02740000
           05  FILLER                 PIC X(40)            VALUE        02750000
                 '  THIS PAGE IS INTENTIALLY LEFT BLANK   '.            02760000
           05  FILLER                 PIC X(46)            VALUE SPACES.02770000
       01  WS-LEVEL-88.                                                 02780000
           05  WS-RECORD-INDICATOR            PIC X(03)  VALUE 'YES'.   02790000
               88  MORE-RECORDS                          VALUE 'YES'.   02800000
               88  NO-MORE-RECORDS                       VALUE 'NO'.    02810000
           05  DEPARTMENT-SWITCH              PIC X(03)  VALUE 'YES'.   02820000
               88  NEW-DEPARTMENT                        VALUE 'YES'.   02830000
               88  OLD-DEPARTMENT                        VALUE 'NO'.    02840000
       01  SD-SYSTEM-DATE.                                              02850000
           05  SD-YY              PIC 9(02).                            02860000
           05  SD-MM              PIC 9(02).                            02870000
           05  SD-DD              PIC 9(02).                            02880000
       01  ST-SYSTEM-TIME.                                              02890000
           05  ST-HH              PIC 9(02).                            02900000
           05  ST-MM              PIC 9(02).                            02910000
           05  ST-SS              PIC 9(02).                            02920000
      *                                                                 02930000
       01  ABEND-AREAS.                                                 02940000
           05  ABEND-CODE              PIC S9(04)  VALUE ZERO           02950000
                                                   COMP SYNC.           02960000
           05  AA-ABEND-LIT            PIC X(40)   VALUE                02970000
                 '*****       ABEND'.                                   02980000
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                02990000
                   '*****   PROGRAM: INKRP333'.                         03000000
           05  AA-PARAGRAPH-LIT.                                        03010000
               10  FILLER              PIC X(17)   VALUE                03020000
                   '***** PARAGRAPH: '.                                 03030000
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.        03040000
           05  AA-MESSAGE-LINE.                                         03050000
               10  FILLER              PIC X(06)   VALUE '*****'.       03060000
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.        03070000
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                03080000
                   '*****    DB2 ERROR'.                                03090000
           05  AA-DB2-OPERATION-LIT.                                    03100000
               10  FILLER              PIC X(17)   VALUE                03110000
                   '***** OPERATION: '.                                 03120000
               10  AA-DB2-OPERATION.                                    03130000
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.        03140000
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.        03150000
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.        03160000
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.        03170000
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.        03180000
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.        03190000
      *                                                                 03200000
W34259     COPY INRD010.                                                03210000
           COPY DPWS132O.                                               03220000
                                                                        03240000
      ******************************************************************03250000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                03260000
      ******************************************************************03270000
           COPY DPWS004.                                                03280000
                                                                        03290000
      ******************************************************************03300000
      *  WORK AREA PASSED IN CALL TO DPKUT500                           03310000
      ******************************************************************03320000
           COPY DPWS500.                                                03330000
                                                                        03340000
      ******************************************************************03350000
      *  DB2 ERRORS                                                     03360000
      ******************************************************************03370000
           COPY SQLCA2.                                                 03380000
                                                                        03390000
      *                                                                 03400000
      ******************************************************************03410000
      *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        03420000
      ******************************************************************03430000
           EXEC SQL                                                     03440000
               INCLUDE TINCNTL                                          03450000
           END-EXEC.                                                    03460000
      *                                                                 03470000
      ******************************************************************03480000
      *  DB2 INVPAR TABLE LAYOUT                                        03490000
      ******************************************************************03500000
           EXEC SQL                                                     03510000
                INCLUDE TINVPAR                                         03520000
           END-EXEC.                                                    03530000
                                                                        03540000
      ******************************************************************03550000
      *  DB2 DTATTR TABLE LAYOUT                                        03560000
      ******************************************************************03570000
           EXEC SQL                                                     03580000
                INCLUDE TDTATTR                                         03590000
           END-EXEC.                                                    03600000
                                                                        03610000
      ******************************************************************03620000
      *  DB2 COMMUNICATION AREA                                         03630000
      ******************************************************************03640000
           EXEC SQL                                                     03650000
                INCLUDE SQLCA                                           03660000
           END-EXEC.                                                    03670000
                                                                        03680000
           EJECT                                                        03690000
      ******************************************************************03700000
       PROCEDURE DIVISION.                                              03710000
      ******************************************************************03720000
                                                                        03730000
      ******************************************************************03740000
      *    THIS PARAGRAPH DOES THE INITIAL READS AND CONTROLS THE       03750000
      *    PROCESSING OF THE PROGRAM.                                   03760000
      ******************************************************************03770000
       A000-MAIN-DRIVER.                                                03780000
                                                                        03790000
           PERFORM Z200-CONTROL-RECORD-PROCESS.                         03800000
                                                                        03810000
           OPEN INPUT  STORE-SHORT-FILE                                 03820000
                       CONTROL-FILE                                     03830000
                       DATE-CONTROL.                                    03840000
           OPEN OUTPUT PRINT-FILE.                                      03850000
                                                                        03860000
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC          03870000
               AT END                                                   03880000
                   GO TO A010-END-JOB.                                  03890000
                                                                        03900000
           PERFORM Z500-READ-DATE-CONTROL.                              03910000
           PERFORM Z525-EDIT-CONTROL-CARD.                              03911000
           PERFORM Z550-VALIDATE-DATES.                                 03920000
                                                                        03930000
           PERFORM Z300-SELECT-TDTATTR-INPUT.                           03960000
                                                                        04000000
      * EITHER LOSS PREVENTION -OR- STORE DISTRIBUTION                  04010000
           PERFORM Z400-READ-CONTROL-FILE.                              04020000
                                                                        04030000
W34259     MOVE IN010-STORE-NUMB     TO PV-STORE-NBR.                   04040000
      *                                                                 04050000
      *   LOSS PREVENTION RECEIVES REPORTS ON ALL LOCATIONS             04060000
           IF CC-LOSS-PREV-REPORT                                       04070000
               CONTINUE                                                 04080000
           ELSE                                                         04090000
      *   DISTRIBUTION TO STORE LOCATIONS NEEDS TO BE DETERMINED        04100000
               IF CC-STORE-DIST-REPORT                                  04110000
                   SET PS-BYPASS-STORE TO TRUE                          04120000
                   MOVE PV-STORE-NBR   TO INVPAR-LOC-NBR                04130000
                   PERFORM H150-GET-PHYS-CNT-STORE                      04140000
                       UNTIL PS-PROCESS-STORE                           04150000
                          OR NO-MORE-RECORDS                            04160000
               END-IF                                                   04170000
           END-IF.                                                      04180000
      *                                                                 04190000
           IF MORE-RECORDS                                              04200000
W34259         MOVE IN010-DEPARTMENT-NUMB  TO MV-PREV-DEPARTMENT        04210000
                                              FH-DEPARTMENT-NUMBER      04220000
                                              DT-DEPARTMENT-NUMBER      04230000
W34259         MOVE IN010-STORE-NUMB  TO MV-PREV-STORE                  04240000
                                         FH-STORE-NUMBER                04250000
                                         ST-STORE-NUMBER                04260000
                                         SH-STORE-NUMBER                04270000
                                         PV-STORE-NBR                   04280000
W34259         MOVE IN010-OPN-FSCL-MN-DTE TO MV-PREV-FSCL-MN-DATE       04290000
W34259         MOVE IN010-INV-ID TO MV-PREV-CYCLE-NBR                   04300000
                                                                        04310000
W34259         MOVE IN010-DEPARTMENT-NAME TO FH-DEPARTMENT-NAME         04320000
W34259         MOVE IN010-GMM-NUMBER      TO FH-GMM-NUMBER              04321000
W34259         MOVE IN010-DMM-NUMBER      TO FH-DMM-NUMBER              04322000
W34259         MOVE IN010-BUYER-NUMB      TO FH-BUYER-NUMBER            04323000
W34259         MOVE IN010-STORE-NAME TO FH-STORE-NAME                   04330000
               PERFORM Z300-SELECT-TDTATTR-INPUT                        04341000
               PERFORM Z310-SELECT-CYCLE-NAME                           04350000
               ACCEPT ST-SYSTEM-TIME FROM TIME                          04360000
               MOVE ST-HH TO DP132O-RUN-HOUR                            04370000
               MOVE ST-MM TO DP132O-RUN-MINUTE                          04380000
               MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME              04390000
               MOVE 1 TO DP132O-REPORT-NUMBER                           04400000
               PERFORM B000-PROCESS-SHORTAGE-RECORDS                    04410000
                   UNTIL NO-MORE-RECORDS                                04420000
               PERFORM C000-DEPARTMENT-BREAK                            04430000
               PERFORM D000-STORE-BREAK                                 04440000
           END-IF.                                                      04490000
                                                                        04500000
           CLOSE STORE-SHORT-FILE                                       04510000
                 CONTROL-FILE                                           04520000
                 PRINT-FILE.                                            04530000
           STOP RUN.                                                    04540000
                                                                        04550000
       A010-END-JOB.                                                    04560000
           CLOSE STORE-SHORT-FILE                                       04570000
                 CONTROL-FILE                                           04580000
                 PRINT-FILE.                                            04590000
           STOP RUN.                                                    04600000
                                                                        04610000
      ******************************************************************04620000
      *    THIS PARAGRAPH DOES ALL THE BREAK PROCESSING AND ACCUMUMATES 04630000
      *    MONTH, CYCLE, DEPARTMENT, STORE, AND COMPANY TOTALS.  SHRINK 04640000
      *    PERCENT IS CALCULATED AND DETAIL RECORDS ARE WRITTEN.        04650000
      ******************************************************************04660000
       B000-PROCESS-SHORTAGE-RECORDS.                                   04670000
                                                                        04680000
           MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.      04690000
           MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.       04700000
                                                                        04800000
W34259     IF MV-PREV-CYCLE-NBR NOT = IN010-INV-ID                      04810000
              PERFORM Z310-SELECT-CYCLE-NAME                            04870000
           END-IF.                                                      04880000
                                                                        04890000
W34259     IF MV-PREV-DEPARTMENT NOT = IN010-DEPARTMENT-NUMB            04900000
              AND PS-CODE-BREAK-NO                                      04910000
               PERFORM C000-DEPARTMENT-BREAK.                           04920000
           MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.      04930000
           MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.       04940000
W34259     IF MV-PREV-STORE NOT = IN010-STORE-NUMB                      04950000
              AND PS-CODE-BREAK-NO                                      04960000
               PERFORM D000-STORE-BREAK.                                04970000
           IF NEW-DEPARTMENT                                            04980000
               MOVE 3 TO MV-NUMB-LINES                                  04990000
               MOVE FH-FIFTH-HEADING-LINE TO PRINT-REC                  05000000
               MOVE 'NO' TO DEPARTMENT-SWITCH                           05010000
               PERFORM F100-PRINT-HEADING5.                             05020000
           SET PS-CODE-BREAK-NO TO TRUE.                                05030000
W34259     ADD IN010-BOOK-INV          TO MV-DPT-BOOK-INV               05039300
                                          MV-STR-BOOK-INV               05039400
                                          MV-CYC-BOOK-INV               05039500
                                          MV-MTH-BOOK-INV               05039600
                                          MV-TOT-BOOK-INV.              05039700
W34259     ADD IN010-SHRINK-ALLOWANCE  TO MV-DPT-SHRINK-ALLOW           05090000
                                          MV-STR-SHRINK-ALLOW           05100000
                                          MV-CYC-SHRINK-ALLOW           05110000
                                          MV-MTH-SHRINK-ALLOW           05120000
                                          MV-TOT-SHRINK-ALLOW.          05130000
W34259     ADD IN010-TOTAL-RETAIL      TO MV-DPT-TOTAL-RETAIL           05140000
                                          MV-STR-TOTAL-RETAIL           05150000
                                          MV-CYC-TOTAL-RETAIL           05160000
                                          MV-MTH-TOTAL-RETAIL           05170000
                                          MV-TOT-TOTAL-RETAIL.          05180000
W34259     ADD IN010-TOTAL-COST         TO MV-DPT-TOTAL-COST            05190000
                                          MV-STR-TOTAL-COST             05200000
                                          MV-CYC-TOTAL-COST             05210000
                                          MV-MTH-TOTAL-COST             05220000
                                          MV-TOT-TOTAL-COST.            05230000
W34259     ADD IN010-PHY-INV-RETAIL    TO MV-DPT-PHY-INV-RETAIL         05240000
                                          MV-STR-PHY-INV-RETAIL         05250000
                                          MV-CYC-PHY-INV-RETAIL         05260000
                                          MV-MTH-PHY-INV-RETAIL         05270000
                                          MV-TOT-PHY-INV-RETAIL.        05280000
W34259     ADD IN010-SHORT-RETAIL      TO MV-DPT-SHORT-RETAIL           05290000
                                          MV-STR-SHORT-RETAIL           05300000
                                          MV-CYC-SHORT-RETAIL           05310000
                                          MV-MTH-SHORT-RETAIL           05320000
                                          MV-TOT-SHORT-RETAIL.          05330000
W34259     ADD IN010-TOTAL-SALES       TO MV-DPT-TOTAL-SALES            05390000
                                          MV-STR-TOTAL-SALES            05400000
                                          MV-CYC-TOTAL-SALES            05410000
                                          MV-MTH-TOTAL-SALES            05420000
                                          MV-TOT-TOTAL-SALES.           05430000
W34259     COMPUTE MV-RND-BOOK-INV  ROUNDED                             05430100
W34259                           = IN010-BOOK-INV * 1.                  05430200
W34259     COMPUTE MV-RND-SHRINK-ALLOW  ROUNDED                         05430300
W34259                           = IN010-SHRINK-ALLOWANCE * 1.          05430400
W34259     COMPUTE MV-RND-TOTAL-RETAIL ROUNDED                          05430500
W34259                           = IN010-TOTAL-RETAIL * 1.              05430600
W34259     COMPUTE MV-RND-PHY-INV-RETAIL ROUNDED                        05430700
W34259                           = IN010-PHY-INV-RETAIL * 1.            05430800
W34259     COMPUTE MV-RND-TY-SHORT-RETAIL ROUNDED                       05430900
W34259                           = IN010-SHORT-RETAIL * 1.              05431000
W34259     COMPUTE MV-RND-TOTAL-SALES  ROUNDED                          05431100
W34259                           = IN010-TOTAL-SALES * 1.               05431200
W34259                                                                  05431300
W34259     MOVE MV-RND-BOOK-INV        TO CL-DEPT-BOOK-INV.             05432000
W34259     MOVE MV-RND-SHRINK-ALLOW    TO CL-DEPT-SHRINK-ALLOW.         05440000
W34259     MOVE MV-RND-TOTAL-RETAIL    TO CL-DEPT-TOTAL-RETAIL.         05460000
W34259     MOVE MV-RND-PHY-INV-RETAIL  TO CL-DEPT-PHY-INV-RETAIL.       05480000
W34259     MOVE MV-RND-TY-SHORT-RETAIL TO CL-DEPT-TY-SHORT-RETAIL.      05500000
W34259     MOVE MV-RND-TOTAL-SALES     TO CL-DEPT-TOTAL-SALES           05502000
                                                                        05510000
W34259     IF IN010-TOTAL-SALES = ZERO                                  05512000
               MOVE 999.99 TO PV-SHRINK-PERCENT                         05530000
           ELSE                                                         05540000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      05550000
W34259         (IN010-SHORT-RETAIL * 100 / IN010-TOTAL-SALES)           05560000
                       ON SIZE ERROR                                    05570000
                           MOVE 999.99 TO PV-SHRINK-PERCENT.            05580000
                                                                        05590000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    05600000
W34259     IF IN010-SHORT-RETAIL > 0                                    05610000
               IF PV-SHRINK-PERCENT < 0                                 05620000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          05630000
W34259                               = (PV-SHRINK-PERCENT * (-1))       05631000
               ELSE                                                     05640000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        05650000
               END-IF                                                   05660000
           ELSE                                                         05670000
      *        IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE05680000
W34259         IF IN010-SHORT-RETAIL < 0                                05690000
                   IF PV-SHRINK-PERCENT > 0                             05700000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   05710000
                                            (PV-SHRINK-PERCENT * (-1))  05720000
                   ELSE                                                 05730000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    05740000
                   END-IF                                               05750000
               ELSE                                                     05760000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    05770000
               END-IF                                                   05780000
           END-IF.                                                      05790000
                                                                        05800000
W34259     MOVE IN010-CLASS-NUMB TO CL-CLASS-NUMBER.                    05810000
W34259     MOVE IN010-CLASS-DESCRIPTION TO CL-CLASS-DESCRIPTION.        05811000
           MOVE 1 TO MV-NUMB-LINES.                                     05820000
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                      05830000
           PERFORM F000-PRINT-A-LINE.                                   05840000
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC          05850000
               AT END                                                   05860000
                   MOVE 'NO' TO WS-RECORD-INDICATOR.                    05870000
      *   LOSS PREVENTION RECEIVES REPORTS ON ALL LOCATIONS             05880000
           IF CC-LOSS-PREV-REPORT                                       05890000
               CONTINUE                                                 05900000
           ELSE                                                         05910000
      *   DISTRIBUTION TO STORE LOCATIONS NEEDS TO BE DETERMINED        05920000
               IF ((CC-STORE-DIST-REPORT) AND                           05930000
W34259             (IN010-STORE-NUMB   NOT EQUAL  PV-STORE-NBR))        05940000
      *                                                                 05950000
W34259             MOVE IN010-STORE-NUMB     TO PV-STORE-NBR            05960000
                   SET PS-BYPASS-STORE TO TRUE                          05970000
                   MOVE PV-STORE-NBR   TO INVPAR-LOC-NBR                05980000
                   PERFORM H150-GET-PHYS-CNT-STORE                      05990000
                       UNTIL PS-PROCESS-STORE                           06000000
                          OR NO-MORE-RECORDS                            06010000
               END-IF                                                   06020000
           END-IF.                                                      06030000
                                                                        06040000
      ******************************************************************06050000
      *    THIS PARAGRAPH MOVES ALL THE DEPARTMENT INFO TO THE DETAIL   06060000
      *    LINE AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK  06070000
      *    PERCENT AT THE DEPARTMENT LEVEL.                             06080000
      ******************************************************************06090000
       C000-DEPARTMENT-BREAK.                                           06100000
                                                                        06101000
W34259     COMPUTE MV-TOTDPT-BOOK-INV  ROUNDED                          06110000
W34259                           = MV-DPT-BOOK-INV * 1.                 06111000
W34259     COMPUTE MV-TOTDPT-SHRINK-ALLOW ROUNDED                       06112000
W34259                           = MV-DPT-SHRINK-ALLOW * 1.             06113000
W34259     COMPUTE MV-TOTDPT-TOTAL-RETAIL ROUNDED                       06114000
W34259                           = MV-DPT-TOTAL-RETAIL * 1.             06115000
W34259     COMPUTE MV-TOTDPT-PHY-INV-RETAIL ROUNDED                     06116000
W34259                          = MV-DPT-PHY-INV-RETAIL * 1.            06117000
W34259     COMPUTE MV-TOTDPT-TY-SHORT-RETAIL ROUNDED                    06118000
W34259                           = MV-DPT-SHORT-RETAIL * 1.             06119000
W34259     COMPUTE MV-TOTDPT-TOTAL-SALES ROUNDED                        06119100
W34259                           = MV-DPT-TOTAL-SALES * 1.              06119200
W34259                                                                  06119300
W34259     MOVE MV-TOTDPT-BOOK-INV         TO CL-DEPT-BOOK-INV.         06119400
W34259     MOVE MV-TOTDPT-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.     06119500
W34259     MOVE MV-TOTDPT-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.     06119600
W34259     MOVE MV-TOTDPT-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.   06119700
W34259     MOVE MV-TOTDPT-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.  06119800
W34259     MOVE MV-TOTDPT-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.      06119900
                                                                        06180000
           IF MV-DPT-TOTAL-SALES = ZERO                                 06190000
W34259         MOVE 999.99 TO CL-TY-SHRINK-PERCENT                      06200000
           ELSE                                                         06210000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      06220000
                   (MV-DPT-SHORT-RETAIL * 100 / MV-DPT-TOTAL-SALES)     06230000
                       ON SIZE ERROR                                    06240000
                           MOVE 999.99 TO PV-SHRINK-PERCENT.            06250000
                                                                        06260000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    06270000
           IF MV-DPT-SHORT-RETAIL > 0                                   06280000
               IF PV-SHRINK-PERCENT < 0                                 06290000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          06300000
W34259                                = (PV-SHRINK-PERCENT * (-1))      06301000
               ELSE                                                     06310000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        06320000
               END-IF                                                   06330000
           ELSE                                                         06340000
      *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE    06350000
               IF MV-DPT-SHORT-RETAIL < 0                               06360000
                   IF PV-SHRINK-PERCENT > 0                             06370000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   06380000
                                            (PV-SHRINK-PERCENT * (-1))  06390000
                   ELSE                                                 06400000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    06410000
                   END-IF                                               06420000
               ELSE                                                     06430000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    06440000
               END-IF                                                   06450000
           END-IF.                                                      06460000
                                                                        06470000
           IF MV-LINE-COUNT > 56                                        06480000
                MOVE 61 TO MV-LINE-COUNT.                               06490000
           MOVE 2 TO MV-NUMB-LINES.                                     06500000
           MOVE DT-DEPARTMENT-TOTAL-LINE TO PRINT-REC.                  06510000
           PERFORM F000-PRINT-A-LINE.                                   06520000
           MOVE SPACES TO CL-CLASS-NUMBER.                              06530000
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                         06531000
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                      06540000
           PERFORM F000-PRINT-A-LINE.                                   06550000
W34259     MOVE IN010-DEPARTMENT-NUMB  TO MV-PREV-DEPARTMENT            06560000
                                          FH-DEPARTMENT-NUMBER          06570000
                                          DT-DEPARTMENT-NUMBER.         06580000
W34259     MOVE IN010-DEPARTMENT-NAME TO FH-DEPARTMENT-NAME.            06590000
W34259     MOVE IN010-GMM-NUMBER      TO FH-GMM-NUMBER                  06591000
W34259     MOVE IN010-DMM-NUMBER      TO FH-DMM-NUMBER                  06592000
W34259     MOVE IN010-BUYER-NUMB      TO FH-BUYER-NUMBER                06593000
W34259     MOVE IN010-STORE-NAME      TO FH-STORE-NAME.                 06600000
           MOVE ZERO TO MV-DPT-BOOK-INV                                 06620000
                        MV-DPT-SHRINK-ALLOW                             06621000
                        MV-DPT-TOTAL-RETAIL                             06630000
                        MV-DPT-PHY-INV-RETAIL                           06640000
                        MV-DPT-SHORT-RETAIL                             06650000
                        MV-DPT-TOTAL-SALES.                             06670000
           MOVE 'YES' TO DEPARTMENT-SWITCH.                             06680000
                                                                        06690000
      ******************************************************************06700000
      *    THIS PARAGRAPH MOVES ALL THE STORE INFO TO THE DETAIL LINE   06710000
      *    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK       06720000
      *    PERCENT AT THE STORE LEVEL.                                  06730000
      ******************************************************************06740000
       D000-STORE-BREAK.                                                06750000
                                                                        06751000
W34259     COMPUTE MV-TOTSTR-BOOK-INV  ROUNDED                          06760000
W34259                           = MV-STR-BOOK-INV * 1.                 06761000
W34259     COMPUTE MV-TOTSTR-SHRINK-ALLOW ROUNDED                       06762000
W34259                           = MV-STR-SHRINK-ALLOW * 1.             06763000
W34259     COMPUTE MV-TOTSTR-TOTAL-RETAIL ROUNDED                       06764000
W34259                           = MV-STR-TOTAL-RETAIL * 1.             06765000
W34259     COMPUTE MV-TOTSTR-PHY-INV-RETAIL ROUNDED                     06766000
W34259                           = MV-STR-PHY-INV-RETAIL * 1.           06767000
W34259     COMPUTE MV-TOTSTR-TY-SHORT-RETAIL ROUNDED                    06768000
W34259                           = MV-STR-SHORT-RETAIL * 1.             06769000
W34259     COMPUTE MV-TOTSTR-TOTAL-SALES ROUNDED                        06769100
W34259                           = MV-STR-TOTAL-SALES * 1.              06769200
W34259                                                                  06769300
W34259     MOVE MV-TOTSTR-BOOK-INV         TO CL-DEPT-BOOK-INV.         06769400
W34259     MOVE MV-TOTSTR-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.     06769500
W34259     MOVE MV-TOTSTR-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.     06769600
W34259     MOVE MV-TOTSTR-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.   06769700
W34259     MOVE MV-TOTSTR-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.  06769800
W34259     MOVE MV-TOTSTR-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.      06769900
                                                                        06830000
           IF MV-STR-TOTAL-SALES = ZERO                                 06840000
W34259         MOVE 999.99 TO CL-TY-SHRINK-PERCENT                      06850000
           ELSE                                                         06860000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      06870000
                   (MV-STR-SHORT-RETAIL * 100 / MV-STR-TOTAL-SALES)     06880000
                       ON SIZE ERROR                                    06890000
                           MOVE 999.99 TO PV-SHRINK-PERCENT.            06900000
                                                                        06910000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    06920000
           IF MV-STR-SHORT-RETAIL > 0                                   06930000
               IF PV-SHRINK-PERCENT < 0                                 06940000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          06950000
W34259                                = (PV-SHRINK-PERCENT * (-1))      06951000
               ELSE                                                     06960000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        06970000
               END-IF                                                   06980000
           ELSE                                                         06990000
      *        IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE07000000
               IF MV-STR-SHORT-RETAIL < 0                               07010000
                   IF PV-SHRINK-PERCENT > 0                             07020000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   07030000
                                            (PV-SHRINK-PERCENT * (-1))  07040000
                   ELSE                                                 07050000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    07060000
                   END-IF                                               07070000
               ELSE                                                     07080000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    07090000
               END-IF                                                   07100000
           END-IF.                                                      07110000
                                                                        07120000
      *    IF CC-PRINT-TOTAL-STR-LN = SPACE                             07130000
           IF CC-PRINT-TOTAL-STR-LN = SPACE                             07140000
              MOVE 3 TO MV-NUMB-LINES                                   07150000
              MOVE ST-STORE-TOTAL-LINE TO PRINT-REC                     07160000
              PERFORM F000-PRINT-A-LINE                                 07170000
              MOVE 2 TO MV-NUMB-LINES                                   07180000
              MOVE SPACES TO CL-CLASS-NUMBER                            07190000
W34259        MOVE SPACES TO CL-CLASS-DESCRIPTION                       07191000
              MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC                    07200000
              PERFORM F000-PRINT-A-LINE                                 07210000
           END-IF.                                                      07220000
                                                                        07230000
           PERFORM Z100-WRITE-STORE-BREAK.                              07240000
W34259     MOVE IN010-STORE-NUMB  TO MV-PREV-STORE                      07250000
                                     FH-STORE-NUMBER                    07260000
                                     ST-STORE-NUMBER                    07270000
                                     SH-STORE-NUMBER.                   07280000
W34259     MOVE IN010-STORE-NAME TO FH-STORE-NAME.                      07290000
           MOVE SH-SECOND-HEADING-LINE2 TO SH-SECOND-HEADING-LINE.      07300000
           MOVE SH-CYCLE-MONTH-HEADER2  TO SH-CYCLE-MONTH-HEADER.       07310000
           MOVE ZERO TO MV-STR-BOOK-INV                                 07330000
                       MV-STR-SHRINK-ALLOW                              07331000
                       MV-STR-TOTAL-RETAIL                              07340000
                       MV-STR-PHY-INV-RETAIL                            07350000
                       MV-STR-SHORT-RETAIL                              07360000
                       MV-STR-TOTAL-SALES.                              07380000
           MOVE 61 TO MV-LINE-COUNT.                                    07390000
                                                                        07400000
      ******************************************************************07410000
      *    THIS PARAGRAPH MOVES THE PARTICULAR CYCLE INFO TO THE DETAIL 07420000
      *    LINE AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK  07430000
      *    PERCENT AT THE COMPANY LEVEL.                                07440000
      ******************************************************************07450000
       D100-CYCLE-BREAK.                                                07460000
                                                                        07461000
W34259     COMPUTE MV-TOTCYC-BOOK-INV  ROUNDED                          07470000
W34259                           = MV-CYC-BOOK-INV * 1.                 07480000
W34259     COMPUTE MV-TOTCYC-SHRINK-ALLOW ROUNDED                       07490000
W34259                           = MV-CYC-SHRINK-ALLOW * 1.             07500000
W34259     COMPUTE MV-TOTCYC-TOTAL-RETAIL ROUNDED                       07510000
W34259                           = MV-CYC-TOTAL-RETAIL * 1.             07520000
W34259     COMPUTE MV-TOTCYC-PHY-INV-RETAIL ROUNDED                     07530000
W34259                           = MV-CYC-PHY-INV-RETAIL * 1.           07531000
W34259     COMPUTE MV-TOTCYC-TY-SHORT-RETAIL ROUNDED                    07532000
W34259                           = MV-CYC-SHORT-RETAIL * 1.             07533000
W34259     COMPUTE MV-TOTCYC-TOTAL-SALES ROUNDED                        07534000
W34259                           = MV-CYC-TOTAL-SALES * 1.              07535000
W34259                                                                  07535100
W34259     MOVE MV-TOTCYC-BOOK-INV         TO CL-DEPT-BOOK-INV.         07536000
W34259     MOVE MV-TOTCYC-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.     07537000
W34259     MOVE MV-TOTCYC-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.     07538000
W34259     MOVE MV-TOTCYC-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.   07539000
W34259     MOVE MV-TOTCYC-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.  07539100
W34259     MOVE MV-TOTCYC-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.      07539200
                                                                        07540000
           IF MV-CYC-TOTAL-SALES = ZERO                                 07550000
W34259         MOVE 999.99 TO CL-TY-SHRINK-PERCENT                      07560000
           ELSE                                                         07570000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      07580000
                   (MV-CYC-SHORT-RETAIL * 100 / MV-CYC-TOTAL-SALES)     07590000
                       ON SIZE ERROR                                    07600000
                           MOVE 999.99 TO PV-SHRINK-PERCENT             07610000
           END-IF.                                                      07620000
                                                                        07630000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    07640000
           IF MV-CYC-SHORT-RETAIL > 0                                   07650000
               IF PV-SHRINK-PERCENT < 0                                 07660000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          07670000
W34259                                  = (PV-SHRINK-PERCENT * (-1))    07671000
               ELSE                                                     07680000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        07690000
               END-IF                                                   07700000
           ELSE                                                         07710000
      *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE    07720000
               IF MV-CYC-SHORT-RETAIL < 0                               07730000
                   IF PV-SHRINK-PERCENT > 0                             07740000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   07750000
                                            (PV-SHRINK-PERCENT * (-1))  07760000
                   ELSE                                                 07770000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    07780000
                   END-IF                                               07790000
               ELSE                                                     07800000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    07810000
               END-IF                                                   07820000
           END-IF.                                                      07830000
                                                                        07840000
           MOVE 4 TO MV-NUMB-LINES.                                     07850000
           MOVE ST-CYCLE-TOTAL-LINE TO PRINT-REC.                       07860000
           MOVE SH-SECOND-HEADING-LINE1 TO SH-SECOND-HEADING-LINE.      07870000
           MOVE SH-CYCLE-MONTH-HEADER3  TO SH-CYCLE-MONTH-HEADER.       07880000
           PERFORM F000-PRINT-A-LINE.                                   07890000
           MOVE 2 TO MV-NUMB-LINES.                                     07900000
           MOVE SPACES TO CL-CLASS-NUMBER.                              07910000
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                         07911000
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                      07920000
           PERFORM F000-PRINT-A-LINE.                                   07930000
                                                                        07940000
           PERFORM Z100-WRITE-STORE-BREAK.                              07950000
W34259     MOVE IN010-INV-ID TO MV-PREV-CYCLE-NBR.                      07960000
           MOVE ZERO TO MV-CYC-BOOK-INV                                 07980000
                       MV-CYC-SHRINK-ALLOW                              07981000
                       MV-CYC-TOTAL-RETAIL                              07990000
                       MV-CYC-PHY-INV-RETAIL                            08000000
                       MV-CYC-SHORT-RETAIL                              08010000
                       MV-CYC-TOTAL-SALES.                              08030000
           MOVE 61 TO MV-LINE-COUNT.                                    08040000
                                                                        08050000
      ******************************************************************08060000
      *    THIS PARAGRAPH MOVES ALL THE MONTH INFO TO THE DETAIL LINE   08070000
      *    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK       08080000
      *    PERCENT AT THE COMPANY LEVEL.                                08090000
      ******************************************************************08100000
       D200-MONTH-BREAK.                                                08110000
                                                                        08111000
W34259     COMPUTE MV-TOTMTH-BOOK-INV  ROUNDED                          08120000
W34259                           = MV-MTH-BOOK-INV * 1.                 08130000
W34259     COMPUTE MV-TOTMTH-SHRINK-ALLOW ROUNDED                       08140000
W34259                           = MV-MTH-SHRINK-ALLOW * 1.             08150000
W34259     COMPUTE MV-TOTMTH-TOTAL-RETAIL ROUNDED                       08160000
W34259                           = MV-MTH-TOTAL-RETAIL * 1.             08170000
W34259     COMPUTE MV-TOTMTH-PHY-INV-RETAIL ROUNDED                     08180000
W34259                           = MV-MTH-PHY-INV-RETAIL * 1.           08181000
W34259     COMPUTE MV-TOTMTH-TY-SHORT-RETAIL ROUNDED                    08182000
W34259                           = MV-MTH-SHORT-RETAIL * 1.             08183000
W34259     COMPUTE MV-TOTMTH-TOTAL-SALES ROUNDED                        08184000
W34259                           = MV-MTH-TOTAL-SALES * 1.              08185000
W34259                                                                  08185100
W34259     MOVE MV-TOTMTH-BOOK-INV         TO CL-DEPT-BOOK-INV.         08186000
W34259     MOVE MV-TOTMTH-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.     08187000
W34259     MOVE MV-TOTMTH-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.     08188000
W34259     MOVE MV-TOTMTH-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.   08189000
W34259     MOVE MV-TOTMTH-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.  08189100
W34259     MOVE MV-TOTMTH-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.      08189200
                                                                        08190000
           IF MV-MTH-TOTAL-SALES = ZERO                                 08200000
W34259         MOVE 999.99 TO CL-TY-SHRINK-PERCENT                      08210000
           ELSE                                                         08220000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      08230000
                   (MV-MTH-SHORT-RETAIL * 100 / MV-MTH-TOTAL-SALES)     08240000
                       ON SIZE ERROR                                    08250000
                           MOVE 999.99 TO PV-SHRINK-PERCENT             08260000
           END-IF.                                                      08270000
                                                                        08280000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    08290000
           IF MV-MTH-SHORT-RETAIL > 0                                   08300000
               IF PV-SHRINK-PERCENT < 0                                 08310000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          08320000
W34259                                   = (PV-SHRINK-PERCENT * (-1))   08321000
               ELSE                                                     08330000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        08340000
               END-IF                                                   08350000
           ELSE                                                         08360000
      *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE    08370000
               IF MV-MTH-SHORT-RETAIL < 0                               08380000
                   IF PV-SHRINK-PERCENT > 0                             08390000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   08400000
                                            (PV-SHRINK-PERCENT * (-1))  08410000
                   ELSE                                                 08420000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    08430000
                   END-IF                                               08440000
               ELSE                                                     08450000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    08460000
               END-IF                                                   08470000
           END-IF.                                                      08480000
                                                                        08490000
           ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                    08500000
           MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    08510000
           MOVE 6 TO MV-LINE-COUNT.                                     08520000
           WRITE PRINT-REC FROM                                         08530000
              DP132O-STANDRD-HEADER-132-COLS                            08540000
                 AFTER ADVANCING PAGE.                                  08550000
           WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE1                 08560000
              AFTER ADVANCING 2 LINES.                                  08570000
           WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER1                  08580000
              AFTER ADVANCING 2 LINES.                                  08590000
           WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                   08600000
              AFTER ADVANCING 2 LINES.                                  08610000
           WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                  08620000
              AFTER ADVANCING 1 LINES.                                  08630000
           MOVE ST-MONTH-TOTAL-LINE TO PRINT-REC.                       08640000
           PERFORM F000-PRINT-A-LINE.                                   08650000
           MOVE 2 TO MV-NUMB-LINES.                                     08660000
           MOVE SPACES TO CL-CLASS-NUMBER.                              08670000
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                         08671000
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                      08680000
           PERFORM F000-PRINT-A-LINE.                                   08690000
                                                                        08700000
W34259     MOVE IN010-OPN-FSCL-MN-DTE TO MV-PREV-FSCL-MN-DATE.          08710000
           MOVE ZERO TO MV-MTH-BOOK-INV                                 08730000
                       MV-MTH-SHRINK-ALLOW                              08731000
                       MV-MTH-TOTAL-RETAIL                              08740000
                       MV-MTH-PHY-INV-RETAIL                            08750000
                       MV-MTH-SHORT-RETAIL                              08760000
                       MV-MTH-TOTAL-SALES.                              08780000
           MOVE 61 TO MV-LINE-COUNT.                                    08790000
                                                                        08800000
      ******************************************************************08810000
      *    THIS PARAGRAPH MOVES ALL THE COMPANY INFO TO THE DETAIL LINE 08820000
      *    AND WRITES THE RECORDS.  IT ALSO CALCULATES THE SHRINK       08830000
      *    PERCENT AT THE COMPANY LEVEL.                                08840000
      ******************************************************************08850000
       E000-FINAL-TOTALS.                                               08860000
                                                                        08861000
W34259     COMPUTE MV-TOTRND-BOOK-INV  ROUNDED                          08870000
W34259                           = MV-TOT-BOOK-INV * 1.                 08880000
W34259     COMPUTE MV-TOTRND-SHRINK-ALLOW ROUNDED                       08890000
W34259                           = MV-TOT-SHRINK-ALLOW * 1.             08900000
W34259     COMPUTE MV-TOTRND-TOTAL-RETAIL ROUNDED                       08910000
W34259                           = MV-TOT-TOTAL-RETAIL * 1.             08920000
W34259     COMPUTE MV-TOTRND-PHY-INV-RETAIL ROUNDED                     08930000
W34259                           = MV-TOT-PHY-INV-RETAIL * 1.           08931000
W34259     COMPUTE MV-TOTRND-TY-SHORT-RETAIL ROUNDED                    08932000
W34259                           = MV-TOT-SHORT-RETAIL * 1.             08933000
W34259     COMPUTE MV-TOTRND-TOTAL-SALES ROUNDED                        08934000
W34259                           = MV-TOT-TOTAL-SALES * 1.              08935000
W34259                                                                  08935100
W34259     MOVE MV-TOTRND-BOOK-INV         TO CL-DEPT-BOOK-INV.         08936000
W34259     MOVE MV-TOTRND-SHRINK-ALLOW     TO CL-DEPT-SHRINK-ALLOW.     08937000
W34259     MOVE MV-TOTRND-TOTAL-RETAIL     TO CL-DEPT-TOTAL-RETAIL.     08938000
W34259     MOVE MV-TOTRND-PHY-INV-RETAIL   TO CL-DEPT-PHY-INV-RETAIL.   08939000
W34259     MOVE MV-TOTRND-TY-SHORT-RETAIL  TO CL-DEPT-TY-SHORT-RETAIL.  08939100
W34259     MOVE MV-TOTRND-TOTAL-SALES      TO CL-DEPT-TOTAL-SALES.      08939200
                                                                        08940000
           IF MV-TOT-TOTAL-SALES = ZERO                                 08950000
W34259         MOVE 999.99 TO CL-TY-SHRINK-PERCENT                      08960000
           ELSE                                                         08970000
               COMPUTE PV-SHRINK-PERCENT ROUNDED =                      08980000
                   (MV-TOT-SHORT-RETAIL * 100 / MV-TOT-TOTAL-SALES)     08990000
                       ON SIZE ERROR                                    09000000
                           MOVE 999.99 TO PV-SHRINK-PERCENT.            09010000
                                                                        09020000
      *    IF SHORTAGE RETAIL IS POSITIVE, SHRINK % MUST BE POSITIVE    09030000
           IF MV-TOT-SHORT-RETAIL > 0                                   09040000
               IF PV-SHRINK-PERCENT < 0                                 09050000
W34259            COMPUTE CL-TY-SHRINK-PERCENT                          09060000
W34259                                 = (PV-SHRINK-PERCENT * (-1))     09061000
               ELSE                                                     09070000
W34259            MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT        09080000
               END-IF                                                   09090000
           ELSE                                                         09100000
      *    IF SHORTAGE RETAIL IS NEGATIVE, SHRINK % MUST BE NEGATIVE    09110000
               IF MV-TOT-SHORT-RETAIL < 0                               09120000
                   IF PV-SHRINK-PERCENT > 0                             09130000
W34259                 COMPUTE CL-TY-SHRINK-PERCENT =                   09140000
                                            (PV-SHRINK-PERCENT * (-1))  09150000
                   ELSE                                                 09160000
W34259                MOVE PV-SHRINK-PERCENT TO CL-TY-SHRINK-PERCENT    09170000
                   END-IF                                               09180000
               ELSE                                                     09190000
W34259             MOVE ZERO TO CL-TY-SHRINK-PERCENT                    09200000
               END-IF                                                   09210000
           END-IF.                                                      09220000
                                                                        09230000
           ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                    09240000
           MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    09250000
           MOVE 6 TO MV-LINE-COUNT.                                     09260000
           WRITE PRINT-REC FROM                                         09270000
              DP132O-STANDRD-HEADER-132-COLS                            09280000
                 AFTER ADVANCING PAGE.                                  09290000
           MOVE 99999 TO SH-CYCLE-NUM.                                  09300000
           MOVE 'ALL INVENTORY CYCLES' TO SH-CYCLE-NAM.                 09310000
           WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE1                 09320000
              AFTER ADVANCING 2 LINES.                                  09330000
           WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER3                  09340000
              AFTER ADVANCING 2 LINES.                                  09350000
           WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                   09360000
              AFTER ADVANCING 2 LINES.                                  09370000
           WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                  09380000
              AFTER ADVANCING 1 LINES.                                  09390000
           MOVE SPACES TO CL-CLASS-NUMBER.                              09400000
W34259     MOVE SPACES TO CL-CLASS-DESCRIPTION.                         09401000
           MOVE CL-CLASS-LEVEL-PRINT TO PRINT-REC.                      09410000
           PERFORM F000-PRINT-A-LINE.                                   09420000
                                                                        09430000
       F000-PRINT-A-LINE.                                               09440000
           IF MV-LINE-COUNT > 59                                        09450000
              MOVE PRINT-REC TO MV-PRINT-REC-SAVE                       09460000
              PERFORM G000-PRINT-HEADINGS                               09470000
              MOVE MV-PRINT-REC-SAVE TO PRINT-REC                       09480000
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                        09490000
              WRITE PRINT-REC                                           09500000
                 AFTER ADVANCING MV-NUMB-LINES                          09510000
           ELSE                                                         09520000
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                        09530000
              WRITE PRINT-REC                                           09540000
                 AFTER ADVANCING MV-NUMB-LINES.                         09550000
                                                                        09560000
       F100-PRINT-HEADING5.                                             09570000
           IF MV-LINE-COUNT > 56                                        09580000
              MOVE PRINT-REC TO MV-PRINT-REC-SAVE                       09590000
              PERFORM G000-PRINT-HEADINGS                               09600000
              MOVE MV-PRINT-REC-SAVE TO PRINT-REC                       09610000
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                        09620000
              WRITE PRINT-REC                                           09630000
                 AFTER ADVANCING MV-NUMB-LINES                          09640000
           ELSE                                                         09650000
              ADD MV-NUMB-LINES TO MV-LINE-COUNT                        09660000
              WRITE PRINT-REC                                           09670000
                 AFTER ADVANCING MV-NUMB-LINES.                         09680000
                                                                        09690000
       G000-PRINT-HEADINGS.                                             09700000
           ADD 1 MV-PAGE-COUNT GIVING MV-PAGE-COUNT.                    09710000
           MOVE MV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    09720000
           MOVE 6 TO MV-LINE-COUNT.                                     09730000
           WRITE PRINT-REC FROM                                         09740000
              DP132O-STANDRD-HEADER-132-COLS                            09750000
                 AFTER ADVANCING PAGE.                                  09760000
           WRITE PRINT-REC FROM SH-SECOND-HEADING-LINE                  09770000
              AFTER ADVANCING 2 LINES.                                  09780000
           WRITE PRINT-REC FROM SH-CYCLE-MONTH-HEADER                   09790000
              AFTER ADVANCING 2 LINES.                                  09800000
           WRITE PRINT-REC FROM TH-THIRD-HEADING-LINE                   09810000
              AFTER ADVANCING 2 LINES.                                  09820000
           WRITE PRINT-REC FROM FH-FOURTH-HEADING-LINE                  09830000
              AFTER ADVANCING 1 LINES.                                  09840000
                                                                        09850000
      ******************************************************************10260000
      *   H150-GET-PHYS-CNT-STORE. - DETERMINES WHICH LOC NOT TO PRINT.*10270000
      *    CLOSED STR = SCHD-PHY-CNT-IND = 'C'                         *10280000
      ******************************************************************10290000
       H150-GET-PHYS-CNT-STORE.                                         10300000
      *                                                                 10310000
           PERFORM H300-SELECT-TINVPAR.                                 10320000
      *                                                                 10330000
           IF ( INVPAR-SCHD-PHY-CNT-CDE = 'CL' )                        10340000
               OR ( PS-PHYS-CNT-LOC-SW = 'N' )                          10350000
               SET PS-BYPASS-STORE TO TRUE                              10360000
               PERFORM H200-BYPASS-CLOSED-STORE                         10370000
W34259            UNTIL IN010-STORE-NUMB NOT EQUAL PV-STORE-NBR         10380000
                     OR NO-MORE-RECORDS                                 10390000
W34259            MOVE IN010-STORE-NUMB TO PV-STORE-NBR                 10400000
                  MOVE PV-STORE-NBR      TO INVPAR-LOC-NBR              10410000
           ELSE                                                         10420000
               SET PS-PROCESS-STORE TO TRUE                             10430000
           END-IF.                                                      10440000
      *                                                                 10460000
      ******************************************************************10470000
      *   H200-BYPASS-CLOSED-STORE.                                    *10480000
      *      OBTAIN THE SCHEDULED PHYSICAL COUNT INDICATOR FOR THE     *10490000
      *      STORE ROW WITHIN THE CURRENTL COUNT INDICATOR             *10500000
      ******************************************************************10510000
       H200-BYPASS-CLOSED-STORE.                                        10520000
      *                                                                 10530000
W34259     READ STORE-SHORT-FILE INTO IN010-STORE-SHORTAGE-REC          10540000
               AT END                                                   10550000
                   MOVE 'NO' TO WS-RECORD-INDICATOR.                    10560000
                                                                        10570000
      ******************************************************************10571000
      *OBTAIN THE SCHEDULED PHYSICAL COUNT & SCAN INV INDICATORS.       10572000
      *THIS WILL BE USED TO DETERMINE WHICH STORE LOC TO PRINT.         10573000
      * FINAL = STORES THAT HAVE FINANCIALLY BOOKED                     10573100
      *         AND DO NOT NECESSARILY NEED TO BE IN AN ACTIVE CYCLE.   10573200
      * FLASH = STORES THAT HAVE NOT FINANCIALLY BOOKED MUST BE IN AN   10573300
      *         ACTIVE CYCLE.                                           10573400
      ******************************************************************10574000
       H300-SELECT-TINVPAR.                                             10575000
                                                                        10575100
           EVALUATE CC-RPT-TYPE                                         10576000
              WHEN PC-FLASH                                             10576100
                 EXEC SQL                                               10577000
                    SELECT SCHD_PHY_CNT_CDE                             10578000
                          ,SCAN_INV_IND                                 10579000
                    INTO  :INVPAR-SCHD-PHY-CNT-CDE                      10579100
                         ,:INVPAR-SCAN-INV-IND                          10579200
                    FROM   TINVPAR A                                    10579300
                          ,TINCNTL B                                    10579400
                    WHERE LOC_NBR = :INVPAR-LOC-NBR                     10579500
                      AND A.LOC_INV_STAT_CDE = 'IN'                     10579600
                      AND A.ACTL_FIN_BK_DTE = '9999-09-09'              10579700
                      AND (ACTL_INV_DTE >= :CC-BEG-DATE  AND            10579800
                           ACTL_INV_DTE <= :CC-END-DATE)                10579900
                      AND A.INV_ID = B.INV_ID                           10580000
                      AND B.ACTV_IND = 'Y'                              10580100
                 END-EXEC                                               10580200
              WHEN PC-FINAL                                             10580300
                 EXEC SQL                                               10580400
                    SELECT SCHD_PHY_CNT_CDE                             10580500
                          ,SCAN_INV_IND                                 10580600
                    INTO  :INVPAR-SCHD-PHY-CNT-CDE                      10580700
                         ,:INVPAR-SCAN-INV-IND                          10580800
                    FROM   TINVPAR A                                    10580900
                          ,TINCNTL B                                    10581000
                    WHERE LOC_NBR = :INVPAR-LOC-NBR                     10581100
                      AND A.LOC_INV_STAT_CDE = 'IN'                     10581200
                      AND A.ACTL_FIN_BK_DTE <> '9999-09-09'             10581300
                      AND (ACTL_INV_DTE >= :CC-BEG-DATE  AND            10581400
                           ACTL_INV_DTE <= :CC-END-DATE)                10581500
                      AND A.INV_ID = B.INV_ID                           10581600
                 END-EXEC                                               10581800
           END-EVALUATE                                                 10581900
                                                                        10582000
           EVALUATE TRUE                                                10582100
               WHEN SQLCODE = ZERO                                      10582200
                   SET PHYS-CNT-LOC TO TRUE                             10582300
               WHEN SQLCODE = +100                                      10582400
                   SET NOT-PHYS-CNT-LOC TO TRUE                         10582500
               WHEN OTHER                                               10582600
                   DISPLAY INVPAR-LOC-NBR                               10582700
                   MOVE 'H300-SELECT-TINVPAR'                           10582800
                                       TO AA-PARAGRAPH-NAME             10582900
                   MOVE 'TINVPAR ROW NOT FOUND'                         10583000
                                       TO AA-MESSAGE                    10583100
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              10583200
                   PERFORM Z999-SQL-ERROR                               10583300
           END-EVALUATE.                                                10583400
                                                                        10583500
      ******************************************************************10600000
      * INSERT A DIVIDER PAGE BETWEEN STORE BREAKS                      10610000
      ******************************************************************10620000
       Z100-WRITE-STORE-BREAK.                                          10630000
      *    DETERMINE IF LAST PAGE NUMBER WAS ODD                        10640000
           DIVIDE MV-PAGE-COUNT                                         10650000
               BY PC-TWO                                                10660000
                  GIVING PV-HALF-PAGES                                  10670000
                      REMAINDER PV-REMAINDER-PAGES.                     10680000
           IF PV-REMAINDER-PAGES NOT = ZERO                             10690000
               PERFORM G000-PRINT-HEADINGS                              10700000
      *        MOVE SPACES TO PRINT-REC.                                10710000
               MOVE BP-BLANK-PAGE-DETAIL TO PRINT-REC                   10720000
               WRITE PRINT-REC                                          10730000
      *            AFTER ADVANCING PAGE.                                10740000
                   AFTER ADVANCING 2 LINES                              10750000
           END-IF.                                                      10760000
      *                                                                 10770000
      ******************************************************************10780000
      *    READ THE CONTROL RECORD AND OBTAIN THE MONTH NAME AND        10790000
      *    NUMBER FOR PROCESSING.                                       10800000
      ******************************************************************10810000
       Z200-CONTROL-RECORD-PROCESS.                                     10820000
                                                                        10830000
           ACCEPT SD-SYSTEM-DATE   FROM  DATE.                          10840000
           MOVE SD-MM                TO  DP132O-RUN-MONTH.              10850000
           MOVE SD-DD                TO  DP132O-RUN-DAY.                10860000
           MOVE SD-YY                TO  DP132O-RUN-YEAR.               10870000
      *                                                                 10880000
      *    GET THE MONTH NAME OF WHEN THE INVENTORY WAS TAKEN           10890000
      *                                                                 10900000
                                                                        10910000
      ******************************************************************10920000
      * OBTAIN THE OPEN FISCAL MONTH NAME FROM THE OPEN FISCAL MONTH    10930000
      * DATE ON THE INPUT FILE                                          10940000
      ******************************************************************10950000
       Z300-SELECT-TDTATTR-INPUT.                                       10960000
                                                                        10970000
           EXEC SQL                                                     10980000
               SELECT FSCL_MN_NM                                        10990000
               INTO  :DTATTR-FSCL-MN-NM                                 11000000
               FROM   TDTATTR                                           11010000
W34259         WHERE KY_DTE = :IN010-OPN-FSCL-MN-DTE                    11020000
           END-EXEC.                                                    11030000
                                                                        11040000
           EVALUATE TRUE                                                11050000
               WHEN SQLCODE = ZERO                                      11060000
                   MOVE DTATTR-FSCL-MN-NM TO SH-REPORT-MONTH            11070000
                                             SH-REPORT-MON              11080000
               WHEN OTHER                                               11090000
                   MOVE 'Z300-SELECT-TDTATTR-INPUT'                     11100000
                                       TO AA-PARAGRAPH-NAME             11110000
                   MOVE 'TDTATTR ROW NOT FOUND'                         11120000
                                       TO AA-MESSAGE                    11130000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              11140000
                   PERFORM Z999-SQL-ERROR                               11150000
           END-EVALUATE.                                                11160000
      *                                                                 11170000
      ******************************************************************11180000
      * Z310-SELECT-CYCLE-NAME - FIND CYCLE NAME FOR THE INV ID       * 11190000
      ******************************************************************11200000
                                                                        11210000
       Z310-SELECT-CYCLE-NAME.                                          11220000
W34259     MOVE IN010-INV-ID  TO SH-CYCLE-NO                            11230000
                                 SH-CYCLE-NUM                           11240000
                                 ST-CYCLE-NUMBER.                       11250000
           INITIALIZE INCNTL-INV-NM-TEXT.                               11260000
                                                                        11270000
           EXEC SQL                                                     11280000
                SELECT  INV_NM                                          11290000
                INTO   :INCNTL-INV-NM                                   11300000
                FROM    TINCNTL                                         11310000
W34259          WHERE INV_ID = :IN010-INV-ID                            11320000
           END-EXEC.                                                    11330000
                                                                        11340000
           EVALUATE TRUE                                                11350000
               WHEN SQLCODE        = +0                                 11360000
                   MOVE INCNTL-INV-NM-TEXT TO SH-CYCLE-NAME             11370000
                                              SH-CYCLE-NAM              11380000
               WHEN OTHER                                               11390000
                   DISPLAY 'INV-ID:  ' SH-CYCLE-NO                      11400000
                   MOVE    'Z310-SELECT-CYCLE-NAME'                     11410000
                                            TO AA-PARAGRAPH-NAME        11420000
                   MOVE 'TINCNTL ROW NOT FOUND'                         11430000
                                       TO AA-MESSAGE                    11440000
                   MOVE 'SELECT'       TO AA-DB2-OPERATION              11450000
                   PERFORM Z999-SQL-ERROR                               11460000
           END-EVALUATE.                                                11470000
      *                                                                 11480000
      ******************************************************************11490000
      * OBTAIN THE OPEN FISCAL MONTH NAME BY USING THE BEGINNING DATE OF11500000
      * OF THE USER-SUPPLIED DATE RANGE                                 11510000
      ******************************************************************11520000
      *Z320-SELECT-TDTATTR-BEG-DTE.                                     11530000
      *                                                                 11540000
      *    EXEC SQL                                                     11550000
      *        SELECT FSCL_MN_BEG_DTE,                                  11560000
      *               FSCL_MN_END_DTE,                                  11570000
      *               FSCL_MN_NM                                        11580000
      *        INTO  :DTATTR-FSCL-MN-BEG-DTE                            11590000
      *           ,  :DTATTR-FSCL-MN-END-DTE                            11600000
      *           ,  :DTATTR-FSCL-MN-NM                                 11610000
      *        FROM   TDTATTR                                           11620000
      *        WHERE KY_DTE = :CC-BEG-DATE                              11630000
      *    END-EXEC.                                                    11640000
      *                                                                 11650000
      *    EVALUATE TRUE                                                11660000
      *        WHEN SQLCODE = ZERO                                      11670000
      *            MOVE DTATTR-FSCL-MN-NM TO SH-REPORT-MONTH            11680000
      *                                      SH-REPORT-MON              11690000
      *        WHEN OTHER                                               11700000
      *            MOVE 'Z320-SELECT-TDTATTR-BEG-DTE'                   11710000
      *                                TO AA-PARAGRAPH-NAME             11720000
      *            MOVE 'TDTATTR ROW NOT FOUND'                         11730000
      *                                TO AA-MESSAGE                    11740000
      *            MOVE 'SELECT'       TO AA-DB2-OPERATION              11750000
      *            PERFORM Z999-SQL-ERROR                               11760000
      *    END-EVALUATE.                                                11770000
      *                                                                 11780000
      ******************************************************************11790000
      *    READ THE CONTROL FILE TO DETERMINE WHICH JOB EXECUTED        11800000
      *    THIS PROGRAM.                                                11810000
      ******************************************************************11820000
       Z400-READ-CONTROL-FILE.                                          11830000
                                                                        11840000
           READ CONTROL-FILE INTO CONTROL-CARD.                         11850000
      *                                                                 11860000
      ******************************************************************11870000
      *    READ THE DATE CONTROL FILE TO DETERMINE WHICH JOB EXECUTED   11880000
      *    THIS PROGRAM.                                                11890000
      ******************************************************************11900000
       Z500-READ-DATE-CONTROL.                                          11910000
                                                                        11920000
           READ DATE-CONTROL INTO DATE-CONTROL-CC.                      11930000
                                                                        11931000
           DISPLAY '             123456789012345678901234567890'.       11931100
           DISPLAY 'CONTROL CARD:' DATE-CONTROL-CC.                     11932000
           DISPLAY 'CC BEG DATE :' CC-BEG-DATE.                         11933000
           DISPLAY 'CC END DATE :' CC-END-DATE.                         11934000
           DISPLAY 'CC RPT TYPE :' CC-RPT-TYPE.                         11935000
                                                                        11940000
      ******************************************************************11950000
      *    EDIT CONTROL CARD                                            11960000
      ******************************************************************11970000
       Z525-EDIT-CONTROL-CARD.                                          11980000
                                                                        11990000
           IF CC-PRINT-TOTAL-STR-LN = SPACE OR PC-S                     12000000
              CONTINUE                                                  12010000
           ELSE                                                         12020000
              MOVE 'INVALID 1ST BYTE OF CC'   TO PV-OPERATION-MSG-1     12021000
              MOVE 'MUST BE ''S'' OR SPACE  ' TO PV-OPERATION-MSG-2     12022000
              MOVE 'Z525-EDIT-CONTROL-CARD'   TO PV-PARAGRAPH-NAME      12023000
              PERFORM Z998-ABEND                                        12027000
           END-IF.                                                      12028000
                                                                        12030000
           IF CC-RPT-TYPE = PC-FLASH OR                                 12040000
              CC-RPT-TYPE = PC-FINAL                                    12041000
              CONTINUE                                                  12050000
           ELSE                                                         12060000
              MOVE 'INVALID REPORT TYPE'    TO PV-OPERATION-MSG-1       12070000
              MOVE 'MUST BE FLASH OR FINAL' TO PV-OPERATION-MSG-2       12080000
              MOVE 'Z525-EDIT-CONTROL-CARD' TO PV-PARAGRAPH-NAME        12090000
              PERFORM Z998-ABEND                                        12110000
           END-IF.                                                      12120000
                                                                        12130000
           IF CC-END-DATE < CC-BEG-DATE                                 12240000
              MOVE 'INVALID DATE RANGE'     TO PV-OPERATION-MSG-1       12250000
              MOVE 'BEG DATE GT END DATE'   TO PV-OPERATION-MSG-2       12260000
              MOVE 'Z525-EDIT-CONTROL-CARD' TO PV-PARAGRAPH-NAME        12270000
              PERFORM Z998-ABEND                                        12280000
           END-IF.                                                      12290000
                                                                        12300000
      ******************************************************************12511000
      *    USE KOHLS CALENDAR ROUTINE TO VALIDATE DATES ON CONTROL CARD 12512000
      ******************************************************************12513000
       Z550-VALIDATE-DATES.                                             12514000
                                                                        12515000
      ***  VALIDATE BEGINNING DATE OF USER SUPPLIED RANGE               12516000
           INITIALIZE DPG51 DPG52 DPG53                                 12517000
                      DPG54 DPG55 DPG56.                                12518000
                                                                        12519000
           SET DPG51-VALID-FISCAL-CALENDAR TO TRUE.                     12519100
                                                                        12519200
           MOVE CC-BEG-DATE               TO DPG52-LK-DATE-INPUT.       12519300
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      12519400
                                                                        12519500
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    12519600
                                                   DPG54 DPG55 DPG56.   12519700
                                                                        12519800
           EVALUATE TRUE                                                12519900
               WHEN DPG54-NO-ERROR                                      12520000
                   CONTINUE                                             12520100
               WHEN DPG54-WARNING-ERROR                                 12520200
               WHEN DPG54-SEVERE-ERROR                                  12520300
                  MOVE 'INVALID BEG DATE FROM DPKUT500'                 12520400
                    TO PV-OPERATION-MSG-1                               12520500
                  MOVE 'Z550-VALIDATE-DATES'                            12520600
                    TO  PV-PARAGRAPH-NAME                               12520700
                  MOVE DPG54-ERROR-MESSAGE                              12520800
                    TO PV-OPERATION-MSG-2                               12520900
                  PERFORM Z998-ABEND                                    12521000
           END-EVALUATE.                                                12521100
                                                                        12521200
      ***  VALIDATE ENDING DATE OF USER SUPPLIED RANGE                  12521300
           INITIALIZE DPG51 DPG52 DPG53                                 12521400
                      DPG54 DPG55 DPG56.                                12521500
                                                                        12521600
           SET DPG51-VALID-FISCAL-CALENDAR TO TRUE.                     12521700
                                                                        12521800
           MOVE CC-END-DATE               TO DPG52-LK-DATE-INPUT.       12521900
           SET DPG52-LK-DTE-ISO-FMT       TO TRUE.                      12522000
                                                                        12522100
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    12522200
                                                   DPG54 DPG55 DPG56.   12522300
                                                                        12522400
           EVALUATE TRUE                                                12522500
               WHEN DPG54-NO-ERROR                                      12522600
                   CONTINUE                                             12522700
               WHEN DPG54-WARNING-ERROR                                 12522800
               WHEN DPG54-SEVERE-ERROR                                  12522900
                  MOVE 'INVALID END DATE FROM DPKUT500'                 12523000
                    TO PV-OPERATION-MSG-1                               12523100
                  MOVE 'Z550-VALIDATE-DATES'                            12523200
                    TO  PV-PARAGRAPH-NAME                               12523300
                  MOVE DPG54-ERROR-MESSAGE                              12523400
                    TO PV-OPERATION-MSG-2                               12523500
                  PERFORM Z998-ABEND                                    12523600
           END-EVALUATE.                                                12523700
                                                                        12523800
      ******************************************************************13160000
      *   Z998-ABEND   FOR NON SQL ERROR                               *13170000
      ******************************************************************13180000
       Z998-ABEND.                                                      13181000
                                                                        13182000
           DISPLAY '*********************'                              13200000
           DISPLAY '** ABEND           **'.                             13210000
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          13220000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        13230000
           DISPLAY '** OPER MSG 1      ** = ' PV-OPERATION-MSG-1.       13240000
           DISPLAY '** OPER MSG 2      ** = ' PV-OPERATION-MSG-2.       13250000
           DISPLAY '********************* = '.                          13260000
           MOVE +4013                  TO ABEND-CODE.                   13270000
           CALL 'ILBOABN0' USING ABEND-CODE.                            13280000
                                                                        13290000
       Z999-SQL-ERROR.                                                  13300000
      ******************************************************************13310000
      *   Z999-SQL-ERROR.                                              *13320000
      *      SQL ERROR ABEND ROUTINE.                                  *13330000
      ******************************************************************13340000
                                                                        13350000
           DISPLAY AA-ABEND-LIT.                                        13360000
           DISPLAY AA-DB2-ERROR-LIT.                                    13370000
           DISPLAY AA-PROGRAM-LIT.                                      13380000
           DISPLAY AA-PARAGRAPH-LIT.                                    13390000
           DISPLAY AA-MESSAGE-LINE.                                     13400000
           DISPLAY AA-DB2-OPERATION-LIT.                                13410000
           MOVE +4013                  TO ABEND-CODE.                   13420000
                                                                        13430000
           COPY DPPD004.                                                13440000
           CALL 'ILBOABN0' USING ABEND-CODE.                            13450000
