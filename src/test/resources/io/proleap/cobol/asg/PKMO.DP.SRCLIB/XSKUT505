       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XSKUT505.                                                  
       AUTHOR.       COGNIZANT.                                                 
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 02/25/2015.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS IS A DATA CORRECTION PROGRAM WHICH WILL EXTRACT DATA               
      * FOR STORE LEVEL RECORD WHEN STORE 0 HAVE SAME PRICE AS STORE AND        
      * REPORTED IN XSK075 INTEGRITY REPORT.                                    
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      * WR/PBI         DATE              DESCRIPTION OF CHANGES                 
      * CHG0111864     06/12/2015        NEW PROGRAM                            
      ******************************************************************        
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SKU-LOC-INP-FILE ASSIGN TO INP01.                             
           SELECT SKU-LOC-DEL-FILE ASSIGN TO OUT01.                             
           SELECT SKU-LOC-ATTR-DEL-FILE ASSIGN TO OUT02.                        
           SELECT SKU-LOC-RPT-FILE ASSIGN TO OUT03.                             
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  SKU-LOC-INP-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-LOC-INPUT-RECORD             PIC X(201).                         
                                                                                
       FD  SKU-LOC-DEL-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-LOC-DEL-RECORD               PIC X(080).                         
                                                                                
       FD  SKU-LOC-ATTR-DEL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-LOC-ATTR-DEL-RECORD          PIC X(080).                         
                                                                                
       FD  SKU-LOC-RPT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-LOC-RPT-RECORD               PIC X(080).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-DEADLOCK-COUNTER           PIC S9(04)  VALUE +0.               
           05 PV-PARAGRAPH-NAME             PIC  X(30)  VALUE SPACES.           
           05 PV-SQLCODE-DISPLAY            PIC  +(8)9  VALUE ZEROES.           
           05 PV-DB2-OPER-ATTEMPTED         PIC  X(30)  VALUE SPACES.           
           05 PV-ZERO-STR-NBR               PIC  S9(04) COMP.                   
           05 PV-ZERO-PRMPT-PRIC-IND        PIC  X(01)  VALUE SPACES.           
           05 PV-SQL-SUB                    PIC  9(03)  VALUE ZEROES.           
           05 PV-NZERO-PRMPT-PRIC-IND       PIC  X(01)  VALUE SPACES.           
           05 PV-COUNT                      PIC S9(04) VALUE +0 COMP.           
           05 PV-RET-AMT-NULL-IND           PIC S9(04) VALUE +0 COMP.           
           05 PV-END-TMST                   PIC S9(04) VALUE +0 COMP.   00310003
           05 PV-NULL-GP-PRIC-ID            PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-GP-PRIC-ID-NULL             VALUE -1.                     
           05 PV-NULL-GP-RTL-QTY            PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-GP-RTL-QTY-NULL             VALUE -1.                     
           05 PV-NULL-GP-RTL-AMT            PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-GP-RTL-AMT-NULL             VALUE -1.                     
           05 PV-NULL-MKDN-CDE              PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-MKDN-CDE-NULL               VALUE -1.                     
           05 PV-NULL-ORIG-CLR-DTE          PIC S9(04) VALUE +0 COMP.   00310003
               88  PV-ORIG-CLR-DTE-NULL           VALUE -1.                     
           05 PV-HIST-INTR-UPC-ID           PIC S9(10) COMP VALUE ZEROS.        
           05 PC-MAX-DEADLOCKS              PIC  S9(04) VALUE +3                
                                                        COMP SYNC.              
           05 PC-MAX-RETRIES                PIC  S9(04) VALUE +3 COMP.          
           05 PV-SOR-RETAIL-AMOUNT          PIC  S9(7)V9(2) COMP-3.             
           05 PV-IN-UPC-ID                  PIC  S9(10) VALUE ZEROES.           
           05 PV-IN-XS-INTR-UPC-ID          PIC S9(10) COMP VALUE ZEROS.        
           05 PV-IN-LOC-NB                  PIC  S9(09) VALUE ZEROES.           
           05 PV-IN-XS-LOC-NBR              PIC  S9(09) VALUE +0 COMP.          
                                                                                
           05 PV-ZERO-LEVEL-REC.                                                
              10 PV-ZERO-INTR-UPC-ID        PIC  S9(09) COMP.                   
              10 PV-ZERO-LOC-NBR            PIC  S9(04) COMP.                   
              10 PV-ZERO-BEG-TMST           PIC   X(26).                        
              10 PV-ZERO-END-TMST           PIC   X(26).                        
              10 PV-ZERO-UNT-RTL-AMT        PIC  S9(7)V9(2) COMP-3.             
              10 PV-ZERO-UNT-COST-AMT       PIC  S9(7)V9(3) COMP-3.             
              10 PV-ZERO-LOC-SKU-STAT-CDE   PIC   X(02).                        
              10 PV-ZERO-UPC-STAT-CHG-DTE   PIC   X(10).                        
              10 PV-ZERO-UNT-RTL-CHG-DTE    PIC   X(10).                        
              10 PV-ZERO-GP-PRIC-ID         PIC  S9(09) COMP.                   
              10 PV-ZERO-GP-RTL-QTY         PIC  S9(04) COMP.                   
              10 PV-ZERO-GP-RTL-AMT         PIC  S9(7)V9(2) COMP-3.             
              10 PV-ZERO-LAST-UPD-TMST      PIC   X(26).                        
              10 PV-ZERO-SOR-LAST-MSG-TMST  PIC   X(26).                        
              10 PV-ZERO-MKDN-CDE           PIC   X(01).                        
              10 PV-ZERO-ORIG-CLR-DTE       PIC   X(10).                        
                                                                                
           05 PV-NZERO-LEVEL-REC.                                               
              10 PV-NZERO-INTR-UPC-ID       PIC  S9(9) COMP.                    
              10 PV-NZERO-LOC-NBR           PIC  S9(4) COMP.                    
              10 PV-NZERO-BEG-TMST          PIC   X(26).                        
              10 PV-NZERO-END-TMST          PIC   X(26).                        
              10 PV-NZERO-UNT-RTL-AMT       PIC  S9(7)V9(2) COMP-3.             
              10 PV-NZERO-UNT-COST-AMT      PIC  S9(7)V9(3) COMP-3.             
              10 PV-NZERO-LOC-SKU-STAT-CDE  PIC   X(2).                         
              10 PV-NZERO-UPC-STAT-CHG-DTE  PIC   X(10).                        
              10 PV-NZERO-UNT-RTL-CHG-DTE   PIC   X(10).                        
              10 PV-NZERO-GP-PRIC-ID        PIC  S9(9) COMP.                    
              10 PV-NZERO-GP-RTL-QTY        PIC  S9(4) COMP.                    
              10 PV-NZERO-GP-RTL-AMT        PIC  S9(7)V9(2) COMP-3.             
              10 PV-NZERO-LAST-UPD-TMST     PIC   X(26).                        
              10 PV-NZERO-SOR-LAST-MSG-TMST PIC   X(26).                        
              10 PV-NZERO-MKDN-CDE          PIC   X(1).                         
              10 PV-NZERO-ORIG-CLR-DTE      PIC   X(10).                        
                                                                                
       01  CC-INP-FILE.                                                         
           05  FILLER                       PIC X(19) VALUE SPACES.             
           05  CC-IN-XS-INTR-UPC-ID         PIC 9(10) VALUE ZEROS.              
           05  FILLER                       PIC X(01) VALUE SPACES.             
           05  CC-IN-XS-STR-NBR             PIC 9(04) VALUE ZEROS.              
           05  FILLER                       PIC X(03) VALUE SPACES.             
           05  CC-IN-BEG-DT                 PIC X(10) VALUE SPACES.             
           05  FILLER                       PIC X(93) VALUE SPACES.             
           05  CC-IN-ERROR-MESSAGE          PIC X(52) VALUE SPACES.             
           05  FILLER                       PIC X(02) VALUE SPACES.             
                                                                                
       01  CC-OUT1-FILE.                                                        
           05  CC-OUT1-XS-INTR-UPC-ID       PIC S9(9) VALUE +0 COMP.            
           05  FILLER                       PIC X(01) VALUE SPACES.             
           05  CC-OUT1-XS-LOC-NBR           PIC S9(9) VALUE +0 COMP.            
           05  FILLER                       PIC X(02) VALUE SPACES.             
           05  CC-OUT1-BEG-TMST             PIC X(26) VALUE SPACES.             
                                                                                
       01  CC-OUT2-FILE.                                                        
           05  CC-OUT2-XS-INTR-UPC-ID       PIC S9(9) VALUE +0 COMP.            
           05  FILLER                       PIC X(01) VALUE SPACES.             
           05  CC-OUT2-XS-LOC-NBR           PIC S9(9) VALUE +0 COMP.            
           05  FILLER                       PIC X(02) VALUE SPACES.             
           05  CC-OUT2-PRMPT-PRIC-IND       PIC X(01) VALUE SPACES.             
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW                  PIC X(01)  VALUE 'N'.               
               88  PS-DATA-ERROR                       VALUE 'Y'.               
               88  PS-NO-DATA-ERROR                    VALUE 'N'.               
                                                                                
           05  PS-SOR-DATA-SW               PIC X(01)  VALUE 'N'.               
               88  PS-SOR-DATA-FOUND                   VALUE 'Y'.               
               88  PS-SOR-DATA-NOT-FOUND               VALUE 'N'.               
                                                                                
           05  PS-END-OF-XS-FILE-SW         PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-XS-FILE                   VALUE 'Y'.               
               88  PS-NOT-END-OF-XS-FILE               VALUE 'N'.               
                                                                                
           05  PS-ZERO-LOC-SW               PIC X(01)  VALUE 'N'.               
               88  PS-ZERO-LOC-REC                     VALUE 'Y'.               
               88  PS-NO-ZERO-LOC-REC                  VALUE 'N'.               
                                                                                
           05  PS-ZERO-LOC-ATTR-SW          PIC X(01)  VALUE 'N'.               
               88  PS-ZERO-LOC-ATTR-REC                VALUE 'Y'.               
               88  PS-NO-ZERO-LOC-ATTR-REC             VALUE 'N'.               
                                                                                
           05  PS-NZERO-LOC-SW              PIC X(01)  VALUE 'N'.               
               88  PS-NZERO-LOC-REC                    VALUE 'Y'.               
               88  PS-NO-NZERO-LOC-REC                 VALUE 'N'.               
                                                                                
           05  PS-NZERO-LOC-ATTR-SW         PIC X(01)  VALUE 'N'.               
               88  PS-NZERO-LOC-ATTR-REC               VALUE 'Y'.               
               88  PS-NO-NZERO-LOC-ATTR-REC            VALUE 'N'.               
                                                                                
           05  PS-SOR-RET-PRICE-SW          PIC X(01)  VALUE 'N'.               
               88  PS-SOR-RET-PRICE-MATCH              VALUE 'Y'.               
               88  PS-SOR-RET-PRICE-NMATCH             VALUE 'N'.               
                                                                                
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-SKU-LOC-ROWS-READ         PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-ATTR-DEL-ROWS     PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-DEL-ROWS          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-SKU-LOC-RPT-ROWS          PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE 0.                 
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.             
           88  ABEND-4002-CC-INVALID-ERR               VALUE +4002.             
                                                                                
      *****************************************************************         
      * XST_SKU_LOC TABLE DCLGEN.                                               
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XST00008                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * XST_SKU_LOC_ATTR TABLE DCLGEN.                                          
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XST00041                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * TUPCPLS TABLE DCLGEN.                                                   
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE TUPCPLS                                                  
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 4000-READ-SKU-LOC-ERROR-FILE.                                
                                                                                
           PERFORM 2000-PROCESS-EXTRA-RECORDS                                   
             UNTIL PS-END-OF-XS-FILE.                                           
                                                                                
           PERFORM 5000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           INITIALIZE PV-HIST-INTR-UPC-ID.                                      
           SET PS-NOT-END-OF-XS-FILE       TO TRUE                              
           MOVE ZEROS                      TO PV-ZERO-STR-NBR                   
                                                                                
           OPEN INPUT SKU-LOC-INP-FILE                                          
                OUTPUT SKU-LOC-DEL-FILE                                         
                       SKU-LOC-ATTR-DEL-FILE                                    
                       SKU-LOC-RPT-FILE.                                        
                                                                                
      ******************************************************************        
      * 4000-READ-SKU-LOC-ERROR-FILE.                                           
      ******************************************************************        
       4000-READ-SKU-LOC-ERROR-FILE.                                            
                                                                                
           INITIALIZE CC-INP-FILE                                               
                                                                                
           READ SKU-LOC-INP-FILE                                                
           INTO CC-INP-FILE                                                     
             AT END                                                             
               SET PS-END-OF-XS-FILE     TO TRUE                                
             NOT AT END                                                         
               ADD +1                    TO  PA-SKU-LOC-ROWS-READ               
           END-READ.                                                            
                                                                                
           MOVE CC-IN-XS-INTR-UPC-ID     TO PV-IN-UPC-ID                        
           MOVE PV-IN-UPC-ID             TO PV-IN-XS-INTR-UPC-ID                
           MOVE CC-IN-XS-STR-NBR         TO PV-IN-LOC-NB                        
           MOVE PV-IN-LOC-NB             TO PV-IN-XS-LOC-NBR.                   
                                                                                
      ******************************************************************        
      * 2000-PROCESS-SKU-LOC-RECS.                                              
      ******************************************************************        
       2000-PROCESS-EXTRA-RECORDS.                                              
                                                                                
           IF  CC-IN-XS-STR-NBR NOT EQUAL ZERO                                  
                                                                                
               IF  PV-IN-XS-INTR-UPC-ID NOT EQUAL PV-HIST-INTR-UPC-ID           
                   SET PS-NO-ZERO-LOC-REC        TO TRUE                        
                   SET PS-NO-ZERO-LOC-ATTR-REC   TO TRUE                        
                   PERFORM 2050-SELECT-XST-SKU-LOC                              
                   PERFORM 2100-SELECT-XST-SKU-LOC-ATTR                         
                   SET PS-SOR-RET-PRICE-MATCH TO TRUE                           
                   IF  PS-ZERO-LOC-REC                                          
                       PERFORM 2110-SELECT-TUPCPLS-ZERO                         
                   END-IF                                                       
                   MOVE PV-IN-XS-INTR-UPC-ID     TO PV-HIST-INTR-UPC-ID         
               END-IF                                                           
                                                                                
               SET PS-NO-NZERO-LOC-REC           TO TRUE                        
               SET PS-NO-NZERO-LOC-ATTR-REC      TO TRUE                        
                                                                                
               PERFORM 2150-SELECT-NZERO-LOC                                    
               PERFORM 2200-SELECT-NZERO-LOC-ATTR                               
                                                                                
                                                                                
               IF  PS-ZERO-LOC-REC                                              
               AND PS-ZERO-LOC-ATTR-REC                                         
               AND PS-SOR-RET-PRICE-MATCH                                       
               AND PS-NZERO-LOC-REC                                             
               AND PS-NZERO-LOC-ATTR-REC                                        
               AND PV-ZERO-INTR-UPC-ID  = PV-NZERO-INTR-UPC-ID                  
               AND PV-ZERO-UNT-RTL-AMT  = PV-NZERO-UNT-RTL-AMT                  
               AND PV-ZERO-UNT-COST-AMT = PV-NZERO-UNT-COST-AMT                 
               AND PV-ZERO-LOC-SKU-STAT-CDE = PV-NZERO-LOC-SKU-STAT-CDE         
               AND PV-ZERO-GP-PRIC-ID = PV-NZERO-GP-PRIC-ID                     
               AND PV-ZERO-GP-RTL-QTY = PV-NZERO-GP-RTL-QTY                     
               AND PV-ZERO-GP-RTL-AMT = PV-NZERO-GP-RTL-AMT                     
               AND PV-ZERO-PRMPT-PRIC-IND = PV-NZERO-PRMPT-PRIC-IND             
                                                                                
                   PERFORM 2300-SELECT-TUPCPLS                                  
                                                                                
                   IF  PS-SOR-DATA-NOT-FOUND                                    
                       PERFORM 3500-WRITE-DEL-REC                               
                       PERFORM 3800-WRITE-DEL-ATTR-REC                          
                   END-IF                                                       
                                                                                
               ELSE                                                             
                   PERFORM 3000-WRITE-ERROR-REC                                 
               END-IF                                                           
           END-IF                                                               
                                                                                
           PERFORM 4000-READ-SKU-LOC-ERROR-FILE.                                
                                                                                
      ******************************************************************        
      * 2050-SELECT-XST-SKU-LOC                                                 
      ******************************************************************        
       2050-SELECT-XST-SKU-LOC.                                                 
                                                                                
               EXEC SQL                                                         
                   SELECT INTR_UPC_ID                                           
                         ,LOC_NBR                                               
                         ,BEG_TMST                                              
                         ,END_TMST                                              
                         ,UNT_RTL_AMT                                           
                         ,UNT_COST_AMT                                          
                         ,LOC_SKU_STAT_CDE                                      
                         ,UPC_STAT_CHG_DTE                                      
                         ,UNT_RTL_CHG_DTE                                       
                         ,GP_PRIC_ID                                            
                         ,GP_RTL_QTY                                            
                         ,GP_RTL_AMT                                            
                         ,LAST_UPD_TMST                                         
                         ,SOR_LAST_MSG_TMST                                     
                         ,MKDN_CDE                                              
                         ,ORIG_CLR_DTE                                          
                     INTO :PV-ZERO-INTR-UPC-ID                                  
                         ,:PV-ZERO-LOC-NBR                                      
                         ,:PV-ZERO-BEG-TMST                                     
                         ,:PV-ZERO-END-TMST :PV-END-TMST                        
                         ,:PV-ZERO-UNT-RTL-AMT                                  
                         ,:PV-ZERO-UNT-COST-AMT                                 
                         ,:PV-ZERO-LOC-SKU-STAT-CDE                             
                         ,:PV-ZERO-UPC-STAT-CHG-DTE                             
                         ,:PV-ZERO-UNT-RTL-CHG-DTE                              
                         ,:PV-ZERO-GP-PRIC-ID :PV-NULL-GP-PRIC-ID               
                         ,:PV-ZERO-GP-RTL-QTY :PV-NULL-GP-RTL-QTY               
                         ,:PV-ZERO-GP-RTL-AMT :PV-NULL-GP-RTL-AMT               
                         ,:PV-ZERO-LAST-UPD-TMST                                
                         ,:PV-ZERO-SOR-LAST-MSG-TMST                            
                         ,:PV-ZERO-MKDN-CDE :PV-NULL-MKDN-CDE                   
                         ,:PV-ZERO-ORIG-CLR-DTE :PV-NULL-ORIG-CLR-DTE           
                   FROM XST_SKU_LOC                                             
                     WHERE INTR_UPC_ID  = :PV-IN-XS-INTR-UPC-ID                 
                       AND LOC_NBR = :PV-ZERO-STR-NBR                           
                       AND END_TMST IS NULL                                     
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-ZERO-LOC-REC   TO TRUE                           
                        IF PV-GP-PRIC-ID-NULL                                   
                           MOVE ZERO          TO PV-ZERO-GP-PRIC-ID             
                        END-IF                                                  
                        IF PV-GP-RTL-QTY-NULL                                   
                           MOVE ZERO          TO PV-ZERO-GP-RTL-QTY             
                        END-IF                                                  
                        IF PV-GP-RTL-AMT-NULL                                   
                           MOVE ZERO          TO PV-ZERO-GP-RTL-AMT             
                        END-IF                                                  
                        IF PV-MKDN-CDE-NULL                                     
                           MOVE ZERO          TO PV-ZERO-MKDN-CDE               
                        END-IF                                                  
                        IF PV-ORIG-CLR-DTE-NULL                                 
                           MOVE ZERO          TO PV-ZERO-ORIG-CLR-DTE           
                        END-IF                                                  
                                                                                
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-ZERO-LOC-REC   TO TRUE                        
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2050-SELECT-XST-SKU-LOC'                          
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT ZERO REC FROM LOC'                         
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2100-SELECT-XST-SKU-LOC-ATTR                                            
      ******************************************************************        
       2100-SELECT-XST-SKU-LOC-ATTR.                                            
                                                                                
               EXEC SQL                                                         
                   SELECT PRMPT_FOR_PRIC_IND                                    
                     INTO :PV-ZERO-PRMPT-PRIC-IND                               
                   FROM XST_SKU_LOC_ATTR                                        
                     WHERE INTR_UPC_ID  = :PV-IN-XS-INTR-UPC-ID                 
                       AND LOC_NBR = :PV-ZERO-STR-NBR                           
                   WITH UR                                                      
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-ZERO-LOC-ATTR-REC TO TRUE                        
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-ZERO-LOC-ATTR-REC TO TRUE                     
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2100-SELECT-XST-SKU-LOC-ATTR'                     
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT ZERO REC ATTR'                             
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2110-SELECT-TUPCPLS-ZERO.                                               
      ******************************************************************        
       2110-SELECT-TUPCPLS-ZERO.                                                
                                                                                
               EXEC SQL                                                         
                   SELECT UNT_RTL_AMT                                           
                     INTO :PV-SOR-RETAIL-AMOUNT                                 
                          :PV-RET-AMT-NULL-IND                                  
                   FROM TUPCPLS                                                 
                     WHERE UPC_ID  = :PV-IN-XS-INTR-UPC-ID                      
                       AND STR_NBR = :PV-ZERO-STR-NBR                           
                       AND EFF_END_TMST IS NULL                                 
                   WITH UR                                                      
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        IF PV-RET-AMT-NULL-IND < 0                              
                            MOVE ZEROES         TO PV-SOR-RETAIL-AMOUNT         
                        END-IF                                                  
                        IF  PV-ZERO-UNT-RTL-AMT  = PV-SOR-RETAIL-AMOUNT         
                            SET PS-SOR-RET-PRICE-MATCH TO TRUE                  
                        ELSE                                                    
                            SET PS-SOR-RET-PRICE-NMATCH TO TRUE                 
                        END-IF                                                  
                    WHEN SQLCODE = +100                                         
                        SET PS-SOR-RET-PRICE-NMATCH TO TRUE                     
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2110-SELECT-TUPCPLS-ZERO'                         
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT TUPCPLS TABLE'                             
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2150-SELECT-NZERO-LOC                                                   
      ******************************************************************        
       2150-SELECT-NZERO-LOC.                                                   
                                                                                
               EXEC SQL                                                         
                   SELECT INTR_UPC_ID                                           
                         ,LOC_NBR                                               
                         ,BEG_TMST                                              
                         ,END_TMST                                              
                         ,UNT_RTL_AMT                                           
                         ,UNT_COST_AMT                                          
                         ,LOC_SKU_STAT_CDE                                      
                         ,UPC_STAT_CHG_DTE                                      
                         ,UNT_RTL_CHG_DTE                                       
                         ,GP_PRIC_ID                                            
                         ,GP_RTL_QTY                                            
                         ,GP_RTL_AMT                                            
                         ,LAST_UPD_TMST                                         
                         ,SOR_LAST_MSG_TMST                                     
                         ,MKDN_CDE                                              
                         ,ORIG_CLR_DTE                                          
                     INTO :PV-NZERO-INTR-UPC-ID                                 
                         ,:PV-NZERO-LOC-NBR                                     
                         ,:PV-NZERO-BEG-TMST                                    
                         ,:PV-NZERO-END-TMST :PV-END-TMST                       
                         ,:PV-NZERO-UNT-RTL-AMT                                 
                         ,:PV-NZERO-UNT-COST-AMT                                
                         ,:PV-NZERO-LOC-SKU-STAT-CDE                            
                         ,:PV-NZERO-UPC-STAT-CHG-DTE                            
                         ,:PV-NZERO-UNT-RTL-CHG-DTE                             
                         ,:PV-NZERO-GP-PRIC-ID :PV-NULL-GP-PRIC-ID              
                         ,:PV-NZERO-GP-RTL-QTY :PV-NULL-GP-RTL-QTY              
                         ,:PV-NZERO-GP-RTL-AMT :PV-NULL-GP-RTL-AMT              
                         ,:PV-NZERO-LAST-UPD-TMST                               
                         ,:PV-NZERO-SOR-LAST-MSG-TMST                           
                         ,:PV-NZERO-MKDN-CDE :PV-NULL-MKDN-CDE                  
                         ,:PV-NZERO-ORIG-CLR-DTE :PV-NULL-ORIG-CLR-DTE          
                   FROM XST_SKU_LOC                                             
                     WHERE INTR_UPC_ID  = :PV-IN-XS-INTR-UPC-ID                 
                       AND LOC_NBR = :PV-IN-XS-LOC-NBR                          
                       AND END_TMST IS NULL                                     
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-NZERO-LOC-REC   TO TRUE                          
                        IF PV-GP-PRIC-ID-NULL                                   
                           MOVE ZERO          TO PV-NZERO-GP-PRIC-ID            
                        END-IF                                                  
                        IF PV-GP-RTL-QTY-NULL                                   
                           MOVE ZERO          TO PV-NZERO-GP-RTL-QTY            
                        END-IF                                                  
                        IF PV-GP-RTL-AMT-NULL                                   
                           MOVE ZERO          TO PV-NZERO-GP-RTL-AMT            
                        END-IF                                                  
                        IF PV-MKDN-CDE-NULL                                     
                           MOVE ZERO          TO PV-NZERO-MKDN-CDE              
                        END-IF                                                  
                        IF PV-ORIG-CLR-DTE-NULL                                 
                           MOVE ZERO          TO PV-NZERO-ORIG-CLR-DTE          
                        END-IF                                                  
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-NZERO-LOC-REC   TO TRUE                       
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2150-SELECT-NZERO-LOC'                            
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT NON ZERO REC FROM LOC'                     
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2200-SELECT-NZERO-LOC-ATTR                                              
      ******************************************************************        
       2200-SELECT-NZERO-LOC-ATTR.                                              
                                                                                
               EXEC SQL                                                         
                   SELECT PRMPT_FOR_PRIC_IND                                    
                     INTO :PV-NZERO-PRMPT-PRIC-IND                              
                   FROM XST_SKU_LOC_ATTR                                        
                     WHERE INTR_UPC_ID  = :PV-IN-XS-INTR-UPC-ID                 
                       AND LOC_NBR = :PV-IN-XS-LOC-NBR                          
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-NZERO-LOC-ATTR-REC TO TRUE                       
                    WHEN SQLCODE = +100                                         
                        SET PS-NO-NZERO-LOC-ATTR-REC TO TRUE                    
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2200-SELECT-NZERO-LOC-ATTR'                       
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT NON ZERO REC ATTR'                         
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 2300-SELECT-TUPCPLS.                                                    
      ******************************************************************        
       2300-SELECT-TUPCPLS.                                                     
                                                                                
           SET PS-SOR-DATA-NOT-FOUND TO TRUE.                                   
                                                                                
               EXEC SQL                                                         
                   SELECT 1                                                     
                     INTO :PV-COUNT                                             
                   FROM TUPCPLS                                                 
                     WHERE UPC_ID  = :PV-IN-XS-INTR-UPC-ID                      
                       AND STR_NBR = :PV-IN-XS-LOC-NBR                          
                       AND EFF_END_TMST IS NULL                                 
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                    WHEN SQLCODE = +0                                           
                        SET PS-SOR-DATA-FOUND     TO TRUE                       
                    WHEN SQLCODE = +100                                         
                        SET PS-SOR-DATA-NOT-FOUND TO TRUE                       
                    WHEN OTHER                                                  
                        DISPLAY 'INTR-UPC-ID = ' CC-IN-XS-INTR-UPC-ID           
                        DISPLAY 'LOC-NBR IN FILE = ' CC-IN-XS-STR-NBR           
                        MOVE '2300-SELECT-TUPCPLS'                              
                                     TO PV-PARAGRAPH-NAME                       
                        MOVE 'SELECT TUPCPLS TABLE'                             
                                     TO PV-DB2-OPER-ATTEMPTED                   
                        PERFORM 9100-SQL-ABEND                                  
               END-EVALUATE.                                                    
                                                                                
      ******************************************************************        
      * 5000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       5000-EOJ-ROUTINE.                                                        
                                                                                
           CLOSE SKU-LOC-INP-FILE                                               
                 SKU-LOC-DEL-FILE                                               
                 SKU-LOC-ATTR-DEL-FILE                                          
                 SKU-LOC-RPT-FILE.                                              
                                                                                
           PERFORM 9005-DISPLAY-TOTALS.                                         
                                                                                
      ******************************************************************        
      * 3000-WRITE-ERROR-REC                                                    
      ******************************************************************        
       3000-WRITE-ERROR-REC.                                                    
                                                                                
           WRITE SKU-LOC-RPT-RECORD                                             
             FROM CC-INP-FILE                                                   
             ADD +1                  TO  PA-SKU-LOC-RPT-ROWS.                   
                                                                                
      ******************************************************************        
      * 3500-WRITE-DEL-REC                                                      
      ******************************************************************        
       3500-WRITE-DEL-REC.                                                      
                                                                                
           INITIALIZE CC-OUT1-FILE                                              
                                                                                
           MOVE PV-NZERO-INTR-UPC-ID     TO CC-OUT1-XS-INTR-UPC-ID              
           MOVE PV-NZERO-LOC-NBR         TO CC-OUT1-XS-LOC-NBR                  
           MOVE PV-NZERO-BEG-TMST        TO CC-OUT1-BEG-TMST                    
                                                                                
           WRITE SKU-LOC-DEL-RECORD                                             
             FROM CC-OUT1-FILE.                                                 
           ADD +1                    TO  PA-SKU-LOC-DEL-ROWS.                   
                                                                                
      ******************************************************************        
      * 3800-WRITE-DEL-REC                                                      
      ******************************************************************        
       3800-WRITE-DEL-ATTR-REC.                                                 
                                                                                
           INITIALIZE CC-OUT2-FILE                                              
                                                                                
           MOVE PV-NZERO-INTR-UPC-ID     TO CC-OUT2-XS-INTR-UPC-ID              
           MOVE PV-NZERO-LOC-NBR         TO CC-OUT2-XS-LOC-NBR                  
           MOVE PV-NZERO-PRMPT-PRIC-IND  TO CC-OUT2-PRMPT-PRIC-IND              
                                                                                
           WRITE SKU-LOC-ATTR-DEL-RECORD                                        
             FROM CC-OUT2-FILE.                                                 
             ADD +1                      TO  PA-SKU-LOC-ATTR-DEL-ROWS.          
                                                                                
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)           
      ******************************************************************        
       9000-DEADLOCK-ERROR.                                                     
                                                                                
               MOVE 'DEADLOCK WHILE DELETING' TO PV-DB2-OPER-ATTEMPTED          
               PERFORM 9100-SQL-ABEND.                                          
                                                                                
      ******************************************************************        
      * 9100-SQL-ABEND.                                                         
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY ' '.                                                         
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = XSKUT505'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPER-ATTEMPTED.                  
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * 9005-DISPLAY-TOTALS.                                                    
      ******************************************************************        
       9005-DISPLAY-TOTALS.                                                     
                                                                                
            DISPLAY '******** XSKUT505 TOTALS *********'.                       
            DISPLAY ' SKU-LOC RECS READ    : ' PA-SKU-LOC-ROWS-READ.            
            DISPLAY ' SKU-LOC RECS WRITTEN : ' PA-SKU-LOC-DEL-ROWS.             
            DISPLAY ' SKU-LOC-ATTR RECS WRITTEN: '                              
                                               PA-SKU-LOC-ATTR-DEL-ROWS.        
            DISPLAY ' SKU-LOC ERROR RECS   : ' PA-SKU-LOC-RPT-ROWS.             
            DISPLAY '***** END OF XSKUT505 TOTALS *****'.                       
            DISPLAY ' '.                                                        
