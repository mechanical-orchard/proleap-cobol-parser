000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400                                                                          
000500 PROGRAM-ID.             SFKUT004.                                        
000600 AUTHOR.                 COGNIZANT.                                       
000700 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000800 DATE-WRITTEN            MARCH 2016.                                      
000900 DATE-COMPILED.                                                           
001000                                                                          
001100*****************************************************************         
001200*  DESCRIPTION:                                                           
001300*  THIS PROGRAM WILL FORMAT THE BUSINESS GROUP FILE TO ASSOCIATE          
001400*  BUSINESS GROUP FILE LAYOUT.                                            
001600*                                                                         
001700*  INPUT:  BUSINESS GROUP FILE                                            
001800*  OUTPUT: ASSOCIATE BUSINESS GROUP FILE                                  
001900*                                                                         
002000*****************************************************************         
002100*----------------------------------------------------------------*        
002200* HISTORY LOG:                                                   *        
002300*                                                                *        
002400* DATE:        03/28/2016                                        *        
002500* WR/IR#:      CHG0146661                                        *        
002600* PROGRAMMER:  COGNIZANT                                         *        
002700* DESCRIPTION: INITIAL VERSION CREATED FOR RMS9 RETIREMENT       *        
002800*              PROJECT                                           *        
002900*                                                                *        
003000*----------------------------------------------------------------*        
003100*****************************************************************         
003200 ENVIRONMENT DIVISION.                                                    
003300*****************************************************************         
003400 CONFIGURATION SECTION.                                                   
003500 SOURCE-COMPUTER.        IBM-3090.                                        
003600 OBJECT-COMPUTER.        IBM-3090.                                        
003700                                                                          
003800 INPUT-OUTPUT SECTION.                                                    
003900                                                                          
004000 FILE-CONTROL.                                                            
004100                                                                          
004200     SELECT BUSGRP-INPUT-FILE                                             
004300         ASSIGN TO INP01.                                                 
004400                                                                          
004500     SELECT BUSGRP-OUTPUT-FILE                                            
004600         ASSIGN TO OUT01.                                                 
004700                                                                          
004800******************************************************************        
004900 DATA DIVISION.                                                           
005000******************************************************************        
005100 FILE SECTION.                                                            
005200                                                                          
005300 FD  BUSGRP-INPUT-FILE                                                    
005400     RECORDING MODE IS F                                                  
005500     BLOCK CONTAINS 0 RECORDS                                             
005600     LABEL RECORDS ARE STANDARD                                           
005700     RECORD CONTAINS 34 CHARACTERS.                                       
005800                                                                          
005900 01  BUSGRP-INPUT-RECORD         PIC X(34).                               
006000                                                                          
006100 FD  BUSGRP-OUTPUT-FILE                                                   
006200     RECORDING MODE IS F                                                  
006300     BLOCK CONTAINS 0 RECORDS                                             
006400     LABEL RECORDS ARE STANDARD                                           
006500     RECORD CONTAINS 80 CHARACTERS.                                       
006600                                                                          
006700 01  BUSGRP-OUTPUT-RECORD        PIC X(80).                               
006800                                                                          
006900******************************************************************        
007000 WORKING-STORAGE SECTION.                                                 
007100******************************************************************        
007200 01  FILLER                            PIC X(50)  VALUE                   
007300     ' *** WORKING STORAGE STARTS HERE FOR SFKUT004 *** '.                
007400                                                                          
007500 01  PS-PROGRAM-SWITCHES.                                                 
007600     05  PS-END-OF-BUSGRP-SW           PIC X(01)  VALUE 'N'.              
007700         88  PS-NOT-END-OF-BUSGRP                 VALUE 'N'.              
007800         88  PS-END-OF-BUSGRP                     VALUE 'Y'.              
007900                                                                          
008000 01  PC-CONSTANTS.                                                        
008100     05  PC-PROGRAM-NAME               PIC  X(08) VALUE                   
008200                                                 'SFKUT004'.              
008300 01  PV-PROGRAM-VARIABLES.                                                
008400     05  PV-BUGGRP-READ-COUNT          PIC  9(09) VALUE ZEROES.           
008500     05  PV-BUSGRP-WRITE-COUNT         PIC  9(09) VALUE ZEROES.           
008700                                                                          
008800******************************************************************        
008900* INPUT FILE LAYOUT                                                       
009000******************************************************************        
009100     COPY HMRD014.                                                        
009200                                                                          
008800******************************************************************        
008900* OUTPUT FILE LAYOUT                                                      
009000******************************************************************        
009000 01 PV-BUSINESS-GROUP-REC.                                                
009000    05 FILLER                          PIC  X(01) VALUE SPACES.           
009000    05 PV-BUS-GRP-NBR                  PIC  9(03) VALUE ZEROES.           
009000    05 FILLER                          PIC  X(02) VALUE SPACES.           
009000    05 PV-BUS-GRP-AREA-ID              PIC  9(02) VALUE ZEROES.           
009000    05 FILLER                          PIC  X(01) VALUE SPACES.           
009000    05 PV-BUS-GRP-DESC                 PIC  X(25) VALUE SPACES.           
009000    05 FILLER                          PIC  X(01) VALUE SPACES.           
009000    05 PV-BUS-GRP-SHRT-DESC            PIC  X(08) VALUE SPACES.           
009000    05 FILLER                          PIC  X(37) VALUE SPACES.           
009000                                                                          
010800******************************************************************        
010900                                                                          
011000 PROCEDURE DIVISION.                                                      
011100                                                                          
011200 0000-MAINLINE-PARA.                                                      
011300                                                                          
011400     PERFORM 1000-INITIALIZE.                                             
011500                                                                          
011600     PERFORM 2000-READ-BUSGRP                                             
011800       UNTIL PS-END-OF-BUSGRP.                                            
011800                                                                          
011900     PERFORM 9000-EOJ-ROUTINE.                                            
012000                                                                          
012100     STOP RUN.                                                            
012200                                                                          
012300******************************************************************        
012400* 1000-INITIALIZE                                                         
012500*     INITIALIZE THE VARIABLES, OPEN THE INPUT AND OUTPUT FILES           
012600*     AND READ THE INPUT FILE                                             
012700******************************************************************        
012800 1000-INITIALIZE.                                                         
012900                                                                          
013000     DISPLAY 'PROGRAM - ' PC-PROGRAM-NAME.                                
013200     INITIALIZE PV-PROGRAM-VARIABLES.                                     
013300                                                                          
013400     OPEN INPUT  BUSGRP-INPUT-FILE                                        
013500          OUTPUT BUSGRP-OUTPUT-FILE.                                      
013600                                                                          
013900******************************************************************        
014000* 2000-READ-BUSGRP                                                        
014000*      READ THE RECORDS IN BUSINESS GROUP FILE                            
014700******************************************************************        
014800 2000-READ-BUSGRP.                                                        
014900                                                                          
017000     READ BUSGRP-INPUT-FILE INTO HMRD014-BUS-GP-RECORD                    
017100        AT END                                                            
017200           SET PS-END-OF-BUSGRP TO TRUE                                   
017300        NOT AT END                                                        
017400           ADD 1               TO PV-BUGGRP-READ-COUNT                    
017400           PERFORM 2100-WRITE-BUSGRP-OUTPUT                               
017500     END-READ.                                                            
015000                                                                          
020200******************************************************************        
020300* 2100-WRITE-BUSGRP-OUTPUT                                                
020400*      WRITE THE RECORDS TO OUTPUT FILE                                   
020500******************************************************************        
020600 2100-WRITE-BUSGRP-OUTPUT.                                                
020700                                                                          
020700     MOVE HM014-BUS-GP-NBR     TO PV-BUS-GRP-NBR.                         
020700     MOVE HM014-BUS-GP-AREA-ID TO PV-BUS-GRP-AREA-ID.                     
020700     MOVE HM014-BUS-GP-DESC    TO PV-BUS-GRP-DESC.                        
020700     MOVE SPACES               TO PV-BUS-GRP-SHRT-DESC.                   
020700                                                                          
020800     WRITE BUSGRP-OUTPUT-RECORD FROM PV-BUSINESS-GROUP-REC.               
020900     ADD 1                     TO PV-BUSGRP-WRITE-COUNT.                  
021000                                                                          
021100******************************************************************        
021200* 9000-EOJ-ROUTINE                                                        
021300*      CLOSE THE INPUT AND OUTPUT FILES, DISPLAY THE MESSAGES             
021400*      AND TERMINATE THE PROCESS.                                         
021500******************************************************************        
021600 9000-EOJ-ROUTINE.                                                        
021700                                                                          
021800     CLOSE BUSGRP-INPUT-FILE                                              
021900           BUSGRP-OUTPUT-FILE.                                            
022000                                                                          
022100     IF PV-BUGGRP-READ-COUNT = 0                                          
022200        DISPLAY 'INPUT BUSGRP FILE IS EMPTY'                              
022300     END-IF.                                                              
022400     DISPLAY 'BUSGRP RECORDS READ   : ' PV-BUGGRP-READ-COUNT.             
022500     DISPLAY 'BUSGRP RECORDS WRITTEN: ' PV-BUSGRP-WRITE-COUNT.    .       
022600                                                                          
023300******************************************************************        
