       ID DIVISION.                                                             
       PROGRAM-ID.         MLKUT030.                                            
       AUTHOR.             HEMAVATHY.                                           
       DATE-WRITTEN.       FEB 2016.                                            
       DATE-COMPILED.                                                           
      *************************************************************             
      *   PROGRAM OVERVIEW:                                       *             
      *                                                           *             
      *   THIS PROGRAM WILL TAKE AN INPUT FILE AND                *             
      *   REFORMAT THE DATA INTO THE FORMAT MLWS003 FOR INSURANCE *             
      *   REPORT INPUT FILE.                                      *             
      *                                                           *             
      *                                                           *             
      *   INPUT:                                                  *             
      *                                                           *             
      *     1. CSV FILE                                           *             
      *                                                           *             
      *                                                           *             
      *   OUTPUT:                                                 *             
      *                                                           *             
      *     1. MLWS003 FILE                                       *             
      *                                                           *             
      *************************************************************             
      *   CHANGE LOG                                              *             
      *************************************************************             
      *   PROD DATE     INITIALS     DESCRIPTION                  *             
      *   ----------    --------     ---------------------------  *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-370.                                         
       OBJECT-COMPUTER.        IBM-370.                                         
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CSV-FILE    ASSIGN TO INP01.                                  
           SELECT MLWS003-FILE     ASSIGN TO OUT01.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CSV-FILE                                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS CSV-REC.                                              
       01  CSV-REC                PIC X(80).                                    
                                                                                
       FD  MLWS003-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS MLWS003-REC.                                          
       01  MLWS003-REC                PIC X(80).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                 MLKUT030
       01  ABEND-CODE                          PIC S9(04)  VALUE +0     MLKUT030
                                                           COMP SYNC.   MLKUT030
       01  PV-PROGRAM-VARIABLES.                                        MLKUT030
           05  PV-PARAGRAPH-NAME           PIC  X(30)      VALUE SPACES.MLKUT030
           05  PV-DB2-OPER-ATTEMPTED       PIC  X(30)      VALUE SPACES.MLKUT030
           05  PV-HAVOC-TIMESTAMP          PIC  X(26)      VALUE                
                                           '2018-10-21-00.00.00.000000'.        
           05  PV-END-TMST                 PIC  X(26)      VALUE                
                                           '9999-09-09-23.59.00.000000'.        
           05  PV-STR-NBR                  PIC  9(04)      VALUE 1133.          
                                                                                
                                                                                
      ******************************************************************        
      *  RECORD LAYOUT OF VARIANCE DATA FOR THE DATA WAREHOUSE                  
      ******************************************************************        
           COPY MLWS003.                                                        
                                                                                
      ******************************************************************MLKUT030
      *  SQL ABEND ROUTINE WORKING STORAGE                              MLKUT030
      ******************************************************************MLKUT030
      *                                                                 MLKUT030
           COPY DPWS004.                                                MLKUT030
      *                                                                 MLKUT030
      ******************************************************************        
      *  RECORD LAYOUT FROM INPUT CSV FILE - LRECL 80                  *        
      *    FUTURE REFERENCE:  DATES ARE IN MMDDYY FORMAT               *        
      ******************************************************************        
       01  INPUT-CSV-REC.                                                       
           05 ML-DATE                   PIC 9(06)       VALUE ZEROS.            
           05 FILLER                    PIC X(01)       VALUE SPACES.           
           05 ML-FROM-STORE             PIC X(04)       VALUE SPACES.           
           05 FILLER                    PIC X(01)       VALUE SPACES.           
           05 ML-TO-STORE               PIC X(04)       VALUE SPACES.           
           05 FILLER                    PIC X(01)       VALUE SPACES.           
           05 ML-ITM-NBR                PIC 9(15)       VALUE ZEROS.            
           05 FILLER                    PIC X(1)        VALUE SPACES.           
           05 ML-UNITS                  PIC 9(07)       VALUE ZEROS.            
           05 FILLER                    PIC X(40)       VALUE SPACES.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  WS-EOF-SW                PIC X(01)     VALUE 'N'.                
               88  NOT-EOF                            VALUE 'N'.                
               88  END-OF-FILE                        VALUE 'Y'.                
           05  WS-COUNTER               PIC 9(9)      VALUE ZERO.               
           05  WS-COUNTER1              PIC 9(9)      VALUE ZERO.               
                                                                                
                                                                                
      ******************************************************************MLKUT030
      * DB2 UPC SKU TABLE DCLGEN                                        MLKUT030
      ******************************************************************MLKUT030
           EXEC SQL                                                     MLKUT030
               INCLUDE TUPC                                             MLKUT030
           END-EXEC.                                                    MLKUT030
      *                                                                         
      ******************************************************************        
      *     DCLGEN FOR THE DB2 TUPCPLS TABLE                                    
      ******************************************************************        
            EXEC SQL                                                            
                INCLUDE TUPCPLS                                                 
            END-EXEC.                                                           
      *                                                                         
      ******************************************************************        
      *     DCLGEN FOR THE DB2 TPPCPRC TABLE                                    
      ******************************************************************        
            EXEC SQL                                                            
                INCLUDE TPPCPRC                                                 
            END-EXEC.                                                           
      *                                                                         
      ******************************************************************        
      *     DCLGEN FOR THE DB2 TSKXREF TABLE                                    
      ******************************************************************        
            EXEC SQL                                                            
                INCLUDE TSKXREF                                                 
            END-EXEC.                                                           
      *                                                                         
      ******************************************************************MLKUT030
      * DB2 COMMON AREA DCLGEN                                          MLKUT030
      ******************************************************************MLKUT030
           EXEC SQL                                                     MLKUT030
               INCLUDE SQLCA                                            MLKUT030
           END-EXEC.                                                    MLKUT030
           EJECT                                                        MLKUT030
                                                                        MLKUT030
           COPY SQLCA2.                                                 MLKUT030
      *************************************************************             
                                                                                
       PROCEDURE DIVISION.                                                      
       MAINLINE-ROUTINE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-CSV-FILE                                        
             UNTIL END-OF-FILE.                                                 
                                                                                
           DISPLAY 'NUMBER OF RECORDS PROCESSED: '  WS-COUNTER.                 
           DISPLAY 'NUMBER OF FETCH FOR CORP STR: '  WS-COUNTER1.               
                                                                                
           PERFORM 8000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT CSV-FILE                                                  
               OUTPUT MLWS003-FILE                                              
                                                                                
           READ CSV-FILE INTO INPUT-CSV-REC                                     
             AT END MOVE 'Y' TO WS-EOF-SW.                                      
                                                                                
                                                                                
       2000-PROCESS-CSV-FILE.                                                   
                                                                                
           PERFORM 2400-WRITE-MLWS003                                           
                                                                                
                                                                                
           READ CSV-FILE INTO INPUT-CSV-REC                                     
             AT END MOVE 'Y' TO WS-EOF-SW.                                      
                                                                                
                                                                                
       2400-WRITE-MLWS003.                                                      
                                                                                
           MOVE ML-DATE                TO ML003-DATE                            
           MOVE ML-FROM-STORE          TO ML003-FROM-STORE                      
           MOVE ML-TO-STORE            TO ML003-TO-STORE                        
           MOVE ML-UNITS               TO ML003-UNITS                           
                                                                                
           PERFORM 2800-GET-UPC-RETAIL.                                         
                                                                                
                                                                                
                                                                                
       2800-GET-UPC-RETAIL.                                                     
                                                                                
               MOVE ML-ITM-NBR  TO UPCPLS-ITM-NBR                               
               MOVE PV-STR-NBR  TO UPCPLS-STR-NBR                               
                                                                                
               EXEC SQL                                                         
                   SELECT  A.UPC_NBR                                            
                          ,B.UNT_RTL_AMT                                        
                   INTO   :UPC-UPC-NBR                                          
                         ,:UPCPLS-UNT-RTL-AMT                                   
                   FROM                                                         
                         TUPC    A                                              
                        ,TUPCPLS B                                              
                   WHERE                                                        
                         B.ITM_NBR          = :UPCPLS-ITM-NBR                   
                     AND B.UPC_ID           = A.UPC_ID                          
                     AND B.STR_NBR          = :UPCPLS-STR-NBR                   
                     AND A.UPC_TYP_CDE      = 'I'                               
                     AND B.EFF_BEG_TMST    <= :PV-HAVOC-TIMESTAMP               
                     AND B.EFF_END_TMST    >= :PV-HAVOC-TIMESTAMP               
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                    MOVE UPC-UPC-NBR TO ML003-UPC                               
                    MOVE UPCPLS-UNT-RTL-AMT TO ML003-RETAIL-PRICE               
                    WRITE MLWS003-REC FROM ML003-TRANSFER-RECORD                
                    ADD 1 TO WS-COUNTER                                         
                   WHEN SQLCODE = +100                                          
                    PERFORM 2805-GET-TPP-RETAIL                                 
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - 2800-GET-UPC-RETAIL**'           
                       DISPLAY ' ITEM-NBR: ' ML-ITM-NBR                         
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
       2805-GET-TPP-RETAIL.                                                     
                                                                                
               EXEC SQL                                                         
                   SELECT  A.UPC_NBR                                            
                          ,B.RTL_AMT                                            
                   INTO   :UPC-UPC-NBR                                          
                         ,:UPCPLS-UNT-RTL-AMT                                   
                   FROM                                                         
                         TUPC    A                                              
                        ,TPPCPRC B                                              
                        ,TSKXREF C                                              
                   WHERE                                                        
                         B.TSK_ITM_NBR      = :UPCPLS-ITM-NBR                   
                     AND A.UPC_ID           = C.INTR_UPC_ID                     
                     AND B.TSK_ITM_NBR      = C.ITM_NBR                         
                     AND B.STR_NBR          = :UPCPLS-STR-NBR                   
                     AND A.UPC_TYP_CDE      = 'I'                               
                     AND B.END_TMST        >= :PV-HAVOC-TIMESTAMP               
                     AND B.STRT_TMST       <= :PV-HAVOC-TIMESTAMP               
                     WITH UR                                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                    MOVE UPC-UPC-NBR TO ML003-UPC                               
                    MOVE UPCPLS-UNT-RTL-AMT TO ML003-RETAIL-PRICE               
                    WRITE MLWS003-REC FROM ML003-TRANSFER-RECORD                
                    ADD 1 TO WS-COUNTER                                         
                   WHEN SQLCODE = +100                                          
                    PERFORM 2900-GET-UPC-RETAIL-COOP                            
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - 2805-GET-TPP-RETAIL**'           
                       DISPLAY ' ITEM-NBR: ' ML-ITM-NBR                         
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
       2900-GET-UPC-RETAIL-COOP.                                                
                                                                                
               EXEC SQL                                                         
                   SELECT  A.UPC_NBR                                            
                          ,B.UNT_RTL_AMT                                        
                   INTO   :UPC-UPC-NBR                                          
                         ,:UPCPLS-UNT-RTL-AMT                                   
                   FROM                                                         
                         TUPC    A                                              
                        ,TUPCPLS B                                              
                   WHERE                                                        
                         B.ITM_NBR          = :UPCPLS-ITM-NBR                   
                     AND B.UPC_ID           = A.UPC_ID                          
                     AND B.STR_NBR          = 0                                 
                     AND A.UPC_TYP_CDE      = 'I'                               
                     AND B.EFF_BEG_TMST    >= :PV-HAVOC-TIMESTAMP               
                     AND B.EFF_END_TMST    <= :PV-HAVOC-TIMESTAMP               
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                    MOVE UPC-UPC-NBR TO ML003-UPC                               
                    MOVE UPCPLS-UNT-RTL-AMT TO ML003-RETAIL-PRICE               
                    WRITE MLWS003-REC FROM ML003-TRANSFER-RECORD                
                    ADD 1 TO WS-COUNTER                                         
                    ADD 1 TO WS-COUNTER1                                        
                   WHEN SQLCODE = +100                                          
                    PERFORM 2905-GET-TPP-RETAIL-COOP                            
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - 2900-GET-UPC-RETAIL**'           
                       DISPLAY ' ITEM-NBR: ' ML-ITM-NBR                         
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
       2905-GET-TPP-RETAIL-COOP.                                                
                                                                                
               EXEC SQL                                                         
                   SELECT  A.UPC_NBR                                            
                          ,B.RTL_AMT                                            
                   INTO   :UPC-UPC-NBR                                          
                         ,:UPCPLS-UNT-RTL-AMT                                   
                   FROM                                                         
                         TUPC    A                                              
                        ,TPPCPRC B                                              
                        ,TSKXREF C                                              
                   WHERE                                                        
                         B.TSK_ITM_NBR      = :UPCPLS-ITM-NBR                   
                     AND A.UPC_ID           = C.INTR_UPC_ID                     
                     AND B.TSK_ITM_NBR      = C.ITM_NBR                         
                     AND B.STR_NBR          = 0                                 
                     AND A.UPC_TYP_CDE      = 'I'                               
                     AND B.STRT_TMST       <= :PV-HAVOC-TIMESTAMP               
                     AND B.END_TMST        >= :PV-HAVOC-TIMESTAMP               
                   WITH UR                                                      
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = 0                                             
                    MOVE UPC-UPC-NBR TO ML003-UPC                               
                    MOVE UPCPLS-UNT-RTL-AMT TO ML003-RETAIL-PRICE               
                    WRITE MLWS003-REC FROM ML003-TRANSFER-RECORD                
                    ADD 1 TO WS-COUNTER                                         
                    ADD 1 TO WS-COUNTER1                                        
                   WHEN SQLCODE = +100                                          
                       DISPLAY 'NOT FOUND'                                      
                       DISPLAY ' ITEM-NBR: ' ML-ITM-NBR                         
                   WHEN OTHER                                                   
                       DISPLAY '***PARAGRAPH - 2905-GET-TPP-RET-CRP**'          
                       DISPLAY ' ITEM-NBR: ' ML-ITM-NBR                         
                       PERFORM Z900-SQL-ABEND                                   
               END-EVALUATE.                                                    
                                                                                
       8000-EOJ-PROCESSING.                                                     
                                                                                
           CLOSE  CSV-FILE                                                      
                  MLWS003-FILE.                                                 
                                                                                
      ******************************************************************MLKUT030
      *  STANDARD DB2 ERROR ABEND ROUTINE                               MLKUT030
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             MLKUT030
      ******************************************************************MLKUT030
       Z900-SQL-ABEND.                                                  MLKUT030
                                                                        MLKUT030
           MOVE +4013                 TO ABEND-CODE.                    MLKUT030
           DISPLAY '**  ABEND     **'.                                  MLKUT030
           DISPLAY '**  PROGRAM   **  =  MLKUT030'.                     MLKUT030
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            MLKUT030
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        MLKUT030
                                                                        MLKUT030
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         MLKUT030
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        MLKUT030
                                                                        MLKUT030
           COPY DPPD004.                                                MLKUT030
                                                                                
           PERFORM Z999-ABEND.                                                  
                                                                        MLKUT030
       Z999-ABEND.                                                      MLKUT030
                                                                        MLKUT030
           CALL 'ILBOABN0'  USING ABEND-CODE.                           MLKUT030
                                                                        MLKUT030
