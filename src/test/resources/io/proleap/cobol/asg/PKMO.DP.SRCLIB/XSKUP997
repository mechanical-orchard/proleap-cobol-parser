       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XSKUP997.                                                  
       AUTHOR.       COGNIZANT.                                                 
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 11/14/2013.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS CORRECTION PROGRAM WHICH WILL UPDATE XST_MDSE_LNE_SELN    *        
      * TABLE THAT HAS STYLE ID MISMATCH RECORDS SEEN IN XSK053        *        
      * INTEGRITY REPORT.                                              *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT MDSE-LNE-ERR-FILE ASSIGN TO INP01.                            
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  MDSE-LNE-ERR-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  MDSE-LNE-REPORT-RECORD            PIC X(133).                        
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-UPCPLS-EFF-BEG-TMST        PIC X(26)  VALUE SPACES.            
           05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.            
           05 PV-SQLCODE-DISPLAY            PIC +(8)9  VALUE ZEROES.            
           05 PV-DB2-OPERATION-ATTEMPTED    PIC X(30)  VALUE SPACES.            
           05 PV-STYL-ID-IND                PIC S9(04) VALUE +0                 
                                                       COMP SYNC.               
           05 PV-REC-EXISTS                 PIC S9(04) COMP SYNC                
                                                       VALUE ZEROES.            
                                                                                
      ******************************************************************        
      * INPUT FILE VARIABLES.                                          *        
      ******************************************************************        
       01  PV-INP-MDSE-LNE-REPORT.                                              
           05 FILLER                       PIC X(1)     VALUE SPACES.           
           05 PV-INP-XS-PROMO-INTFC-ID     PIC 9(10)    VALUE ZEROES.           
           05 PV-INP-XS-MDSE-LNE-ID        PIC 9(10)    VALUE ZEROES.           
           05 PV-INP-XS-INTFC-SUB-ID       PIC 9(10)    VALUE ZEROES.           
           05 FILLER                       PIC X(5)     VALUE SPACES.           
           05 PV-INP-RPT-DATA-NAME         PIC X(20)    VALUE SPACES.           
           05 FILLER                       PIC X(2)     VALUE SPACES.           
           05 PV-INP-RPT-DATA-VALUE        PIC X(40)    VALUE SPACES.           
           05 PV-INP-REPORT-NOTES          PIC X(35)    VALUE SPACES.           
                                                                                
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-PM-ERROR-SW                  PIC X(01)  VALUE 'N'.    00280003
               88  PS-PM-STYLE-ERROR                      VALUE 'Y'.    00290003
               88  PS-NO-PM-STYLE-ERROR                   VALUE 'N'.    00300003
           05  PS-END-OF-MDSE-LNE-SW           PIC X(01)  VALUE 'N'.            
               88  PS-END-OF-MDSE-LNE                     VALUE 'Y'.            
               88  PS-NOT-END-OF-MDSE-LNE                 VALUE 'N'.            
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-MDSE-LNE-ROWS-READ         PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-MDSE-LNE-ROWS-UPDATED      PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-SMP-PRICG-ROWS-UPDATED     PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-MDSE-LNE-ROWS-SELECT       PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-SMP-PRICG-ROWS-READ        PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-SMP-PRICG-ROWS-UPD         PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-SMP-PRICG-ROWS-SELECT      PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
                                                                                
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE 0.                 
           88  ABEND-4013-DB2-ERROR                    VALUE +4013.             
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
      *****************************************************************         
      * XST_MDSE_LNE_SELN TABLE DCLGEN.                                         
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XST00028                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  DB2 TABLE XST00030(XST_SMP_PRICG) - SIMPLE PRICING TABLE               
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00030                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * XS DOMAIN MDSE LNE SELN MISMATCH ERROR REPORT FILE LAYOUT.              
      *****************************************************************         
           COPY XSRD019.                                                        
                                                                                
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-READ-MDSE-LNE-ERROR-FILE.                               
                                                                                
           PERFORM 3000-PROCESS-EXTRA-RECORDS                                   
             UNTIL PS-END-OF-MDSE-LNE.                                          
                                                                                
           PERFORM 4000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
                                                                                
           INITIALIZE DCLXST00028.                                              
           INITIALIZE DCLXST00030.                                              
                                                                                
           OPEN INPUT MDSE-LNE-ERR-FILE.                                        
                                                                                
      ******************************************************************        
      * 2000-READ-MDSE-LNE-ERROR-FILE.                                          
      ******************************************************************        
       2000-READ-MDSE-LNE-ERROR-FILE.                                           
                                                                                
           READ MDSE-LNE-ERR-FILE INTO PV-INP-MDSE-LNE-REPORT                   
             AT END                                                             
               SET PS-END-OF-MDSE-LNE TO TRUE                                   
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 3000-PROCESS-SKU-LOC-RECS.                                              
      ******************************************************************        
       3000-PROCESS-EXTRA-RECORDS.                                              
                                                                                
           SET PS-NO-PM-STYLE-ERROR    TO TRUE.                                 
                                                                                
           INITIALIZE DCLXST00028.                                              
                                                                                
           IF  PV-INP-RPT-DATA-NAME = 'PM706-STYLE-ID'                          
               ADD +1                  TO  PA-MDSE-LNE-ROWS-READ                
               SET PS-PM-STYLE-ERROR   TO TRUE                                  
               PERFORM 3100-SELECT-RECORDS                                      
           ELSE                                                                 
              IF  PV-INP-RPT-DATA-NAME = 'PM706-ASSN-LVL-CDE'                   
                  ADD +1               TO  PA-SMP-PRICG-ROWS-READ               
                  PERFORM 3200-SELECT-RECORDS                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-MDSE-LNE-ERROR-FILE.                               
                                                                                
      ******************************************************************        
      * 3100-SELECT-RECORDS.                                                    
      ******************************************************************        
       3100-SELECT-RECORDS.                                                     
                                                                                
           MOVE PV-INP-XS-PROMO-INTFC-ID TO XST00028-PROMO-INTFC-ID             
           MOVE PV-INP-XS-MDSE-LNE-ID    TO XST00028-MDSE-LNE-ID                
                                                                                
               EXEC SQL                                                         
                   SELECT STYL_ID                                               
                     INTO :XST00028-STYL-ID :PV-STYL-ID-IND                     
                     FROM XST_MDSE_LNE_SELN                                     
                    WHERE PROMO_INTFC_ID = :XST00028-PROMO-INTFC-ID             
                      AND MDSE_LNE_ID    = :XST00028-MDSE-LNE-ID                
               END-EXEC.                                                        
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                   PERFORM 3110-UPDATION-PROCESS                                
               WHEN OTHER                                                       
                   DISPLAY '3100-SELECT-RECORDS'                                
                   DISPLAY 'RECORD NOT AVAILABLE XST_MDSE_LNE_SELN'             
                   DISPLAY 'PROMO_INTFC_ID = ' XST00028-PROMO-INTFC-ID          
                   DISPLAY 'MDSE_LNE_ID    = ' XST00028-MDSE-LNE-ID             
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 3110-UPDATION-PROCESS.                                                  
      ******************************************************************        
       3110-UPDATION-PROCESS.                                                   
                                                                                
            IF XST00028-STYL-ID = ZEROES                                        
                ADD +1  TO PA-MDSE-LNE-ROWS-SELECT                              
                PERFORM 3111-UPDATE-MDSE-LNE-SELN                               
            END-IF.                                                             
                                                                                
      ******************************************************************        
      * 3111-UPDATE-MDSE-LNE-SELN.                                              
      ******************************************************************        
       3111-UPDATE-MDSE-LNE-SELN.                                               
                                                                                
            MOVE PV-INP-RPT-DATA-VALUE(1:8) TO XST00028-STYL-ID.                
                                                                                
               EXEC SQL                                                         
                   UPDATE XST_MDSE_LNE_SELN                                     
                      SET STYL_ID        = :XST00028-STYL-ID                    
                         ,LAST_UPD_TMST  = CURRENT TIMESTAMP                    
                    WHERE PROMO_INTFC_ID = :XST00028-PROMO-INTFC-ID             
                      AND MDSE_LNE_ID    = :XST00028-MDSE-LNE-ID                
               END-EXEC                                                         
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                   ADD +1                 TO PA-MDSE-LNE-ROWS-UPDATED           
               WHEN OTHER                                                       
                   MOVE '3111-UPDATE-MDSE-LNE-SELN' TO PV-PARAGRAPH-NAME        
                   MOVE 'UPDATE MDSE LNE SELN TABLE'                            
                                          TO PV-DB2-OPERATION-ATTEMPTED         
                   DISPLAY 'PROMO_INTFC_ID = ' XST00028-PROMO-INTFC-ID          
                   DISPLAY 'MDSE_LNE_ID    = ' XST00028-MDSE-LNE-ID             
                   PERFORM 9000-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 3200-SELECT-RECORDS.                                                    
      ******************************************************************        
       3200-SELECT-RECORDS.                                                     
                                                                                
           MOVE PV-INP-XS-PROMO-INTFC-ID TO XST00030-PROMO-INTFC-ID             
           MOVE PV-INP-XS-MDSE-LNE-ID    TO XST00030-MDSE-LNE-ID                
                                                                                
               EXEC SQL                                                         
                   SELECT 1                                                     
                     INTO :PV-REC-EXISTS                                        
                     FROM XST_SMP_PRICG                                         
                    WHERE PROMO_INTFC_ID = :XST00030-PROMO-INTFC-ID             
                      AND MDSE_LNE_ID    = :XST00030-MDSE-LNE-ID                
               END-EXEC.                                                        
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                   PERFORM 3250-UPDATE-SMP-PRICG                                
               WHEN OTHER                                                       
                   DISPLAY '3200-SELECT-RECORDS'                                
                   DISPLAY 'RECORD NOT AVAILABLE XST_SMP_PRICG '                
                   DISPLAY 'PROMO_INTFC_ID = ' XST00030-PROMO-INTFC-ID          
                   DISPLAY 'MDSE_LNE_ID    = ' XST00030-MDSE-LNE-ID             
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 3200-UPDATE-SMP-PRICG.                                                  
      ******************************************************************        
       3250-UPDATE-SMP-PRICG.                                                   
                                                                                
            MOVE PV-INP-RPT-DATA-VALUE(1:1) TO XST00030-ASSN-LVL-CDE            
            MOVE PV-INP-XS-PROMO-INTFC-ID   TO XST00030-PROMO-INTFC-ID          
            MOVE PV-INP-XS-MDSE-LNE-ID      TO XST00030-MDSE-LNE-ID             
                                                                                
               EXEC SQL                                                         
                   UPDATE XST_SMP_PRICG                                         
                      SET ASSN_LVL_CDE   = :XST00030-ASSN-LVL-CDE               
                         ,LAST_UPD_TMST  = CURRENT TIMESTAMP                    
                    WHERE PROMO_INTFC_ID = :XST00030-PROMO-INTFC-ID             
                      AND MDSE_LNE_ID    = :XST00030-MDSE-LNE-ID                
               END-EXEC.                                                        
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                   ADD +1  TO PA-SMP-PRICG-ROWS-UPDATED                         
               WHEN OTHER                                                       
                   MOVE '3200-UPDATE-SMP-PRICG'                                 
                         TO PV-PARAGRAPH-NAME                                   
                   MOVE 'UPDATE SMP_PRICG TABLE'                                
                         TO PV-DB2-OPERATION-ATTEMPTED                          
                   DISPLAY 'PROMO_INTFC_ID = ' XST00030-PROMO-INTFC-ID          
                   DISPLAY 'MDSE_LNE_ID    = ' XST00030-MDSE-LNE-ID             
                   PERFORM 9000-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
      * 4000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       4000-EOJ-ROUTINE.                                                        
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           CLOSE MDSE-LNE-ERR-FILE.                                             
                                                                                
           DISPLAY '******** XSKUP997 TOTALS *********'.                        
           DISPLAY 'MDSE-LNE ROWS READ    : ' PA-MDSE-LNE-ROWS-READ.            
           DISPLAY 'MDSE-LNE ROWS SELECT  : ' PA-MDSE-LNE-ROWS-SELECT.          
           DISPLAY 'MDSE-LNE ROWS UPDATED : ' PA-MDSE-LNE-ROWS-UPDATED.         
           DISPLAY 'SMP-PRICG ROWS READ   : ' PA-SMP-PRICG-ROWS-READ.           
           DISPLAY 'SMP-PRICG ROWS UPDATED: ' PA-SMP-PRICG-ROWS-UPDATED.        
           DISPLAY '***** END OF XSKUP997 TOTALS *****'.                        
           DISPLAY ' '.                                                         
                                                                                
      ******************************************************************        
      * 9000-SQL-ABEND.                                                         
      ******************************************************************        
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY ' '.                                                         
           DISPLAY '9000-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = XSKUP997'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
