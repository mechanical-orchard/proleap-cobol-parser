       IDENTIFICATION DIVISION.                                         00010001
       PROGRAM-ID.    MLKUT604                                          00020001
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030001
       DATE-WRITTEN.  09-01-00.                                         00040001
       DATE-COMPILED.                                                   00050001
                                                                        00060001
      ******************************************************************00070001
      *           MLKUT604 - FORMAT FOR LEDGER UPDATE                   00080001
      *                                                                 00090001
      * THIS PROGRAM READS FOUR TRANSACTION EXTRACT FILES LISTED BELOW..00100001
      *                                                                 00110001
      ****    PURCHASES (INCLUDING TRANSFER FROM / TRANSFER TO)         00120001
      ****    SALES                                                     00130001
      ****    PRICE CHANGES (INCLUDING MARKDOWNS / MARKUPS)             00140001
      ****    ADJUSTMENTS (INV ADJUSTMENT)                              00150001
      *                                                                 00160001
      * AN EXTRACT FILE WILL BE OPENED SEPARATELY, AND READ UNTIL ALL   00170001
      * RECORDS ON THE FILE HAVE BEEN PROCESSED. FILE WILL BE CLOSED    00180001
      * AFTER ALL TRANSACTIONS HAVE BEEN REFORMATTED TO THE MLRD149 FMT 00190001
      * AND WRITTEN TO THE OUTPUT FILE.                                 00200001
      *                                                                 00210001
      * EACH EXTRACT FILE WILL PROCESSED IN THE SAME FASHION, FORMATTING00220001
      * THE DATA FIELDS AS EACH TRAN CODE TYPE/EXTRACT FILE IS PROCESSED00230001
      *                                                                 00240001
      * ALL RECORD TYPES (EXCEPT SALES) WILL UTILIZE THE PROCESSING DATE00250001
      * TO APPEND FISCAL CALENDAR INFO TO OUTPUT. SO THE SALES FILE WILL00260001
      * BE PROCESSED LAST SINCE CALENDAR ROUTINE VARIABLES WILL CHANGE  00270001
      * DEPENDANT ON THE FINANCIAL LIABILITY DATE READ ON THE INPUT FILE00280001
      *                                                                 00290001
      * IF CHANGES ARE MADE TO MLKUT604, PLEASE EXAMINE MLKUT400 FOR A  00300001
      * SIMILAR CHANGE. (THE MODULE WAS CREATED FROM MLKUT400)          00310001
      *                                                                 00320001
      *CHANGE HISTORY:                                                  00330001
      * WR/PROJ DATE       PROGRAMMER   DESCRIPTION                     00340001
      * ------- ---------- ------------ --------------------------      00350001
99999 * 99999   11/13/2000 ROD JANSSEN  MISSING BACKPOSTING LOGIC CAUSED00360001
99999 *                                 AN ABEND IN MLK163.....         00370001
120400* 99999   12/04/2000 ROD JANSSEN  BUILD EFFICIENCIES INTO THE     00380001
120400*                                 TABLE STRUCTURE...              00390001
W09051* 99999   09/05/2001 ROD JANSSEN  TRIM I/O WHEN CALCING CHNL SHNK 00400001
23565 * 23565   09-10-2003 ROD JANSSEN  DETAILED PURCH ADJUSTMENTS ENH. 00410001
25554 * 25554   12-24-2003 PAUL KEBER   MARKDOWN REFINEMENT FOR PLANNING00420001
      *                                                                 00430001
W26603******************************************************************00440001
W26603* CHG ID   DATE        DESCRIPTION                               *00450001
W26603* ------   ----------  ----------------------------------------- *00460001
W26603* 26603    03-17-2004  STORE EXPANSION.                          *00470001
W26603*                      ADDED 'WITH UR' TO SOME SQL SELECTS.      *00480001
W26603*                      REPLACED TSTR000 WITH XMT_LOC             *00490001
W26603*                      - RONNA MCDONALD                          *00500001
W27511* W27511  12-28-2004 BROADCAST OPTIMIZATION SPLIT PLANNING        00510001
W27511*                    - J LINDEMANN                                00520001
      ******************************************************************00530001
W26006* 26006   03-31-2005 REMOVE REFERENCES TO OBSOLETE STORE TABLE.  *00540001
W26006*                    - ERIN SEARL                                *00550001
W28630* W28630  04-15-2005 LICENSE FEE SPLIT PLANNING                   00560001
W28630*                    COMMENT ADD '55 TO BE PULLED FOR RTV'S       00570001
W28630*                      (LEAVE FOR A LATER MODIFICATION)           00580001
W28630*                    - S ASEN                                     00590001
W28630* W28630  04-20-2005 LICENSE FEE SPLIT PLANNING                   00600001
W28630*                    COMMENT OUT CHECK FOR LICENSE FEE UNTIL      00610001
W28630*                      BP BUYER PLANNING SPLIT CHGS ARE READY.    00620001
W28630* W28630  04-26-2005 LICENSE FEE SPLIT PLANNING                   00630001
W28630*                    UNCOMMENT CHECK FOR LICENSE FEE UNTIL        00640001
R28630* W28630  07-14-2005 RONNA M. - LICENSE FEE SPLIT PLANNING        00650001
R28630*                    ANOTHER CODE WAS ADDED.  '0F' SHOULD BE      00660001
R28630*                    PROCESSED JUST LIKE '0D'                     00670001
R28630*                    ALSO ADDED '55' '54' AND '0A' TO RTV,        00680001
R28630*                    AND '5A' TO BROADCAST - PER JOHN KING        00690001
W26606* W28582  24-01-2006   - JOHN SELWITSCHKA                         00700001
W26606*                      PRICE CHANGE COPYBOOKS EXPANSION.          00710001
W26606*                      MODIFIED PRICE-CHANGE-FILE LRECL.          00720001
W30578* W30578  04-14-2006 JIM LEIKNESS - ADDED MARKDOWN CATEGORY       00730001
W30578*                    FOR EMPLOYEE DISCOUNT.                       00740001
X30578* W30578  08-09-2006 ROD JANSSEN - REMOVE THE 07 TRANSACTION CODE 00741001
MLBV  *         11-15-2006 S JACKSON - LRECL MISMATCH DURING MLBV TEST  00742001
MLBV  *                    FROM MLRD023 TRUNC ISSUE. CHNG BACK TO 287   00743001
W31118* W31118  08-23-2007 - SHARON SZPISZAR (STRATAGEM)                00744001
W31118*                    CHECK FOR MOS SALVAGE (REASON CODE 189) AND  00745001
W31118*                    MOVE IT TO BUYER MARKDOWN INSTEAD OF MARKDOWN00746001
W31118*                    OTHER.                                       00747001
SS0907* INC000000063063    - SHARON SZPISZAR (STRATAGEM)                00747102
SS0907*         09-12-2007 IF MOS SALVAGE (REASON CODE 189) MOVE MKDN   00747302
SS0907*                    RETAIL AMT TO MKDN BUYER RETAIL AMT ELSE     00747402
SS0907*                    MOVE MKDN RETAIL AMT TO MKDN STORE RETAIL    00747502
SS0907*                    AND CHECK IF PRICE POINT (REASON CODE 110 OR 00747602
SS0907*                    112).  IF PRICE POINT, MOVE MKDN RETAIL AMT  00747702
SS0907*                    TO MKDN PRICE POINT RETAIL AMT ELSE MOVE     00747802
SS0907*                    MKDN RETAIL AMT TO MKDN OTHER RETAIL AMT.    00747902
      ******************************************************************00748002
                                                                        00748102
       ENVIRONMENT DIVISION.                                            00749001
       CONFIGURATION SECTION.                                           00750001
       SOURCE-COMPUTER.        IBM-3090.                                00760001
       OBJECT-COMPUTER.        IBM-3090.                                00770001
       INPUT-OUTPUT SECTION.                                            00780001
       FILE-CONTROL.                                                    00790001
                                                                        00800001
           SELECT PURCHASE-FILE                                         00810001
               ASSIGN TO INP01.                                         00820001
                                                                        00830001
           SELECT PRICE-CHANGE-FILE                                     00840001
               ASSIGN TO INP02.                                         00850001
                                                                        00860001
           SELECT ADJUSTMENT-FILE                                       00870001
               ASSIGN TO INP03.                                         00880001
                                                                        00890001
           SELECT SALES-FILE                                            00900001
               ASSIGN TO INP04.                                         00910001
                                                                        00920001
99999      SELECT CONTROL-CARD-FILE                                     00930001
99999          ASSIGN TO INP05.                                         00940001
99999                                                                   00950001
           SELECT OUTPUT-FILE                                           00960001
               ASSIGN TO OUT01.                                         00970001
                                                                        00980001
       DATA DIVISION.                                                   00990001
       FILE SECTION.                                                    01000001
                                                                        01010001
       FD  PURCHASE-FILE                                                01020001
           LABEL RECORDS ARE STANDARD                                   01030001
           RECORDING MODE IS F                                          01040001
           BLOCK CONTAINS 0 RECORDS.                                    01050001
       01  PURCHASE-RECORD              PIC X(150).                     01060001
                                                                        01070001
       FD  PRICE-CHANGE-FILE                                            01080001
           LABEL RECORDS ARE STANDARD                                   01090001
           RECORDING MODE IS F                                          01100001
           BLOCK CONTAINS 0 RECORDS.                                    01110001
W26606 01  PRICE-CHANGE-RECORD          PIC X(130).                     01120001
                                                                        01130001
       FD  ADJUSTMENT-FILE                                              01140001
           LABEL RECORDS ARE STANDARD                                   01150001
           RECORDING MODE IS F                                          01160001
           BLOCK CONTAINS 0 RECORDS.                                    01170001
       01  ADJUSTMENT-RECORD            PIC X(90).                      01180001
                                                                        01190001
       FD  SALES-FILE                                                   01200001
           LABEL RECORDS ARE STANDARD                                   01210001
           RECORDING MODE IS F                                          01220001
           BLOCK CONTAINS 0 RECORDS.                                    01230001
       01  SALES-RECORD                 PIC X(80).                      01240001
                                                                        01250001
99999  FD  CONTROL-CARD-FILE                                            01260001
  |        LABEL RECORDS ARE STANDARD                                   01270001
  |        RECORDING MODE IS F                                          01280001
  |        BLOCK CONTAINS 0 RECORDS.                                    01290001
  |    01  CONTROL-CARD-RECORD          PIC X(80).                      01300001
99999                                                                   01310001
       FD  OUTPUT-FILE                                                  01320001
           LABEL RECORDS ARE STANDARD                                   01330001
           RECORDING MODE IS F                                          01340001
           BLOCK CONTAINS 0 RECORDS.                                    01350001
26603**01  OUTPUT-RECORD                PIC X(280).                     01360001
MLBV   01  OUTPUT-RECORD                PIC X(287).                     01370001
                                                                        01380001
      ***************************************************************** 01390001
       WORKING-STORAGE SECTION.                                         01400001
      ***************************************************************** 01410001
                                                                        01420001
99999  01  CC-CONTROL-CARD-REC.                                         01430001
  |        05  CC-BACKPOST-SALES-IND   PIC  X.                          01440001
  |            88  CC-BACKPOST-SALES                 VALUE 'Y'.         01450001
  |            88  CC-DO-NOT-BACKPOST-SALES          VALUE 'N'.         01460001
  |        05  FILLER                  PIC  X(79).                      01470001
99999                                                                   01480001
W26603*01  WS-EDIT-STORE-ALPH          PIC X(03)  VALUE ZEROS.          01490001
W26603 01  WS-EDIT-STORE-ALPH          PIC X(05)  VALUE ZEROS.          01500001
W09051 01  WS-EDIT-STORE-NUM REDEFINES                                  01510001
W26603*    WS-EDIT-STORE-ALPH          PIC 9(03).                       01520001
W26603     WS-EDIT-STORE-ALPH          PIC 9(05).                       01530001
                                                                        01540001
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.01550001
                                                                        01560001
       01  PC-PROGRAM-CONSTANTS.                                        01570001
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUT604'.  01580001
                                                                        01590001
       01  PE-PROGRAM-ERROR-MESSAGES.                                   01600001
           05  PE-MSG-02                       PIC X(40) VALUE          01610001
                'SALES FINANCIAL LIABILITY DATE INVALID'.               01620001
      *                                                                 01630001
       01  PS-PROGRAM-SWITCHES.                                         01640001
           05  PS-PURCHASE-EOF-SW           PIC  X        VALUE 'N'.    01650001
               88  PS-NO-MORE-PURCHASES                   VALUE 'Y'.    01660001
           05  PS-PCHNG-EOF-SW              PIC  X        VALUE 'N'.    01670001
               88  PS-NO-MORE-PRICE-CHANGES               VALUE 'Y'.    01680001
           05  PS-ADJ-EOF-SW                PIC  X        VALUE 'N'.    01690001
               88  PS-NO-MORE-ADJUSTMENTS                 VALUE 'Y'.    01700001
           05  PS-SALE-EOF-SW               PIC  X        VALUE 'N'.    01710001
               88  PS-NO-MORE-SALES                       VALUE 'Y'.    01720001
           05  PS-SALE-FIRST-TIME-SW        PIC  X        VALUE 'N'.    01730001
               88  PS-FIRST-SALES-REC                     VALUE 'Y'.    01740001
99999      05  PS-CONTROL-CARD-EOF-SW       PIC  X        VALUE 'N'.    01750001
99999          88  PS-END-OF-CONTROL-CARD-FILE            VALUE 'Y'.    01760001
W26603     05  PS-XMT-LOC-DONE-SW              PIC X     VALUE 'N'.     01770001
W26603         88  XMT-LOC-PROCESS-COMPLETE              VALUE 'Y'.     01780001
W09051                                                                  01790001
       01  PV-PROGRAM-VARIABLES.                                        01800001
120400     05  PV-STORE-NBR-SAVE               PIC  X(03) VALUE SPACES. 01810001
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01820001
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01830001
           05  PV-OPERATION-ATTEMPTED          PIC  X(40) VALUE SPACES. 01840001
           05  PV-PFW-WK-NBR                   PIC  X(02).              01850001
W26603*    05  PV-STORE-NUMBER                 PIC  X(03) VALUE SPACES. 01860001
W26603     05  PV-STORE-NUMBER                 PIC  X(05) VALUE SPACES. 01870001
           05  PV-PURCH-CALC-FIELDS.                                    01880001
               10  PV-PURCH-EXT-RTL-AMT            PIC S9(07)V99        01890001
                                            COMP-3    VALUE ZEROES.     01900001
               10  PV-PURCH-EXT-COST-AMT           PIC S9(07)V99        01910001
                                            COMP-3    VALUE ZEROES.     01920001
23565          10  PV-PURCH-GRO-COST-AMT           PIC S9(09)V99        01930001
23565                                       COMP-3  VALUE ZEROES.       01940001
23565          10  PV-PURCH-DISC-COST-AMT PIC S9(09)V99                 01950001
23565                                       COMP-3  VALUE ZEROES.       01960001
               10  PV-PURCH-FRGT-COST-AMT          PIC S9(07)V99        01970001
                                            COMP-3    VALUE ZEROES.     01980001
               10  PV-PURCH-NET-COST-AMT           PIC S9(07)V99        01990001
                                            COMP-3    VALUE ZEROES.     02000001
               10  PV-TRX-OUT-RTL-AMT              PIC S9(11)V99        02010001
                                            COMP-3    VALUE ZEROES.     02020001
               10  PV-TRX-IN-RTL-AMT               PIC S9(11)V99        02030001
                                            COMP-3    VALUE ZEROES.     02040001
               10  PV-MKDN-RTL-AMT                 PIC S9(11)V99        02050001
                                            COMP-3    VALUE ZEROES.     02060001
               10  PV-MKUP-RTL-AMT                 PIC S9(11)V99        02070001
                                            COMP-3    VALUE ZEROES.     02080001
                                                                        02090001
           05  PV-MARKDOWN-TYPE            PIC X(01)  VALUE SPACES.     02100001
               88  MARKDOWN-PLU                       VALUE  '1'.       02110001
               88  MARKDOWN-PERM                      VALUE  '2'.       02120001
               88  MARKDOWN-UNMATCHED                 VALUE  '3'.       02130001
               88  MARKDOWN-BUYER                     VALUE  '4'.       02140001
               88  MARKDOWN-STORE                     VALUE  '5'.       02150001
25554      05  PV-MKDN-RSN-CODE            PIC X(03)  VALUE SPACES.     02160001
25554          88  MKDN-RSN-PROMO              VALUES '746' '775' '777'.02170001
25554          88  MKDN-RSN-GOLDSTAR           VALUE  '753'.            02180001
25554          88  MKDN-RSN-PRICE-POINT        VALUES '110' '112'.      02190001
25554          88  MKDN-RSN-EMPL-DISC          VALUES '790'.            02200001
W31118         88  MKDN-RSN-MOS-SALVAGE        VALUE  '189'.            02210001
W09051     05  PV-STR-NDX              PIC S9(05) COMP-3 VALUE ZEROS.   02220001
23565      05  PV-AP-TRANS-TYPE        PIC X(02)  VALUE SPACES.         02230001
R28630         88  AP-RETURN-TO-VENDOR VALUES '20' '21' '22' '55'       02240001
R28630                                        '54' '0A'.                02250001
X30578*******  88  AP-DEBIT-ALLOWANCE        VALUES '24' '07' '2F'      02260001
X30578         88  AP-DEBIT-ALLOWANCE        VALUES '24' '2F'           02270001
W30578                                              '2H' '2I'.          02280001
R28630         88  AP-BRDCST-COST     VALUES '50' '51' '56' '57' '5A'.  02290001
R28630         88  AP-LIC-FEE-COST           VALUES '0D' '0F'.          02300001
W26603     05 PV-NULL-INDICATOR           PIC S9(04) COMP SYNC          02310001
W26603                                                 VALUE ZERO.      02320001
      *                                                                 02330001
       01  PA-PROGRAM-ACCUMULATORS.                                     02340001
           05  PA-INPUT-COUNTS.                                         02350001
               10  PA-INPUT-COUNT-IN      PIC  9(8)  VALUE ZEROES.      02360001
               10  PA-PURCHASE-COUNT-IN   PIC  9(8)  VALUE ZEROES.      02370001
               10  PA-PURCH-EXTR-CNT-IN   PIC  9(8)  VALUE ZEROES.      02380001
               10  PA-PRICE-CHG-COUNT-IN  PIC  9(8)  VALUE ZEROES.      02390001
               10  PA-ADJUST-COUNT-IN     PIC  9(8)  VALUE ZEROES.      02400001
               10  PA-SALES-COUNT-IN      PIC  9(8)  VALUE ZEROES.      02410001
           05  PA-OUTPUT-COUNTS.                                        02420001
               10  PA-OUTPUT-COUNT-OUT    PIC  9(8)  VALUE ZEROES.      02430001
               10  PA-PURCHASE-COUNT-OUT  PIC  9(8)  VALUE ZEROES.      02440001
               10  PA-PURCH-EXTR-CNT-OUT  PIC  9(8)  VALUE ZEROES.      02450001
               10  PA-TRX-FROM-COUNT-OUT  PIC  9(8)  VALUE ZEROES.      02460001
               10  PA-TRX-TO-COUNT-OUT    PIC  9(8)  VALUE ZEROES.      02470001
               10  PA-PCHANGE-COUNT-OUT   PIC  9(8)  VALUE ZEROES.      02480001
               10  PA-MARKDOWN-COUNT-OUT  PIC  9(8)  VALUE ZEROES.      02490001
               10  PA-MARKON-COUNT-OUT    PIC  9(8)  VALUE ZEROES.      02500001
               10  PA-ADJUST-COUNT-OUT    PIC  9(8)  VALUE ZEROES.      02510001
               10  PA-SALES-COUNT-OUT     PIC  9(8)  VALUE ZEROES.      02520001
      *                                                                 02530001
       01  SAVE-AREA.                                                   02540001
           05  SV-SALE-FIN-LIAB-DTE           PIC X(10).                02550001
                                                                        02560001
W09051* TABLE TO CONTAIN INFORMATION ON THE STORES..                    02570001
  |    01  WS-STORE-TABLE.                                              02580001
W09051     05  WS-STORE-TABLE-ELEMENTS   OCCURS 9999 TIMES.             02590001
W26603         10  WS-E-BUS-IND          PIC X(01) VALUE SPACES.        02600001
W09051                                                                  02610001
      ****************************************************              02620001
      *  COMMUNICATION AREAS FOR DB2                     *              02630001
      ****************************************************              02640001
           EXEC SQL                                                     02650001
               INCLUDE SQLCA                                            02660001
           END-EXEC.                                                    02670001
                                                                        02680001
      ****************************************************              02690001
      *  ERROR MESSAGE AREA FOR DB2                      *              02700001
      ****************************************************              02710001
           COPY DPWS004.                                                02720001
                                                                        02730001
      ****************************************************              02740001
      * M/L OPEN FISCAL DATE INFORMATION TABLE           *              02750001
      ****************************************************              02760001
                                                                        02770001
           EXEC SQL                                                     02780001
               INCLUDE TMLCNTL                                          02790001
           END-EXEC.                                                    02800001
                                                                        02810001
      ****************************************************              02820001
      *  M/L DATE TABLE                                  *              02830001
      ****************************************************              02840001
           EXEC SQL                                                     02850001
               INCLUDE TDTATTR                                          02860001
           END-EXEC.                                                    02870001
                                                                        02880001
      ****************************************************              02890001
      *  STORE TABLE DESCRIPTION ARGUMENTS......         *              02900001
      ****************************************************              02910001
           EXEC SQL                                                     02920001
W26603         INCLUDE XMT00001                                         02930001
           END-EXEC.                                                    02940001
                                                                        02950001
W09051******************************************************************02960001
W26603* XMT_LOC CURSOR FOR SHRINK CALCULATIONS.                         02970001
W09051******************************************************************02980001
W09051     EXEC SQL                                                     02990001
W26603         DECLARE XMT_LOC_CURSOR CURSOR FOR                        03000001
W26603           SELECT LOC_NBR,                                        03010001
W26603                  E_BUS_IND                                       03020001
W26603           FROM XMT_LOC                                           03030001
W09051           FOR FETCH ONLY                                         03040001
W09051     END-EXEC                                                     03050001
W09051                                                                  03060001
      ******************************************************************03070001
      * MLRD021 - PURCHASE AND TRANSFER EXTRACT RECORD LAYOUT           03080001
      * MLRD022 - SALES EXTRACT RECORD LAYOUT                           03090001
      * MLRD023 - PRICE CHANGE EXTRACT RECORD LAYOUT (MARKDOWN/MARKUPS) 03100001
      * MLRD024 - INVENTORY ADJ TRANSFER EXTRACT RECORD LAYOUT          03110001
      * MLRD149 - M/L DATABASE UPDATE TRANSACTIONS OUTPUT FORMAT        03120001
      ******************************************************************03130001
           COPY MLRD021.                                                03140001
           COPY MLRD022.                                                03150001
W26606     COPY MLRD023.                                                03160001
           COPY MLRD024.                                                03170001
           COPY MLRD149.                                                03180001
                                                                        03190001
      ******************************************************************03200001
       PROCEDURE DIVISION.                                              03210001
      ******************************************************************03220001
                                                                        03230001
       0000-MAIN-MODULE.                                                03240001
                                                                        03250001
           PERFORM 1000-INITIALIZATION.                                 03260001
                                                                        03270001
           PERFORM 2100-PROCESS-PURCHASE-EXTRACT.                       03280001
                                                                        03290001
           PERFORM 2200-PROCESS-PRICE-CHG-EXTRACT.                      03300001
                                                                        03310001
           PERFORM 2300-PROCESS-INV-ADJ-EXTRACT.                        03320001
                                                                        03330001
           PERFORM 2400-PROCESS-SALES-EXTRACT.                          03340001
                                                                        03350001
           PERFORM 8000-EOJ-ROUTINE.                                    03360001
                                                                        03370001
           STOP RUN.                                                    03380001
      *                                                                 03390001
      ******************************************************************03400001
      * INITIALIZE VARIABLES                                            03410001
      ******************************************************************03420001
       1000-INITIALIZATION.                                             03430001
                                                                        03440001
           PERFORM 4000-READ-CONTROL-TABLE.                             03450001
                                                                        03460001
99999      OPEN INPUT  CONTROL-CARD-FILE                                03470001
           OPEN OUTPUT OUTPUT-FILE.                                     03480001
                                                                        03490001
99999      PERFORM 4050-READ-CONTROL-CARD-FILE.                         03500001
99999      DISPLAY 'CONTROL CARD: ' CC-CONTROL-CARD-REC.                03510001
99999                                                                   03520001
99999      IF PS-END-OF-CONTROL-CARD-FILE                               03530001
99999          DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME           03540001
99999          DISPLAY '** PARAGRAPH 1000-INITIALIATION'                03550001
99999          DISPLAY '** CONTROL CARD FILE IS EMPTY.'                 03560001
99999          MOVE +4003 TO ABEND-CODE                                 03570001
99999          PERFORM 9999-ABEND-ROUTINE                               03580001
99999      END-IF.                                                      03590001
99999                                                                   03600001
           IF CC-BACKPOST-SALES                                         03610001
               PERFORM 7100-GET-PREV-FSCL-WK-DTE                        03620001
               PERFORM 7200-GET-PREV-FSCL-WK-NBR                        03630001
           ELSE                                                         03640001
               IF CC-DO-NOT-BACKPOST-SALES                              03650001
                   CONTINUE                                             03660001
               ELSE                                                     03670001
                   DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME       03680001
                   DISPLAY '** PARAGRAPH 1000-INITIALIATION'            03690001
                   DISPLAY '** INVALID SALES BACKPOSTING INDICATOR '    03700001
                   DISPLAY '** IN TABLE.                    '           03710001
                   DISPLAY '** VALID VALUES ARE "Y" AND "N".       '    03720001
                   MOVE +4003 TO ABEND-CODE                             03730001
                   PERFORM 9999-ABEND-ROUTINE                           03740001
               END-IF                                                   03750001
           END-IF.                                                      03760001
                                                                        03770001
           MOVE 'N'     TO PS-PURCHASE-EOF-SW                           03780001
                           PS-PCHNG-EOF-SW                              03790001
                           PS-ADJ-EOF-SW                                03800001
                           PS-SALE-EOF-SW.                              03810001
                                                                        03820001
           MOVE SPACES  TO OUTPUT-RECORD.                               03830001
           MOVE ZEROES  TO PA-PROGRAM-ACCUMULATORS.                     03840001
120400     MOVE SPACES  TO PV-STORE-NBR-SAVE.                           03850001
                                                                        03860001
W09051*** LOAD THE STORE TABLE UP FOR SHRINK RECALCS....                03870001
  |        INITIALIZE      WS-STORE-TABLE.                              03880001
W09051     MOVE ZEROS      TO PV-STR-NDX.                               03890001
W26603     MOVE 'N'        TO PS-XMT-LOC-DONE-SW.                       03900001
W26603     PERFORM 8200-OPEN-XMT-LOC.                                   03910001
W09051     PERFORM 8300-LOAD-STORE-TABLE                                03920001
W26603         UNTIL XMT-LOC-PROCESS-COMPLETE  OR                       03930001
W26603               PV-STR-NDX IS GREATER THAN 9999.                   03940001
W26603     PERFORM 8400-CLOSE-XMT-LOC.                                  03950001
W09051                                                                  03960001
      ******************************************************************03970001
      * PROCESS PURCHASE FILE (INC. PURCHASE AND TRANSFER TRAN CODES)   03980001
      ******************************************************************03990001
       2100-PROCESS-PURCHASE-EXTRACT.                                   04000001
      *                                                                 04010001
           OPEN INPUT PURCHASE-FILE.                                    04020001
           PERFORM 4100-READ-PURCHASE-FILE.                             04030001
      *                                                                 04040001
           PERFORM 3100-PROCESS-PURCHASE-FILE                           04050001
               UNTIL PS-NO-MORE-PURCHASES.                              04060001
      *                                                                 04070001
           COMPUTE PA-PURCH-EXTR-CNT-OUT = (  PA-PURCHASE-COUNT-OUT     04080001
                                            + PA-TRX-FROM-COUNT-OUT     04090001
                                            + PA-TRX-TO-COUNT-OUT  ).   04100001
      *                                                                 04110001
           DISPLAY '                                              '.    04120001
           DISPLAY '**** PURCHASE STATS ************'.                  04130001
           DISPLAY 'EXTRACT  RECORDS READ = ', PA-PURCH-EXTR-CNT-IN.    04140001
           DISPLAY '                                              '.    04150001
           DISPLAY 'PURCHASE OUTPUT COUNT = ', PA-PURCHASE-COUNT-OUT.   04160001
           DISPLAY 'TRX FROM OUTPUT COUNT = ', PA-TRX-FROM-COUNT-OUT.   04170001
           DISPLAY 'TRX  TO  OUTPUT COUNT = ', PA-TRX-TO-COUNT-OUT.     04180001
           DISPLAY '                       ----------'                  04190001
           DISPLAY 'EXTRACT RECS WRITTEN  = ', PA-PURCH-EXTR-CNT-OUT.   04200001
      *                                                                 04210001
           CLOSE PURCHASE-FILE.                                         04220001
      *                                                                 04230001
      ******************************************************************04240001
      * PROCESS PURCHASE FILE (INC. PURCHASE AND TRANSFER TRAN CODES)   04250001
      ******************************************************************04260001
       2200-PROCESS-PRICE-CHG-EXTRACT.                                  04270001
      *                                                                 04280001
           OPEN INPUT PRICE-CHANGE-FILE.                                04290001
           PERFORM 4200-READ-PRICE-CHANGE-FILE.                         04300001
      *                                                                 04310001
           PERFORM 3200-PROCESS-PRICE-CHANGE-FILE                       04320001
               UNTIL PS-NO-MORE-PRICE-CHANGES.                          04330001
      *                                                                 04340001
           COMPUTE PA-PCHANGE-COUNT-OUT  = (  PA-MARKDOWN-COUNT-OUT     04350001
                                            + PA-MARKON-COUNT-OUT  ).   04360001
      *                                                                 04370001
           DISPLAY '                                              '.    04380001
           DISPLAY '**** PRICE CHG STATS ***********'.                  04390001
           DISPLAY 'EXTRACT  RECORDS READ = ', PA-PRICE-CHG-COUNT-IN.   04400001
           DISPLAY '                                              '.    04410001
           DISPLAY 'MARKDOWN OUTPUT COUNT = ', PA-MARKDOWN-COUNT-OUT.   04420001
           DISPLAY 'MARKUPS  OUTPUT COUNT = ', PA-MARKON-COUNT-OUT.     04430001
           DISPLAY '                       ----------'                  04440001
           DISPLAY 'EXTRACT RECS WRITTEN  = ', PA-PCHANGE-COUNT-OUT.    04450001
      *                                                                 04460001
           CLOSE PRICE-CHANGE-FILE.                                     04470001
      *                                                                 04480001
      ******************************************************************04490001
      * PROCESS INVENTORY ADJUSTMENT FILE                               04500001
      ******************************************************************04510001
       2300-PROCESS-INV-ADJ-EXTRACT.                                    04520001
      *                                                                 04530001
           OPEN INPUT ADJUSTMENT-FILE.                                  04540001
           PERFORM 4300-READ-ADJUSTMENT-FILE.                           04550001
      *                                                                 04560001
           PERFORM 3300-PROCESS-ADJUSTMENT-FILE                         04570001
               UNTIL PS-NO-MORE-ADJUSTMENTS.                            04580001
      *                                                                 04590001
           DISPLAY '                                '.                  04600001
           DISPLAY '**** INV ADJ STATS *************'.                  04610001
           DISPLAY 'ADJUSTMENT INPUT CNT  = ', PA-ADJUST-COUNT-IN.      04620001
           DISPLAY 'ADJUSTMENT OUTPUT CNT = ', PA-ADJUST-COUNT-OUT.     04630001
      *                                                                 04640001
           CLOSE ADJUSTMENT-FILE.                                       04650001
      *                                                                 04660001
      ******************************************************************04670001
      * PROCESS SALES FILE                                              04680001
      ******************************************************************04690001
       2400-PROCESS-SALES-EXTRACT.                                      04700001
      *                                                                 04710001
           OPEN INPUT SALES-FILE.                                       04720001
                                                                        04730001
           PERFORM 4400-READ-SALES-FILE.                                04740001
      *                                                                 04750001
           MOVE 'Y' TO PS-SALE-FIRST-TIME-SW.                           04760001
      *                                                                 04770001
           PERFORM 3400-PROCESS-SALES-FILE                              04780001
               UNTIL PS-NO-MORE-SALES.                                  04790001
      *                                                                 04800001
           DISPLAY SPACES.                                              04810001
           DISPLAY '**** SALES FILE STATS **********'.                  04820001
           DISPLAY 'SALES INPUT  COUNT    = ', PA-SALES-COUNT-IN.       04830001
           DISPLAY 'SALES OUTPUT COUNT    = ', PA-SALES-COUNT-OUT.      04840001
      *                                                                 04850001
           CLOSE SALES-FILE.                                            04860001
      *                                                                 04870001
      ******************************************************************04880001
      * DETERMINE WHAT RECORD TYPE TO PROCESS (PURCHASES OR TRANSFERS)  04890001
      ******************************************************************04900001
       3100-PROCESS-PURCHASE-FILE.                                      04910001
      *                                                                 04920001
           ADD 1 TO PA-INPUT-COUNT-IN                                   04930001
                    PA-PURCH-EXTR-CNT-IN.                               04940001
      *                                                                 04950001
           EVALUATE TRUE                                                04960001
               WHEN ML021-PURCH-PURCHASE                                04970001
                    PERFORM 3125-FORMAT-PURCHASE-RECORDS                04980001
               WHEN ML021-PURCH-TRANSFER-FRM-STORE                      04990001
                    PERFORM 3150-FORMAT-TRX-FROM-RECORDS                05000001
               WHEN ML021-PURCH-TRANSFER-TO-STORE                       05010001
                    PERFORM 3175-FORMAT-TRX-TO-RECORDS                  05020001
               WHEN OTHER                                               05030001
                    DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME      05040001
                    DISPLAY '** ERROR PROCESSING PURCHASE EXTRACT FILE.'05050001
                    DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'    05060001
                    DISPLAY  ML021-PURCH-RECORD                         05070001
                    MOVE +4008 TO ABEND-CODE                            05080001
                    DISPLAY 'TRANS CODE : ', ML021-PURCH-TRANS-CDE      05090001
                    PERFORM 9999-ABEND-ROUTINE                          05100001
           END-EVALUATE.                                                05110001
      *                                                                 05120001
           PERFORM 4100-READ-PURCHASE-FILE.                             05130001
      *                                                                 05140001
      ******************************************************************05150001
      * FORMAT PURCHASE RECORDS (CODE '10') TO OUTPUT FORMAT.......     05160001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   05170001
      * TYPE OF STORE....                                               05180001
      *    V - VIRTUAL STORE (E-STORE)                                  05190001
      *    P - PHYSICAL STORE.                                          05200001
      *    A - ALL STORES.                                              05210001
      ******************************************************************05220001
      *                                                                 05230001
       3125-FORMAT-PURCHASE-RECORDS.                                    05240001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  05250001
                                                                        05260001
           INITIALIZE ML149-UPDATE-REC.                                 05270001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        05280001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        05290001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    05300001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    05310001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           05320001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         05330001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       05340001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        05350001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO PV-PURCH-EXT-RTL-AMT.     05360001
           MOVE ML021-PURCH-FRGT-AMT       TO PV-PURCH-FRGT-COST-AMT.   05370001
           MOVE ML021-PURCH-EXTD-NET-COST-AMT                           05380001
                                           TO PV-PURCH-NET-COST-AMT.    05390001
23565      MOVE ML021-PURCH-EXTD-GRO-COST-AMT                           05400001
23565                                      TO PV-PURCH-GRO-COST-AMT.    05410001
23565      MOVE ML021-PURCH-DISC-AMT       TO PV-PURCH-DISC-COST-AMT.   05420001
                                                                        05430001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     05440001
           MOVE ML021-PURCH-STORE-NBR      TO PV-STORE-NUMBER.          05450001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        05460001
  |        INSPECT WS-EDIT-STORE-ALPH                                   05470001
  |            REPLACING LEADING ' ' BY '0'.                            05480001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               05490001
W09051                                                                  05500001
W26603*    IF WS-E-BUS-LOC-IND (PV-STR-NDX) IS EQUAL TO 'Y'             05510001
W26603**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        05520001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 05530001
               MOVE 'V'                    TO ML149-CHNL-CDE            05540001
           ELSE                                                         05550001
               MOVE 'P'                    TO ML149-CHNL-CDE            05560001
           END-IF.                                                      05570001
                                                                        05580001
      *FOLLOWING FIELDS REQUIRE CALCULATION PRIOR TO MOVING VALUES      05590001
           IF (ML021-PURCH-DR-CR-CDE = '02' OR '08')                    05600001
               COMPUTE PV-PURCH-EXT-RTL-AMT =                           05610001
                                 (PV-PURCH-EXT-RTL-AMT * -1)            05620001
               COMPUTE PV-PURCH-FRGT-COST-AMT =                         05630001
                                 (PV-PURCH-FRGT-COST-AMT * -1)          05640001
               COMPUTE PV-PURCH-NET-COST-AMT =                          05650001
                                 (PV-PURCH-NET-COST-AMT * -1)           05660001
23565          COMPUTE PV-PURCH-GRO-COST-AMT =                          05670001
23565                            (PV-PURCH-GRO-COST-AMT * -1)           05680001
           END-IF.                                                      05690001
                                                                        05700001
23565      IF ML021-PURCH-DR-CR-CDE = '01'                              05710001
23565          COMPUTE PV-PURCH-DISC-COST-AMT =                         05720001
23565                            (PV-PURCH-DISC-COST-AMT * -1)          05730001
23565      END-IF.                                                      05740001
23565                                                                   05750001
      *IF THE SOURCE SYSTEM IS RECEIVING, MOVE THE DOLLARS TO THE       05760001
      *RECEIPT FIELDS. OTHERWISE, MOVE THE DOLLARS TO THE PURCHASE      05770001
      *ADJUSTMENT FIELDS.                                               05780001
           IF ML021-PURCH-RECEIPT                                       05790001
               MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-RCPT-RTL-AMT        05800001
               MOVE PV-PURCH-NET-COST-AMT  TO ML149-RCPT-NET-COST-AMT   05810001
           ELSE                                                         05820001
             MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-RTL-AMT          05830001
             MOVE PV-PURCH-FRGT-COST-AMT TO ML149-PADJ-FRGT-COST-AMT    05840001
             MOVE PV-PURCH-NET-COST-AMT  TO ML149-PADJ-NET-COST-AMT     05850001
23565        MOVE ML021-PURCH-AP-TRANS-CDE TO PV-AP-TRANS-TYPE          05860001
  |          IF AP-RETURN-TO-VENDOR                                     05870001
  |            MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-RTV-RTL-AMT    05880001
  |            MOVE PV-PURCH-DISC-COST-AMT TO ML149-PADJ-RTV-COST-AMT   05890001
  |            ADD PV-PURCH-GRO-COST-AMT   TO ML149-PADJ-RTV-COST-AMT   05900001
  |          ELSE                                                       05910001
  |          IF AP-DEBIT-ALLOWANCE                                      05920001
  |            MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-DA-RTL-AMT     05930001
  |            MOVE PV-PURCH-DISC-COST-AMT TO ML149-PADJ-DA-COST-AMT    05940001
  |            ADD PV-PURCH-GRO-COST-AMT   TO ML149-PADJ-DA-COST-AMT    05950001
  |          ELSE                                                       05960001
23565          MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-OTH-RTL-AMT    05970001
W27511         IF AP-BRDCST-COST                                        05980001
  |              MOVE PV-PURCH-DISC-COST-AMT                            05990001
  |                                      TO ML149-PADJ-BROP-COST-AMT    06000001
  |              ADD PV-PURCH-GRO-COST-AMT                              06010001
  |                                      TO ML149-PADJ-BROP-COST-AMT    06020001
W27511         ELSE                                                     06030001
W28630         IF AP-LIC-FEE-COST                                       06040001
  |              MOVE PV-PURCH-DISC-COST-AMT                            06050001
  |                                      TO ML149-LIC-FEE-COST-AMT      06060001
  |              ADD PV-PURCH-GRO-COST-AMT                              06070001
  |                                      TO ML149-LIC-FEE-COST-AMT      06080001
W28630         ELSE                                                     06090001
                 MOVE PV-PURCH-DISC-COST-AMT                            06100001
                                           TO ML149-PADJ-OTH-COST-AMT   06110001
                 ADD PV-PURCH-GRO-COST-AMT TO ML149-PADJ-OTH-COST-AMT   06120001
23565      END-IF.                                                      06130001
      *                                                                 06140001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            06150001
      *                                                                 06160001
           ADD +1 TO PA-PURCHASE-COUNT-OUT                              06170001
                     PA-OUTPUT-COUNT-OUT.                               06180001
      *                                                                 06190001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          06200001
                                                                        06210001
           INITIALIZE ML149-UPDATE-REC.                                 06220001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        06230001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        06240001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    06250001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    06260001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           06270001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         06280001
           MOVE 'A'                        TO ML149-CHNL-CDE.           06290001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       06300001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        06310001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO PV-PURCH-EXT-RTL-AMT.     06320001
           MOVE ML021-PURCH-FRGT-AMT       TO PV-PURCH-FRGT-COST-AMT.   06330001
           MOVE ML021-PURCH-EXTD-NET-COST-AMT                           06340001
                                           TO PV-PURCH-NET-COST-AMT.    06350001
23565      MOVE ML021-PURCH-EXTD-GRO-COST-AMT                           06360001
23565                                      TO PV-PURCH-GRO-COST-AMT.    06370001
23565      MOVE ML021-PURCH-DISC-AMT       TO PV-PURCH-DISC-COST-AMT.   06380001
                                                                        06390001
      *FOLLOWING FIELDS REQUIRE CALCULATION PRIOR TO MOVING VALUES      06400001
           IF (ML021-PURCH-DR-CR-CDE = '02' OR '08')                    06410001
               COMPUTE PV-PURCH-EXT-RTL-AMT =                           06420001
                                 (PV-PURCH-EXT-RTL-AMT * -1)            06430001
               COMPUTE PV-PURCH-FRGT-COST-AMT =                         06440001
                                 (PV-PURCH-FRGT-COST-AMT * -1)          06450001
               COMPUTE PV-PURCH-NET-COST-AMT =                          06460001
                                 (PV-PURCH-NET-COST-AMT * -1)           06470001
23565          COMPUTE PV-PURCH-GRO-COST-AMT =                          06480001
23565                            (PV-PURCH-GRO-COST-AMT * -1)           06490001
           END-IF.                                                      06500001
                                                                        06510001
23565      IF ML021-PURCH-DR-CR-CDE = '01'                              06520001
23565          COMPUTE PV-PURCH-DISC-COST-AMT =                         06530001
23565                            (PV-PURCH-DISC-COST-AMT * -1)          06540001
23565      END-IF.                                                      06550001
23565                                                                   06560001
      *IF THE SOURCE SYSTEM IS RECEIVING, MOVE THE DOLLARS TO THE       06570001
      *RECEIPT FIELDS. OTHERWISE, MOVE THE DOLLARS TO THE PURCHASE      06580001
      *ADJUSTMENT FIELDS.                                               06590001
           IF ML021-PURCH-RECEIPT                                       06600001
               MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-RCPT-RTL-AMT        06610001
               MOVE PV-PURCH-NET-COST-AMT  TO ML149-RCPT-NET-COST-AMT   06620001
           ELSE                                                         06630001
             MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-RTL-AMT          06640001
             MOVE PV-PURCH-FRGT-COST-AMT TO ML149-PADJ-FRGT-COST-AMT    06650001
             MOVE PV-PURCH-NET-COST-AMT  TO ML149-PADJ-NET-COST-AMT     06660001
23565        MOVE ML021-PURCH-AP-TRANS-CDE TO PV-AP-TRANS-TYPE          06670001
  |          IF AP-RETURN-TO-VENDOR                                     06680001
  |            MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-RTV-RTL-AMT    06690001
  |            MOVE PV-PURCH-DISC-COST-AMT TO ML149-PADJ-RTV-COST-AMT   06700001
  |            ADD PV-PURCH-GRO-COST-AMT   TO ML149-PADJ-RTV-COST-AMT   06710001
  |          ELSE                                                       06720001
  |          IF AP-DEBIT-ALLOWANCE                                      06730001
  |            MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-DA-RTL-AMT     06740001
  |            MOVE PV-PURCH-DISC-COST-AMT TO ML149-PADJ-DA-COST-AMT    06750001
  |            ADD PV-PURCH-GRO-COST-AMT   TO ML149-PADJ-DA-COST-AMT    06760001
  |          ELSE                                                       06770001
23565          MOVE PV-PURCH-EXT-RTL-AMT   TO ML149-PADJ-OTH-RTL-AMT    06780001
W27511         IF AP-BRDCST-COST                                        06790001
  |              MOVE PV-PURCH-DISC-COST-AMT                            06800001
  |                                     TO ML149-PADJ-BROP-COST-AMT     06810001
  |              ADD PV-PURCH-GRO-COST-AMT                              06820001
  |                                     TO ML149-PADJ-BROP-COST-AMT     06830001
W27511         ELSE                                                     06840001
W28630         IF AP-LIC-FEE-COST                                       06850001
  |              MOVE PV-PURCH-DISC-COST-AMT                            06860001
  |                                     TO ML149-LIC-FEE-COST-AMT       06870001
  |              ADD PV-PURCH-GRO-COST-AMT                              06880001
  |                                     TO ML149-LIC-FEE-COST-AMT       06890001
W28630         ELSE                                                     06900001
                 MOVE PV-PURCH-DISC-COST-AMT                            06910001
                                           TO ML149-PADJ-OTH-COST-AMT   06920001
                 ADD PV-PURCH-GRO-COST-AMT TO ML149-PADJ-OTH-COST-AMT   06930001
23565      END-IF.                                                      06940001
                                                                        06950001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            06960001
                                                                        06970001
           ADD +1 TO PA-PURCHASE-COUNT-OUT                              06980001
                     PA-OUTPUT-COUNT-OUT.                               06990001
                                                                        07000001
      ******************************************************************07010001
      * FORMAT TRANSFER FROM RECORDS (CODE '30') TO OUTPUT FORMAT       07020001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   07030001
      * TYPE OF STORE....                                               07040001
      *    V - VIRTUAL STORE (E-STORE)                                  07050001
      *    P - PHYSICAL STORE.                                          07060001
      *    A - ALL STORES.                                              07070001
      ******************************************************************07080001
       3150-FORMAT-TRX-FROM-RECORDS.                                    07090001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  07100001
                                                                        07110001
           INITIALIZE ML149-UPDATE-REC.                                 07120001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        07130001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        07140001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    07150001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    07160001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           07170001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         07180001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       07190001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        07200001
                                                                        07210001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     07220001
           MOVE ML021-PURCH-STORE-NBR      TO PV-STORE-NUMBER.          07230001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        07240001
  |        INSPECT WS-EDIT-STORE-ALPH                                   07250001
  |            REPLACING LEADING ' ' BY '0'.                            07260001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               07270001
  |                                                                     07280001
W26603**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        07290001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 07300001
               MOVE 'V'                    TO ML149-CHNL-CDE            07310001
           ELSE                                                         07320001
               MOVE 'P'                    TO ML149-CHNL-CDE            07330001
           END-IF.                                                      07340001
                                                                        07350001
      * RETAIL TRANSFER 'OUT' AMOUNTS                                   07360001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO ML149-XFER-OUT-RTL-AMT.   07370001
                                                                        07380001
050000     PERFORM 5000-WRITE-OUTPUT-RECORD.                            07390001
050100*                                                                 07400001
050200     ADD +1 TO PA-TRX-FROM-COUNT-OUT                              07410001
050300               PA-OUTPUT-COUNT-OUT.                               07420001
                                                                        07430001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          07440001
                                                                        07450001
           INITIALIZE ML149-UPDATE-REC.                                 07460001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        07470001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        07480001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    07490001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    07500001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           07510001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         07520001
           MOVE 'A'                        TO ML149-CHNL-CDE.           07530001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       07540001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        07550001
                                                                        07560001
      * RETAIL TRANSFER 'OUT' AMOUNTS                                   07570001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO ML149-XFER-OUT-RTL-AMT.   07580001
                                                                        07590001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            07600001
                                                                        07610001
           ADD +1 TO PA-TRX-FROM-COUNT-OUT                              07620001
                     PA-OUTPUT-COUNT-OUT.                               07630001
                                                                        07640001
      ******************************************************************07650001
      * FORMAT TRANSFER 'TO' RECORDS (CODE '31') TO OUTPUT FORMAT.      07660001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   07670001
      * TYPE OF STORE....                                               07680001
      *    V - VIRTUAL STORE (E-STORE)                                  07690001
      *    P - PHYSICAL STORE.                                          07700001
      *    A - ALL STORES.                                              07710001
      ******************************************************************07720001
       3175-FORMAT-TRX-TO-RECORDS.                                      07730001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  07740001
                                                                        07750001
           INITIALIZE ML149-UPDATE-REC.                                 07760001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        07770001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        07780001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    07790001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    07800001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           07810001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         07820001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       07830001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        07840001
                                                                        07850001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     07860001
           MOVE ML021-PURCH-STORE-NBR      TO PV-STORE-NUMBER.          07870001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        07880001
  |        INSPECT WS-EDIT-STORE-ALPH                                   07890001
  |            REPLACING LEADING ' ' BY '0'.                            07900001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               07910001
  |                                                                     07920001
W26603**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        07930001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 07940001
               MOVE 'V'                    TO ML149-CHNL-CDE            07950001
           ELSE                                                         07960001
               MOVE 'P'                    TO ML149-CHNL-CDE            07970001
           END-IF.                                                      07980001
                                                                        07990001
      * RETAIL TRANSFER 'IN' AMOUNTS                                    08000001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO ML149-XFER-IN-RTL-AMT.    08010001
                                                                        08020001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            08030001
      *                                                                 08040001
           ADD +1 TO PA-TRX-TO-COUNT-OUT                                08050001
                     PA-OUTPUT-COUNT-OUT.                               08060001
                                                                        08070001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          08080001
                                                                        08090001
           INITIALIZE ML149-UPDATE-REC.                                 08100001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        08110001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        08120001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    08130001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    08140001
           MOVE ML021-PURCH-DEPT-NBR       TO ML149-DEPT-NBR.           08150001
           MOVE ML021-PURCH-CLASS-NBR      TO ML149-SUB-CL-NBR.         08160001
           MOVE 'A'                        TO ML149-CHNL-CDE.           08170001
           MOVE ML021-PURCH-FIN-LIAB-DTE   TO ML149-FIN-LIAB-DTE.       08180001
           MOVE ML021-PURCH-TRANS-CDE      TO ML149-TRN-TYP-CDE.        08190001
                                                                        08200001
      * RETAIL TRANSFER 'IN' AMOUNTS                                    08210001
           MOVE ML021-PURCH-EXTD-RTL-AMT   TO ML149-XFER-IN-RTL-AMT.    08220001
                                                                        08230001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            08240001
                                                                        08250001
           ADD +1 TO PA-TRX-TO-COUNT-OUT                                08260001
                     PA-OUTPUT-COUNT-OUT.                               08270001
                                                                        08280001
      ******************************************************************08290001
      * PROCESS ALL RECORDS ON THE PRICE CHANGE FILE                    08300001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   08310001
      * TYPE OF STORE....                                               08320001
      *    V - VIRTUAL STORE (E-STORE)                                  08330001
      *    P - PHYSICAL STORE.                                          08340001
      *    A - ALL STORES.                                              08350001
      ******************************************************************08360001
       3200-PROCESS-PRICE-CHANGE-FILE.                                  08370001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  08380001
                                                                        08390001
           ADD 1 TO PA-INPUT-COUNT-IN                                   08400001
                    PA-PRICE-CHG-COUNT-IN.                              08410001
                                                                        08420001
           INITIALIZE ML149-UPDATE-REC.                                 08430001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        08440001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        08450001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    08460001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    08470001
           MOVE ML023-PC-DEPT-NBR          TO ML149-DEPT-NBR.           08480001
           MOVE ML023-PC-CLASS-NBR         TO ML149-SUB-CL-NBR.         08490001
           MOVE ML023-PC-FIN-LIAB-DTE      TO ML149-FIN-LIAB-DTE.       08500001
           MOVE ML023-PC-TRANS-CDE         TO ML149-TRN-TYP-CDE.        08510001
                                                                        08520001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     08530001
           MOVE ML023-PC-STORE-NBR         TO PV-STORE-NUMBER.          08540001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        08550001
  |        INSPECT WS-EDIT-STORE-ALPH                                   08560001
  |            REPLACING LEADING ' ' BY '0'.                            08570001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               08580001
  |                                                                     08590001
W26603**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        08600001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 08610001
               MOVE 'V'                    TO ML149-CHNL-CDE            08620001
           ELSE                                                         08630001
               MOVE 'P'                    TO ML149-CHNL-CDE            08640001
           END-IF.                                                      08650001
                                                                        08660001
      *CALCULATE THE MARKDOWN/MARKUP EXTENDED RETAIL AMOUNTS            08670001
           IF  ML023-PC-MARKDOWN                                        08680001
               MOVE ML023-PC-EXTD-RTL-AMT TO PV-MKDN-RTL-AMT            08690001
               PERFORM 3210-DETERMINE-MARKDOWN-TYPE                     08700001
           ELSE                                                         08710001
               IF ML023-PC-MARKUP                                       08720001
                   MOVE ML023-PC-EXTD-RTL-AMT TO PV-MKUP-RTL-AMT        08730001
                   MOVE PV-MKUP-RTL-AMT          TO ML149-MKUP-RTL-AMT  08740001
               ELSE                                                     08750001
                   DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME       08760001
                   DISPLAY                                              08770001
                        '** ERROR PROCESSING PRICE CHANGE EXTRACT FILE.'08780001
                   DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'     08790001
                   DISPLAY  ML023-PC-RECORD                             08800001
                   MOVE +4008 TO ABEND-CODE                             08810001
                   DISPLAY 'TRANS CODE : ', ML023-PC-TRANS-CDE          08820001
                   PERFORM 9999-ABEND-ROUTINE                           08830001
               END-IF                                                   08840001
           END-IF.                                                      08850001
                                                                        08860001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            08870001
                                                                        08880001
           IF  ML023-PC-MARKDOWN                                        08890001
               ADD +1 TO PA-MARKDOWN-COUNT-OUT                          08900001
                         PA-OUTPUT-COUNT-OUT                            08910001
           ELSE                                                         08920001
               ADD +1 TO PA-MARKON-COUNT-OUT                            08930001
                         PA-OUTPUT-COUNT-OUT                            08940001
           END-IF.                                                      08950001
      *                                                                 08960001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          08970001
                                                                        08980001
           ADD 1 TO PA-INPUT-COUNT-IN                                   08990001
                    PA-PRICE-CHG-COUNT-IN.                              09000001
                                                                        09010001
           INITIALIZE ML149-UPDATE-REC.                                 09020001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        09030001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        09040001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    09050001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    09060001
           MOVE ML023-PC-DEPT-NBR          TO ML149-DEPT-NBR.           09070001
           MOVE ML023-PC-CLASS-NBR         TO ML149-SUB-CL-NBR.         09080001
           MOVE 'A'                        TO ML149-CHNL-CDE.           09090001
           MOVE ML023-PC-FIN-LIAB-DTE      TO ML149-FIN-LIAB-DTE.       09100001
           MOVE ML023-PC-TRANS-CDE         TO ML149-TRN-TYP-CDE.        09110001
                                                                        09120001
      *CALCULATE THE MARKDOWN/MARKUP EXTENDED RETAIL AMOUNTS            09130001
           IF  ML023-PC-MARKDOWN                                        09140001
               MOVE ML023-PC-EXTD-RTL-AMT TO PV-MKDN-RTL-AMT            09150001
               PERFORM 3210-DETERMINE-MARKDOWN-TYPE                     09160001
           ELSE                                                         09170001
               IF ML023-PC-MARKUP                                       09180001
                   MOVE ML023-PC-EXTD-RTL-AMT TO PV-MKUP-RTL-AMT        09190001
                   MOVE PV-MKUP-RTL-AMT          TO ML149-MKUP-RTL-AMT  09200001
               ELSE                                                     09210001
                   DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME       09220001
                   DISPLAY                                              09230001
                        '** ERROR PROCESSING PRICE CHANGE EXTRACT FILE.'09240001
                   DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'     09250001
                   DISPLAY  ML023-PC-RECORD                             09260001
                   MOVE +4008 TO ABEND-CODE                             09270001
                   DISPLAY 'TRANS CODE : ', ML023-PC-TRANS-CDE          09280001
                   PERFORM 9999-ABEND-ROUTINE                           09290001
               END-IF                                                   09300001
           END-IF.                                                      09310001
                                                                        09320001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            09330001
                                                                        09340001
           IF  ML023-PC-MARKDOWN                                        09350001
               ADD +1 TO PA-MARKDOWN-COUNT-OUT                          09360001
                         PA-OUTPUT-COUNT-OUT                            09370001
           ELSE                                                         09380001
               ADD +1 TO PA-MARKON-COUNT-OUT                            09390001
                         PA-OUTPUT-COUNT-OUT                            09400001
           END-IF.                                                      09410001
                                                                        09420001
      *** READ NEXT RECORD - PRICE CHANGE.....                          09430001
           PERFORM 4200-READ-PRICE-CHANGE-FILE.                         09440001
                                                                        09450001
      ******************************************************************09460001
      * CHECK THE TYPE OF MARKDOWN AND MOVE IT TO THE CORRECT           09470001
      * OUTPUT FIELDS.                                                  09480001
      ******************************************************************09490001
       3210-DETERMINE-MARKDOWN-TYPE.                                    09500001
                                                                        09510001
           MOVE ML023-PC-CATEGORY          TO PV-MARKDOWN-TYPE.         09520001
25554      MOVE ML023-PC-MKDN-MKUP-RSN-CDE TO PV-MKDN-RSN-CODE.         09530001
                                                                        09540001
           EVALUATE TRUE                                                09550001
             WHEN MARKDOWN-PLU                                          09560001
                 MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-PLU-RTL-AMT         09570001
25554            IF MKDN-RSN-PROMO                                      09580001
25554                MOVE PV-MKDN-RTL-AMT                               09590001
25554                                 TO ML149-MKDN-PROMO-RTL-AMT       09600001
25554            ELSE                                                   09610001
25554                MOVE PV-MKDN-RTL-AMT                               09620001
25554                                 TO ML149-MKDN-OTH-RTL-AMT         09630001
25554            END-IF                                                 09640001
             WHEN MARKDOWN-PERM                                         09650001
                 MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-RGST-RTL-AMT        09660001
25554            IF MKDN-RSN-GOLDSTAR                                   09670001
25554                MOVE PV-MKDN-RTL-AMT                               09680001
25554                                 TO ML149-MKDN-GLDST-RTL-AMT       09690001
25554            ELSE                                                   09700001
30578                IF MKDN-RSN-EMPL-DISC                              09710001
30578                   MOVE PV-MKDN-RTL-AMT                            09720001
30578                                 TO ML149-MKDN-EMP-RTL-AMT         09730001
30578                ELSE                                               09740001
                        MOVE PV-MKDN-RTL-AMT                            09750001
                                      TO ML149-MKDN-OTH-RTL-AMT         09760001
                     END-IF                                             09770001
30578            END-IF                                                 09780001
             WHEN MARKDOWN-UNMATCHED                                    09790001
                MOVE PV-MKDN-RTL-AMT  TO ML149-MKDN-UNMTCH-RTL-AMT      09800001
25554           MOVE PV-MKDN-RTL-AMT  TO ML149-MKDN-OTH-RTL-AMT         09810001
             WHEN MARKDOWN-BUYER                                        09820001
                MOVE PV-MKDN-RTL-AMT  TO ML149-MKDN-BYR-RTL-AMT         09830001
             WHEN MARKDOWN-STORE                                        09840001
SS0907          IF MKDN-RSN-MOS-SALVAGE                                 09850003
SS0907              MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-BYR-RTL-AMT      09851003
SS0907          ELSE                                                    09852003
SS0907              MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-STR-RTL-AMT      09853003
                                                                        09854001
25554               IF MKDN-RSN-PRICE-POINT                             09855001
25554                   MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-PRPT-RTL-AMT 09856001
                    ELSE                                                09857001
SS0907                  MOVE PV-MKDN-RTL-AMT TO ML149-MKDN-OTH-RTL-AMT  09858003
SS0907              END-IF                                              09859003
SS0907                                                                  09860004
25554           END-IF                                                  09870001
             WHEN OTHER                                                 09880001
                MOVE PV-MKDN-RTL-AMT  TO ML149-MKDN-UNMTCH-RTL-AMT      09890001
25554           MOVE PV-MKDN-RTL-AMT  TO ML149-MKDN-OTH-RTL-AMT         09900001
           END-EVALUATE.                                                09910001
                                                                        09920001
      ******************************************************************09930001
      * PROCESS THE INVENTORY ADJUSTMENT FILE                           09940001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   09950001
      * TYPE OF STORE....                                               09960001
      *    V - VIRTUAL STORE (E-STORE)                                  09970001
      *    P - PHYSICAL STORE.                                          09980001
      *    A - ALL STORES.                                              09990001
      ******************************************************************10000001
       3300-PROCESS-ADJUSTMENT-FILE.                                    10010001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  10020001
      *                                                                 10030001
           ADD 1 TO PA-INPUT-COUNT-IN                                   10040001
                    PA-ADJUST-COUNT-IN.                                 10050001
      *                                                                 10060001
           IF ML024-ADJ-ADJUSTMENT                                      10070001
               CONTINUE                                                 10080001
           ELSE                                                         10090001
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME           10100001
               DISPLAY '** ERROR PROCESSING ADJUSTMENT EXTRACT FILE.'   10110001
               DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'         10120001
               DISPLAY  ML024-ADJ-RECORD                                10130001
               MOVE +4008 TO ABEND-CODE                                 10140001
               DISPLAY 'TRANS CODE : ', ML024-ADJ-TRANS-CDE             10150001
               PERFORM 9999-ABEND-ROUTINE                               10160001
           END-IF.                                                      10170001
      *                                                                 10180001
           INITIALIZE ML149-UPDATE-REC.                                 10190001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        10200001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        10210001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    10220001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    10230001
           MOVE ML024-ADJ-DEPT-NBR            TO ML149-DEPT-NBR.        10240001
           MOVE ML024-ADJ-CLASS-NBR           TO ML149-SUB-CL-NBR.      10250001
           MOVE ML024-ADJ-FIN-LIAB-DTE        TO ML149-FIN-LIAB-DTE.    10260001
           MOVE ML024-ADJ-TRANS-CDE           TO ML149-TRN-TYP-CDE.     10270001
           MOVE ML024-ADJ-AT-RTL-AMT          TO ML149-INV-ADJ-RTL-AMT. 10280001
                                                                        10290001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     10300001
           MOVE ML024-ADJ-STORE-NBR        TO PV-STORE-NUMBER.          10310001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        10320001
  |        INSPECT WS-EDIT-STORE-ALPH                                   10330001
  |            REPLACING LEADING ' ' BY '0'.                            10340001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               10350001
  |                                                                     10360001
W26603**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        10370001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 10380001
               MOVE 'V'                    TO ML149-CHNL-CDE            10390001
           ELSE                                                         10400001
               MOVE 'P'                    TO ML149-CHNL-CDE            10410001
           END-IF.                                                      10420001
                                                                        10430001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            10440001
                                                                        10450001
           ADD +1 TO PA-ADJUST-COUNT-OUT                                10460001
                     PA-OUTPUT-COUNT-OUT.                               10470001
                                                                        10480001
INVADJ***  PERFORM 4300-READ-ADJUSTMENT-FILE.                           10490001
INVADJ***                                                               10500001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          10510001
                                                                        10520001
           ADD 1 TO PA-INPUT-COUNT-IN                                   10530001
                    PA-ADJUST-COUNT-IN.                                 10540001
                                                                        10550001
           IF ML024-ADJ-ADJUSTMENT                                      10560001
               CONTINUE                                                 10570001
           ELSE                                                         10580001
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME           10590001
               DISPLAY '** ERROR PROCESSING ADJUSTMENT EXTRACT FILE.'   10600001
               DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'         10610001
               DISPLAY  ML024-ADJ-RECORD                                10620001
               MOVE +4008 TO ABEND-CODE                                 10630001
               DISPLAY 'TRANS CODE : ', ML024-ADJ-TRANS-CDE             10640001
               PERFORM 9999-ABEND-ROUTINE                               10650001
           END-IF.                                                      10660001
                                                                        10670001
           INITIALIZE ML149-UPDATE-REC.                                 10680001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        10690001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        10700001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    10710001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    10720001
           MOVE ML024-ADJ-DEPT-NBR         TO ML149-DEPT-NBR.           10730001
           MOVE ML024-ADJ-CLASS-NBR        TO ML149-SUB-CL-NBR.         10740001
           MOVE 'A'                        TO ML149-CHNL-CDE.           10750001
           MOVE ML024-ADJ-FIN-LIAB-DTE     TO ML149-FIN-LIAB-DTE.       10760001
           MOVE ML024-ADJ-TRANS-CDE        TO ML149-TRN-TYP-CDE.        10770001
           MOVE ML024-ADJ-AT-RTL-AMT       TO ML149-INV-ADJ-RTL-AMT.    10780001
                                                                        10790001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            10800001
                                                                        10810001
           ADD +1 TO PA-ADJUST-COUNT-OUT                                10820001
                     PA-OUTPUT-COUNT-OUT.                               10830001
                                                                        10840001
           PERFORM 4300-READ-ADJUSTMENT-FILE.                           10850001
                                                                        10860001
      ******************************************************************10870001
      * PROCESS THE SALES FILE                                          10880001
      *    EXTRACT AND FORMAT INFORMATION BASED ON THE TYPE OF STORE.   10890001
      * TYPE OF STORE....                                               10900001
      *    V - VIRTUAL STORE (E-STORE)                                  10910001
      *    P - PHYSICAL STORE.                                          10920001
      *    A - ALL STORES.                                              10930001
      ******************************************************************10940001
       3400-PROCESS-SALES-FILE.                                         10950001
      *** EXTRACT RECORD BY CHANNEL CODE, V - VIRTUAL, P - PHYSICAL...  10960001
      *                                                                 10970001
           ADD 1 TO PA-INPUT-COUNT-IN                                   10980001
                    PA-SALES-COUNT-IN.                                  10990001
      *                                                                 11000001
           IF ML022-SALE                                                11010001
               CONTINUE                                                 11020001
           ELSE                                                         11030001
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME           11040001
               DISPLAY '** ERROR PROCESSING SALES EXTRACT FILE.'        11050001
               DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'         11060001
               DISPLAY  ML022-SALE-RECORD                               11070001
               MOVE +4008 TO ABEND-CODE                                 11080001
               DISPLAY 'TRANS CODE : ', ML022-SALE-TRANS-CDE            11090001
               PERFORM 9999-ABEND-ROUTINE                               11100001
           END-IF.                                                      11110001
      *                                                                 11120001
           INITIALIZE ML149-UPDATE-REC.                                 11130001
      *                                                                 11140001
           IF PS-FIRST-SALES-REC                                        11150001
               MOVE 'N' TO PS-SALE-FIRST-TIME-SW                        11160001
               MOVE ML022-SALE-FIN-LIAB-DTE TO SV-SALE-FIN-LIAB-DTE     11170001
               PERFORM 7000-GET-SALE-FSCL-WK-END-DTE                    11180001
           ELSE                                                         11190001
               IF ML022-SALE-FIN-LIAB-DTE NOT = SV-SALE-FIN-LIAB-DTE    11200001
                   MOVE ML022-SALE-FIN-LIAB-DTE TO SV-SALE-FIN-LIAB-DTE 11210001
                   PERFORM 7000-GET-SALE-FSCL-WK-END-DTE                11220001
               END-IF                                                   11230001
           END-IF.                                                      11240001
                                                                        11250001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        11260001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    11270001
                                                                        11280001
      *    DETERMINE THE POSTING DATE FOR THE SALE.                     11290001
      *        BKPOST_SLS_IND INDICATES THAT SALES SHOULD BE BACK-      11300001
      *        POSTED.  IF THE SALE OCCURRED DURING THE POSTING WEEK,   11310001
      *        POST IT TO THE POSTING WEEK.  IF NOT, BACKPOST IT TO THE 11320001
      *        PREVIOUS WEEK.                                           11330001
           IF CC-BACKPOST-SALES                                         11340001
               IF DTATTR-FSCL-WK-END-DTE = MLCNTL-PSTG-FSCL-WK-DTE      11350001
                   MOVE MLCNTL-PSTG-FSCL-WK-NBR TO ML149-FSCL-WK-NBR    11360001
                   MOVE MLCNTL-PSTG-FSCL-WK-DTE TO ML149-FSCL-WK-END-DTE11370001
               ELSE                                                     11380001
                   MOVE PV-PFW-WK-NBR           TO ML149-FSCL-WK-NBR    11390001
                   MOVE DTATTR-PFW-END-DTE      TO ML149-FSCL-WK-END-DTE11400001
               END-IF                                                   11410001
           ELSE                                                         11420001
      *        BKPOST_SLS_IND INDICATES THAT SALES SHOULD NOT BE        11430001
      *        BACKPOSTED.  POST ALL SALES TO THE POSTING WEEK.         11440001
               MOVE MLCNTL-PSTG-FSCL-WK-NBR  TO ML149-FSCL-WK-NBR       11450001
               MOVE MLCNTL-PSTG-FSCL-WK-DTE  TO ML149-FSCL-WK-END-DTE   11460001
           END-IF.                                                      11470001
                                                                        11480001
           MOVE ML022-SALE-DEPT-NBR          TO ML149-DEPT-NBR.         11490001
           MOVE ML022-SALE-CLASS-NBR         TO ML149-SUB-CL-NBR.       11500001
           MOVE ML022-SALE-FIN-LIAB-DTE      TO ML149-FIN-LIAB-DTE.     11510001
           MOVE ML022-SALE-TRANS-CDE         TO ML149-TRN-TYP-CDE.      11520001
           MOVE ML022-SALE-EXTD-RTL-AMT      TO ML149-SLS-RTL-AMT.      11530001
                                                                        11540001
      *** DETERMINE THE CHANNEL CODE FOR THE STORE.                     11550001
           MOVE ML022-SALE-STORE-NBR       TO PV-STORE-NUMBER.          11560001
  |        MOVE PV-STORE-NUMBER            TO WS-EDIT-STORE-ALPH        11570001
  |        INSPECT WS-EDIT-STORE-ALPH                                   11580001
  |            REPLACING LEADING ' ' BY '0'.                            11590001
  |        MOVE WS-EDIT-STORE-NUM          TO PV-STR-NDX.               11600001
  |                                                                     11610001
W26606**** IF XMT00001-E-BUS-IND IS EQUAL TO 'Y'                        11620001
W26603     IF WS-E-BUS-IND (PV-STR-NDX) IS EQUAL TO 'Y'                 11630001
               MOVE 'V'                    TO ML149-CHNL-CDE            11640001
           ELSE                                                         11650001
               MOVE 'P'                    TO ML149-CHNL-CDE            11660001
           END-IF.                                                      11670001
                                                                        11680001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            11690001
                                                                        11700001
           ADD +1 TO PA-SALES-COUNT-OUT                                 11710001
                     PA-OUTPUT-COUNT-OUT.                               11720001
      *                                                                 11730001
      *** EXTRACT RECORD FOR THE "ALL STORES" CHANNEL CODE....          11740001
                                                                        11750001
           ADD 1 TO PA-INPUT-COUNT-IN                                   11760001
                    PA-SALES-COUNT-IN.                                  11770001
                                                                        11780001
           IF ML022-SALE                                                11790001
               CONTINUE                                                 11800001
           ELSE                                                         11810001
               DISPLAY '** ABEND IN PROGRAM ' PC-PROGRAM-NAME           11820001
               DISPLAY '** ERROR PROCESSING SALES EXTRACT FILE.'        11830001
               DISPLAY '** TRAN CODE NOT VALID WITH FILE TYPE.'         11840001
               DISPLAY  ML022-SALE-RECORD                               11850001
               MOVE +4008 TO ABEND-CODE                                 11860001
               DISPLAY 'TRANS CODE : ', ML022-SALE-TRANS-CDE            11870001
               PERFORM 9999-ABEND-ROUTINE                               11880001
           END-IF.                                                      11890001
                                                                        11900001
           INITIALIZE ML149-UPDATE-REC.                                 11910001
                                                                        11920001
           IF PS-FIRST-SALES-REC                                        11930001
               MOVE 'N' TO PS-SALE-FIRST-TIME-SW                        11940001
               MOVE ML022-SALE-FIN-LIAB-DTE TO SV-SALE-FIN-LIAB-DTE     11950001
               PERFORM 7000-GET-SALE-FSCL-WK-END-DTE                    11960001
           ELSE                                                         11970001
               IF ML022-SALE-FIN-LIAB-DTE NOT = SV-SALE-FIN-LIAB-DTE    11980001
                   MOVE ML022-SALE-FIN-LIAB-DTE TO SV-SALE-FIN-LIAB-DTE 11990001
                   PERFORM 7000-GET-SALE-FSCL-WK-END-DTE                12000001
               END-IF                                                   12010001
           END-IF.                                                      12020001
                                                                        12030001
           MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO ML149-FSCL-MN-NBR.        12040001
           MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO ML149-FSCL-MN-END-DTE.    12050001
           MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO ML149-FSCL-WK-NBR.        12060001
           MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO ML149-FSCL-WK-END-DTE.    12070001
           MOVE ML022-SALE-DEPT-NBR        TO ML149-DEPT-NBR.           12080001
           MOVE ML022-SALE-CLASS-NBR       TO ML149-SUB-CL-NBR.         12090001
           MOVE 'A'                        TO ML149-CHNL-CDE.           12100001
           MOVE ML022-SALE-FIN-LIAB-DTE    TO ML149-FIN-LIAB-DTE.       12110001
           MOVE ML022-SALE-TRANS-CDE       TO ML149-TRN-TYP-CDE.        12120001
           MOVE ML022-SALE-EXTD-RTL-AMT    TO ML149-SLS-RTL-AMT.        12130001
                                                                        12140001
           PERFORM 5000-WRITE-OUTPUT-RECORD.                            12150001
                                                                        12160001
           ADD +1 TO PA-SALES-COUNT-OUT                                 12170001
                     PA-OUTPUT-COUNT-OUT.                               12180001
                                                                        12190001
           PERFORM 4400-READ-SALES-FILE.                                12200001
                                                                        12210001
      ******************************************************************12220001
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO     12230001
      ******************************************************************12240001
       4000-READ-CONTROL-TABLE.                                         12250001
      *                                                                 12260001
            EXEC SQL                                                    12270001
                SELECT PSTG_FSCL_MN_DTE                                 12280001
                      ,PSTG_FSCL_MN_NBR                                 12290001
                      ,PSTG_FSCL_WK_DTE                                 12300001
                      ,PSTG_FSCL_WK_NBR                                 12310001
                 INTO  :MLCNTL-PSTG-FSCL-MN-DTE                         12320001
                      ,:MLCNTL-PSTG-FSCL-MN-NBR                         12330001
                      ,:MLCNTL-PSTG-FSCL-WK-DTE                         12340001
                      ,:MLCNTL-PSTG-FSCL-WK-NBR                         12350001
                 FROM  TMLCNTL                                          12360001
W26603           WITH UR                                                12370001
            END-EXEC.                                                   12380001
      *                                                                 12390001
           EVALUATE SQLCODE                                             12400001
               WHEN 0                                                   12410001
                  DISPLAY 'CTRL TABLE POSTING MONTH END DATE : ',       12420001
                                             MLCNTL-PSTG-FSCL-MN-DTE    12430001
                  DISPLAY 'CTRL TABLE POSTING MONTH NUMBER   : ',       12440001
                                             MLCNTL-PSTG-FSCL-MN-NBR    12450001
                  DISPLAY 'CTRL TABLE POSTING WEEK DATE      : '        12460001
                                             MLCNTL-PSTG-FSCL-WK-DTE    12470001
                  DISPLAY 'CTRL TABLE POSTING WEEK NUMBER    : '        12480001
                                             MLCNTL-PSTG-FSCL-WK-NBR    12490001
               WHEN OTHER                                               12500001
                  MOVE '4000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME   12510001
                  MOVE 'SELECT FROM TMLCNTL'   TO PV-OPERATION-ATTEMPTED12520001
                  PERFORM 9999-SQL-ABEND                                12530001
           END-EVALUATE.                                                12540001
                                                                        12550001
99999 ******************************************************************12560001
  |   * READ FILE BACKPOSTING CONTROL CARD.....                         12570001
  |   ******************************************************************12580001
  |    4050-READ-CONTROL-CARD-FILE.                                     12590001
  |                                                                     12600001
  |         READ CONTROL-CARD-FILE  INTO CC-CONTROL-CARD-REC            12610001
  |             AT END SET PS-END-OF-CONTROL-CARD-FILE TO TRUE.         12620001
99999                                                                   12630001
      ******************************************************************12640001
      * READ FILE CONTAINING PURCHASE AND TRANSFER TRANSACTIONS         12650001
      ******************************************************************12660001
       4100-READ-PURCHASE-FILE.                                         12670001
            READ PURCHASE-FILE  INTO ML021-PURCH-RECORD                 12680001
                AT END SET PS-NO-MORE-PURCHASES TO TRUE.                12690001
      *                                                                 12700001
      ******************************************************************12710001
      * READ FILE CONTAINING MARKDOWN AND MARKUP TRANSACTIONS           12720001
      ******************************************************************12730001
       4200-READ-PRICE-CHANGE-FILE.                                     12740001
            READ PRICE-CHANGE-FILE  INTO ML023-PC-RECORD                12750001
                AT END SET PS-NO-MORE-PRICE-CHANGES TO TRUE.            12760001
      *                                                                 12770001
      ******************************************************************12780001
      * READ FILE CONTAINING INVENTORY ADJUSTMENT TRANSACTIONS          12790001
      ******************************************************************12800001
       4300-READ-ADJUSTMENT-FILE.                                       12810001
            READ ADJUSTMENT-FILE INTO ML024-ADJ-RECORD                  12820001
                AT END SET PS-NO-MORE-ADJUSTMENTS TO TRUE.              12830001
      *                                                                 12840001
      ******************************************************************12850001
      * READ SALES TRANSACTION FILE                                     12860001
      ******************************************************************12870001
       4400-READ-SALES-FILE.                                            12880001
            READ SALES-FILE INTO ML022-SALE-RECORD                      12890001
                AT END SET PS-NO-MORE-SALES TO TRUE.                    12900001
      *                                                                 12910001
      ******************************************************************12920001
      * CONTROL WRITING OF ALL RECORDS TO THE OUTPUT FILE               12930001
      ******************************************************************12940001
       5000-WRITE-OUTPUT-RECORD.                                        12950001
      *                                                                 12960001
           WRITE OUTPUT-RECORD FROM ML149-UPDATE-REC.                   12970001
      *                                                                 12980001
      ******************************************************************12990001
      * 7000-GET-SALE-FSCL-WK-END-DTE                                   13000001
      *      GET THE FISCAL WEEK IN WHICH THE SALE OCCURRED.            13010001
      ******************************************************************13020001
       7000-GET-SALE-FSCL-WK-END-DTE.                                   13030001
                                                                        13040001
           EXEC SQL                                                     13050001
               SELECT  FSCL_WK_END_DTE                                  13060001
                INTO  :DTATTR-FSCL-WK-END-DTE                           13070001
                FROM TDTATTR                                            13080001
                WHERE KY_DTE = :SV-SALE-FIN-LIAB-DTE                    13090001
W26603           WITH UR                                                13100001
           END-EXEC.                                                    13110001
                                                                        13120001
           EVALUATE SQLCODE                                             13130001
               WHEN ZERO                                                13140001
                   CONTINUE                                             13150001
               WHEN OTHER                                               13160001
                  MOVE '7000-GET-SALE-FSCL-WK-END-DTE'                  13170001
                                               TO PV-PARAGRAPH-NAME     13180001
                  MOVE PE-MSG-02               TO PV-OPERATION-ATTEMPTED13190001
                  PERFORM 9999-SQL-ABEND                                13200001
           END-EVALUATE.                                                13210001
                                                                        13220001
       7100-GET-PREV-FSCL-WK-DTE.                                       13230001
                                                                        13240001
           EXEC SQL                                                     13250001
               SELECT  PFW_END_DTE                                      13260001
                INTO  :DTATTR-PFW-END-DTE                               13270001
                FROM TDTATTR                                            13280001
                WHERE KY_DTE = :MLCNTL-PSTG-FSCL-WK-DTE                 13290001
W26603           WITH UR                                                13300001
           END-EXEC.                                                    13310001
                                                                        13320001
           EVALUATE SQLCODE                                             13330001
               WHEN ZERO                                                13340001
                   CONTINUE                                             13350001
               WHEN OTHER                                               13360001
                  MOVE '7100-GET-PREV-FSCL-WK-DTE'                      13370001
                                               TO PV-PARAGRAPH-NAME     13380001
                  MOVE 'SELECT FROM TDTATTR'   TO PV-OPERATION-ATTEMPTED13390001
                  PERFORM 9999-SQL-ABEND                                13400001
           END-EVALUATE.                                                13410001
                                                                        13420001
       7200-GET-PREV-FSCL-WK-NBR.                                       13430001
                                                                        13440001
           EXEC SQL                                                     13450001
               SELECT  FSCL_WK_NBR                                      13460001
                INTO  :PV-PFW-WK-NBR                                    13470001
                FROM TDTATTR                                            13480001
                WHERE KY_DTE = :DTATTR-PFW-END-DTE                      13490001
W26603           WITH UR                                                13500001
           END-EXEC.                                                    13510001
                                                                        13520001
           EVALUATE SQLCODE                                             13530001
               WHEN ZERO                                                13540001
                   CONTINUE                                             13550001
               WHEN OTHER                                               13560001
                  MOVE '7200-GET-PREV-FSCL-WK-NBR'                      13570001
                                               TO PV-PARAGRAPH-NAME     13580001
                  MOVE 'SELECT FROM TDTATTR'   TO PV-OPERATION-ATTEMPTED13590001
                  PERFORM 9999-SQL-ABEND                                13600001
           END-EVALUATE.                                                13610001
                                                                        13620001
      ******************************************************************13630001
      * PROCESS FINAL COUNTS FOR INPUT AND OUTPUT COUNTS                13640001
      ******************************************************************13650001
       8000-EOJ-ROUTINE.                                                13660001
99999                                                                   13670001
99999      CLOSE CONTROL-CARD-FILE.                                     13680001
99999                                                                   13690001
           DISPLAY '                                           '.       13700001
           DISPLAY 'TOTAL INPUT  RECORDS  = ', PA-INPUT-COUNT-IN.       13710001
           DISPLAY 'TOTAL OUTPUT RECORDS  = ', PA-OUTPUT-COUNT-OUT.     13720001
           DISPLAY '                                           '.       13730001
           DISPLAY '***** GOOD EOJ *****'.                              13740001
      *                                                                 13750001
W09051***************************************************************** 13760001
W26603* 8200-OPEN-XMT-LOC.                                              13770001
W09051***************************************************************** 13780001
S26603 8200-OPEN-XMT-LOC.                                               13790001
W09051                                                                  13800001
W09051     EXEC SQL                                                     13810001
W26603         OPEN XMT_LOC_CURSOR                                      13820001
W09051     END-EXEC.                                                    13830001
  |                                                                     13840001
  |        EVALUATE TRUE                                                13850001
  |          WHEN SQLCODE IS EQUAL TO ZERO                              13860001
  |            CONTINUE                                                 13870001
  |          WHEN SQLWARN IS NOT EQUAL TO SPACES                        13880001
W09051       WHEN SQLCODE IS NOT EQUAL TO ZERO                          13890001
W26603        MOVE '8200-OPEN-XMT-LOC' TO PV-PARAGRAPH-NAME             13900001
W09051        MOVE 'OPEN CURSOR'       TO PV-OPERATION-ATTEMPTED        13910001
W26603        MOVE 'Y'                 TO PS-XMT-LOC-DONE-SW            13920001
W09051        PERFORM 9999-SQL-ABEND                                    13930001
  |        END-EVALUATE.                                                13940001
  |                                                                     13950001
  |   ***************************************************************** 13960001
  |   * 8300-LOAD-STORE-TABLE.                                          13970001
  |   ***************************************************************** 13980001
  |    8300-LOAD-STORE-TABLE.                                           13990001
  |                                                                     14000001
W09051     EXEC SQL                                                     14010001
W26603         FETCH XMT_LOC_CURSOR                                     14020001
W26603         INTO :XMT00001-LOC-NBR,                                  14030001
W26603              :XMT00001-E-BUS-IND :PV-NULL-INDICATOR              14040001
W09051     END-EXEC.                                                    14050001
  |                                                                     14060001
  |        EVALUATE TRUE                                                14070001
W09051       WHEN SQLCODE IS EQUAL TO ZERO                              14080001
W26603         MOVE XMT00001-LOC-NBR    TO PV-STR-NDX                   14090001
W26603*        MOVE WS-EDIT-STORE-NUM   TO PV-STR-NDX                   14100001
W26603         IF PV-NULL-INDICATOR < 0                                 14110001
W26603            MOVE 'N'     TO WS-E-BUS-IND (PV-STR-NDX)             14120001
W26603         ELSE                                                     14130001
W26603            MOVE XMT00001-E-BUS-IND                               14140001
W26603                         TO WS-E-BUS-IND (PV-STR-NDX)             14150001
               END-IF                                                   14160001
W09051       WHEN SQLCODE IS EQUAL TO +100                              14170001
W26603         MOVE 'Y' TO PS-XMT-LOC-DONE-SW                           14180001
W09051       WHEN SQLWARN IS NOT EQUAL TO SPACES                        14190001
  |          WHEN SQLCODE IS NOT EQUAL TO ZERO                          14200001
  |            MOVE '8300-LOAD-STORE-TABLE' TO PV-PARAGRAPH-NAME        14210001
W09051         MOVE 'LOAD TABLE'        TO PV-OPERATION-ATTEMPTED       14220001
W26603         MOVE 'Y'        TO PS-XMT-LOC-DONE-SW                    14230001
W09051         PERFORM 9999-SQL-ABEND                                   14240001
  |        END-EVALUATE.                                                14250001
  |                                                                     14260001
W09051***************************************************************** 14270001
W26603* 8400-CLOSE-XMT-LOC.                                             14280001
W09051***************************************************************** 14290001
W26603  8400-CLOSE-XMT-LOC.                                             14300001
W09051                                                                  14310001
W09051     EXEC SQL                                                     14320001
W26603*        CLOSE TSRT000_CURSOR                                     14330001
W26603         CLOSE XMT_LOC_CURSOR                                     14340001
W09051     END-EXEC.                                                    14350001
  |                                                                     14360001
   |       EVALUATE TRUE                                                14370001
   |         WHEN SQLCODE IS EQUAL TO ZERO                              14380001
   |           CONTINUE                                                 14390001
   |         WHEN SQLWARN IS NOT EQUAL TO SPACES                        14400001
W09051       WHEN SQLCODE IS NOT EQUAL TO ZERO                          14410001
W26603        MOVE '8400-CLOSE-XMT-LOC' TO PV-PARAGRAPH-NAME            14420001
W09051        MOVE 'CLOSE CURSOR'       TO PV-OPERATION-ATTEMPTED       14430001
W26603        MOVE 'Y'                  TO PS-XMT-LOC-DONE-SW           14440001
W09051        PERFORM 9999-SQL-ABEND                                    14450001
   |       END-EVALUATE.                                                14460001
W09051                                                                  14470001
      ******************************************************************14480001
      * COMMON ABEND MODULE                                             14490001
      ******************************************************************14500001
       9999-ABEND-ROUTINE.                                              14510001
               CALL 'ILBOABN0' USING ABEND-CODE.                        14520001
                                                                        14530001
      ******************************************************************14540001
      *  STANDARD DB2 ERROR ABEND ROUTINE                               14550001
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             14560001
      ******************************************************************14570001
       9999-SQL-ABEND.                                                  14580001
                                                                        14590001
           MOVE +4013                 TO ABEND-CODE.                    14600001
           DISPLAY '**  ABEND     **'.                                  14610001
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              14620001
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            14630001
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       14640001
                                                                        14650001
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         14660001
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        14670001
                                                                        14680001
           COPY DPPD004.                                                14690001
           CALL 'ILBOABN0'  USING ABEND-CODE.                           14700001
      *                                                                 14710001
