000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT178.                                                 
000600 AUTHOR.        DENISE HAHN                                               
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   MAY, 2016.                                                
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM READS THE EXTRACT FILE CONTAINING THE SHIPPED             
001300*  CARTONS FOR A GIVEN DATE RANGE.  CREATE BY SUKUT176                    
001301*                                                                         
001302*  EXTRACT FILE IS THEN PROCESSED BY SUKUT177 TO GET THE FOLLOWING        
001304*  FOR EACH TRIP - GET THE TRIP CONTENTS                                  
001305*      - SML#'S FROM SUT_OTBND_CART (OTBND_TRIP_ID)                       
001306*      - P2L CARTON - DETERMINE - OPEN/CLOSE TMST                         
001307*        TIMES CONVERTED TO CENTRAL TIME BASED ON PARM                    
001308*        TIMEZN - DIFFERENCE BETWEEN DC AND CENTRAL TIME                  
001309*      - CHECK WHEN THE CARTON WAS ATTACHED TO THE TRAILER                
001310*      - CHECK IF SCANNED ON OR DIVERTED TO THE TRAILER                   
001311*      - CHECK IF CARTON WAS PART OF A MASTER CONTAINER                   
001314*                                                                         
001315*  SUKUT178 WILL GET THE CARTON CONTENTS AND RECEIVER INFORMATION         
001316*                                                                         
001317*  SUKUT179 WILL CALCULATE THE TIMES BETWEEN EACH STATUS                  
001318*                                                                         
001319*  INPUT FILE IS IN SURD117 FORMAT                                        
001320*  OUTPUT FILE IS IN SURD117 FORMAT                                       
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003300     SELECT INPUT-FILE                                                    
003400            ASSIGN TO INP01.                                              
003401                                                                          
003402     SELECT OUTPUT-FILE                                                   
003403            ASSIGN TO OUT01.                                              
003410                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
003910                                                                          
004000 FD  INPUT-FILE                                                           
004100     RECORDING MODE IS F                                                  
004200     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004600 01  INPUT-RECORD               PIC X(550).                               
004700                                                                          
004800 FD  OUTPUT-FILE                                                          
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005300 01  OUTPUT-RECORD              PIC X(550).                               
005470                                                                          
005500                                                                          
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005801                                                                          
005810 01  DK-FIELDS.                                                           
005820     05  DK-OTBND-TRIP-ID                  PIC S9(09)  COMP.              
005821     05  DK-SHIPT-UNT-ID                   PIC S9(18)V COMP-3.            
005822     05  DK-OPER-UNT-ID                    PIC S9(04)  COMP.              
005823     05  DK-DEST-OPER-UNT-ID               PIC S9(04)  COMP.              
005830                                                                          
005840 01  PROGRAM-VARIABLES.                                                   
005841     05  PV-PARAGRAPH-NAME                  PIC  X(40).                   
005842     05  PV-DB2-OPERATION-ATTEMPTED         PIC  X(40).                   
005843*                                                                         
005844     05  PV-MESSAGE-1                       PIC  X(100).                  
005845     05  PV-MESSAGE-2                       PIC  X(100).                  
005846     05  PV-SQLCODE                         PIC  -999.                    
005847*                                                                         
005857     05  PV-RCVR-ARRIVE-TMST                PIC X(26).                    
005858     05  PV-RCVR-ARRIVE-TMST-R REDEFINES                                  
005859                                  PV-RCVR-ARRIVE-TMST.                    
005860         10  PV-RCVR-ARRIVE-DATE            PIC  X(10).                   
005861         10  PV-RCVR-ARRIVE-DASH1           PIC  X(01).                   
005862         10  PV-RCVR-ARRIVE-TIME            PIC  X(08).                   
005863         10  PV-RCVR-ARRIVE-MS              PIC  X(07).                   
005864*                                                                         
005865     05  PV-P2L-PACKED-TMST                 PIC X(26).                    
005866     05  PV-P2L-PACKED-TMST-R  REDEFINES                                  
005867                                  PV-P2L-PACKED-TMST.                     
005868         10  PV-P2L-PACKED-DATE             PIC  X(10).                   
005869         10  PV-P2L-PACKED-DASH1            PIC  X(01).                   
005870         10  PV-P2L-PACKED-TIME             PIC  X(08).                   
005880         10  PV-P2L-PACKED-MS               PIC  X(07).                   
006036*                                                                         
006037     05  PV-PREV-PO                         PIC S9(9) COMP.               
006038     05  PV-PREV-RCVR                       PIC S9(9) COMP.               
006039*                                                                         
006041     05  PV-DISP-PO                         PIC  Z(09).                   
006042     05  PV-DISP-RCVR                       PIC  Z(04).                   
006046     05  PV-DISP-SHIPT-UNT-ID               PIC  9(18).                   
006050*                                                                         
006051     05  PV-DISPLAY-TRIP                    PIC  Z(09).                   
006052     05  PV-DISPLAY-OPER                    PIC  Z(04).                   
006053     05  PV-DISPLAY-STORE                   PIC  Z(04).                   
006054     05  PV-DISPLAY-COUNT                   PIC  ZZZ,ZZZ,ZZZ.             
006055     05  PV-FETCH-COUNT                     PIC  9(09).                   
006056     05  PV-TRIP-CRTN-CNT                   PIC  9(09).                   
006057     05  PV-INPUT-COUNTER                   PIC  9(09).                   
006058     05  PV-INPUT-BREAK                     PIC  9(09).                   
006059     05  PV-WRITE-COUNTER                   PIC  9(09).                   
006060     05  PV-FETCH-COUNT-DISP                PIC  9(09).                   
006061     05  PV-MST-SHIPT-UNT-ID                PIC S9(18)V COMP-3.           
006062                                                                          
006063     05  PV-MIN-P2L-UCC128-TMST             PIC  X(26).                   
006064     05  PV-MIN-P2L-UCC128-TMST-R                                         
006065                        REDEFINES PV-MIN-P2L-UCC128-TMST.                 
006066         10  PV-MIN-P2L-UCC128-DATE         PIC  X(10).                   
006067         10  FILLER                         PIC  X(01).                   
006068         10  PV-MIN-P2L-UCC128-TIME         PIC  X(08).                   
006069         10  FILLER                         PIC  X(07).                   
006070                                                                          
006071     05  PV-MIN-P2L-PHYSCL-TMST             PIC  X(26).                   
006072     05  PV-MIN-P2L-PHYSCL-TMST-R                                         
006073                        REDEFINES PV-MIN-P2L-PHYSCL-TMST.                 
006074         10  PV-MIN-P2L-PHYSCL-DATE         PIC  X(10).                   
006075         10  FILLER                         PIC  X(01).                   
006076         10  PV-MIN-P2L-PHYSCL-TIME         PIC  X(08).                   
006077         10  FILLER                         PIC  X(07).                   
006078                                                                          
006079     05  PV-MIN-P2L-TRACK-TMST              PIC  X(26).                   
006080     05  PV-MIN-P2L-TRACK-TMST-R                                          
006081                        REDEFINES PV-MIN-P2L-TRACK-TMST.                  
006082         10  PV-MIN-P2L-TRACK-DATE          PIC  X(10).                   
006083         10  FILLER                         PIC  X(01).                   
006084         10  PV-MIN-P2L-TRACK-TIME          PIC  X(08).                   
006085         10  FILLER                         PIC  X(07).                   
006086                                                                          
006087     05  PV-PROCESSED-TMST                  PIC  X(26).                   
006088     05  PV-PROCESSED-TMST-R      REDEFINES PV-PROCESSED-TMST.            
006089         10  PV-PROCESSED-DATE              PIC  X(10).                   
006090         10  FILLER                         PIC  X(01).                   
006091         10  PV-PROCESSED-TIME              PIC  X(08).                   
006092         10  FILLER                         PIC  X(07).                   
006093                                                                          
006094*                                                                         
006095 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
006096                                                   COMP SYNC.             
006097 01  PROGRAM-SWITCHES.                                                    
006098     05  PS-EOF-INPUT-SW                    PIC X(01) VALUE 'N'.          
006099         88  PS-EOF-INPUT                             VALUE 'Y'.          
006100         88  PS-EOF-INPUT-NO                          VALUE 'N'.          
006101     05  PS-EOF-CRTN-CONTENTS-SW            PIC X(01) VALUE 'N'.          
006102         88  PS-EOF-CRTN-CONTENTS                     VALUE 'Y'.          
006103         88  PS-EOF-CRTN-CONTENTS-NO                  VALUE 'N'.          
006104     05  PS-EOF-CRTN-PKD-UPC-SW             PIC X(01) VALUE 'N'.          
006105         88  PS-EOF-CRTN-PKD-UPC                      VALUE 'Y'.          
006106         88  PS-EOF-CRTN-PKD-UPC-NO                   VALUE 'N'.          
006107                                                                          
006108******************************************************************        
006109* INPUT & OUTPUT RECORD LAYOUT                                            
006110******************************************************************        
006111     COPY SURD117.                                                        
006112*                                                                         
006113* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006114* SUT_OTBND_UPC - OUTBOUND CARTON UPC                                     
006115* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006116     EXEC SQL                                                             
006117          INCLUDE SUT00028                                                
006118     END-EXEC.                                                            
006119                                                                          
006120* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006121* SUT_OTBND_PKD_UPC - OUTBOUND PACKED UPC (P2L/JWL ONLY)                  
006122* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006123     EXEC SQL                                                             
006124          INCLUDE SUT00025                                                
006130     END-EXEC.                                                            
006200                                                                          
006256* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006257* TRECEIV - RECEIVER                                                      
006258* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006259     EXEC SQL                                                             
006260          INCLUDE TRECEIV                                                 
006261     END-EXEC.                                                            
006262                                                                          
006263* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006264* TDISTHD - DISTRIBUTION HEADER                                           
006265* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006266     EXEC SQL                                                             
006267          INCLUDE TDISTHD                                                 
006268     END-EXEC.                                                            
006269                                                                          
006270* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006271* TPKSLIP - PACK SLIP                                                     
006272* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006273     EXEC SQL                                                             
006274          INCLUDE TPKSLIP                                                 
006275     END-EXEC.                                                            
006276                                                                          
006277* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006278* TINPKSU - PACKSLIP TO INBOUND CARTON ASSOCIATION                        
006279* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006280     EXEC SQL                                                             
006281          INCLUDE TINPKSU                                                 
006282     END-EXEC.                                                            
006283                                                                          
006284* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006285* TSULBXF - INBOUND TO OUTBOUND CROSS REFERENCE                           
006286* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006287     EXEC SQL                                                             
006288          INCLUDE TSULBXF                                                 
006289     END-EXEC.                                                            
006290                                                                          
006291* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006292* TSHUUPC - INBOUND CARTON CONTENTS                                       
006293* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006294     EXEC SQL                                                             
006295          INCLUDE TSHUUPC                                                 
006296     END-EXEC.                                                            
006297                                                                          
006305* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006306* RCT_PO_RCVR_TRKG - PO TRACKING                                          
006307* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006308     EXEC SQL                                                             
006309          INCLUDE RCT00017                                                
006310     END-EXEC.                                                            
006311                                                                          
006312* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006313*                                                                         
006314* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006315     EXEC SQL                                                             
006316          INCLUDE SQLCA                                                   
006320     END-EXEC.                                                            
006400                                                                          
006500* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
006600*                                                                         
006700* OUTBOUND CARTON CONTENTS CURSOR                                         
006800*                                                                         
006900* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
007000     EXEC SQL                                                             
007100         DECLARE DC-TRIP-CRTN-CONTENTS-CSR CURSOR FOR                     
007200          SELECT                                                          
007210                 TRN_NBR                                                  
007220               , RCVR_SEQ_NBR                                             
007230               , TRN_LNE_NBR                                              
007231               , TRN_TYP_CDE                                              
007232               , UPC_NBR                                                  
007240               , SHPD_UNT_QTY                                             
007250            FROM SUT_OTBND_UPC                                            
007260           WHERE SHIPT_UNT_ID  =  :DK-SHIPT-UNT-ID                        
007261           ORDER BY TRN_NBR                                               
007262                  , RCVR_SEQ_NBR                                          
007263                  , TRN_LNE_NBR                                           
007270         WITH UR                                                          
007280     END-EXEC.                                                            
007290                                                                          
007291                                                                          
007292* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
007293*                                                                         
007294* PACKED UPC CURSOR - THE SAME ITEM CAN BE PICKED MULTIPLE TIMES          
007295*                                                                         
007296* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *         
007297     EXEC SQL                                                             
007298         DECLARE DC-P2L-PACKED-UPC-CSR CURSOR FOR                         
007299          SELECT                                                          
007300                 PKD_TMST                                                 
007310               , PKD_QTY                                                  
007360            FROM SUT_OTBND_PKD_UPC                                        
007370           WHERE SHIPT_UNT_ID  =  :DK-SHIPT-UNT-ID                        
007371             AND TRN_NBR       =  :SUT00028-TRN-NBR                       
007372             AND RCVR_SEQ_NBR  =  :SUT00028-RCVR-SEQ-NBR                  
007373             AND TRN_LNE_NBR   =  :SUT00028-TRN-LNE-NBR                   
007374             AND TRN_TYP_CDE   =  :SUT00028-TRN-TYP-CDE                   
007375             AND UPC_NBR       =  :SUT00028-UPC-NBR                       
007380           ORDER BY PKD_TMST                                              
007392         WITH UR                                                          
007393     END-EXEC.                                                            
007394                                                                          
007395                                                                          
007400*-----------------------------------------------------------------        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007510*-----------------------------------------------------------------        
007700     COPY DPWS004.                                                        
007800                                                                          
018300                                                                          
018400                                                                          
018600 PROCEDURE DIVISION.                                                      
018801                                                                          
018810*-----------------------------------------------------------------        
018820*                                                                         
018840*-----------------------------------------------------------------        
018900 0000-PROGRAM-MAINLINE.                                                   
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     DISPLAY '********************************************'               
019530             '**********'.                                                
019600     IF  PS-EOF-INPUT-NO                                                  
019700         PERFORM 2000-PROCESS-FILE                                        
019800           UNTIL PS-EOF-INPUT                                             
019900     END-IF.                                                              
019910                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020113                                                                          
020131     DISPLAY '********************************************'               
020132             '**********'.                                                
020140                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020310                                                                          
020320*-----------------------------------------------------------------        
020330*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020340*    -- OPEN INPUT FILE                                                   
020341*    -- OPEN OUTPUT FILE                                                  
020350*    -- INITIAL READ OF INPUT                                             
020361*    -- IF EMPTY FILE  - JOB FINISHES - NOTHING TO REPORT                 
020370*-----------------------------------------------------------------        
020400 1000-INITIALIZE.                                                         
021100                                                                          
021500     OPEN INPUT  INPUT-FILE                                               
021510          OUTPUT OUTPUT-FILE.                                             
021600                                                                          
021700     PERFORM 8000-READ-INPUT-REC.                                         
021710                                                                          
022010                                                                          
025449*-----------------------------------------------------------------        
025450*  LOOP THROUGH THE RESULTS TABLE.                                        
025451*  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT MEET CRITERIA.       
025452*-----------------------------------------------------------------        
025460 2000-PROCESS-FILE.                                                       
026000                                                                          
026001     MOVE  SU117-SML-NBR  TO  DK-SHIPT-UNT-ID.                            
026002                                                                          
026003     SET  PS-EOF-CRTN-CONTENTS-NO  TO  TRUE.                              
026004                                                                          
026005     PERFORM 7015-OPEN-CARTON-CONTENTS-CSR.                               
026006     PERFORM 7020-FETCH-CARTON-CONTENTS-CSR.                              
026007                                                                          
026008     PERFORM 3000-PROCESS-CONTENTS-CSR                                    
026009       UNTIL PS-EOF-CRTN-CONTENTS.                                        
026010                                                                          
026200     PERFORM 7025-CLOSE-CARTON-CONTENTS-CSR.                              
026206                                                                          
026210     PERFORM 8000-READ-INPUT-REC.                                         
026300                                                                          
026310                                                                          
026311*-----------------------------------------------------------------        
026312*  CARTON CONTENTS                                                        
026313*-----------------------------------------------------------------        
026314 3000-PROCESS-CONTENTS-CSR.                                               
026320                                                                          
026321     MOVE SUT00028-TRN-NBR      TO SU117-TRN-NBR.                         
026322     MOVE SUT00028-RCVR-SEQ-NBR TO SU117-REC-SEQ-NBR.                     
026323     MOVE SUT00028-TRN-LNE-NBR  TO SU117-TRN-LNE-NBR.                     
026326     MOVE SUT00028-SHPD-UNT-QTY TO SU117-SML-UNT-QTY.                     
026332                                                                          
026335* SU117-SML-SRC-CDE                                                       
026336*      ASN - CHECK-IN           P2L/JWL - PACK-TO-LIGHT/JEWELRY           
026337*      CPS - CASEPACK SPLIT     RDM - CENTRAL STOCK                       
026338*      DMG - DAMAGED            SMO - SMOOTHING                           
026339*                               WMC - WMOS CREATED                        
026340*      MST - MASTER CONTAINER   WMU - WMOS USED UCC128                    
026341*      3WD - 3RD PARTY WAREHOUSE DISTRIBUTION                             
026342                                                                          
026343     IF  SU117-SML-SRC-CDE     = 'MST'                                    
026344         MOVE SPACES  TO  PV-RCVR-ARRIVE-TMST                             
026345     ELSE                                                                 
026346         IF  SU117-SML-SRC-CDE     = 'RDM'  OR                            
026347             SU117-SML-SRC-CDE     = '3WD'                                
026348             MOVE SU117-SML-CRE-DTE  TO  PV-RCVR-ARRIVE-DATE              
026349             MOVE '-'                TO  PV-RCVR-ARRIVE-DASH1             
026350             MOVE SU117-SML-CRE-TM   TO  PV-RCVR-ARRIVE-TIME              
026351             MOVE '.000000'          TO  PV-RCVR-ARRIVE-MS                
026354         ELSE                                                             
026355             IF  SUT00028-TRN-NBR       =  PV-PREV-PO  AND                
026356                 SUT00028-RCVR-SEQ-NBR  =  PV-PREV-RCVR                   
026357                 CONTINUE                                                 
026358             ELSE                                                         
026359                 PERFORM 4000-GET-RECV-TMST                               
026360                 MOVE SUT00028-TRN-NBR      TO  PV-PREV-PO                
026361                 MOVE SUT00028-RCVR-SEQ-NBR TO  PV-PREV-RCVR              
026362             END-IF                                                       
026363         END-IF                                                           
026364     END-IF.                                                              
026365                                                                          
026366     MOVE  PV-RCVR-ARRIVE-DATE TO SU117-REC-SEQ-ARRIVE-DTE.               
026367     MOVE  PV-RCVR-ARRIVE-TIME TO SU117-REC-SEQ-ARRIVE-TM.                
026368                                                                          
026369     IF  SU117-SML-SRC-CDE     = 'P2L'  OR                                
026370         SU117-SML-SRC-CDE     = 'JWL'                                    
026371         PERFORM 4150-P2L-JWL-PROCESS                                     
026372         PERFORM 4100-CHECK-PKD-UPC                                       
026380     ELSE                                                                 
026382         PERFORM 8100-WRITE-OUTPUT-RECORD                                 
026383     END-IF.                                                              
026384                                                                          
026385     SET  PS-EOF-CRTN-CONTENTS-NO   TO  TRUE                              
026386     SET  PS-EOF-CRTN-PKD-UPC-NO    TO  TRUE                              
026390                                                                          
026433     PERFORM 7020-FETCH-CARTON-CONTENTS-CSR.                              
026434                                                                          
026435                                                                          
026621*-----------------------------------------------------------------        
026622*  GET RECEIVER ARRIVED TIMESTAMP                                         
026623*-----------------------------------------------------------------        
026624 4000-GET-RECV-TMST.                                                      
026625                                                                          
026630     EXEC SQL                                                             
026640         SELECT                                                           
026657                 REC_TMST                                                 
026697           INTO                                                           
026698                :PV-RCVR-ARRIVE-TMST                                      
026701           FROM                                                           
026702                 TRECEIV                                                  
026707          WHERE                                                           
026708                 ORD_NBR         = :SUT00028-TRN-NBR                      
026709            AND  REC_SEQ_NBR     = :SUT00028-RCVR-SEQ-NBR                 
026762          WITH UR                                                         
026763                                                                          
026764     END-EXEC.                                                            
026765                                                                          
026766     EVALUATE TRUE                                                        
026767         WHEN SQLCODE = ZERO                                              
026768              CONTINUE                                                    
026769                                                                          
026770         WHEN SQLCODE = +100                                              
026771              MOVE SPACES  TO  PV-RCVR-ARRIVE-TMST                        
026772                                                                          
026773         WHEN SQLWARN0 NOT = SPACES                                       
026774         WHEN SQLCODE < ZERO                                              
026775         WHEN SQLCODE > ZERO                                              
026776              MOVE SQLCODE               TO PV-SQLCODE                    
026779              MOVE SUT00028-TRN-NBR      TO PV-DISP-PO                    
026780              MOVE SUT00028-RCVR-SEQ-NBR TO PV-DISP-RCVR                  
026781              MOVE SU117-SML-NBR         TO PV-DISP-SHIPT-UNT-ID          
026803*                                                                         
026804              STRING '4005-'                                              
026805                     ' SQL:'   PV-SQLCODE                                 
026806                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
026807                     ' RCVR:'  PV-DISP-PO                                 
026808                     '-'       PV-DISP-RCVR                               
026809                     ' '       SU117-SML-SRC-CDE                          
026810                               DELIMITED BY SIZE                          
026811                          INTO PV-MESSAGE-1                               
026812                                                                          
026813             DISPLAY PV-MESSAGE-1                                         
026814                                                                          
026815             MOVE '4000-GET-RECV-TMST             '                       
026816                                 TO PV-PARAGRAPH-NAME                     
026817             MOVE 'GET RECEIVER ARRIVED TIMESTAMP '                       
026818                                 TO PV-DB2-OPERATION-ATTEMPTED            
026819             PERFORM 9100-SQL-ABEND                                       
026820     END-EVALUATE.                                                        
026821                                                                          
026822                                                                          
026823*-----------------------------------------------------------------        
026824*  PROCESS PACKED UPC DATABASE FOR CARTON CREATED IN P2L                  
026825*-----------------------------------------------------------------        
026830 4100-CHECK-PKD-UPC.                                                      
026900                                                                          
027000     PERFORM 7050-OPEN-PKD-UPD-CSR.                                       
027001     PERFORM 7055-FETCH-PKD-UPD-CSR.                                      
027010     PERFORM 4110-PROCESS-PKD-UPC                                         
027020       UNTIL PS-EOF-CRTN-PKD-UPC.                                         
027200     PERFORM 7060-CLOSE-PKD-UPD-CSR.                                      
027300                                                                          
027400                                                                          
027665*-----------------------------------------------------------------        
027666*                                                                         
027667*-----------------------------------------------------------------        
027668 4110-PROCESS-PKD-UPC.                                                    
027669                                                                          
027670     MOVE SUT00025-PKD-TMST   TO  PV-P2L-PACKED-TMST.                     
027671     MOVE PV-P2L-PACKED-DATE  TO  SU117-PKD-IN-P2L-SML-DTE.               
027672     MOVE PV-P2L-PACKED-TIME  TO  SU117-PKD-IN-P2L-SML-TM.                
027673     MOVE SUT00025-PKD-QTY    TO  SU117-SML-UNT-QTY.                      
027674                                                                          
027675     PERFORM 8100-WRITE-OUTPUT-RECORD                                     
027676                                                                          
027680     PERFORM 7055-FETCH-PKD-UPD-CSR.                                      
027690                                                                          
027700                                                                          
027800*-----------------------------------------------------------------        
027900* FOR P2L/JWL - WHEN WAS THE EARLIERS CARTON CHECKED IN THAT              
028000*               WENT TO P2L/JWL                                           
028100* - CHECK ALL OF THE RECEIVERS IN THE CARTON THAT USED UCC128 DATA        
028200*   AND DETERMINE THE EARLIEST CHECKIN TIME.                              
028300* - CHECK ALL OF THE RECEIVERS IN THE CARTON THAT USED PHYSICAL           
028400*   LABELS AND DETERMINE THE EARLIEST CREATE TIME.                        
028500*-----------------------------------------------------------------        
028600 4150-P2L-JWL-PROCESS.                                                    
028700                                                                          
028800     MOVE  SPACES  TO  PV-MIN-P2L-UCC128-TMST                             
028900                       PV-MIN-P2L-PHYSCL-TMST                             
028910                       PV-MIN-P2L-TRACK-TMST.                             
029000                                                                          
029100     PERFORM 4155-FIRST-P2L-UCC128.                                       
029200     PERFORM 4160-FIRST-P2L-PHYSCL.                                       
029201                                                                          
029202     IF  PV-MIN-P2L-UCC128-TMST  > SPACES  OR                             
029203         PV-MIN-P2L-PHYSCL-TMST  > SPACES                                 
029204         CONTINUE                                                         
029205     ELSE                                                                 
029210         PERFORM 4165-FIRST-P2L-TRACK                                     
029220     END-IF.                                                              
029300                                                                          
029400     IF  PV-MIN-P2L-UCC128-TMST  > SPACES  AND                            
029500         PV-MIN-P2L-PHYSCL-TMST  > SPACES                                 
029600         IF  PV-MIN-P2L-UCC128-TMST < PV-MIN-P2L-PHYSCL-TMST              
029700             MOVE PV-MIN-P2L-UCC128-TMST TO PV-PROCESSED-TMST             
029800         ELSE                                                             
029900             MOVE PV-MIN-P2L-PHYSCL-TMST TO PV-PROCESSED-TMST             
030000         END-IF                                                           
030100     ELSE                                                                 
030200         IF  PV-MIN-P2L-UCC128-TMST  >  SPACES                            
030300             MOVE PV-MIN-P2L-UCC128-TMST TO PV-PROCESSED-TMST             
030400         ELSE                                                             
030500         IF  PV-MIN-P2L-PHYSCL-TMST  >  SPACES                            
030510             MOVE PV-MIN-P2L-PHYSCL-TMST TO PV-PROCESSED-TMST             
030520         ELSE                                                             
030530             MOVE PV-MIN-P2L-TRACK-TMST  TO PV-PROCESSED-TMST             
030600         END-IF                                                           
030610         END-IF                                                           
030700     END-IF.                                                              
030800                                                                          
030900     MOVE  PV-PROCESSED-DATE TO SU117-SRC-CART-PRC-DTE.                   
031000     MOVE  PV-PROCESSED-TIME TO SU117-SRC-CART-PRC-TM.                    
031100                                                                          
031200                                                                          
031300*-----------------------------------------------------------------        
031400* - CHECK ALL OF THE RECEIVERS IN THE CARTON THAT USED UCC128 DATA        
031500*   AND DETERMINE THE EARLIEST CHECKIN TIME.                              
031600*-----------------------------------------------------------------        
031700 4155-FIRST-P2L-UCC128.                                                   
031800                                                                          
031900     EXEC SQL                                                             
032000         SELECT MIN(E.CRE_TMST)                                           
032100           INTO :PV-MIN-P2L-UCC128-TMST                                   
032200           FROM SUT_OTBND_UPC A                                           
032300              , TDISTHD B                                                 
032400              , TPKSLIP C                                                 
032500              , TINPKSU D                                                 
032600              , TSULBXF E                                                 
032700              , TSHUUPC F                                                 
033000          WHERE A.SHIPT_UNT_ID       = :DK-SHIPT-UNT-ID                   
033100            AND A.TRN_NBR            = :SUT00028-TRN-NBR                  
033200            AND A.RCVR_SEQ_NBR       = :SUT00028-RCVR-SEQ-NBR             
033300            AND A.TRN_LNE_NBR        = :SUT00028-TRN-LNE-NBR              
033310            AND A.TRN_NBR            = B.ORD_NBR                          
033320            AND A.RCVR_SEQ_NBR       = B.RCVR_SEQ_NBR                     
033330            AND A.TRN_LNE_NBR        = B.ORD_LNE_NBR                      
033400            AND B.PREPK_LNE_PRC_CDE IN (' ', 'N', 'D')                    
033500            AND B.UCC128_DAT_USG_CDE = 'U'                                
033600            AND A.TRN_NBR            = C.PO_NBR                           
033700            AND A.RCVR_SEQ_NBR       =  C.REC_SEQ_NBR                     
033800            AND C.PO_NBR             =  D.PO_NBR                          
033900            AND C.SEQ_NBR            =  D.SEQ_NBR                         
034000            AND D.SHIPT_UNT_ID       =  E.SHIPT_UNT_ID                    
034100            AND E.CART_LBL_TYP_CDE   = 'P'                                
034110            AND D.SHIPT_UNT_ID       =  F.SHIPT_UNT_ID                    
034120            AND A.TRN_NBR            =  F.TRN_NBR                         
034130            AND A.TRN_LNE_NBR        =  F.TRN_LNE_NBR                     
034161          WITH UR                                                         
034162                                                                          
034163     END-EXEC.                                                            
034164                                                                          
034165     EVALUATE TRUE                                                        
034166         WHEN SQLCODE = ZERO                                              
034167              CONTINUE                                                    
034168                                                                          
034169         WHEN SQLCODE = +100                                              
034170         WHEN SQLCODE = -305                                              
034171              MOVE SPACES  TO  PV-MIN-P2L-UCC128-TMST                     
034172                                                                          
034173         WHEN SQLWARN0 NOT = SPACES                                       
034174         WHEN SQLCODE < ZERO                                              
034175         WHEN SQLCODE > ZERO                                              
034176              MOVE SQLCODE              TO PV-SQLCODE                     
034177              MOVE DK-OTBND-TRIP-ID     TO PV-DISPLAY-TRIP                
034178              MOVE DK-SHIPT-UNT-ID      TO PV-DISP-SHIPT-UNT-ID           
034179              MOVE SU117-OPER-UNT-ID    TO PV-DISPLAY-OPER                
034180              MOVE SU117-STORE-DEST     TO PV-DISPLAY-STORE               
034181                                                                          
034182              STRING '4155-'                                              
034183                     ' SQL:'   PV-SQLCODE                                 
034184                     ' TRIP:'  PV-DISPLAY-TRIP                            
034185                     ' '       PV-DISPLAY-OPER                            
034186                     '-'       PV-DISPLAY-STORE                           
034187                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034188                               DELIMITED BY SIZE                          
034189                          INTO PV-MESSAGE-1                               
034190                                                                          
034191             DISPLAY PV-MESSAGE-1                                         
034192                                                                          
034193             MOVE '4155-FIRST-P2L-UCC128          '                       
034194                                 TO PV-PARAGRAPH-NAME                     
034195             MOVE 'CHECK WHEN WAS P2L CHECKED IN  '                       
034196                                 TO PV-DB2-OPERATION-ATTEMPTED            
034197             PERFORM 9100-SQL-ABEND                                       
034198     END-EVALUATE.                                                        
034199                                                                          
034200                                                                          
034201*-----------------------------------------------------------------        
034202* - CHECK ALL OF THE RECEIVERS IN THE CARTON THAT USED PHYSICAL           
034203*   LABELS AND DETERMINE THE EARLIEST CREATE TIME.                        
034204*-----------------------------------------------------------------        
034205 4160-FIRST-P2L-PHYSCL.                                                   
034206                                                                          
034207     EXEC SQL                                                             
034208         SELECT MIN(B.CRE_TMST)                                           
034209           INTO :PV-MIN-P2L-PHYSCL-TMST                                   
034210           FROM SUT_OTBND_UPC A                                           
034211              , TDISTHD B                                                 
034212          WHERE A.SHIPT_UNT_ID       = :DK-SHIPT-UNT-ID                   
034213            AND A.TRN_NBR            = :SUT00028-TRN-NBR                  
034214            AND A.RCVR_SEQ_NBR       = :SUT00028-RCVR-SEQ-NBR             
034215            AND A.TRN_LNE_NBR        = :SUT00028-TRN-LNE-NBR              
034216            AND A.TRN_NBR            = B.ORD_NBR                          
034217            AND A.RCVR_SEQ_NBR       = B.RCVR_SEQ_NBR                     
034218            AND A.TRN_LNE_NBR        = B.ORD_LNE_NBR                      
034219            AND B.PREPK_LNE_PRC_CDE IN (' ', 'N', 'D')                    
034220            AND NOT B.UCC128_DAT_USG_CDE = 'U'                            
034221          WITH UR                                                         
034222                                                                          
034223     END-EXEC.                                                            
034224                                                                          
034225     EVALUATE TRUE                                                        
034226         WHEN SQLCODE = ZERO                                              
034227              CONTINUE                                                    
034228                                                                          
034229         WHEN SQLCODE = +100                                              
034230         WHEN SQLCODE = -305                                              
034231              MOVE SPACES  TO  PV-MIN-P2L-PHYSCL-TMST                     
034232                                                                          
034233         WHEN SQLWARN0 NOT = SPACES                                       
034234         WHEN SQLCODE < ZERO                                              
034235         WHEN SQLCODE > ZERO                                              
034236              MOVE SQLCODE              TO PV-SQLCODE                     
034237              MOVE DK-OTBND-TRIP-ID     TO PV-DISPLAY-TRIP                
034238              MOVE DK-SHIPT-UNT-ID      TO PV-DISP-SHIPT-UNT-ID           
034239              MOVE SU117-OPER-UNT-ID    TO PV-DISPLAY-OPER                
034240              MOVE SU117-STORE-DEST     TO PV-DISPLAY-STORE               
034241                                                                          
034242              STRING '4160-'                                              
034243                     ' SQL:'   PV-SQLCODE                                 
034244                     ' TRIP:'  PV-DISPLAY-TRIP                            
034245                     ' '       PV-DISPLAY-OPER                            
034246                     '-'       PV-DISPLAY-STORE                           
034247                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034248                               DELIMITED BY SIZE                          
034249                          INTO PV-MESSAGE-1                               
034250                                                                          
034251             DISPLAY PV-MESSAGE-1                                         
034252                                                                          
034253             MOVE '4160-FIRST-P2L-PHYSCL          '                       
034254                                 TO PV-PARAGRAPH-NAME                     
034255             MOVE 'CHECK WHEN WAS P2L PHYSICAL LBL'                       
034256                                 TO PV-DB2-OPERATION-ATTEMPTED            
034257             PERFORM 9100-SQL-ABEND                                       
034258     END-EVALUATE.                                                        
034259                                                                          
034260                                                                          
034261*-----------------------------------------------------------------        
034262*  IF DISTRIBUTION HEADER HAS ALREADY PURGED, WILL HAVE TO GET            
034263*  THE PROCESS DATE FROM THE TRACKING TABLE. CHECK PO-RCVR-LINE           
034264*-----------------------------------------------------------------        
034265 4165-FIRST-P2L-TRACK.                                                    
034267                                                                          
034268     EXEC SQL                                                             
034269         SELECT MIN(LAST_UPD_TMST)                                        
034270           INTO :PV-MIN-P2L-TRACK-TMST                                    
034271           FROM RCT_PO_RCVR_TRKG                                          
034274          WHERE PO_NBR        = :SUT00028-TRN-NBR                         
034275            AND RCVR_SEQ_NBR  = :SUT00028-RCVR-SEQ-NBR                    
034276            AND PO_LNE_NBR    = :SUT00028-TRN-LNE-NBR                     
034280            AND DC_FUNC_CDE  IN ('22','23')                               
034282          WITH UR                                                         
034283                                                                          
034284     END-EXEC.                                                            
034285                                                                          
034286     EVALUATE TRUE                                                        
034287         WHEN SQLCODE = ZERO                                              
034288              CONTINUE                                                    
034289                                                                          
034290         WHEN SQLCODE = +100                                              
034291         WHEN SQLCODE = -305                                              
034292              MOVE SPACES  TO  PV-MIN-P2L-TRACK-TMST                      
034293              PERFORM 4170-FIRST-P2L-TRACK-RCVR                           
034294                                                                          
034295         WHEN SQLWARN0 NOT = SPACES                                       
034296         WHEN SQLCODE < ZERO                                              
034297         WHEN SQLCODE > ZERO                                              
034298              MOVE SQLCODE              TO PV-SQLCODE                     
034299              MOVE DK-OTBND-TRIP-ID     TO PV-DISPLAY-TRIP                
034300              MOVE DK-SHIPT-UNT-ID      TO PV-DISP-SHIPT-UNT-ID           
034301              MOVE SU117-OPER-UNT-ID    TO PV-DISPLAY-OPER                
034302              MOVE SU117-STORE-DEST     TO PV-DISPLAY-STORE               
034303                                                                          
034304              STRING '4165-'                                              
034305                     ' SQL:'   PV-SQLCODE                                 
034306                     ' TRIP:'  PV-DISPLAY-TRIP                            
034307                     ' '       PV-DISPLAY-OPER                            
034308                     '-'       PV-DISPLAY-STORE                           
034309                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034310                               DELIMITED BY SIZE                          
034311                          INTO PV-MESSAGE-1                               
034312                                                                          
034313             DISPLAY PV-MESSAGE-1                                         
034314                                                                          
034315             MOVE '4165-FIRST-P2L-TRACK           '                       
034316                                 TO PV-PARAGRAPH-NAME                     
034317             MOVE 'CHECK WHEN WAS P2L PHYSICAL LBL'                       
034318                                 TO PV-DB2-OPERATION-ATTEMPTED            
034319             PERFORM 9100-SQL-ABEND                                       
034320     END-EVALUATE.                                                        
034321                                                                          
034322                                                                          
034323*-----------------------------------------------------------------        
034324*  IF DISTRIBUTION HEADER HAS ALREADY PURGED, WILL HAVE TO GET            
034325*  THE PROCESS DATE FROM THE TRACKING TABLE. CHECK PO-RCVR                
034326*-----------------------------------------------------------------        
034327 4170-FIRST-P2L-TRACK-RCVR.                                               
034328                                                                          
034329     EXEC SQL                                                             
034330         SELECT MIN(LAST_UPD_TMST)                                        
034331           INTO :PV-MIN-P2L-TRACK-TMST                                    
034332           FROM RCT_PO_RCVR_TRKG                                          
034333          WHERE PO_NBR        = :SUT00028-TRN-NBR                         
034334            AND RCVR_SEQ_NBR  = :SUT00028-RCVR-SEQ-NBR                    
034336            AND DC_FUNC_CDE  IN ('22','23')                               
034337          WITH UR                                                         
034338                                                                          
034339     END-EXEC.                                                            
034340                                                                          
034341     EVALUATE TRUE                                                        
034342         WHEN SQLCODE = ZERO                                              
034343              CONTINUE                                                    
034344                                                                          
034345         WHEN SQLCODE = +100                                              
034346         WHEN SQLCODE = -305                                              
034347              MOVE SPACES  TO  PV-MIN-P2L-TRACK-TMST                      
034348                                                                          
034349         WHEN SQLWARN0 NOT = SPACES                                       
034350         WHEN SQLCODE < ZERO                                              
034351         WHEN SQLCODE > ZERO                                              
034352              MOVE SQLCODE              TO PV-SQLCODE                     
034353              MOVE DK-OTBND-TRIP-ID     TO PV-DISPLAY-TRIP                
034354              MOVE DK-SHIPT-UNT-ID      TO PV-DISP-SHIPT-UNT-ID           
034355              MOVE SU117-OPER-UNT-ID    TO PV-DISPLAY-OPER                
034356              MOVE SU117-STORE-DEST     TO PV-DISPLAY-STORE               
034357                                                                          
034358              STRING '4160-'                                              
034359                     ' SQL:'   PV-SQLCODE                                 
034360                     ' TRIP:'  PV-DISPLAY-TRIP                            
034361                     ' '       PV-DISPLAY-OPER                            
034362                     '-'       PV-DISPLAY-STORE                           
034363                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034364                               DELIMITED BY SIZE                          
034365                          INTO PV-MESSAGE-1                               
034366                                                                          
034367             DISPLAY PV-MESSAGE-1                                         
034368                                                                          
034369             MOVE '4160-FIRST-P2L-PHYSCL          '                       
034370                                 TO PV-PARAGRAPH-NAME                     
034371             MOVE 'CHECK WHEN WAS P2L PHYSICAL LBL'                       
034372                                 TO PV-DB2-OPERATION-ATTEMPTED            
034373             PERFORM 9100-SQL-ABEND                                       
034374     END-EVALUATE.                                                        
034375                                                                          
034376                                                                          
034377*-----------------------------------------------------------------        
034378*  OPEN THE CARTON CONTENTS CURSOR                                        
034379*-----------------------------------------------------------------        
034380 7015-OPEN-CARTON-CONTENTS-CSR.                                           
034381                                                                          
034382     EXEC SQL                                                             
034383          OPEN DC-TRIP-CRTN-CONTENTS-CSR                                  
034384     END-EXEC                                                             
034385                                                                          
034386     EVALUATE TRUE                                                        
034387         WHEN SQLCODE = ZERO                                              
034388              CONTINUE                                                    
034389         WHEN SQLCODE = -904                                              
034390         WHEN SQLCODE = -911                                              
034391              CONTINUE                                                    
034392         WHEN SQLWARN0 NOT = SPACES                                       
034393         WHEN SQLCODE < ZERO                                              
034394         WHEN SQLCODE > ZERO                                              
034395              MOVE SQLCODE              TO PV-SQLCODE                     
034396*                                                                         
034397              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034398              STRING '7015-'                                              
034399                     ' SQL:'   PV-SQLCODE                                 
034400                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034401                               DELIMITED BY SIZE                          
034402                          INTO PV-MESSAGE-1                               
034403                                                                          
034404                                                                          
034405             MOVE '7015-OPEN-CARTON-CONTENTS-CSR '                        
034406                                 TO PV-PARAGRAPH-NAME                     
034407             MOVE 'OPEN THE DC-TRIP-CRTN-CONTENTS-CSR '                   
034408                                 TO PV-DB2-OPERATION-ATTEMPTED            
034409             PERFORM 9100-SQL-ABEND                                       
034410     END-EVALUATE.                                                        
034411                                                                          
034412                                                                          
034413*-----------------------------------------------------------------        
034414*  FETCH THE ROW FROM TRIP CARTON CONTENTS CURSOR                         
034415*-----------------------------------------------------------------        
034416 7020-FETCH-CARTON-CONTENTS-CSR.                                          
034417                                                                          
034418     EXEC SQL                                                             
034419          FETCH DC-TRIP-CRTN-CONTENTS-CSR                                 
034420           INTO :SUT00028-TRN-NBR                                         
034421               ,:SUT00028-RCVR-SEQ-NBR                                    
034422               ,:SUT00028-TRN-LNE-NBR                                     
034423               ,:SUT00028-TRN-TYP-CDE                                     
034424               ,:SUT00028-UPC-NBR                                         
034425               ,:SUT00028-SHPD-UNT-QTY                                    
034426     END-EXEC.                                                            
034427                                                                          
034428     EVALUATE TRUE                                                        
034429         WHEN SQLCODE = ZERO                                              
034430              CONTINUE                                                    
034431         WHEN SQLCODE = +100                                              
034432             SET PS-EOF-CRTN-CONTENTS           TO  TRUE                  
034433                                                                          
034434         WHEN SQLWARN0 NOT = SPACES                                       
034435         WHEN SQLCODE < ZERO                                              
034436         WHEN SQLCODE > ZERO                                              
034437              MOVE SQLCODE              TO PV-SQLCODE                     
034438              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034439              STRING '7020-'                                              
034440                     ' SQL:'   PV-SQLCODE                                 
034441                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034442                               DELIMITED BY SIZE                          
034443                          INTO PV-MESSAGE-1                               
034444             DISPLAY PV-MESSAGE-1                                         
034445                                                                          
034446             MOVE '7020-FETCH-CARTON-CONTENTS-CSR   '                     
034447                                 TO PV-PARAGRAPH-NAME                     
034448             MOVE 'FETCH THE DC-TRIP-CRTN-CONTENTS-CSR '                  
034449                                 TO PV-DB2-OPERATION-ATTEMPTED            
034450             PERFORM 9100-SQL-ABEND                                       
034451     END-EVALUATE.                                                        
034452                                                                          
034453                                                                          
034454*-----------------------------------------------------------------        
034455*  CLOSE THE CARTON CONTENTS CURSOR                                       
034456*-----------------------------------------------------------------        
034457 7025-CLOSE-CARTON-CONTENTS-CSR.                                          
034458                                                                          
034459     EXEC SQL                                                             
034460          CLOSE DC-TRIP-CRTN-CONTENTS-CSR                                 
034461     END-EXEC                                                             
034462                                                                          
034463     EVALUATE TRUE                                                        
034464         WHEN SQLCODE = ZERO                                              
034465              CONTINUE                                                    
034466         WHEN SQLCODE = -904                                              
034467         WHEN SQLCODE = -911                                              
034468              CONTINUE                                                    
034469         WHEN SQLWARN0 NOT = SPACES                                       
034470         WHEN SQLCODE < ZERO                                              
034471         WHEN SQLCODE > ZERO                                              
034472              MOVE SQLCODE              TO PV-SQLCODE                     
034473              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034474*                                                                         
034475              STRING '7025-'                                              
034476                     ' SQL:'   PV-SQLCODE                                 
034477                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034478                               DELIMITED BY SIZE                          
034479                          INTO PV-MESSAGE-1                               
034480                                                                          
034481             MOVE '7025-CLOSE-CARTON-CONTENTS-CSR '                       
034482                                 TO PV-PARAGRAPH-NAME                     
034483             MOVE 'CLOSE THE DC-TRIP-CRTN-CONTENTS-CSR '                  
034484                                 TO PV-DB2-OPERATION-ATTEMPTED            
034485             PERFORM 9100-SQL-ABEND                                       
034486     END-EVALUATE.                                                        
034487                                                                          
034488                                                                          
034489*-----------------------------------------------------------------        
034490*  OPEN PACKED UPC CURSOR                                                 
034491*-----------------------------------------------------------------        
034492 7050-OPEN-PKD-UPD-CSR.                                                   
034493                                                                          
034494     EXEC SQL                                                             
034495          OPEN DC-P2L-PACKED-UPC-CSR                                      
034496     END-EXEC                                                             
034497                                                                          
034498     EVALUATE TRUE                                                        
034499         WHEN SQLCODE = ZERO                                              
034500              CONTINUE                                                    
034501         WHEN SQLCODE = -904                                              
034502         WHEN SQLCODE = -911                                              
034503              CONTINUE                                                    
034504         WHEN SQLWARN0 NOT = SPACES                                       
034505         WHEN SQLCODE < ZERO                                              
034506         WHEN SQLCODE > ZERO                                              
034507              MOVE SQLCODE              TO PV-SQLCODE                     
034508              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034509*                                                                         
034510              STRING '7050-'                                              
034511                     ' SQL:'   PV-SQLCODE                                 
034512                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034513                               DELIMITED BY SIZE                          
034514                          INTO PV-MESSAGE-1                               
034515                                                                          
034516             MOVE '7050-OPEN-PKD-UPD-CSR         '                        
034517                                 TO PV-PARAGRAPH-NAME                     
034518             MOVE 'OPEN PACKED UPC CURSOR             '                   
034519                                 TO PV-DB2-OPERATION-ATTEMPTED            
034520             PERFORM 9100-SQL-ABEND                                       
034521     END-EVALUATE.                                                        
034522                                                                          
034523                                                                          
034524*-----------------------------------------------------------------        
034525*  FETCH PACKED UPC CURSOR                                                
034526*-----------------------------------------------------------------        
034527 7055-FETCH-PKD-UPD-CSR.                                                  
034528                                                                          
034529     EXEC SQL                                                             
034530          FETCH DC-P2L-PACKED-UPC-CSR                                     
034531           INTO :SUT00025-PKD-TMST                                        
034532               ,:SUT00025-PKD-QTY                                         
034533     END-EXEC.                                                            
034534                                                                          
034535     EVALUATE TRUE                                                        
034536         WHEN SQLCODE = ZERO                                              
034537              CONTINUE                                                    
034538         WHEN SQLCODE = +100                                              
034539             SET PS-EOF-CRTN-PKD-UPC            TO  TRUE                  
034540                                                                          
034541         WHEN SQLWARN0 NOT = SPACES                                       
034542         WHEN SQLCODE < ZERO                                              
034543         WHEN SQLCODE > ZERO                                              
034544              MOVE SQLCODE              TO PV-SQLCODE                     
034545              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034546*                                                                         
034547              STRING '7055-'                                              
034548                     ' SQL:'   PV-SQLCODE                                 
034549                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034550                               DELIMITED BY SIZE                          
034551                          INTO PV-MESSAGE-1                               
034552                                                                          
034553             MOVE '7055-FETCH-PKD-UPD-CSR         '                       
034554                                 TO PV-PARAGRAPH-NAME                     
034555             MOVE 'FETCH PACKED UPC CURSOR             '                  
034556                                 TO PV-DB2-OPERATION-ATTEMPTED            
034557             PERFORM 9100-SQL-ABEND                                       
034558     END-EVALUATE.                                                        
034559                                                                          
034560                                                                          
034561*-----------------------------------------------------------------        
034562*  CLOSE PACKED UPC CURSOR                                                
034563*-----------------------------------------------------------------        
034564 7060-CLOSE-PKD-UPD-CSR.                                                  
034565                                                                          
034566     EXEC SQL                                                             
034567          CLOSE DC-P2L-PACKED-UPC-CSR                                     
034568     END-EXEC                                                             
034569                                                                          
034570     EVALUATE TRUE                                                        
034571         WHEN SQLCODE = ZERO                                              
034572              CONTINUE                                                    
034573         WHEN SQLCODE = -904                                              
034574         WHEN SQLCODE = -911                                              
034575              CONTINUE                                                    
034576         WHEN SQLWARN0 NOT = SPACES                                       
034577         WHEN SQLCODE < ZERO                                              
034578         WHEN SQLCODE > ZERO                                              
034579              MOVE SQLCODE              TO PV-SQLCODE                     
034580              MOVE DK-SHIPT-UNT-ID   TO  PV-DISP-SHIPT-UNT-ID             
034581*                                                                         
034582              STRING '7060-'                                              
034583                     ' SQL:'   PV-SQLCODE                                 
034584                     ' SML:'   PV-DISP-SHIPT-UNT-ID                       
034585                               DELIMITED BY SIZE                          
034586                          INTO PV-MESSAGE-1                               
034587                                                                          
034588             MOVE '7060-CLOSE-PKD-UPD-CSR         '                       
034589                                 TO PV-PARAGRAPH-NAME                     
034590             MOVE 'CLOSE PACKED UPC CURSOR             '                  
034591                                 TO PV-DB2-OPERATION-ATTEMPTED            
034592             PERFORM 9100-SQL-ABEND                                       
034593     END-EVALUATE.                                                        
034594                                                                          
034595                                                                          
034596*-----------------------------------------------------------------        
034597*  READ INPUT FILE                                                        
034598*-----------------------------------------------------------------        
034599 8000-READ-INPUT-REC.                                                     
034600                                                                          
034601     READ INPUT-FILE       INTO                                           
034602           SU117-EXTRACT-RECORD                                           
034603     AT END                                                               
034604         SET PS-EOF-INPUT             TO TRUE                             
034605         DISPLAY ' SUKUT178'                                              
034606                 '  END OF INPUT FILE'                                    
034607         DISPLAY SPACES                                                   
034608     NOT AT END                                                           
034609         ADD  1                 TO  PV-INPUT-COUNTER                      
034610                                    PV-INPUT-BREAK                        
034611         IF  PV-INPUT-BREAK >= 500                                        
034612             MOVE PV-INPUT-COUNTER  TO  PV-DISPLAY-COUNT                  
034613             MOVE SU117-TRIP-ID     TO  PV-DISPLAY-TRIP                   
034614             MOVE SU117-OPER-UNT-ID TO  PV-DISPLAY-OPER                   
034615             MOVE SU117-STORE-DEST  TO  PV-DISPLAY-STORE                  
034616             MOVE SU117-SML-NBR     TO  PV-DISP-SHIPT-UNT-ID              
034617             DISPLAY ' SUKUT178'                                          
034618                     ' TRIP:'  PV-DISPLAY-TRIP                            
034619                     ' OPER:'  PV-DISPLAY-OPER                            
034620                     ' STR:'   PV-DISPLAY-STORE                           
034621                     '  SML:'  PV-DISP-SHIPT-UNT-ID                       
034622                     ' COUNT:'  PV-DISPLAY-COUNT                          
034623             DISPLAY SPACES                                               
034624             MOVE  ZEROES  TO  PV-INPUT-BREAK                             
034625         END-IF                                                           
034626     END-READ.                                                            
034627                                                                          
034628*-----------------------------------------------------------------        
034629*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034630*-----------------------------------------------------------------        
034631 8100-WRITE-OUTPUT-RECORD.                                                
034632                                                                          
034640     WRITE OUTPUT-RECORD                                                  
034700      FROM SU117-EXTRACT-RECORD.                                          
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
035690                                                                          
035700                                                                          
035710*-----------------------------------------------------------------        
035711*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
035720*-----------------------------------------------------------------        
035800 9000-EOJ-ROUTINE.                                                        
035900                                                                          
036300     CLOSE INPUT-FILE                                                     
036310           OUTPUT-FILE.                                                   
036400                                                                          
036410     DISPLAY SPACES.                                                      
036420     DISPLAY ' SUKUT178 '.                                                
036500     MOVE PV-INPUT-COUNTER  TO PV-DISPLAY-COUNT.                          
036601     DISPLAY 'TOTAL INPUT:   ' PV-DISPLAY-COUNT.                          
036610     MOVE PV-WRITE-COUNTER  TO PV-DISPLAY-COUNT.                          
036620     DISPLAY 'TOTAL OUTPUT:  ' PV-DISPLAY-COUNT.                          
036630     DISPLAY SPACES.                                                      
036700                                                                          
040200                                                                          
040210*-----------------------------------------------------------------        
040220*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040230*  ERROR OR WARNING CODE OCCURS.                                          
040240*-----------------------------------------------------------------        
040300 9100-SQL-ABEND.                                                          
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** =  SUKUT178'.                               
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
042310/                                                                         
