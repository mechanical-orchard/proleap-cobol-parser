000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUP007.                                                 
000600 AUTHOR.        GERMAN BANDA.                                             
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   FEBRUARY, 2002.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM USES THE AUDIT EXTRACT FILE TO PERFORM THE PHYSCIAL       
001300*  DELETES ON THE PO AUDIT REQUEST FILE.                                  
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003420     SELECT SUT00015-AUDIT-FILE                                           
003430         ASSIGN TO IN01.                                                  
003431                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
005410 FD  SUT00015-AUDIT-FILE                                                  
005420     RECORDING MODE IS F                                                  
005430     LABEL RECORDS ARE STANDARD                                           
005440     BLOCK CONTAINS 0 RECORDS.                                            
005450                                                                          
005460 01  SUT00015-AUDIT-RECORD      PIC  X(120).                              
005470*                                                                         
005502*                                                                         
005510*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008800*                                                                         
011180******************************************************************        
011190*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011191*  PO AUD REQUEST TABLE                                                   
011192******************************************************************        
011193     EXEC SQL                                                             
011194          INCLUDE SUT00015                                                
011195     END-EXEC.                                                            
011196                                                                          
011220 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-AUDIT-FILE-SW        PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-AUDIT-FILE                     VALUE 'Y'.             
011800         88  PS-MORE-AUDIT-FILE                    VALUE 'N'.             
011900                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'SUKUP007'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013010     05  PC-COMPLETE-TIMESTAMP       PIC  X(26)    VALUE SPACES.          
013020     05  PC-INPROCESS-TIMESTAMP      PIC  X(26)    VALUE SPACES.          
013030     05  PC-COMMIT-HOLD              PIC 9(3)      VALUE 020.             
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014301     05  PV-DELETE-COUNTER           PIC S9(09)    VALUE ZERO             
014302                                                   COMP SYNC.             
014310     05  PV-READ-COUNTER             PIC S9(09)    VALUE ZERO             
014320                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014500                                                                          
014600     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014710     05  PV-COMMIT-COUNT             PIC S9(09)    VALUE +0               
014720                                                   COMP SYNC.             
014730     05  PV-COMMIT-COUNTER           PIC 9(03)     VALUE ZERO.            
014740                                                                          
014750     05  PV-STATUS-COUNTER           PIC S9(09)    VALUE ZERO             
014760                                                   COMP SYNC.             
014800                                                                          
014900                                                                          
015000 01  SD-SYSTEM-DATE.                                                      
015100     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
015200     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
015300     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
015400                                                                          
018440                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018900 0000-PROGRAM-MAINLINE.                                                   
019000******************************************************************        
019100* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019200*      PROGRAM.                                                           
019300******************************************************************        
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     IF PS-MORE-AUDIT-FILE                                                
019700         PERFORM 2000-PROCESS-AUDITS                                      
019800            UNTIL PS-EOF-AUDIT-FILE                                       
019900     END-IF.                                                              
019901                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020400 1000-INITIALIZE.                                                         
020500******************************************************************        
020600*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020700*    -- OPEN OUTPUT FILE                                                  
020800*    -- OPEN CURSOR                                                       
020900*    -- FETCH CURSOR                                                      
021000******************************************************************        
021100                                                                          
021200     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
021300                                                                          
021310     OPEN INPUT  SUT00015-AUDIT-FILE.                                     
021600                                                                          
021700     PERFORM 7000-READ-AUDIT-FILE.                                        
021800                                                                          
022000                                                                          
025410 2000-PROCESS-AUDITS.                                                     
025500******************************************************************        
025600*  DELETE THE AUDITS ON THE EXTRACT FILE                                  
025900******************************************************************        
026000                                                                          
026102     PERFORM 7100-DELETE-AUDITS.                                          
026103                                                                          
026104     ADD +1 TO PV-COMMIT-COUNTER.                                         
026105                                                                          
026106     IF PV-COMMIT-COUNTER > PC-COMMIT-HOLD                                
026107         EXEC SQL                                                         
026108            COMMIT                                                        
026109         END-EXEC                                                         
026110                                                                          
026120         IF  SQLCODE NOT = +0                                             
026130             MOVE '2000-PROCESS-AUDITS           '                        
026140                             TO PV-PARAGRAPH-NAME                         
026150             MOVE 'COMMIT FAILED            '                             
026160                             TO PV-DB2-OPERATION-ATTEMPTED                
026161             PERFORM 9100-SQL-ABEND                                       
026162         END-IF                                                           
026163         ADD +1                          TO PV-COMMIT-COUNT               
026164         MOVE +0 TO PV-COMMIT-COUNTER                                     
026165     END-IF.                                                              
026166                                                                          
026167     IF  PV-STATUS-COUNTER = 5000                                         
026168         MOVE PV-DELETE-COUNTER TO PV-DISPLAY-COUNT                       
026169         DISPLAY 'SUT_PO_AUD_REQ RECORDS DELETED' PV-DISPLAY-COUNT        
026170         MOVE +0 TO PV-STATUS-COUNTER                                     
026171     END-IF.                                                              
026172                                                                          
026173                                                                          
026180     PERFORM 7000-READ-AUDIT-FILE.                                        
026403                                                                          
026410 7000-READ-AUDIT-FILE.                                                    
026500******************************************************************        
026600*  READ THE INPUT FILE                                                    
026700******************************************************************        
026710                                                                          
026720      READ SUT00015-AUDIT-FILE INTO                                       
026730           DCLSUT00015  AT END SET                                        
026740           PS-EOF-AUDIT-FILE TO TRUE.                                     
026750                                                                          
026760      IF PS-EOF-AUDIT-FILE                                                
026770         CONTINUE                                                         
026780      ELSE                                                                
026790         ADD +1 TO PV-READ-COUNTER                                        
026791      END-IF.                                                             
026792                                                                          
026800 7100-DELETE-AUDITS.                                                      
026900******************************************************************        
027000*  DELETE THE SELECTED AUDIT                                              
027100******************************************************************        
027400     EXEC SQL                                                             
027500          DELETE                                                          
027510            FROM SUT_PO_AUD_REQ                                           
027520           WHERE AUD_CNTL_NBR = :SUT00015-AUD-CNTL-NBR                    
027600     END-EXEC.                                                            
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820             ADD +1 TO PV-DELETE-COUNTER                                  
027830                       PV-STATUS-COUNTER                                  
      ***CHG0162221 START                                                       
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
      ***CHG0162221 END                                                         
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7100-DELETE-AUDITS            '                        
028600                             TO PV-PARAGRAPH-NAME                         
028700             MOVE 'DELETE THE SELECTED AUDIT'                             
028800                             TO PV-DB2-OPERATION-ATTEMPTED                
028810             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
028820                     SUT00015-AUD-CNTL-NBR                                
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
030200                                                                          
035800 9000-EOJ-ROUTINE.                                                        
035900******************************************************************        
036000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
036100******************************************************************        
036200     CLOSE SUT00015-AUDIT-FILE.                                           
036400                                                                          
036500     MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                            
036600     DISPLAY 'TOTAL INPUT RECORDS READ '                                  
036601     PV-DISPLAY-COUNT.                                                    
036610     MOVE PV-DELETE-COUNTER TO PV-DISPLAY-COUNT.                          
036620     DISPLAY 'TOTAL SUT00015 RECORDS DELETED ' PV-DISPLAY-COUNT.          
036630     MOVE PV-COMMIT-COUNTER TO PV-DISPLAY-COUNT.                          
036640     DISPLAY 'TOTAL NUMBER OF COMMITS      ' PV-DISPLAY-COUNT.            
036700                                                                          
040292                                                                          
040300 9100-SQL-ABEND.                                                          
040400******************************************************************        
040500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040600*  ERROR OR WARNING CODE OCCURS.                                          
040700******************************************************************        
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
