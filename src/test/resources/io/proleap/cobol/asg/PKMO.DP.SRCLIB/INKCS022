      *-----------------------*                                         12/22/94
00002  IDENTIFICATION DIVISION.                                         INKCS022
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    INKCS022.                                         INKCS022
00005  AUTHOR.        TESS BESTE.                                       INKCS022
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS022
00007  DATE-WRITTEN.  08-05-93.                                         INKCS022
00008  DATE-COMPILED.                                                   INKCS022
00009                                                                   INKCS022
00010 *---------------------------------------------------------------* INKCS022
00011 *    I022 - STOCKROOM WITHDRAWAL LIST PROGRAM                   * INKCS022
00012 *                                                               * INKCS022
00013 *    THIS PROGRAM GENERATES A LIST OF SHEET NUMBERS FOR A       * INKCS022
00014 *    PARTICULAR STORE.  THE TOTAL RETAIL, WHICH IS THE SUM OF   * INKCS022
00015 *    THE EXTENDED AMOUNTS FOR EACH LINE ON THE SHEET, IS ALSO   * INKCS022
00016 *    DISPLAYED.   THE APPROPRIATE PROGRAM IS INVOKED WHEN THE   * INKCS022
00017 *    OPERATOR SELECTS ONE OR MORE ITEMS AND PRESSES A FUNCTION  * INKCS022
00018 *    KEY.                                                         INKCS022
00019 *                                                               * INKCS022
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
W26600* W26600  06-29-2004 ROD JANSSEN INV STORE EXPANSION                      
      *                                                                         
      *---------------------------------------------------------------* INKCS022
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
      *---------------------------------------------------------------* INKCS022
1122RM* CHANGED 11/03/2022    BY BOB MCASEY      WR: CHG0277188                 
1122RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1122RM* THE CICS ARCHITECTURE API.                                              
1122RM*---------------------------------------------------------------*         
                                                                        INKCS022
00022  ENVIRONMENT DIVISION.                                            INKCS022
00024  DATA DIVISION.                                                   INKCS022
00025                                                                   INKCS022
00026  WORKING-STORAGE SECTION.                                         INKCS022
00027                                                                   INKCS022
00028  01  PV-DB2-KEYS.                                                 INKCS022
W26600     15  PV-DB2-STORE-NBR-X             PIC  X(04).               INKCS022
00030      15  PV-DB2-SHEET-NBR               PIC S9(09)  COMP.         INKCS022
00031                                                                   INKCS022
00032  01  PC-PROGRAM-CONSTANTS.                                        INKCS022
00033      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS022
00034                                                     'IN022A  '.   INKCS022
00035      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS022
00036                                                     'INKM022 '.   INKCS022
00037      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS022
00038                                                     'INKCS022'.   INKCS022
00039      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS022
00040      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS022
00041      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS022
00042      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +12     INKCS022
00043                                                     COMP SYNC.    INKCS022
00044      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS022
00045                                                     COMP SYNC.    INKCS022
00046      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS022
00047                                                     +1000000      INKCS022
00048                                                     COMP-3.       INKCS022
00049      05  PC-ITEM-NUMBER-LENGTH          PIC S9(04)  VALUE +5      INKCS022
00050                                                     COMP SYNC.    INKCS022
00051      05  PC-INQ                         PIC  X(03)  VALUE 'INQ'.  INKCS022
00052      05  PC-UPD                         PIC  X(03)  VALUE 'UPD'.  INKCS022
00053      05  PC-DEL                         PIC  X(03)  VALUE 'DEL'.  INKCS022
00054      05  PC-ERR                         PIC  X(03)  VALUE 'ERR'.  INKCS022
00055 *                                                                 INKCS022
00056      05  PC-TSYMSG-NUMBERS.                                       INKCS022
00057          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS022
00058          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS022
00059          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS022
00060          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS022
00061          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS022
00062          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS022
00063          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS022
00064          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS022
00065          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS022
00066          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS022
00067          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS022
00068          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS022
00069          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS022
00070          10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.  INKCS022
00071 *                                                                 INKCS022
00072  01  PS-PROGRAM-SWITCHES.                                         INKCS022
00073      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS022
00074          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS022
00075          88  PS-ERROR                               VALUE 'N'.    INKCS022
00076      05  PS-START-ITEM-ERROR-SW         PIC  X(01)  VALUE 'Y'.    INKCS022
00077          88  PS-NO-START-ITEM-ERROR                 VALUE 'Y'.    INKCS022
00078          88  PS-START-ITEM-ERROR                    VALUE 'N'.    INKCS022
00079      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS022
00080          88  PS-DONE                                VALUE 'Y'.    INKCS022
00081          88  PS-NOT-DONE                            VALUE 'N'.    INKCS022
00082 *                                                                 INKCS022
00083  01  PV-PROGRAM-VARIABLES.                                        INKCS022
00084      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS022
00085                                                     COMP SYNC.    INKCS022
00086      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS022
00087                                                     COMP SYNC.    INKCS022
00088      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS022
00089                                                     COMP-3.       INKCS022
00090      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS022
00091                                                     COMP-3.       INKCS022
00092      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS022
00093                                                     COMP-3.       INKCS022
00094      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS022
00095                                                     COMP-3.       INKCS022
00096      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS022
00097                                                     COMP-3.       INKCS022
00098      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS022
00099                                                     COMP-3.       INKCS022
00100      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS022
00101                                                     COMP-3.       INKCS022
00102      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS022
00103                                                     COMP SYNC.    INKCS022
00104      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS022
00105                                                     COMP SYNC.    INKCS022
00106      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS022
00107                                                     COMP SYNC.    INKCS022
00108      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS022
00109                                                     COMP SYNC.    INKCS022
00110      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS022
00111                                                     COMP SYNC.    INKCS022
00112      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS022
00113                                                     COMP SYNC.    INKCS022
00114      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS022
00115                                                     COMP SYNC.    INKCS022
00116      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS022
00117                                                     COMP SYNC.    INKCS022
00118      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS022
00119                                                     COMP SYNC.    INKCS022
00120      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS022
00121                                                     COMP SYNC.    INKCS022
00122      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS022
00123                                                     COMP SYNC.    INKCS022
00124      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS022
00125                                                     COMP SYNC.    INKCS022
00126      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS022
00127                                                     COMP SYNC.    INKCS022
00128      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS022
00129                                                     COMP SYNC.    INKCS022
00130      05  PV-SUM-ITM-EXTD-AMT            PIC S9(09)V99             INKCS022
00131                                                     COMP-3.       INKCS022
00132 ******************************************************************INKCS022
00133 **  THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.   **INKCS022
00134 **  THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE           **INKCS022
00135 **  APPLICATION-SPECIFIC COMMAREA.                              **INKCS022
00136 ******************************************************************INKCS022
00137  01  LQW-LIST-QUEUE-WORK-AREA.                                    INKCS022
00138      05  LQW-BLOCK-MODIFIED             PIC  X(01)  VALUE SPACE.  INKCS022
00139      05  LQW-ITEM-ENTRIES               OCCURS 12 TIMES           INKCS022
00140                                         INDEXED BY LQW-IDX.       INKCS022
00141          10  LQW-LIST-ITEM-NUMBER       PIC S9(09)  COMP-3.       INKCS022
00142          10  LQW-TEMP-SELECTED-SW       PIC  X(01).               INKCS022
00143          10  LQW-SELECTED-SW            PIC  X(01).               INKCS022
00144              88  LQW-SELECT                         VALUE 'S'.    INKCS022
00145              88  LQW-DESELECT                       VALUE ' '.    INKCS022
00146          10  LQW-ACTION                 PIC  X(03).               INKCS022
00147          10  LQW-SHEET-NBR              PIC  9(06).               INKCS022
00148          10  LQW-SUM-ITM-EXTD-AMT       PIC S9(09)V99             INKCS022
00149                                                     COMP-3.       INKCS022
00150                                                                   INKCS022
00151      EJECT                                                        INKCS022
00152 *---------------------------------------------------------------* INKCS022
00153 *  RECORD LAYOUT FOR THE STOCKROOM WITHDRAWAL SELECTION         * INKCS022
00154 *  TEMP STORAGE QUEUE.                                          * INKCS022
00155 *---------------------------------------------------------------* INKCS022
00156                                                                   INKCS022
00157      COPY INWS022.                                                INKCS022
00158                                                                   INKCS022
00159      EJECT                                                        INKCS022
00160 *---------------------------------------------------------------* INKCS022
00161 *    MAP LAYOUT                                                 * INKCS022
00162 *---------------------------------------------------------------* INKCS022
00163                                                                   INKCS022
W26600     COPY INKM022.                                                INKCS022
00165 *                                                                 INKCS022
00166  01  MR-OCCURS-LAYOUT                   REDEFINES  IN022AO.       INKCS022
W26600     05  FILLER                         PIC X(81).                INKCS022
00168      05  MR-SCROLL-AREA                 OCCURS 12 TIMES.          INKCS022
00169          10  MR-SELECTL                 PIC S9(04) COMP.          INKCS022
00170          10  MR-SELECTA                 PIC  X(01).               INKCS022
00171          10  MR-SELECTC                 PIC  X(01).               INKCS022
00172          10  MR-SELECTH                 PIC  X(01).               INKCS022
00173          10  MR-SELECTI                 PIC  X(01).               INKCS022
00174                                                                   INKCS022
00175          10  MR-ACTIONL                 PIC S9(04) COMP.          INKCS022
00176          10  MR-ACTIONA                 PIC  X(01).               INKCS022
00177          10  MR-ACTIONC                 PIC  X(01).               INKCS022
00178          10  MR-ACTIONH                 PIC  X(01).               INKCS022
00179          10  MR-ACTION                  PIC  X(03).               INKCS022
00180                                                                   INKCS022
00181          10  MR-SHEET-NBRL              PIC S9(04) COMP.          INKCS022
00182          10  MR-SHEET-NBRA              PIC  X(01).               INKCS022
00183          10  MR-SHEET-NBRC              PIC  X(01).               INKCS022
00184          10  MR-SHEET-NBRH              PIC  X(01).               INKCS022
00185          10  MR-SHEET-NBR-X.                                      INKCS022
00186              15  MR-SHEET-NBR           PIC 9(06).                INKCS022
00187                                                                   INKCS022
00188          10  MR-SUM-ITM-EXTD-AMTL       PIC S9(04) COMP.          INKCS022
00189          10  MR-SUM-ITM-EXTD-AMTA       PIC  X(01).               INKCS022
00190          10  MR-SUM-ITM-EXTD-AMTC       PIC  X(01).               INKCS022
00191          10  MR-SUM-ITM-EXTD-AMTH       PIC  X(01).               INKCS022
00192          10  MR-SUM-ITM-EXTD-AMT-X.                               INKCS022
00193              15  MR-SUM-ITM-EXTD-AMT    PIC Z(09).99.             INKCS022
00194                                                                   INKCS022
00195      EJECT                                                        INKCS022
00196 *---------------------------------------------------------------* INKCS022
00197 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS022
00198 *---------------------------------------------------------------* INKCS022
00199                                                                   INKCS022
00200      COPY DPWS015.                                                INKCS022
00201                                                                   INKCS022
00202 *---------------------------------------------------------------* INKCS022
00203 *    FUNCTION KEYS COPYBOOK.                                    * INKCS022
00204 *---------------------------------------------------------------* INKCS022
00205                                                                   INKCS022
00206      COPY DPWS016.                                                INKCS022
00207                                                                   INKCS022
00208 *---------------------------------------------------------------* INKCS022
00209 *    NUMERIC EDIT LAYOUT.                                       * INKCS022
00210 *---------------------------------------------------------------* INKCS022
00211                                                                   INKCS022
00212      COPY DPWS010I.                                               INKCS022
00213  EJECT                                                            INKCS022
00214 *---------------------------------------------------------------* INKCS022
00215 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS022
00216 *---------------------------------------------------------------* INKCS022
00217                                                                   INKCS022
00218      COPY DPWS030.                                                INKCS022
1122RM     COPY DPWS930.                                                INKCS022
00219  EJECT                                                            INKCS022
00220 *---------------------------------------------------------------* INKCS022
00221 *    STANDARD COMMAREA.                                         * INKCS022
00222 *---------------------------------------------------------------* INKCS022
00223                                                                   INKCS022
00224      COPY DPWS020.                                                INKCS022
00225      05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                INKCS022
00226                                                                   INKCS022
00227 *---------------------------------------------------------------* INKCS022
00228 *  THIS IS THE INTER-APPLICATION COMMAREA FOR THE PHYSICAL      * INKCS022
00229 *  INVENTORY SCREENS.                                           * INKCS022
00230 *---------------------------------------------------------------* INKCS022
00231                                                                   INKCS022
00232          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS022
W26600     COPY INWS001.                                                INKCS022
00234 *---------------------------------------------------------------* INKCS022
00235 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS022
00236 *---------------------------------------------------------------* INKCS022
00237          10  ASC-SPECIFIC-COMMAREA.                               INKCS022
00238              15  ASC-LIST-KEYS.                                   INKCS022
W26600                 20  ASC-KEY-STORE-NBR-X             PIC X(04).   INKCS022
W26600                 20  ASC-KEY-STORE-NBR REDEFINES                  INKCS022
W26600                     ASC-KEY-STORE-NBR-X             PIC 9(04).   INKCS022
00240                  20  ASC-KEY-SHEET-NBR-X.                         INKCS022
00241                      25  ASC-KEY-SHEET-NBR           PIC 9(06).   INKCS022
00242              15  ASC-STORE-NAME                      PIC X(25).   INKCS022
00243              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS022
00244                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS022
00245                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS022
00246                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS022
00247                                                                   INKCS022
00248                  20  ASC-ITEM-ARRAY.                              INKCS022
00249                      25  ASC-ITEM-ENTRIES OCCURS 12 TIMES.        INKCS022
00250                          30  ASC-LIST-ITEM-NUMBER                 INKCS022
00251                                                      PIC S9(09)   INKCS022
00252                                                      COMP-3.      INKCS022
00253                          30  ASC-TEMP-SELECTED-SW    PIC  X(01).  INKCS022
00254                              88  ASC-VALID-SELECT    VALUE ' '    INKCS022
00255                                                            'S'    INKCS022
00256                                                            'B'    INKCS022
00257                                                            'E'.   INKCS022
00258                              88  ASC-TEMP-NO-SELECT  VALUE ' '.   INKCS022
00259                              88  ASC-TEMP-SELECT     VALUE 'S'.   INKCS022
00260                              88  ASC-TEMP-BEGIN      VALUE 'B'.   INKCS022
00261                              88  ASC-TEMP-END        VALUE 'E'.   INKCS022
00262                          30  ASC-SELECTED-SW         PIC  X(01).  INKCS022
00263                              88  ASC-NO-SELECT       VALUE ' '.   INKCS022
00264                              88  ASC-SELECT          VALUE 'S'.   INKCS022
00265                              88  ASC-BEGIN           VALUE 'B'.   INKCS022
00266                              88  ASC-END             VALUE 'E'.   INKCS022
00267                          30  ASC-ACTION              PIC  X(03).  INKCS022
00268                          30  ASC-SHEET-NBR           PIC  9(06).  INKCS022
00269                          30  ASC-SUM-ITM-EXTD-AMT    PIC S9(09)V99INKCS022
00270                                                           COMP-3. INKCS022
00271                                                                   INKCS022
00272              15  ASC-BLK-SUB                         PIC S9(04)   INKCS022
00273                                                           COMP    INKCS022
00274                                                           SYNC.   INKCS022
00275              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS022
00276                                                           COMP    INKCS022
00277                                                           SYNC.   INKCS022
00278              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS022
00279                                                           COMP-3. INKCS022
00280              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS022
00281                                                           COMP-3. INKCS022
00282              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS022
00283                                                           COMP-3. INKCS022
00284              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS022
00285                                                           COMP-3. INKCS022
00286              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS022
00287                                                           COMP-3. INKCS022
00288              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS022
00289                                                           COMP    INKCS022
00290                                                           SYNC.   INKCS022
00291              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS022
00292                                                           COMP-3. INKCS022
00293              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS022
00294                                                           COMP-3. INKCS022
00295              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS022
00296                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS022
00297                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS022
00298              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS022
00299                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS022
00300                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS022
00301              15  ASC-LIST-QUEUE-NAME.                             INKCS022
00302                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS022
00303                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS022
00304                                                           COMP-3. INKCS022
00305                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS022
00306              15  ASC-SEL-QUEUE-NAME.                              INKCS022
00307                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS022
00308                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS022
00309                                                           COMP-3. INKCS022
00310                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS022
00311                                                                   INKCS022
00312              15  ASC-CURSOR-SW                       PIC X(01).   INKCS022
00313                  88  ASC-STORE-NBR-CSR               VALUE 'S'.   INKCS022
00314                  88  ASC-SHEET-NBR-CSR               VALUE 'T'.   INKCS022
00315              15  ASC-START-ITEM-REQUEST.                          INKCS022
00316                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS022
00317      EJECT                                                        INKCS022
00318 *---------------------------------------------------------------* INKCS022
00319 *    ABEND PROCESSING COPYBOOK.                                 * INKCS022
00320 *---------------------------------------------------------------* INKCS022
00321                                                                   INKCS022
00322      COPY DPWS013.                                                INKCS022
00323                                                                   INKCS022
00324  EJECT                                                            INKCS022
00325 *---------------------------------------------------------------* INKCS022
00326 *    DB2 AREA FOR STOCKROOM WITHDRAWAL TABLE                    * INKCS022
00327 *---------------------------------------------------------------* INKCS022
00328      EXEC SQL                                                     INKCS022
00329           INCLUDE TSTKRMW                                         INKCS022
00330      END-EXEC.                                                    INKCS022
00331 *---------------------------------------------------------------* INKCS022
00332 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS022
00333 *---------------------------------------------------------------* INKCS022
00334      EXEC SQL                                                     INKCS022
00335           INCLUDE SQLCA                                           INKCS022
00336      END-EXEC.                                                    INKCS022
00337                                                                   INKCS022
00338 *---------------------------------------------------------------* INKCS022
00339 *    STOCKROOM WITHDRAWAL CURSOR WHEN ALL SHEETS FOR A STORE    * INKCS022
00340 *    ARE SELECTED.                                              * INKCS022
00341 *---------------------------------------------------------------* INKCS022
00342                                                                   INKCS022
00343      EXEC SQL                                                     INKCS022
00344          DECLARE  STORE-NBR-CSR CURSOR FOR                        INKCS022
00345          SELECT   SHEET_NBR                                       INKCS022
00346                 , SUM(ITM_EXTD_AMT)                               INKCS022
00347          FROM     TSTKRMW                                         INKCS022
W26600         WHERE    LOC_NBR = :STKRMW-LOC-NBR                       INKCS022
00349          GROUP BY SHEET_NBR                                       INKCS022
00350          ORDER BY SHEET_NBR                                       INKCS022
00351      END-EXEC.                                                    INKCS022
00352                                                                   INKCS022
00353 *---------------------------------------------------------------* INKCS022
00354 *    STOCKROOM WITHDRAWAL CURSOR WHEN A SPECIFIC SHEET FOR A    * INKCS022
00355 *    STORE IS SELECTED.                                         * INKCS022
00356 *---------------------------------------------------------------* INKCS022
00357                                                                   INKCS022
00358      EXEC SQL                                                     INKCS022
00359          DECLARE  SHEET-NBR-CSR CURSOR FOR                        INKCS022
00360          SELECT   SHEET_NBR                                       INKCS022
00361                 , SUM(ITM_EXTD_AMT)                               INKCS022
00362          FROM     TSTKRMW                                         INKCS022
W26600         WHERE    LOC_NBR = :STKRMW-LOC-NBR     AND               INKCS022
00364                   SHEET_NBR = :PV-DB2-SHEET-NBR                   INKCS022
00365          GROUP BY SHEET_NBR                                       INKCS022
00366      END-EXEC.                                                    INKCS022
00367                                                                   INKCS022
00368  EJECT                                                            INKCS022
00369  LINKAGE SECTION.                                                 INKCS022
00370                                                                   INKCS022
00371  01  DFHCOMMAREA.                                                 INKCS022
00372      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS022
00373                                         DEPENDING ON EIBCALEN.    INKCS022
00374          10  FILLER                     PIC X.                    INKCS022
00375                                                                   INKCS022
00376  EJECT                                                            INKCS022
00377  PROCEDURE DIVISION.                                              INKCS022
00378 ******************************************************************INKCS022
00379 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS022
00380 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS022
00381 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS022
00382 ******************************************************************INKCS022
00383  0000-MAIN-MODULE.                                                INKCS022
00384      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS022
00385      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS022
00386      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS022
00387      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS022
00388      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS022
00389      MOVE LENGTH OF IN022AI        TO DP030-MAP-LENGTH (1).       INKCS022
00390      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS022
00391      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS022
00392 *                                                                 INKCS022
00393      PERFORM 1000-CONTROL-PROCESSING                              INKCS022
00394 *                                                                 INKCS022
00395      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS022
00396      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS022
00397                                                                   INKCS022
00398 ******************************************************************INKCS022
00399 **  THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE   **INKCS022
00400 **  AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.         **INKCS022
00401 ******************************************************************INKCS022
00402  0001-CALL-CICS-ARCH-API.                                         INKCS022
1122RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS022
1122RM     CALL DP930-CICS-ARCH-API                                             
1122RM                     USING DFHEIBLK                                       
00404                            DFHCOMMAREA                            INKCS022
00405                            DP030-CICS-API-FIELDS                  INKCS022
00406                            DP020-STANDARD-COMMAREA                INKCS022
00407                            IN022AI.                               INKCS022
00408 *                                                                 INKCS022
00409      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS022
00410          CONTINUE                                                 INKCS022
00411      ELSE                                                         INKCS022
00412          SET DP013-NO-ROLLBACK                                    INKCS022
00413              DP013-XCTL-DISPLAY-RESTART                           INKCS022
00414              DP013-CICS-ABEND      TO TRUE                        INKCS022
00415          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS022
00416                                    TO DP013-PARAGRAPH             INKCS022
1122RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS022
1122RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS022
1122RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1122RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00419          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS022
00420          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
00421      END-IF.                                                      INKCS022
00422 *----------------------------------------------------------------*INKCS022
00423 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS022
00424 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS022
00425 *                                                                *INKCS022
00426 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS022
00427 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS022
00428 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS022
00429 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS022
00430 *----------------------------------------------------------------*INKCS022
00431                                                                   INKCS022
00432  1000-CONTROL-PROCESSING.                                         INKCS022
00433                                                                   INKCS022
00434      EVALUATE TRUE                                                INKCS022
00435          WHEN DP020-NEXT-ACT-INITIAL                              INKCS022
00436              INITIALIZE ASC-SPECIFIC-COMMAREA                     INKCS022
00437              MOVE EIBTRMID         TO ASC-LIST-TERM-ID            INKCS022
00438                                       ASC-SEL-TERM-ID             INKCS022
00439              MOVE DP020-SRC-TASK-NUMBER                           INKCS022
00440                                    TO ASC-LIST-TASK-NUMBER        INKCS022
00441                                       ASC-SEL-TASK-NUMBER         INKCS022
00442              MOVE PC-LIST-QUEUE-SEQ                               INKCS022
00443                                    TO ASC-LIST-SEQ                INKCS022
00444              MOVE PC-SEL-QUEUE-SEQ TO ASC-SEL-SEQ                 INKCS022
00445              PERFORM 1100-PROCESS-INTER-APPL-COMM                 INKCS022
00446              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS022
00447                                                                   INKCS022
00448          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS022
00449              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        INKCS022
00450                                    TO PV-TOP-ITEM                 INKCS022
00451              PERFORM 2000-PROCESS-PANEL                           INKCS022
00452                                                                   INKCS022
00453          WHEN DP020-NEXT-ACT-RETURN                               INKCS022
00454              MOVE ASC-ITEM-AT-TOP-OF-PANEL                        INKCS022
00455                                    TO PV-TOP-ITEM                 INKCS022
00456              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS022
00457              PERFORM 6000-REFRESH-PANEL-FROM-TEMP                 INKCS022
00458              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS022
00459              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00460                                                                   INKCS022
00461          WHEN OTHER                                               INKCS022
00462              SET DP013-LOGIC-ABEND TO TRUE                        INKCS022
00463              SET DP013-NO-ROLLBACK TO TRUE                        INKCS022
00464              MOVE '1000-CONTROL-PROCESSING'                       INKCS022
00465                                    TO DP013-PARAGRAPH             INKCS022
00466              MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'    INKCS022
00467                                    TO DP013-MESSAGE-TEXT(1)       INKCS022
00468              PERFORM DP013-0000-PROCESS-ABEND                     INKCS022
00469      END-EVALUATE.                                                INKCS022
00470                                                                   INKCS022
00471  EJECT                                                            INKCS022
00472 *----------------------------------------------------------------*INKCS022
00473 * DETERMINE IF DATA WAS PASSED THROUGH INTER-APPL COMMAREA OR    *INKCS022
00474 * IF THERE IS DATA TO BE USED IN THE NAV-KEY FIELD.              *INKCS022
00475 *----------------------------------------------------------------*INKCS022
00476                                                                   INKCS022
00477  1100-PROCESS-INTER-APPL-COMM.                                    INKCS022
00478                                                                   INKCS022
00479      MOVE SPACE                    TO ASC-START-ITEM-REQUEST.     INKCS022
00480      IF  DP020-INT-APPL-FORMAT-IND = PC-CRIT-SPEC-FORMAT          INKCS022
00481          MOVE IN001-STORE-NBR-X    TO ASC-KEY-STORE-NBR-X         INKCS022
00482          MOVE IN001-SHEET-NBR-X    TO ASC-KEY-SHEET-NBR-X         INKCS022
00483          MOVE IN001-STORE-NAME     TO ASC-STORE-NAME              INKCS022
00484      END-IF.                                                      INKCS022
00485                                                                   INKCS022
00486  EJECT                                                            INKCS022
00487 *----------------------------------------------------------------*INKCS022
00488 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS022
00489 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS022
00490 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS022
00491 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS022
00492 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS022
00493 *----------------------------------------------------------------*INKCS022
00494                                                                   INKCS022
00495  2000-PROCESS-PANEL.                                              INKCS022
00496      EVALUATE TRUE                                                INKCS022
00497          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS022
00498              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS022
00499              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS022
00500              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00501                                                                   INKCS022
00502          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS022
00503              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS022
00504              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS022
00505                                                                   INKCS022
00506          WHEN DP020-FK-DESELECT(DP020-SRC-AID)                    INKCS022
00507              PERFORM 2140-DESELECT-ALL-ITEMS                      INKCS022
00508              PERFORM 3200-RESET-ATTRIBUTES                        INKCS022
00509              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00510                                                                   INKCS022
00511          WHEN DP020-FK-SELECT-ALL(DP020-SRC-AID)                  INKCS022
00512              PERFORM 2150-SELECT-ALL-ITEMS                        INKCS022
00513              PERFORM 3200-RESET-ATTRIBUTES                        INKCS022
00514              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00515                                                                   INKCS022
00516          WHEN OTHER                                               INKCS022
00517              PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                 INKCS022
00518              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS022
00519              IF  PS-NO-ERROR                                      INKCS022
00520                  SET DP030-CONTINUE-GO TO TRUE                    INKCS022
00521                  PERFORM 2100-CHECK-FUNCTION-KEY                  INKCS022
00522              ELSE                                                 INKCS022
00523                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             INKCS022
00524                  SET DP030-OVERRIDE-GO TO TRUE                    INKCS022
00525              END-IF                                               INKCS022
00526      END-EVALUATE.                                                INKCS022
00527  EJECT                                                            INKCS022
00528 *----------------------------------------------------------------*INKCS022
00529 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS022
00530 *----------------------------------------------------------------*INKCS022
00531  2050-DELETE-LIST-QUEUE.                                          INKCS022
00532                                                                   INKCS022
00533      EXEC CICS                                                    INKCS022
00534          DELETEQ TS                                               INKCS022
00535              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
00536              RESP  (PV-CICS-RESP)                                 INKCS022
00537      END-EXEC.                                                    INKCS022
00538                                                                   INKCS022
00539      IF (PV-CICS-RESP = DFHRESP(NORMAL)) OR                       INKCS022
00540         (PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS022
00541          CONTINUE                                                 INKCS022
00542      ELSE                                                         INKCS022
00543          SET DP013-CICS-ABEND      TO TRUE                        INKCS022
00544          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS022
00545                                                                   INKCS022
00546          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS022
00547                                    TO DP013-PARAGRAPH             INKCS022
00548          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS022
00549                                    TO DP013-MESSAGE-TEXT (1)      INKCS022
00550          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
00551      END-IF.                                                      INKCS022
00552                                                                   INKCS022
00553      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS022
00554  EJECT                                                            INKCS022
00555 *----------------------------------------------------------------*INKCS022
00556 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS022
00557 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS022
00558 * CICS ARCHITECTURE API.                                         *INKCS022
00559 *----------------------------------------------------------------*INKCS022
00560  2100-CHECK-FUNCTION-KEY.                                         INKCS022
00561      EVALUATE TRUE                                                INKCS022
00562          WHEN DP020-FK-LOCAL-GOTO(DP020-SRC-AID)                  INKCS022
00563              IF  ASC-NUMBER-OF-SELECTED-ITEMS > ZERO              INKCS022
00564                  PERFORM 2105-PREPARE-INTER-APPL-COMM             INKCS022
00565                  INITIALIZE IN001-NUMBER-OF-TS-ITEMS              INKCS022
00566                             IN001-SEL-QUEUE-NAME                  INKCS022
00567                  MOVE ASC-NUMBER-OF-SELECTED-ITEMS                INKCS022
00568                                 TO IN001-NUMBER-OF-SELECTED-ITEMS INKCS022
00569                  PERFORM 5000-PREPARE-SEL-QUEUE                   INKCS022
00570              ELSE                                                 INKCS022
00571                  SET DP030-OVERRIDE-GO                            INKCS022
00572                                    TO TRUE                        INKCS022
00573 *                ---- A LIST ITEM MUST BE SELECTED ----           INKCS022
00574                  MOVE PC-TSYMSG-00057                             INKCS022
00575                                    TO DP020-MSG-NUMBER            INKCS022
00576                  SET DP020-MSG-WARNING                            INKCS022
00577                                    TO TRUE                        INKCS022
00578                  PERFORM 4400-MOVE-COMMAREA-TO-SCREEN             INKCS022
00579              END-IF                                               INKCS022
00580                                                                   INKCS022
00581          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS022
00582              MOVE +1               TO PV-TOP-ITEM                 INKCS022
00583              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00584                                                                   INKCS022
00585          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS022
00586              PERFORM 2110-BROWSE-BACKWARD                         INKCS022
00587              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00588                                                                   INKCS022
00589          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS022
00590              PERFORM 2120-BROWSE-FORWARD                          INKCS022
00591              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00592                                                                   INKCS022
00593          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS022
00594              PERFORM 2130-CHECK-FOR-JUMP-BROWSE                   INKCS022
00595              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
00596                                                                   INKCS022
00597          WHEN OTHER                                               INKCS022
00598              SET DP013-NO-ROLLBACK                                INKCS022
00599                  DP013-XCTL-DISPLAY-RESTART                       INKCS022
00600                  DP013-CICS-ABEND  TO TRUE                        INKCS022
00601              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS022
00602                                    TO DP013-PARAGRAPH             INKCS022
00603              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS022
00604                                    TO DP013-MESSAGE-TEXT (1)      INKCS022
00605              PERFORM DP013-0000-PROCESS-ABEND                     INKCS022
00606      END-EVALUATE.                                                INKCS022
00607                                                                   INKCS022
00608                                                                   INKCS022
00609  2105-PREPARE-INTER-APPL-COMM.                                    INKCS022
00610      MOVE ASC-KEY-STORE-NBR-X TO IN001-STORE-NBR-X.               INKCS022
00611      MOVE ASC-KEY-SHEET-NBR-X TO IN001-SHEET-NBR-X.               INKCS022
00612      MOVE ASC-STORE-NAME      TO IN001-STORE-NAME.                INKCS022
00613                                                                   INKCS022
00614                                                                   INKCS022
00615 *----------------------------------------------------------------*INKCS022
00616 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS022
00617 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS022
00618 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS022
00619 *    MESSAGE.                                                    *INKCS022
00620 *----------------------------------------------------------------*INKCS022
00621                                                                   INKCS022
00622  2110-BROWSE-BACKWARD.                                            INKCS022
00623      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS022
00624          GIVING PV-TOP-ITEM.                                      INKCS022
00625 *                                                                 INKCS022
00626      IF  ((PV-TOP-ITEM < +1)                                      INKCS022
00627        OR (PV-TOP-ITEM = +1))                                     INKCS022
00628          MOVE +1                   TO PV-TOP-ITEM                 INKCS022
00629 *        -- TOP OF LIST --                                        INKCS022
00630          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS022
00631          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS022
00632      END-IF.                                                      INKCS022
00633                                                                   INKCS022
00634  EJECT                                                            INKCS022
00635 *----------------------------------------------------------------*INKCS022
00636 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS022
00637 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS022
00638 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS022
00639 *----------------------------------------------------------------*INKCS022
00640                                                                   INKCS022
00641  2120-BROWSE-FORWARD.                                             INKCS022
00642                                                                   INKCS022
00643      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS022
00644          GIVING PV-TOP-ITEM.                                      INKCS022
00645      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS022
00646          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS022
00647                                    TO PV-TOP-ITEM                 INKCS022
00648 *        -- BOTTOM OF LIST --                                     INKCS022
00649          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS022
00650          SET DP020-MSG-INFORMATIONAL                              INKCS022
00651                                    TO TRUE                        INKCS022
00652      ELSE                                                         INKCS022
00653          COMPUTE PV-BOTTOM-ITEM =                                 INKCS022
00654              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS022
00655          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS022
00656 *            -- BOTTOM OF LIST --                                 INKCS022
00657              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS022
00658              SET DP020-MSG-INFORMATIONAL                          INKCS022
00659                                    TO TRUE                        INKCS022
00660          END-IF                                                   INKCS022
00661      END-IF.                                                      INKCS022
00662  EJECT                                                            INKCS022
00663 *----------------------------------------------------------------*INKCS022
00664 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS022
00665 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS022
00666 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS022
00667 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS022
00668 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS022
00669 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS022
00670 *----------------------------------------------------------------*INKCS022
00671                                                                   INKCS022
00672  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS022
00673      IF (ASC-START-ITEM-REQUEST = SPACE)                          INKCS022
00674          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS022
00675                                    TO PV-TOP-ITEM                 INKCS022
00676      ELSE                                                         INKCS022
00677          MOVE ASC-START-ITEM-REQUEST  TO DP010I-UNEDITED-FIELD    INKCS022
00678          MOVE PC-ITEM-NUMBER-LENGTH   TO DP010I-MAXIMUM-DIGITS    INKCS022
00679          MOVE +0                      TO DP010I-MAXIMUM-DECIMALS  INKCS022
00680          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS022
00681                                    TO TRUE                        INKCS022
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
00683                                                                   INKCS022
00684          IF  NOT DP010I-ERROR-DETECTED                            INKCS022
00685              MOVE DP010I-NUMERIC-FIELD                            INKCS022
00686                                    TO  ASC-START-ITEM-REQUEST-N   INKCS022
00687              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS022
00688                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS022
00689                                           ASC-NUMBER-OF-ITEMS-READINKCS022
00690                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS022
00691                      COMPUTE PV-TOP-ITEM =                        INKCS022
00692                              ASC-NUMBER-OF-ITEMS-READ             INKCS022
00693                              - PC-ITEMS-PER-PANEL + 1             INKCS022
00694                      IF  PV-TOP-ITEM < +1                         INKCS022
00695                          MOVE +1   TO PV-TOP-ITEM                 INKCS022
00696 *                        ---- BOTTOM OF LIST ----                 INKCS022
00697                          MOVE PC-TSYMSG-00070                     INKCS022
00698                                    TO DP020-MSG-NUMBER            INKCS022
00699                          SET DP020-MSG-INFORMATIONAL              INKCS022
00700                                    TO TRUE                        INKCS022
00701                      END-IF                                       INKCS022
00702                  ELSE                                             INKCS022
00703                      MOVE ASC-START-ITEM-REQUEST-N                INKCS022
00704                                    TO PV-TOP-ITEM                 INKCS022
00705                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS022
00706                      COMPUTE PV-BOTTOM-ITEM =                     INKCS022
00707                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS022
00708                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS022
00709 *                        -- BOTTOM OF LIST --                     INKCS022
00710                          MOVE PC-TSYMSG-00070                     INKCS022
00711                                    TO DP020-MSG-NUMBER            INKCS022
00712                          SET DP020-MSG-INFORMATIONAL              INKCS022
00713                                    TO TRUE                        INKCS022
00714                      END-IF                                       INKCS022
00715                  END-IF                                           INKCS022
00716              ELSE                                                 INKCS022
00717 *                --- VALUE OUT OF RANGE ----                      INKCS022
00718                  MOVE PC-TSYMSG-00101     TO DP020-MSG-NUMBER     INKCS022
00719                  MOVE -1                  TO ASTRTITL             INKCS022
00720                  SET DP030-SET-CURSOR-APPL-1                      INKCS022
00721                                           TO TRUE                 INKCS022
00722                  MOVE DP015-REVERSE       TO ASTRTITH             INKCS022
00723                  MOVE DP015-RED           TO ASTRTITC             INKCS022
00724                  MOVE DP015-UNP-NUM-BRT-OFF                       INKCS022
00725                                           TO ASTRTITA             INKCS022
00726                  SET DP020-MSG-FATAL      TO TRUE                 INKCS022
00727                  SET PS-START-ITEM-ERROR TO TRUE                  INKCS022
00728              END-IF                                               INKCS022
00729                                                                   INKCS022
00730          ELSE                                                     INKCS022
00731 *            ---- MUST BE NUMERIC ----                            INKCS022
00732              MOVE PC-TSYMSG-00008        TO DP020-MSG-NUMBER      INKCS022
00733              MOVE -1                     TO ASTRTITL              INKCS022
00734              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  INKCS022
00735              MOVE DP015-REVERSE          TO ASTRTITH              INKCS022
00736              MOVE DP015-RED              TO ASTRTITC              INKCS022
00737              MOVE DP015-UNP-NUM-BRT-OFF  TO ASTRTITA              INKCS022
00738              SET DP020-MSG-FATAL         TO TRUE                  INKCS022
00739              SET PS-START-ITEM-ERROR     TO TRUE                  INKCS022
00740          END-IF                                                   INKCS022
00741      END-IF.                                                      INKCS022
00742  EJECT                                                            INKCS022
00743 *----------------------------------------------------------------*INKCS022
00744 *    IF THE USER HITS THE DESELECT KEY, CLEAR THE SELECTION      *INKCS022
00745 *    SWITCH FOR ALL ITEMS THAT WERE PREVIOUSLY SELECTED.         *INKCS022
00746 *----------------------------------------------------------------*INKCS022
00747  2140-DESELECT-ALL-ITEMS.                                         INKCS022
00748      MOVE +1                       TO ASC-BLK-SUB.                INKCS022
00749                                                                   INKCS022
00750      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS022
00751          VARYING ASC-ITEM-SUB                                     INKCS022
00752          FROM 1 BY 1                                              INKCS022
00753          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
00754                                                                   INKCS022
00755      ADD +1                        TO ASC-BLK-SUB.                INKCS022
00756                                                                   INKCS022
00757      PERFORM 2141-CLEAR-SELECTED-INDS                             INKCS022
00758          VARYING ASC-ITEM-SUB                                     INKCS022
00759          FROM 1 BY 1                                              INKCS022
00760          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
00761                                                                   INKCS022
00762      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS022
00763                                    TO PV-LIST-ITEM-NUMBER.        INKCS022
00764                                                                   INKCS022
00765      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS022
00766          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS022
00767                                                                   INKCS022
00768      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS022
00769         (ASC-HIGHEST-BLOCK-WRITTEN > PC-MAX-BLOCKS-IN-ARRAY)      INKCS022
00770          MOVE +1                   TO ASC-BLK-SUB                 INKCS022
00771                                                                   INKCS022
00772          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS022
00773                                                                   INKCS022
00774          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS022
00775                                                                   INKCS022
00776          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS022
00777              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS022
00778                                                                   INKCS022
00779          PERFORM                                                  INKCS022
00780              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS022
00781                                                                   INKCS022
00782              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
00783                                                                   INKCS022
00784              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS022
00785                  VARYING ASC-ITEM-SUB FROM 1 BY 1                 INKCS022
00786                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
00787                                                                   INKCS022
00788              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
00789                                                                   INKCS022
00790              ADD +1                TO PV-BLOCK-NUMBER             INKCS022
00791                                                                   INKCS022
00792          END-PERFORM                                              INKCS022
00793                                                                   INKCS022
00794          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS022
00795              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS022
00796                                                                   INKCS022
00797          PERFORM                                                  INKCS022
00798              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS022
00799                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS022
00800                                                                   INKCS022
00801              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS022
00802                                    TO PV-BLOCK-NUMBER             INKCS022
00803                                                                   INKCS022
00804              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
00805                                                                   INKCS022
00806              PERFORM 2141-CLEAR-SELECTED-INDS                     INKCS022
00807                  VARYING ASC-ITEM-SUB                             INKCS022
00808                  FROM 1 BY 1                                      INKCS022
00809                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
00810                                                                   INKCS022
00811              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
00812                                                                   INKCS022
00813              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS022
00814                                                                   INKCS022
00815          END-PERFORM                                              INKCS022
00816                                                                   INKCS022
00817          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS022
00818                                    TO PV-BLOCK-NUMBER             INKCS022
00819                                                                   INKCS022
00820          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
00821      END-IF.                                                      INKCS022
00822                                                                   INKCS022
00823                                                                   INKCS022
00824      MOVE +0                     TO ASC-NUMBER-OF-SELECTED-ITEMS. INKCS022
00825      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE.INKCS022
00826      MOVE +0                       TO ASC-END-OF-SELECTED-RANGE.  INKCS022
00827                                                                   INKCS022
00828      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS022
00829                                                                   INKCS022
00830  EJECT                                                            INKCS022
00831 *----------------------------------------------------------------*INKCS022
00832 *    MARK EACH ITEM AS BEING NOT SELECTED.                       *INKCS022
00833 *----------------------------------------------------------------*INKCS022
00834                                                                   INKCS022
00835  2141-CLEAR-SELECTED-INDS.                                        INKCS022
00836                                                                   INKCS022
00837      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS022
00838              NOT EQUAL ZERO                                       INKCS022
00839          IF  NOT ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS022
00840              SET ASC-NO-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS022
00841                  ASC-TEMP-NO-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS022
00842                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS022
00843                                    TO TRUE                        INKCS022
00844          END-IF                                                   INKCS022
00845      END-IF.                                                      INKCS022
00846                                                                   INKCS022
00847  EJECT                                                            INKCS022
00848 *----------------------------------------------------------------*INKCS022
00849 *  IF THE USER HITS THE SELECT-ALL KEY, SET THE SELECTIO SWITCH  *INKCS022
00850 *  FOR ALL ITEMS.                                                *INKCS022
00851 *----------------------------------------------------------------*INKCS022
00852                                                                   INKCS022
00853  2150-SELECT-ALL-ITEMS.                                           INKCS022
00854                                                                   INKCS022
00855      MOVE +1                       TO ASC-BLK-SUB.                INKCS022
00856                                                                   INKCS022
00857      PERFORM 2151-SET-SELECTED-INDS                               INKCS022
00858          VARYING ASC-ITEM-SUB                                     INKCS022
00859          FROM 1 BY 1                                              INKCS022
00860          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
00861                                                                   INKCS022
00862      ADD +1                        TO ASC-BLK-SUB.                INKCS022
00863                                                                   INKCS022
00864      PERFORM 2151-SET-SELECTED-INDS                               INKCS022
00865          VARYING ASC-ITEM-SUB                                     INKCS022
00866          FROM 1 BY 1                                              INKCS022
00867          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
00868                                                                   INKCS022
00869      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS022
00870                                    TO PV-LIST-ITEM-NUMBER.        INKCS022
00871                                                                   INKCS022
00872      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS022
00873          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS022
00874                                                                   INKCS022
00875      IF (PV-FIRST-BLOCK-IN-ARRAY NOT = +1) OR                     INKCS022
00876         (ASC-HIGHEST-BLOCK-WRITTEN >   PC-MAX-BLOCKS-IN-ARRAY)    INKCS022
00877          MOVE +1                   TO ASC-BLK-SUB                 INKCS022
00878                                                                   INKCS022
00879          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS022
00880                                                                   INKCS022
00881          MOVE +1                   TO PV-BLOCK-NUMBER             INKCS022
00882                                                                   INKCS022
00883          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS022
00884              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS022
00885                                                                   INKCS022
00886          PERFORM                                                  INKCS022
00887              UNTIL PV-BLOCK-NUMBER > PV-PREV-BLOCK-IN-ARRAY       INKCS022
00888                                                                   INKCS022
00889              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
00890                                                                   INKCS022
00891              PERFORM 2151-SET-SELECTED-INDS                       INKCS022
00892                  VARYING ASC-ITEM-SUB                             INKCS022
00893                  FROM 1 BY 1                                      INKCS022
00894                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
00895                                                                   INKCS022
00896              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
00897                                                                   INKCS022
00898              ADD +1                TO PV-BLOCK-NUMBER             INKCS022
00899                                                                   INKCS022
00900          END-PERFORM                                              INKCS022
00901                                                                   INKCS022
00902          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS022
00903              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS022
00904                                                                   INKCS022
00905          PERFORM                                                  INKCS022
00906              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS022
00907                    ASC-HIGHEST-BLOCK-WRITTEN                      INKCS022
00908                                                                   INKCS022
00909              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS022
00910                                    TO PV-BLOCK-NUMBER             INKCS022
00911                                                                   INKCS022
00912              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
00913                                                                   INKCS022
00914              PERFORM 2151-SET-SELECTED-INDS                       INKCS022
00915                  VARYING ASC-ITEM-SUB                             INKCS022
00916                  FROM 1 BY 1                                      INKCS022
00917                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
00918                                                                   INKCS022
00919              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
00920                                                                   INKCS022
00921              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS022
00922                                                                   INKCS022
00923          END-PERFORM                                              INKCS022
00924                                                                   INKCS022
00925          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS022
00926                                    TO PV-BLOCK-NUMBER             INKCS022
00927                                                                   INKCS022
00928          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
00929      END-IF.                                                      INKCS022
00930                                                                   INKCS022
00931                                                                   INKCS022
00932      MOVE ASC-NUMBER-OF-ITEMS-READ TO                             INKCS022
00933                        ASC-NUMBER-OF-SELECTED-ITEMS.              INKCS022
00934                                                                   INKCS022
00935      PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                        INKCS022
00936                                                                   INKCS022
00937  EJECT                                                            INKCS022
00938 *----------------------------------------------------------------*INKCS022
00939 *    MARK EACH ITEM AS BEING SELECTED.                           *INKCS022
00940 *----------------------------------------------------------------*INKCS022
00941                                                                   INKCS022
00942  2151-SET-SELECTED-INDS.                                          INKCS022
00943                                                                   INKCS022
00944      IF  ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB ASC-ITEM-SUB)           INKCS022
00945              NOT EQUAL ZERO                                       INKCS022
00946          IF  NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS022
00947              SET ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS022
00948                  ASC-TEMP-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)        INKCS022
00949                  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                  INKCS022
00950                                TO  TRUE                           INKCS022
00951          END-IF                                                   INKCS022
00952      END-IF.                                                      INKCS022
00953                                                                   INKCS022
00954                                                                   INKCS022
00955 ******************************************************************INKCS022
00956 ** MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE    **INKCS022
00957 ** SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM. **INKCS022
00958 ******************************************************************INKCS022
00959  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS022
00960                                                                   INKCS022
00961      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS022
00962      MOVE +1                       TO PV-SUB.                     INKCS022
00963 *                                                                 INKCS022
00964      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS022
00965      IF  ASTRTITL > ZERO                                          INKCS022
00966          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST          INKCS022
00967      ELSE                                                         INKCS022
00968          IF ASTRTITF = DP015-ERASE-EOF                            INKCS022
00969              MOVE SPACE TO ASC-START-ITEM-REQUEST                 INKCS022
00970          END-IF.                                                  INKCS022
00971                                                                   INKCS022
00972      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          INKCS022
00973          VARYING PV-SUB                                           INKCS022
00974          FROM 1 BY 1                                              INKCS022
00975          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      INKCS022
00976 ******************************************************************INKCS022
00977 ** MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA**INKCS022
00978 ******************************************************************INKCS022
00979  2210-MOVE-LINES-TO-COMMAREA.                                     INKCS022
00980      IF  MR-SELECTL (PV-SUB) > ZERO                               INKCS022
00981          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS022
00982          MOVE MR-SELECTI (PV-SUB)                                 INKCS022
00983                                    TO ASC-TEMP-SELECTED-SW        INKCS022
00984                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS022
00985      ELSE                                                         INKCS022
00986          IF  MR-SELECTA (PV-SUB) = DP015-ERASE-EOF                INKCS022
00987                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE     INKCS022
00988                  MOVE SPACE        TO ASC-TEMP-SELECTED-SW        INKCS022
00989                                  (ASC-BLK-SUB, ASC-ITEM-SUB)      INKCS022
00990          END-IF                                                   INKCS022
00991      END-IF.                                                      INKCS022
00992      ADD +1                        TO ASC-ITEM-SUB.               INKCS022
00993      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS022
00994          ADD +1                    TO ASC-BLK-SUB                 INKCS022
00995          MOVE +1                   TO ASC-ITEM-SUB                INKCS022
00996      END-IF.                                                      INKCS022
00997 ******************************************************************INKCS022
00998 ** EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE      **INKCS022
00999 ** COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY). **INKCS022
01000 ******************************************************************INKCS022
01001  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS022
01002      SET PS-NO-ERROR               TO TRUE.                       INKCS022
01003      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS022
01004      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS022
01005 **                                                                INKCS022
01006 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS022
01007 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS022
01008 **                                                                INKCS022
01009      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS022
01010          ASC-ITEMS-DISPLAYED - 1.                                 INKCS022
01011      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS022
01012      PERFORM 3300-EDIT-SELECTIONS                                 INKCS022
01013          VARYING PV-SUB                                           INKCS022
01014          FROM PV-SUB BY -1                                        INKCS022
01015          UNTIL PV-SUB < +1                                        INKCS022
01016 *                                                                 INKCS022
01017      IF  PS-NO-ERROR                                              INKCS022
01018          PERFORM 3400-CHECK-RANGE-SEQUENCE                        INKCS022
01019      END-IF.                                                      INKCS022
01020 *                                                                 INKCS022
01021      IF  ((PS-NO-ERROR)                                           INKCS022
01022        AND (ASC-START-OF-SELECTED-RANGE > ZERO)                   INKCS022
01023        AND (ASC-END-OF-SELECTED-RANGE > ZERO))                    INKCS022
01024              PERFORM 3500-MARK-RANGE-AS-SELECTED                  INKCS022
01025      END-IF.                                                      INKCS022
01026  EJECT                                                            INKCS022
01027 *----------------------------------------------------------------*INKCS022
01028 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS022
01029 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS022
01030 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS022
01031 *                                                                *INKCS022
01032 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS022
01033 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS022
01034 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS022
01035 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS022
01036 *----------------------------------------------------------------*INKCS022
01037                                                                   INKCS022
01038  3100-SET-SUBSCRIPTS.                                             INKCS022
01039                                                                   INKCS022
01040      COMPUTE ASC-ITEM-SUB =                                       INKCS022
01041          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS022
01042                                                                   INKCS022
01043      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS022
01044          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS022
01045                                                                   INKCS022
01046          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS022
01047      ELSE                                                         INKCS022
01048          MOVE +1                     TO ASC-BLK-SUB               INKCS022
01049      END-IF.                                                      INKCS022
01050 ******************************************************************INKCS022
01051 ** RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.     **INKCS022
01052 ******************************************************************INKCS022
01053                                                                   INKCS022
01054  3200-RESET-ATTRIBUTES.                                           INKCS022
01055      MOVE DP015-UNP-NUM-NOR-OFF    TO ASTRTITA.                   INKCS022
01056      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS022
01057      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS022
01058 *                                                                 INKCS022
01059      PERFORM                                                      INKCS022
01060          VARYING PV-SUB                                           INKCS022
01061          FROM 1 BY 1                                              INKCS022
01062          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        INKCS022
01063          MOVE DP015-UNP-ALP-NOR-OFF                               INKCS022
01064                                    TO MR-SELECTA (PV-SUB)         INKCS022
01065          MOVE DP015-GREEN          TO MR-SELECTC (PV-SUB)         INKCS022
01066          MOVE DP015-UNDERLINE      TO MR-SELECTH (PV-SUB)         INKCS022
01067      END-PERFORM.                                                 INKCS022
01068 ******************************************************************INKCS022
01069 ** EDIT THE SELECTIONS MADE.  NOTE THAT THE 'SELECTED-SW' IS    **INKCS022
01070 ** WHAT WAS PREVIOUSLY CHECKED AND CORRECT 'TEMP-SELECTED-SW'   **INKCS022
01071 ** IS WHAT HAS JUST BEEN ENTERED AND HAS NOT BEEN EDITTED OR    **INKCS022
01072 ** WAS ENTERED EARLIER, BUT DID NOT PASS THE EDITS.             **INKCS022
01073 ******************************************************************INKCS022
01074  3300-EDIT-SELECTIONS.                                            INKCS022
01075      EVALUATE TRUE                                                INKCS022
01076          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS022
01077              = ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS022
01078                  CONTINUE                                         INKCS022
01079          WHEN ASC-TEMP-SELECT  (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS022
01080              IF  NOT ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS022
01081                  ADD +1         TO ASC-NUMBER-OF-SELECTED-ITEMS   INKCS022
01082                  MOVE ASC-TEMP-SELECTED-SW                        INKCS022
01083                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS022
01084                                 TO ASC-SELECTED-SW                INKCS022
01085                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS022
01086              END-IF                                               INKCS022
01087          WHEN ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS022
01088                  = SPACE                                          INKCS022
01089              EVALUATE TRUE                                        INKCS022
01090                  WHEN ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS022
01091                      SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMSINKCS022
01092                      MOVE SPACE TO ASC-SELECTED-SW                INKCS022
01093                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS022
01094                  WHEN ASC-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS022
01095                      MOVE ZERO  TO ASC-START-OF-SELECTED-RANGE    INKCS022
01096                      MOVE SPACE TO ASC-SELECTED-SW                INKCS022
01097                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS022
01098                  WHEN ASC-END (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS022
01099                      MOVE ZERO  TO ASC-END-OF-SELECTED-RANGE      INKCS022
01100                      MOVE SPACE TO ASC-SELECTED-SW                INKCS022
01101                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS022
01102              END-EVALUATE                                         INKCS022
01103          WHEN ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS022
01104              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS022
01105                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS022
01106              END-IF                                               INKCS022
01107              PERFORM 3310-EDIT-RANGE                              INKCS022
01108          WHEN ASC-TEMP-END (ASC-BLK-SUB ASC-ITEM-SUB)             INKCS022
01109              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS022
01110                  SUBTRACT +1 FROM ASC-NUMBER-OF-SELECTED-ITEMS    INKCS022
01111              END-IF                                               INKCS022
01112              PERFORM 3310-EDIT-RANGE                              INKCS022
01113          WHEN OTHER                                               INKCS022
01114 *            ---- INVALID VALUE ----                              INKCS022
01115              MOVE PC-TSYMSG-00005   TO DP020-MSG-NUMBER           INKCS022
01116              SET DP020-MSG-FATAL    TO TRUE                       INKCS022
01117              MOVE -1                TO MR-SELECTL (PV-SUB)        INKCS022
01118              SET DP030-SET-CURSOR-APPL-1                          INKCS022
01119                                     TO TRUE                       INKCS022
01120              MOVE DP015-RED         TO MR-SELECTC (PV-SUB)        INKCS022
01121              MOVE DP015-REVERSE     TO MR-SELECTH (PV-SUB)        INKCS022
01122              SET PS-ERROR TO TRUE                                 INKCS022
01123              IF  ASC-SELECT (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS022
01124                  SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS     INKCS022
01125                  MOVE SPACE         TO ASC-SELECTED-SW            INKCS022
01126                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01127              END-IF                                               INKCS022
01128      END-EVALUATE.                                                INKCS022
01129                                                                   INKCS022
01130      SUBTRACT +1             FROM PV-ITEM-IN-USE.                 INKCS022
01131      SUBTRACT +1 FROM ASC-ITEM-SUB.                               INKCS022
01132      IF  ASC-ITEM-SUB < 1                                         INKCS022
01133          SUBTRACT +1 FROM ASC-BLK-SUB                             INKCS022
01134          MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                INKCS022
01135      END-IF.                                                      INKCS022
01136 ******************************************************************INKCS022
01137 ** IF A BEGINNING AND END OF RANGE WAS ENTERED TO SELECT A      **INKCS022
01138 ** RANGE OF LINES, MAKE SURE THERE IS ONLY 1 BEGINNING AND 1    **INKCS022
01139 ** ENDING AND MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN)**INKCS022
01140 ** THE ENDING.                                                  **INKCS022
01141 ******************************************************************INKCS022
01142  3310-EDIT-RANGE.                                                 INKCS022
01143      IF  ASC-TEMP-BEGIN (ASC-BLK-SUB ASC-ITEM-SUB)                INKCS022
01144          IF  ASC-START-OF-SELECTED-RANGE = ZERO                   INKCS022
01145              MOVE ASC-TEMP-SELECTED-SW  (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01146                                    TO ASC-SELECTED-SW             INKCS022
01147                                         (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01148              MOVE PV-ITEM-IN-USE   TO ASC-START-OF-SELECTED-RANGE INKCS022
01149          ELSE                                                     INKCS022
01150              IF  ASC-START-OF-SELECTED-RANGE = PV-ITEM-IN-USE     INKCS022
01151                  CONTINUE                                         INKCS022
01152              ELSE                                                 INKCS022
01153 *                -- ONLY ONE BEGINNING-OF-RANGE-ALLOWED --        INKCS022
01154                  MOVE PC-TSYMSG-00054                             INKCS022
01155                                    TO DP020-MSG-NUMBER            INKCS022
01156                  SET DP020-MSG-FATAL                              INKCS022
01157                                    TO TRUE                        INKCS022
01158                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS022
01159                  SET DP030-SET-CURSOR-APPL-1                      INKCS022
01160                                    TO TRUE                        INKCS022
01161                  MOVE DP015-RED    TO MR-SELECTC (PV-SUB)         INKCS022
01162                  MOVE DP015-UNDERLINE                             INKCS022
01163                                    TO MR-SELECTH (PV-SUB)         INKCS022
01164                  SET PS-ERROR      TO TRUE                        INKCS022
01165              END-IF                                               INKCS022
01166          END-IF                                                   INKCS022
01167      ELSE                                                         INKCS022
01168          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS022
01169              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS022
01170                                    TO ASC-SELECTED-SW             INKCS022
01171                                      (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS022
01172              MOVE PV-ITEM-IN-USE   TO ASC-END-OF-SELECTED-RANGE   INKCS022
01173          ELSE                                                     INKCS022
01174              IF  ASC-END-OF-SELECTED-RANGE = PV-ITEM-IN-USE       INKCS022
01175                  CONTINUE                                         INKCS022
01176              ELSE                                                 INKCS022
01177 *                -- ONLY ONE END-OF-RANGE ALLOWED --              INKCS022
01178                  MOVE PC-TSYMSG-00055                             INKCS022
01179                                    TO DP020-MSG-NUMBER            INKCS022
01180                  SET DP020-MSG-FATAL                              INKCS022
01181                                    TO TRUE                        INKCS022
01182                  MOVE -1           TO MR-SELECTL (PV-SUB)         INKCS022
01183                  SET DP030-SET-CURSOR-APPL-1                      INKCS022
01184                                    TO TRUE                        INKCS022
01185                  SET PS-ERROR      TO TRUE                        INKCS022
01186              END-IF                                               INKCS022
01187          END-IF                                                   INKCS022
01188      END-IF.                                                      INKCS022
01189 *                                                                 INKCS022
01190 ******************************************************************INKCS022
01191 ** IF BOTH A BEGINNING AND END OF RANGE HAVE BEEN SPECIFIED,    **INKCS022
01192 ** MAKE SURE THAT THE BEGINNING IS BEFORE (LESS THAN) THE END.  **INKCS022
01193 ******************************************************************INKCS022
01194  3400-CHECK-RANGE-SEQUENCE.                                       INKCS022
01195      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS022
01196        AND  ASC-END-OF-SELECTED-RANGE > ZERO                      INKCS022
01197        AND  ASC-START-OF-SELECTED-RANGE >                         INKCS022
01198                                          ASC-END-OF-SELECTED-RANGEINKCS022
01199 *        -- END-OF-RANGE BEFORE BEGINNING-OF-RANGE                INKCS022
01200          MOVE PC-TSYMSG-00052 TO DP020-MSG-NUMBER                 INKCS022
01201          SET DP020-MSG-FATAL TO TRUE                              INKCS022
01202          SET PS-ERROR TO TRUE                                     INKCS022
01203          PERFORM 3410-POSITION-AT-ERROR                           INKCS022
01204      ELSE                                                         INKCS022
01205          MOVE -1 TO MR-SELECTL (1)                                INKCS022
01206      END-IF.                                                      INKCS022
01207      SET DP030-SET-CURSOR-APPL-1   TO TRUE.                       INKCS022
01208                                                                   INKCS022
01209                                                                   INKCS022
01210 ******************************************************************INKCS022
01211 ** BECAUSE RANGES CAN SPAN ACROSS MULTIPLE PAGES, A SEPARATE    **INKCS022
01212 ** PARAGRAPH (THIS ONE) IS NEEDED TO DETERMINE WHERE THE CURSOR **INKCS022
01213 ** SHOULD BE POSITIONED ON A RANGE ERROR.                       **INKCS022
01214 ******************************************************************INKCS022
01215  3410-POSITION-AT-ERROR.                                          INKCS022
01216      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS022
01217                     - PV-TOP-ITEM + 1.                            INKCS022
01218      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS022
01219          CONTINUE                                                 INKCS022
01220      ELSE                                                         INKCS022
01221          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS022
01222          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS022
01223          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS022
01224      END-IF.                                                      INKCS022
01225 *                                                                 INKCS022
01226      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS022
01227                     - PV-TOP-ITEM + 1.                            INKCS022
01228      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS022
01229          CONTINUE                                                 INKCS022
01230      ELSE                                                         INKCS022
01231          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS022
01232          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS022
01233          MOVE DP015-RED            TO MR-SELECTC (PV-SUB)         INKCS022
01234      END-IF.                                                      INKCS022
01235  EJECT                                                            INKCS022
01236 *----------------------------------------------------------------*INKCS022
01237 *    WHENEVER THE USER SELECTS AN ITEM AS THE BEGINNING RANGE    *INKCS022
01238 *    AND ONE AS THE ENDING RANGE, MARK THESE ITEMS AS WELL AS    *INKCS022
01239 *    THOSE THAT FALL IN THE RANGE AS BEING SELECTED.             *INKCS022
01240 *----------------------------------------------------------------*INKCS022
01241                                                                   INKCS022
01242  3500-MARK-RANGE-AS-SELECTED.                                     INKCS022
01243      MOVE +1                       TO  ASC-BLK-SUB.               INKCS022
01244      PERFORM 3510-MARK-SELECTED-INDS                              INKCS022
01245          VARYING ASC-ITEM-SUB                                     INKCS022
01246          FROM 1 BY 1                                              INKCS022
01247          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
01248                                                                   INKCS022
01249      ADD +1                        TO ASC-BLK-SUB.                INKCS022
01250      PERFORM 3510-MARK-SELECTED-INDS                              INKCS022
01251          VARYING ASC-ITEM-SUB                                     INKCS022
01252          FROM 1 BY 1                                              INKCS022
01253          UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL.                 INKCS022
01254                                                                   INKCS022
01255      MOVE ASC-LIST-ITEM-NUMBER(1 1)                               INKCS022
01256                                    TO  PV-LIST-ITEM-NUMBER.       INKCS022
01257      COMPUTE PV-FIRST-BLOCK-IN-ARRAY =                            INKCS022
01258          1 + ((PV-LIST-ITEM-NUMBER - 1) /                         INKCS022
01259                PC-ITEMS-PER-PANEL).                               INKCS022
01260                                                                   INKCS022
01261      MOVE ASC-START-OF-SELECTED-RANGE                             INKCS022
01262                                    TO PV-START-OF-SELECTED-RANGE. INKCS022
01263      COMPUTE PV-FIRST-BLOCK-IN-RANGE =                            INKCS022
01264          1 + ((PV-START-OF-SELECTED-RANGE - 1) /                  INKCS022
01265                PC-ITEMS-PER-PANEL).                               INKCS022
01266                                                                   INKCS022
01267      MOVE ASC-END-OF-SELECTED-RANGE                               INKCS022
01268                                    TO  PV-END-OF-SELECTED-RANGE.  INKCS022
01269      COMPUTE PV-LAST-BLOCK-IN-RANGE =                             INKCS022
01270          1 + ((PV-END-OF-SELECTED-RANGE - 1) /                    INKCS022
01271                PC-ITEMS-PER-PANEL).                               INKCS022
01272      COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                             INKCS022
01273          PV-FIRST-BLOCK-IN-ARRAY + 1.                             INKCS022
01274                                                                   INKCS022
01275      IF (PV-FIRST-BLOCK-IN-RANGE < PV-FIRST-BLOCK-IN-ARRAY) OR    INKCS022
01276         (PV-LAST-BLOCK-IN-RANGE  > PV-NEXT-BLOCK-IN-ARRAY)        INKCS022
01277          MOVE +1                   TO ASC-BLK-SUB                 INKCS022
01278          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS022
01279          COMPUTE PV-PREV-BLOCK-IN-ARRAY =                         INKCS022
01280              PV-FIRST-BLOCK-IN-ARRAY - 1                          INKCS022
01281                                                                   INKCS022
01282          PERFORM                                                  INKCS022
01283              UNTIL PV-FIRST-BLOCK-IN-RANGE >                      INKCS022
01284                    PV-PREV-BLOCK-IN-ARRAY                         INKCS022
01285              MOVE PV-FIRST-BLOCK-IN-RANGE                         INKCS022
01286                                    TO PV-BLOCK-NUMBER             INKCS022
01287              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
01288              PERFORM 3510-MARK-SELECTED-INDS                      INKCS022
01289                  VARYING ASC-ITEM-SUB                             INKCS022
01290                  FROM 1 BY 1                                      INKCS022
01291                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
01292              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
01293              ADD +1                TO PV-FIRST-BLOCK-IN-RANGE     INKCS022
01294          END-PERFORM                                              INKCS022
01295                                                                   INKCS022
01296          COMPUTE PV-NEXT-BLOCK-IN-ARRAY =                         INKCS022
01297              PV-FIRST-BLOCK-IN-ARRAY + PC-MAX-BLOCKS-IN-ARRAY     INKCS022
01298          PERFORM                                                  INKCS022
01299              UNTIL PV-NEXT-BLOCK-IN-ARRAY >                       INKCS022
01300                    PV-LAST-BLOCK-IN-RANGE                         INKCS022
01301              MOVE PV-NEXT-BLOCK-IN-ARRAY                          INKCS022
01302                                    TO PV-BLOCK-NUMBER             INKCS022
01303              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
01304              PERFORM 3510-MARK-SELECTED-INDS                      INKCS022
01305                  VARYING ASC-ITEM-SUB                             INKCS022
01306                  FROM 1 BY 1                                      INKCS022
01307                  UNTIL ASC-ITEM-SUB > PC-ITEMS-PER-PANEL          INKCS022
01308              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
01309              ADD +1                TO PV-NEXT-BLOCK-IN-ARRAY      INKCS022
01310          END-PERFORM                                              INKCS022
01311                                                                   INKCS022
01312          MOVE PV-FIRST-BLOCK-IN-ARRAY                             INKCS022
01313                                    TO PV-BLOCK-NUMBER             INKCS022
01314          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
01315      END-IF.                                                      INKCS022
01316                                                                   INKCS022
01317      MOVE +0                       TO ASC-START-OF-SELECTED-RANGE INKCS022
01318                                       ASC-END-OF-SELECTED-RANGE.  INKCS022
01319                                                                   INKCS022
01320  EJECT                                                            INKCS022
01321 *----------------------------------------------------------------*INKCS022
01322 * SET THE SELECTION INDICATOR TO 'S' WITHIN THE RANGE.           *INKCS022
01323 *----------------------------------------------------------------*INKCS022
01324                                                                   INKCS022
01325  3510-MARK-SELECTED-INDS.                                         INKCS022
01326                                                                   INKCS022
01327      IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS022
01328                                 ASC-ITEM-SUB) >                   INKCS022
01329          ASC-START-OF-SELECTED-RANGE)                             INKCS022
01330      OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                         INKCS022
01331                                 ASC-ITEM-SUB) =                   INKCS022
01332          ASC-START-OF-SELECTED-RANGE)                             INKCS022
01333                                                                   INKCS022
01334          IF (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS022
01335                                     ASC-ITEM-SUB) <               INKCS022
01336              ASC-END-OF-SELECTED-RANGE)                           INKCS022
01337          OR (ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB                     INKCS022
01338                                     ASC-ITEM-SUB) =               INKCS022
01339              ASC-END-OF-SELECTED-RANGE)                           INKCS022
01340                                                                   INKCS022
01341              IF NOT ASC-SELECT(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS022
01342                  SET ASC-SELECT     (ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS022
01343                      ASC-TEMP-SELECT(ASC-BLK-SUB  ASC-ITEM-SUB)   INKCS022
01344                                TO TRUE                            INKCS022
01345                  SET ASC-BLOCK-MODIFIED(ASC-BLK-SUB)              INKCS022
01346                                TO TRUE                            INKCS022
01347                  ADD +1        TO ASC-NUMBER-OF-SELECTED-ITEMS    INKCS022
01348              END-IF                                               INKCS022
01349          END-IF                                                   INKCS022
01350      END-IF.                                                      INKCS022
01351                                                                   INKCS022
01352  EJECT                                                            INKCS022
01353 *----------------------------------------------------------------*INKCS022
01354 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS022
01355 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS022
01356 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS022
01357 *    DISPLAY THE PANEL TO USER.                                  *INKCS022
01358 *----------------------------------------------------------------*INKCS022
01359                                                                   INKCS022
01360  4000-BUILD-INITIAL-PANEL.                                        INKCS022
01361                                                                   INKCS022
01362      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS022
01363                                  ASC-BLOCK-ENTRIES (2).           INKCS022
01364      MOVE +1                 TO ASC-BLK-SUB.                      INKCS022
01365      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS022
01366                              TO TRUE.                             INKCS022
01367                                                                   INKCS022
01368      MOVE +0                 TO ASC-ITEM-SUB                      INKCS022
01369                                 ASC-NUMBER-OF-ITEMS-READ          INKCS022
01370                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS022
01371                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS022
01372                                 ASC-START-OF-SELECTED-RANGE       INKCS022
01373                                 ASC-END-OF-SELECTED-RANGE         INKCS022
01374                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS022
01375                                                                   INKCS022
W26600     MOVE ASC-KEY-STORE-NBR    TO STKRMW-LOC-NBR.                 INKCS022
01377                                                                   INKCS022
01378      IF ASC-KEY-SHEET-NBR IS NUMERIC                              INKCS022
01379          MOVE ASC-KEY-SHEET-NBR TO PV-DB2-SHEET-NBR               INKCS022
01380          SET ASC-SHEET-NBR-CSR TO TRUE                            INKCS022
01381      ELSE                                                         INKCS022
01382          SET ASC-STORE-NBR-CSR TO TRUE                            INKCS022
01383      END-IF.                                                      INKCS022
01384                                                                   INKCS022
01385      PERFORM 4010-OPEN-CURSOR.                                    INKCS022
01386                                                                   INKCS022
01387      IF SQLCODE = ZERO                                            INKCS022
01388          PERFORM 4300-READ-ITEMS-FROM-DB                          INKCS022
01389                                                                   INKCS022
01390          PERFORM 4020-CLOSE-CURSOR                                INKCS022
01391                                                                   INKCS022
01392          MOVE +1                   TO  PV-TOP-ITEM                INKCS022
01393          IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                      INKCS022
01394              SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                INKCS022
01395              SET DP020-MSG-FATAL   TO  TRUE                       INKCS022
01396 *            ---- NO LIST ITEMS FOUND FOR CRITERIA ----           INKCS022
01397              MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER           INKCS022
01398          ELSE                                                     INKCS022
01399              MOVE -1               TO MR-SELECTL (1)              INKCS022
01400              SET DP030-SET-CURSOR-APPL-1                          INKCS022
01401                                    TO TRUE                        INKCS022
01402              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS022
01403          END-IF                                                   INKCS022
01404      ELSE                                                         INKCS022
01405          SET  DP020-NEXT-ACT-APPL-ERROR                           INKCS022
01406               DP020-MSG-WARNING         TO TRUE                   INKCS022
01407 *        ---- WAIT 15 SECONDS, THEN RETRY ----                    INKCS022
01408          MOVE PC-TSYMSG-00551  TO  DP020-MSG-NUMBER               INKCS022
01409      END-IF.                                                      INKCS022
01410  EJECT                                                            INKCS022
01411 *----------------------------------------------------------------*INKCS022
01412 * OPEN THE STOCKROOM WITHDRAWAL CURSOR.                          *INKCS022
01413 *   IN THIS PARTICULAR APPLICATION ONE OF 2 DIFFERENT CURSORS    *INKCS022
01414 *   WILL BE OPENED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *INKCS022
01415 *   USER.                                                        *INKCS022
01416 *----------------------------------------------------------------*INKCS022
01417                                                                   INKCS022
01418  4010-OPEN-CURSOR.                                                INKCS022
01419      EVALUATE TRUE                                                INKCS022
01420          WHEN ASC-STORE-NBR-CSR                                   INKCS022
01421              MOVE 'CURSOR:  STORE-NBR-CSR'                        INKCS022
01422                                TO DP013-MESSAGE-TEXT (2)          INKCS022
01423              EXEC SQL                                             INKCS022
01424                   OPEN STORE-NBR-CSR                              INKCS022
01425              END-EXEC                                             INKCS022
01426                                                                   INKCS022
01427          WHEN ASC-SHEET-NBR-CSR                                   INKCS022
01428              MOVE 'CURSOR:  SHEET-NBR-CSR'                        INKCS022
01429                                TO DP013-MESSAGE-TEXT (2)          INKCS022
01430              EXEC SQL                                             INKCS022
01431                   OPEN SHEET-NBR-CSR                              INKCS022
01432              END-EXEC                                             INKCS022
01433      END-EVALUATE.                                                INKCS022
01434                                                                   INKCS022
01435      EVALUATE TRUE                                                INKCS022
01436          WHEN SQLCODE = ZERO                                      INKCS022
01437          WHEN SQLCODE = -904                                      INKCS022
01438          WHEN SQLCODE = -913                                      INKCS022
01439              CONTINUE                                             INKCS022
01440          WHEN SQLWARN0 NOT = SPACES                               INKCS022
01441          WHEN SQLCODE  NOT = ZERO                                 INKCS022
01442              MOVE '4010-OPEN-CURSOR'                              INKCS022
01443                                TO DP013-PARAGRAPH                 INKCS022
01444              MOVE 'OPEN STOCKROOM WITHDRAWAL CURSOR - '           INKCS022
01445                                TO DP013-MESSAGE-TEXT (1)          INKCS022
01446              MOVE SQLCA        TO DP013-SQLCA                     INKCS022
01447              MOVE 'TSTKRMW '   TO DP013-DB2-TABLE-NAME (1)        INKCS022
01448              SET DP013-DB2-ABEND                                  INKCS022
01449                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS022
01450              PERFORM DP013-0000-PROCESS-ABEND                     INKCS022
01451      END-EVALUATE.                                                INKCS022
01452  EJECT                                                            INKCS022
01453 *----------------------------------------------------------------*INKCS022
01454 * CLOSE THE STOCKROOM WITHDRAWAL CURSOR.                         *INKCS022
01455 *   IN THIS PARTICULAR APPLICATION ONE OF 2 DIFFERENT CURSORS    *INKCS022
01456 *   WILL BE CLOSED DEPENDING ON THE CRITERIA SPECIFIED BY THE    *INKCS022
01457 *   USER.                                                        *INKCS022
01458 *----------------------------------------------------------------*INKCS022
01459                                                                   INKCS022
01460  4020-CLOSE-CURSOR.                                               INKCS022
01461      EVALUATE TRUE                                                INKCS022
01462          WHEN ASC-STORE-NBR-CSR                                   INKCS022
01463              EXEC SQL                                             INKCS022
01464                  CLOSE STORE-NBR-CSR                              INKCS022
01465              END-EXEC                                             INKCS022
01466                                                                   INKCS022
01467          WHEN ASC-SHEET-NBR-CSR                                   INKCS022
01468              EXEC SQL                                             INKCS022
01469                  CLOSE SHEET-NBR-CSR                              INKCS022
01470              END-EXEC                                             INKCS022
01471      END-EVALUATE.                                                INKCS022
01472 *                                                                 INKCS022
01473      EVALUATE TRUE                                                INKCS022
01474          WHEN SQLCODE = ZERO                                      INKCS022
01475              CONTINUE                                             INKCS022
01476          WHEN SQLWARN0 NOT = SPACES                               INKCS022
01477          WHEN SQLCODE  NOT = ZERO                                 INKCS022
01478              MOVE '4020-CLOSE-CURSOR'                             INKCS022
01479                                TO DP013-PARAGRAPH                 INKCS022
01480              MOVE 'CLOSE STOCKROOM WITHDRAWAL CURSOR'             INKCS022
01481                                TO DP013-MESSAGE-TEXT (1)          INKCS022
01482              MOVE SQLCA        TO DP013-SQLCA                     INKCS022
01483              MOVE 'TSTKRMW '   TO DP013-DB2-TABLE-NAME (1)        INKCS022
01484              SET DP013-DB2-ABEND                                  INKCS022
01485                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS022
01486              PERFORM DP013-0000-PROCESS-ABEND                     INKCS022
01487      END-EVALUATE.                                                INKCS022
01488  EJECT                                                            INKCS022
01489 *----------------------------------------------------------------*INKCS022
01490 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS022
01491 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS022
01492 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS022
01493 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS022
01494 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS022
01495 *                                                                *INKCS022
01496 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS022
01497 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS022
01498 *    UPON INITIALIZATION.                                        *INKCS022
01499 *----------------------------------------------------------------*INKCS022
01500                                                                   INKCS022
01501  4300-READ-ITEMS-FROM-DB.                                         INKCS022
01502                                                                   INKCS022
01503      PERFORM                                                      INKCS022
01504          VARYING PV-READ-COUNT                                    INKCS022
01505          FROM 1 BY 1                                              INKCS022
01506          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS022
01507             OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS022
01508                                                                   INKCS022
01509          PERFORM 4310-FETCH                                       INKCS022
01510                                                                   INKCS022
01511          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS022
01512              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                INKCS022
01513                  ADD +1            TO ASC-ITEM-SUB                INKCS022
01514              ELSE                                                 INKCS022
01515                  ADD +1            TO ASC-BLK-SUB                 INKCS022
01516                  MOVE +1           TO ASC-ITEM-SUB                INKCS022
01517                                                                   INKCS022
01518                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         INKCS022
01519                      MOVE +1       TO ASC-BLK-SUB                 INKCS022
01520                      SET ASC-BLOCK-MODIFIED (1) TO TRUE           INKCS022
01521                      PERFORM 5100-WRITE-TEMP-STORAGE              INKCS022
01522                      MOVE ASC-BLOCK-ENTRIES                       INKCS022
01523                                           (PC-MAX-BLOCKS-IN-ARRAY)INKCS022
01524                                  TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)INKCS022
01525                      ADD +1        TO ASC-BLK-SUB                 INKCS022
01526                      INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)   INKCS022
01527                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      INKCS022
01528                                                            TO TRUEINKCS022
01529                  END-IF                                           INKCS022
01530              END-IF                                               INKCS022
01531              ADD +1                TO ASC-NUMBER-OF-ITEMS-READ    INKCS022
01532              MOVE ASC-NUMBER-OF-ITEMS-READ                        INKCS022
01533                 TO ASC-LIST-ITEM-NUMBER (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01534              MOVE STKRMW-SHEET-NBR                                INKCS022
01535                 TO ASC-SHEET-NBR        (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01536              MOVE PV-SUM-ITM-EXTD-AMT                             INKCS022
01537                 TO ASC-SUM-ITM-EXTD-AMT (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01538          END-IF                                                   INKCS022
01539      END-PERFORM.                                                 INKCS022
01540                                                                   INKCS022
01541      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS022
01542          MOVE +1               TO ASC-BLK-SUB                     INKCS022
01543          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS022
01544          ADD +1                TO ASC-BLK-SUB                     INKCS022
01545          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS022
01546                                                                   INKCS022
01547          MOVE 1                TO ASC-BLK-SUB                     INKCS022
01548                                   PV-BLOCK-NUMBER                 INKCS022
01549          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
01550          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS022
01551              MOVE 2            TO ASC-BLK-SUB                     INKCS022
01552                                   PV-BLOCK-NUMBER                 INKCS022
01553              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
01554          END-IF                                                   INKCS022
01555      END-IF.                                                      INKCS022
01556  EJECT                                                            INKCS022
01557 *----------------------------------------------------------------*INKCS022
01558 *    FETCH THE MESSAGE CURSOR.                                   *INKCS022
01559 *----------------------------------------------------------------*INKCS022
01560                                                                   INKCS022
01561  4310-FETCH.                                                      INKCS022
01562                                                                   INKCS022
01563      EVALUATE TRUE                                                INKCS022
01564          WHEN ASC-STORE-NBR-CSR                                   INKCS022
01565               EXEC SQL                                            INKCS022
01566                    FETCH STORE-NBR-CSR                            INKCS022
01567                    INTO  :STKRMW-SHEET-NBR                        INKCS022
01568                        , :PV-SUM-ITM-EXTD-AMT                     INKCS022
01569               END-EXEC                                            INKCS022
01570                                                                   INKCS022
01571          WHEN ASC-SHEET-NBR-CSR                                   INKCS022
01572               EXEC SQL                                            INKCS022
01573                    FETCH SHEET-NBR-CSR                            INKCS022
01574                    INTO                                           INKCS022
01575                          :STKRMW-SHEET-NBR                        INKCS022
01576                        , :PV-SUM-ITM-EXTD-AMT                     INKCS022
01577               END-EXEC                                            INKCS022
01578      END-EVALUATE.                                                INKCS022
01579                                                                   INKCS022
01580      EVALUATE TRUE                                                INKCS022
01581          WHEN SQLCODE = ZERO                                      INKCS022
01582          WHEN SQLCODE = +100                                      INKCS022
01583               CONTINUE                                            INKCS022
01584          WHEN SQLWARN0 NOT = SPACE                                INKCS022
01585          WHEN SQLCODE  NOT = ZERO                                 INKCS022
01586               MOVE '4310-FETCH-CURSOR'                            INKCS022
01587                                TO DP013-PARAGRAPH                 INKCS022
01588               MOVE 'FETCH STOCKROOM WITHDRAWAL CURSOR'            INKCS022
01589                                TO DP013-MESSAGE-TEXT (1)          INKCS022
01590               MOVE SQLCA       TO DP013-SQLCA                     INKCS022
01591               SET DP013-DB2-ABEND                                 INKCS022
01592                                TO TRUE                            INKCS022
01593               PERFORM DP013-0000-PROCESS-ABEND                    INKCS022
01594      END-EVALUATE.                                                INKCS022
01595                                                                   INKCS022
01596      IF  SQLCODE EQUAL +100                                       INKCS022
01597          SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS022
01598                                    TO TRUE                        INKCS022
01599      END-IF.                                                      INKCS022
01600                                                                   INKCS022
01601  EJECT                                                            INKCS022
01602 *----------------------------------------------------------------*INKCS022
01603 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS022
01604 *----------------------------------------------------------------*INKCS022
01605                                                                   INKCS022
01606  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS022
01607                                                                   INKCS022
W26600     MOVE ASC-KEY-STORE-NBR-X TO ASTRNBRO.                        INKCS022
01608      MOVE ASC-STORE-NAME TO ASTNAMEO.                             INKCS022
01609                                                                   INKCS022
01610      IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS022
01611          AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS022
01612          OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS022
01613              MOVE +1    TO ASC-BLK-SUB                            INKCS022
01614              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
01615              ADD +1     TO ASC-BLK-SUB                            INKCS022
01616              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS022
01617              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS022
01618                  GIVING PV-BLOCK-NUMBER                           INKCS022
01619                  REMAINDER PV-REMAINDER                           INKCS022
01620              IF  PV-REMAINDER > ZERO                              INKCS022
01621                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS022
01622              END-IF                                               INKCS022
01623              MOVE +1    TO ASC-BLK-SUB                            INKCS022
01624              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
01625              ADD +1     TO ASC-BLK-SUB                            INKCS022
01626                            PV-BLOCK-NUMBER                        INKCS022
01627              PERFORM 4410-READ-TEMP-STORAGE                       INKCS022
01628      END-IF.                                                      INKCS022
01629      MOVE +1            TO ASC-BLK-SUB.                           INKCS022
01630      COMPUTE ASC-ITEM-SUB =                                       INKCS022
01631          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS022
01632          + PC-ITEMS-PER-PANEL).                                   INKCS022
01633 *                                                                 INKCS022
01634      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS022
01635          ADD +1         TO ASC-BLK-SUB                            INKCS022
01636          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS022
01637      END-IF.                                                      INKCS022
01638 *                                                                 INKCS022
01639      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS022
01640          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS022
01641      PERFORM 4430-CHECK-FOR-BEGIN-AND-END.                        INKCS022
01642      PERFORM 4420-FORMAT-LIST-LINES                               INKCS022
01643          VARYING PV-SUB                                           INKCS022
01644          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS022
01645          UNTIL PV-SUB < +1.                                       INKCS022
01646 *                                                                 INKCS022
01647      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS022
01648          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS022
01649      IF PS-NO-START-ITEM-ERROR                                    INKCS022
01650          MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANELINKCS022
01651                                           ASTRTITO                INKCS022
01652          MOVE SPACE                    TO ASC-START-ITEM-REQUEST  INKCS022
01653      ELSE                                                         INKCS022
01654          MOVE ASC-START-ITEM-REQUEST   TO ASTRTITO                INKCS022
01655      END-IF.                                                      INKCS022
01656      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS022
01657      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS022
01658      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS022
01659                                    TO ASELITO.                    INKCS022
01660 *                                                                 INKCS022
01661      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS022
01662          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS022
01663 *            --- MORE ITEMS TO DISPLAY ----                       INKCS022
01664              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS022
01665              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS022
01666          END-IF                                                   INKCS022
01667      END-IF.                                                      INKCS022
01668 *----------------------------------------------------------------*INKCS022
01669 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS022
01670 *----------------------------------------------------------------*INKCS022
01671                                                                   INKCS022
01672  4410-READ-TEMP-STORAGE.                                          INKCS022
01673                                                                   INKCS022
01674      EXEC CICS                                                    INKCS022
01675          READQ TS                                                 INKCS022
01676              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
01677              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS022
01678              ITEM  (PV-BLOCK-NUMBER)                              INKCS022
01679              RESP  (PV-CICS-RESP)                                 INKCS022
01680      END-EXEC.                                                    INKCS022
01681                                                                   INKCS022
01682      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS022
01683        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS022
01684          CONTINUE                                                 INKCS022
01685      ELSE                                                         INKCS022
01686          SET DP013-CICS-ABEND     TO TRUE                         INKCS022
01687          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS022
01688          MOVE '4410-READ-TEMP-STORAGE'                            INKCS022
01689                                   TO DP013-PARAGRAPH              INKCS022
01690          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS022
01691                                   TO DP013-MESSAGE-TEXT(1)        INKCS022
01692          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
01693      END-IF.                                                      INKCS022
01694                                                                   INKCS022
01695      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS022
01696 *                                                                 INKCS022
01697      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS022
01698          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS022
01699      END-IF.                                                      INKCS022
01700  EJECT                                                            INKCS022
01701 *----------------------------------------------------------------*INKCS022
01702 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS022
01703 *----------------------------------------------------------------*INKCS022
01704                                                                   INKCS022
01705  4420-FORMAT-LIST-LINES.                                          INKCS022
01706      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS022
01707          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS022
01708          MOVE DP015-ASK-DRK-OFF  TO MR-SELECTA (PV-SUB)           INKCS022
01709          MOVE SPACE              TO MR-SELECTI (PV-SUB)           INKCS022
01710                                     MR-ACTION  (PV-SUB)           INKCS022
01711                                     MR-SHEET-NBR-X (PV-SUB)       INKCS022
01712                                     MR-SUM-ITM-EXTD-AMT-X (PV-SUB)INKCS022
01713      ELSE                                                         INKCS022
01714          IF  ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS022
01715                                                    > SPACE        INKCS022
01716              MOVE ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB) INKCS022
01717                                  TO MR-SELECTI (PV-SUB)           INKCS022
01718          ELSE                                                     INKCS022
01719              MOVE ASC-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS022
01720                                  TO MR-SELECTI (PV-SUB)           INKCS022
01721          END-IF                                                   INKCS022
01722 *                                                                 INKCS022
01723          IF  DP030-SET-CURSOR-TRAILER                             INKCS022
01724              IF  ((ASC-TEMP-SELECTED-SW (ASC-BLK-SUB ASC-ITEM-SUB)INKCS022
01725                  > SPACE)                                         INKCS022
01726                OR (PV-SUB = 1))                                   INKCS022
01727                 SET DP030-SET-CURSOR-APPL-1 TO TRUE               INKCS022
01728                 MOVE -1          TO MR-SELECTL (PV-SUB)           INKCS022
01729              END-IF                                               INKCS022
01730          END-IF                                                   INKCS022
01731 *                                                                 INKCS022
01732          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS022
01733              MOVE ASC-ACTION (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS022
01734                                  TO MR-ACTION (PV-SUB)            INKCS022
01735              MOVE ASC-SHEET-NBR (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS022
01736                                  TO MR-SHEET-NBR (PV-SUB)         INKCS022
01737              MOVE ASC-SUM-ITM-EXTD-AMT (ASC-BLK-SUB ASC-ITEM-SUB) INKCS022
01738                                  TO MR-SUM-ITM-EXTD-AMT (PV-SUB)  INKCS022
01739          END-IF                                                   INKCS022
01740      END-IF.                                                      INKCS022
01741      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS022
01742      IF  ASC-ITEM-SUB < + 1                                       INKCS022
01743          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS022
01744          MOVE +1                 TO ASC-BLK-SUB                   INKCS022
01745      END-IF.                                                      INKCS022
01746 ******************************************************************INKCS022
01747 ** IF A BEGINNING OF RANGE WAS SPECIFIED, MAKE SURE AN END OF   **INKCS022
01748 ** RANGE WAS SPECIFIED AND VICE-VERSA.  IF ONE IS MISSING,      **INKCS022
01749 ** ISSUE A WARNING.                                             **INKCS022
01750 ******************************************************************INKCS022
01751  4430-CHECK-FOR-BEGIN-AND-END.                                    INKCS022
01752      IF  ASC-START-OF-SELECTED-RANGE > ZERO                       INKCS022
01753          IF  ASC-END-OF-SELECTED-RANGE = ZERO                     INKCS022
01754 *            -- NO END-OF-RANGE SPECFIED ---                      INKCS022
01755              MOVE PC-TSYMSG-00051  TO DP020-MSG-NUMBER            INKCS022
01756              SET DP020-MSG-WARNING TO TRUE                        INKCS022
01757              PERFORM 4431-POSITION-AT-WARNING                     INKCS022
01758          END-IF                                                   INKCS022
01759      ELSE                                                         INKCS022
01760          IF  ASC-END-OF-SELECTED-RANGE > ZERO                     INKCS022
01761 *            -- NO BEGINNING-OF-RANGE SPECFIED ---                INKCS022
01762              MOVE PC-TSYMSG-00053  TO DP020-MSG-NUMBER            INKCS022
01763              SET DP020-MSG-WARNING TO TRUE                        INKCS022
01764              PERFORM 4431-POSITION-AT-WARNING                     INKCS022
01765          END-IF                                                   INKCS022
01766      END-IF.                                                      INKCS022
01767 ******************************************************************INKCS022
01768 ** ONCE IT HAS BEEN DETERMINED THAT A WARNING IS TO BE ISSUED,  **INKCS022
01769 ** DETERMINE IF EITHER THE BEGINNING OF RANGE OR END OF RANGE   **INKCS022
01770 ** IS ON THE CURRENT SCREEN.  IF IT IS, HIGHLIGHT IT AND        **INKCS022
01771 ** POSITION THE CURSOR THERE.                                   **INKCS022
01772 ******************************************************************INKCS022
01773  4431-POSITION-AT-WARNING.                                        INKCS022
01774      COMPUTE PV-SUB = ASC-START-OF-SELECTED-RANGE                 INKCS022
01775                     - PV-TOP-ITEM + 1.                            INKCS022
01776      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS022
01777          CONTINUE                                                 INKCS022
01778      ELSE                                                         INKCS022
01779          SET DP030-SET-CURSOR-APPL-1                              INKCS022
01780                                    TO TRUE                        INKCS022
01781          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS022
01782          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS022
01783          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS022
01784      END-IF.                                                      INKCS022
01785 *                                                                 INKCS022
01786      COMPUTE PV-SUB = ASC-END-OF-SELECTED-RANGE                   INKCS022
01787                     - PV-TOP-ITEM + 1.                            INKCS022
01788      IF  PV-SUB < +1 OR > PC-ITEMS-PER-PANEL                      INKCS022
01789          CONTINUE                                                 INKCS022
01790      ELSE                                                         INKCS022
01791          SET DP030-SET-CURSOR-APPL-1                              INKCS022
01792                                    TO TRUE                        INKCS022
01793          MOVE -1                   TO MR-SELECTL (PV-SUB)         INKCS022
01794          MOVE DP015-REVERSE        TO MR-SELECTH (PV-SUB)         INKCS022
01795          MOVE DP015-YELLOW         TO MR-SELECTC (PV-SUB)         INKCS022
01796      END-IF.                                                      INKCS022
01797  EJECT                                                            INKCS022
01798 *----------------------------------------------------------------*INKCS022
01799 * PREPARE THE SELECTED ITEMS TEMP STORAGE QUEUE.  MOVE ALL SEL-  *INKCS022
01800 * ECTED ITEMS FROM THE LIST TEMP STORAGE QUEUE TO THE LAYOUT     *INKCS022
01801 * FOR THE SELECTED ITEMS QUEUE.                                  *INKCS022
01802 *                                                                *INKCS022
01803 * FIRST, DELETE THE SELECTION QUEUE, NEXT LOAD THE SELECTION     *INKCS022
01804 * QUEUE RECORD.  FINALLY, WRITE THE SELECTION QUEUE ITEM WHEN    *INKCS022
01805 * THE ARRAY IS FULL.                                             *INKCS022
01806 *----------------------------------------------------------------*INKCS022
01807                                                                   INKCS022
01808  5000-PREPARE-SEL-QUEUE.                                          INKCS022
01809                                                                   INKCS022
01810      MOVE +1                   TO  ASC-BLK-SUB.                   INKCS022
01811      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS022
01812      ADD +1                    TO  ASC-BLK-SUB.                   INKCS022
01813      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS022
01814                                                                   INKCS022
01815      MOVE ASC-SEL-QUEUE-NAME   TO  IN001-SEL-QUEUE-NAME.          INKCS022
01816      PERFORM 6010-DELETE-SEL-QUEUE.                               INKCS022
01817                                                                   INKCS022
01818      SET ASC-UPDATE-TSQ        TO  TRUE.                          INKCS022
01819                                                                   INKCS022
01820      SET IN022-IDX             TO  1.                             INKCS022
01821      MOVE ZERO                 TO IN001-NUMBER-OF-TS-ITEMS.       INKCS022
01822      INITIALIZE IN022-SELECTED-ITEM-ARRAY.                        INKCS022
01823                                                                   INKCS022
01824      PERFORM 5200-FILL-SEL-QUEUE-RECORD                           INKCS022
01825              VARYING PV-ITEM                                      INKCS022
01826              FROM 1 BY 1                                          INKCS022
01827              UNTIL PV-ITEM > ASC-HIGHEST-BLOCK-WRITTEN.           INKCS022
01828                                                                   INKCS022
01829      IF  IN022-IDX > 1                                            INKCS022
01830          PERFORM 5205-WRITE-SEL-QUEUE                             INKCS022
01831          ADD +1                TO IN001-NUMBER-OF-TS-ITEMS        INKCS022
01832      END-IF.                                                      INKCS022
01833  EJECT                                                            INKCS022
01834 *----------------------------------------------------------------*INKCS022
01835 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS022
01836 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS022
01837 *----------------------------------------------------------------*INKCS022
01838                                                                   INKCS022
01839  5100-WRITE-TEMP-STORAGE.                                         INKCS022
01840      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS022
01841                                    TO PV-LIST-ITEM-NUMBER.        INKCS022
01842      COMPUTE PV-BLOCK-NUMBER =                                    INKCS022
01843          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS022
01844                                                                   INKCS022
01845      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS022
01846      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS022
01847          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS022
01848              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS022
01849          END-IF                                                   INKCS022
01850      ELSE                                                         INKCS022
01851          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS022
01852          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS022
01853          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS022
01854          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS022
01855      END-IF.                                                      INKCS022
01856                                                                   INKCS022
01857      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS022
01858 ******************************************************************INKCS022
01859 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS022
01860 ** SPECIFIC COMMAREA.                                           **INKCS022
01861 ******************************************************************INKCS022
01862  5105-REWRITE-ASC-LISTQ.                                          INKCS022
01863      EXEC CICS                                                    INKCS022
01864          WRITEQ TS                                                INKCS022
01865              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
01866              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS022
01867              ITEM  (PV-BLOCK-NUMBER)                              INKCS022
01868              REWRITE                                              INKCS022
01869              RESP  (PV-CICS-RESP)                                 INKCS022
01870      END-EXEC.                                                    INKCS022
01871                                                                   INKCS022
01872      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS022
01873          CONTINUE                                                 INKCS022
01874      ELSE                                                         INKCS022
01875          SET DP013-CICS-ABEND            TO TRUE                  INKCS022
01876          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS022
01877          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS022
01878          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS022
01879                                          TO DP013-MESSAGE-TEXT(1) INKCS022
01880          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
01881      END-IF.                                                      INKCS022
01882                                                                   INKCS022
01883 ******************************************************************INKCS022
01884 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS022
01885 ** SPECIFIC COMMAREA.                                           **INKCS022
01886 ******************************************************************INKCS022
01887  5110-WRITE-ASC-LISTQ.                                            INKCS022
01888      EXEC CICS                                                    INKCS022
01889          WRITEQ TS                                                INKCS022
01890              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
01891              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS022
01892              ITEM  (PV-BLOCK-NUMBER)                              INKCS022
01893              RESP  (PV-CICS-RESP)                                 INKCS022
01894      END-EXEC.                                                    INKCS022
01895      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS022
01896          SET DP013-CICS-ABEND      TO TRUE                        INKCS022
01897          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS022
01898          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS022
01899                                    TO DP013-PARAGRAPH             INKCS022
01900          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS022
01901                                    TO DP013-MESSAGE-TEXT(1)       INKCS022
01902          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
01903      END-IF.                                                      INKCS022
01904  EJECT                                                            INKCS022
01905 *----------------------------------------------------------------*INKCS022
01906 * READ A LIST QUEUE RECORD AND MOVE THE KEYS OF SELECTED OBJECTS *INKCS022
01907 * TO THE SELECTED ITEMS TEMP STORAGE QUEUE RECORD.               *INKCS022
01908 *----------------------------------------------------------------*INKCS022
01909                                                                   INKCS022
01910  5200-FILL-SEL-QUEUE-RECORD.                                      INKCS022
01911                                                                   INKCS022
01912      PERFORM 6112-READ-LIST-QUEUE.                                INKCS022
01913                                                                   INKCS022
01914      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS022
01915          PERFORM                                                  INKCS022
01916          VARYING LQW-IDX                                          INKCS022
01917          FROM 1 BY 1                                              INKCS022
01918          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS022
01919            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS022
01920                                                                   INKCS022
01921              IF  LQW-SELECT (LQW-IDX)                             INKCS022
01922                  MOVE LQW-LIST-ITEM-NUMBER (LQW-IDX)              INKCS022
01923                                    TO IN022-ITEM (IN022-IDX)      INKCS022
01924                  MOVE LQW-SHEET-NBR (LQW-IDX)                     INKCS022
01925                                    TO IN022-SHEET-NBR (IN022-IDX) INKCS022
01926                  MOVE SPACE        TO IN022-ACTION-SW (IN022-IDX) INKCS022
01927                  SET IN022-IDX UP BY 1                            INKCS022
01928                  IF  IN022-IDX > IN022-MAX-ITEMS                  INKCS022
01929                      PERFORM 5205-WRITE-SEL-QUEUE                 INKCS022
01930                      ADD +1        TO IN001-NUMBER-OF-TS-ITEMS    INKCS022
01931                      INITIALIZE IN022-SELECTED-ITEM-ARRAY         INKCS022
01932                      SET IN022-IDX TO  1                          INKCS022
01933                  END-IF                                           INKCS022
01934              END-IF                                               INKCS022
01935          END-PERFORM                                              INKCS022
01936      END-IF.                                                      INKCS022
01937                                                                   INKCS022
01938 *----------------------------------------------------------------*INKCS022
01939 *    WRITE THE SELECTED ITEMS TEMP STORAGE QUEUE.                *INKCS022
01940 *----------------------------------------------------------------*INKCS022
01941                                                                   INKCS022
01942  5205-WRITE-SEL-QUEUE.                                            INKCS022
01943                                                                   INKCS022
01944      MOVE ASC-NUMBER-OF-SELECTED-ITEMS                            INKCS022
01945                                    TO IN022-SELECTED-ITEMS.       INKCS022
01946                                                                   INKCS022
01947      EXEC CICS                                                    INKCS022
01948           WRITEQ TS                                               INKCS022
01949               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS022
01950               FROM  (IN022-SHEET-SEL-REC)                         INKCS022
01951               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS022
01952               RESP  (PV-CICS-RESP)                                INKCS022
01953      END-EXEC.                                                    INKCS022
01954                                                                   INKCS022
01955      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS022
01956          CONTINUE                                                 INKCS022
01957      ELSE                                                         INKCS022
01958          SET DP013-CICS-ABEND      TO TRUE                        INKCS022
01959          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS022
01960          MOVE '5205-WRITE-SEL-QUEUE'                              INKCS022
01961                                    TO DP013-PARAGRAPH             INKCS022
01962          MOVE 'ATTEMPTING TO WRITE SELECTED ITEMS QUEUE'          INKCS022
01963                                    TO DP013-MESSAGE-TEXT(1)       INKCS022
01964          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
01965      END-IF.                                                      INKCS022
01966  EJECT                                                            INKCS022
01967 ******************************************************************INKCS022
01968 ** WHEN RETURNING TO THIS TRANSACTION FROM ANOTHER TRANSACTION, **INKCS022
01969 ** SETUP THE ARRAY AS IT WAS WHEN WE HAD LEFT PREVIOUSLY.  EDIT **INKCS022
01970 ** THE SELECTIONS MADE AT THAT TIME AND DISPLAY THE PANEL TO    **INKCS022
01971 ** THE USER.                                                    **INKCS022
01972 ******************************************************************INKCS022
01973  6000-REFRESH-PANEL-FROM-TEMP.                                    INKCS022
01974      IF  ASC-UPDATE-TSQ                                           INKCS022
01975          SET ASC-NO-TSQ-UPDATE TO TRUE                            INKCS022
01976          MOVE ZERO                 TO PV-SAVE-ITEM                INKCS022
01977                                       PV-SEL-QUEUE-ITEM           INKCS022
01978          MOVE ASC-NUMBER-OF-SELECTED-ITEMS                        INKCS022
01979                                    TO PV-HOLD-SELECTED-ITEMS      INKCS022
01980          PERFORM 6100-UPDATE-TSQ                                  INKCS022
01981              UNTIL PV-ITEMS-PROCESSED >                           INKCS022
01982                                      ASC-NUMBER-OF-SELECTED-ITEMS INKCS022
01983          PERFORM 6010-DELETE-SEL-QUEUE                            INKCS022
01984          INITIALIZE ASC-BLOCK-ENTRIES (1)                         INKCS022
01985                     ASC-BLOCK-ENTRIES (2)                         INKCS022
01986                                                                   INKCS022
01987          DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL                 INKCS022
01988                 GIVING PV-BLOCK-NUMBER                            INKCS022
01989                 REMAINDER PV-REMAINDER                            INKCS022
01990          IF  PV-REMAINDER > ZERO                                  INKCS022
01991              ADD +1     TO PV-BLOCK-NUMBER                        INKCS022
01992          END-IF                                                   INKCS022
01993          MOVE +1        TO ASC-BLK-SUB                            INKCS022
01994          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
01995          ADD +1         TO ASC-BLK-SUB                            INKCS022
01996                            PV-BLOCK-NUMBER                        INKCS022
01997          PERFORM 4410-READ-TEMP-STORAGE                           INKCS022
01998                                                                   INKCS022
01999      END-IF.                                                      INKCS022
02000                                                                   INKCS022
02001 *----------------------------------------------------------------*INKCS022
02002 *    DELETE THE SELECTED ITEMS TEMP STORAGE QUEUE.               *INKCS022
02003 *----------------------------------------------------------------*INKCS022
02004  6010-DELETE-SEL-QUEUE.                                           INKCS022
02005                                                                   INKCS022
02006      EXEC CICS                                                    INKCS022
02007          DELETEQ TS                                               INKCS022
02008              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS022
02009              RESP  (PV-CICS-RESP)                                 INKCS022
02010      END-EXEC.                                                    INKCS022
02011 *                                                                 INKCS022
02012      IF ((PV-CICS-RESP = DFHRESP(NORMAL)) OR                      INKCS022
02013         (PV-CICS-RESP = DFHRESP(QIDERR)))                         INKCS022
02014          CONTINUE                                                 INKCS022
02015      ELSE                                                         INKCS022
02016          SET DP013-CICS-ABEND TO TRUE                             INKCS022
02017          SET DP013-NO-ROLLBACK TO TRUE                            INKCS022
02018          MOVE '6010-DELETE-SEL-QUEUE'                             INKCS022
02019                         TO DP013-PARAGRAPH                        INKCS022
02020          MOVE 'ERROR TRYING TO DELETE SELECTION TS QUEUE'         INKCS022
02021                         TO DP013-MESSAGE-TEXT (1)                 INKCS022
02022          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
02023      END-IF.                                                      INKCS022
02024                                                                   INKCS022
02025 *----------------------------------------------------------------*INKCS022
02026 *    INCREMENT THE TSQ ITEM NUMBER AND READ THE LIST QUEUE.      *INKCS022
02027 *    LOOP THROUGH THE LIST QUEUE TO OBTAIN THE LINE ITEM NUMBER  *INKCS022
02028 *    TO BE USED TO UPDATE (DESELECT) THE CORRECT LINE ITEM IN    *INKCS022
02029 *    THE TEMP QUEUE.                                             *INKCS022
02030 *----------------------------------------------------------------*INKCS022
02031                                                                   INKCS022
02032  6100-UPDATE-TSQ.                                                 INKCS022
02033                                                                   INKCS022
02034      ADD 1                     TO  PV-SEL-QUEUE-ITEM.             INKCS022
02035      PERFORM 6105-READ-SEL-QUEUE.                                 INKCS022
02036      PERFORM                                                      INKCS022
02037          VARYING IN022-IDX                                        INKCS022
02038          FROM 1 BY 1                                              INKCS022
02039          UNTIL ((PV-ITEMS-PROCESSED >                             INKCS022
02040                               PV-HOLD-SELECTED-ITEMS)             INKCS022
02041          OR (IN022-IDX GREATER THAN IN022-MAX-ITEMS))             INKCS022
02042          IF  IN022-ACTION-SW (IN022-IDX) > SPACE                  INKCS022
02043              PERFORM 6110-PROCESS-ITEMS                           INKCS022
02044          END-IF                                                   INKCS022
02045          ADD +1                TO PV-ITEMS-PROCESSED              INKCS022
02046      END-PERFORM.                                                 INKCS022
02047      PERFORM 6111-WRITE-LIST-QUEUE.                               INKCS022
02048 *----------------------------------------------------------------*INKCS022
02049 *    READ THE LIST QUEUE RECORD.                                 *INKCS022
02050 *----------------------------------------------------------------*INKCS022
02051                                                                   INKCS022
02052  6105-READ-SEL-QUEUE.                                             INKCS022
02053                                                                   INKCS022
02054      EXEC CICS                                                    INKCS022
02055           READQ TS                                                INKCS022
02056               QUEUE (ASC-SEL-QUEUE-NAME)                          INKCS022
02057               INTO  (IN022-SHEET-SEL-REC)                         INKCS022
02058               ITEM  (PV-SEL-QUEUE-ITEM)                           INKCS022
02059               RESP  (PV-CICS-RESP)                                INKCS022
02060      END-EXEC.                                                    INKCS022
02061 *                                                                 INKCS022
02062      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS022
02063          CONTINUE                                                 INKCS022
02064      ELSE                                                         INKCS022
02065          SET DP013-CICS-ABEND      TO  TRUE                       INKCS022
02066          SET DP013-NO-ROLLBACK     TO  TRUE                       INKCS022
02067          MOVE '6105-READ-SEL-QUEUE'                               INKCS022
02068                                    TO  DP013-PARAGRAPH            INKCS022
02069          MOVE 'ATTEMPTING TO READ SELECTED ITEMS QUEUE'           INKCS022
02070                                    TO  DP013-MESSAGE-TEXT(1)      INKCS022
02071          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
02072      END-IF.                                                      INKCS022
02073  EJECT                                                            INKCS022
02074 *----------------------------------------------------------------*INKCS022
02075 *    CALCULATE THE TEMP QUEUE ITEM THAT MUST BE READ AND READ IT.*INKCS022
02076 *    REWRITE THE CURRENT TEMP QUEUE ITEM IF NECESSARY.  CALCULATE*INKCS022
02077 *    THE LINE ITEM OCCURENCE AND UPDATE (DESELECT) IT.           *INKCS022
02078 *----------------------------------------------------------------*INKCS022
02079                                                                   INKCS022
02080  6110-PROCESS-ITEMS.                                              INKCS022
02081                                                                   INKCS022
02082      COMPUTE PV-ITEM = 1 + ((IN022-ITEM (IN022-IDX) - 1)          INKCS022
02083                      / PC-ITEMS-PER-PANEL).                       INKCS022
02084                                                                   INKCS022
02085      IF  PV-ITEM NOT EQUAL PV-SAVE-ITEM                           INKCS022
02086          IF  PV-SAVE-ITEM NOT EQUAL ZERO                          INKCS022
02087              PERFORM 6111-WRITE-LIST-QUEUE                        INKCS022
02088          END-IF                                                   INKCS022
02089          MOVE PV-ITEM          TO PV-SAVE-ITEM                    INKCS022
02090                                                                   INKCS022
02091          PERFORM 6112-READ-LIST-QUEUE                             INKCS022
02092          IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)               INKCS022
02093              CONTINUE                                             INKCS022
02094          END-IF                                                   INKCS022
02095      END-IF.                                                      INKCS022
02096                                                                   INKCS022
02097      COMPUTE PV-SUB = (IN022-ITEM (IN022-IDX)                     INKCS022
02098                     - LQW-LIST-ITEM-NUMBER (1)) + 1.              INKCS022
02099      SET LQW-DESELECT (PV-SUB) TO    TRUE                         INKCS022
02100      MOVE LQW-SELECTED-SW (PV-SUB)                                INKCS022
02101                                TO LQW-TEMP-SELECTED-SW (PV-SUB).  INKCS022
02102      EVALUATE TRUE                                                INKCS022
02103          WHEN IN022-INQ (IN022-IDX)                               INKCS022
02104               MOVE PC-INQ      TO LQW-ACTION (PV-SUB)             INKCS022
02105                                                                   INKCS022
02106          WHEN IN022-UPD (IN022-IDX)                               INKCS022
02107               MOVE PC-UPD      TO LQW-ACTION (PV-SUB)             INKCS022
02108                                                                   INKCS022
02109          WHEN IN022-DEL (IN022-IDX)                               INKCS022
02110               MOVE PC-DEL      TO LQW-ACTION (PV-SUB)             INKCS022
02111                                                                   INKCS022
02112          WHEN IN022-ERR (IN022-IDX)                               INKCS022
02113               MOVE PC-ERR      TO LQW-ACTION (PV-SUB)             INKCS022
02114      END-EVALUATE.                                                INKCS022
02115                                                                   INKCS022
02116      SUBTRACT 1 FROM ASC-NUMBER-OF-SELECTED-ITEMS.                INKCS022
02117                                                                   INKCS022
02118 *----------------------------------------------------------------*INKCS022
02119 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS022
02120 *----------------------------------------------------------------*INKCS022
02121                                                                   INKCS022
02122  6111-WRITE-LIST-QUEUE.                                           INKCS022
02123                                                                   INKCS022
02124      EXEC CICS                                                    INKCS022
02125          WRITEQ TS                                                INKCS022
02126              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
02127              FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS022
02128              ITEM  (PV-SAVE-ITEM)                                 INKCS022
02129              REWRITE                                              INKCS022
02130              RESP  (PV-CICS-RESP)                                 INKCS022
02131      END-EXEC.                                                    INKCS022
02132 *                                                                 INKCS022
02133      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS022
02134          CONTINUE                                                 INKCS022
02135      ELSE                                                         INKCS022
02136          SET DP013-CICS-ABEND  TO  TRUE                           INKCS022
02137          SET DP013-NO-ROLLBACK TO  TRUE                           INKCS022
02138          MOVE '6111-WRITE-LIST-QUEUE'                             INKCS022
02139                                TO  DP013-PARAGRAPH                INKCS022
02140          MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS022
02141                                TO  DP013-MESSAGE-TEXT(1)          INKCS022
02142          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
02143      END-IF.                                                      INKCS022
02144                                                                   INKCS022
02145  EJECT                                                            INKCS022
02146 *----------------------------------------------------------------*INKCS022
02147 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS022
02148 *----------------------------------------------------------------*INKCS022
02149                                                                   INKCS022
02150  6112-READ-LIST-QUEUE.                                            INKCS022
02151                                                                   INKCS022
02152      EXEC CICS                                                    INKCS022
02153          READQ TS                                                 INKCS022
02154              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS022
02155              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS022
02156              ITEM  (PV-ITEM)                                      INKCS022
02157              RESP  (PV-CICS-RESP)                                 INKCS022
02158      END-EXEC.                                                    INKCS022
02159 *                                                                 INKCS022
02160      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS022
02161          CONTINUE                                                 INKCS022
02162      ELSE                                                         INKCS022
02163          SET DP013-CICS-ABEND        TO TRUE                      INKCS022
02164          SET DP013-NO-ROLLBACK       TO TRUE                      INKCS022
02165          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS022
02166          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS022
02167                                      TO DP013-MESSAGE-TEXT(1)     INKCS022
02168          PERFORM DP013-0000-PROCESS-ABEND                         INKCS022
02169      END-IF.                                                      INKCS022
02170                                                                   INKCS022
02171  EJECT                                                            INKCS022
02172 *----------------------------------------------------------------*INKCS022
02173 *    ABEND PROCESSOR MODULE                                      *INKCS022
02174 *----------------------------------------------------------------*INKCS022
02175                                                                   INKCS022
02176      COPY DPPD013.                                                INKCS022
