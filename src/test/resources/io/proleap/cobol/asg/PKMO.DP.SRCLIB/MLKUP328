       IDENTIFICATION DIVISION.                                         00010010
                                                                        00020010
       PROGRAM-ID.    MLKUP328.                                         00030010
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00040010
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050010
       DATE-WRITTEN.  6-24-99.                                          00060010
       DATE-COMPILED.                                                   00070010
                                                                        00080010
      ******************************************************************00090010
      *     MLKUP328 - MONTHLY DEPARTMENT PLAN TABLE UPDATE PROGRAM    *00100010
      ******************************************************************00110010
      *     THIS PROGRAM WILL MAINTAIN THE MONTHLY DEPARTMENT PLAN DB2 *00120010
      * TABLE WITH THE CURRENT MONTH'S DATA.  THE TABLE IS READ USING  *00130010
      * THE TABLE KEY FROM EACH INPUT FILE RECORD.  IF THE ROW IS      *00140010
      * FOUND, THE ROW IS UPDATED WITH DATA FROM THE INPUT FILE RECORD.*00150010
      * IF THE ROW IS NOT FOUND, A NEW ROW IS INSERTED ON THE DB2      *00160010
      * TABLE (TMNDPLN).  COMMITS ARE TAKEN EVERY TEN SECONDS, AND     *00170010
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                      *00180010
      *                                                                *00190010
      *    WR/PITS#     DATE        DESCRIPTION                        *00200010
      *    --------   --------     ---------------------------------   *00210010
      *    WR10422    09/10/99     ADDED RECEIPT ADJUSTMENTS           *00220010
      *    WR10422    01/07/00     ADDED BEGINNING INVENTORY           *00240010
      *                            COMP SALES AMOUNT AND CUM MARKUP    *00250010
      *                            PERCENT                             *00260010
      *    WR21517    10/02/00     ADDED COMP EOM INVENTORY DOLLAR     *00261011
      *                            FOR COMBINED LEDGER PHASE 6         *00262011
31355 *    WR31355    11/17/2000   ROD JANSSEN                         *00264014
31355 *    E-Commerce Combined Ledger Report Modification.......       *00265014
31355 *                                                                *00266014
      ******************************************************************00270010
                                                                        00290010
       ENVIRONMENT DIVISION.                                            00300010
                                                                        00310010
       CONFIGURATION SECTION.                                           00320010
       SOURCE-COMPUTER.        IBM-3090.                                00330010
       OBJECT-COMPUTER.        IBM-3090.                                00340010
       INPUT-OUTPUT SECTION.                                            00350010
       FILE-CONTROL.                                                    00360010
           SELECT EXTRACT-FILE                                          00370010
               ASSIGN TO INP01.                                         00380010
                                                                        00390010
                                                                        00400010
       DATA DIVISION.                                                   00410010
       FILE SECTION.                                                    00420010
                                                                        00430010
       FD  EXTRACT-FILE                                                 00440010
           LABEL RECORDS ARE STANDARD                                   00450010
           RECORDING MODE IS F                                          00460010
           BLOCK CONTAINS 0 RECORDS.                                    00470010
       01  EXTRACT-RECORD              PIC X(130).                      00480010
                                                                        00490014
                                                                        00510010
       WORKING-STORAGE SECTION.                                         00520010
                                                                        00530010
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00540010
                                                                        00550010
       01  PC-PROGRAM-CONSTANTS.                                        00560010
           05  PC-MAX-DEADLOCKS        PIC S9(04)    VALUE +3 COMP.     00570010
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK236'.    00580010
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP328'.  00590010
                                                                        00600010
       01  PC-PROGRAM-COUNTER.                                          00610010
           05  PC-UPDATE-CNT           PIC  9(09).                      00620010
           05  PC-DISPLAY-CNT          PIC  9(09).                      00630010
                                                                        00640010
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00650010
           05  PE-MSG-01                       PIC X(50) VALUE          00660010
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED   '.    00670010
           05  PE-MSG-02                       PIC X(50) VALUE          00680010
                'ATTEMPT TO UPDATE ROW ON TABLE TMNDPLN FAILED    '.    00690010
           05  PE-MSG-03                       PIC X(50) VALUE          00700010
                'ATTEMPT TO INSERT ROW ON TABLE TMNDPLN FAILED    '.    00710010
           05  PE-MSG-04                       PIC X(50) VALUE          00720010
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00730010
           05  PE-MSG-05                       PIC X(50) VALUE          00740010
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00750010
           05  PE-MSG-06                       PIC X(50) VALUE          00760010
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00770010
           05  PE-MSG-07                       PIC X(50) VALUE          00780010
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00790010
           05  PE-MSG-08                       PIC X(50) VALUE          00800010
                'AFTER DEADLOCK ERROR, AN UPDATE OR INSERT FAILED'.     00810010
           05  PE-MSG-09                       PIC X(50) VALUE          00820010
                'CHECKPNT RESTART FAILED UPON FIRST INSERT/UPDATE'.     00830010
           05  PE-MSG-10                       PIC X(50) VALUE          00840010
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00850010
           05  PE-MSG-11                       PIC X(50) VALUE          00860010
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00870010
      *                                                                 00880010
       01  PS-PROGRAM-SWITCHES.                                         00890010
           05  PS-INPUT-FILE-EOF-SW         PIC  X        VALUE 'N'.    00900010
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00910010
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00920010
           05  PS-EMPTY-FILE-SW             PIC  X        VALUE 'N'.    00930010
               88  PS-EMPTY-INPUT-FILE                    VALUE 'Y'.    00940010
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00950010
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00960010
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00970010
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00980010
           05  PS-PROCESSING-OPTIONS        PIC X         VALUE 'P'.    00990010
               88  PS-UPDATE-MNDPLN-ROW                   VALUE 'U'.    01000010
               88  PS-INSERT-MNDPLN-ROW                   VALUE 'I'.    01010010
      *                                                                 01020010
      *                                                                 01030010
       01  PROGRAM-VARIABLES.                                           01040010
           05  PV-COUNT                        PIC S9(09) VALUE +0 COMP.01050010
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0 COMP.01060010
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070010
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01080010
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01090010
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01100010
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01110010
           05  PV-SQLCODE-DISPLAY              PIC  ++++9 VALUE ZERO.   01120010
      *                                                                 01130010
      *                                                                 01140010
       01  PA-PROGRAM-ACCUMULATORS.                                     01150010
           05  PA-RECORD-COUNTS.                                        01160010
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01170010
               10  PA-UPD-TMNDPLN-COUNT        PIC  9(9)  VALUE ZEROES. 01180010
               10  PA-INS-TMNDPLN-COUNT        PIC  9(9)  VALUE ZEROES. 01190010
               10  PA-SEL-TMNDPLN-COUNT        PIC  9(9)  VALUE ZEROES. 01200010
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01210010
      *                                                                 01220010
       01  PD-PROGRAM-DISPLAYS.                                         01230010
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01240010
      *                                                                 01250010
      ******************************************************************01260010
      * MLRD124 - M/L MONTHLY DEPARTMENT PLAN DATABASE UPDATE TXN       01270010
      ******************************************************************01280010
31355      COPY MLRD124.                                                01290014
      *                                                                 01300010
      ******************************************************************01310010
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01320010
      ******************************************************************01330010
           COPY DPWS004.                                                01340010
      *                                                                 01350010
      ******************************************************************01360010
      *  USED FOR DB2 ERRORS                                            01370010
      ******************************************************************01380010
           EXEC SQL                                                     01390010
               INCLUDE SQLCA                                            01400010
           END-EXEC.                                                    01410010
      *                                                                 01420010
      ******************************************************************01430010
      *  M/L MONTHLY DEPARTMENT PLAN TABLE                              01440010
      ******************************************************************01450010
           EXEC SQL                                                     01460010
21517          INCLUDE TMNDPLN                                          01470011
           END-EXEC.                                                    01480010
      *                                                                 01490010
      ***************************************************************** 01500010
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01510010
      ***************************************************************** 01520010
           EXEC SQL                                                     01530010
               INCLUDE TCKRSTCN                                         01540010
           END-EXEC.                                                    01550010
      *                                                                 01560010
           EXEC SQL                                                     01570010
               INCLUDE TCKRSTIN                                         01580010
           END-EXEC.                                                    01590010
      *                                                                 01600010
      ******************************************************************01610010
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01620010
      ******************************************************************01630010
       01  CR-CHECKPOINT-RESTART-AREA.                                  01640010
           05  CR-AREA-LEN                  PIC S9(04) VALUE +62 COMP.  01650010
           05  CR-CHECKPOINT-RESTART-VAR.                               01660010
31355          10  CR-PREV-DTL-KEY          PIC  X(16) VALUE SPACES.    01670014
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01680010
                   15  CR-FISCAL-MN-NBR     PIC  X(02).                 01690010
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01700010
                   15  CR-DEPT-NBR          PIC  X(03).                 01710010
31355              15  CR-CHNL-CDE          PIC  X(01).                 01711014
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01720010
               10  CR-TMNDPLN-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.    01730010
               10  CR-UPD-TMNDPLN-COUNT     PIC  9(09) VALUE ZEROES.    01740010
               10  CR-INS-TMNDPLN-COUNT     PIC  9(09) VALUE ZEROES.    01750010
               10  CR-SEL-TMNDPLN-COUNT     PIC  9(09) VALUE ZEROES.    01760010
      *                                                                 01770010
       01  CK-CHECKPOINT-SAVE-AREA.                                     01780010
31355      05  CK-SAVE-PREV-KEY         PIC  X(16) VALUE SPACES.        01790014
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01800010
               10  CK-FISCAL-MN-NBR     PIC  X(02).                     01810010
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01820010
               10  CK-DEPT-NBR          PIC  X(03).                     01830010
31355          10  CK-CHNL-CDE          PIC  X(01).                     01831014
      *                                                                 01840010
      *                                                                 01850010
      *-------------------*                                             01860010
       PROCEDURE DIVISION.                                              01870010
      *-------------------*                                             01880010
                                                                        01890010
       0000-MAIN-MODULE.                                                01900010
                                                                        01910010
           PERFORM 1000-INITIALIZATION.                                 01920010
      *                                                                 01930010
           PERFORM 2000-PROCESS-INPUT                                   01940010
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         01950010
      *                                                                 01960010
           PERFORM 8000-EOJ-ROUTINE.                                    01970010
                                                                        01980010
           STOP RUN.                                                    01990010
      *                                                                 02000010
      *                                                                 02010010
      *                                                                 02020010
      ******************************************************************02030010
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02040010
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02050010
      ******************************************************************02060010
      *                                                                 02070010
       1000-INITIALIZATION.                                             02080010
      *                                                                 02090010
           OPEN INPUT EXTRACT-FILE.                                     02100010
      *                                                                 02110010
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02120010
      *                                                                 02130010
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02140010
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02150010
               PERFORM 7500-SELECT-RESTART-INFO                         02160010
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02170010
                                             CR-CHECKPOINT-RESTART-VAR  02180010
               MOVE CR-PREV-DTL-KEY         TO CK-SAVE-PREV-KEY         02190010
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02200010
               MOVE CR-UPD-TMNDPLN-COUNT    TO PA-UPD-TMNDPLN-COUNT     02210010
               MOVE CR-INS-TMNDPLN-COUNT    TO PA-INS-TMNDPLN-COUNT     02220010
               MOVE CR-SEL-TMNDPLN-COUNT    TO PA-SEL-TMNDPLN-COUNT     02230010
               MOVE CR-TMNDPLN-COMMIT-COUNT TO PA-COMMIT-COUNT          02240010
           ELSE                                                         02250010
               SET PS-FIRST-COMMIT TO TRUE                              02260010
           END-IF.                                                      02270010
      *                                                                 02280010
           DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                      02290010
                   TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'.             02300010
      *                                                                 02310010
           PERFORM 4000-READ-EXTRACT-FILE UNTIL                         02320010
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02330010
            OR PS-NO-MORE-INPUT-RECORDS.                                02340010
      *                                                                 02350010
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02360010
               DISPLAY 'FISCAL MONTH NBR      ' ML124-FSCL-MN-NBR       02370010
               DISPLAY 'FISCAL MONTH END DATE ' ML124-FSCL-MN-END-DTE   02380010
               DISPLAY 'DEPARTMENT NBR        ' ML124-DEPT-NBR          02390010
31355          DISPLAY 'CHANNEL CODE          ' ML124-CHNL-CDE          02391014
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              02400010
                        PA-INPUT-COUNT                                  02410010
           END-IF.                                                      02420010
                                                                        02430010
           IF PS-NO-MORE-INPUT-RECORDS                                  02440010
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02450010
                   CONTINUE                                             02460010
               ELSE                                                     02470010
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02480010
           ELSE                                                         02490010
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02500010
           END-IF.                                                      02510010
      *                                                                 02520010
      *                                                                 02530010
      ******************************************************************02540010
      * PROCESS THE INPUT EXTRACT FILE - UPDATE OR INSERT AS NEEDED     02550010
      ******************************************************************02560010
       2000-PROCESS-INPUT.                                              02570010
      *                                                                 02580010
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02590010
      *                                                                 02600010
           MOVE ML124-FSCL-MN-NBR     TO MNDPLN-FSCL-MN-NBR.            02610010
           MOVE ML124-FSCL-MN-END-DTE TO MNDPLN-FSCL-MN-END-DTE.        02620010
           MOVE ML124-DEPT-NBR        TO MNDPLN-DEPT-NBR.               02630010
31355      MOVE ML124-CHNL-CDE        TO MNDPLN-CHNL-CDE.               02631014
           MOVE ML124-SLS-RTL-AMT     TO MNDPLN-SLS-RTL-AMT.            02640010
           MOVE ML124-MKDN-RTL-AMT    TO MNDPLN-MKDN-RTL-AMT.           02650010
           MOVE ML124-RCPT-RTL-AMT    TO MNDPLN-RCPT-RTL-AMT.           02660010
           MOVE ML124-RCPT-NET-COST-AMT TO MNDPLN-RCPT-NET-COST-AMT.    02670010
           MOVE ML124-SHNK-RTL-AMT      TO MNDPLN-SHNK-RTL-AMT.         02680010
           MOVE ML124-END-INV-RTL-AMT   TO MNDPLN-END-INV-RTL-AMT.      02690010
           MOVE ML124-MKUP-PCT        TO MNDPLN-MKUP-PCT.               02700010
           MOVE ML124-SLS-COST-AMT    TO MNDPLN-SLS-COST-AMT.           02710010
           MOVE ML124-GRO-MRGN-AMT    TO MNDPLN-GRO-MRGN-AMT.           02720010
           MOVE ML124-GRO-MRGN-PCT    TO MNDPLN-GRO-MRGN-PCT.           02730010
           MOVE ML124-PADJ-RTL-AMT    TO MNDPLN-PADJ-RTL-AMT.           02740010
           MOVE ML124-PADJ-NET-COST-AMT                                 02750010
                                      TO MNDPLN-PADJ-NET-COST-AMT.      02760010
           MOVE ML124-BEG-INV-RTL-AMT TO MNDPLN-BEG-INV-RTL-AMT.        02770010
           MOVE ML124-CMPR-STR-SLS-AMT                                  02780010
                                      TO MNDPLN-CMPR-STR-SLS-AMT.       02790010
           MOVE ML124-CUM-MKUP-PCT    TO MNDPLN-CUM-MKUP-PCT.           02800010
21517      MOVE ML124-CMPR-END-INV-AMT                                  02801012
21517                                 TO MNDPLN-CMPR-END-INV-AMT.       02802012
      *                                                                 02810010
           PERFORM 6000-SELECT-MNDPLN-ROW.                              02820010
      *                                                                 02830010
           IF PS-PROGRAM-DEADLOCKED                                     02840010
               CONTINUE                                                 02850010
           ELSE                                                         02860010
               EVALUATE TRUE                                            02870010
                   WHEN PS-UPDATE-MNDPLN-ROW                            02880010
                       PERFORM 6100-UPDATE-MNDPLN-ROW                   02890010
                   WHEN PS-INSERT-MNDPLN-ROW                            02900010
                       PERFORM 6200-INSERT-MNDPLN-ROW                   02910010
               END-EVALUATE                                             02920010
           END-IF.                                                      02930010
      *                                                                 02940010
           IF PS-PROGRAM-DEADLOCKED                                     02950010
               CONTINUE                                                 02960010
           ELSE                                                         02970010
               PERFORM 7700-COMMIT-PROCESSING                           02980010
               PERFORM 4000-READ-EXTRACT-FILE                           02990010
           END-IF.                                                      03000010
      *                                                                 03010010
      *                                                                 03020010
      ******************************************************************03030010
      * READS THE CONDENSED FILE INTO THE TMNDPLN LAYOUT                03040010
      ******************************************************************03050010
       4000-READ-EXTRACT-FILE.                                          03060010
           READ EXTRACT-FILE INTO ML124-MNTHLY-PLN-EXTRACT-REC          03070010
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW.                 03080010
      *                                                                 03090010
           IF PS-NO-MORE-INPUT-RECORDS                                  03100010
               MOVE ML124-FSCL-MN-NBR         TO CR-FISCAL-MN-NBR       03110010
               MOVE ML124-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03120010
               MOVE ML124-DEPT-NBR            TO CR-DEPT-NBR            03130010
31355          MOVE ML124-CHNL-CDE            TO CR-CHNL-CDE            03131014
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03140010
               MOVE PA-UPD-TMNDPLN-COUNT      TO CR-UPD-TMNDPLN-COUNT   03150010
               MOVE PA-INS-TMNDPLN-COUNT      TO CR-INS-TMNDPLN-COUNT   03160010
               MOVE PA-SEL-TMNDPLN-COUNT      TO CR-SEL-TMNDPLN-COUNT   03170010
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03180010
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03190010
                                             TCKRSTINF-WORK-STRG-DESC   03200010
               EXEC SQL                                                 03210010
                 UPDATE TCKRSTINF                                       03220010
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03230010
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03240010
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03250010
               END-EXEC                                                 03260010
               IF SQLCODE = 0                                           03270010
                   CONTINUE                                             03280010
               ELSE                                                     03290010
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED  03300010
                   MOVE '* 4000-READ-EXTRACT-FILE *' TO                 03310010
                        PV-PARAGRAPH-NAME                               03320010
                   PERFORM 9999-SQL-ABEND                               03330010
               END-IF                                                   03340010
           ELSE                                                         03350010
               ADD 1                         TO PA-INPUT-COUNT          03360010
                                                PC-UPDATE-CNT           03370010
               IF PC-UPDATE-CNT > 100000                                03380010
                    ADD 1                    TO PC-DISPLAY-CNT          03390010
                    DISPLAY 'UPDATE COUNT: ' PC-DISPLAY-CNT             03400010
                    MOVE +0                  TO PC-UPDATE-CNT           03410010
               END-IF                                                   03420010
           END-IF.                                                      03430010
      *                                                                 03440010
      *                                                                 03450010
      ******************************************************************03460010
      * SELECT A ROW FROM THE MONTHLY DEPT PLAN TABLE THAT MATCHES INPUT03470010
      *  KEY SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT   03480010
      ******************************************************************03490010
       6000-SELECT-MNDPLN-ROW.                                          03500010
      *                                                                 03510010
           EXEC SQL                                                     03520010
               SELECT 1                                                 03530010
                 INTO :PV-COUNT                                         03540010
               FROM TMNDPLN                                             03550010
              WHERE   FSCL_MN_NBR         = :MNDPLN-FSCL-MN-NBR         03560010
                AND   FSCL_MN_END_DTE     = :MNDPLN-FSCL-MN-END-DTE     03570010
                AND   DEPT_NBR            = :MNDPLN-DEPT-NBR            03580010
31355           AND   CHNL_CDE            = :MNDPLN-CHNL-CDE            03581014
           END-EXEC.                                                    03590010
      *                                                                 03600010
           EVALUATE SQLCODE                                             03610010
               WHEN 0                                                   03620010
                   MOVE ZERO                   TO PV-DEADLOCK-COUNT     03630010
                   SET PS-UPDATE-MNDPLN-ROW    TO TRUE                  03640010
                   ADD +1                      TO PA-SEL-TMNDPLN-COUNT  03650010
               WHEN +100                                                03660010
                   MOVE ZERO                   TO PV-DEADLOCK-COUNT     03670010
                   SET PS-INSERT-MNDPLN-ROW    TO TRUE                  03680010
               WHEN -911                                                03690010
                   ADD +1             TO PV-DEADLOCK-COUNT              03700010
                   DISPLAY 'DEADLOCK -911 IN 6000-SELECT-MNDPLN-ROW'    03710010
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03720010
                       MOVE '6000-SELECT-MNDPLN-ROW'                    03730010
                                                 TO PV-PARAGRAPH-NAME   03740010
                       PERFORM 9000-DEADLOCK-ERROR                      03750010
                   ELSE                                                 03760010
                       PERFORM 7999-RESTART-PROCESS                     03770010
                   END-IF                                               03780010
               WHEN OTHER                                               03790010
                   MOVE '6000-SELECT-MNDPLN-ROW' TO PV-PARAGRAPH-NAME   03800010
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03810010
                   PERFORM 9999-SQL-ABEND                               03820010
           END-EVALUATE.                                                03830010
      *                                                                 03840010
      *                                                                 03850010
      ***************************************************************** 03860010
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO AND    03870010
      * DEPARTMENT.  CURRENT TABLE VALUES ARE UPDATED WITH THE VALUES   03880010
      * FROM THE INPUT RECORD.                                          03890010
      ***************************************************************** 03900010
       6100-UPDATE-MNDPLN-ROW.                                          03910010
      *                                                                 03920010
      *                                                                 03930010
           EXEC SQL                                                     03940010
               UPDATE TMNDPLN                                           03950010
                   SET SLS_RTL_AMT        = :MNDPLN-SLS-RTL-AMT         03960010
                     , MKDN_RTL_AMT       = :MNDPLN-MKDN-RTL-AMT        03970010
                     , RCPT_RTL_AMT       = :MNDPLN-RCPT-RTL-AMT        03980010
                     , RCPT_NET_COST_AMT  = :MNDPLN-RCPT-NET-COST-AMT   03990010
                     , SHNK_RTL_AMT       = :MNDPLN-SHNK-RTL-AMT        04000010
                     , END_INV_RTL_AMT    = :MNDPLN-END-INV-RTL-AMT     04010010
                     , MKUP_PCT           = :MNDPLN-MKUP-PCT            04020010
                     , SLS_COST_AMT       = :MNDPLN-SLS-COST-AMT        04030010
                     , GRO_MRGN_AMT       = :MNDPLN-GRO-MRGN-AMT        04040010
                     , GRO_MRGN_PCT       = :MNDPLN-GRO-MRGN-PCT        04050010
                     , PADJ_RTL_AMT       = :MNDPLN-PADJ-RTL-AMT        04060010
                     , PADJ_NET_COST_AMT  = :MNDPLN-PADJ-NET-COST-AMT   04070010
                     , BEG_INV_RTL_AMT    = :MNDPLN-BEG-INV-RTL-AMT     04080010
                     , CMPR_STR_SLS_AMT   = :MNDPLN-CMPR-STR-SLS-AMT    04090010
                     , CUM_MKUP_PCT       = :MNDPLN-CUM-MKUP-PCT        04100010
21517                , CMPR_END_INV_AMT   = :MNDPLN-CMPR-END-INV-AMT    04101013
              WHERE   FSCL_MN_NBR         = :MNDPLN-FSCL-MN-NBR         04110010
                AND   FSCL_MN_END_DTE     = :MNDPLN-FSCL-MN-END-DTE     04120010
                AND   DEPT_NBR            = :MNDPLN-DEPT-NBR            04130010
31355           AND   CHNL_CDE            = :MNDPLN-CHNL-CDE            04131014
           END-EXEC.                                                    04140010
      *                                                                 04150010
           EVALUATE SQLCODE                                             04160010
               WHEN 0                                                   04170010
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04180010
                   ADD 1              TO PA-UPD-TMNDPLN-COUNT           04190010
               WHEN -911                                                04200010
                   ADD +1             TO PV-DEADLOCK-COUNT              04210010
                   DISPLAY 'DEADLOCK -911 IN 6100-UPDATE-MNDPLN-ROW'    04220010
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04230010
                       MOVE '6100-UPDATE-MNDPLN-ROW'                    04240010
                                      TO PV-PARAGRAPH-NAME              04250010
                       PERFORM 9000-DEADLOCK-ERROR                      04260010
                   ELSE                                                 04270010
                       PERFORM 7999-RESTART-PROCESS                     04280010
                   END-IF                                               04290010
               WHEN OTHER                                               04300010
                   MOVE '6100-UPDATE-MNDPLN-ROW'                        04310010
                                      TO PV-PARAGRAPH-NAME              04320010
                   MOVE PE-MSG-02     TO PV-OPERATION-ATTEMPTED         04330010
                   PERFORM 9999-SQL-ABEND                               04340010
           END-EVALUATE.                                                04350010
      *                                                                 04360010
      *                                                                 04370010
      ***************************************************************** 04380010
      * THERE WAS NO MATCHING ROW ON THE MONTHLY DEPT PLAN TABLE.     * 04390010
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04400010
      ***************************************************************** 04410010
       6200-INSERT-MNDPLN-ROW.                                          04420010
      *                                                                 04430010
      *                                                                 04440010
           EXEC SQL                                                     04450010
               INSERT INTO TMNDPLN                                      04460010
                   (   FSCL_MN_NBR                                      04470010
                     , FSCL_MN_END_DTE                                  04480010
                     , DEPT_NBR                                         04490010
31355                , CHNL_CDE                                         04491014
                     , SLS_RTL_AMT                                      04500010
                     , MKDN_RTL_AMT                                     04510010
                     , RCPT_RTL_AMT                                     04520010
                     , RCPT_NET_COST_AMT                                04530010
                     , SHNK_RTL_AMT                                     04540010
                     , END_INV_RTL_AMT                                  04550010
                     , MKUP_PCT                                         04560010
                     , SLS_COST_AMT                                     04570010
                     , GRO_MRGN_AMT                                     04580010
                     , GRO_MRGN_PCT                                     04590010
                     , PADJ_RTL_AMT                                     04600010
                     , PADJ_NET_COST_AMT                                04610010
                     , BEG_INV_RTL_AMT                                  04620010
                     , CMPR_STR_SLS_AMT                                 04630010
21517                , CUM_MKUP_PCT                                     04640013
21517                , CMPR_END_INV_AMT)                                04641013
               VALUES                                                   04650010
                   (   :MNDPLN-FSCL-MN-NBR                              04660010
                     , :MNDPLN-FSCL-MN-END-DTE                          04670010
                     , :MNDPLN-DEPT-NBR                                 04680010
31355                , :MNDPLN-CHNL-CDE                                 04681014
                     , :MNDPLN-SLS-RTL-AMT                              04690010
                     , :MNDPLN-MKDN-RTL-AMT                             04700010
                     , :MNDPLN-RCPT-RTL-AMT                             04710010
                     , :MNDPLN-RCPT-NET-COST-AMT                        04720010
                     , :MNDPLN-SHNK-RTL-AMT                             04730010
                     , :MNDPLN-END-INV-RTL-AMT                          04740010
                     , :MNDPLN-MKUP-PCT                                 04750010
                     , :MNDPLN-SLS-COST-AMT                             04760010
                     , :MNDPLN-GRO-MRGN-AMT                             04770010
                     , :MNDPLN-GRO-MRGN-PCT                             04780010
                     , :MNDPLN-PADJ-RTL-AMT                             04790010
                     , :MNDPLN-PADJ-NET-COST-AMT                        04800010
                     , :MNDPLN-BEG-INV-RTL-AMT                          04810010
                     , :MNDPLN-CMPR-STR-SLS-AMT                         04820010
21517                , :MNDPLN-CUM-MKUP-PCT                             04830013
21517                , :MNDPLN-CMPR-END-INV-AMT)                        04831013
           END-EXEC.                                                    04840010
      *                                                                 04850010
           EVALUATE SQLCODE                                             04860010
               WHEN 0                                                   04870010
                   MOVE ZERO          TO PV-DEADLOCK-COUNT              04880010
                   ADD 1              TO PA-INS-TMNDPLN-COUNT           04890010
               WHEN -911                                                04900010
                   ADD +1             TO PV-DEADLOCK-COUNT              04910010
                   DISPLAY 'DEADLOCK -911 IN 6200-INSERT-MNDPLN-ROW'    04920010
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04930010
                       MOVE '6200-INSERT-MNDPLN-ROW' TO                 04940010
                                                    PV-PARAGRAPH-NAME   04950010
                       PERFORM 9000-DEADLOCK-ERROR                      04960010
                   ELSE                                                 04970010
                       PERFORM 7999-RESTART-PROCESS                     04980010
                   END-IF                                               04990010
               WHEN OTHER                                               05000010
                   MOVE '6200-INSERT-MNDPLN-ROW' TO PV-PARAGRAPH-NAME   05010010
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05020010
                   PERFORM 9999-SQL-ABEND                               05030010
           END-EVALUATE.                                                05040010
      *                                                                 05050010
      *                                                                 05060010
      ******************************************************************05070010
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05080010
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05090010
      *            CONTROL TABLE                                       *05100010
      ******************************************************************05110010
       7300-GET-COMMIT-TIMESTAMP.                                       05120010
                                                                        05130010
           EXEC SQL                                                     05140010
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05150010
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05160010
               INTO :PV-COMMIT-TIMESTAMP                                05170010
               FROM TCKRSTCNTL                                          05180010
              WHERE JOB_NAME  = :PC-JOB-NAME                            05190010
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05200010
           END-EXEC.                                                    05210010
                                                                        05220010
           IF SQLCODE = 0                                               05230010
               CONTINUE                                                 05240010
           ELSE                                                         05250010
               MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME    05260010
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED     05270010
               PERFORM 9999-SQL-ABEND                                   05280010
           END-IF.                                                      05290010
      *                                                                 05300010
      *                                                                 05310010
      ******************************************************************05320010
      * 7400-INIT-CHECKPOINT-SELECT                                    *05330010
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05340010
      *            IF WE ARE IN A RESTART CONDITION.                   *05350010
      ******************************************************************05360010
       7400-INIT-CHECKPOINT-SELECT.                                     05370010
                                                                        05380010
           EXEC SQL                                                     05390010
             SELECT  CKPT_STAT_CODE                                     05400010
                    ,CKPT_FRQNCY_QTY                                    05410010
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05420010
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05430010
               FROM  TCKRSTCNTL                                         05440010
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05450010
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05460010
           END-EXEC.                                                    05470010
                                                                        05480010
           IF SQLCODE = 0                                               05490010
               CONTINUE                                                 05500010
           ELSE                                                         05510010
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05520010
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05530010
               PERFORM 9999-SQL-ABEND                                   05540010
           END-IF.                                                      05550010
      *                                                                 05560010
      *                                                                 05570010
      ******************************************************************05580010
      * 7500-SELECT-RESTART-INFO                                       *05590010
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05600010
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05610010
      ******************************************************************05620010
       7500-SELECT-RESTART-INFO.                                        05630010
                                                                        05640010
           EXEC SQL                                                     05650010
             SELECT  WORK_STRG_DESC                                     05660010
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05670010
               FROM  TCKRSTINF                                          05680010
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05690010
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05700010
           END-EXEC.                                                    05710010
                                                                        05720010
           IF SQLCODE = 0                                               05730010
               CONTINUE                                                 05740010
           ELSE                                                         05750010
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05760010
               PERFORM 9999-SQL-ABEND                                   05770010
           END-IF.                                                      05780010
      *                                                                 05790010
      *                                                                 05800010
      ******************************************************************05810010
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05820010
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05830010
      ******************************************************************05840010
       7600-SET-CURRENT-TIMESTAMP.                                      05850010
                                                                        05860010
           EXEC SQL                                                     05870010
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           05880010
           END-EXEC.                                                    05890010
      *                                                                 05900010
           IF SQLCODE = 0                                               05910010
               CONTINUE                                                 05920010
           ELSE                                                         05930010
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 05940010
               PERFORM 9999-SQL-ABEND                                   05950010
           END-IF.                                                      05960010
      *                                                                 05970010
      *                                                                 05980010
      ******************************************************************05990010
      * 7700-COMMIT-PROCESSING.                                        *06000010
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06010010
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06020010
      ******************************************************************06030010
       7700-COMMIT-PROCESSING.                                          06040010
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06050010
                                                                        06060010
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06070010
      *                                                                 06080010
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06090010
      *        PREPARE COMMIT RECORD FOR UPDATE                         06100010
               ADD 1                          TO PA-COMMIT-COUNT        06110010
               MOVE ML124-FSCL-MN-NBR         TO CR-FISCAL-MN-NBR       06120010
               MOVE ML124-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   06130010
               MOVE ML124-DEPT-NBR            TO CR-DEPT-NBR            06140010
31355          MOVE ML124-CHNL-CDE            TO CR-CHNL-CDE            06141014
               MOVE PA-COMMIT-COUNT           TO CR-TMNDPLN-COMMIT-COUNT06150010
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06160010
               MOVE PA-UPD-TMNDPLN-COUNT      TO CR-UPD-TMNDPLN-COUNT   06170010
               MOVE PA-INS-TMNDPLN-COUNT      TO CR-INS-TMNDPLN-COUNT   06180010
               MOVE PA-SEL-TMNDPLN-COUNT      TO CR-SEL-TMNDPLN-COUNT   06190010
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06200010
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06210010
                                             TCKRSTINF-WORK-STRG-DESC   06220010
               IF PS-FIRST-COMMIT                                       06230010
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06240010
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06250010
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06260010
               END-IF                                                   06270010
               PERFORM 7800-COMMIT-CHANGES                              06280010
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06290010
           END-IF.                                                      06300010
      *                                                                 06310010
      *                                                                 06320010
      ******************************************************************06330010
      * 7800-COMMIT-CHANGES.                                            06340010
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06350010
      *            TAKE A COMMIT.                                       06360010
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06370010
      ******************************************************************06380010
       7800-COMMIT-CHANGES.                                             06390010
                                                                        06400010
           EXEC SQL                                                     06410010
             UPDATE TCKRSTINF                                           06420010
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06430010
              WHERE JOB_NAME       = :PC-JOB-NAME                       06440010
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06450010
           END-EXEC.                                                    06460010
                                                                        06470010
           IF SQLCODE = 0                                               06480010
               CONTINUE                                                 06490010
           ELSE                                                         06500010
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06510010
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06520010
               PERFORM 9999-SQL-ABEND                                   06530010
           END-IF.                                                      06540010
                                                                        06550010
           EXEC SQL                                                     06560010
               COMMIT                                                   06570010
           END-EXEC.                                                    06580010
                                                                        06590010
           IF SQLCODE = 0                                               06600010
               CONTINUE                                                 06610010
           ELSE                                                         06620010
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06630010
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06640010
               PERFORM 9999-SQL-ABEND                                   06650010
           END-IF.                                                      06660010
      *                                                                 06670010
      *                                                                 06680010
      ******************************************************************06690010
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06700010
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06710010
      *                                                                 06720010
      ******************************************************************06730010
       7900-UPDATE-CHECKPOINT-STATUS.                                   06740010
                                                                        06750010
           EXEC SQL                                                     06760010
             UPDATE  TCKRSTCNTL                                         06770010
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06780010
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06790010
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06800010
           END-EXEC.                                                    06810010
                                                                        06820010
           IF SQLCODE = 0                                               06830010
               CONTINUE                                                 06840010
           ELSE                                                         06850010
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06860010
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED06870010
               PERFORM 9999-SQL-ABEND                                   06880010
           END-IF.                                                      06890010
                                                                        06900010
           EJECT                                                        06910010
      *                                                                 06920010
      *                                                                 06930010
      ******************************************************************06940010
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNDPLN ROW FOR UPDATE06950010
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  06960010
      ******************************************************************06970010
       7999-RESTART-PROCESS.                                            06980010
      *                                                                 06990010
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07000010
      *                                                                 07010010
           CLOSE EXTRACT-FILE.                                          07020010
      *                                                                 07030010
           PERFORM 7500-SELECT-RESTART-INFO.                            07040010
      *                                                                 07050010
           DISPLAY '**** RESTART **** '.                                07060010
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07070010
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07080010
      *                                                                 07090010
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07100010
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07110010
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07120010
           IF PS-FIRST-COMMIT                                           07130010
               INITIALIZE TCKRSTINF-WORK-STRG-DESC                      07140010
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07150010
                      ' BEGINNING'                                      07160010
           ELSE                                                         07170010
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07180010
                        TCKRSTINF-WORK-STRG-DESC (1:62)                 07190010
      ***    INFO ON LAST CHECKPOINT TAKEN IS IN                        07200010
      ***    CR-CHECKPOINT-RESTART-AREA.                                07210010
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07220010
                     CR-CHECKPOINT-RESTART-AREA                         07230010
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07240010
               DISPLAY 'MONTH NBR       ' CR-FISCAL-MN-NBR              07250010
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          07260010
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07270010
31355          DISPLAY 'CHANNEL CODE    ' CR-CHNL-CDE                   07271014
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07280010
           END-IF.                                                      07290010
      *                                                                 07300010
           OPEN INPUT EXTRACT-FILE.                                     07310010
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07320010
      *                                                                 07330010
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07340010
           PERFORM 4000-READ-EXTRACT-FILE                               07350010
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07360010
                 OR PS-NO-MORE-INPUT-RECORDS.                           07370010
           IF PS-NO-MORE-INPUT-RECORDS                                  07380010
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07390010
           ELSE                                                         07400010
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07410010
                        PA-INPUT-COUNT                                  07420010
               DISPLAY 'FISCAL MONTH NBR      ' ML124-FSCL-MN-NBR       07430010
               DISPLAY 'FISCAL MONTH END DATE ' ML124-FSCL-MN-END-DTE   07440010
               DISPLAY 'DEPARTMENT NBR        ' ML124-DEPT-NBR          07450010
31355          DISPLAY 'CHANNEL CODE          ' ML124-CHNL-CDE          07451014
           END-IF.                                                      07460010
      *                                                                 07470010
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07480010
           MOVE CR-UPD-TMNDPLN-COUNT    TO PA-UPD-TMNDPLN-COUNT.        07490010
           MOVE CR-INS-TMNDPLN-COUNT    TO PA-INS-TMNDPLN-COUNT.        07500010
           MOVE CR-SEL-TMNDPLN-COUNT    TO PA-SEL-TMNDPLN-COUNT.        07510010
           MOVE CR-TMNDPLN-COMMIT-COUNT TO PA-COMMIT-COUNT.             07520010
      *                                                                 07530010
      *                                                                 07540010
      ******************************************************************07550010
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07560010
      ******************************************************************07570010
       8000-EOJ-ROUTINE.                                                07580010
      *                                                                 07590010
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07600010
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07610010
      *                                                                 07620010
           DISPLAY '                                             '.     07630010
           DISPLAY 'INPUT RECORDS   ', PA-INPUT-COUNT.                  07640010
           DISPLAY 'SELECT TMNDPLN  ', PA-SEL-TMNDPLN-COUNT.            07650010
           DISPLAY 'UPDATE TMNDPLN  ', PA-UPD-TMNDPLN-COUNT.            07660010
           DISPLAY 'INSERT TMNDPLN  ', PA-INS-TMNDPLN-COUNT.            07670010
           DISPLAY '                                             '.     07680010
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                 07690010
      *                                                                 07700010
           CLOSE EXTRACT-FILE.                                          07710010
      *                                                                 07720010
      *                                                                 07730010
      ******************************************************************07740010
      *  PERFORMED BY 6100-UPDATE AND 7200-INSERT                       07750010
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07760010
      ******************************************************************07770010
       9000-DEADLOCK-ERROR.                                             07780010
      *                                                                 07790010
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07800010
               PERFORM 9999-SQL-ABEND.                                  07810010
      *                                                                 07820010
      ******************************************************************07830010
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07840010
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07850010
      ******************************************************************07860010
       9999-SQL-ABEND.                                                  07870010
                                                                        07880010
           MOVE +4013                 TO ABEND-CODE.                    07890010
           MOVE SQLCODE               TO PV-SQLCODE-DISPLAY.            07900010
           DISPLAY '**  ABEND     **'.                                  07910010
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07920010
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07930010
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07940010
           DISPLAY '**  SQLCODE   **  = ' PV-SQLCODE-DISPLAY.           07950010
                                                                        07960010
           EXEC SQL                                                     07970010
                ROLLBACK                                                07980010
           END-EXEC.                                                    07990010
                                                                        08000010
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08010010
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08020010
                                                                        08030010
           COPY DPPD004.                                                08040010
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08050010
