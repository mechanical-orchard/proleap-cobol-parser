       IDENTIFICATION DIVISION.                                         00010033
       PROGRAM-ID.    IMKUT061.                                         00020033
       AUTHOR.        R. MCDONALD                                       00030033
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040033
       DATE-WRITTEN.  10/06/2004.                                       00050033
       DATE-COMPILED.                                                   00060033
                                                                        00070033
      ******************************************************************00080033
      * IMKUT061 - CREATE NCC PERMANENT PRICE CHANGE UPDATES            00090033
      ******************************************************************00100033
      * THIS PROGRAM WILL CREATE NON-CONTINUED CLEARANCE PERM PRICE     00110033
      * CHANGE UPDATE RECORDS FROM THE XMT NCC PERMANENT PRICE CHANGES  00120033
      * EXTRACT FILE.                                                   00130033
      *                                                                 00140033
      * THE INPUT FILE TO THIS PROGRAM MUST BE SORTED BY SKU NUMBER,    00150033
      * UNIT RETAIL AMOUNT(DESCENDING), AND STORE.                      00160033
      *                                                                 00170033
      ******************************************************************00180033
      *  WR28048   05/03/05 P. KEBER    ADDED LOGIC TO ACCESS TSKXREF  *00181033
      *  TO OBTAIN INTERNAL UPC ID FOR EACH SKU.                       *00182033
      ******************************************************************00182133
      *  WR29434   06/28/06 P. KEBER    ADDED NEW PERM PRICE CHANGES   *00182233
      *  RECORD COUNT OUTPUT FILE.                                     *00182333
      ******************************************************************00182433
      *  WR29434   12/20/06 P. KEBER    CREATE A CORPORATE LEVEL STORE *00182533
      *  RECORD FOR MK2 AND FNL PRICE CHANGE TYPES IN ADDITION TO RCP  *00182634
      *  PRICE CHANGE TYPES IF ALL-RMNG-STR-IND IS SET TO "Y" ONLY IF  *00182734
      *  THE SKU IS STATUS 30.  SET THE NEW ALL-SAME-PRICE-IND FOR RCP *00182834
      *  (ACTIVE TO CLEARANCE ONLY), MK2 OR FNL PRICE CHANGES IF ALL   *00182934
      *  STORES HAVE THE SAME PRICE.  CHECK EXISTING TUPCPLS ROWS FOR  *00183034
      *  THE SAME PRICE ON FNL PRICE CHGS.                             *00183134
      ******************************************************************00183334
                                                                        00183634
      ******************************************************************00183734
       ENVIRONMENT DIVISION.                                            00183834
      ******************************************************************00183934
                                                                        00184034
       CONFIGURATION SECTION.                                           00184134
       SOURCE-COMPUTER.    IBM-3090.                                    00184234
       OBJECT-COMPUTER.    IBM-3090.                                    00184334
                                                                        00184434
       INPUT-OUTPUT SECTION.                                            00185033
       FILE-CONTROL.                                                    00186033
                                                                        00187033
           SELECT IN-PRICE-CHANGE-FILE                                  00188033
               ASSIGN TO INP01.                                         00189033
                                                                        00190033
           SELECT OUT-PRICE-CHANGE-FILE                                 00200033
               ASSIGN TO OUT01.                                         00210033
                                                                        00220033
           SELECT PRICE-CHANGES-COUNT-FILE                              00230033
               ASSIGN TO OUT02.                                         00240033
                                                                        00250033
      ******************************************************************00260033
       DATA DIVISION.                                                   00270033
      ******************************************************************00280033
                                                                        00290033
       FILE SECTION.                                                    00300033
                                                                        00310033
       FD  IN-PRICE-CHANGE-FILE                                         00320033
           RECORDING MODE IS F                                          00330033
           BLOCK CONTAINS 0 RECORDS.                                    00340033
       01  IN-PRICE-CHANGE-REC     PIC X(48).                           00350033
                                                                        00360033
       FD  OUT-PRICE-CHANGE-FILE                                        00370033
           RECORDING MODE IS F                                          00380033
           BLOCK CONTAINS 0 RECORDS.                                    00390033
       01  OUT-PRICE-CHANGE-REC    PIC X(56).                           00400033
                                                                        00410033
       FD  PRICE-CHANGES-COUNT-FILE                                     00420033
           RECORDING MODE IS F                                          00430033
           BLOCK CONTAINS 0 RECORDS.                                    00440033
       01  PRICE-CHANGES-COUNT-REC PIC X(9).                            00450033
                                                                        00460033
                                                                        00470033
       WORKING-STORAGE SECTION.                                         00480033
                                                                        00490033
       01  PA-PROGRAM-ACCUMULATORS.                                     00500033
           05  PA-RETRY-COUNT              PIC S9(04)  VALUE +0 COMP.   00501033
           05  PA-READ-COUNT               PIC 9(09)   VALUE ZERO.      00502033
           05  PA-WRITE-COUNT              PIC 9(09)   VALUE ZERO.      00503033
           05  PA-STORE-ZERO-COUNT         PIC 9(09)   VALUE ZERO.      00504033
           05  PA-STORE-SPECIFIC-COUNT     PIC 9(09)   VALUE ZERO.      00505033
           05  PA-COUNT                    PIC S9(07)  VALUE +0 COMP-3. 00506033
                                                                        00507033
       01  PC-PROGRAM-CONSTANTS.                                        00508033
           05  PC-PROGRAM-NAME             PIC X(08)   VALUE 'IMKUT061'.00509033
           05  PC-MAX-RETRIES              PIC S9(04)  VALUE +3 COMP.   00510033
           05  PC-N                        PIC X(01)   VALUE 'N'.       00520033
           05  PC-Y                        PIC X(01)   VALUE 'Y'.       00530033
           05  PC-CLEARANCE                PIC X(02)   VALUE '30'.      00540033
           05  PC-MIXED                    PIC X(02)   VALUE '25'.      00550033
                                                                        00560033
       01  PS-PROGRAM-SWITCHES.                                         00570033
           05  PS-END-OF-INP01-FILE-SW     PIC X(01)   VALUE 'N'.       00580033
               88  END-OF-INP01-FILE                   VALUE 'Y'.       00590033
           05  PS-CORP-LVL-REC-SW          PIC X(01)   VALUE 'N'.       00600033
               88  CORP-LVL-REC-CREATED                VALUE 'Y'.       00610033
               88  CORP-LVL-REC-NOT-CREATED            VALUE 'N'.       00620033
           05  PS-CURRENT-ROW-FOUND-SW     PIC X(01)   VALUE 'N'.       00630033
               88  PS-CURRENT-ROW-FOUND                VALUE 'Y'.       00640033
               88  PS-CURRENT-ROW-NOT-FOUND            VALUE 'N'.       00650033
           05  PS-ALL-STORES-SAME-PRICE-SW PIC X(01)   VALUE 'N'.       00660033
               88  PS-ALL-STORES-HAVE-SAME-PRICE       VALUE 'Y'.       00670033
               88  PS-ALL-STORES-NOT-SAME-PRICE        VALUE 'N'.       00680033
                                                                        00690033
       01  PV-PROGRAM-VARIABLES.                                        00700033
           05  PV-ONE                      PIC S9(04)  VALUE +0 COMP.   00710033
           05  PV-PARA-NAME                PIC X(35)   VALUE SPACES.    00720033
           05  PV-PREV-PUR-SKU             PIC S9(08)  VALUE +0 COMP-3. 00730033
           05  PV-SAVE-PUR-NEW-UNT-RTL-AMT PIC S9(07)V99                00740033
                                                       VALUE +0 COMP-3. 00741033
           05  PV-DB2-OPERATION-ATTEMPTED  PIC X(35)   VALUE SPACES.    00742033
           05  PV-NUM-DISPLAY              PIC Z(8)9   VALUE ZERO.      00743033
           05  PV-SQL-SUB                  PIC 9(03)   VALUE ZERO.      00744033
           05  PV-SQLCODE                  PIC S9(9)   VALUE +0.        00745033
           05  PV-SQLCODE-DISPLAY          PIC +(8)9   VALUE ZEROES.    00746033
           05  PV-SAVE-PRCHG-TYP-CDE       PIC X(03)   VALUE SPACES.    00747033
           05  PV-SAVE-CORP-LVL-REC        PIC X(56)   VALUE SPACES.    00748033
                                                                        00749033
       01  ABEND-CODE                      PIC S9(04)  VALUE +0 COMP.   00750033
           88  AC-ABEND-4013                           VALUE +4013.     00760033
      *        SQL ERROR                                                00770033
                                                                        00780033
      ******************************************************************00790033
      *  INPUT PERM PRICE CHANGE UPDATES FILE LAYOUT                    00800033
      ******************************************************************00810033
           COPY IMRD041.                                                00820033
                                                                        00830033
      ******************************************************************00840033
      *  OUTPUT PERM PRICE CHANGE UPDATES FILE LAYOUT                   00850033
      ******************************************************************00860033
           COPY IMRD048.                                                00870033
                                                                        00871033
      ***************************************************************** 00872033
      *  RECORD LAYOUT FOR THE PRICE CHANGES COUNT FILE                 00873033
      ***************************************************************** 00874033
       01  WS-PRICE-CHANGES-COUNT-REC.                                  00875033
           05  WS-PRICE-CHANGES-COUNT       PIC 9(09) VALUE ZERO.       00876033
                                                                        00877033
      ***************************************************************** 00878033
      *  TSKXREF DCLGEN                                                 00879033
      ***************************************************************** 00879133
           EXEC SQL                                                     00879233
               INCLUDE TSKXREF                                          00879333
           END-EXEC.                                                    00879433
                                                                        00879533
      ***************************************************************** 00879633
      *  TUPCPLS DCLGEN                                                 00879733
      ***************************************************************** 00879833
           EXEC SQL                                                     00879933
               INCLUDE TUPCPLS                                          00880033
           END-EXEC.                                                    00880133
                                                                        00880233
      *                                                                 00880333
           EXEC SQL                                                     00880433
                INCLUDE SQLCA                                           00880533
           END-EXEC.                                                    00880633
                                                                        00880733
           COPY SQLCA2.                                                 00880833
                                                                        00880933
      ******************************************************************00881033
      *  DB2 ERROR MESSAGES FORMAT AREA.                                00882033
      ******************************************************************00883033
           COPY DPWS004.                                                00884033
                                                                        00885033
      ******************************************************************00886033
       PROCEDURE DIVISION.                                              00887033
      ******************************************************************00888033
      *                                                                 00889033
      ******************************************************************00890033
      * MAIN PROCESSING ROUTINE                                        *00900033
      ******************************************************************00910033
       0000-IMKUT061.                                                   00920033
                                                                        00930033
           PERFORM 1000-HOUSEKEEPING.                                   00940033
                                                                        00950033
           PERFORM 2000-PROCESS-PRICE-CHANGES                           00960033
               UNTIL END-OF-INP01-FILE.                                 00970033
                                                                        00980033
           PERFORM 1500-END-OF-JOB.                                     00990033
           GOBACK.                                                      01000033
                                                                        01010033
      ******************************************************************01020033
      *  PROGRAM INITIALIZION                                          *01030033
      ******************************************************************01040033
       1000-HOUSEKEEPING.                                               01050033
                                                                        01060033
           OPEN INPUT  IN-PRICE-CHANGE-FILE                             01070033
                OUTPUT OUT-PRICE-CHANGE-FILE                            01080033
                       PRICE-CHANGES-COUNT-FILE.                        01090033
                                                                        01100033
           PERFORM 4000-READ-INP01.                                     01110033
                                                                        01120033
           IF END-OF-INP01-FILE                                         01130033
              DISPLAY '*********************************************'   01140033
              DISPLAY '****** NO PRICE CHANGES TO PROCESS FOR TODAY.'   01150033
              DISPLAY '*********************************************'   01160033
           END-IF.                                                      01170033
                                                                        01180033
      ******************************************************************01190033
      *  CLOSE FILES AND DISPLAY COUNTS                                *01200033
      ******************************************************************01210033
       1500-END-OF-JOB.                                                 01220033
                                                                        01230033
           IF CORP-LVL-REC-CREATED                                      01240033
      *--      WRITE OUT CORPORATE LEVEL STORE RECORD (STORE 0000)      01250033
               PERFORM 2100-WRITE-CORPORATE-REC                         01260033
           END-IF.                                                      01270033
                                                                        01280033
           MOVE PA-WRITE-COUNT TO WS-PRICE-CHANGES-COUNT.               01290033
           WRITE PRICE-CHANGES-COUNT-REC FROM                           01300033
                 WS-PRICE-CHANGES-COUNT-REC.                            01310033
                                                                        01311033
           CLOSE IN-PRICE-CHANGE-FILE                                   01312033
                 OUT-PRICE-CHANGE-FILE                                  01313033
                 PRICE-CHANGES-COUNT-FILE.                              01314033
                                                                        01315033
           DISPLAY '**********************************'.                01316033
           DISPLAY 'IMKUT061 - END OF PROGRAM STATS'                    01317033
           DISPLAY 'INPUT RECORDS READ:      ' PA-READ-COUNT.           01318033
           DISPLAY 'STORE ZERO RECS WRITTEN: ' PA-STORE-ZERO-COUNT.     01319033
           DISPLAY 'STORE SPECIFIC RECS OUT: ' PA-STORE-SPECIFIC-COUNT. 01320033
           DISPLAY 'TOTAL RECORDS WRITTEN:   ' PA-WRITE-COUNT.          01330033
           DISPLAY '**********************************'.                01340033
                                                                        01350033
      ******************************************************************01360033
      * PROCESS PRICE CHANGE CURSOR                                    *01370033
      ******************************************************************01380033
       2000-PROCESS-PRICE-CHANGES.                                      01390033
           MOVE '*2000-PROCESS-PRICE-CHANGES.'                          01400033
                                          TO PV-PARA-NAME.              01410033
                                                                        01420033
           IF IM041-PUR-SKU NOT = PV-PREV-PUR-SKU                       01430033
             AND CORP-LVL-REC-CREATED                                   01440033
      *--      WRITE OUT CORPORATE LEVEL STORE RECORD (STORE 0000)      01450033
               PERFORM 2100-WRITE-CORPORATE-REC                         01460033
           END-IF.                                                      01470033
                                                                        01471033
           MOVE IM041-PUR-NEW-UNT-RTL-AMT TO IM048-UIR-NEW-UNT-RTL-AMT. 01471133
           MOVE IM041-PUR-NEW-GP-PRIC-ID  TO IM048-UIR-NEW-GP-PRIC-ID.  01471233
           MOVE IM041-PUR-NEW-GP-RTL-QTY  TO IM048-UIR-NEW-GP-RTL-QTY.  01471333
           MOVE IM041-PUR-NEW-GP-RTL-AMT  TO IM048-UIR-NEW-GP-RTL-AMT.  01471433
           MOVE IM041-PUR-EFF-DTE         TO IM048-UIR-EFF-DTE.         01471533
           MOVE IM041-PUR-PRCHG-ID        TO IM048-UIR-PRCHG-ID.        01471633
           MOVE IM041-PUR-PRCHG-TYP-CDE   TO IM048-UIR-PRCHG-TYP-CDE.   01471733
           MOVE IM041-PUR-ALL-RMNG-STR-IND                              01472333
                                          TO IM048-UIR-ALL-RMNG-STR-IND.01472433
           MOVE SPACE TO IM048-UIR-ALL-SAME-PRICE-IND.                  01472533
                                                                        01473033
      *-- WHEN SKU CHANGES, TYPE CODE IS RCP, MK2, OR FNL, AND IF       01474033
      *-- IM041-PUR-ALL-RMNG-STR-IND IS SET TO "Y", CREATE A            01475033
      *-- CORPORATE LEVEL STORE RECORD (STORE 0000)                     01476033
           IF IM041-PUR-SKU NOT = PV-PREV-PUR-SKU                       01477033
               MOVE IM041-PUR-SKU         TO PV-PREV-PUR-SKU            01478033
                                             SKXREF-SKU                 01479033
               MOVE IM041-PUR-PRCHG-TYP-CDE TO PV-SAVE-PRCHG-TYP-CDE    01480033
               SET CORP-LVL-REC-NOT-CREATED TO TRUE                     01490033
               SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE                 01491036
               PERFORM 6000-SELECT-PC-SKU                               01500033
               IF PS-CURRENT-ROW-FOUND                                  01501033
                   MOVE SKXREF-INTR-UPC-ID  TO IM048-UIR-INTR-UPC-ID    01502033
                   MOVE IM041-PUR-SKU       TO IM048-UIR-SKU            01503033
                   MOVE SKXREF-SKU-STAT-CDE TO IM048-UIR-STAT-CDE       01504033
                   MOVE SKXREF-ITM-NBR      TO IM048-UIR-ITM-NBR        01505033
                   IF (IM041-PRCHGTYP-REG-CLEARNCE                      01506033
                       OR (IM041-PRCHGTYP-2ND-CLR-MRKDWN                01507034
                           AND IM048-UIR-STAT-CDE = PC-CLEARANCE)       01507134
                       OR (IM041-PRCHGTYP-FINL-CLR-MRKDWN)              01508034
                           AND IM048-UIR-STAT-CDE = PC-CLEARANCE)       01508134
                     AND IM041-PUR-ALL-RMNG-STRS                        01509033
                       MOVE IM041-PUR-NEW-UNT-RTL-AMT                   01510033
                                          TO PV-SAVE-PUR-NEW-UNT-RTL-AMT01520033
                       MOVE IM048-UPC-ID-PRICE-UPDATE-REC TO            01530033
                             PV-SAVE-CORP-LVL-REC                       01540033
                       SET CORP-LVL-REC-CREATED TO TRUE                 01550033
                       IF (IM041-PRCHGTYP-REG-CLEARNCE                  01551033
                           AND NOT (IM048-UIR-STAT-CDE = PC-CLEARANCE   01551133
                                     OR PC-MIXED))                      01551233
                         OR IM041-PRCHGTYP-2ND-CLR-MRKDWN               01551334
                         OR IM041-PRCHGTYP-FINL-CLR-MRKDWN              01551533
                           SET PS-ALL-STORES-HAVE-SAME-PRICE TO TRUE    01551633
                       END-IF                                           01551733
                   END-IF                                               01551833
               END-IF                                                   01551933
           END-IF.                                                      01552033
                                                                        01552133
           IF PS-CURRENT-ROW-FOUND                                      01553033
               IF CORP-LVL-REC-CREATED                                  01554033
      *--          MOVE 'N' TO INDICATOR FOR LAST TIER STORE NOT THE    01555033
      *--          HIGHEST PRICE                                        01556033
                   IF IM041-PUR-NEW-UNT-RTL-AMT <                       01557033
                                          PV-SAVE-PUR-NEW-UNT-RTL-AMT   01558033
                       MOVE PC-N          TO IM048-UIR-ALL-RMNG-STR-IND 01559033
                       SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE         01560033
                   ELSE                                                 01570033
                       IF IM041-PUR-NEW-UNT-RTL-AMT >                   01580033
                                      PV-SAVE-PUR-NEW-UNT-RTL-AMT       01590033
      *--                  ABEND -- THIS SHOULD NOT OCCUR               01591033
                           DISPLAY '*** BAD SORT OF INPUT FILE  ***'    01592033
                           DISPLAY '*** UNIT RETAIL AMOUNT SHOULD BE IN 01593033
      -                            'DESCENDING ORDER BY SKU.'           01593133
                           DISPLAY '*** BAD SKU/STORE: ' IM041-PUR-SKU  01593233
                                   ' ' IM041-PUR-STR-NBR                01593333
                           MOVE +4016 TO ABEND-CODE                     01593433
                           PERFORM 9999-ABEND                           01593533
                       ELSE                                             01593633
                           IF IM041-PUR-PRCHG-TYP-CDE NOT =             01593733
                                 PV-SAVE-PRCHG-TYP-CDE                  01593833
                             OR IM048-UIR-ALL-RMNG-STR-IND = PC-N       01593933
                               SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE 01594033
                           END-IF                                       01594133
                       END-IF                                           01594233
                   END-IF                                               01594333
               ELSE                                                     01594433
                   IF IM041-PUR-STR-NBR > 0                             01594533
                       MOVE PC-N          TO IM048-UIR-ALL-RMNG-STR-IND 01594633
                   END-IF                                               01594733
               END-IF                                                   01594833
      *--      AND WRITE AN OUTPUT RECORD                               01594933
               MOVE IM041-PUR-STR-NBR     TO IM048-UIR-STR-NBR          01595033
               PERFORM 5000-WRITE-PRICE-CHANGE-FILE                     01596033
           END-IF.                                                      01597033
                                                                        01598033
           PERFORM 4000-READ-INP01.                                     01599033
                                                                        01600033
      *                                                                 01610033
      ******************************************************************01620033
      * WRITE OUT CORPORATE LEVEL STORE RECORD (STORE 0000)            *01630033
      ******************************************************************01640033
       2100-WRITE-CORPORATE-REC.                                        01650033
                                                                        01660033
           MOVE PV-SAVE-CORP-LVL-REC TO IM048-UPC-ID-PRICE-UPDATE-REC.  01661033
           MOVE ZERO                 TO IM048-UIR-STR-NBR.              01662033
           IF PS-ALL-STORES-HAVE-SAME-PRICE                             01663033
               IF IM048-PRCHGTYP-REG-CLEARNCE                           01664033
                OR IM048-PRCHGTYP-2ND-CLR-MRKDWN                        01665033
                   SET IM048-UIR-ALL-SAME-PRICE TO TRUE                 01665133
               ELSE                                                     01665233
                   IF IM048-PRCHGTYP-FINL-CLR-MRKDWN                    01665333
                       SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE         01665433
                       PERFORM 6100-SELECT-TUPCPLS-FNL-PRICES           01665533
                       IF PS-ALL-STORES-HAVE-SAME-PRICE                 01665633
                           SET IM048-UIR-ALL-SAME-PRICE TO TRUE         01665733
                       END-IF                                           01665833
                   END-IF                                               01665933
               END-IF                                                   01666033
           END-IF.                                                      01666133
                                                                        01666233
           PERFORM 5000-WRITE-PRICE-CHANGE-FILE.                        01666333
      *                                                                 01666433
      ******************************************************************01666533
      * READ INPUT FILE.                                               *01666633
      ******************************************************************01666733
       4000-READ-INP01.                                                 01666833
                                                                        01666933
           READ IN-PRICE-CHANGE-FILE INTO IM041-PRICE-UPDATE-RECORD     01667033
               AT END SET END-OF-INP01-FILE                             01668033
                                          TO TRUE                       01669033
               NOT AT END ADD +1          TO PA-READ-COUNT              01670033
           END-READ.                                                    01680033
                                                                        01690033
      ******************************************************************01700033
      * WRITE OUTPUT FILE.                                             *01710033
      ******************************************************************01720033
       5000-WRITE-PRICE-CHANGE-FILE.                                    01730033
                                                                        01740033
           WRITE OUT-PRICE-CHANGE-REC                                   01750033
                 FROM IM048-UPC-ID-PRICE-UPDATE-REC.                    01760033
           ADD +1                         TO PA-WRITE-COUNT.            01770033
           IF IM048-UIR-STR-NBR = +0                                    01780033
               ADD +1 TO PA-STORE-ZERO-COUNT                            01790033
           ELSE                                                         01800033
               ADD +1 TO PA-STORE-SPECIFIC-COUNT                        01810033
           END-IF.                                                      01820033
                                                                        01830033
      ************************************************************      01840033
      *  SELECT THE PRICE CHANGE SKU                            **      01850033
      ************************************************************      01860033
       6000-SELECT-PC-SKU.                                              01870033
           MOVE '*6000-SELECT-PC-SKU              *' TO PV-PARA-NAME.   01880033
                                                                        01890033
           INITIALIZE PA-RETRY-COUNT.                                   01900033
                                                                        01910033
           PERFORM                                                      01920033
               WITH TEST AFTER                                          01930033
               VARYING PV-SQL-SUB                                       01940033
               FROM 1 BY 1                                              01941033
               UNTIL SQLCODE = 0                                        01942033
                 OR  SQLCODE = +100                                     01943033
                 OR  PV-SQL-SUB > PC-MAX-RETRIES                        01944033
                                                                        01945033
               EXEC SQL                                                 01946033
                  SELECT  SKU_STAT_CDE                                  01947033
                        , ITM_NBR                                       01948033
                        , INTR_UPC_ID                                   01949033
                    INTO :SKXREF-SKU-STAT-CDE                           01949133
                       , :SKXREF-ITM-NBR                                01949233
                       , :SKXREF-INTR-UPC-ID                            01949333
                    FROM TSKXREF                                        01949433
                   WHERE SKU = :SKXREF-SKU                              01949533
               END-EXEC                                                 01949633
                                                                        01949733
               EVALUATE TRUE                                            01949833
                   WHEN SQLCODE = 0                                     01949933
                        SET PS-CURRENT-ROW-FOUND TO TRUE                01950033
                   WHEN SQLCODE = +100                                  01951033
                        SET PS-CURRENT-ROW-NOT-FOUND TO TRUE            01951133
                        MOVE IM041-PUR-PRCHG-ID TO PV-NUM-DISPLAY       01951233
                        DISPLAY 'SKU INFO MISSING FOR SKU: ' SKXREF-SKU 01951333
                                ' PRCHG_ID: ' PV-NUM-DISPLAY            01951433
                   WHEN SQLCODE = -911                                  01951533
                   WHEN SQLCODE = -913                                  01951633
                        CONTINUE                                        01951733
                   WHEN SQLWARN NOT = SPACES                            01951833
                   WHEN SQLCODE NOT = ZERO                              01951933
                        MOVE 'SELECT PRICE CHANGE SKU'                  01952033
                                   TO PV-DB2-OPERATION-ATTEMPTED        01952133
                        PERFORM 9980-SQL-ERROR                          01952233
               END-EVALUATE                                             01952333
           END-PERFORM.                                                 01952433
                                                                        01952533
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        01952633
               MOVE 'SELECT PC SKU RETRIES EXCEEDED'                    01952733
                                   TO  PV-DB2-OPERATION-ATTEMPTED       01952833
               PERFORM 9980-SQL-ERROR                                   01952933
           END-IF.                                                      01953033
                                                                        01953133
      ************************************************************      01953233
      *  VERIFY THERE ARE NO DIFFERENT PRICES FOR A FINAL MARKDOWN      01953333
      *  PRICE CHANGE WITH ALL STORES HAVING THE SAME PRICE.            01953433
      ************************************************************      01953533
       6100-SELECT-TUPCPLS-FNL-PRICES.                                  01953633
           MOVE '*6100-SELECT-TUPCPLS-FNL-PRICES  *' TO PV-PARA-NAME.   01953733
                                                                        01953833
           MOVE IM048-UIR-INTR-UPC-ID TO UPCPLS-UPC-ID.                 01953933
           MOVE PV-SAVE-PUR-NEW-UNT-RTL-AMT TO UPCPLS-UNT-RTL-AMT.      01954033
                                                                        01954133
           INITIALIZE PA-RETRY-COUNT.                                   01954233
                                                                        01954333
           PERFORM                                                      01954433
               WITH TEST AFTER                                          01954533
               VARYING PV-SQL-SUB                                       01954633
               FROM 1 BY 1                                              01954733
               UNTIL SQLCODE = 0                                        01954833
                 OR  SQLCODE = +100                                     01954933
                 OR  PV-SQL-SUB > PC-MAX-RETRIES                        01955033
                                                                        01955133
               EXEC SQL                                                 01955233
                  SELECT 1                                              01955333
                    INTO :PV-ONE                                        01955433
                    FROM TUPCPLS                                        01955533
                   WHERE UPC_ID = :UPCPLS-UPC-ID                        01955633
                     AND STR_NBR > SMALLINT(0)                          01955733
                     AND EFF_END_TMST IS NULL                           01955833
                     AND UNT_RTL_AMT <> :UPCPLS-UNT-RTL-AMT             01955933
                     AND MKDN_CDE = 'F'                                 01956033
                   FETCH FIRST 1 ROW ONLY                               01956133
                   WITH UR                                              01956233
               END-EXEC                                                 01956333
                                                                        01956433
               EVALUATE TRUE                                            01956533
                   WHEN SQLCODE = 0                                     01956633
                        CONTINUE                                        01956733
                   WHEN SQLCODE = +100                                  01956833
                        SET PS-ALL-STORES-HAVE-SAME-PRICE TO TRUE       01956933
                   WHEN SQLCODE = -911                                  01957033
                   WHEN SQLCODE = -913                                  01957133
                        CONTINUE                                        01957233
                   WHEN SQLWARN NOT = SPACES                            01957333
                   WHEN SQLCODE NOT = ZERO                              01957433
                        MOVE 'SELECT TUPCPLS FNL PRICES'                01957533
                                   TO PV-DB2-OPERATION-ATTEMPTED        01957633
                        PERFORM 9980-SQL-ERROR                          01957733
               END-EVALUATE                                             01957833
           END-PERFORM.                                                 01957933
                                                                        01958033
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        01958133
               MOVE 'SELECT TUPCPLS RETRIES EXCEEDED'                   01958233
                                   TO  PV-DB2-OPERATION-ATTEMPTED       01958333
               PERFORM 9980-SQL-ERROR                                   01958433
           END-IF.                                                      01958533
                                                                        01958633
       9980-SQL-ERROR.                                                  01958733
      ******************************************************************01958833
      * ABEND ROUTINE FOR BAD SQL CALLS                                *01958933
      ******************************************************************01959033
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                          01959133
           DISPLAY '** ABEND **          = SQL ERROR'.                  01959233
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           01959333
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              01959433
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.01959533
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.        01959633
           DISPLAY '**'                                                 01959733
           DISPLAY 'INPUT RECORDS READ:      ' PA-READ-COUNT.           01959833
           DISPLAY 'STORE ZERO RECS WRITTEN: ' PA-STORE-ZERO-COUNT.     01959933
           DISPLAY 'STORE SPECIFIC RECS OUT: ' PA-STORE-SPECIFIC-COUNT. 01960033
           DISPLAY 'TOTAL RECORDS WRITTEN:   ' PA-WRITE-COUNT.          01961033
      *                                                                 01961133
      ******************************************************************01961233
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    01961333
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         01961433
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     01961533
      * FORMAT.                                                         01961633
      ******************************************************************01961733
           COPY DPPD004.                                                01961833
                                                                        01961933
           SET AC-ABEND-4013 TO TRUE.                                   01962033
           CALL 'ILBOABN0' USING ABEND-CODE.                            01962133
                                                                        01962233
      ******************************************************************01962333
      * ABEND ROUTINE MISC ERRORS                                      *01962433
      ******************************************************************01962533
       9999-ABEND.                                                      01962633
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           01962733
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              01962833
           DISPLAY '**'                                                 01962933
           DISPLAY 'INPUT RECORDS READ:      ' PA-READ-COUNT.           01963033
           DISPLAY 'STORE ZERO RECS WRITTEN: ' PA-STORE-ZERO-COUNT.     01964033
           DISPLAY 'STORE SPECIFIC RECS OUT: ' PA-STORE-SPECIFIC-COUNT. 01965033
           DISPLAY 'TOTAL RECORDS WRITTEN:   ' PA-WRITE-COUNT.          01966033
           CALL 'ILBOABN0' USING ABEND-CODE.                            01967033
