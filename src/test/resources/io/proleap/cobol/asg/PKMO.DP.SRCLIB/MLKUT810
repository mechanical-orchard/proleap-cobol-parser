       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKUT810                                                  
       AUTHOR.        SHARON SZPISZAR - STRATAGEM.                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  06-23-2008.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      * MLKUT810 - M/L RECALC MONTH-TO-DATE DEPT TABLE (TMNDPT)        *        
      *            CLONE OF PROGRAM MLKUP313 FOR PVT GIVEBACK          *        
      ******************************************************************        
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TMNDPT  *        
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *        
      *    END_INV_RTL_AMT  - RETAIL ENDING INVENTORY                  *        
      *    CUM_MKUP_PCT     - CUM-TO-DATE MARKUP PERCENT               *        
      *    END_INV_COST_AMT - COST ENDING INVENTORY                    *        
      ******************************************************************        
      *  ALL THE DEPT RECS FOR THE FISCAL MONTH END ARE PULLED FROM    *        
      *  TMNDPT TABLE. IT IS MATCHED AGAINST THE PVT GIVEBACK FILE.    *        
      *  IF THERE IS NO MATCH FOR THE REC IT IS WRITTEN AS SUCH TO THE *        
      *  OUTPUT FILE. IF MATCH FOUND THE EIR CUM AND EIC ARE           *        
      *  RECALCULATED AFTER REMOVING THE GB VALUES.                    *        
      ******************************************************************        
      *                        CHANGE LOG                              *        
      *  CHANGE DATE    CHANGE ID            REASON FOR CHANGE         *        
      *  02/08/2013     COGNIZANT(KARTHIK K) FIXED WEEK 53 ISSUE IN    *        
      *                                      PARA 7100-. ADDED A UNION *        
      *                                      TO BRING BACK VALID DATA  *        
      *                                      WHEN THE DATE BEING LOOKED*        
      *                                      UP FOR IS IN WEEK 53  AS  *        
      *                                      THE FIRST SELECT WONT     *        
      *                                      RETURN ANY ROWS           *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-CNTL-INPUT-FILE                                          
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CURR-GIVEBK-ACTL-FILE                                         
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT MNDPT-WO-PB-GIVEBK-IN-FILE                                    
               ASSIGN TO INP03.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT RECALC-OUTPUT-FILE                                            
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT MNDPT-WO-PB-GIVEBK-OUT-FILE                                   
               ASSIGN TO OUT03.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  DATE-CNTL-INPUT-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATE-CNTL-INPUT-RECORD          PIC X(80).                           
                                                                                
       FD  CURR-GIVEBK-ACTL-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  CURR-GIVEBK-ACTL-RECORD         PIC X(80).                           
                                                                                
       FD  MNDPT-WO-PB-GIVEBK-IN-FILE                                           
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MNDPT-WO-PB-GIVEBK-IN-RECORD    PIC X(190).                          
                                                                                
       FD  OUTPUT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  OUTPUT-RECORD                   PIC X(80).                           
                                                                                
       FD  RECALC-OUTPUT-FILE                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  RECALC-OUTPUT-RECORD            PIC X(30).                           
                                                                                
       FD  MNDPT-WO-PB-GIVEBK-OUT-FILE                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  MNDPT-WO-PB-GIVEBK-OUT-RECORD   PIC X(190).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAMMING TABLES BEGIN ***'.                                  
                                                                                
       01  PT-MNDPT-TBL-AREA.                                                   
           05  PT-MNDPT-TBL-MAX            PIC S9(05)      VALUE +9999          
                               COMP.                                            
           05  PT-MNDPT-TBL-COUNT          PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PT-MNDPT-TBL-INDX           PIC S9(05)      VALUE ZEROES         
                               COMP.                                            
           05  PT-MNDPT-TBL OCCURS   9999 TIMES.                                
               10  PT-FSCL-MN-NBR          PIC X(02)       VALUE SPACES.        
               10  PT-FSCL-MN-END-DTE      PIC X(10)       VALUE SPACES.        
               10  PT-DEPT-NBR             PIC X(03)       VALUE SPACES.        
               10  PT-DEPT-NBR-9 REDEFINES PT-DEPT-NBR                          
                                           PIC 9(03).                           
               10  PT-SLS-RTL-AMT          PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-MKDN-RTL-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-MKUP-RTL-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-XFER-IN-RTL-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-XFER-OUT-RTL-AMT     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-RCPT-RTL-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-RCPT-NET-COST-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-PADJ-RTL-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-PADJ-NET-COST-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-PADJ-GIVEBK-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-INV-ADJ-RTL-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-SHNK-RTL-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-END-INV-RTL-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-CUM-MKUP-PCT         PIC S9(4)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
               10  PT-END-INV-COST-AMT     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-SLS-COST-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-GRO-MRGN-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-ML-CUM-MKUP-PCT      PIC S9(4)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
               10  PT-ML-END-INV-COST-AMT  PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-ML-SLS-COST-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-ML-GRO-MRGN-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-VAR-END-INV-COST-AMT PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-VAR-SLS-COST-AMT     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PT-VAR-GRO-MRGN-AMT     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** MNDPT W/O PB GIVEBK RECD ***'.                                  
                                                                                
      *01  ML810-MNDPT-WO-PB-GIVEBACK.                                          
           COPY MLRD810.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAM VARIABLE WS AREA ***'.                                  
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-UPDATE-VARIABLES.                                             
      * THESE VARIABLES WILL UPDATE CURRENT TMNDPT ROW *****************        
               10  PV-END-INV-RTL-AMT      PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-MKUP-PCT         PIC S9(4)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-MKUP-PCT-MAX     PIC S9(9)V9(7)  VALUE ZEROES         
                               COMP-3.                                          
               10  PV-END-INV-COST-AMT     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SLS-COST-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-GRO-MRGN-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
      ******************************************************************        
           05  PV-SEASON-TO-DATE-CSR-VAR.                                       
               10  PV-SEAS-RCPT-RTL-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-RCPT-NET-COST   PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-PADJ-RTL-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-PADJ-NET-COST   PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-PADJ-GIVEBK-AMT PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-MKUP-RTL-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-SEAS-XFER-NET-AMT    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CURR-MONTH-COST-VAR.                                          
               10  PV-CURR-RCPT-NET-COST   PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CURR-PADJ-NET-COST   PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PREV-MONTH-VAR.                                               
               10  PV-PREV-END-INV-RTL     PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PREV-END-INV-COST    PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PREV-ML-END-INV-COST PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-PREV-SEASON-VAR.                                              
               10  PV-PREV-SEAS-PURCH-RTL  PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-PREV-SEAS-PURCH-COST PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
           05  PV-CUM-TO-DATE-VARIABLES.                                        
               10  PV-CUM-PURCH-RTL        PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-PURCH-COST       PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
               10  PV-CUM-MKUP-AMT         PIC S9(11)V9(2) VALUE ZEROES         
                               COMP-3.                                          
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** HOLD AND OTHER VARIABLES ***'.                                  
                                                                                
       01  EXTRA-PROGRAM-VARIABLES.                                             
           05  PV-SRCH-FSCL-MN-END-DTE     PIC X(10)       VALUE SPACES.        
           05  PV-SRCH-DEPT-NBR            PIC X(03)       VALUE ZEROES.        
           05  PV-SRCH-DEPT-NBR-9 REDEFINES PV-SRCH-DEPT-NBR                    
                                           PIC 9(03).                           
           05  PV-HOLD-DEPT-NBR            PIC X(3)        VALUE SPACES.        
           05  PV-HOLD-GIVEBK-CURR-COST    PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-XFER-NET                 PIC S9(11)V99   VALUE ZEROES         
                               COMP-3.                                          
           05  PV-HOLD-DATA-RETENTION-DTE  PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-DATE                PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-FISCAL-MN-END-DTE   PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-FISCAL-MN-NBR       PIC X(02)       VALUE SPACES.        
           05  PV-HOLD-FISCAL-MX-END-DTE   PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-FISCAL-MX-NBR       PIC X(02)       VALUE SPACES.        
           05  PV-HOLD-PFM-END-DTE         PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-SEA-BEG-DTE         PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-OPN-MN-END-DTE      PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-CAL-MN-NBR          PIC 9(02)       VALUE ZEROES.        
           05  PV-HOLD-PREV-SEA-END-DTE    PIC X(10)       VALUE SPACES.        
           05  PV-HOLD-FSCL-MN-NBR         PIC X(02)       VALUE SPACES.        
           05  PV-HOLD-FSCL-YR-NBR         PIC X(04)       VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC X(30)       VALUE SPACES.        
           05  PV-OPERATION-ATTEMPTED      PIC X(30)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** ABEND CODE               ***'.                                  
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAM ACCUMULATORS     ***'.                                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-DEPT-COUNT               PIC  9(09)      VALUE ZEROES.        
           05  PA-GIVEBACK-COUNT           PIC  9(09)      VALUE ZEROES.        
           05  PA-GIVEBACK-LOADED          PIC  9(09)      VALUE ZEROES.        
           05  PA-MNDPT-IN-COUNT           PIC  9(09)      VALUE ZEROES.        
           05  PA-CARRY-FOWARD-COUNT       PIC  9(09)      VALUE ZEROES.        
           05  PA-RECALC-COUNT             PIC  9(09)      VALUE ZEROES.        
           05  PA-MNDPT-OUT-COUNT          PIC  9(09)      VALUE ZEROES.        
           05  PA-CUM-MKUP-MAX-LIMIT       PIC  9(09)      VALUE ZEROES.        
           05  PA-DATE-CNTL-COUNT          PIC  9(09)      VALUE ZEROES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PROGRAM SWITCH AREA      ***'.                                  
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-FIRST-TIME-SW            PIC X           VALUE 'N'.           
               88  PS-FIRST-TIME-THRU                      VALUE 'Y'.           
               88  PS-NOT-FIRST-TIME-THRU                  VALUE 'N'.           
           05  PS-DATE-CNTL-OVERRIDE-SW    PIC X           VALUE 'N'.           
               88  PS-DATE-CNTL-OVERRIDE                   VALUE 'Y'.           
               88  PS-NO-DATE-CNTL-OVERRIDE                VALUE 'N'.           
           05  PS-DTATTR-FOUND-SW          PIC X           VALUE 'N'.           
               88  PS-DTATTR-FOUND                         VALUE 'Y'.           
               88  PS-DTATTR-NOT-FOUND                     VALUE 'N'.           
           05  PS-MNDPT-TBL-FOUND-SW       PIC X           VALUE 'N'.           
               88  PS-MNDPT-TBL-FOUND                      VALUE 'Y'.           
               88  PS-MNDPT-TBL-NOT-FOUND                  VALUE 'N'.           
           05  PS-EOF-DATE-CNTL-SW         PIC X           VALUE 'N'.           
               88  PS-EOF-DATE-CNTL                        VALUE 'Y'.           
               88  PS-NOT-EOF-DATE-CNTL                    VALUE 'N'.           
           05  PS-EOF-GIVEBK-ACTL-SW       PIC X           VALUE 'N'.           
               88  PS-EOF-GIVEBK-ACTL                      VALUE 'Y'.           
               88  PS-NOT-EOF-GIVEBK                       VALUE 'N'.           
           05  PS-EOF-MNDPT-WO-GIVEBK-IN-SW PIC X          VALUE 'N'.           
               88  PS-EOF-MNDPT-WO-GIVEBK-IN               VALUE 'Y'.           
               88  PS-NOT-EOF-MNDPT-WO-GIVEBK-IN           VALUE 'N'.           
           05  PS-END-OF-DTE-CSR-SW        PIC X           VALUE 'N'.           
               88  PS-END-OF-DTE-CSR                       VALUE 'Y'.           
               88  PS-NOT-END-OF-DTE-CSR                   VALUE 'N'.           
           05  PS-END-OF-DEPT-CSR-SW       PIC X           VALUE 'N'.           
               88  PS-END-OF-DEPT-CSR                      VALUE 'Y'.           
               88  PS-NOT-END-OF-DEPT-CSR                  VALUE 'N'.           
           05  PS-CURR-MONTH-FOUND-SW      PIC X           VALUE 'N'.           
               88  PS-CURR-MONTH-FOUND                     VALUE 'Y'.           
               88  PS-CURR-MONTH-NOT-FOUND                 VALUE 'N'.           
           05  PS-PREV-MONTH-FOUND-SW      PIC X           VALUE 'N'.           
               88  PS-PREV-MONTH-FOUND                     VALUE 'Y'.           
               88  PS-PREV-MONTH-NOT-FOUND                 VALUE 'N'.           
           05  PS-PREV-SEAS-INV-FOUND-SW   PIC X           VALUE 'N'.           
               88  PS-PREV-SEAS-INV-NOT-FOUND              VALUE 'N'.           
               88  PS-PREV-SEAS-INV-FOUND                  VALUE 'Y'.           
           05  PS-SEAS-TO-DTE-CSR-SW       PIC X           VALUE 'N'.           
               88  PS-END-OF-SEAS-TO-DTE-CSR               VALUE 'Y'.           
               88  PS-NOT-END-OF-SEAS-TO-DTE-CSR           VALUE 'N'.           
           05  PS-CURR-PURCH-FOUND-SW      PIC X           VALUE 'N'.           
               88  PS-CURR-PURCH-FOUND                     VALUE 'Y'.           
               88  PS-CURR-PURCH-NOT-FOUND                 VALUE 'N'.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME                 PIC X(08)       VALUE SPACES.        
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE                
               'MLKUT810'.                                                      
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                   PIC X(30)       VALUE                
                'INSPECT WHERE CLAUSE VARIABLE'.                                
           05  PE-MSG-02                   PIC X(30)       VALUE                
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                   PIC X(30)       VALUE                
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                   PIC X(30)       VALUE                
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                   PIC X(30)       VALUE                
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-07                   PIC X(30)       VALUE                
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-13                   PIC X(30)       VALUE                
                'SHRINK NOT FND FOR DEPT/DATE '.                                
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** OVERRIDE DATE CONTROL    ***'.                                  
                                                                                
      *****************************************************************         
      *  INPUT DATE CONTROL LAYOUT                                    *         
      *****************************************************************         
       01  DC-DATE-CNTL-LAYOUT.                                                 
           05  DC-FSCL-MN-END-DTE          PIC  X(10)      VALUE SPACES.        
           05  FILLER                      PIC  X(70)      VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** PB GIVEBACK RECORD LAYOUT***'.                                  
                                                                                
      *****************************************************************         
      *  PRIVATE BRAND GIVEBACK ACTUALS BY FISCAL DATE                *         
      *****************************************************************         
      *01  ML811-PB-GIVEBACK-RECORD.                                            
           COPY MLRD811.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** CMU% OVERFLOW/TRUNCATION ***'.                                  
                                                                                
      *****************************************************************         
      *  OUTPUT DETAIL FILE                                           *         
      *****************************************************************         
       01  OUTPUT-DETAIL-LINE.                                                  
           05  FILLER                      PIC X(12)       VALUE                
               ' DEPT NBR : '.                                                  
           05  DL-DEPT-NBR                 PIC X(03)       VALUE SPACES.        
           05  DL-CUM-MKUP-PCT             PIC ZZZ,ZZ9,999.9999999-.            
           05  FILLER                      PIC X(09)       VALUE SPACES.        
           05  FILLER                      PIC X(14)       VALUE                
               'ML MKUP USED: '.                                                
           05  DL-CUM-MKUP-USED            PIC 9,999.9999999-.                  
           05  FILLER                      PIC X(08)       VALUE SPACES.        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** SMOOTH/RECALC RECORD OUT ***'.                                  
                                                                                
      *****************************************************************         
      *  SMOOTHING W/O PB GIVEBACK DATA LAYOUT                        *         
      *****************************************************************         
      *01  ML812-PB-GIVEBACK-SMOOTH.                                            
           COPY MLRD812.                                                        
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** CONTROL DATE TABLE AREA  ***'.                                  
                                                                                
       01  DATE-TABLE.                                                          
           05  DT-CNTL-INDX                PIC S9(2)       VALUE ZEROES         
                               COMP.                                            
           05  DT-CNTL-COUNT               PIC S9(2)       VALUE ZEROES         
                               COMP.                                            
           05  DT-CNTL-MAX                 PIC S9(2)       VALUE +24            
                               COMP.                                            
           05  CONTROL-DATES OCCURS 24 TIMES.                                   
               10  DT-OPN-MN-END-DTE       PIC X(10)       VALUE SPACES.        
               10  DT-CAL-MN-NBR           PIC X(02)       VALUE SPACES.        
               10  DT-PFM-END-DTE          PIC X(10)       VALUE SPACES.        
               10  DT-SEA-BEG-DTE          PIC X(10)       VALUE SPACES.        
               10  DT-PREV-SEA-END-DTE     PIC X(10)       VALUE SPACES.        
               10  DT-FSCL-MN-NBR          PIC X(02)       VALUE SPACES.        
               10  DT-FSCL-YR-NBR          PIC X(04)       VALUE SPACES.        
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  M/L MONTH-TO-DATE DEPARTMENT TABLE              *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TMNDPT                                                   
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      * M/L DATE TABLE AND CURRENT OPEN FISCAL DATE INFO *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *********** THIS CURSOR WILL DRIVE PROGRAM PROCESSING *******             
      * CURSOR USED TO GET ALL DATA FOR FISCAL-MN-END-DTE                       
      *************************************************************             
           EXEC SQL                                                             
               DECLARE DEPT_CSR CURSOR FOR                                      
                 SELECT                                                         
                         DEPT_NBR                                               
                        ,END_INV_RTL_AMT                                        
                        ,END_INV_COST_AMT                                       
                   FROM                                                         
                         TMNDPT                                                 
                   WHERE                                                        
                         FSCL_MN_END_DTE = :PV-HOLD-FISCAL-MN-END-DTE           
                   ORDER BY                                                     
                         DEPT_NBR                                               
                   FOR FETCH ONLY                                               
           END-EXEC.                                                            
                                                                                
      * MAX (6) ROWS FOR EACH DEPT (MONTHS PER SEASON)                          
           EXEC SQL                                                             
               DECLARE SEASON_TO_DATE_CSR CURSOR FOR                            
                 SELECT                                                         
                         FSCL_MN_END_DTE                                        
                        ,XFER_IN_RTL_AMT                                        
                        ,XFER_OUT_RTL_AMT                                       
                        ,RCPT_RTL_AMT                                           
                        ,RCPT_NET_COST_AMT                                      
                        ,PADJ_RTL_AMT                                           
                        ,PADJ_NET_COST_AMT                                      
                        ,MKUP_RTL_AMT                                           
                        ,CUM_MKUP_PCT                                           
                        ,END_INV_COST_AMT                                       
                        ,SLS_COST_AMT                                           
                        ,GRO_MRGN_AMT                                           
                   FROM                                                         
                         TMNDPT                                                 
                   WHERE                                                        
                         DEPT_NBR = :MNDPT-DEPT-NBR                             
                     AND (FSCL_MN_END_DTE >= :PV-HOLD-SEA-BEG-DTE               
                     AND  FSCL_MN_END_DTE <= :PV-HOLD-OPN-MN-END-DTE)           
                   FOR FETCH ONLY                                               
           END-EXEC.                                                            
                                                                                
      * MONTHS TO BE PROCESSED - ALL DATES BETWEEN OPEN AND MAX DATES           
           EXEC SQL                                                             
               DECLARE FISCAL_DATE_CSR CURSOR FOR                               
                 SELECT DISTINCT                                                
                         FSCL_MN_END_DTE                                        
                   FROM                                                         
                         TDTATTR                                                
                   WHERE                                                        
                         FSCL_MN_END_DTE                                        
                             BETWEEN :PV-HOLD-FISCAL-MN-END-DTE                 
                                 AND :PV-HOLD-FISCAL-MX-END-DTE                 
                   ORDER BY                                                     
                         FSCL_MN_END_DTE                                        
                   FOR FETCH ONLY                                               
           END-EXEC.                                                            
                                                                                
           EJECT                                                                
                                                                                
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-END-OF-DEPT-CSR.                                       
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           CLOSE DATE-CNTL-INPUT-FILE                                           
                 CURR-GIVEBK-ACTL-FILE                                          
                 MNDPT-WO-PB-GIVEBK-IN-FILE                                     
                 OUTPUT-FILE                                                    
                 RECALC-OUTPUT-FILE                                             
                 MNDPT-WO-PB-GIVEBK-OUT-FILE.                                   
                                                                                
           IF PA-CUM-MKUP-MAX-LIMIT > 0                                         
              MOVE 4095 TO RETURN-CODE                                          
           ELSE                                                                 
              MOVE 0    TO RETURN-CODE                                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * LOAD TABLE WITH MONTHS TO BE RECALCULATED                               
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  DATE-CNTL-INPUT-FILE                                     
                       CURR-GIVEBK-ACTL-FILE                                    
                       MNDPT-WO-PB-GIVEBK-IN-FILE                               
                OUTPUT OUTPUT-FILE,                                             
                       RECALC-OUTPUT-FILE,                                      
                       MNDPT-WO-PB-GIVEBK-OUT-FILE.                             
                                                                                
           SET PS-NO-DATE-CNTL-OVERRIDE     TO TRUE.                            
           SET PS-NOT-EOF-DATE-CNTL         TO TRUE.                            
           PERFORM 6000-READ-DATE-CNTL-FILE.                                    
           IF PS-DATE-CNTL-OVERRIDE                                             
               DISPLAY 'OVERRIDE CONTROL CARD: '                                
               DISPLAY '----+----1----+----2----+----3'                         
               DISPLAY DC-DATE-CNTL-LAYOUT                                      
               IF DC-FSCL-MN-END-DTE NOT = PV-HOLD-FISCAL-MN-END-DTE            
                   DISPLAY 'RESOLVED INTO ' PV-HOLD-FISCAL-MN-END-DTE           
               END-IF                                                           
           ELSE                                                                 
               DISPLAY 'NO OVERRIDES; USING MLCNTL'                             
               PERFORM 7000-READ-CONTROL-TABLE                                  
           END-IF.                                                              
                                                                                
           PERFORM 7025-PROCESS-CNTL-TBL.                                       
                                                                                
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
                                                                                
           ADD 1 TO DT-CNTL-INDX.                                               
           PERFORM 2800-FORMAT-DATES.                                           
                                                                                
           SET PS-NOT-EOF-MNDPT-WO-GIVEBK-IN TO TRUE.                           
           PERFORM 5550-READ-MNDPT-INPUT.                                       
           PERFORM 5500-LOAD-MNDPT                                              
                VARYING PT-MNDPT-TBL-INDX FROM 1 BY 1                           
                UNTIL PT-MNDPT-TBL-INDX > PT-MNDPT-TBL-MAX                      
                    OR PS-EOF-MNDPT-WO-GIVEBK-IN.                               
           IF PT-MNDPT-TBL-INDX <= PT-MNDPT-TBL-MAX                             
               MOVE PA-MNDPT-IN-COUNT  TO PT-MNDPT-TBL-COUNT                    
           ELSE                                                                 
               DISPLAY '*********** ABEND *************************'            
               DISPLAY ' TABLE OVERFLOW ON INTERNAL GIVEBACK TABLE'             
               DISPLAY ' INCREASE PT-MNDPT-TBL-MAX'                             
               DISPLAY ' CURRENT LIMIT = ' PT-MNDPT-TBL-MAX                     
               DISPLAY '*******************************************'            
               DISPLAY '  '                                                     
               MOVE +4004 TO ABEND-CODE                                         
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
           SET PS-NOT-EOF-GIVEBK TO TRUE.                                       
           PERFORM 6550-READ-GIVEBK-ACTL-FILE.                                  
           PERFORM 6500-LOAD-GIVEBK-ACTL                                        
                UNTIL PS-EOF-GIVEBK-ACTL.                                       
                                                                                
           PERFORM 7149-OPEN-DEPT-CURSOR.                                       
                                                                                
           MOVE ZERO TO PA-CUM-MKUP-MAX-LIMIT.                                  
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING PARAGRAPH-PERFORMED UNTIL END OF CSR PROCESSING         
      * IF DT-CNTL-INDX > DT-CNTL-COUNT, THEN ALL ROWS FOR THIS                 
      * DEPT RECALCED                                                           
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           MOVE 1    TO DT-CNTL-INDX.                                           
           PERFORM 7150-PROCESS-DEPT-CURSOR.                                    
                                                                                
           IF PS-END-OF-DEPT-CSR                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-HOLD-FISCAL-MN-END-DTE TO PV-SRCH-FSCL-MN-END-DTE        
               MOVE MNDPT-DEPT-NBR            TO PV-SRCH-DEPT-NBR               
               PERFORM 5600-SEARCH-MNDPT                                        
               IF PS-MNDPT-TBL-FOUND                                            
                   ADD 1       TO PA-DEPT-COUNT                                 
                   MOVE PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-INDX)                   
                       TO PV-HOLD-GIVEBK-CURR-COST                              
              ELSE                                                              
                   MOVE ZEROES  TO PV-HOLD-GIVEBK-CURR-COST                     
              END-IF                                                            
              PERFORM 2025-PROCESS-DEPT                                         
                  UNTIL DT-CNTL-INDX > DT-CNTL-COUNT                            
      *       PERFORM 7425-OPEN-SEASON-CURSOR                                   
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * DEPT LEVEL PROCESSING PARAGRAPH                                         
      * PROCESS ALL DATES FOR THIS DEPARTMENT                                   
      ******************************************************************        
       2025-PROCESS-DEPT.                                                       
           INITIALIZE  PV-UPDATE-VARIABLES                                      
                       PV-SEASON-TO-DATE-CSR-VAR.                               
                                                                                
           MOVE MNDPT-DEPT-NBR TO PV-HOLD-DEPT-NBR.                             
                                                                                
      *    IF PS-FIRST-TIME-THRU                                                
      *       MOVE 'N' TO PS-FIRST-TIME-SW                                      
      *    ELSE                                                                 
              PERFORM 2800-FORMAT-DATES                                         
              PERFORM 7425-OPEN-SEASON-CURSOR                                   
      *    END-IF.                                                              
                                                                                
           PERFORM 7350-SELECT-PREV-MONTH-END-INV.                              
           PERFORM 7300-SELECT-CURRENT-DOLLARS.                                 
                                                                                
      ******************************************************************        
      *****                   CARRY FORWARD LOGIC                  *****        
      ******************************************************************        
      *    CURRENT $ FOUND FOR MONTH       - RECALC ROW                *        
      * NO CURRENT $ FOUND/   PREV $ FOUND - CARRY FORWARD INVENTORY $ *        
      * NO CURRENT $ FOUND/NO PREV $ FOUND - BYPASS RECALC PROCESSING  *        
      *                                      PROCESS ANOTHER ROW       *        
      ******************************************************************        
           IF PS-CURR-MONTH-NOT-FOUND                                           
               IF PS-PREV-MONTH-NOT-FOUND                                       
                   CONTINUE                                                     
               ELSE                                                             
                   PERFORM 7950-INSERT-CARRY-FWD-ROW                            
               END-IF                                                           
           ELSE                                                                 
               PERFORM 2035-RECALC-PROCESSING                                   
           END-IF.                                                              
                                                                                
           PERFORM 7575-CLOSE-SEASON-CURSOR.                                    
           ADD  1 TO DT-CNTL-INDX.                                              
                                                                                
                                                                                
      ******************************************************************        
      *                                                                *        
      *              ********* RECALC PROCESSING **********            *        
      *                                                                *        
      ******************************************************************        
       2035-RECALC-PROCESSING.                                                  
                                                                                
           PERFORM 2150-CALC-ENDINV-RETAIL.                                     
                                                                                
           PERFORM 7400-PREV-SEASON-END-INV.                                    
           PERFORM 2200-CALC-PREV-SEASN-END-PURCH.                              
                                                                                
           PERFORM 7450-PROCESS-SEASONAL-CURSOR.                                
           IF PS-END-OF-SEAS-TO-DTE-CSR                                         
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2250-CALC-SEASON-TO-DATE-PURCH                           
                   UNTIL PS-END-OF-SEAS-TO-DTE-CSR                              
           END-IF.                                                              
                                                                                
           PERFORM 2300-CALC-CUMM-TO-DATE-PURCH.                                
           PERFORM 2400-CALC-CUMM-MARKUP-PCT.                                   
                                                                                
           PERFORM 2500-CALC-END-INV-COST.                                      
           PERFORM 7475-CURR-SEASON-COST.                                       
           PERFORM 2600-CALC-SALES-COST-AMT.                                    
                                                                                
           PERFORM 2700-CALC-GROSS-MARGIN-AMT.                                  
           PERFORM 7501-OUTPUT-TMNDPT-DATA.                                     
                                                                                
      ******************************************************************        
      * CALCULATE THE ENDING INVENTORY OF CURRENT MONTH BASED ON THE            
      *       ENDING INVENTORY FROM THE PRIOR MONTH                             
      ******************************************************************        
       2150-CALC-ENDINV-RETAIL.                                                 
                                                                                
           COMPUTE PV-END-INV-RTL-AMT = (  PV-PREV-END-INV-RTL                  
                                         + MNDPT-RCPT-RTL-AMT                   
                                         + MNDPT-PADJ-RTL-AMT                   
                                         + MNDPT-MKUP-RTL-AMT                   
                                         - MNDPT-SLS-RTL-AMT                    
                                         + MNDPT-XFER-IN-RTL-AMT                
                                         - MNDPT-XFER-OUT-RTL-AMT               
                                         - MNDPT-MKDN-RTL-AMT                   
                                         - MNDPT-INV-ADJ-RTL-AMT                
                                         - MNDPT-SHNK-RTL-AMT).                 
                                                                                
                                                                                
      ******************************************************************        
      * CALCULATE CUMULATIVE MARKUP PERCENTAGE FOR UPDATE                       
      ******************************************************************        
       2200-CALC-PREV-SEASN-END-PURCH.                                          
                                                                                
           IF PS-PREV-SEAS-INV-NOT-FOUND                                        
                MOVE ZEROES TO PV-PREV-SEAS-PURCH-RTL                           
                               PV-PREV-SEAS-PURCH-COST                          
           ELSE                                                                 
                MOVE MNDPT-END-INV-RTL-AMT                                      
                    TO PV-PREV-SEAS-PURCH-RTL                                   
                MOVE MNDPT-END-INV-COST-AMT                                     
                    TO PV-PREV-SEAS-PURCH-COST                                  
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * ACCUMULATE CURRENT SEASON RETAIL/COST AMTS FROM SEASON BEGINNING        
      ******************************************************************        
       2250-CALC-SEASON-TO-DATE-PURCH.                                          
                                                                                
           COMPUTE PV-XFER-NET =                                                
              (MNDPT-XFER-IN-RTL-AMT -                                          
               MNDPT-XFER-OUT-RTL-AMT).                                         
                                                                                
           ADD PV-XFER-NET             TO  PV-SEAS-XFER-NET-AMT.                
           ADD MNDPT-RCPT-RTL-AMT      TO  PV-SEAS-RCPT-RTL-AMT.                
           ADD MNDPT-RCPT-NET-COST-AMT TO  PV-SEAS-RCPT-NET-COST.               
           ADD MNDPT-PADJ-RTL-AMT      TO  PV-SEAS-PADJ-RTL-AMT.                
           ADD MNDPT-PADJ-NET-COST-AMT TO  PV-SEAS-PADJ-NET-COST.               
           ADD MNDPT-MKUP-RTL-AMT      TO  PV-SEAS-MKUP-RTL-AMT.                
                                                                                
           PERFORM 7450-PROCESS-SEASONAL-CURSOR.                                
                                                                                
                                                                                
      ******************************************************************        
      * DETERMINE CUMULATIVE RETAIL AND COST                                    
      * ADD PRIOR SEASON END INV AMTS TO CURRENT SEASON-TO-DATE AMTS            
      ******************************************************************        
       2300-CALC-CUMM-TO-DATE-PURCH.                                            
                                                                                
                                                                                
           COMPUTE PV-CUM-PURCH-COST           =                                
                                     (  PV-PREV-SEAS-PURCH-COST                 
                                      + PV-SEAS-RCPT-NET-COST                   
                                      + PV-SEAS-PADJ-NET-COST                   
                                      - PV-SEAS-PADJ-GIVEBK-AMT).               
           COMPUTE PV-CUM-PURCH-RTL =                                           
                                          (  PV-PREV-SEAS-PURCH-RTL             
                                           + PV-SEAS-XFER-NET-AMT               
                                           + PV-SEAS-RCPT-RTL-AMT               
                                           + PV-SEAS-PADJ-RTL-AMT               
                                           + PV-SEAS-MKUP-RTL-AMT).             
                                                                                
                                                                                
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNDPT - CUM_MKUP_PCT            
      ******************************************************************        
       2400-CALC-CUMM-MARKUP-PCT.                                               
                                                                                
           COMPUTE PV-CUM-MKUP-AMT = (     PV-CUM-PURCH-RTL                     
                                         - PV-CUM-PURCH-COST          ).        
      * CUM SEASON RETAIL CANNOT EQUAL ZERO IN DIVISOR ('ON SIZE ERROR')        
           IF PV-CUM-PURCH-RTL = 0                                              
                MOVE 100 TO PV-CUM-MKUP-PCT                                     
           ELSE                                                                 
                COMPUTE PV-CUM-MKUP-PCT-MAX ROUNDED =                           
               (PV-CUM-MKUP-AMT * 100) / PV-CUM-PURCH-RTL                       
                IF ( PV-CUM-MKUP-PCT-MAX > 9999.9999999 OR                      
                     PV-CUM-MKUP-PCT-MAX < -9999.9999999  )                     
                    MOVE PV-HOLD-DEPT-NBR          TO DL-DEPT-NBR               
                    MOVE PV-CUM-MKUP-PCT-MAX       TO DL-CUM-MKUP-PCT           
                                                      DL-CUM-MKUP-USED          
                    PERFORM 5000-WRITE-OUTPUT-RECORD                            
                    ADD +1 TO PA-CUM-MKUP-MAX-LIMIT                             
                END-IF                                                          
                MOVE PV-CUM-MKUP-PCT-MAX TO PV-CUM-MKUP-PCT                     
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNDPT - END_INV_COST_AMT        
      ******************************************************************        
       2500-CALC-END-INV-COST.                                                  
                                                                                
           COMPUTE PV-END-INV-COST-AMT ROUNDED =                                
                        ( (100 - PV-CUM-MKUP-PCT)  *                            
                               ( PV-END-INV-RTL-AMT) ) / 100.                   
                                                                                
                                                                                
      ******************************************************************        
      * FINAL CALCULATIONS OF - SALES_COST_AMT                         *        
      ******************************************************************        
       2600-CALC-SALES-COST-AMT.                                                
                                                                                
           IF PS-CURR-PURCH-NOT-FOUND                                           
               MOVE ZEROES            TO PV-CURR-RCPT-NET-COST                  
                                         PV-CURR-PADJ-NET-COST                  
           END-IF.                                                              
                                                                                
           COMPUTE PV-SLS-COST-AMT =                                            
                 (  PV-PREV-END-INV-COST                                        
                  + PV-CURR-RCPT-NET-COST                                       
                  + PV-CURR-PADJ-NET-COST )                                     
                  - PV-HOLD-GIVEBK-CURR-COST                                    
                  - PV-END-INV-COST-AMT.                                        
                                                                                
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TMNDPT - GRO_MRGN_AMT   *        
      ******************************************************************        
       2700-CALC-GROSS-MARGIN-AMT.                                              
                                                                                
           IF MNDPT-SLS-RTL-AMT = 0                                             
               MOVE ZEROES TO PV-GRO-MRGN-AMT                                   
           ELSE                                                                 
               COMPUTE PV-GRO-MRGN-AMT =                                        
               ( ( MNDPT-SLS-RTL-AMT ) - ( PV-SLS-COST-AMT ) )                  
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * MOVE SUBSCRIPTED VARIABLE TO FORMAT USABLE BY DB2                       
      ******************************************************************        
       2800-FORMAT-DATES.                                                       
           MOVE DT-SEA-BEG-DTE(DT-CNTL-INDX) TO PV-HOLD-SEA-BEG-DTE.            
           MOVE DT-OPN-MN-END-DTE(DT-CNTL-INDX)                                 
               TO PV-HOLD-OPN-MN-END-DTE.                                       
           MOVE DT-PFM-END-DTE(DT-CNTL-INDX) TO PV-HOLD-PFM-END-DTE.            
           MOVE DT-CAL-MN-NBR(DT-CNTL-INDX) TO PV-HOLD-CAL-MN-NBR.              
           MOVE DT-PREV-SEA-END-DTE(DT-CNTL-INDX)                               
               TO PV-HOLD-PREV-SEA-END-DTE.                                     
           MOVE DT-FSCL-MN-NBR(DT-CNTL-INDX) TO PV-HOLD-FSCL-MN-NBR.            
           MOVE DT-FSCL-YR-NBR(DT-CNTL-INDX) TO PV-HOLD-FSCL-YR-NBR.            
                                                                                
                                                                                
      ******************************************************************        
      * WRITE CONTROL CARD FOR OUTPUT MESSAGE TO BP (CUM MKUP PCT VAR)          
      ******************************************************************        
       5000-WRITE-OUTPUT-RECORD.                                                
                                                                                
           WRITE OUTPUT-RECORD FROM OUTPUT-DETAIL-LINE.                         
                                                                                
      ******************************************************************        
      * LOAD TMNDPT INTO WORKING-STORAGE TABLE                                  
      ******************************************************************        
       5500-LOAD-MNDPT.                                                         
                                                                                
           MOVE ML810-FSCL-MN-NBR                                               
               TO PT-FSCL-MN-NBR      (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-FSCL-MN-END-DTE                                           
               TO PT-FSCL-MN-END-DTE  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-DEPT-NBR                                                  
               TO PT-DEPT-NBR         (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-SLS-RTL-AMT                                               
               TO PT-SLS-RTL-AMT      (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-MKDN-RTL-AMT                                              
               TO PT-MKDN-RTL-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-MKUP-RTL-AMT                                              
               TO PT-MKUP-RTL-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-XFER-IN-RTL-AMT                                           
               TO PT-XFER-IN-RTL-AMT  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-XFER-OUT-RTL-AMT                                          
               TO PT-XFER-OUT-RTL-AMT (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-RCPT-RTL-AMT                                              
               TO PT-RCPT-RTL-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-RCPT-NET-COST-AMT                                         
               TO PT-RCPT-NET-COST-AMT(PT-MNDPT-TBL-INDX).                      
           MOVE ML810-PADJ-RTL-AMT                                              
               TO PT-PADJ-RTL-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-PADJ-NET-COST-AMT                                         
               TO PT-PADJ-NET-COST-AMT(PT-MNDPT-TBL-INDX).                      
           MOVE ML810-PADJ-GIVEBK-AMT                                           
               TO PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-INDX).                        
           MOVE ML810-INV-ADJ-RTL-AMT                                           
               TO PT-INV-ADJ-RTL-AMT  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-SHNK-RTL-AMT                                              
               TO PT-SHNK-RTL-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-END-INV-RTL-AMT                                           
               TO PT-END-INV-RTL-AMT  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-CUM-MKUP-PCT                                              
               TO PT-CUM-MKUP-PCT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-END-INV-COST-AMT                                          
               TO PT-END-INV-COST-AMT (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-SLS-COST-AMT                                              
               TO PT-SLS-COST-AMT     (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-GRO-MRGN-AMT                                              
               TO PT-GRO-MRGN-AMT     (PT-MNDPT-TBL-INDX).                      
                                                                                
           MOVE ML810-ML-CUM-MKUP-PCT                                           
               TO PT-ML-CUM-MKUP-PCT  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-ML-END-INV-COST-AMT                                       
               TO PT-ML-END-INV-COST-AMT (PT-MNDPT-TBL-INDX).                   
           MOVE ML810-ML-SLS-COST-AMT                                           
               TO PT-ML-SLS-COST-AMT  (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-ML-GRO-MRGN-AMT                                           
               TO PT-ML-GRO-MRGN-AMT  (PT-MNDPT-TBL-INDX).                      
                                                                                
           MOVE ML810-VAR-END-INV-COST-AMT                                      
               TO PT-VAR-END-INV-COST-AMT (PT-MNDPT-TBL-INDX).                  
           MOVE ML810-VAR-SLS-COST-AMT                                          
               TO PT-VAR-SLS-COST-AMT (PT-MNDPT-TBL-INDX).                      
           MOVE ML810-VAR-GRO-MRGN-AMT                                          
               TO PT-VAR-GRO-MRGN-AMT (PT-MNDPT-TBL-INDX).                      
                                                                                
           PERFORM 5550-READ-MNDPT-INPUT.                                       
                                                                                
      ******************************************************************        
      * READ TMNDPT INPUT FILE                                                  
      ******************************************************************        
       5550-READ-MNDPT-INPUT.                                                   
                                                                                
           READ MNDPT-WO-PB-GIVEBK-IN-FILE                                      
               INTO ML810-MNDPT-WO-PB-GIVEBACK                                  
               AT END                                                           
                   SET PS-EOF-MNDPT-WO-GIVEBK-IN TO TRUE                        
               NOT AT END                                                       
                   ADD 1 TO PA-MNDPT-IN-COUNT                                   
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * SEARCH MNDPT TABLE                                                      
      ******************************************************************        
       5600-SEARCH-MNDPT.                                                       
                                                                                
           SET PS-MNDPT-TBL-NOT-FOUND      TO TRUE.                             
                                                                                
           MOVE +1                         TO PT-MNDPT-TBL-INDX.                
           PERFORM                                                              
               UNTIL PT-MNDPT-TBL-INDX > PT-MNDPT-TBL-COUNT                     
                   OR PS-MNDPT-TBL-FOUND                                        
               IF PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-INDX)                         
                       = PV-SRCH-FSCL-MN-END-DTE                                
                   AND (PT-DEPT-NBR(PT-MNDPT-TBL-INDX)                          
                           = PV-SRCH-DEPT-NBR OR                                
                           PV-SRCH-DEPT-NBR = ZEROES)                           
                   SET PS-MNDPT-TBL-FOUND      TO TRUE                          
               ELSE                                                             
                   ADD +1                      TO PT-MNDPT-TBL-INDX             
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      * READ INPUT GIVEBACK FILE                                                
      ******************************************************************        
       6000-READ-DATE-CNTL-FILE.                                                
           READ DATE-CNTL-INPUT-FILE INTO DC-DATE-CNTL-LAYOUT                   
               AT END                                                           
                   SET PS-EOF-DATE-CNTL TO TRUE                                 
               NOT AT END                                                       
                   ADD 1 TO PA-DATE-CNTL-COUNT                                  
                   MOVE DC-FSCL-MN-END-DTE TO PV-HOLD-DATE                      
                   PERFORM 7100-SELECT-DB2-DATES                                
                   IF PS-DTATTR-FOUND                                           
                       SET PS-DATE-CNTL-OVERRIDE    TO TRUE                     
                       MOVE DTATTR-FSCL-MN-END-DTE                              
                           TO PV-HOLD-FISCAL-MN-END-DTE                         
                              PV-HOLD-FISCAL-MX-END-DTE                         
                       MOVE DTATTR-FSCL-MN-NBR                                  
                           TO PV-HOLD-FISCAL-MN-NBR                             
                              PV-HOLD-FISCAL-MX-NBR                             
                   END-IF                                                       
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * LOAD GIVEBACK TO MNDPT TABLE                                            
      ******************************************************************        
       6500-LOAD-GIVEBK-ACTL.                                                   
                                                                                
           IF ML811-FSCL-MN-END-DTE >= PV-HOLD-FISCAL-MN-END-DTE                
               AND ML811-FSCL-MN-END-DTE <= PV-HOLD-FISCAL-MX-END-DTE           
               MOVE ML811-FSCL-MN-END-DTE                                       
                   TO PV-SRCH-FSCL-MN-END-DTE                                   
               MOVE ML811-DEPT-NBR                                              
                   TO PV-SRCH-DEPT-NBR-9                                        
               PERFORM 5600-SEARCH-MNDPT                                        
               IF PS-MNDPT-TBL-FOUND                                            
                   IF ML811-PADJ-NET-COST-AMT                                   
                       NOT = PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-INDX)              
                       ADD 1             TO PA-GIVEBACK-LOADED                  
                       MOVE ML811-PADJ-NET-COST-AMT                             
                           TO PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-INDX)             
                   END-IF                                                       
               ELSE                                                             
                   IF ML811-FSCL-MN-END-DTE(1:4) IS NUMERIC                     
                       AND ML811-FSCL-MN-END-DTE(6:2) IS NUMERIC                
                       AND ML811-FSCL-MN-END-DTE(9:2) IS NUMERIC                
                       AND ML811-DEPT-NBR NOT = ZEROES                          
                       ADD 1             TO PT-MNDPT-TBL-COUNT                  
                       ADD 1             TO PA-GIVEBACK-LOADED                  
                       IF PT-MNDPT-TBL-COUNT <= PT-MNDPT-TBL-MAX                
                           MOVE ML811-FSCL-MN-END-DTE                           
                               TO PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-COUNT)        
                           MOVE ML811-DEPT-NBR                                  
                               TO PT-DEPT-NBR-9     (PT-MNDPT-TBL-COUNT)        
                           MOVE ML811-PADJ-NET-COST-AMT                         
                               TO PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-COUNT)        
                       ELSE                                                     
                           DISPLAY '*********** ABEND *************'            
                           DISPLAY ' INTERNAL GIVEBACK TBL OVERFLOW'            
                           DISPLAY ' INCREASE PT-MNDPT-TBL-MAX'                 
                           DISPLAY ' CURRENT LIMIT = ' PT-MNDPT-TBL-MAX         
                           DISPLAY '*******************************'            
                           DISPLAY '  '                                         
                           MOVE +4004 TO ABEND-CODE                             
                           PERFORM 9999-ABEND                                   
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6550-READ-GIVEBK-ACTL-FILE.                                  
                                                                                
      ******************************************************************        
      * READ INPUT GIVEBACK FILE                                                
      ******************************************************************        
       6550-READ-GIVEBK-ACTL-FILE.                                              
                                                                                
           READ CURR-GIVEBK-ACTL-FILE INTO ML811-PB-GIVEBACK-RECORD             
               AT END                                                           
                   SET PS-EOF-GIVEBK-ACTL TO TRUE                               
               NOT AT END                                                       
                   ADD 1 TO PA-GIVEBACK-COUNT                                   
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       7000-READ-CONTROL-TABLE.                                                 
                                                                                
            EXEC SQL                                                            
                SELECT OPN_FSCL_MN_DTE                                          
                      ,OPN_FSCL_MN_NBR                                          
                      ,MXPSTG_FSCL_MN_DTE                                       
                      ,MXPSTG_FSCL_MN_NBR                                       
                 INTO  :MLCNTL-OPN-FSCL-MN-DTE                                  
                      ,:MLCNTL-OPN-FSCL-MN-NBR                                  
                      ,:MLCNTL-MXPSTG-FSCL-MN-DTE                               
                      ,:MLCNTL-MXPSTG-FSCL-MN-NBR                               
                 FROM  TMLCNTL                                                  
                 WITH UR                                                        
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE MLCNTL-OPN-FSCL-MN-DTE                                  
                                           TO PV-HOLD-FISCAL-MN-END-DTE         
                   MOVE MLCNTL-OPN-FSCL-MN-NBR                                  
                                           TO PV-HOLD-FISCAL-MN-NBR             
                   MOVE MLCNTL-MXPSTG-FSCL-MN-DTE                               
                                           TO PV-HOLD-FISCAL-MX-END-DTE         
                   MOVE MLCNTL-MXPSTG-FSCL-MN-NBR                               
                                           TO PV-HOLD-FISCAL-MX-NBR             
               WHEN OTHER                                                       
                  MOVE '7000-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME           
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9900-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * LOAD ALL DATES TO BE PROCESSED TO INTERNAL TABLE                        
      ******************************************************************        
       7025-PROCESS-CNTL-TBL.                                                   
                                                                                
            PERFORM 7050-OPEN-DATE-CURSOR.                                      
            PERFORM 7075-PROCESS-DATE-CURSOR                                    
                UNTIL PS-END-OF-DTE-CSR.                                        
            PERFORM 7080-CLOSE-DATE-CURSOR.                                     
                                                                                
                                                                                
      *****************************************************************         
      * OPEN CURSOR                                                             
      *****************************************************************         
       7050-OPEN-DATE-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN FISCAL_DATE_CSR                                             
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   SET PS-NOT-END-OF-DTE-CSR TO TRUE                            
               WHEN OTHER                                                       
                   MOVE '7050-OPEN-DATE-CSR' TO PV-PARAGRAPH-NAME               
                   MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * INCREMENT SUBSCRIPT AND CONTINUE LOADING INTERNAL TABLE                 
      *****************************************************************         
       7075-PROCESS-DATE-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
                  FETCH FISCAL_DATE_CSR                                         
                   INTO :PV-HOLD-DATE                                           
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   PERFORM 7100-SELECT-DB2-DATES                                
                   IF PS-DTATTR-FOUND                                           
                       ADD 1 TO DT-CNTL-INDX                                    
                       IF DT-CNTL-INDX <= DT-CNTL-MAX                           
                           PERFORM 7125-LOAD-DATE-TABLE                         
                       ELSE                                                     
                           DISPLAY '*********** ABEND ***************'          
                           DISPLAY ' CNTL DATE INTERNAL TABLE OVERFLOW'         
                           DISPLAY ' INCREASE DT-CNTL-MAX'                      
                           DISPLAY ' CURRENT LIMIT = ' DT-CNTL-MAX              
                           DISPLAY '*********************************'          
                           DISPLAY '  '                                         
                           MOVE +4004 TO ABEND-CODE                             
                           PERFORM 9999-ABEND                                   
                       END-IF                                                   
                   END-IF                                                       
               WHEN +100                                                        
                   MOVE DT-CNTL-INDX TO DT-CNTL-COUNT                           
                   MOVE ZEROES  TO DT-CNTL-INDX                                 
                   SET PS-END-OF-DTE-CSR TO TRUE                                
               WHEN OTHER                                                       
                   MOVE '7075-PROCESS-DATE-CSR' TO PV-PARAGRAPH-NAME            
                   MOVE PE-MSG-04 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * CLOSE DATE CURSOR                                                       
      ******************************************************************        
       7080-CLOSE-DATE-CURSOR.                                                  
           EXEC SQL                                                             
               CLOSE FISCAL_DATE_CSR                                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7080-CLOSE-DATE-CURSOR' TO PV-PARAGRAPH-NAME          
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * SELECT DATES:                                                           
      * THIS QUERY WILL BRING BACK DATES FROM THE DATE ATTRIBUTE TABLE.         
      * IT WILL BRING BACK DATES FOR THE KEY DATE AND THE                       
      * PV-HOLD-DATA-RETENTION-DTE GOES TWO YEARS BACK FROM THE                 
      *  PV-HOLD-DATE                                                           
      ******************************************************************        
       7100-SELECT-DB2-DATES.                                                   
                                                                                
           SET PS-DTATTR-NOT-FOUND   TO TRUE.                                   
           EXEC SQL                                                             
               SELECT  A.CAL_MN_NBR                                             
                      ,A.PFM_END_DTE                                            
                      ,A.SEA_BEG_DTE                                            
                      ,A.PREV_SEA_END_DTE                                       
                      ,A.FSCL_MN_NBR                                            
                      ,A.FSCL_MN_END_DTE                                        
                      ,A.FSCL_YR_NBR                                            
                      ,B.LY_THIS_DAY_KY_DTE                                     
                INTO  :DTATTR-CAL-MN-NBR                                        
                     ,:DTATTR-PFM-END-DTE                                       
                     ,:DTATTR-SEA-BEG-DTE                                       
                     ,:DTATTR-PREV-SEA-END-DTE                                  
                     ,:DTATTR-FSCL-MN-NBR                                       
                     ,:DTATTR-FSCL-MN-END-DTE                                   
                     ,:DTATTR-FSCL-YR-NBR                                       
                     ,:PV-HOLD-DATA-RETENTION-DTE                               
                FROM                                                            
                     TDTATTR         A                                          
                    ,TDTATTR         B                                          
                WHERE                                                           
                      A.KY_DTE  = :PV-HOLD-DATE                                 
                  AND B.KY_DTE  = A.LY_THIS_DAY_KY_DTE                          
              UNION                                                             
               SELECT  A.CAL_MN_NBR                                             
                      ,A.PFM_END_DTE                                            
                      ,A.SEA_BEG_DTE                                            
                      ,A.PREV_SEA_END_DTE                                       
                      ,A.FSCL_MN_NBR                                            
                      ,A.FSCL_MN_END_DTE                                        
                      ,A.FSCL_YR_NBR                                            
                      ,B.LY_THIS_DAY_KY_DTE                                     
                INTO  :DTATTR-CAL-MN-NBR                                        
                     ,:DTATTR-PFM-END-DTE                                       
                     ,:DTATTR-SEA-BEG-DTE                                       
                     ,:DTATTR-PREV-SEA-END-DTE                                  
                     ,:DTATTR-FSCL-MN-NBR                                       
                     ,:DTATTR-FSCL-MN-END-DTE                                   
                     ,:DTATTR-FSCL-YR-NBR                                       
                     ,:PV-HOLD-DATA-RETENTION-DTE                               
                FROM                                                            
                     TDTATTR         A                                          
                    ,TDTATTR         B                                          
                    ,TDTATTR         C                                          
                WHERE                                                           
                      A.KY_DTE  + 7 DAYS   = :PV-HOLD-DATE                      
                  AND B.KY_DTE             = A.LY_THIS_DAY_KY_DTE               
                  AND C.KY_DTE             = A.KY_DTE  + 7 DAYS                 
                  AND C.LY_THIS_DAY_KY_DTE = '9999-09-09'                       
                WITH UR                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   SET PS-DTATTR-FOUND       TO TRUE                            
               WHEN +100                                                        
                   DISPLAY 'NO ROWS QUALIFY FOR RECALCULATION'                  
               WHEN OTHER                                                       
                  MOVE '7100-SELECT-DB2-DATES' TO PV-PARAGRAPH-NAME             
                  MOVE PE-MSG-02 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9900-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * POPULATE INTERNAL TABLE WITH DATES RELATED TO MONTH-END-DATE            
      *****************************************************************         
       7125-LOAD-DATE-TABLE.                                                    
           MOVE PV-HOLD-DATE        TO DT-OPN-MN-END-DTE(DT-CNTL-INDX).         
           MOVE DTATTR-CAL-MN-NBR   TO DT-CAL-MN-NBR(DT-CNTL-INDX).             
           MOVE DTATTR-PFM-END-DTE  TO DT-PFM-END-DTE(DT-CNTL-INDX)             
           MOVE DTATTR-SEA-BEG-DTE  TO DT-SEA-BEG-DTE(DT-CNTL-INDX).            
           MOVE DTATTR-PREV-SEA-END-DTE                                         
               TO DT-PREV-SEA-END-DTE(DT-CNTL-INDX).                            
           MOVE DTATTR-FSCL-MN-NBR  TO DT-FSCL-MN-NBR(DT-CNTL-INDX).            
           MOVE DTATTR-FSCL-YR-NBR  TO DT-FSCL-YR-NBR(DT-CNTL-INDX).            
                                                                                
                                                                                
      *****************************************************************         
      *                                                                         
      *****************************************************************         
       7149-OPEN-DEPT-CURSOR.                                                   
                                                                                
           EXEC SQL                                                             
               OPEN DEPT_CSR                                                    
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   SET PS-NOT-END-OF-DEPT-CSR TO TRUE                           
               WHEN OTHER                                                       
                   MOVE '7149-OPEN-DEPT-CSR' TO PV-PARAGRAPH-NAME               
                   MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                     
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
      *****************************************************************         
      * FETCH A DEPT NUMBER TO PROCESS                                          
      *****************************************************************         
       7150-PROCESS-DEPT-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
                  FETCH DEPT_CSR                                                
                  INTO :MNDPT-DEPT-NBR                                          
                      ,:MNDPT-END-INV-RTL-AMT                                   
                      ,:MNDPT-END-INV-COST-AMT                                  
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET PS-END-OF-DEPT-CSR TO TRUE                               
               WHEN OTHER                                                       
                   MOVE '7150-PROCESS-DEPT-C'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9900-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
                                                                                
      *****************************************************************         
      * SELECT A ROW FROM DB2 TO BE RECALCED & UPDATED (OR INSERT/UPD)          
      *****************************************************************         
       7300-SELECT-CURRENT-DOLLARS.                                             
                                                                                
           EXEC SQL                                                             
                SELECT  SLS_RTL_AMT                                             
                       ,MKDN_RTL_AMT                                            
                       ,MKUP_RTL_AMT                                            
                       ,XFER_IN_RTL_AMT                                         
                       ,XFER_OUT_RTL_AMT                                        
                       ,RCPT_RTL_AMT                                            
                       ,PADJ_RTL_AMT                                            
                       ,INV_ADJ_RTL_AMT                                         
                       ,SHNK_RTL_AMT                                            
                  INTO :MNDPT-SLS-RTL-AMT                                       
                      ,:MNDPT-MKDN-RTL-AMT                                      
                      ,:MNDPT-MKUP-RTL-AMT                                      
                      ,:MNDPT-XFER-IN-RTL-AMT                                   
                      ,:MNDPT-XFER-OUT-RTL-AMT                                  
                      ,:MNDPT-RCPT-RTL-AMT                                      
                      ,:MNDPT-PADJ-RTL-AMT                                      
                      ,:MNDPT-INV-ADJ-RTL-AMT                                   
                      ,:MNDPT-SHNK-RTL-AMT                                      
                  FROM TMNDPT                                                   
                 WHERE DEPT_NBR        = :PV-HOLD-DEPT-NBR                      
                   AND FSCL_MN_NBR     = :PV-HOLD-FSCL-MN-NBR                   
                   AND FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE                
                 WITH UR                                                        
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   SET PS-CURR-MONTH-FOUND     TO TRUE                          
               WHEN +100                                                        
                   SET PS-CURR-MONTH-NOT-FOUND TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '7300-SELECT-CURRENT'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9900-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
                                                                                
      ******************************************************************        
      * RETRIEVE LAST MONTH'S ENDING INVENTORY RETAIL/COST                      
      *     THIS IS ALSO THE BEGINNING INVENTORY FOR THIS MONTH                 
      ******************************************************************        
       7350-SELECT-PREV-MONTH-END-INV.                                          
                                                                                
           SET PS-PREV-MONTH-NOT-FOUND   TO TRUE.                               
           MOVE PV-HOLD-PFM-END-DTE      TO PV-SRCH-FSCL-MN-END-DTE.            
           MOVE PV-HOLD-DEPT-NBR         TO PV-SRCH-DEPT-NBR.                   
           PERFORM 5600-SEARCH-MNDPT.                                           
           IF PS-MNDPT-TBL-FOUND                                                
               MOVE ZEROES                   TO SQLCODE                         
               MOVE PT-END-INV-RTL-AMT  (PT-MNDPT-TBL-INDX)                     
                   TO PV-PREV-END-INV-RTL                                       
               MOVE PT-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                     
                   TO PV-PREV-END-INV-COST                                      
           ELSE                                                                 
               EXEC SQL                                                         
                    SELECT  END_INV_RTL_AMT                                     
                          , END_INV_COST_AMT                                    
                      INTO  :PV-PREV-END-INV-RTL                                
                           ,:PV-PREV-END-INV-COST                               
                      FROM  TMNDPT                                              
                     WHERE (       DEPT_NBR = :PV-HOLD-DEPT-NBR)                
                       AND (FSCL_MN_END_DTE = :PV-HOLD-PFM-END-DTE)             
                     WITH UR                                                    
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   IF ((PV-PREV-END-INV-RTL  NOT = 0) OR                        
                       (PV-PREV-END-INV-COST NOT = 0))                          
                       SET PS-PREV-MONTH-FOUND      TO TRUE                     
                   END-IF                                                       
               WHEN +100                                                        
                   MOVE ZEROES TO PV-PREV-END-INV-RTL                           
                                  PV-PREV-END-INV-COST                          
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7350-SELECT-PREV-MONTH-END-INV'                       
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * 7400-PREV-SEASON-END-INV SELECTS THE ENDING INVENTORY COST &   *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
       7400-PREV-SEASON-END-INV.                                                
                                                                                
           MOVE PV-HOLD-PREV-SEA-END-DTE TO PV-SRCH-FSCL-MN-END-DTE.            
           MOVE PV-HOLD-DEPT-NBR         TO PV-SRCH-DEPT-NBR.                   
           PERFORM 5600-SEARCH-MNDPT.                                           
           IF PS-MNDPT-TBL-FOUND                                                
               SET PS-PREV-SEAS-INV-FOUND        TO TRUE                        
               MOVE PT-END-INV-RTL-AMT  (PT-MNDPT-TBL-INDX)                     
                   TO MNDPT-END-INV-RTL-AMT                                     
               MOVE PT-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                     
                   TO MNDPT-END-INV-COST-AMT                                    
               MOVE PT-ML-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                  
                   TO PV-PREV-ML-END-INV-COST                                   
           ELSE                                                                 
               EXEC SQL                                                         
                    SELECT  END_INV_RTL_AMT                                     
                           ,END_INV_COST_AMT                                    
                      INTO  :MNDPT-END-INV-RTL-AMT                              
                           ,:MNDPT-END-INV-COST-AMT                             
                      FROM  TMNDPT                                              
                     WHERE (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                
                       AND (FSCL_MN_END_DTE = :PV-HOLD-PREV-SEA-END-DTE)        
                     WITH UR                                                    
               END-EXEC                                                         
               EVALUATE SQLCODE                                                 
                   WHEN 0                                                       
                       SET PS-PREV-SEAS-INV-FOUND        TO TRUE                
                       MOVE MNDPT-END-INV-COST-AMT                              
                           TO PV-PREV-ML-END-INV-COST                           
                   WHEN +100                                                    
                       SET PS-PREV-SEAS-INV-NOT-FOUND    TO TRUE                
                   WHEN OTHER                                                   
                       MOVE PE-MSG-02        TO PV-OPERATION-ATTEMPTED          
                       MOVE '7400-PREV-SEASON-END-INV'                          
                           TO PV-PARAGRAPH-NAME                                 
                       PERFORM 9900-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
                                                                                
      *****************************************************************         
      * OPEN CURSOR FOR MONTH-END VALUES FOR THIS SEASON                        
      *****************************************************************         
       7425-OPEN-SEASON-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               OPEN SEASON_TO_DATE_CSR                                          
           END-EXEC                                                             
                                                                                
           IF SQLCODE = +0                                                      
               SET PS-NOT-END-OF-SEAS-TO-DTE-CSR TO TRUE                        
           ELSE                                                                 
               MOVE PE-MSG-03                 TO PV-OPERATION-ATTEMPTED         
               MOVE '7425-OPEN-SEASON-CURSOR' TO PV-PARAGRAPH-NAME              
               PERFORM 9900-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * 7450-PROCESS-SEASON-CURSOR WILL RETRIEVE ALL ROWS WITH AN ENDING        
      * INVENTORY AMOUNT FOR EACH MONTH END DATE FOR THE CURRENT SEASON.        
      ******************************************************************        
       7450-PROCESS-SEASONAL-CURSOR.                                            
                                                                                
           EXEC SQL                                                             
                 FETCH SEASON_TO_DATE_CSR                                       
                  INTO                                                          
                        :MNDPT-FSCL-MN-END-DTE                                  
                       ,:MNDPT-XFER-IN-RTL-AMT                                  
                       ,:MNDPT-XFER-OUT-RTL-AMT                                 
                       ,:MNDPT-RCPT-RTL-AMT                                     
                       ,:MNDPT-RCPT-NET-COST-AMT                                
                       ,:MNDPT-PADJ-RTL-AMT                                     
                       ,:MNDPT-PADJ-NET-COST-AMT                                
                       ,:MNDPT-MKUP-RTL-AMT                                     
                       ,:MNDPT-CUM-MKUP-PCT                                     
                       ,:MNDPT-END-INV-COST-AMT                                 
                       ,:MNDPT-SLS-COST-AMT                                     
                       ,:MNDPT-GRO-MRGN-AMT                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   MOVE MNDPT-FSCL-MN-END-DTE                                   
                       TO PV-SRCH-FSCL-MN-END-DTE                               
                   MOVE MNDPT-DEPT-NBR           TO PV-SRCH-DEPT-NBR            
                   PERFORM 5600-SEARCH-MNDPT                                    
                   IF PS-MNDPT-TBL-FOUND                                        
                       ADD PT-PADJ-GIVEBK-AMT (PT-MNDPT-TBL-INDX)               
                           TO PV-SEAS-PADJ-GIVEBK-AMT                           
                   END-IF                                                       
               WHEN +100                                                        
                   SET PS-END-OF-SEAS-TO-DTE-CSR    TO TRUE                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED         
                   MOVE '7450-PROCESS-SEASONAL-CURSOR'                          
                                              TO PV-PARAGRAPH-NAME              
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7475-CURR-SEASON-COST SELECTS THE ENDING INVENTORY COST &      *        
      *      RETAIL FROM THE LAST DAY OF THE PREVIOUS FISCAL SEASON    *        
      ******************************************************************        
       7475-CURR-SEASON-COST.                                                   
                                                                                
           EXEC SQL                                                             
                SELECT  RCPT_NET_COST_AMT                                       
                       ,PADJ_NET_COST_AMT                                       
                  INTO  :PV-CURR-RCPT-NET-COST                                  
                       ,:PV-CURR-PADJ-NET-COST                                  
                  FROM  TMNDPT                                                  
                 WHERE (DEPT_NBR =        :PV-HOLD-DEPT-NBR)                    
                   AND (FSCL_MN_END_DTE = :PV-HOLD-OPN-MN-END-DTE)              
                 WITH UR                                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   SET PS-CURR-PURCH-FOUND           TO TRUE                    
               WHEN +100                                                        
                   SET PS-CURR-PURCH-NOT-FOUND       TO TRUE                    
               WHEN OTHER                                                       
                   MOVE PE-MSG-02            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7475-CURRENT-SEASON-COST'                             
                                             TO PV-PARAGRAPH-NAME               
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7501-OUTPUT-TMNDPT-DATA WRITES RECALC OUTPUT RECORD & TMNDPT   *        
      *                         OUTPUT RECORD                          *        
      ******************************************************************        
       7501-OUTPUT-TMNDPT-DATA.                                                 
           MOVE PV-HOLD-FSCL-MN-NBR     TO ML812-FSCL-MN-NBR                    
                                           ML810-FSCL-MN-NBR.                   
           MOVE PV-HOLD-OPN-MN-END-DTE  TO ML812-FSCL-MN-END-DTE                
                                           ML810-FSCL-MN-END-DTE.               
           MOVE PV-HOLD-DEPT-NBR        TO ML812-DEPT-NBR                       
                                           ML810-DEPT-NBR.                      
           MOVE MNDPT-SLS-RTL-AMT       TO ML810-SLS-RTL-AMT.                   
           MOVE MNDPT-MKDN-RTL-AMT      TO ML810-MKDN-RTL-AMT.                  
           MOVE MNDPT-MKUP-RTL-AMT      TO ML810-MKUP-RTL-AMT.                  
           MOVE MNDPT-XFER-IN-RTL-AMT   TO ML810-XFER-IN-RTL-AMT.               
           MOVE MNDPT-XFER-OUT-RTL-AMT  TO ML810-XFER-OUT-RTL-AMT.              
           MOVE MNDPT-RCPT-RTL-AMT      TO ML810-RCPT-RTL-AMT.                  
           MOVE MNDPT-RCPT-NET-COST-AMT TO ML810-RCPT-NET-COST-AMT.             
           MOVE MNDPT-PADJ-RTL-AMT      TO ML810-PADJ-RTL-AMT.                  
           MOVE MNDPT-PADJ-NET-COST-AMT TO ML810-PADJ-NET-COST-AMT.             
           MOVE PV-HOLD-GIVEBK-CURR-COST                                        
               TO ML810-PADJ-GIVEBK-AMT.                                        
           MOVE MNDPT-INV-ADJ-RTL-AMT   TO ML810-INV-ADJ-RTL-AMT.               
           MOVE MNDPT-SHNK-RTL-AMT      TO ML810-SHNK-RTL-AMT.                  
           MOVE PV-END-INV-RTL-AMT      TO ML812-END-INV-RTL-AMT                
                                           ML810-END-INV-RTL-AMT.               
           MOVE PV-CUM-MKUP-PCT         TO ML810-CUM-MKUP-PCT.                  
           MOVE PV-END-INV-COST-AMT     TO ML812-END-INV-COST-AMT               
                                           ML810-END-INV-COST-AMT.              
           MOVE PV-SLS-COST-AMT         TO ML810-SLS-COST-AMT.                  
           MOVE PV-GRO-MRGN-AMT         TO ML810-GRO-MRGN-AMT.                  
                                                                                
           MOVE MNDPT-CUM-MKUP-PCT      TO ML810-ML-CUM-MKUP-PCT.               
           MOVE MNDPT-END-INV-COST-AMT  TO ML810-ML-END-INV-COST-AMT.           
           MOVE MNDPT-SLS-COST-AMT      TO ML810-ML-SLS-COST-AMT.               
           MOVE MNDPT-GRO-MRGN-AMT      TO ML810-ML-GRO-MRGN-AMT.               
                                                                                
           COMPUTE ML810-VAR-END-INV-COST-AMT                                   
                   = ML810-ML-END-INV-COST-AMT                                  
                   - ML810-END-INV-COST-AMT.                                    
           COMPUTE ML810-VAR-SLS-COST-AMT = ML810-ML-SLS-COST-AMT               
                   - ML810-SLS-COST-AMT.                                        
           COMPUTE ML810-VAR-GRO-MRGN-AMT = ML810-ML-GRO-MRGN-AMT               
                   - ML810-GRO-MRGN-AMT.                                        
                                                                                
           WRITE MNDPT-WO-PB-GIVEBK-OUT-RECORD                                  
               FROM ML810-MNDPT-WO-PB-GIVEBACK.                                 
           ADD 1 TO PA-MNDPT-OUT-COUNT.                                         
                                                                                
           WRITE RECALC-OUTPUT-RECORD FROM ML812-PB-GIVEBACK-SMOOTH.            
           ADD 1 TO PA-RECALC-COUNT.                                            
                                                                                
      ******************************************************************        
      *    WRITE OUT REMAINING MNDPT RECORDS FOR DATES LESS THAN       *        
      *    RECALC FISCAL PERIODS                                       *        
      ******************************************************************        
       7502-WRITE-REMAIN-TMNDPT.                                                
           MOVE PT-FSCL-MN-NBR      (PT-MNDPT-TBL-INDX)                         
               TO ML810-FSCL-MN-NBR.                                            
           MOVE PT-FSCL-MN-END-DTE  (PT-MNDPT-TBL-INDX)                         
               TO ML810-FSCL-MN-END-DTE.                                        
           MOVE PT-DEPT-NBR         (PT-MNDPT-TBL-INDX)                         
               TO ML810-DEPT-NBR.                                               
           MOVE PT-SLS-RTL-AMT      (PT-MNDPT-TBL-INDX)                         
               TO ML810-SLS-RTL-AMT.                                            
           MOVE PT-MKDN-RTL-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-MKDN-RTL-AMT.                                           
           MOVE PT-MKUP-RTL-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-MKUP-RTL-AMT.                                           
           MOVE PT-XFER-IN-RTL-AMT  (PT-MNDPT-TBL-INDX)                         
               TO ML810-XFER-IN-RTL-AMT.                                        
           MOVE PT-XFER-OUT-RTL-AMT (PT-MNDPT-TBL-INDX)                         
               TO ML810-XFER-OUT-RTL-AMT.                                       
           MOVE PT-RCPT-RTL-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-RCPT-RTL-AMT.                                           
           MOVE PT-RCPT-NET-COST-AMT(PT-MNDPT-TBL-INDX)                         
               TO ML810-RCPT-NET-COST-AMT.                                      
           MOVE PT-PADJ-RTL-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-PADJ-RTL-AMT.                                           
           MOVE PT-PADJ-NET-COST-AMT(PT-MNDPT-TBL-INDX)                         
               TO ML810-PADJ-NET-COST-AMT.                                      
           MOVE PT-PADJ-GIVEBK-AMT(PT-MNDPT-TBL-INDX)                           
               TO ML810-PADJ-GIVEBK-AMT.                                        
           MOVE PT-INV-ADJ-RTL-AMT  (PT-MNDPT-TBL-INDX)                         
               TO ML810-INV-ADJ-RTL-AMT.                                        
           MOVE PT-SHNK-RTL-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-SHNK-RTL-AMT.                                           
           MOVE PT-END-INV-RTL-AMT  (PT-MNDPT-TBL-INDX)                         
               TO ML810-END-INV-RTL-AMT.                                        
           MOVE PT-CUM-MKUP-PCT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-CUM-MKUP-PCT.                                           
           MOVE PT-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                         
               TO ML810-END-INV-COST-AMT.                                       
           MOVE PT-SLS-COST-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-SLS-COST-AMT.                                           
           MOVE PT-GRO-MRGN-AMT     (PT-MNDPT-TBL-INDX)                         
               TO ML810-GRO-MRGN-AMT.                                           
                                                                                
           MOVE PT-ML-CUM-MKUP-PCT     (PT-MNDPT-TBL-INDX)                      
               TO ML810-ML-CUM-MKUP-PCT.                                        
           MOVE PT-ML-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                      
               TO ML810-ML-END-INV-COST-AMT.                                    
           MOVE PT-ML-SLS-COST-AMT     (PT-MNDPT-TBL-INDX)                      
               TO ML810-ML-SLS-COST-AMT.                                        
           MOVE PT-ML-GRO-MRGN-AMT     (PT-MNDPT-TBL-INDX)                      
               TO ML810-ML-GRO-MRGN-AMT.                                        
                                                                                
           MOVE PT-VAR-END-INV-COST-AMT (PT-MNDPT-TBL-INDX)                     
               TO ML810-VAR-END-INV-COST-AMT.                                   
           MOVE PT-VAR-SLS-COST-AMT     (PT-MNDPT-TBL-INDX)                     
               TO ML810-VAR-SLS-COST-AMT.                                       
           MOVE PT-VAR-GRO-MRGN-AMT     (PT-MNDPT-TBL-INDX)                     
               TO ML810-VAR-GRO-MRGN-AMT.                                       
                                                                                
           WRITE MNDPT-WO-PB-GIVEBK-OUT-RECORD                                  
               FROM ML810-MNDPT-WO-PB-GIVEBACK.                                 
           ADD 1 TO PA-MNDPT-OUT-COUNT.                                         
                                                                                
                                                                                
      ******************************************************************        
      * PERFORMED BY END OF JOB PROCESSING                                      
      ******************************************************************        
       7550-CLOSE-DEPT-CURSOR.                                                  
           EXEC SQL                                                             
               CLOSE DEPT_CSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7550-CLOSE-DEPT_CURSOR' TO PV-PARAGRAPH-NAME          
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7575-CLOSE-SEASON-CURSOR.                                                
                                                                                
           EXEC SQL                                                             
               CLOSE SEASON_TO_DATE_CSR                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED          
                   MOVE '7575-CLOSE-SEASON-CURSOR' TO PV-PARAGRAPH-NAME         
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * 7950-INSERT-CARRY-FWD-ROW                                               
      * PERFORMED IF NO ROW IS FOUND ON DB2 (FOR THIS PROCESSING MONTH)         
      * BUT INVENTORY DOLLARS (COST AND/OR RETAIL) FOUND FOR LAST MONTH         
      ******************************************************************        
       7950-INSERT-CARRY-FWD-ROW.                                               
                                                                                
                                                                                
           MOVE ZEROES                     TO MNDPT-SLS-RTL-AMT                 
                                              MNDPT-MKDN-RTL-AMT                
                                              MNDPT-MKUP-RTL-AMT                
                                              MNDPT-XFER-IN-RTL-AMT             
                                              MNDPT-XFER-OUT-RTL-AMT            
                                              MNDPT-RCPT-RTL-AMT                
                                              MNDPT-RCPT-NET-COST-AMT           
                                              MNDPT-PADJ-RTL-AMT                
                                              MNDPT-PADJ-NET-COST-AMT           
                                              MNDPT-INV-ADJ-RTL-AMT             
                                              MNDPT-SHNK-RTL-AMT                
                                              PV-CUM-MKUP-PCT                   
                                              PV-SLS-COST-AMT                   
                                              PV-GRO-MRGN-AMT                   
                                              MNDPT-CUM-MKUP-PCT                
                                              MNDPT-SLS-COST-AMT                
                                              MNDPT-GRO-MRGN-AMT.               
           MOVE PV-PREV-END-INV-RTL        TO PV-END-INV-RTL-AMT.               
           MOVE PV-PREV-END-INV-COST       TO PV-END-INV-COST-AMT.              
           MOVE PV-PREV-ML-END-INV-COST    TO MNDPT-END-INV-COST-AMT.           
                                                                                
           ADD +1                          TO PA-CARRY-FOWARD-COUNT.            
           PERFORM 7501-OUTPUT-TMNDPT-DATA.                                     
                                                                                
                                                                                
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 7550-CLOSE-DEPT-CURSOR.                                      
                                                                                
           MOVE PV-HOLD-DATA-RETENTION-DTE TO PV-SRCH-FSCL-MN-END-DTE.          
           MOVE ZEROES                     TO PV-SRCH-DEPT-NBR.                 
           PERFORM 5600-SEARCH-MNDPT.                                           
           IF PS-MNDPT-TBL-FOUND                                                
               DISPLAY '2 YEARS AGO DATE FOUND; SAVE ONLY 2 YRS OF DATA'        
           ELSE                                                                 
               MOVE +1         TO PT-MNDPT-TBL-INDX                             
           END-IF.                                                              
           IF PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-INDX)                             
               >= PV-HOLD-FISCAL-MN-END-DTE                                     
               DISPLAY 'TMNDPT RECS DATE RANGE '                                
                       PV-HOLD-FISCAL-MN-END-DTE                                
                       ' - ' PV-HOLD-FISCAL-MN-END-DTE                          
           ELSE                                                                 
               DISPLAY 'TMNDPT RECS DATE RANGE '                                
                       PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-INDX)                    
                       ' - ' PV-HOLD-FISCAL-MN-END-DTE                          
           END-IF.                                                              
           PERFORM                                                              
               UNTIL PT-MNDPT-TBL-INDX > PT-MNDPT-TBL-COUNT                     
                   OR PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-INDX)                     
                       >= PV-HOLD-FISCAL-MN-END-DTE                             
               IF PT-FSCL-MN-END-DTE(PT-MNDPT-TBL-INDX)                         
                   < PV-HOLD-FISCAL-MN-END-DTE                                  
                   PERFORM 7502-WRITE-REMAIN-TMNDPT                             
               END-IF                                                           
               ADD +1          TO PT-MNDPT-TBL-INDX                             
           END-PERFORM.                                                         
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'DEPTS PROCESSED      ' PA-DEPT-COUNT.                       
           DISPLAY 'DATE CNTL RECS READ  ' PA-DATE-CNTL-COUNT.                  
           DISPLAY 'GIVEBACK ACTUAL READ ' PA-GIVEBACK-COUNT.                   
           DISPLAY 'GIVEBACK ACTL LOADED ' PA-GIVEBACK-LOADED.                  
           DISPLAY 'MNDPT W/O GIVEBK IN  ' PA-MNDPT-IN-COUNT.                   
           DISPLAY 'CARRY FORWARD ROWS   ' PA-CARRY-FOWARD-COUNT.               
           DISPLAY 'RECALC RECS WRITTEN  ' PA-RECALC-COUNT.                     
           DISPLAY 'CMU% OVERFLOW COUNT  ' PA-CUM-MKUP-MAX-LIMIT.               
           DISPLAY 'MNDPT W/O GIVEBK OUT ' PA-MNDPT-OUT-COUNT.                  
           DISPLAY ' '.                                                         
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
                                                                                
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
