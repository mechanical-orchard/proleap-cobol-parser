00001  IDENTIFICATION DIVISION.                                         06/07/95
00002  PROGRAM-ID.    ETKUP044.                                         ETKUP044
00003  AUTHOR.        JOHN OBERHAUSER - CPU.                               LV001
00004  INSTALLATION.  KOHLS DEPARTMENT STORES.                          ETKUP044
00005  DATE-WRITTEN.  APRIL-95.                                         ETKUP044
00006  DATE-COMPILED.                                                   ETKUP044
00007 ******************************************************************ETKUP044
00008 *     ETKUP044 - EXTRACT DOCUMENTS TO BE RELEASED                *ETKUP044
00009 *                                                                *ETKUP044
00010 *     THIS PROGRAM WILL DETERMINE WHICH EDI DOCUMENTS ARE TO     *ETKUP044
00011 *     BE RELEASED FROM THE EDI ON-HOLD TABLES AND WRITE THE      *ETKUP044
00012 *     DATA FOR THOSE DOCUMENTS TO A TEMPORARY DB2 TABLE.         *ETKUP044
00013 *     THE PROGRAM WILL BUMP UP AGAINST THE DOCUMENT ON HOLD      *ETKUP044
00014 *     TABLE (TFGTRHL) TO DETERMINE THE DOCUMENTS TO BE RELEASED. *ETKUP044
00015 *     IT WILL THEN EXTRACT THE DATA FOR THOSE DOCUMENTS FROM     *ETKUP044
00016 *     THE DOCUMENT ON HOLD DATA TABLE (TFGTRDT) AND WRITE IT TO  *ETKUP044
00017 *     A TEMPORARY DB (TOUTXXX) WHERE XXX = THE TYPE OF THE EDI   *ETKUP044
00018 *     TRANSATION SET.  AFTER A ROW IS EXTRACTED, IT IS DELETED   *ETKUP044
00019 *     FROM THE TABLE.  AFTER ALL ROWS ARE EXTRACTED, THE         *ETKUP044
00020 *     P.O. INFORMATION FOR THAT DOCUMENT IS DELETED FROM THE     *ETKUP044
00021 *     P.O.'S ON HOLD TABLE (TTRPOHL) AND THE STATUS OF THE       *ETKUP044
00022 *     DOCUMENT ON HOLD IS UPDATED (TFGTRHL).                     *ETKUP044
00023 ******************************************************************ETKUP044
00024 *                                                                 ETKUP044
00025  ENVIRONMENT DIVISION.                                            ETKUP044
00026 *                                                                 ETKUP044
00027 *                                                                 ETKUP044
00028  CONFIGURATION SECTION.                                           ETKUP044
00029 *                                                                 ETKUP044
00030  SOURCE-COMPUTER.        IBM-3090.                                ETKUP044
00031  OBJECT-COMPUTER.        IBM-3090.                                ETKUP044
00032 *                                                                 ETKUP044
00033 *                                                                 ETKUP044
00034  INPUT-OUTPUT SECTION.                                            ETKUP044
00035  FILE-CONTROL.                                                    ETKUP044
00036                                                                   ETKUP044
00037 ******************************************************************ETKUP044
00038  DATA DIVISION.                                                   ETKUP044
00039 *                                                                 ETKUP044
00040 *                                                                 ETKUP044
00041  FILE SECTION.                                                    ETKUP044
00042                                                                   ETKUP044
00043 ******************************************************************ETKUP044
00044  WORKING-STORAGE SECTION.                                         ETKUP044
00045 ******************************************************************ETKUP044
00046 *                                                                 ETKUP044
00047 *                                                                 ETKUP044
00048  01  ABEND-CODE                      PIC S9(04)      VALUE ZERO   ETKUP044
00049                                                      COMP SYNC.   ETKUP044
00050 *                                                                 ETKUP044
00051 *                                                                 ETKUP044
00052  01  WS-WORKING-STORAGE.                                          ETKUP044
00053      05  WS-TODAYS-DATE.                                          ETKUP044
00054          10 WS-TODAYS-YY              PIC X(02) VALUE SPACES.     ETKUP044
00055          10 WS-TODAYS-MM              PIC X(02) VALUE SPACES.     ETKUP044
00056          10 WS-TODAYS-DD              PIC X(02) VALUE SPACES.     ETKUP044
00057      05  WS-TODAYS-DATE-ISO.                                      ETKUP044
00058          10 WS-TODAYS-ISO-CC          PIC X(02) VALUE SPACES.     ETKUP044
00059          10 WS-TODAYS-ISO-YY          PIC X(02) VALUE SPACES.     ETKUP044
00060          10 FILLER                    PIC X(01) VALUE '-'.        ETKUP044
00061          10 WS-TODAYS-ISO-MM          PIC X(02) VALUE SPACES.     ETKUP044
00062          10 FILLER                    PIC X(01) VALUE '-'.        ETKUP044
00063          10 WS-TODAYS-ISO-DD          PIC X(02) VALUE SPACES.     ETKUP044
00064      05  WS-COMPARE-DATE              PIC X(10) VALUE SPACES.     ETKUP044
00065      05  WS-SAVE-DATE-TIME            PIC X(26) VALUE SPACES.     ETKUP044
00066      05  WS-CURR-DATE-TIME.                                       ETKUP044
00067          10  WS-CURR-DATE             PIC X(10) VALUE SPACES.     ETKUP044
00068          10  WS-FILLER                PIC X(01) VALUE SPACES.     ETKUP044
00069          10  WS-CURR-TIME             PIC X(15) VALUE SPACES.     ETKUP044
00070      05  WS-FGTRHL-TIMESTAMP.                                     ETKUP044
00071          10  WS-FGTRHL-DATE           PIC X(10) VALUE SPACES.     ETKUP044
00072          10  WS-FGTRHL-DASH           PIC X(01) VALUE SPACES.     ETKUP044
00073          10  WS-FGTRHL-TIME           PIC X(15) VALUE SPACES.     ETKUP044
00074      05  WS-UPDATE-ROW-SW             PIC X(01) VALUE SPACES.     ETKUP044
00075          88  WS-UPDATE-ROW                      VALUE 'Y'.        ETKUP044
00076          88  WS-NO-UPDATE-ROW                   VALUE 'N'.        ETKUP044
00077      05  WS-855-SEQ-NBR               PIC S9(9) VALUE +0          ETKUP044
00078                                                 COMP.             ETKUP044
00079      05  WS-856-SEQ-NBR               PIC S9(9) VALUE +0          ETKUP044
00080                                                 COMP.             ETKUP044
00081      05  WS-810-SEQ-NBR               PIC S9(9) VALUE +0          ETKUP044
00082                                                 COMP.             ETKUP044
00083      05  WS-WS-DESC                   PIC X(4022).                ETKUP044
00084      05  FILLER REDEFINES WS-WS-DESC.                             ETKUP044
00085          10  WS-RESTART-SEQ-NBR       PIC 9(09).                  ETKUP044
00086          10  FILLER                   PIC X(4013).                ETKUP044
00087      05  WS-USER-ID                   PIC X(08).                  ETKUP044
00088                                                                   ETKUP044
00089  01  PC-PROGRAM-CONSTANTS.                                        ETKUP044
00090      05  PC-CALENDAR-ROUTINE          PIC  X(08)    VALUE         ETKUP044
00091                                                     'DPKUT500'.   ETKUP044
00092 *                                                                 ETKUP044
00093 * CALENDAR ROUTINE WORK AREA.                                     ETKUP044
00094      COPY DPWS500.                                                ETKUP044
00095                                                                   ETKUP044
00096 *                                                                 ETKUP044
00097 *  DB2 CHECKPOINT RESTART INFORMATION TABLE                       ETKUP044
00098      EXEC SQL                                                     ETKUP044
00099          INCLUDE TCKRSTIN                                         ETKUP044
00100      END-EXEC.                                                    ETKUP044
00101 *                                                                 ETKUP044
00102 *  TEMPORARY ASN (ADVANCE SHIP NOTICE) DATA TABLE                 ETKUP044
00103      EXEC SQL                                                     ETKUP044
00104          INCLUDE TOUT856                                          ETKUP044
00105      END-EXEC.                                                    ETKUP044
00106 *                                                                 ETKUP044
00107 *  TEMPORARY INVOICE DATA TABLE                                   ETKUP044
00108      EXEC SQL                                                     ETKUP044
00109          INCLUDE TOUT810                                          ETKUP044
00110      END-EXEC.                                                    ETKUP044
00111 *                                                                 ETKUP044
00112 *  TEMPORARY P.O. DATA TABLE                                      ETKUP044
00113      EXEC SQL                                                     ETKUP044
00114          INCLUDE TOUT855                                          ETKUP044
00115      END-EXEC.                                                    ETKUP044
00116 *                                                                 ETKUP044
00117 *  DOCUMENTS ON HOLD TABLE                                        ETKUP044
00118      EXEC SQL                                                     ETKUP044
00119          INCLUDE TFGTRHL                                          ETKUP044
00120      END-EXEC.                                                    ETKUP044
00121 *                                                                 ETKUP044
00122 *  DOCUMENT P.O.'S ON HOLD TABLE                                  ETKUP044
00123      EXEC SQL                                                     ETKUP044
00124          INCLUDE TTRPOHL                                          ETKUP044
00125      END-EXEC.                                                    ETKUP044
00126 *                                                                 ETKUP044
00127 *  DOCUMENT ON HOLD DETAIL DATA TABLE                             ETKUP044
00128      EXEC SQL                                                     ETKUP044
00129          INCLUDE TFGTRDT                                          ETKUP044
00130      END-EXEC.                                                    ETKUP044
00131 *                                                                 ETKUP044
00132 *  DB2 COMMUNICATION AREA                                         ETKUP044
00133 *                                                                 ETKUP044
00134      EXEC SQL                                                     ETKUP044
00135         INCLUDE SQLCA                                             ETKUP044
00136      END-EXEC.                                                    ETKUP044
00137 *                                                                 ETKUP044
00138 *  DB2 COMMUNICATION AREA 2                                       ETKUP044
00139 *                                                                 ETKUP044
00140      COPY SQLCA2.                                                 ETKUP044
00141 *                                                                 ETKUP044
00142 *  DB2 DOCUMENT CURSOR                                            ETKUP044
00143 *  SELECTS DOCUMENTS THAT ARE TO BE RELEASED                      ETKUP044
00144 *                                                                 ETKUP044
00145      EXEC SQL                                                     ETKUP044
00146         DECLARE DOC_CURSOR CURSOR WITH HOLD FOR                   ETKUP044
00147           SELECT TP_DKEY,                                         ETKUP044
00148                  TP_DKEY_TMST,                                    ETKUP044
00149                  TRN_SET_CDE,                                     ETKUP044
00150                  HOLD_DTE,                                        ETKUP044
00151                  RLSE_IND,                                        ETKUP044
00152                  USER_ID                                          ETKUP044
00153             FROM TFGTRHL                                          ETKUP044
00154             WHERE (RLSE_IND = 'Y'           AND                   ETKUP044
00155                    RLSE_DTE = '9999-09-09')  OR                   ETKUP044
00156                   (RLSE_IND = 'N')                                ETKUP044
00157             FOR UPDATE OF RLSE_DTE,                               ETKUP044
00158                           RLSE_IND,                               ETKUP044
00159                           USER_ID                                 ETKUP044
00160      END-EXEC.                                                    ETKUP044
00161 *                                                                 ETKUP044
00162 *  DB2 DOCUMENT DATA CURSOR                                       ETKUP044
00163 *  SELECTS DOCUMENT DATA THAT IS TO BE RELEASED                   ETKUP044
00164 *                                                                 ETKUP044
00165      EXEC SQL                                                     ETKUP044
00166         DECLARE DOC_DATA_CURSOR CURSOR FOR                        ETKUP044
00167           SELECT TRN_SET_DATA,                                    ETKUP044
00168                  TRN_SET_SEQ_NBR                                  ETKUP044
00169             FROM TFGTRDT                                          ETKUP044
00170             WHERE TP_DKEY      = :FGTRHL-TP-DKEY                  ETKUP044
00171               AND TP_DKEY_TMST = :FGTRHL-TP-DKEY-TMST             ETKUP044
00172             ORDER BY TRN_SET_SEQ_NBR                              ETKUP044
00173      END-EXEC.                                                    ETKUP044
00174 *                                                                 ETKUP044
00175 *                                                                 ETKUP044
00176  PROCEDURE DIVISION.                                              ETKUP044
00177 *                                                                 ETKUP044
00178  A100-MAIN-MODULE.                                                ETKUP044
00179                                                                   ETKUP044
00180 ******************************************************************ETKUP044
00181 *    OPEN THE DOCUMENTS ON HOLD CURSOR                            ETKUP044
00182 *    DO SOME HOUSEKEEPING.                                        ETKUP044
00183 *    PROCESS ALL DOCUMENTS ON HOLD AND DETERMINE IF IT IS TO BE   ETKUP044
00184 *    RELEASED.                                                    ETKUP044
00185 *    CLOSE THE DOCUMENTS ON HOLD CURSOR.                          ETKUP044
00186 *    FINISH UP HOUSEKEEPING.                                      ETKUP044
00187 ******************************************************************ETKUP044
00188                                                                   ETKUP044
00189      PERFORM Z000-OPEN-DOC-CURSOR.                                ETKUP044
00190                                                                   ETKUP044
00191      PERFORM B100-INITIALIZE-DUTIES.                              ETKUP044
00192                                                                   ETKUP044
00193      PERFORM B200-DETERMINE-RLSE-TYPE                             ETKUP044
00194          UNTIL SQLCODE = +100.                                    ETKUP044
00195                                                                   ETKUP044
00196      PERFORM Z500-CLOSE-DOC-CURSOR.                               ETKUP044
00197                                                                   ETKUP044
00198      PERFORM B300-END-DUTIES.                                     ETKUP044
00199                                                                   ETKUP044
00200      STOP RUN.                                                    ETKUP044
00201                                                                   ETKUP044
00202  B100-INITIALIZE-DUTIES.                                          ETKUP044
00203                                                                   ETKUP044
00204 ******************************************************************ETKUP044
00205 *    GET TODAYS DATE AND TIME                                     ETKUP044
00206 *    READ THE CHECKPOINT WORKING STORAGE TABLE TO DETERMINE THE   ETKUP044
00207 *    BEGINNING SEQUENCE #'S TO USE WHEN WRITING TO THE TEMPORARY  ETKUP044
00208 *    TABLES (TOUTXXX).                                            ETKUP044
00209 *    FETCH THE 1ST DOCUMENT ON HOLD                               ETKUP044
00210 *                                                                 ETKUP044
00211 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP044
00212 ******************************************************************ETKUP044
00213                                                                   ETKUP044
00214      EXEC SQL                                                     ETKUP044
00215          SET :WS-SAVE-DATE-TIME = CURRENT TIMESTAMP               ETKUP044
00216      END-EXEC.                                                    ETKUP044
00217                                                                   ETKUP044
00218      MOVE WS-SAVE-DATE-TIME TO WS-CURR-DATE-TIME.                 ETKUP044
00219                                                                   ETKUP044
00220 **** DETERMINE CENTURY ****                                       ETKUP044
00221                                                                   ETKUP044
00222      ACCEPT WS-TODAYS-DATE FROM DATE.                             ETKUP044
00223      IF WS-TODAYS-YY > '94'                                       ETKUP044
00224          MOVE '19' TO WS-TODAYS-ISO-CC                            ETKUP044
00225      ELSE                                                         ETKUP044
00226          MOVE '20' TO WS-TODAYS-ISO-CC                            ETKUP044
00227      END-IF.                                                      ETKUP044
00228      MOVE WS-TODAYS-YY    TO WS-TODAYS-ISO-YY.                    ETKUP044
00229      MOVE WS-TODAYS-MM    TO WS-TODAYS-ISO-MM.                    ETKUP044
00230      MOVE WS-TODAYS-DD    TO WS-TODAYS-ISO-DD.                    ETKUP044
00231                                                                   ETKUP044
00232      EXEC SQL                                                     ETKUP044
00233          SELECT WORK_STRG_DESC                                    ETKUP044
00234          INTO :TCKRSTINF-WORK-STRG-DESC                           ETKUP044
00235          FROM TCKRSTINF                                           ETKUP044
00236          WHERE PLAN_NAME = 'ETKUP044' AND                         ETKUP044
00237                SEQ_NMBR  = 1                                      ETKUP044
00238      END-EXEC.                                                    ETKUP044
00239                                                                   ETKUP044
00240      IF SQLCODE = +000                                            ETKUP044
00241          MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO WS-WS-DESC         ETKUP044
00242          MOVE WS-RESTART-SEQ-NBR            TO WS-810-SEQ-NBR     ETKUP044
00243                                                                   ETKUP044
00244          EXEC SQL                                                 ETKUP044
00245              SELECT WORK_STRG_DESC                                ETKUP044
00246              INTO :TCKRSTINF-WORK-STRG-DESC                       ETKUP044
00247              FROM TCKRSTINF                                       ETKUP044
00248              WHERE PLAN_NAME = 'ETKUP044' AND                     ETKUP044
00249                    SEQ_NMBR  = 2                                  ETKUP044
00250          END-EXEC                                                 ETKUP044
00251                                                                   ETKUP044
00252          IF SQLCODE = +000                                        ETKUP044
00253              MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO WS-WS-DESC     ETKUP044
00254              MOVE WS-RESTART-SEQ-NBR            TO WS-855-SEQ-NBR ETKUP044
00255                                                                   ETKUP044
00256              EXEC SQL                                             ETKUP044
00257                  SELECT WORK_STRG_DESC                            ETKUP044
00258                  INTO :TCKRSTINF-WORK-STRG-DESC                   ETKUP044
00259                  FROM TCKRSTINF                                   ETKUP044
00260                  WHERE PLAN_NAME = 'ETKUP044' AND                 ETKUP044
00261                        SEQ_NMBR  = 3                              ETKUP044
00262              END-EXEC                                             ETKUP044
00263                                                                   ETKUP044
00264              IF SQLCODE = +000                                    ETKUP044
00265                  MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO            ETKUP044
00266                       WS-WS-DESC                                  ETKUP044
00267                  MOVE WS-RESTART-SEQ-NBR            TO            ETKUP044
00268                       WS-856-SEQ-NBR                              ETKUP044
00269              ELSE                                                 ETKUP044
00270                  DISPLAY '** ABEND **'                            ETKUP044
00271                  DISPLAY '** B100-INITIALIZE-DUTIES **'           ETKUP044
00272                  DISPLAY '** SELECT TCKRSTINF SEQ # 3 **'         ETKUP044
00273                  PERFORM Z900-SQL-ERROR                           ETKUP044
00274              END-IF                                               ETKUP044
00275                                                                   ETKUP044
00276          ELSE                                                     ETKUP044
00277              DISPLAY '** ABEND **'                                ETKUP044
00278              DISPLAY '** B100-INITIALIZE-DUTIES **'               ETKUP044
00279              DISPLAY '** SELECT TCKRSTINF SEQ # 2 **'             ETKUP044
00280              PERFORM Z900-SQL-ERROR                               ETKUP044
00281          END-IF                                                   ETKUP044
00282                                                                   ETKUP044
00283      ELSE                                                         ETKUP044
00284          DISPLAY '** ABEND **'                                    ETKUP044
00285          DISPLAY '** B100-INITIALIZE-DUTIES **'                   ETKUP044
00286          DISPLAY '** SELECT TCKRSTINF SEQ # 1 **'                 ETKUP044
00287          PERFORM Z900-SQL-ERROR                                   ETKUP044
00288      END-IF.                                                      ETKUP044
00289                                                                   ETKUP044
00290      PERFORM Z100-FETCH-DOC-CURSOR.                               ETKUP044
00291                                                                   ETKUP044
00292  B200-DETERMINE-RLSE-TYPE.                                        ETKUP044
00293                                                                   ETKUP044
00294 ******************************************************************ETKUP044
00295 *    IF THE RELEASE INDICATOR IS SET TO 'Y' AND THE RELEASE DATE  ETKUP044
00296 *    HAS NOT BEEN FILLED IN YET, GO AHEAD AND RELEASE THE DOCUMENTETKUP044
00297 *                                                                 ETKUP044
00298 *    IF THE RELEASE INDICATOR IS SET TO 'N', DETERMINE IT IS TIME ETKUP044
00299 *    TO RELEASE IT ANYWAY BASED ON HOW LONG IT HAS BEEN ON HOLD.  ETKUP044
00300 *                                                                 ETKUP044
00301 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP044
00302 ******************************************************************ETKUP044
00303                                                                   ETKUP044
00304      IF FGTRHL-RLSE-IND = 'Y'                                     ETKUP044
00305          PERFORM Z600-OPEN-DOC-DATA-CURSOR                        ETKUP044
00306          PERFORM Z700-FETCH-DOC-DATA-CURSOR                       ETKUP044
00307                                                                   ETKUP044
00308          PERFORM C100-PROCESS-DOCUMENT-DATA                       ETKUP044
00309              UNTIL SQLCODE = +100                                 ETKUP044
00310                                                                   ETKUP044
00311          PERFORM Z800-CLOSE-DOC-DATA-CURSOR                       ETKUP044
00312                                                                   ETKUP044
00313          PERFORM Z300-DEL-TRPOHL-ROW                              ETKUP044
00314          PERFORM Z400-UPDATE-DOC-ROW                              ETKUP044
00315          PERFORM Z850-UPDATE-CHKPT-ROWS                           ETKUP044
00316      ELSE                                                         ETKUP044
00317          MOVE FGTRHL-TP-DKEY-TMST TO WS-FGTRHL-TIMESTAMP          ETKUP044
00318                                                                   ETKUP044
00319          SET WS-NO-UPDATE-ROW TO TRUE                             ETKUP044
00320          PERFORM C200-CALENDAR                                    ETKUP044
00321                                                                   ETKUP044
00322          IF WS-UPDATE-ROW                                         ETKUP044
00323              PERFORM Z600-OPEN-DOC-DATA-CURSOR                    ETKUP044
00324              PERFORM Z700-FETCH-DOC-DATA-CURSOR                   ETKUP044
00325                                                                   ETKUP044
00326              PERFORM C100-PROCESS-DOCUMENT-DATA                   ETKUP044
00327                  UNTIL SQLCODE = +100                             ETKUP044
00328                                                                   ETKUP044
00329              PERFORM Z800-CLOSE-DOC-DATA-CURSOR                   ETKUP044
00330                                                                   ETKUP044
00331              PERFORM Z300-DEL-TRPOHL-ROW                          ETKUP044
00332              PERFORM Z400-UPDATE-DOC-ROW                          ETKUP044
00333              PERFORM Z850-UPDATE-CHKPT-ROWS                       ETKUP044
00334          END-IF                                                   ETKUP044
00335      END-IF.                                                      ETKUP044
00336                                                                   ETKUP044
00337      PERFORM Z100-FETCH-DOC-CURSOR.                               ETKUP044
00338                                                                   ETKUP044
00339  B300-END-DUTIES.                                                 ETKUP044
00340                                                                   ETKUP044
00341 ******************************************************************ETKUP044
00342 *    UPDATE THE CHECKPOINT WORKING STORAGE TABLES TO INDICATE     ETKUP044
00343 *    THAT THE NEXT SEQUENCE NUMBERS SHOULD START FROM 0.          ETKUP044
00344 *                                                                 ETKUP044
00345 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP044
00346 ******************************************************************ETKUP044
00347                                                                   ETKUP044
00348      MOVE +9          TO TCKRSTINF-WORK-STRG-DESC-LEN.            ETKUP044
00349      MOVE '000000000' TO TCKRSTINF-WORK-STRG-DESC-TEXT.           ETKUP044
00350                                                                   ETKUP044
00351      EXEC SQL                                                     ETKUP044
00352          UPDATE TCKRSTINF                                         ETKUP044
00353          SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC           ETKUP044
00354          WHERE PLAN_NAME = 'ETKUP044' AND                         ETKUP044
00355                SEQ_NMBR  = 1                                      ETKUP044
00356      END-EXEC.                                                    ETKUP044
00357                                                                   ETKUP044
00358      IF SQLCODE = +000                                            ETKUP044
00359          EXEC SQL                                                 ETKUP044
00360              UPDATE TCKRSTINF                                     ETKUP044
00361              SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC       ETKUP044
00362              WHERE PLAN_NAME = 'ETKUP044' AND                     ETKUP044
00363                    SEQ_NMBR  = 2                                  ETKUP044
00364          END-EXEC                                                 ETKUP044
00365                                                                   ETKUP044
00366          IF SQLCODE = +000                                        ETKUP044
00367              EXEC SQL                                             ETKUP044
00368                  UPDATE TCKRSTINF                                 ETKUP044
00369                  SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC   ETKUP044
00370                  WHERE PLAN_NAME = 'ETKUP044' AND                 ETKUP044
00371                        SEQ_NMBR  = 3                              ETKUP044
00372              END-EXEC                                             ETKUP044
00373                                                                   ETKUP044
00374              IF SQLCODE = +000                                    ETKUP044
00375                  CONTINUE                                         ETKUP044
00376              ELSE                                                 ETKUP044
00377                  DISPLAY '** ABEND **'                            ETKUP044
00378                  DISPLAY '** B100-INITIALIZE-DUTIES **'           ETKUP044
00379                  DISPLAY '** UPDATE TCKRSTINF SEQ # 3 **'         ETKUP044
00380                  PERFORM Z900-SQL-ERROR                           ETKUP044
00381              END-IF                                               ETKUP044
00382                                                                   ETKUP044
00383          ELSE                                                     ETKUP044
00384              DISPLAY '** ABEND **'                                ETKUP044
00385              DISPLAY '** B100-INITIALIZE-DUTIES **'               ETKUP044
00386              DISPLAY '** UPDATE TCKRSTINF SEQ # 2 **'             ETKUP044
00387              PERFORM Z900-SQL-ERROR                               ETKUP044
00388          END-IF                                                   ETKUP044
00389                                                                   ETKUP044
00390      ELSE                                                         ETKUP044
00391          DISPLAY '** ABEND **'                                    ETKUP044
00392          DISPLAY '** B100-INITIALIZE-DUTIES **'                   ETKUP044
00393          DISPLAY '** UPDATE TCKRSTINF SEQ # 1 **'                 ETKUP044
00394          PERFORM Z900-SQL-ERROR                                   ETKUP044
00395      END-IF.                                                      ETKUP044
00396                                                                   ETKUP044
00397  C100-PROCESS-DOCUMENT-DATA.                                      ETKUP044
00398                                                                   ETKUP044
00399 ******************************************************************ETKUP044
00400 *    WRITE THE DOCUMENT DATA TO THE APPROPRIATE TEMPORARY DB.     ETKUP044
00401 *                                                                 ETKUP044
00402 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE                        ETKUP044
00403 ******************************************************************ETKUP044
00404                                                                   ETKUP044
00405      IF FGTRHL-TRN-SET-CDE = '855'                                ETKUP044
00406          ADD +1 TO WS-855-SEQ-NBR                                 ETKUP044
00407          PERFORM D100-INSERT-PO                                   ETKUP044
00408      ELSE                                                         ETKUP044
00409          IF FGTRHL-TRN-SET-CDE = '810'                            ETKUP044
00410              ADD +1 TO WS-810-SEQ-NBR                             ETKUP044
00411              PERFORM D200-INSERT-INVOICE                          ETKUP044
00412          ELSE                                                     ETKUP044
00413              IF FGTRHL-TRN-SET-CDE = '856'                        ETKUP044
00414                  ADD +1 TO WS-856-SEQ-NBR                         ETKUP044
00415                  PERFORM D300-INSERT-ASN                          ETKUP044
00416              ELSE                                                 ETKUP044
00417                  DISPLAY 'UNDEFINED TRN-SET-CDE ON FGTRHL:',      ETKUP044
00418                           FGTRHL-TRN-SET-CDE                      ETKUP044
00419              END-IF                                               ETKUP044
00420          END-IF                                                   ETKUP044
00421      END-IF.                                                      ETKUP044
00422                                                                   ETKUP044
00423      PERFORM Z700-FETCH-DOC-DATA-CURSOR.                          ETKUP044
00424                                                                   ETKUP044
00425  C200-CALENDAR.                                                   ETKUP044
00426                                                                   ETKUP044
00427 ******************************************************************ETKUP044
00428 *    IF TRN-SET-CDE = 855 OR 856, ADD 3 BUSINESS DAYS TO THE      ETKUP044
00429 *    DATE.  IF TRN-SET-CDE = 810, ADD 5 BUSINESS DAYS TO THE      ETKUP044
00430 *    DATE.... USED TO COMPARE TO TODAYS DATE TO DETERMINE IF      ETKUP044
00431 *    ENOUGH TIME HAS PASSED WHERE THESE ROWS SHOULD HAVE BEEN     ETKUP044
00432 *    FIXED.                                                       ETKUP044
00433 *                                                                 ETKUP044
00434 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE                        ETKUP044
00435 ******************************************************************ETKUP044
00436                                                                   ETKUP044
00437      IF FGTRHL-TRN-SET-CDE = '855'                                ETKUP044
00438                           OR '856'                                ETKUP044
00439          INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56           ETKUP044
VF0604         MOVE 10   TO DPG51-INCR-DECR-BUS-DAYS-9                  ETKUP044
00441          PERFORM D400-INCREMENT-DATE                              ETKUP044
00442      ELSE                                                         ETKUP044
00443          INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56           ETKUP044
VF0604         MOVE 12   TO DPG51-INCR-DECR-BUS-DAYS-9                  ETKUP044
00445          PERFORM D400-INCREMENT-DATE                              ETKUP044
00446      END-IF.                                                      ETKUP044
00447                                                                   ETKUP044
00448      IF WS-TODAYS-DATE-ISO > WS-COMPARE-DATE                      ETKUP044
00449          SET WS-UPDATE-ROW TO TRUE                                ETKUP044
00450      ELSE                                                         ETKUP044
00451          IF WS-TODAYS-DATE-ISO = WS-COMPARE-DATE                  ETKUP044
00452              DISPLAY 'WS-CURR-TIME   ' WS-CURR-TIME               ETKUP044
00453              DISPLAY 'WS-FGTRHL-TIME ' WS-FGTRHL-TIME             ETKUP044
00454              IF WS-CURR-TIME >= WS-FGTRHL-TIME                    ETKUP044
00455                  SET WS-UPDATE-ROW TO TRUE                        ETKUP044
00456              END-IF                                               ETKUP044
00457          END-IF                                                   ETKUP044
00458      END-IF.                                                      ETKUP044
00459                                                                   ETKUP044
00460  D100-INSERT-PO.                                                  ETKUP044
00461                                                                   ETKUP044
00462 ******************************************************************ETKUP044
00463 *    INSERT A ROW TO THE TOUT855 TABLE AND DELETE THE SAME ROW    ETKUP044
00464 *    FROM THE TFGTRDT TABLE.                                      ETKUP044
00465 *                                                                 ETKUP044
00466 *    PERFORMED BY C100-PROCESS-DOCUMENT-DATA.                     ETKUP044
00467 ******************************************************************ETKUP044
00468                                                                   ETKUP044
00469      EXEC SQL                                                     ETKUP044
00470           INSERT INTO TOUT855                                     ETKUP044
00471                 (SEQ_NBR,                                         ETKUP044
00472                  TRN_SET_DATA)                                    ETKUP044
00473           VALUES                                                  ETKUP044
00474                (:WS-855-SEQ-NBR,                                  ETKUP044
00475                 :FGTRDT-TRN-SET-DATA)                             ETKUP044
00476      END-EXEC.                                                    ETKUP044
00477                                                                   ETKUP044
00478      IF SQLCODE < 0                                               ETKUP044
00479          DISPLAY '** ABEND **'                                    ETKUP044
00480          DISPLAY '** INSERT ERROR ON TOUT855 TABLE **'            ETKUP044
00481          PERFORM Z900-SQL-ERROR                                   ETKUP044
00482      ELSE                                                         ETKUP044
00483          IF SQLCODE > +100                                        ETKUP044
00484              DISPLAY '** ABEND **'                                ETKUP044
00485              DISPLAY '** INSERT WARNING ON TOUT855 TABLE'         ETKUP044
00486              PERFORM Z900-SQL-WARNING                             ETKUP044
00487          END-IF                                                   ETKUP044
00488      END-IF.                                                      ETKUP044
00489                                                                   ETKUP044
00490      PERFORM Z200-DEL-DOC-DATA-ROW.                               ETKUP044
00491                                                                   ETKUP044
00492  D200-INSERT-INVOICE.                                             ETKUP044
00493                                                                   ETKUP044
00494 ******************************************************************ETKUP044
00495 *    INSERT A ROW TO THE TOUT810 TABLE AND DELETE THE SAME ROW    ETKUP044
00496 *    FROM THE TFGTRDT TABLE.                                      ETKUP044
00497 *                                                                 ETKUP044
00498 *    PERFORMED BY C100-PROCESS-DOCUMENT-DATA.                     ETKUP044
00499 ******************************************************************ETKUP044
00500                                                                   ETKUP044
00501      EXEC SQL                                                     ETKUP044
00502           INSERT INTO TOUT810                                     ETKUP044
00503                 (SEQ_NBR,                                         ETKUP044
00504                  TRN_SET_DATA)                                    ETKUP044
00505           VALUES                                                  ETKUP044
00506                (:WS-810-SEQ-NBR,                                  ETKUP044
00507                 :FGTRDT-TRN-SET-DATA)                             ETKUP044
00508      END-EXEC.                                                    ETKUP044
00509                                                                   ETKUP044
00510      IF SQLCODE < 0                                               ETKUP044
00511          DISPLAY '** ABEND **'                                    ETKUP044
00512          DISPLAY '** INSERT ERROR ON TOUT810 TABLE **'            ETKUP044
00513          PERFORM Z900-SQL-ERROR                                   ETKUP044
00514      ELSE                                                         ETKUP044
00515          IF SQLCODE > +100                                        ETKUP044
00516              DISPLAY '** ABEND **'                                ETKUP044
00517              DISPLAY '** INSERT WARNING ON TOUT810 TABLE'         ETKUP044
00518              PERFORM Z900-SQL-WARNING                             ETKUP044
00519          END-IF                                                   ETKUP044
00520      END-IF.                                                      ETKUP044
00521                                                                   ETKUP044
00522      PERFORM Z200-DEL-DOC-DATA-ROW.                               ETKUP044
00523                                                                   ETKUP044
00524  D300-INSERT-ASN.                                                 ETKUP044
00525                                                                   ETKUP044
00526 ******************************************************************ETKUP044
00527 *    INSERT A ROW TO THE TOUT856 TABLE AND DELETE THE SAME ROW    ETKUP044
00528 *    FROM THE TFGTRDT TABLE.                                      ETKUP044
00529 *                                                                 ETKUP044
00530 *    PERFORMED BY C100-PROCESS-DOCUMENT-DATA.                     ETKUP044
00531 ******************************************************************ETKUP044
00532                                                                   ETKUP044
00533      EXEC SQL                                                     ETKUP044
00534           INSERT INTO TOUT856                                     ETKUP044
00535                 (SEQ_NBR,                                         ETKUP044
00536                  TRN_SET_DATA)                                    ETKUP044
00537           VALUES                                                  ETKUP044
00538                (:WS-856-SEQ-NBR,                                  ETKUP044
00539                 :FGTRDT-TRN-SET-DATA)                             ETKUP044
00540      END-EXEC.                                                    ETKUP044
00541                                                                   ETKUP044
00542      IF SQLCODE < 0                                               ETKUP044
00543          DISPLAY '** ABEND **'                                    ETKUP044
00544          DISPLAY '** INSERT ERROR ON TOUT856 TABLE **'            ETKUP044
00545          PERFORM Z900-SQL-ERROR                                   ETKUP044
00546      ELSE                                                         ETKUP044
00547          IF SQLCODE > +100                                        ETKUP044
00548              DISPLAY '** ABEND **'                                ETKUP044
00549              DISPLAY '** INSERT WARNING ON TOUT856 TABLE'         ETKUP044
00550              PERFORM Z900-SQL-WARNING                             ETKUP044
00551          END-IF                                                   ETKUP044
00552      END-IF.                                                      ETKUP044
00553                                                                   ETKUP044
00554      PERFORM Z200-DEL-DOC-DATA-ROW.                               ETKUP044
00555                                                                   ETKUP044
00556  D400-INCREMENT-DATE.                                             ETKUP044
00557                                                                   ETKUP044
00558 ******************************************************************ETKUP044
00559 *    USE THE CALENDAR ROUTINE TO INCREMENT THE HOLD DATE BY A     ETKUP044
00560 *    A CERTAIN NUMBER OF BUSINESS DAYS.                           ETKUP044
00561 *                                                                 ETKUP044
00562 *    PERFORMED BY C200-CALENDAR.                                  ETKUP044
00563 ******************************************************************ETKUP044
00564                                                                   ETKUP044
00565      SET  DPG51-INCREMENT-DATE TO TRUE.                           ETKUP044
00566      ACCEPT DPG51-SYSTEM-DATE FROM DATE.                          ETKUP044
00567 *    MOVE DPG51-SYSTEM-DATE    TO WS-TODAYS-DATE.                 ETKUP044
00568      MOVE 'A0'                 TO DPG51-CALENDAR-TYPE.            ETKUP044
00569      SET  DPG52-LK-DTE-ISO-FMT TO TRUE.                           ETKUP044
00570      MOVE FGTRHL-HOLD-DTE      TO DPG52-LK-DATE-INPUT.            ETKUP044
00571                                                                   ETKUP044
00572      CALL PC-CALENDAR-ROUTINE                                     ETKUP044
00573          USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               ETKUP044
00574                                                                   ETKUP044
00575      EVALUATE TRUE                                                ETKUP044
00576          WHEN DPG54-SEVERE-ERROR                                  ETKUP044
00577              DISPLAY '** ABEND **'                                ETKUP044
00578              DISPLAY '** PGM = ETKUP044'                          ETKUP044
00579              DISPLAY '** D400 - INCREMENT DATE ROUTINE. '         ETKUP044
00580              DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  ETKUP044
00581              DISPLAY '** ERROR MESSAGE = '                        ETKUP044
00582                      DPG54-ERROR-MESSAGE                          ETKUP044
00583              DISPLAY '** DPG51 = ' DPG51                          ETKUP044
00584              DISPLAY '** DPG52 = ' DPG52                          ETKUP044
00585              DISPLAY '** DPG53 = ' DPG53                          ETKUP044
00586              DISPLAY '** DPG54 = ' DPG54                          ETKUP044
00587              MOVE +4003 TO ABEND-CODE                             ETKUP044
00588              CALL 'ILBOABN0' USING ABEND-CODE                     ETKUP044
00589          WHEN OTHER                                               ETKUP044
00590              CONTINUE                                             ETKUP044
00591      END-EVALUATE.                                                ETKUP044
00592                                                                   ETKUP044
00593      MOVE DPG55-DB2-ISO-DATE-FMT TO WS-COMPARE-DATE.              ETKUP044
00594                                                                   ETKUP044
00595  Z000-OPEN-DOC-CURSOR.                                            ETKUP044
00596                                                                   ETKUP044
00597 ******************************************************************ETKUP044
00598 *    OPEN THE DOCUMENT CURSOR                                    *ETKUP044
00599 *                                                                *ETKUP044
00600 *    PERFORMED BY A100-MAIN-MODULE.                              *ETKUP044
00601 ******************************************************************ETKUP044
00602                                                                   ETKUP044
00603      EXEC SQL                                                     ETKUP044
00604          OPEN DOC_CURSOR                                          ETKUP044
00605      END-EXEC.                                                    ETKUP044
00606                                                                   ETKUP044
00607      IF SQLCODE < 0                                               ETKUP044
00608          DISPLAY '** ABEND **'                                    ETKUP044
00609          DISPLAY '** OPEN CURSOR ERROR FOR TFGTRHL TABLE **'      ETKUP044
00610          PERFORM Z900-SQL-ERROR                                   ETKUP044
00611      ELSE                                                         ETKUP044
00612          IF SQLCODE > +100                                        ETKUP044
00613              DISPLAY '** ABEND **'                                ETKUP044
00614              DISPLAY '** OPEN CURSOR WARNING TFGTRHL TABLE'       ETKUP044
00615              PERFORM Z900-SQL-WARNING                             ETKUP044
00616          END-IF                                                   ETKUP044
00617      END-IF.                                                      ETKUP044
00618                                                                   ETKUP044
00619  Z100-FETCH-DOC-CURSOR.                                           ETKUP044
00620                                                                   ETKUP044
00621 ******************************************************************ETKUP044
00622 *    FETCH THE ROW VIA DOCUMENT CURSOR                            ETKUP044
00623 *                                                                 ETKUP044
00624 *    PERFORMED BY B100-INITIALIZE-DUTIES.                         ETKUP044
00625 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00626 ******************************************************************ETKUP044
00627                                                                   ETKUP044
00628      EXEC SQL                                                     ETKUP044
00629          FETCH DOC_CURSOR                                         ETKUP044
00630          INTO  :FGTRHL-TP-DKEY,                                   ETKUP044
00631                :FGTRHL-TP-DKEY-TMST,                              ETKUP044
00632                :FGTRHL-TRN-SET-CDE,                               ETKUP044
00633                :FGTRHL-HOLD-DTE,                                  ETKUP044
00634                :FGTRHL-RLSE-IND,                                  ETKUP044
00635                :FGTRHL-USER-ID                                    ETKUP044
00636      END-EXEC.                                                    ETKUP044
00637                                                                   ETKUP044
00638      IF SQLCODE < 0                                               ETKUP044
00639          DISPLAY '** ABEND **'                                    ETKUP044
00640          DISPLAY '** FETCH ERROR ON TFGTRHL TABLE'                ETKUP044
00641          PERFORM Z900-SQL-ERROR                                   ETKUP044
00642      ELSE                                                         ETKUP044
00643          IF SQLCODE > +100                                        ETKUP044
00644              DISPLAY '** ABEND **'                                ETKUP044
00645              DISPLAY '** FETCH WARNING ON TFGTRHL TABLE'          ETKUP044
00646              PERFORM Z900-SQL-WARNING                             ETKUP044
00647          END-IF                                                   ETKUP044
00648      END-IF.                                                      ETKUP044
00649                                                                   ETKUP044
00650  Z200-DEL-DOC-DATA-ROW.                                           ETKUP044
00651                                                                   ETKUP044
00652 ******************************************************************ETKUP044
00653 *    DELETE THE CURRENT ROW FROM TFGTRDT                          ETKUP044
00654 *                                                                 ETKUP044
00655 *    PERFORMED BY D100-INSERT-PO                                  ETKUP044
00656 *    PERFORMED BY D200-INSERT-INVOICE                             ETKUP044
00657 *    PERFORMED BY D300-INSERT-ASN                                 ETKUP044
00658 ******************************************************************ETKUP044
00659                                                                   ETKUP044
00660      EXEC SQL                                                     ETKUP044
00661          DELETE                                                   ETKUP044
00662            FROM TFGTRDT                                           ETKUP044
00663            WHERE TP_DKEY         = :FGTRHL-TP-DKEY         AND    ETKUP044
00664                  TP_DKEY_TMST    = :FGTRHL-TP-DKEY-TMST    AND    ETKUP044
00665                  TRN_SET_SEQ_NBR = :FGTRDT-TRN-SET-SEQ-NBR        ETKUP044
00666      END-EXEC.                                                    ETKUP044
00667                                                                   ETKUP044
00668      IF SQLCODE < 0                                               ETKUP044
00669          DISPLAY '** ABEND **'                                    ETKUP044
00670          DISPLAY '** DELETE ERROR ON TFGTRDT TABLE'               ETKUP044
00671          PERFORM Z900-SQL-ERROR                                   ETKUP044
00672      ELSE                                                         ETKUP044
00673          IF SQLCODE > +100                                        ETKUP044
00674              DISPLAY '** ABEND **'                                ETKUP044
00675              DISPLAY '** DELETE WARNING ON TFGTRDT TABLE'         ETKUP044
00676              PERFORM Z900-SQL-WARNING                             ETKUP044
00677          END-IF                                                   ETKUP044
00678      END-IF.                                                      ETKUP044
00679                                                                   ETKUP044
00680  Z300-DEL-TRPOHL-ROW.                                             ETKUP044
00681                                                                   ETKUP044
00682 ******************************************************************ETKUP044
00683 *    DELETE THE ROWS FROM TTRPOHL                                 ETKUP044
00684 *                                                                 ETKUP044
00685 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00686 ******************************************************************ETKUP044
00687                                                                   ETKUP044
00688      EXEC SQL                                                     ETKUP044
00689          DELETE                                                   ETKUP044
00690            FROM TTRPOHL                                           ETKUP044
00691           WHERE  TP_DKEY      = :FGTRHL-TP-DKEY                   ETKUP044
00692             AND  TP_DKEY_TMST = :FGTRHL-TP-DKEY-TMST              ETKUP044
00693      END-EXEC.                                                    ETKUP044
00694                                                                   ETKUP044
00695      IF SQLCODE < 0                                               ETKUP044
00696          DISPLAY '** ABEND **'                                    ETKUP044
00697          DISPLAY '** DELETE ERROR ON TTRPOHL TABLE'               ETKUP044
00698          PERFORM Z900-SQL-ERROR                                   ETKUP044
00699      ELSE                                                         ETKUP044
00700          IF SQLCODE > +100                                        ETKUP044
00701              DISPLAY '** ABEND **'                                ETKUP044
00702              DISPLAY '** DELETE WARNING ON TTRPOHL TABLE'         ETKUP044
00703              PERFORM Z900-SQL-WARNING                             ETKUP044
00704          END-IF                                                   ETKUP044
00705      END-IF.                                                      ETKUP044
00706                                                                   ETKUP044
00707  Z400-UPDATE-DOC-ROW.                                             ETKUP044
00708                                                                   ETKUP044
00709 ******************************************************************ETKUP044
00710 *    UPDATE TFGTRHL ROW TO SHOW IT HAS BEEN RELEASED              ETKUP044
00711 *                                                                 ETKUP044
00712 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00713 ******************************************************************ETKUP044
00714                                                                   ETKUP044
00715      IF FGTRHL-RLSE-IND = 'Y'                                     ETKUP044
00716          MOVE FGTRHL-USER-ID TO WS-USER-ID                        ETKUP044
00717      ELSE                                                         ETKUP044
00718          MOVE 'ETKUP044'     TO WS-USER-ID                        ETKUP044
00719      END-IF.                                                      ETKUP044
00720                                                                   ETKUP044
00721      EXEC SQL                                                     ETKUP044
00722          UPDATE TFGTRHL                                           ETKUP044
00723             SET  RLSE_DTE = CURRENT DATE                          ETKUP044
00724                 ,RLSE_IND = 'Y'                                   ETKUP044
00725                 ,USER_ID = :WS-USER-ID                            ETKUP044
00726           WHERE CURRENT OF DOC_CURSOR                             ETKUP044
00727      END-EXEC.                                                    ETKUP044
00728                                                                   ETKUP044
00729      IF SQLCODE < 0                                               ETKUP044
00730          DISPLAY '** ABEND **'                                    ETKUP044
00731          DISPLAY '** UPDATE ERROR ON TFGTRHL TABLE'               ETKUP044
00732          PERFORM Z900-SQL-ERROR                                   ETKUP044
00733      ELSE                                                         ETKUP044
00734          IF SQLCODE > +100                                        ETKUP044
00735              DISPLAY '** ABEND **'                                ETKUP044
00736              DISPLAY '** UPDATE WARNING ON TFGTRHL TABLE'         ETKUP044
00737              PERFORM Z900-SQL-WARNING                             ETKUP044
00738          END-IF                                                   ETKUP044
00739      END-IF.                                                      ETKUP044
00740                                                                   ETKUP044
00741  Z500-CLOSE-DOC-CURSOR.                                           ETKUP044
00742                                                                   ETKUP044
00743 ******************************************************************ETKUP044
00744 *    CLOSE THE DOCUMENT CURSOR                                    ETKUP044
00745 *                                                                 ETKUP044
00746 *    PERFORMED BY A100-MAIN-MODULE.                               ETKUP044
00747 ******************************************************************ETKUP044
00748                                                                   ETKUP044
00749      EXEC SQL                                                     ETKUP044
00750          CLOSE DOC_CURSOR                                         ETKUP044
00751      END-EXEC.                                                    ETKUP044
00752                                                                   ETKUP044
00753      IF SQLCODE < 0                                               ETKUP044
00754          DISPLAY '** ABEND **'                                    ETKUP044
00755          DISPLAY '** CLOSE CURSOR ERROR FOR TFGTRHL TABLES'       ETKUP044
00756          PERFORM Z900-SQL-ERROR                                   ETKUP044
00757      ELSE                                                         ETKUP044
00758          IF SQLCODE > +100                                        ETKUP044
00759              DISPLAY '** ABEND **'                                ETKUP044
00760              DISPLAY '** CLOSE CURSOR WARNING TFGTRHL TABLES'     ETKUP044
00761              PERFORM Z900-SQL-WARNING                             ETKUP044
00762          END-IF                                                   ETKUP044
00763      END-IF.                                                      ETKUP044
00764                                                                   ETKUP044
00765  Z600-OPEN-DOC-DATA-CURSOR.                                       ETKUP044
00766                                                                   ETKUP044
00767 ******************************************************************ETKUP044
00768 *    OPEN THE DOCUMENT DATA CURSOR                               *ETKUP044
00769 *                                                                *ETKUP044
00770 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00771 ******************************************************************ETKUP044
00772                                                                   ETKUP044
00773      EXEC SQL                                                     ETKUP044
00774          OPEN DOC_DATA_CURSOR                                     ETKUP044
00775      END-EXEC.                                                    ETKUP044
00776                                                                   ETKUP044
00777      IF SQLCODE < 0                                               ETKUP044
00778          DISPLAY '** ABEND **'                                    ETKUP044
00779          DISPLAY '** OPEN CURSOR ERROR FOR TFGTRDT TABLE **'      ETKUP044
00780          PERFORM Z900-SQL-ERROR                                   ETKUP044
00781      ELSE                                                         ETKUP044
00782          IF SQLCODE > +100                                        ETKUP044
00783              DISPLAY '** ABEND **'                                ETKUP044
00784              DISPLAY '** OPEN CURSOR WARNING TFGTRDT TABLE'       ETKUP044
00785              PERFORM Z900-SQL-WARNING                             ETKUP044
00786          END-IF                                                   ETKUP044
00787      END-IF.                                                      ETKUP044
00788                                                                   ETKUP044
00789  Z700-FETCH-DOC-DATA-CURSOR.                                      ETKUP044
00790                                                                   ETKUP044
00791 ******************************************************************ETKUP044
00792 *    FETCH THE ROW VIA DOCUMENT DATA CURSOR                       ETKUP044
00793 *                                                                 ETKUP044
00794 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00795 *    PERFORMED BY C100-PROCESS-DOCUMENT-DATA.                     ETKUP044
00796 ******************************************************************ETKUP044
00797                                                                   ETKUP044
00798      EXEC SQL                                                     ETKUP044
00799          FETCH DOC_DATA_CURSOR                                    ETKUP044
00800          INTO  :FGTRDT-TRN-SET-DATA,                              ETKUP044
00801                :FGTRDT-TRN-SET-SEQ-NBR                            ETKUP044
00802      END-EXEC.                                                    ETKUP044
00803                                                                   ETKUP044
00804      IF SQLCODE < 0                                               ETKUP044
00805          DISPLAY '** ABEND **'                                    ETKUP044
00806          DISPLAY '** FETCH ERROR ON TFGTRDT TABLE'                ETKUP044
00807          PERFORM Z900-SQL-ERROR                                   ETKUP044
00808      ELSE                                                         ETKUP044
00809          IF SQLCODE > +100                                        ETKUP044
00810              DISPLAY '** ABEND **'                                ETKUP044
00811              DISPLAY '** FETCH WARNING ON TFGTRDT TABLE'          ETKUP044
00812              PERFORM Z900-SQL-WARNING                             ETKUP044
00813          END-IF                                                   ETKUP044
00814      END-IF.                                                      ETKUP044
00815                                                                   ETKUP044
00816  Z800-CLOSE-DOC-DATA-CURSOR.                                      ETKUP044
00817                                                                   ETKUP044
00818 ******************************************************************ETKUP044
00819 *    CLOSE THE DOCUMENT DATA CURSOR.                              ETKUP044
00820 *                                                                 ETKUP044
00821 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00822 ******************************************************************ETKUP044
00823                                                                   ETKUP044
00824      EXEC SQL                                                     ETKUP044
00825          CLOSE DOC_DATA_CURSOR                                    ETKUP044
00826      END-EXEC.                                                    ETKUP044
00827                                                                   ETKUP044
00828      IF SQLCODE < 0                                               ETKUP044
00829          DISPLAY '** ABEND **'                                    ETKUP044
00830          DISPLAY '** CLOSE CURSOR ERROR FOR TFGTRDT TABLES'       ETKUP044
00831          PERFORM Z900-SQL-ERROR                                   ETKUP044
00832      ELSE                                                         ETKUP044
00833          IF SQLCODE > +100                                        ETKUP044
00834              DISPLAY '** ABEND **'                                ETKUP044
00835              DISPLAY '** CLOSE CURSOR WARNING TFGTRDT TABLES'     ETKUP044
00836              PERFORM Z900-SQL-WARNING                             ETKUP044
00837          END-IF                                                   ETKUP044
00838      END-IF.                                                      ETKUP044
00839                                                                   ETKUP044
00840  Z850-UPDATE-CHKPT-ROWS.                                          ETKUP044
00841                                                                   ETKUP044
00842 ******************************************************************ETKUP044
00843 *    UPDATE THE CHECKPOINT WORKING STORAGE TABLE WITH THE LAST    ETKUP044
00844 *    SEQUENCE NUMBER WRITTEN TO THE TEMPORARY DATA TABLES AFTER   ETKUP044
00845 *    WE ARE DONE WITH A PARTICULAR DOCUMENT.  THIS IS NEEDED IN   ETKUP044
00846 *    CASE THE JOB ABENDS, SO WE CAN PICK UP WITH THE MOST RECENT  ETKUP044
00847 *    SEQUENCE NUMBER UPON RESTARTING THE JOB.                     ETKUP044
00848 *                                                                 ETKUP044
00849 *    PERFORMED BY B200-DETERMINE-RLSE-TYPE.                       ETKUP044
00850 ******************************************************************ETKUP044
00851                                                                   ETKUP044
00852      MOVE SPACES         TO WS-WS-DESC.                           ETKUP044
00853      MOVE WS-810-SEQ-NBR TO WS-RESTART-SEQ-NBR.                   ETKUP044
00854                                                                   ETKUP044
00855      MOVE +9         TO TCKRSTINF-WORK-STRG-DESC-LEN.             ETKUP044
00856      MOVE WS-WS-DESC TO TCKRSTINF-WORK-STRG-DESC-TEXT.            ETKUP044
00857                                                                   ETKUP044
00858      EXEC SQL                                                     ETKUP044
00859          UPDATE TCKRSTINF                                         ETKUP044
00860          SET WORK_STRG_DESC = :WS-WS-DESC                         ETKUP044
00861          WHERE PLAN_NAME = 'ETKUP044' AND                         ETKUP044
00862                SEQ_NMBR  = 1                                      ETKUP044
00863      END-EXEC.                                                    ETKUP044
00864                                                                   ETKUP044
00865      IF SQLCODE = +000                                            ETKUP044
00866          MOVE WS-855-SEQ-NBR TO WS-RESTART-SEQ-NBR                ETKUP044
00867                                                                   ETKUP044
00868          MOVE +9         TO TCKRSTINF-WORK-STRG-DESC-LEN          ETKUP044
00869          MOVE WS-WS-DESC TO TCKRSTINF-WORK-STRG-DESC-TEXT         ETKUP044
00870                                                                   ETKUP044
00871          EXEC SQL                                                 ETKUP044
00872              UPDATE TCKRSTINF                                     ETKUP044
00873              SET WORK_STRG_DESC = :WS-WS-DESC                     ETKUP044
00874              WHERE PLAN_NAME = 'ETKUP044' AND                     ETKUP044
00875                    SEQ_NMBR  = 2                                  ETKUP044
00876          END-EXEC                                                 ETKUP044
00877                                                                   ETKUP044
00878          IF SQLCODE = +000                                        ETKUP044
00879              MOVE WS-856-SEQ-NBR TO WS-RESTART-SEQ-NBR            ETKUP044
00880                                                                   ETKUP044
00881              MOVE +9         TO TCKRSTINF-WORK-STRG-DESC-LEN      ETKUP044
00882              MOVE WS-WS-DESC TO TCKRSTINF-WORK-STRG-DESC-TEXT     ETKUP044
00883                                                                   ETKUP044
00884              EXEC SQL                                             ETKUP044
00885                  UPDATE TCKRSTINF                                 ETKUP044
00886                  SET WORK_STRG_DESC = :WS-WS-DESC                 ETKUP044
00887                  WHERE PLAN_NAME = 'ETKUP044' AND                 ETKUP044
00888                        SEQ_NMBR  = 3                              ETKUP044
00889              END-EXEC                                             ETKUP044
00890                                                                   ETKUP044
00891              IF SQLCODE = +000                                    ETKUP044
00892                  CONTINUE                                         ETKUP044
00893              ELSE                                                 ETKUP044
00894                  DISPLAY '** ABEND **'                            ETKUP044
00895                  DISPLAY '** Z850-UPDATE-CHKPT-ROWS **'           ETKUP044
00896                  DISPLAY '** UPDATE TCKRSTINF SEQ # 3 **'         ETKUP044
00897                  PERFORM Z900-SQL-ERROR                           ETKUP044
00898              END-IF                                               ETKUP044
00899                                                                   ETKUP044
00900          ELSE                                                     ETKUP044
00901              DISPLAY '** ABEND **'                                ETKUP044
00902              DISPLAY '** Z850-UPDATE-CHKPT-ROWS **'               ETKUP044
00903              DISPLAY '** UPDATE TCKRSTINF SEQ # 2 **'             ETKUP044
00904              PERFORM Z900-SQL-ERROR                               ETKUP044
00905          END-IF                                                   ETKUP044
00906                                                                   ETKUP044
00907      ELSE                                                         ETKUP044
00908          DISPLAY '** ABEND **'                                    ETKUP044
00909          DISPLAY '** Z850-UPDATE-CHKPT-ROWS **'                   ETKUP044
00910          DISPLAY '** UPDATE TCKRSTINF SEQ # 1 **'                 ETKUP044
00911          PERFORM Z900-SQL-ERROR                                   ETKUP044
00912      END-IF.                                                      ETKUP044
00913                                                                   ETKUP044
00914      EXEC SQL                                                     ETKUP044
00915          COMMIT                                                   ETKUP044
00916      END-EXEC.                                                    ETKUP044
00917                                                                   ETKUP044
00918  Z900-SQL-WARNING.                                                ETKUP044
00919                                                                   ETKUP044
00920 ******************************************************************ETKUP044
00921 *    SQL WARNING PROCESSING                                       ETKUP044
00922 ******************************************************************ETKUP044
00923                                                                   ETKUP044
00924      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUP044
00925      MOVE SQLCODE    TO SQLCODE2.                                 ETKUP044
00926      DISPLAY '** ABEND **       ** = SQLWARNING'.                 ETKUP044
00927      DISPLAY '** PROGRAM        ** = ETKUP044'.                   ETKUP044
00928      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUP044
00929      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUP044
00930      DISPLAY '** SQLWARN0       ** = ' SQLWARN0.                  ETKUP044
00931      DISPLAY '** SQLWARN1       ** = ' SQLWARN1.                  ETKUP044
00932      DISPLAY '** SQLWARN2       ** = ' SQLWARN2.                  ETKUP044
00933      DISPLAY '** SQLWARN3       ** = ' SQLWARN3.                  ETKUP044
00934      DISPLAY '** SQLWARN4       ** = ' SQLWARN4.                  ETKUP044
00935      DISPLAY '** SQLWARN5       ** = ' SQLWARN5.                  ETKUP044
00936      DISPLAY '** SQLWARN6       ** = ' SQLWARN6.                  ETKUP044
00937      DISPLAY '** SQLWARN7       ** = ' SQLWARN7.                  ETKUP044
00938                                                                   ETKUP044
00939      MOVE +4013 TO ABEND-CODE.                                    ETKUP044
00940      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP044
00941                                                                   ETKUP044
00942  Z900-SQL-ERROR.                                                  ETKUP044
00943                                                                   ETKUP044
00944 ******************************************************************ETKUP044
00945 *    SQL ERROR PROCESSING.                                        ETKUP044
00946 ******************************************************************ETKUP044
00947                                                                   ETKUP044
00948      MOVE SQLCODE    TO SQLCODE2.                                 ETKUP044
00949      MOVE SQLERRD(3) TO SQLERRD3.                                 ETKUP044
00950      DISPLAY '** ABEND **       ** = SQLERROR'.                   ETKUP044
00951      DISPLAY '** PROGRAM        ** = ETKUP044'.                   ETKUP044
00952      DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  ETKUP044
00953      DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  ETKUP044
00954      DISPLAY '** SQLERRM        ** = ' SQLERRM.                   ETKUP044
00955      DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  ETKUP044
00956                                                                   ETKUP044
00957      MOVE +4013 TO ABEND-CODE.                                    ETKUP044
00958      CALL 'ILBOABN0' USING ABEND-CODE.                            ETKUP044
00959                                                                   ETKUP044
