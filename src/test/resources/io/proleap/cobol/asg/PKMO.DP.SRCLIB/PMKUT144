      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
                                                                        00040000
       PROGRAM-ID.      PMKUT144.                                       00050000
       AUTHOR.          DARYL WOELFEL.                                  00060000
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00070000
       DATE-WRITTEN.           MARCH 2006.                              00080000
       DATE-COMPILED.                                                   00090000
      ******************************************************************        
      * THIS PROGRAM IS A SPINOFF OF PMKUT107 TO MOVE SKU_LOC CURSOR   *        
      * PROCESSING FROM THAT PROGRAM FOR IMPROVED PERFORMANCE PER DBA. *        
      * THIS REQUIRES SPLITTING PMKUT107 INTO 3 PROGRAMS: PMKUT107,    *        
      * PMKUT143, AND THIS ONE (PMKUT144).                             *        
      ******************************************************************        
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         03/08/2006                                                
      * WR/IR#:       WR30242                                                   
      * PROGRAMMER:   JOHN BOLDT, STRATAGEM.                            00100000
      * DESCRIPTION:  INITIAL CREATION.                                 00472200
      *----------------------------------------------------------               
      * DATE:         08/08/2006                                                
      * WR/IR#:       PM00906045                                                
      * PROGRAMMER:   JOHN BOLDT, STRATAGEM.                            00100000
      * DESCRIPTION:  CORRECT FUTURE PERM CHANGES ERROR.                00472200
DS1123*----------------------------------------------------------               
DS1123* DATE:         11/23/2007                                       *        
DS1123* WR/IR#:       WR33833                                          *        
DS1123* PROGRAMMER:   DARYL STEWART                                    *        
DS1123* DESCRIPTION:  CREATED A GLOBAL TEMP TABLE SO THE DB DOESN'T    *        
DS1123*               NEED REPEATED CALLS, AND BECOMES MORE EFFECIENT. *        
PK1208*----------------------------------------------------------               
PK1208* DATE:         12/05/2008                                                
PK1208* WR/IR#:       WR35762                                                   
PK1208* PROGRAMMER:   PAUL KEBER                                                
PK1208* DESCRIPTION:  CHANGED THE GLOBAL TEMP TABLE TO READ WORKING             
PK1208*               TABLE PMTW_CORP_ACTV_SKU TO AVOID THE LOAD TIME.          
PK1208*               THIS TABLE WILL ONLY CONTAIN NON-CLEARANCE SKUS           
PK1208*               AND THE SKU NUMBER ALONG WITH TSKST INFO. IT WILL         
PK1208*               ALSO CONTAIN SKU_LOC CURSOR INFO FROM PMKUT143.           
PK1208*               THAT PROGRAM'S LOGIC WILL BE MOVED INTO THIS              
PK1208*               PROGRAM SO THAT PMKUT143 WILL NO LONGER BE NEEDED.        
P41267*----------------------------------------------------------               
P41267* DATE:         04/26/2013                                                
P41267* WR/IR#:       PRB0041267                                                
P41267* PROGRAMMER:   COGNIZANT                                                 
P41267* DESCRIPTION:  INCLUDED WAIT LOGIC IN SELECT PARA                        
P41267*               H300-MOVE-TSKSTOO-FIELD TO DELAY 30 SECS                  
P41267*               IF THERE IS ANY CONTENTION WITH TSKSTOO TABLE.            
P41267* DATE:         08/25/2015                                                
P41267* WR/IR#:       CHG0120944                                                
P41267* PROGRAMMER:   COGNIZANT                                                 
P41267* DESCRIPTION:  INCLUDED THE SKIP CONDITION USED IN THE                   
P41267*               PROGRAM PMKUT111 TO REMOVE THE DUPLICATES                 
C47460* DATE:         07/28/2016                                                
C47460* WR/IR#:       CHG0147460                                                
C47460* PROGRAMMER:   COGNIZANT                                                 
C47460* DESCRIPTION:  REMOVED TIME CHECKS, COMMITS AND DECREASED                
C47460*               DISPLAY FREQUENCY FOR CPU PERFORMANCE                     
C47460*               IMPROVEMENT. INCLUDED CHECK PARA BEFORE H300- AND         
C47460*               H500- PARA'S TO REDUCE DB2 CALLS.                         
C49356* DATE:         08/24/2016                                                
C49356* WR/IR#:       CHG0149356                                                
C49356* PROGRAMMER:   COGNIZANT                                                 
C49356* DESCRIPTION:  RESTORED COMMIT LOGIC TO AVOID DBK435 JOB                 
C49356*               CONTENTION WITH PMK(069-073) JOBS.                        
C44470* DATE:         20/05/2020                                                
C44470* WR/IR#:       CHG0244470                                                
C44470* PROGRAMMER:   COGNIZANT                                                 
C44470* DESCRIPTION:  ADDED NEW VIEW CALLED XST_STR_GP_MBSHP_MQT INSTEAD        
C44470*               OF HARDCODED STORE NUMBERS TO REDUCE CPU USAGE.           
      ******************************************************************00472400
       ENVIRONMENT DIVISION.                                            00472500
      ******************************************************************00472600
                                                                        00473000
       CONFIGURATION SECTION.                                           00474000
                                                                        00475000
       SOURCE-COMPUTER. IBM-3090.                                       00476000
       OBJECT-COMPUTER. IBM-3090.                                       00477000
                                                                        00478000
       INPUT-OUTPUT SECTION.                                            00479000
                                                                        00480000
       FILE-CONTROL.                                                    00490000
                                                                        00500000
           SELECT INTERIM-EXPLODED-FILE                                 00510000
               ASSIGN                       TO INP01.                   00520000
                                                                        00530000
           SELECT EXPLODED-FILE                                         00540000
               ASSIGN                       TO OUT01.                   00550000
                                                                        00560000
      ******************************************************************00570000
       DATA DIVISION.                                                   00580000
      ******************************************************************00590000
                                                                        00600000
       FILE SECTION.                                                    00610000
                                                                        00620000
       FD  INTERIM-EXPLODED-FILE                                        00630000
           RECORDING MODE F                                             00640000
           BLOCK CONTAINS 0 RECORDS                                     00650000
           LABEL RECORDS ARE STANDARD.                                  00660000
       01  INTERIM-EXPLODED-REC          PIC X(247).                    00670000
                                                                        00680000
       FD  EXPLODED-FILE                                                00690000
           RECORDING MODE F                                             00700000
           BLOCK CONTAINS 0 RECORDS                                     00710000
           LABEL RECORDS ARE STANDARD.                                  00720000
       01  EXPLODED-REC                  PIC X(243).                    00730000
                                                                        00740000
       WORKING-STORAGE SECTION.                                         00750000
                                                                        00760000
      ******************************************************************00770000
      * PC PROGRAM CONSTANTS                                            00780000
      ******************************************************************00790000
       01  PC-PROGRAM-CONSTANTS.                                        00800000
           05  PC-PROGRAM-NAME              PIC X(08)      VALUE        00810000
                                                           'PMKUT144'.  00820000
                                                                        00840000
      ******************************************************************00850000
      * PV PROGRAM VARIABLES                                            00860000
      ******************************************************************00870000
       01  PV-PROGRAM-VARIABLES.                                        00880000
           05  PV-PARAGRAPH-NAME            PIC X(40) VALUE SPACES.     00890000
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACES.     00900000
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACES.     00910000
           05  PV-PROMO-END-TMST            PIC X(19) VALUE ALL '9'.            
C49356     05  PV-RUN-TIME                  PIC X(26)  VALUE SPACES.            
           05  PV-CPU-TIME                  PIC 9(08) VALUE 0.          00930000
C49356     05  WS-TMST                      PIC X(26)  VALUE SPACES.            
           05  PV-FIRST-RCVD-DTE-NUM        PIC 9(06) VALUE 0.          01090000
           05  PV-FIRST-RCVD-RDF REDEFINES PV-FIRST-RCVD-DTE-NUM.       01100000
               10  PV-FIRST-RCVD-RDF-YY     PIC 9(02).                  01110000
               10  PV-FIRST-RCVD-RDF-MM     PIC 9(02).                  01120000
               10  PV-FIRST-RCVD-RDF-DD     PIC 9(02).                  01130000
           05  PV-FIRST-RCVD-DTE.                                       01140000
               10  PV-FIRST-RCVD-DTE-CC     PIC X(02) VALUE SPACES.     01150000
               10  PV-FIRST-RCVD-DTE-YY     PIC X(02) VALUE SPACES.     01160000
               10  PV-FIRST-RCVD-DTE-D1     PIC X(01) VALUE '-'.        01170000
               10  PV-FIRST-RCVD-DTE-MM     PIC X(02) VALUE SPACES.     01180000
               10  PV-FIRST-RCVD-DTE-D2     PIC X(01) VALUE '-'.        01190000
               10  PV-FIRST-RCVD-DTE-DD     PIC X(02) VALUE SPACES.     01200000
           05  PV-STR-NBR                   PIC S9(04) COMP.            01270000
           05  PV-END-TMST                  PIC X(26) VALUE SPACES.     01390000
           05  PV-END-TMST-RDF REDEFINES PV-END-TMST.                   01400000
               10  PV-END-TMST-DATE         PIC X(10).                  01410000
               10  PV-END-TMST-HYPHEN       PIC X(01).                  01420000
               10  PV-END-TMST-HH           PIC X(02).                  01430000
               10  PV-END-TMST-DOT1         PIC X(01).                  01440000
               10  PV-END-TMST-MI           PIC X(02).                  01450000
               10  PV-END-TMST-DOT2         PIC X(01).                  01460000
               10  PV-END-TMST-SS           PIC X(02).                  01470000
               10  PV-END-TMST-DB2-EXTRA    PIC X(07).                  01480000
******     05  PV-SKU-NBR                   PIC S9(08)V COMP-3 VALUE 0. 01530000
           05  PV-PREV-SKU-NBR              PIC  9(08)  VALUE 0.                
C47460     05  PV-PREV-ITM-NBR              PIC  9(15)  VALUE 0.                
           05  PV-PREV-LOC-NBR              PIC  9(05)  VALUE 0.                
PK1208     05  PV-HOLD-SKU-NBR              PIC S9(08)V COMP-3 VALUE 0. 01540000
PK1208     05  PV-HOLD-FRST-RCVD-DTE        PIC X(10)   VALUE SPACES.   01540000
PK1208     05  PV-HOLD-OH-UNT-QTY           PIC  9(07)  VALUE 0.        01540000
           05  PV-RECS-DIV                  PIC S9(09)  VALUE 0.        01570000
           05  PV-RECS-REM                  PIC S9(09)  VALUE 0.        01580000
JB0808     05  PV-PRIC-PT                   PIC  9(07)V9(2)    VALUE 0.         
           05  PV-NULL-INDICATORS.                                              
               10  PV-GP-RTL-QTY-NI         PIC S9(04)  COMP   VALUE 0.         
               10  PV-GP-RTL-AMT-NI         PIC S9(04)  COMP   VALUE 0.         
                                                                        01700000
           05  PV-RETRY-COUNT          PIC S9(4)  COMP SYNC VALUE ZERO.         
           05  PC-MAX-RETRIES          PIC S9(3)  VALUE 10  COMP-3.             
                                                                                
           05  PV-DELAY-INTERVAL.                                               
               10  FILLER                 PIC X(02)  VALUE SPACES.              
               10  PV-DELAY-TIME          PIC 9(08)  VALUE 3000.                
           05  WS-SYSWAIT                 PIC X(08)  VALUE 'SYSWAIT'.           
           05  PV-PREV-PROMO-ID             PIC  9(10)  VALUE 0.                
           05  PV-PREV-PROMO-PRICG-TYP-CDE  PIC  X(03)  VALUE SPACES.           
           05  PV-PREV-MDSE-HIER-LVL-CDE    PIC  9(01)  VALUE 0.                
C47460     05  PV-DB2-COUNT                 PIC S9(9) COMP.                     
C47460     05  PV-DB2-COUNT1                PIC S9(9) COMP.                     
C47460     05  PV-DB2-COUNT2                PIC S9(9) COMP.                     
C47460     05  PV-COUNTER                   PIC 9(9)    VALUE 1.                
      ******************************************************************01710000
      * PS PROGRAM SWITCHES                                            *01720000
      ******************************************************************01730000
       01  PS-PROGRAM-SWITCHES.                                         01740000
           05  PS-END-OF-INTERIM-FILE-SW    PIC  X(01)  VALUE 'N'.      01750000
               88  PS-END-OF-INTERIM-FILE               VALUE 'Y'.      01760000
           05  PS-SKU-CLEARANCED-SW         PIC  X(01)  VALUE 'N'.      01930000
               88  PS-SKU-CLEARANCED                    VALUE 'Y'.      01940000
               88  PS-SKU-NOT-CLEARANCED                VALUE 'N'.      01950000
PK1208     05  PS-SKU-LOC-FOUND-SW          PIC  X(01)  VALUE 'N'.              
PK1208         88  PS-SKU-LOC-FOUND                     VALUE 'Y'.              
PK1208         88  PS-SKU-LOC-NOT-FOUND                 VALUE 'N'.              
C47460     05  PS-ITM-TSKSTOO-FOUND-SW      PIC  X(01)  VALUE 'N'.              
C47460         88  PS-ITM-TSKSTOO-FOUND                 VALUE 'Y'.              
C47460         88  PS-ITM-TSKSTOO-NOT-FOUND             VALUE 'N'.              
      ******************************************************************01960000
      * PA PROGRAM ACCUMULATORS                                        *01970000
      ******************************************************************01980000
       01  PA-PROGRAM-ACCUMULATORS.                                     01990000
           05  PA-RECS-WRITTEN               PIC S9(13) COMP-3 VALUE 0. 02000000
           05  PA-READ-COUNTER               PIC S9(11) COMP-3 VALUE 0. 02010000
           05  PA-SKIP-COUNTER               PIC 9(09)  VALUE 0.                
C47460     05  PA-PAR1-COUNTER               PIC 9(09)  VALUE 0.                
C47460     05  PA-PAR2-COUNTER               PIC 9(09)  VALUE 0.                
C47460     05  PA-PAR3-COUNTER               PIC 9(09)  VALUE 0.                
C47460     05  PA-PAR4-COUNTER               PIC 9(09)  VALUE 0.                
                                                                        02020000
      *----------------------------------------------------------               
      *    INPUT RECORD - INTERIM EXTRACT EXPLODED BY SKU/STORE                 
      *----------------------------------------------------------               
           COPY PMRD113.                                                        
                                                                                
      ******************************************************************02030000
      *    OUTPUT RECORD - EXPLODED BY SKU/STORE                        02040000
      ******************************************************************02050000
           COPY PMRD107.                                                02060000
                                                                        02070000
      ******************************************************************02130000
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004      02140000
      ******************************************************************02150000
           COPY DPWS004.                                                02160000
                                                                        02170000
      ******************************************************************02180000
      *    TABLE INCLUDES                                               02190000
      ******************************************************************02200000
PK1208*    PMTW_CORP_ACTV_SKU                                                   
PK1208     EXEC SQL INCLUDE PMT00000 END-EXEC.                                  
           EXEC SQL INCLUDE TSKSTOO END-EXEC.                           02220000
           EXEC SQL INCLUDE TPLNSHP END-EXEC.                           02240000
      *    XST_STR_GP_MBSHP                                                     
           EXEC SQL INCLUDE XST00023 END-EXEC.                                  
      *    XST_PRCHG                                                    02370000
           EXEC SQL INCLUDE XST00063 END-EXEC.                          02380000
      *    XST_STR_GP_PRCHG                                                     
           EXEC SQL INCLUDE XST00064 END-EXEC.                                  
      *    XST_PRCHG_MDSE                                               02410000
           EXEC SQL INCLUDE XST00065 END-EXEC.                          02420000
                                                                        02430000
      ******************************************************************02440000
      *    SQL COMMUNICATIONS AREA DCLGEN                               02450000
      ******************************************************************02460000
           EXEC SQL                                                     02470000
               INCLUDE SQLCA                                            02480000
           END-EXEC.                                                    02490000
                                                                        02500000
      ******************************************************************02510000
      *    EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING        *02520000
      *    SQL CODES FOR DISPLAY                                       *02530000
      ******************************************************************02540000
           COPY SQLCA2.                                                 02550000
                                                                        02560000
      ******************************************************************03426000
      * AC ABEND CODES                                                 *03427000
      ******************************************************************03428000
                                                                        03429000
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0. 03430000
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.   03440000
           88  ABEND-4002-READ-ERROR                     VALUE +4002.   03450000
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.   03460000
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.   03470000
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.   03480000
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.   03490000
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.   03500000
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.   03510000
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.   03520000
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.   03530000
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.   03540000
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.   03550000
                                                                        03560000
       PROCEDURE DIVISION.                                              03570000
                                                                        03580000
      ******************************************************************03590000
      * MAIN PROCESSING                                                 03600000
      ******************************************************************03610000
       A100-PROGRAM-MAINLINE.                                           03620000
                                                                        03630000
           DISPLAY 'PROCESSING BEGINNING: '.                            03641002
           ACCEPT PV-CPU-TIME FROM TIME.                                03650000
           DISPLAY 'STARTING TIME: ' PV-CPU-TIME.                       03660000
                                                                        03670000
           PERFORM B100-INITIALIZE                                      03680000
           PERFORM B200-READ-INTERIM-FILE                               03690000
                                                                        03700000
           IF PS-END-OF-INTERIM-FILE                                    03710000
               DISPLAY ' '                                              03720000
               DISPLAY '*** EMPTY INPUT FILE. ***'                      03730000
               DISPLAY ' '                                              03740000
           ELSE                                                         03750000
C49356     PERFORM B250-FIND-TIME                                               
           PERFORM B300-PROCESS-INTERIM-FILE                            03770000
                   UNTIL PS-END-OF-INTERIM-FILE                         03780000
           END-IF                                                               
                                                                        03800000
           PERFORM B900-EOJ-ROUTINE                                     03810000
                                                                        03820000
           ACCEPT PV-CPU-TIME FROM TIME.                                03830000
           DISPLAY ' '.                                                 03840000
           DISPLAY 'ENDING TIME: ' PV-CPU-TIME.                         03850000
                                                                        03860000
           GOBACK.                                                      03870000
                                                                        03880000
      ******************************************************************03890000
      * OPEN FILES                                                      03900000
      ******************************************************************03910000
       B100-INITIALIZE.                                                 03920000
                                                                        03930000
           OPEN INPUT  INTERIM-EXPLODED-FILE                            03940000
                OUTPUT EXPLODED-FILE.                                   03950000
                                                                        03960000
           MOVE '-'                   TO PV-END-TMST-HYPHEN.            04740000
           MOVE '.'                   TO PV-END-TMST-DOT1               04760000
                                         PV-END-TMST-DOT2.              04780000
           MOVE '.000000'             TO PV-END-TMST-DB2-EXTRA.         04800000
                                                                        03960000
      ******************************************************************03970000
      * READ IN THE PRICING EXTRACT FILE                                03980000
      ******************************************************************03990000
       B200-READ-INTERIM-FILE.                                          04000000
           READ INTERIM-EXPLODED-FILE                                   04020000
               INTO PM113-EXCPT-PRCG-EXT-SKU                            04030000
               AT END                                                   04040000
                   SET PS-END-OF-INTERIM-FILE TO TRUE.                  04050000
                                                                                
                                                                                
C49356 B250-FIND-TIME.                                                          
                                                                                
C49356     EXEC SQL                                                             
C49356         SELECT (CURRENT_TIMESTAMP + 60 SECONDS)                          
C49356         INTO   :PV-RUN-TIME                                              
C49356         FROM   SYSIBM.SYSDUMMY1                                          
C49356         WITH UR                                                          
C49356     END-EXEC.                                                            
                                                                                
C49356     EVALUATE TRUE                                                        
C49356         WHEN SQLCODE = ZERO                                              
C49356             CONTINUE                                                     
C49356                                                                          
C49356          WHEN OTHER                                                      
C49356             MOVE 'SYSIBM.SYSDUMMY1'    TO PV-OPERATION-MSG-1             
C49356             MOVE 'B250-FIND-TIME '     TO PV-PARAGRAPH-NAME              
C49356             PERFORM Z900-SQL-ABEND                                       
C49356     END-EVALUATE.                                                        
                                                                                
C49356 B260-CHECK-TIME.                                                         
                                                                                
C49356     EXEC SQL                                                             
C49356         SET :WS-TMST = CURRENT_TIMESTAMP                                 
C49356     END-EXEC.                                                            
                                                                                
C49356     EVALUATE TRUE                                                        
C49356         WHEN SQLCODE = ZERO                                              
C49356             CONTINUE                                                     
                                                                                
C49356          WHEN OTHER                                                      
C49356             MOVE 'SYSIBM.SYSDUMMY1'    TO PV-OPERATION-MSG-1             
C49356             MOVE 'B260-CHECK-TIME '     TO PV-PARAGRAPH-NAME             
C49356             PERFORM Z900-SQL-ABEND                                       
C49356     END-EVALUATE.                                                        
                                                                                
C49356 B270-COMMIT.                                                             
                                                                                
C49356     EXEC SQL                                                             
C49356         COMMIT                                                           
C49356     END-EXEC.                                                            
                                                                                
C49356     EVALUATE TRUE                                                        
C49356         WHEN SQLCODE = ZERO                                              
C49356             CONTINUE                                                     
                                                                                
C49356          WHEN OTHER                                                      
C49356             MOVE 'B270-COMMIT' TO PV-PARAGRAPH-NAME                      
C49356             PERFORM Z900-SQL-ABEND                                       
C49356     END-EVALUATE.                                                        
                                                                                
      ******************************************************************04580000
      * PROCESS THE PRICING EXTRACT FILE                                04590000
      ******************************************************************04600000
       B300-PROCESS-INTERIM-FILE.                                       04610000
                                                                        04620000
           MOVE PM113-LOC-NBR              TO PV-STR-NBR                06920000
                                              SKSTOO-STR-NBR.           14270000
           MOVE PM113-EXCPT-PRCG-EXT-SKU   TO PM107-EXCPT-PRCG-EXT-SKU.         
      *    CONVERT FROM ORACLE TO DB2 DATE/TIME                         04630000
           IF (PM113-SKU-NBR NOT = PV-PREV-SKU-NBR) OR                          
              (PM113-LOC-NBR NOT = PV-PREV-LOC-NBR) OR                          
              (PM113-PROMO-ID NOT = PV-PREV-PROMO-ID) OR                        
              ((PV-PREV-PROMO-PRICG-TYP-CDE =                                   
                'GAO' OR 'GPO' OR 'GBL' OR 'GBD' OR 'GBP') AND                  
                PM113-SKU-NBR = PV-PREV-SKU-NBR) OR                             
              ((PM113-PROMO-PRICG-TYP-CDE NOT =                                 
                PV-PREV-PROMO-PRICG-TYP-CDE) AND                                
               (PM113-MDSE-HIER-LVL-CDE =                                       
                PV-PREV-MDSE-HIER-LVL-CDE))                                     
                                                                        04810000
             IF  PM113-SKU-NBR        = PV-SKU-NBR                              
             AND PM113-PROMO-END-TMST = PV-PROMO-END-TMST                       
                 CONTINUE                                                       
             ELSE                                                               
                 MOVE PM113-SKU-NBR               TO PV-SKU-NBR               06
                 MOVE PM113-PROMO-END-TMST        TO PV-PROMO-END-TMST          
                 MOVE PM113-PROMO-END-TMST (1:10) TO PV-END-TMST-DATE           
                 MOVE PM113-PROMO-END-TMST (12:2) TO PV-END-TMST-HH             
                 MOVE PM113-PROMO-END-TMST (15:2) TO PV-END-TMST-MI             
                 MOVE PM113-PROMO-END-TMST (18:2) TO PV-END-TMST-SS             
                 PERFORM H100-GET-SKU-LOC-INFO                                  
             END-IF                                                             
                                                                        04810000
C47460         IF (PMT00000-ITM-NBR NOT = PV-PREV-ITM-NBR)                      
C47460           MOVE ZEROS TO PV-DB2-COUNT                                     
C47460           SET PS-ITM-TSKSTOO-NOT-FOUND TO TRUE                           
C47460           PERFORM H200-CHECK-TSKSTOO                                     
C47460         END-IF                                                           
C47460                                                                          
C47460         IF (PM113-SKU-NBR NOT = PV-PREV-SKU-NBR)                         
C47460           MOVE ZEROS TO PV-DB2-COUNT1                                    
C47460           MOVE ZEROS TO PV-DB2-COUNT2                                    
C47460           MOVE 1 TO PV-COUNTER                                           
C47460           PERFORM H600-PRICE-CHNG-CHECK                                  
C47460         END-IF                                                           
C47460                                                                          
C47460         MOVE PM113-SKU-NBR TO PV-PREV-SKU-NBR                            
C47460         MOVE PM113-PROMO-ID TO PV-PREV-PROMO-ID                          
C47460         MOVE PM113-PROMO-PRICG-TYP-CDE TO                                
C47460            PV-PREV-PROMO-PRICG-TYP-CDE                                   
C47460         MOVE PM113-MDSE-HIER-LVL-CDE TO                                  
C47460            PV-PREV-MDSE-HIER-LVL-CDE                                     
C47460         MOVE PM113-LOC-NBR              TO PV-PREV-LOC-NBR         069200
C47460         MOVE PMT00000-ITM-NBR TO PV-PREV-ITM-NBR                         
                                                                                
             IF PS-SKU-LOC-FOUND                                                
               PERFORM C100-PROCESS-SKU-LOC                                     
                                                                                
C47460        EVALUATE TRUE                                                     
C47460         WHEN PV-DB2-COUNT1 > 1                                           
C47460             PERFORM H500-CHECK-FUTURE-PERM-CHANGES                   0693
C47460             ADD  1 TO PA-PAR1-COUNTER                                    
C47460         WHEN PV-DB2-COUNT2 < 9 AND (PV-DB2-COUNT2 IS NOT ZERO)           
C47460             PERFORM H500-CHECK-FUTURE-PERM-CHANGES                   0693
C47460             ADD  1 TO PA-PAR1-COUNTER                                    
C47460         WHEN PV-DB2-COUNT1 = 1 AND PV-DB2-COUNT2 = 9                     
C47460             IF PV-COUNTER = 1                                            
C47460               PERFORM H500-CHECK-FUTURE-PERM-CHANGES                     
C47460               ADD  1 TO PA-PAR1-COUNTER                                  
C47460               ADD 1 TO PV-COUNTER                                        
C47460             ELSE                                                         
C47460               ADD 1 TO PA-PAR4-COUNTER                                   
C47460              IF XST00063-EVNT-CTG-CDE = 'CLR'                            
C47460               SET PS-SKU-CLEARANCED TO TRUE                              
C47460              ELSE                                                        
C47460                MOVE XST00065-UNT-RTL-AMT TO PM107-UNT-RTL-AMT            
C47460                IF PM107-PROMO-PRICG-TYP-CDE = 'SPO'                      
C47460                 COMPUTE PV-PRIC-PT = XST00065-UNT-RTL-AMT -              
C47460                         ((PM107-PROMO-PRICG-AMT / 100)                   
C47460                            * XST00065-UNT-RTL-AMT)                       
C47460                 MOVE PV-PRIC-PT        TO PM107-PROMO-PRIC-PT-AMT        
C47460                END-IF                                                    
C47460              END-IF                                                      
C47460             END-IF                                                       
C47460         WHEN PV-DB2-COUNT1 = 0 AND PV-DB2-COUNT2 = 0                     
C47460            ADD 1 TO PA-PAR4-COUNTER                                      
C47460            CONTINUE                                                      
C47460        END-EVALUATE                                                      
                                                                                
               IF PS-SKU-NOT-CLEARANCED                                     0695
                                                                                
C47460           IF PS-ITM-TSKSTOO-FOUND                                        
                   PERFORM H300-MOVE-TSKSTOO-FIELD                          0704
C47460             ADD 1 TO PA-PAR2-COUNTER                                     
C47460           ELSE                                                           
C47460             MOVE 'N' TO PM107-ONORD-UNT-QTY-IND                          
C47460             MOVE ' ' TO PM107-DONT-SHIP-BEF-DTE                          
C47460             ADD 1 TO PA-PAR3-COUNTER                                     
C47460           END-IF                                                         
                                                                                
                   PERFORM W100-WRITE-EXPLODED-FILE                         0708
               ELSE                                                             
                   SET PS-SKU-NOT-CLEARANCED TO TRUE                            
               END-IF                                                       0711
             END-IF                                                     07110000
                                                                        07130000
             ADD +1 TO PA-READ-COUNTER                                  05920000
                                                                        05930000
      *    DISPLAY PROGRESS                                             05940000
C47460       DIVIDE PA-READ-COUNTER BY 1000000                          05950001
               GIVING PV-RECS-DIV                                       05980000
               REMAINDER PV-RECS-REM                                    05990000
              IF PV-RECS-REM = 0                                        06000000
               ACCEPT PV-CPU-TIME FROM TIME                             06010000
               DISPLAY ' '                                              06020000
               DISPLAY 'TIME: ' PV-CPU-TIME                             06030000
               DISPLAY PA-READ-COUNTER ' INPUT RECORDS PROCESSED'       06040000
              END-IF                                                    06050000
                                                                                
C49356       PERFORM B260-CHECK-TIME                                            
C49356       IF WS-TMST > PV-RUN-TIME                                           
C49356        PERFORM B270-COMMIT                                               
C49356        PERFORM B250-FIND-TIME                                            
C49356       END-IF                                                             
           ELSE                                                                 
               ADD +1 TO PA-SKIP-COUNTER                                        
           END-IF.                                                              
PK1208     MOVE PM113-SKU-NBR TO PV-HOLD-SKU-NBR.                       06910000
           PERFORM B200-READ-INTERIM-FILE.                              06070000
                                                                        06080000
      ******************************************************************06090000
      * CLEAN UP                                                        06100000
      ******************************************************************06110000
       B900-EOJ-ROUTINE.                                                06120000
                                                                        06130000
           CLOSE INTERIM-EXPLODED-FILE                                  06140000
                 EXPLODED-FILE.                                         06150000
                                                                        06160000
           DISPLAY ' '                                                  06170000
           DISPLAY 'TOTAL NUMBER OF RECORDS WRITTEN: ' PA-RECS-WRITTEN  06180000
            DISPLAY '# OF DUPLICATE RECORDS SKIPPED = '                         
                                                       PA-SKIP-COUNTER          
C47460     DISPLAY 'COUNT FOR PARA H300 OCC = ' PA-PAR2-COUNTER                 
C47460     DISPLAY 'COUNT FOR PARA H300 SKIP = ' PA-PAR3-COUNTER                
C47460     DISPLAY 'COUNT FOR PARA H500 OCC = ' PA-PAR1-COUNTER                 
C47460     DISPLAY 'COUNT FOR PARA H500 SKIP = ' PA-PAR4-COUNTER                
           DISPLAY ' '.                                                 06190000
                                                                        06200000
      ******************************************************************06090000
      * COMPLETE DATA ON EXPLODED RECORD FOR OUTPUT                             
      ******************************************************************06090000
       C100-PROCESS-SKU-LOC.                                                    
PK1208     MOVE PMT00000-GP-RTL-QTY       TO PM107-GP-RTL-QTY.                  
PK1208     MOVE PMT00000-GP-RTL-AMT       TO PM107-GP-RTL-AMT.                  
PK1208     MOVE PMT00000-LOC-SKU-STAT-CDE TO PM107-LOC-SKU-STAT-CDE.            
                                                                                
           IF PM107-UNT-RTL-AMT = ZEROS                                         
PK1208         MOVE PMT00000-UNT-RTL-AMT  TO PM107-UNT-RTL-AMT                  
           END-IF.                                                              
                                                                                
           IF PM107-PROMO-PRICG-TYP-CDE = 'SPO'                                 
      *        CALCULATE THE PRICE POINT FROM THE PERCENT                       
      *        TRUNCATE, NOT ROUND!                                             
PK1208         COMPUTE PV-PRIC-PT = PMT00000-UNT-RTL-AMT -                      
PK1208         ((PM107-PROMO-PRICG-AMT / 100) * PMT00000-UNT-RTL-AMT)           
               MOVE PV-PRIC-PT            TO PM107-PROMO-PRIC-PT-AMT            
           END-IF.                                                              
                                                                                
           MOVE PV-HOLD-FRST-RCVD-DTE     TO PM107-FRST-RCVD-DTE.               
           MOVE PV-HOLD-OH-UNT-QTY        TO PM107-OH-UNT-QTY.                  
                                                                                
      ******************************************************************13570000
      * RETRIEVE SKU LOC INFORMATION                                            
      ******************************************************************13570000
PK1208 H100-GET-SKU-LOC-INFO.                                           15560000
           INITIALIZE DCLPMT00000                                       04010000
PK1208                                                                  15570000
PK1208     EXEC SQL                                                     15580000
PK1208         SELECT ITM_NBR                                           13680000
PK1208               ,FRST_RCVD_DTE                                     13680000
PK1208               ,OH_QTY                                            13690000
PK1208               ,UNT_RTL_AMT                                       15600000
PK1208               ,GP_RTL_QTY                                        15610000
PK1208               ,GP_RTL_AMT                                        15620000
PK1208               ,LOC_SKU_STAT_CDE                                  15630000
PK1208           INTO :PMT00000-ITM-NBR                                 13700000
PK1208               ,:PMT00000-FRST-RCVD-DTE                           13700000
PK1208               ,:PMT00000-OH-QTY                                  13710000
PK1208               ,:PMT00000-UNT-RTL-AMT                             15640000
PK1208               ,:PMT00000-GP-RTL-QTY :PV-GP-RTL-QTY-NI                1565
PK1208               ,:PMT00000-GP-RTL-AMT :PV-GP-RTL-AMT-NI                1566
PK1208               ,:PMT00000-LOC-SKU-STAT-CDE                            1567
PK1208           FROM PMTW_CORP_ACTV_SKU                                13720000
PK1208          WHERE SKU_NBR = :PV-SKU-NBR                             13740000
PK1208            AND (END_TMST >= :PV-END-TMST OR                      15710000
PK1208                 END_TMST IS NULL)                                15720000
PK1208            AND BEG_TMST <= :PV-END-TMST                          15730000
PK1208         FETCH FIRST 1 ROWS ONLY                                  15750000
PK1208         WITH UR                                                  15760000
PK1208     END-EXEC.                                                    15770000
PK1208                                                                  15780000
PK1208     EVALUATE TRUE                                                15790000
PK1208         WHEN SQLCODE = +0                                        15800000
PK1208             SET PS-SKU-LOC-FOUND TO TRUE                         15810000
PK1208             IF PV-GP-RTL-QTY-NI < 0                              15820000
PK1208                 MOVE 0 TO PMT00000-GP-RTL-QTY                    15830000
PK1208             END-IF                                               15840000
PK1208             IF PV-GP-RTL-AMT-NI < 0                              15850000
PK1208                 MOVE 0 TO PMT00000-GP-RTL-AMT                    15860000
PK1208             END-IF                                               15870000
PK1208             IF PMT00000-FRST-RCVD-DTE > 0                        13830000
PK1208*                CONVERT FIRST-RCVD-DATE TO A DATE FORMAT         13840000
PK1208                 MOVE PMT00000-FRST-RCVD-DTE TO                   13850000
PK1208                                            PV-FIRST-RCVD-DTE-NUM 13860000
PK1208                 IF PV-FIRST-RCVD-RDF-YY < 30                     13870000
PK1208                     MOVE '20' TO PV-FIRST-RCVD-DTE-CC            13880000
PK1208                 ELSE                                             13890000
PK1208                     MOVE '19' TO PV-FIRST-RCVD-DTE-CC            13900000
PK1208                 END-IF                                           13910000
PK1208                 MOVE PV-FIRST-RCVD-RDF-YY TO PV-FIRST-RCVD-DTE-YY13920000
PK1208                 MOVE PV-FIRST-RCVD-RDF-MM TO PV-FIRST-RCVD-DTE-MM13930000
PK1208                 MOVE PV-FIRST-RCVD-RDF-DD TO PV-FIRST-RCVD-DTE-DD13940000
PK1208                 MOVE PV-FIRST-RCVD-DTE  TO PV-HOLD-FRST-RCVD-DTE 13950000
PK1208             ELSE                                                 13960000
PK1208                 MOVE ' ' TO PV-HOLD-FRST-RCVD-DTE                13970000
PK1208             END-IF                                               13980000
PK1208             IF PMT00000-OH-QTY > 0                               14000000
PK1208                 MOVE PMT00000-OH-QTY TO PV-HOLD-OH-UNT-QTY       14010000
PK1208             ELSE                                                 14020000
PK1208                 MOVE '0' TO PV-HOLD-OH-UNT-QTY                   14030000
PK1208             END-IF                                               14040000
PK1208         WHEN SQLCODE = +100                                      15880000
PK1208             SET PS-SKU-LOC-NOT-FOUND TO TRUE                     15890000
PK1208             MOVE ' ' TO PV-HOLD-FRST-RCVD-DTE                    14070000
PK1208             MOVE '0' TO PV-HOLD-OH-UNT-QTY                       14080000
PK1208         WHEN OTHER                                               15900000
PK1208             MOVE 'H100-GET-SKU-LOC-INFO         '                15910000
PK1208                               TO PV-PARAGRAPH-NAME               15920000
PK1208             MOVE 'ERROR GETTING SKU LOC DATA  '                  15930000
PK1208                               TO PV-OPERATION-MSG-1              15940000
PK1208             PERFORM Z900-SQL-ABEND                               15950000
PK1208     END-EVALUATE.                                                15960000
                                                                        15970000
      ******************************************************************14220000
      * GET THE TSKSTOO DATA AND MOVE IT OUT                            14230000
      ******************************************************************14240000
       H300-MOVE-TSKSTOO-FIELD.                                         14250000
                                                                        14260000
P41267     INITIALIZE PV-RETRY-COUNT.                                           
P41267     PERFORM  WITH TEST AFTER                                             
P41267       UNTIL SQLCODE = ZERO                                               
P41267          OR SQLCODE = +100                                               
P41267          OR PV-RETRY-COUNT > PC-MAX-RETRIES                              
           EXEC SQL                                                     14290000
               SELECT DISTINCT PO_NBR                                   14300000
                     ,SUM(ONORD_UNT_QTY)                                14310000
                 INTO :SKSTOO-PO-NBR                                    14320000
                     ,:SKSTOO-ONORD-UNT-QTY                             14330000
                 FROM TSKSTOO                                           14340000
PK1208          WHERE ITM_NBR = :PMT00000-ITM-NBR                       14360000
                  AND STR_NBR = :SKSTOO-STR-NBR                         14390000
                GROUP BY PO_NBR                                         14400000
JB0201          FETCH FIRST 1 ROWS ONLY                                 14401002
                WITH UR                                                 14410000
P41267     END-EXEC                                                     14420000
                                                                        14430000
           EVALUATE TRUE                                                14440000
               WHEN SQLCODE = 0                                         14450000
               WHEN SQLCODE = -811                                      14460000
      *            ONLY CARE ABOUT WHETHER AT LEAST ONE IS ON ORDER,    14470000
      *            SO JUST USE THE FIRST ROW IF MULTIPLE COME BACK      14480000
                   IF SKSTOO-ONORD-UNT-QTY > 0                          14490000
                       MOVE 'Y' TO PM107-ONORD-UNT-QTY-IND              14500000
                   ELSE                                                 14510000
                       MOVE 'N' TO PM107-ONORD-UNT-QTY-IND              14520000
                   END-IF                                               14530000
                                                                        14540000
                   PERFORM H400-MOVE-TPLNSHP-FIELD                      14550000
                                                                        14560000
               WHEN SQLCODE = +100                                      14570000
                   MOVE 'N' TO PM107-ONORD-UNT-QTY-IND                  14580000
                   MOVE ' ' TO PM107-DONT-SHIP-BEF-DTE                  14590000
                                                                        14600000
               WHEN SQLCODE = -911                                              
P41267             ADD +1  TO PV-RETRY-COUNT                                    
P41267*            DELAY 30 SECS IF RESOURCE IS UNAVAILABLE                     
P41267             CALL WS-SYSWAIT USING PV-DELAY-INTERVAL                      
                                                                                
               WHEN OTHER                                               14610000
                   MOVE 'H300-MOVE-TSKSTOO-FIELD'  TO PV-PARAGRAPH-NAME 14620000
                   MOVE 'ERROR ON TSKSTOO SELECT ' TO PV-OPERATION-MSG-114630000
                   PERFORM Z900-SQL-ABEND                               14640000
P41267     END-EVALUATE                                                 14650000
P41267     END-PERFORM.                                                         
                                                                        14660000
P41267     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
P41267         MOVE 'MAX RETRIES EXCEEDED ON TSKSTOO SELECT'                    
P41267                                TO PV-OPERATION-MSG-1                     
P41267         PERFORM Z900-SQL-ABEND                                           
P41267     END-IF.                                                              
C47460******************************************************************14220000
C47460* CHECK TSKSTOO DATA FOR ITEM                                     14230000
C47460******************************************************************14240000
C47460 H200-CHECK-TSKSTOO.                                              14250000
C47460                                                                  14260000
C47460     INITIALIZE PV-RETRY-COUNT.                                           
C47460     PERFORM  WITH TEST AFTER                                             
C47460       UNTIL SQLCODE = ZERO                                               
C47460          OR PV-RETRY-COUNT > PC-MAX-RETRIES                              
C47460     EXEC SQL                                                     14290000
C47460         SELECT COUNT(*)                                          14300000
C47460           INTO :PV-DB2-COUNT                                     14320000
C47460           FROM TSKSTOO                                           14340000
C47460          WHERE ITM_NBR = :PMT00000-ITM-NBR                       14360000
C47460            AND STR_NBR IN (222,447,620,654,783,873,951,969,977)  14390000
C47460          WITH UR                                                 14410000
C47460     END-EXEC                                                     14420000
C47460                                                                  14430000
C47460     EVALUATE TRUE                                                14440000
C47460         WHEN SQLCODE = 0                                         14450000
C47460           IF PV-DB2-COUNT > 0                                    14570000
C47460             SET PS-ITM-TSKSTOO-FOUND TO TRUE                     14600000
C47460           END-IF                                                         
C47460         WHEN SQLCODE = -911                                              
C47460             ADD +1  TO PV-RETRY-COUNT                                    
C47460*            DELAY 30 SECS IF RESOURCE IS UNAVAILABLE                     
C47460             CALL WS-SYSWAIT USING PV-DELAY-INTERVAL                      
C47460                                                                          
C47460         WHEN OTHER                                               14610000
C47460             MOVE 'H200-CHECK-TSKSTOO'  TO PV-PARAGRAPH-NAME      14620000
C47460             MOVE 'ERROR ON TSKSTOO SELECT ' TO PV-OPERATION-MSG-114630000
C47460             PERFORM Z900-SQL-ABEND                               14640000
C47460     END-EVALUATE                                                 14650000
C47460     END-PERFORM.                                                         
C47460                                                                  14660000
C47460     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
C47460         MOVE 'MAX RETRIES EXCEEDED ON TSKSTOO SELECT'                    
C47460                                TO PV-OPERATION-MSG-1                     
C47460         PERFORM Z900-SQL-ABEND                                           
C47460     END-IF.                                                              
      ******************************************************************14670000
      * GET THE TPLNSHP DATA AND MOVE IT OUT                            14680000
      ******************************************************************14690000
       H400-MOVE-TPLNSHP-FIELD.                                         14700000
                                                                        14710000
           EXEC SQL                                                     14720000
               SELECT DONT_SHIP_BEF_DTE                                 14730000
                 INTO :PLNSHP-DONT-SHIP-BEF-DTE                         14740000
                 FROM TPLNSHP                                           14750000
                WHERE PO_NBR = :SKSTOO-PO-NBR                           14760000
                  AND VER_STATUS_CDE = 'A'                              14770000
                WITH UR                                                 14780000
           END-EXEC.                                                    14790000
                                                                        14800000
           EVALUATE TRUE                                                14810000
               WHEN SQLCODE = 0                                         14820000
               WHEN SQLCODE = -811                                      14830000
                   MOVE PLNSHP-DONT-SHIP-BEF-DTE TO                     14840000
                               PM107-DONT-SHIP-BEF-DTE                  14850000
                                                                        14860000
               WHEN SQLCODE = +100                                      14870000
                   MOVE ' ' TO PM107-DONT-SHIP-BEF-DTE                  14880000
                                                                        14890000
               WHEN OTHER                                               14900000
                   MOVE 'H400-MOVE-TPLNSHP-FIELD'  TO PV-PARAGRAPH-NAME 14910000
                   MOVE 'ERROR ON TPLNSHP SELECT ' TO PV-OPERATION-MSG-114920000
                   PERFORM Z900-SQL-ABEND                               14930000
           END-EVALUATE.                                                14940000
C47460******************************************************************14960000
C47460* CHECK PERM CHANGES FOR 9 SAMPLE STORES                          14970000
C47460******************************************************************14980000
C47460 H600-PRICE-CHNG-CHECK.                                           14990000
C47460                                                                  15000000
C47460     EXEC SQL                                                     15010000
C47460         SELECT COUNT (DISTINCT (CONCAT (F.EVNT_CTG_CDE,          15020000
C47460                D.UNT_RTL_AMT ) ) ),                              15030000
C47460                COUNT(M.STR_NBR)                                          
C47460           INTO :PV-DB2-COUNT1,                                   15040000
C47460                :PV-DB2-COUNT2                                    15040000
C47460           FROM XST_PRCHG_MDSE D,                                 15060000
C47460                XST_PRCHG F,                                      15080000
C44470                XST_STR_GP_MBSHP_MQT M                            15090000
C44470****     WHERE M.STR_NBR IN (222,447,620,654,783,873,951,969,977) 15100000
C47460          WHERE  M.EFF_BEG_DTE      <= :PV-END-TMST-DATE          15110002
C47460            AND (M.EFF_END_DTE      >= :PV-END-TMST-DATE          15120002
C47460             OR  M.EFF_END_DTE IS NULL)                           15130000
C47460            AND D.SKU_NBR           = :PV-SKU-NBR                 15180000
C47460            AND D.STR_GP_ID         = M.STR_GP_ID                 15181002
C47460            AND D.STR_GP_TYP_CDE    = M.STR_GP_TYP_CDE            15182002
C47460            AND D.LAST_ACTN_CDE     <> 'D'                        15183002
C47460            AND F.PRCHG_ST_CDE      = 'APR'                       15183102
C47460            AND F.EVNT_CTG_CDE      IN ('ACT', 'CLR')             15184002
C47460            AND F.PRCHG_ID          = D.PRCHG_ID                  15190002
C47460         AND EXISTS                                               15210002
C47460        (SELECT 1                                                 15220002
C47460           FROM XST_STR_GP_PRCHG E                                15230002
C47460          WHERE E.STR_GP_TYP_CDE     = M.STR_GP_TYP_CDE           15240002
C47460            AND E.STR_GP_ID          = M.STR_GP_ID                15250002
C47460            AND E.PRCHG_EFF_DTE     <= :PV-END-TMST-DATE          15251002
C47460            AND E.PRCHG_EFF_DTE     >= CURRENT DATE               15252002
C47460            AND E.PRCHG_ID           = D.PRCHG_ID)                15253002
C47460           WITH UR                                                15261002
C47460     END-EXEC.                                                    15270000
C47460                                                                  15280000
C47460     EVALUATE TRUE                                                15290000
C47460         WHEN SQLCODE = 0                                         15300000
C47460            CONTINUE                                                      
C47460                                                                          
C47460         WHEN OTHER                                               15420000
C47460             DISPLAY '********************************'           15430000
C47460             DISPLAY 'PV-SKU-NBR       = ' PV-SKU-NBR             15440000
C47460             DISPLAY 'PV-STR-NBR       = ' PV-STR-NBR             15450000
C47460             DISPLAY 'PV-END-TMST-DATE = ' PV-END-TMST-DATE       15460000
C47460             MOVE 'H600-PRICE-CHNG-CHECK'  TO PV-PARAGRAPH-NAME   15470000
C47460             MOVE 'ERROR ON XST_CHG_EVNT ' TO PV-OPERATION-MSG-1  15480000
C47460             PERFORM Z900-SQL-ABEND                               15490000
C47460     END-EVALUATE.                                                15500000
                                                                        15970000
                                                                        14950000
      ******************************************************************14960000
      * CHECK FUTURE PERM CHANGES                                       14970000
      ******************************************************************14980000
       H500-CHECK-FUTURE-PERM-CHANGES.                                  14990000
                                                                        15000000
           EXEC SQL                                                     15010000
               SELECT F.EVNT_CTG_CDE,                                   15020000
                      D.UNT_RTL_AMT                                     15030000
                 INTO :XST00063-EVNT-CTG-CDE                            15040000
                     ,:XST00065-UNT-RTL-AMT                             15050000
                 FROM XST_PRCHG_MDSE D,                                 15060000
                      XST_PRCHG F,                                      15080000
                      XST_STR_GP_MBSHP M                                15090000
                WHERE M.STR_NBR            = :PV-STR-NBR                15100000
JB0201            AND  M.EFF_BEG_DTE      <= :PV-END-TMST-DATE          15110002
                  AND (M.EFF_END_DTE      >= :PV-END-TMST-DATE          15120002
                   OR  M.EFF_END_DTE IS NULL)                           15130000
                  AND D.SKU_NBR           = :PV-SKU-NBR                 15180000
JB0201            AND D.STR_GP_ID         = M.STR_GP_ID                 15181002
JB0201            AND D.STR_GP_TYP_CDE    = M.STR_GP_TYP_CDE            15182002
JB0201            AND D.LAST_ACTN_CDE     <> 'D'                        15183002
                  AND F.PRCHG_ST_CDE      = 'APR'                       15183102
                  AND F.EVNT_CTG_CDE      IN ('ACT', 'CLR')             15184002
JB0201            AND F.PRCHG_ID          = D.PRCHG_ID                  15190002
JB0201         AND EXISTS                                               15210002
JB0201        (SELECT 1                                                 15220002
JB0201           FROM XST_STR_GP_PRCHG E                                15230002
JB0201          WHERE E.STR_GP_TYP_CDE     = M.STR_GP_TYP_CDE           15240002
JB0201            AND E.STR_GP_ID          = M.STR_GP_ID                15250002
JB0201            AND E.PRCHG_EFF_DTE     <= :PV-END-TMST-DATE          15251002
JB0201            AND E.PRCHG_EFF_DTE     >= CURRENT DATE               15252002
JB0201            AND E.PRCHG_ID           = D.PRCHG_ID)                15253002
JB0201         FETCH FIRST 1 ROWS ONLY                                  15260002
                 WITH UR                                                15261002
           END-EXEC.                                                    15270000
                                                                        15280000
           EVALUATE TRUE                                                15290000
               WHEN SQLCODE = 0 OR -811                                 15300000
                   IF XST00063-EVNT-CTG-CDE = 'CLR'                     15310000
      *SKU IS GOING TO BE CLEARANCED - DON'T INCLUDE IN OUTPUT FILE     15320000
                     SET PS-SKU-CLEARANCED TO TRUE                      15330000
                   ELSE                                                 15340000
      *USE XST0065-UNT-RTL-AMT TO CALC PRICE POINT                      15350000
JB0808*FOR 'SPO' RECALC PROMO PRICE POINT AMOUNT FROM THE PERCENT       15350000
JB0808*(TRUNCATE, NOT ROUND!)                                           15350000
                     MOVE XST00065-UNT-RTL-AMT TO PM107-UNT-RTL-AMT     15360000
JB0808               IF PM107-PROMO-PRICG-TYP-CDE = 'SPO'                       
JB0808                 COMPUTE PV-PRIC-PT = XST00065-UNT-RTL-AMT -              
JB0808                         ((PM107-PROMO-PRICG-AMT / 100)                   
JB0808                            * XST00065-UNT-RTL-AMT)                       
JB0808                 MOVE PV-PRIC-PT        TO PM107-PROMO-PRIC-PT-AMT        
JB0808               END-IF                                                     
                   END-IF                                               15370000
                                                                        15380000
               WHEN SQLCODE = +100                                      15390000
                   CONTINUE                                                     
                                                                        15410000
               WHEN OTHER                                               15420000
                   DISPLAY '********************************'           15430000
                   DISPLAY 'PV-SKU-NBR       = ' PV-SKU-NBR             15440000
                   DISPLAY 'PV-STR-NBR       = ' PV-STR-NBR             15450000
                   DISPLAY 'PV-END-TMST-DATE = ' PV-END-TMST-DATE       15460000
                   MOVE 'H500-CHECK-FUTURE-PERM'  TO PV-PARAGRAPH-NAME  15470000
                   MOVE 'ERROR ON XST_CHG_EVNT ' TO PV-OPERATION-MSG-1  15480000
                   PERFORM Z900-SQL-ABEND                               15490000
           END-EVALUATE.                                                15500000
                                                                        15970000
      ******************************************************************15980000
      * WRITE THE EXPLODED FILE                                         15990000
      ******************************************************************16000000
       W100-WRITE-EXPLODED-FILE.                                        16010000
                                                                        16020000
           WRITE EXPLODED-REC FROM PM107-EXCPT-PRCG-EXT-SKU.            16030000
           ADD +1 TO PA-RECS-WRITTEN.                                   16040000
                                                                        16050000
      ******************************************************************16060000
      * SQL ABEND / EL ABENDO DEL SQL                                   16070000
      ******************************************************************16080000
       Z900-SQL-ABEND.                                                  16090000
                                                                        16100000
           DISPLAY 'Z900-SQL-ABEND'.                                    16110000
           DISPLAY '**  ABEND     **'.                                  16120000
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              16130000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            16140000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-MSG-1.           16150000
           DISPLAY '**            **  = ' PV-OPERATION-MSG-2.           16160000
                                                                        16170000
      ******************************************************************16180000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *16190000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *16200000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *16210000
      * FORMAT.                                                        *16220000
      ******************************************************************16230000
           COPY DPPD004.                                                16240000
                                                                        16250000
           SET ABEND-4013-DB2-ERROR TO TRUE.                            16260000
           CALL 'ILBOABN0' USING ABEND-CODE.                            16270000
