000100******************************************************************11/21/95
000200 IDENTIFICATION DIVISION.                                         RVKCS020
000500 PROGRAM-ID.             SUKRF520.                                RVKCS020
000600 AUTHOR.                 MARY SARDINA.                            RVKCS020
000700 INSTALLATION.           KOHLS.                                   RVKCS020
000800 DATE-WRITTEN            SEPTEMBER, 1997.                         RVKCS020
000900 DATE-COMPILED.                                                   RVKCS020
001000                                                                  RVKCS020
001100******************************************************************RVKCS020
001200* THIS PROGRAM DISPLAYS THE CARTON AUDIT ASSOCIATE LOCATION       RVKCS020
001300* SCREEN.                                                               20
001400******************************************************************RVKCS020
001500                                                                  RVKCS020
001600******************************************************************RVKCS020
001700 ENVIRONMENT DIVISION.                                            RVKCS020
002100 DATA DIVISION.                                                   RVKCS020
002200******************************************************************RVKCS020
002300                                                                  RVKCS020
002400******************************************************************RVKCS020
002500 WORKING-STORAGE SECTION.                                         RVKCS020
002600******************************************************************RVKCS020
002700 01  CA-COMMAREA.                                                 RVKCS020
002800     COPY SUWS006.                                                   CL**3
003300*                                                                 RVKCS020
003400 01  PC-PROGRAM-CONSTANTS.                                        RVKCS020
003500     05  PC-LOGON-TRAN-ID                PIC  X(04)  VALUE 'S520'.RVKCS020
003600     05  PC-CURRENT-PROGRAM-NAME         PIC  X(08)  VALUE        RVKCS020
003700         'SUKRF520'.                                              RVKCS020
003800     05  PC-NEXT-PROGRAM                 PIC  X(08)  VALUE        RVKCS020
003900         'SUKRF511'.                                              RVKCS020
004000     05  PC-PREV-PROGRAM                 PIC  X(08)  VALUE        RVKCS020
004100         'SUKRF510'.                                              RVKCS020
004200     05  PC-CURRENT-TRANID               PIC  X(04)  VALUE 'S520'.RVKCS020
004210     05  PC-TRANS-UNAVAILABLE-MSG   PIC X(19) VALUE                       
004220                                           'TRANS NOT AVAILABLE'.         
004300                                                                  RVKCS020
004400 01  PM-PROGRAM-LITERALS.                                         RVKCS020
004500     05  PM-100-CHECK                    PIC  X(10)  VALUE              20
004600         '100% CHECK'.                                            RVKCS020
004700     05  PM-SPOT-CHECK                   PIC  X(10)  VALUE        RVKCS020
004800         'SPOT CHECK'.                                            RVKCS020
004900     05  PM-REWORK-CHECK                 PIC  X(10)  VALUE        RVKCS020
005000         'REWORK    '.                                            RVKCS020
005100                                                                  RVKCS020
005200 01  PS-PROGRAM-SWITCHES.                                         RVKCS020
005300     05  PS-ACCESS-ALLOWED-IND           PIC  X(01)  VALUE 'N'.   RVKCS020
005400         88  PS-NO-ACCESS-ALLOWED                    VALUE 'N'.   RVKCS020
005500         88  PS-ACCESS-ALLOWED                       VALUE 'Y'.   RVKCS020
005600                                                                  RVKCS020
005700 01  PV-PROGRAM-VARIABLES.                                        RVKCS020
005800     05  PV-COMMAREA-LENGTH              PIC S9(04)  VALUE ZERO   RVKCS020
005900                                                     COMP SYNC.   RVKCS020
006000     05  PV-SYSID                        PIC  X(04)  VALUE SPACE. RVKCS020
006100     05  PV-MESSAGE-AREA                 PIC  X(19)  VALUE SPACE. RVKCS020
006200     05  PV-CICS-RESPONSE                PIC S9(08)  VALUE ZEROS  RVKCS020
006300                                                     COMP SYNC.   RVKCS020
006400     05  PV-TRANSFER-PROGRAM             PIC  X(08)  VALUE SPACE. RVKCS020
006500     05  PV-OPER-UNT-ID                  PIC S9(04)  VALUE ZEROS  RVKCS020
006600                                                     COMP.        RVKCS020
006900     05  PV-TS-ITEM                      PIC S9(04)  VALUE ZERO   RVKCS020
007000                                                     COMP.        RVKCS020
007100     05  PV-SIGN-ON-INFO-LENGTH          PIC S9(04)  VALUE ZERO   RVKCS020
007200                                                     COMP SYNC.   RVKCS020
007300     05  PV-AUDIT-TYPE                   PIC  X(02)  VALUE SPACE.       20
007400     05  PV-AUDIT-CNTL-NBR               PIC  9(16)  VALUE ZERO.  RVWS006 
007500*                                                                 RVKCS020
007600 01  TS-TEMP-STORAGE-QUEUE-INFO.                                  RVKCS020
007700     05  TS-INFO-QUEUE-NAME.                                      RVKCS020
007800         10  TS-INFO-QUEUE-TERMINAL      PIC  X(04)  VALUE SPACE. RVKCS020
007900         10  FILLER                      PIC  X(04)  VALUE 'INFO'.RVKCS020
008000                                                                  RVKCS020
008100*                                                                 RVKCS020
008200*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA                RVKCS020
008400     COPY DPWS013.                                                RVKCS020
008500                                                                  RVKCS020
008600*                                                                 RVKCS020
008700*    COPY BOOK FOR THE CICSAUTH SUBROUTINE                        RVKCS020
008900     COPY DPWS008.                                                RVKCS020
009000                                                                  RVKCS020
009100*                                                                 RVKCS020
009200*  SENDING/RECEIVING AREA FOR THE NUMERIC EDIT ROUTINE (DPKUT100) RVKCS020
009400     COPY DPWS010I.                                               RVKCS020
009500                                                                  RVKCS020
009600*                                                                 RVKCS020
009700*  GENERATED DSECT FOR THE SUKM520 MAPSET                         RVKCS020
009900     COPY SUKM520.                                                RVKCS020
010000                                                                  RVKCS020
010100*                                                                 RVKCS020
010200*  KOHL'S STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST   RVKCS020
010400     COPY DPWS015.                                                RVKCS020
010500                                                                  RVKCS020
010520*----------------------------------------------------------------*        
010530* HELP SCREEN COMMAREA                                                    
010540     COPY PCWS192.                                                        
010550*----------------------------------------------------------------*        
010560* HELP SCREEN WORK AREA                                                   
010570     COPY PCWS188.                                                        
010580                                                                          
010600*                                                                 RVKCS020
010700*  KOHL'S STANDARD ATTENTION IDENTIFIER LIST                      RVKCS020
010900     COPY DFHAID.                                                 RVKCS020
011000                                                                  RVKCS020
011100*                                                                 RVKCS020
011200*  TEMPORARY STORAGE QUEUE LAYOUT FOR LOGON/UNIT RELATED          RVKCS020
011300*  INFORMATION CAPTURED BY THE NAVIGATOR AT LOGON TIME            RVKCS020
011400*                                                                 RVKCS020
011500     COPY DPWS026.                                                RVKCS020
011600                                                                  RVKCS020
011700*                                                                 RVKCS020
011800*  COPY BOOK CONTAINING ERROR/WARNING/INFORMATIONAL MESSAGES      RVKCS020
011900*  TO BE DISPLAYED ON THE LRT                                     RVKCS020
012000*                                                                 RVKCS020
012100     COPY SUWS005.                                                RVKCS020
012200                                                                          
012900*                                                                 RVKCS021
013000*  DCLGEN LAYOUT AND COBOL DECLARATION FOR THE AUDIT TABLE        RVKCS021
013100*                                                                 RVKCS021
013200     EXEC SQL                                                     RVKCS021
013300         INCLUDE TSUNAUD                                          RVKCS021
013400     END-EXEC.                                                    RVKCS021
013500                                                                  RVKCS021
013600*                                                                 RVKCS021
013700*  COMMUNICATIONS AREA FOR DB2                                    RVKCS021
013800*                                                                 RVKCS021
013900     EXEC SQL                                                     RVKCS021
014000         INCLUDE SQLCA                                            RVKCS021
014100     END-EXEC.                                                    RVKCS021
014200                                                                  RVKCS021
014300                                                                  RVKCS020
014400******************************************************************RVKCS020
014500 LINKAGE SECTION.                                                 RVKCS020
014600******************************************************************RVKCS020
014700 01  DFHCOMMAREA.                                                         
014800     05  FILLER                         OCCURS  1 TO 4072 TIMES           
014900                                        DEPENDING ON EIBCALEN.            
015000         10  FILLER                     PIC X.                            
015100                                                                          
015200                                                                  RVKCS020
015300******************************************************************RVKCS020
015400 PROCEDURE DIVISION.                                              RVKCS020
015500******************************************************************RVKCS020
015600 0000-PROGRAM-MAINLINE.                                           RVKCS020
015700     MOVE DFHCOMMAREA  TO  CA-COMMAREA.                           RVKCS020
015800     MOVE LOW-VALUES   TO  SU520AI.                               RVKCS020
015900                                                                  RVKCS020
016000     EVALUATE TRUE                                                RVKCS020
016010****************************************************************          
016020* DPKRF278 IS THE PGM FOR THE HELP SCREEN.                                
016030****************************************************************          
016040         WHEN EIBTRNID = 'Z278'                                           
016050             MOVE DFHCOMMAREA          TO PC192-COMMAREA                  
016060             MOVE PC192-HOST-SAVE-AREA TO CA-COMMAREA                     
016070             SET SU006-PROCESS-SCREEN  TO TRUE                            
016080             PERFORM 1500-FILL-SCREEN                                     
016090             PERFORM 8000-SEND-MAP                                        
016091             PERFORM 8200-RETURN-TRANID                                   
016092                                                                          
016100         WHEN EIBCALEN = ZERO                                     RVKCS020
016200         WHEN EIBAID = DFHCLEAR                                   RVKCS020
016300         WHEN SU006-INITIALIZE-SCREEN                             RVKCS020
016500             SET SU006-PROCESS-SCREEN  TO  TRUE                   RVKCS020
016600             PERFORM 1500-FILL-SCREEN                                     
016700             PERFORM 8000-SEND-MAP                                RVKCS020
016800             PERFORM 8200-RETURN-TRANID                           RVKCS020
016900         WHEN EIBAID = DFHENTER                                   RVKCS020
017000             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
017100             PERFORM 8200-RETURN-TRANID                           RVKCS020
017200         WHEN EIBAID = DFHPF3                                     RVKCS020
017300             MOVE PC-PREV-PROGRAM       TO  PV-TRANSFER-PROGRAM   RVKCS020
017400             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                RVKCS020
017500         WHEN EIBAID = DFHPF2                                     RVKCS020
017600             MOVE PC-NEXT-PROGRAM       TO  PV-TRANSFER-PROGRAM   RVKCS020
017700             PERFORM 2100-XCTL-TO-SELECTED-PROGRAM                RVKCS020
017800         WHEN EIBAID = DFHPF4                                     RVKCS020
017900            SET SU006-100-AUDIT-CHECK TO TRUE                             
018000             MOVE SU006-AUDIT-TYPE TO PV-AUDIT-TYPE                       
018100             MOVE SU006-AUDIT-CNTL-NBR TO SUNAUD-AUD-CNTL-NBR             
018200             PERFORM 7000-UPDATE-AUDIT-TYPE                               
018300             MOVE PM-PF2-CONTINUE-MSG      TO  AMSGLNO                  20
018400             MOVE PM-100-CHECK TO    ATYPEI                               
018500             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
018600             PERFORM 8200-RETURN-TRANID                           RVKCS020
018700         WHEN EIBAID = DFHPF5                                     RVKCS020
018800             SET SU006-SPOT-AUDIT-CHECK TO TRUE                           
018900             MOVE SU006-AUDIT-TYPE TO PV-AUDIT-TYPE                       
019000             MOVE PM-SPOT-CHECK  TO   ATYPEI                              
019100             MOVE SU006-AUDIT-CNTL-NBR TO SUNAUD-AUD-CNTL-NBR             
019200             PERFORM 7000-UPDATE-AUDIT-TYPE                               
019300             MOVE PM-PF2-CONTINUE-MSG      TO  AMSGLNO                  20
019400             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
019500             PERFORM 8200-RETURN-TRANID                           RVKCS020
019600         WHEN EIBAID = DFHPF6                                     RVKCS020
019700             IF SU006-SPOT-AUDIT-CHECK                                    
019800                 SET SU006-REWORK-AUDIT-CHECK TO TRUE                     
019900                 MOVE SU006-AUDIT-TYPE TO PV-AUDIT-TYPE                   
020000                 MOVE PM-REWORK-CHECK TO  ATYPEI                          
020100                 MOVE SU006-AUDIT-CNTL-NBR TO SUNAUD-AUD-CNTL-NBR         
020200                 PERFORM 7000-UPDATE-AUDIT-TYPE                           
020300                 MOVE PM-PF2-CONTINUE-MSG      TO  AMSGLNO                
020400             ELSE                                                         
020500                 MOVE PM-INVALID-SELECTION-MSG TO  AMSGLNO                
020600             END-IF                                                       
020700             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
020800             PERFORM 8200-RETURN-TRANID                           RVKCS020
020810         WHEN EIBAID = DFHPF1                                             
020820              PERFORM 1400-HELP-SCREEN                                    
020830                                                                          
020900         WHEN OTHER                                               RVKCS020
021000             MOVE PM-UNRECOGNIZED-PF-KEY-MSG  TO  AMSGLNO         RVKCS020
021100             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
021200             PERFORM 8200-RETURN-TRANID                           RVKCS020
021300     END-EVALUATE.                                                RVKCS020
021400                                                                  RVKCS020
021500     GOBACK.                                                      RVKCS020
021600*                                                                 RVKCS020
021700 1000-FIND-SYSTEM-ID.                                             RVKCS020
021800     EXEC CICS                                                    RVKCS020
021900         INQUIRE                                                  RVKCS020
022000             TRANSACTION   (PC-LOGON-TRAN-ID)                     RVKCS020
022100             REMOTESYSTEM  (PV-SYSID)                             RVKCS020
022200             RESP          (PV-CICS-RESPONSE)                     RVKCS020
022300     END-EXEC.                                                    RVKCS020
022400                                                                  RVKCS020
022500     EVALUATE TRUE                                                RVKCS020
022600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
022700             CONTINUE                                             RVKCS020
022800         WHEN OTHER                                               RVKCS020
022900             MOVE '1000-FIND-SYSTEM-ID'                           RVKCS020
023000                 TO DP013-PARAGRAPH                               RVKCS020
023100             MOVE 'UNABLE TO DETERMINE SYSTEM ID'                 RVKCS020
023200                 TO DP013-MESSAGE-TEXT (1)                        RVKCS020
023300             SET DP013-CICS-ABEND      TO  TRUE                   RVKCS020
023400             SET DP013-NO-ROLLBACK     TO  TRUE                   RVKCS020
023500             PERFORM 9999-WRITE-CICS-LOG-MSG                      RVKCS020
023600             MOVE PM-CICS-FATAL-ERROR-MSG                         RVKCS020
023700                                       TO  AMSGLNO                RVKCS020
023800             SET SU006-PROCESS-SCREEN  TO  TRUE                   RVKCS020
023900             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
024000             PERFORM 8200-RETURN-TRANID                           RVKCS020
024100     END-EVALUATE.                                                RVKCS020
024200*                                                                 RVKCS020
024300 1100-FIND-LOGON-INFO.                                            RVKCS020
024400     MOVE LENGTH OF DP026-SIGN-ON-INFO                            RVKCS020
024500                    TO  PV-SIGN-ON-INFO-LENGTH.                   RVKCS020
024600     MOVE EIBTRMID  TO  TS-INFO-QUEUE-TERMINAL.                   RVKCS020
024700     MOVE +1        TO  PV-TS-ITEM.                               RVKCS020
024800                                                                  RVKCS020
024900     IF PV-SYSID = SPACES                                         RVKCS020
025000         EXEC CICS                                                RVKCS020
025100             READQ TS                                             RVKCS020
025200                 QUEUE  (TS-INFO-QUEUE-NAME)                      RVKCS020
025300                 ITEM   (PV-TS-ITEM)                              RVKCS020
025400                 INTO   (DP026-SIGN-ON-INFO)                      RVKCS020
025500                 RESP   (PV-CICS-RESPONSE)                        RVKCS020
025600         END-EXEC                                                 RVKCS020
025700     ELSE                                                         RVKCS020
025800         EXEC CICS                                                RVKCS020
025900             READQ TS                                             RVKCS020
026000                 QUEUE   (TS-INFO-QUEUE-NAME)                     RVKCS020
026100                 SYSID   (PV-SYSID)                               RVKCS020
026200                 ITEM    (PV-TS-ITEM)                             RVKCS020
026300                 INTO    (DP026-SIGN-ON-INFO)                     RVKCS020
026400                 LENGTH  (PV-SIGN-ON-INFO-LENGTH)                 RVKCS020
026500                 RESP    (PV-CICS-RESPONSE)                       RVKCS020
026600         END-EXEC                                                 RVKCS020
026700     END-IF.                                                      RVKCS020
026800                                                                  RVKCS020
026900     EVALUATE TRUE                                                RVKCS020
027000         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
027100             MOVE DP026-LOCATION-NBR                              RVKCS020
027200                                  TO  SU006-OPER-UNT-ID           RVKCS020
027300             MOVE DP026-USERID    TO  SU006-USERID                RVKCS020
027400         WHEN OTHER                                               RVKCS020
027500             MOVE '1100-FIND-LOGON-INFO'                          RVKCS020
027600                 TO DP013-PARAGRAPH                               RVKCS020
027700             MOVE 'UNABLE TO READ LOGON TSQ'                      RVKCS020
027800                 TO DP013-MESSAGE-TEXT (1)                        RVKCS020
027900             SET DP013-CICS-ABEND     TO TRUE                     RVKCS020
028000             SET DP013-NO-ROLLBACK    TO TRUE                     RVKCS020
028100             PERFORM 9999-WRITE-CICS-LOG-MSG                      RVKCS020
028200             MOVE PM-SYSTEM-ERROR-MSG TO AMSGLNO                  RVKCS020
028300             SET SU006-PROCESS-SCREEN TO TRUE                     RVKCS020
028400             PERFORM 8100-SEND-DATA-ONLY                          RVKCS020
028500             PERFORM 8200-RETURN-TRANID                           RVKCS020
028600     END-EVALUATE.                                                RVKCS020
028700*                                                                 RVKCS020
028800*1200-RECEIVE-MAP.                                                RVKCS020
028900*    EXEC CICS                                                    RVKCS020
029000*        RECEIVE                                                  RVKCS020
029100*            MAP     ('SU520A')                                   RVKCS020
029200*            MAPSET  ('SUKM520')                                  RVKCS020
029300*            RESP    (PV-CICS-RESPONSE)                           RVKCS020
029400*    END-EXEC.                                                    RVKCS020
029500*                                                                 RVKCS020
029600**   EVALUATE TRUE                                                RVKCS020
029700*        WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
029800*             PERFORM 1210-RECEIVE-SCREEN-FIELDS                  RVKCS020
029900*        WHEN PV-CICS-RESPONSE = DFHRESP (MAPFAIL)                RVKCS020
030000*             CONTINUE                                            RVKCS020
030100*        WHEN OTHER                                               RVKCS020
030200*             MOVE '1200-RECEIVE-MAP'                             RVKCS020
030300*                                    TO DP013-PARAGRAPH           RVKCS020
030400*             MOVE 'UNABLE TO RECEIVE MAP'                        RVKCS020
030500*                                    TO DP013-MESSAGE-TEXT (1)    RVKCS020
030600*             SET DP013-LOGIC-ABEND  TO TRUE                      RVKCS020
030700*             SET DP013-NO-ROLLBACK  TO TRUE                      RVKCS020
030800*             PERFORM 9999-WRITE-CICS-LOG-MSG                     RVKCS020
030900*    END-EVALUATE.                                                RVKCS020
031000*                                                                 RVKCS020
031100*1210-RECEIVE-SCREEN-FIELDS.                                      RVKCS020
031200*                                                                         
031300*    IF  ACNTLL > ZERO                                            RVKCS020
031400*        MOVE ACNTLI  TO SU006-AUDIT-CNTL-NBR-X                   RVKCS020
031500*    END-IF.                                                      RVKCS020
031600*                                                                         
031700*    IF  A100L    > ZERO                                          RVKCS020
031800*        MOVE A100I     TO  SU006-100-CHECK                       RVKCS020
031900*    END-IF.                                                      RVKCS020
032000*                                                                         
032100*    IF  ASPOTL   > ZERO                                          RVKCS020
032200*        MOVE ASPOTI    TO  SU006-SPOT-CHECK                      RVKCS020
032300*    END-IF.                                                      RVKCS020
032400*                                                                         
037810                                                                          
037820 1400-HELP-SCREEN.                                                        
037830     MOVE 'DPKRF278'                  TO PV-TRANSFER-PROGRAM              
037840     MOVE 'SUKRF520'                  TO PC192-PROGRAM-NAME               
037850     COPY PCPD188.                                                        
037860     MOVE PC188-DISPLAY-DATE          TO PC192-DISPLAY-DATE.              
037870     PERFORM 4200-XCTL-TO-HELP.                                           
037880                                                                  08190043
037900**************************************************************            
038000* MOVES DATA FROM THE PREVIOUS SCREEEN                       *            
038100**************************************************************            
038200 1500-FILL-SCREEN.                                                        
038300     MOVE SU006-AUDIT-CNTL-NBR-X     TO    ANBRI.                         
038400     MOVE SU006-AUDIT-CNTL-NBR   TO    PV-AUDIT-CNTL-NBR.                 
038500     IF SU006-100-AUDIT-CHECK                                             
038600          MOVE  PM-100-CHECK         TO    ATYPEI                         
038700     END-IF.                                                              
038800     IF SU006-SPOT-AUDIT-CHECK                                            
038900          MOVE  PM-SPOT-CHECK         TO    ATYPEI                        
039000     END-IF.                                                              
039100     IF SU006-REWORK-AUDIT-CHECK                                          
039200          MOVE  PM-REWORK-CHECK       TO    ATYPEI                        
039300     END-IF.                                                              
039400*                                                                 RVKCS020
039500**************************************************************            
039600* ALL DATA HAS PASSED THE EDITS, CONTINUE ON WITH PROCESSING *            
039700**************************************************************            
039800 2000-PROCESS-SELECTION.                                          RVKCS020
039900     MOVE PC-NEXT-PROGRAM            TO PV-TRANSFER-PROGRAM.      RVKCS020
040000*    PERFORM 2100-XCTL-TO-SELECTED-PROGRAM.                       RVKCS020
040100*                                                                 RVKCS020
040200 2100-XCTL-TO-SELECTED-PROGRAM.                                   RVKCS020
040300     SET SU006-INITIALIZE-SCREEN   TO  TRUE.                      RVKCS020
040400     MOVE LENGTH OF CA-COMMAREA    TO  PV-COMMAREA-LENGTH.        RVKCS020
040500                                                                  RVKCS020
040600     EXEC CICS                                                    RVKCS020
040700         XCTL                                                     RVKCS020
040800             PROGRAM   (PV-TRANSFER-PROGRAM)                      RVKCS020
040900             COMMAREA  (CA-COMMAREA)                              RVKCS020
041000             LENGTH    (PV-COMMAREA-LENGTH)                       RVKCS020
041100             RESP      (PV-CICS-RESPONSE)                         RVKCS020
041200     END-EXEC.                                                    RVKCS020
041300                                                                  RVKCS020
041400     EVALUATE TRUE                                                RVKCS020
041500         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
041600             MOVE PC-CURRENT-PROGRAM-NAME                                 
041700                TO  SU006-LAST-PROGRAM                                    
041800         WHEN OTHER                                               RVKCS020
041900              MOVE '2100-XCTL-TO-SELECTED-PROGRAM'                RVKCS020
042000                                     TO DP013-PARAGRAPH           RVKCS020
042100              MOVE 'UNABLE TO XCTL THE FOLLOWING PROGRAM: '       RVKCS020
042200                                     TO DP013-MESSAGE-TEXT (1)    RVKCS020
042300              MOVE PV-TRANSFER-PROGRAM                            RVKCS020
042400                                     TO DP013-MESSAGE-TEXT (2)    RVKCS020
042500              SET DP013-CICS-ABEND   TO TRUE                      RVKCS020
042600              SET DP013-NO-ROLLBACK  TO TRUE                      RVKCS020
042700              PERFORM 9999-WRITE-CICS-LOG-MSG                     RVKCS020
042800              MOVE PM-TRANS-NOT-AVAILABLE-MSG                     RVKCS020
042900                                     TO AMSGLNO                   RVKCS020
043000              SET SU006-PROCESS-SCREEN TO TRUE                    RVKCS020
043100              PERFORM 8100-SEND-DATA-ONLY                         RVKCS020
043200              PERFORM 8200-RETURN-TRANID                          RVKCS020
043300     END-EVALUATE.                                                RVKCS020
047510*                                                                         
047520 4200-XCTL-TO-HELP.                                                       
047530                                                                          
047540     MOVE LENGTH OF PC192-COMMAREA    TO PV-COMMAREA-LENGTH.              
047550     SET  SU006-INITIALIZE-SCREEN     TO TRUE.                            
047560     MOVE CA-COMMAREA                 TO PC192-HOST-SAVE-AREA.            
047570                                                                          
047580     EXEC CICS                                                            
047590         XCTL                                                             
047591             PROGRAM   (PV-TRANSFER-PROGRAM)                              
047592             COMMAREA  (PC192-COMMAREA)                                   
047593             LENGTH    (PV-COMMAREA-LENGTH)                               
047594             RESP      (PV-CICS-RESPONSE)                                 
047595     END-EXEC.                                                            
047596                                                                          
047597     EVALUATE TRUE                                                        
047598         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                         
047599              CONTINUE                                                    
047600         WHEN OTHER                                                       
047601             MOVE '4200-XCTL-TO-HELP '                                    
047602                 TO DP013-PARAGRAPH                                       
047603             MOVE 'SUKRF520 - UNABLE TO XCTL TO PROGRAM: '                
047604                 TO DP013-MESSAGE-TEXT (1)                                
047605             MOVE PV-TRANSFER-PROGRAM                                     
047606                 TO DP013-MESSAGE-TEXT (2)                                
047607             SET DP013-CICS-ABEND  TO TRUE                                
047608             SET DP013-ROLLBACK TO TRUE                                   
047609             PERFORM 9999-WRITE-CICS-LOG-MSG                              
047610             MOVE PC-TRANS-UNAVAILABLE-MSG TO AMSGLNO                     
047611             SET SU006-PROCESS-SCREEN TO TRUE                             
047612             PERFORM 8000-SEND-MAP                                        
047613             PERFORM 8200-RETURN-TRANID                                   
047614     END-EVALUATE.                                                        
047620 7000-UPDATE-AUDIT-TYPE.                                                  
047700     EXEC SQL                                                             
047800          UPDATE                                                          
047900               TSUNAUD                                                    
048000          SET AUD_TYP_CDE       =  :PV-AUDIT-TYPE                         
048100        WHERE AUD_CNTL_NBR      =  :SUNAUD-AUD-CNTL-NBR                   
048200     END-EXEC.                                                            
048300                                                                          
048400     EVALUATE TRUE                                                        
048500         WHEN SQLCODE = +0                                                
048600         WHEN SQLCODE = +100                                              
048700         WHEN SQLCODE = -904                                              
048800         WHEN SQLCODE = -913                                              
048900              CONTINUE                                                    
049000         WHEN SQLWARN0 NOT = SPACES                                       
049100         WHEN SQLCODE  NOT = ZERO                                         
049200              MOVE '7000-UPDATE-AUDIT-TYPE'                               
049300                              TO DP013-PARAGRAPH                          
049400              MOVE 'UNSUCCESSFUL UPDATE ON TSUNAUD'                       
049500                              TO DP013-MESSAGE-TEXT (1)                   
049600              MOVE SQLCA      TO DP013-SQLCA                              
049700              MOVE 'TSUNAUD ' TO DP013-DB2-TABLE-NAME (1)                 
049800              SET DP013-DB2-ABEND    TO TRUE                              
049900              SET DP013-NO-ROLLBACK  TO TRUE                              
050000              PERFORM 9999-WRITE-CICS-LOG-MSG                             
050100     END-EVALUATE.                                                        
050200                                                                          
050300 8000-SEND-MAP.                                                   RVKCS020
050400     EXEC CICS                                                    RVKCS020
050500         SEND                                                     RVKCS020
050600             MAP     ('SU520A')                                   RVKCS020
050700             MAPSET  ('SUKM520')                                  RVKCS020
050800             ERASE                                                RVKCS020
050900             RESP    (PV-CICS-RESPONSE)                           RVKCS020
051000     END-EXEC.                                                    RVKCS020
051100                                                                  RVKCS020
051200     EVALUATE TRUE                                                RVKCS020
051300         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
051400             CONTINUE                                             RVKCS020
051500         WHEN OTHER                                               RVKCS020
051600             MOVE '8000-SEND-MAP'   TO DP013-PARAGRAPH            RVKCS020
051700             MOVE 'UNABLE TO SEND MAP'                            RVKCS020
051800                                    TO DP013-MESSAGE-TEXT (1)     RVKCS020
051900             SET DP013-CICS-ABEND   TO TRUE                       RVKCS020
052000             SET DP013-NO-ROLLBACK  TO TRUE                       RVKCS020
052100             PERFORM 9999-WRITE-CICS-LOG-MSG                      RVKCS020
052200     END-EVALUATE.                                                RVKCS020
052300*                                                                 RVKCS020
052400 8100-SEND-DATA-ONLY.                                             RVKCS020
052500                                                                  RVKCS020
052600     EXEC CICS                                                    RVKCS020
052700         SEND                                                     RVKCS020
052800             MAP       ('SU520A')                                 RVKCS020
052900             MAPSET    ('SUKM520')                                RVKCS020
053000             CURSOR                                               RVKCS020
053100             DATAONLY                                             RVKCS020
053200             RESP      (PV-CICS-RESPONSE)                         RVKCS020
053300     END-EXEC.                                                    RVKCS020
053400                                                                  RVKCS020
053500     EVALUATE TRUE                                                RVKCS020
053600         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
053700             CONTINUE                                             RVKCS020
053800         WHEN OTHER                                               RVKCS020
053900             MOVE '8100-SEND-DATA-ONLY'                           RVKCS020
054000                                    TO DP013-PARAGRAPH            RVKCS020
054100             MOVE 'UNABLE TO SEND DATA'                           RVKCS020
054200                                    TO DP013-MESSAGE-TEXT (1)     RVKCS020
054300             SET DP013-CICS-ABEND   TO TRUE                       RVKCS020
054400             SET DP013-NO-ROLLBACK  TO TRUE                       RVKCS020
054500             PERFORM 9999-WRITE-CICS-LOG-MSG                      RVKCS020
054600     END-EVALUATE.                                                RVKCS020
054700*                                                                 RVKCS020
054800 8200-RETURN-TRANID.                                              RVKCS020
054900     MOVE LENGTH OF CA-COMMAREA  TO  PV-COMMAREA-LENGTH.          RVKCS020
055000                                                                  RVKCS020
055100     EXEC CICS                                                    RVKCS020
055200         RETURN                                                   RVKCS020
055300             TRANSID   (PC-CURRENT-TRANID)                        RVKCS020
055400             COMMAREA  (CA-COMMAREA)                              RVKCS020
055500             LENGTH    (PV-COMMAREA-LENGTH)                       RVKCS020
055600             RESP      (PV-CICS-RESPONSE)                         RVKCS020
055700     END-EXEC.                                                    RVKCS020
055800                                                                  RVKCS020
055900     EVALUATE TRUE                                                RVKCS020
056000         WHEN PV-CICS-RESPONSE = DFHRESP (NORMAL)                 RVKCS020
056100             CONTINUE                                             RVKCS020
056200         WHEN OTHER                                               RVKCS020
056300             MOVE '8200-RETURN-TRANID'                            RVKCS020
056400                                    TO DP013-PARAGRAPH            RVKCS020
056500             MOVE 'UNABLE TO RETURN TO TRANID'                    RVKCS020
056600                                    TO DP013-MESSAGE-TEXT (1)     RVKCS020
056700             SET DP013-CICS-ABEND   TO TRUE                       RVKCS020
056800             SET DP013-NO-ROLLBACK  TO TRUE                       RVKCS020
056900             PERFORM 9999-WRITE-CICS-LOG-MSG                      RVKCS020
057000     END-EVALUATE.                                                RVKCS020
057100*                                                                 RVKCS020
057200*                                                                 RVKCS020
057300 9999-WRITE-CICS-LOG-MSG.                                         RVKCS020
057400     MOVE PC-CURRENT-PROGRAM-NAME    TO DP013-PROGRAM.            RVKCS020
057500     MOVE DFHEIBLK                   TO DP013-DFHEIBLK.           RVKCS020
057600     INITIALIZE DP013-SRC-TASK-NUMBER                             RVKCS020
057700                DP013-STACK.                                      RVKCS020
057800     SET DP013-LINK-RETURN           TO TRUE.                     RVKCS020
057900                                                                  RVKCS020
058000     EXEC CICS                                                    RVKCS020
058100         LINK                                                     RVKCS020
058200             PROGRAM   (DP013-NAME)                               RVKCS020
058300             COMMAREA  (DP013-PARAMETERS)                         RVKCS020
058400             LENGTH    (DP013-LENGTH)                             RVKCS020
058500     END-EXEC.                                                    RVKCS020
