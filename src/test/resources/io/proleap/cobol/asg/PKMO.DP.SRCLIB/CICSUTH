00001 ******************************************************************06/19/95
00002  IDENTIFICATION DIVISION.                                         CICSAUTH
00003 ******************************************************************   LV001
00004                                                                   CICSAUTH
00005  PROGRAM-ID.                CICSAUTH.                             CICSAUTH
00006  AUTHOR.                    URSULA MCCLOSKEY.                     CICSAUTH
00007  INSTALLATION.              KOHLS DEPARTMENT STORES.              CICSAUTH
00008  DATE-WRITTEN.              02-25-93.                             CICSAUTH
00009  DATE-COMPILED.                                                   CICSAUTH
00010                                                                   CICSAUTH
00011 ******************************************************************CICSAUTH
00012 *  CICS PROGRAM - CICSAUTH                                       *CICSAUTH
00013 *                                                                *CICSAUTH
00014 *  CALL CICS SECURITY FACILITY TO DETERMINE IF THE USER HAS      *CICSAUTH
00015 *  ACCESS TO THE RESOURCE.                                       *CICSAUTH
00016 *  THE CALLING ROUTINE PRESENTS THE RESOURCE REQUIRED.           *CICSAUTH
00017 *  THIS ROUTINE USES THE ACEE OF THE USER LOGGED ON.             *CICSAUTH
00018 ******************************************************************CICSAUTH
00019                                                                   CICSAUTH
00020 ******************************************************************CICSAUTH
00021  ENVIRONMENT DIVISION.                                            CICSAUTH
00022 ******************************************************************CICSAUTH
00023                                                                   CICSAUTH
00024 ******************************************************************CICSAUTH
00025  DATA DIVISION.                                                   CICSAUTH
00026 ******************************************************************CICSAUTH
00027                                                                   CICSAUTH
00028  WORKING-STORAGE SECTION.                                         CICSAUTH
00029                                                                   CICSAUTH
00030  01  PV-PROGRAM-VARIABLES.                                        CICSAUTH
00031      05  PV-Q-MESSAGE                PIC X(40) VALUE SPACES.      CICSAUTH
00032      05  PV-Q-LENGTH                 PIC S9(4) COMP VALUE 40.     CICSAUTH
00033      05  PV-CICS-ID.                                              CICSAUTH
00034          10  FILLER                  PIC X(4).                    CICSAUTH
00035          10  PV-CICS-REGION-TYPE     PIC X.                       CICSAUTH
00036              88  PV-CICS-REGION-PRODUCTION  VALUE 'P'.            CICSAUTH
00037              88  PV-CICS-REGION-ACCEPTANCE  VALUE 'A'.            CICSAUTH
00038              88  PV-CICS-REGION-QUALITY     VALUE 'Q'.            CICSAUTH
00039              88  PV-CICS-REGION-LEARNING    VALUE 'L'.            CICSAUTH
00040              88  PV-CICS-REGION-TEST        VALUE 'T'.            CICSAUTH
00041              88  PV-CICS-REGION-SYSTEST     VALUE 'S'.            CICSAUTH
00042          10  PV-CICS-REGION-CODE     PIC X.                       CICSAUTH
00043          10  FILLER                  PIC X(2).                    CICSAUTH
00044      05  PV-RESCLASS-USER.                                        CICSAUTH
00045          10  FILLER              PIC X(2)     VALUE 'M$'.         CICSAUTH
00046          10  PV-RESCLASS-U-CODE    PIC X.                         CICSAUTH
00047              88  PV-RESCLASS-U-PRODUCTION     VALUE 'P'.          CICSAUTH
00048              88  PV-RESCLASS-U-TEST           VALUE 'T'.          CICSAUTH
00049              88  PV-RESCLASS-U-QUALITY        VALUE 'Q'.          CICSAUTH
00050          10  FILLER              PIC X(5)     VALUE '$USER'.      CICSAUTH
00051      05  PV-RESCLASS-TRAN.                                        CICSAUTH
00052          10  FILLER              PIC X(2)     VALUE 'T$'.         CICSAUTH
00053          10  PV-RESCLASS-T-CODE    PIC X.                         CICSAUTH
00054              88  PV-RESCLASS-T-PRODUCTION     VALUE 'P'.          CICSAUTH
00055              88  PV-RESCLASS-T-TEST           VALUE 'T'.          CICSAUTH
00056              88  PV-RESCLASS-T-QUALITY        VALUE 'Q'.          CICSAUTH
00057          10  FILLER              PIC X(5)     VALUE '1TRN '.      CICSAUTH
00058      05  PV-RESID-LENGTH         PIC S9(8)    COMP.               CICSAUTH
00059      05  PV-LOGMESSAGE           PIC X(5).                        CICSAUTH
00060      05  PV-AUTH-READ            PIC S9(8)    COMP    SYNC.       CICSAUTH
00061      05  PV-AUTH-ALTER           PIC S9(8)    COMP    SYNC.       CICSAUTH
00062      05  PV-AUTH-UPDATE          PIC S9(8)    COMP    SYNC.       CICSAUTH
00063      05  PV-RESP                 PIC S9(8)    COMP    SYNC.       CICSAUTH
00064      05  PV-QUERY-DONE           PIC X.                           CICSAUTH
00065          88  PV-QUERY-COMPLETE     VALUE 'T'.                     CICSAUTH
00066          88  PV-QUERY-INCOMPLETE   VALUE 'F'.                     CICSAUTH
00067      05  PV-EDIT-INPUT           PIC X       VALUE 'F'.           CICSAUTH
00068          88  PV-ERROR-IN-INPUT     VALUE 'T'.                     CICSAUTH
00069          88  PV-NO-ERROR-IN-INPUT  VALUE 'F'.                     CICSAUTH
00070      05  PV-RESID.                                                CICSAUTH
00071          10  PV-RESID-CODE           PIC X(2)     VALUE SPACES.   CICSAUTH
00072              88  PV-CICS-CODE-M             VALUE 'M-'.           CICSAUTH
00073              88  PV-CICS-CODE-F             VALUE 'F-'.           CICSAUTH
00074              88  PV-CICS-CODE-S             VALUE 'S#'.           CICSAUTH
00075              88  PV-CICS-CODE-D             VALUE 'D#'.           CICSAUTH
00076          10  PV-RESID-VALUE          PIC X(10)    VALUE SPACES.   CICSAUTH
00077 *        10  FILLER                  PIC X(234)   VALUE SPACES.   CICSAUTH
00078                                                                   CICSAUTH
00079      COPY DPWS008.                                                CICSAUTH
00081                                                                   CICSAUTH
00082 ******************************************************************CICSAUTH
00083  LINKAGE SECTION.                                                 CICSAUTH
00084 ******************************************************************CICSAUTH
00085                                                                   CICSAUTH
00086  01  DFHCOMMAREA                PIC X(21).                        CICSAUTH
00087                                                                   CICSAUTH
00088 ******************************************************************CICSAUTH
00089  PROCEDURE DIVISION.                                              CICSAUTH
00090 ******************************************************************CICSAUTH
00091                                                                   CICSAUTH
00092  0000-MAINLINE.                                                   CICSAUTH
00093      PERFORM 1000-INITIALIZE.                                     CICSAUTH
00094                                                                   CICSAUTH
00095      IF DP008-RSRC-TYPE-TRAN                                      CICSAUTH
00096           PERFORM 2000-QUERY-TRANS-CLASS                          CICSAUTH
00097      ELSE                                                         CICSAUTH
00098           PERFORM 3000-QUERY-USER-CLASS.                          CICSAUTH
00099                                                                   CICSAUTH
00100      IF PV-QUERY-COMPLETE                                         CICSAUTH
00101          PERFORM 4000-EVALUATE-QUERY-RESULTS.                     CICSAUTH
00102                                                                   CICSAUTH
00103      PERFORM 9000-RETURN-TO-CALLING-TRANS.                        CICSAUTH
00104                                                                   CICSAUTH
00105      GOBACK.                                                      CICSAUTH
00106                                                                   CICSAUTH
00107 ******************************************************************CICSAUTH
00108 *                                                                *CICSAUTH
00109 ******************************************************************CICSAUTH
00110                                                                   CICSAUTH
00111  1000-INITIALIZE.                                                 CICSAUTH
00112                                                                   CICSAUTH
00113      MOVE DFHCOMMAREA TO DP008-SECURITY-COMMAREA.                 CICSAUTH
00114      EXEC CICS                                                    CICSAUTH
00115        ASSIGN                                                     CICSAUTH
00116          APPLID (PV-CICS-ID)                                      CICSAUTH
00117      END-EXEC.                                                    CICSAUTH
00118                                                                   CICSAUTH
00119      EVALUATE TRUE                                                CICSAUTH
00120          WHEN PV-CICS-REGION-PRODUCTION                           CICSAUTH
00121          WHEN PV-CICS-REGION-ACCEPTANCE                           CICSAUTH
00122          WHEN PV-CICS-REGION-LEARNING                             CICSAUTH
00123              SET PV-RESCLASS-U-PRODUCTION                         CICSAUTH
00124                  PV-RESCLASS-T-PRODUCTION TO TRUE                 CICSAUTH
00125          WHEN PV-CICS-REGION-TEST                                 CICSAUTH
00126          WHEN PV-CICS-REGION-SYSTEST                              CICSAUTH
00127              SET PV-RESCLASS-U-TEST                               CICSAUTH
00128                  PV-RESCLASS-T-TEST TO TRUE                       CICSAUTH
00129          WHEN PV-CICS-REGION-QUALITY                              CICSAUTH
00130              SET PV-RESCLASS-U-QUALITY                            CICSAUTH
00131                  PV-RESCLASS-T-QUALITY TO TRUE                    CICSAUTH
00132 *                                                                 CICSAUTH
00133      SET PV-QUERY-INCOMPLETE  TO TRUE.                            CICSAUTH
00134                                                                   CICSAUTH
00135 *1000-EXIT.                                                       CICSAUTH
00136                                                                   CICSAUTH
00137 ******************************************************************CICSAUTH
00138 *  CHECK TRANSACTION AUTHORITY                                   *CICSAUTH
00139 ******************************************************************CICSAUTH
00140                                                                   CICSAUTH
00141  2000-QUERY-TRANS-CLASS.                                          CICSAUTH
00142      MOVE LENGTH OF DP008-TRAN-ID TO PV-RESID-LENGTH.             CICSAUTH
00143      EXEC CICS                                                    CICSAUTH
00144          QUERY SECURITY                                           CICSAUTH
00145              RESCLASS(PV-RESCLASS-TRAN)                           CICSAUTH
00146              RESID   (DP008-TRAN-ID)                              CICSAUTH
00147              RESIDLENGTH (PV-RESID-LENGTH)                        CICSAUTH
00148              READ    (PV-AUTH-READ)                               CICSAUTH
00149              ALTER   (PV-AUTH-ALTER)                              CICSAUTH
00150              UPDATE  (PV-AUTH-UPDATE)                             CICSAUTH
00151              RESP    (PV-RESP)                                    CICSAUTH
00152              NOLOG                                                CICSAUTH
00153      END-EXEC.                                                    CICSAUTH
00154                                                                   CICSAUTH
00155      SET PV-QUERY-COMPLETE  TO TRUE.                              CICSAUTH
00156                                                                   CICSAUTH
00157 *2000-EXIT.                                                       CICSAUTH
00158                                                                   CICSAUTH
00159 ******************************************************************CICSAUTH
00160 *  CHECK TRANSACTION AUTHORITY                                   *CICSAUTH
00161 ******************************************************************CICSAUTH
00162                                                                   CICSAUTH
00163  3000-QUERY-USER-CLASS.                                           CICSAUTH
00164                                                                   CICSAUTH
00165      EVALUATE TRUE                                                CICSAUTH
00166         WHEN DP008-RSRC-TYPE-MENU                                 CICSAUTH
00167             SET PV-CICS-CODE-M TO TRUE                            CICSAUTH
00168             MOVE DP008-MENU-NAME TO PV-RESID-VALUE                CICSAUTH
00169             MOVE +10 TO PV-RESID-LENGTH                           CICSAUTH
00170         WHEN DP008-RSRC-TYPE-FUNC                                 CICSAUTH
00171             SET PV-CICS-CODE-F TO TRUE                            CICSAUTH
00172             MOVE DP008-FUNCTION-NAME TO PV-RESID-VALUE            CICSAUTH
00173             MOVE +10 TO PV-RESID-LENGTH                           CICSAUTH
00174         WHEN DP008-RSRC-TYPE-STR                                  CICSAUTH
00175             SET PV-CICS-CODE-S TO TRUE                            CICSAUTH
00176             MOVE DP008-STORE-NBR TO PV-RESID-VALUE                CICSAUTH
00177             MOVE +5 TO PV-RESID-LENGTH                            CICSAUTH
00178         WHEN DP008-RSRC-TYPE-DEPT                                 CICSAUTH
00179             SET PV-CICS-CODE-M TO TRUE                            CICSAUTH
00180             MOVE DP008-DEPARTMENT-NBR TO PV-RESID-VALUE           CICSAUTH
00181             MOVE +5 TO PV-RESID-LENGTH                            CICSAUTH
00182         WHEN OTHER                                                CICSAUTH
00183             SET PV-ERROR-IN-INPUT TO TRUE                         CICSAUTH
00184      END-EVALUATE.                                                CICSAUTH
00185                                                                   CICSAUTH
00186      IF PV-ERROR-IN-INPUT                                         CICSAUTH
00187         SET DP008-ACCESS-DENIED   TO TRUE                         CICSAUTH
00188         SET DP008-NOT-AUTHORIZED  TO TRUE                         CICSAUTH
00189      ELSE                                                         CICSAUTH
00190          EXEC CICS                                                CICSAUTH
00191              QUERY SECURITY                                       CICSAUTH
00192                  NOLOG                                            CICSAUTH
00193                  RESCLASS (PV-RESCLASS-USER)                      CICSAUTH
00194                  RESID    (PV-RESID)                              CICSAUTH
00195                  RESIDLENGTH (PV-RESID-LENGTH)                    CICSAUTH
00196                  READ     (PV-AUTH-READ)                          CICSAUTH
00197                  ALTER    (PV-AUTH-ALTER)                         CICSAUTH
00198                  UPDATE   (PV-AUTH-UPDATE)                        CICSAUTH
00199                  RESP     (PV-RESP)                               CICSAUTH
00200          END-EXEC                                                 CICSAUTH
00201      SET PV-QUERY-COMPLETE  TO TRUE                               CICSAUTH
00202      END-IF.                                                      CICSAUTH
00203                                                                   CICSAUTH
00204 *3000-EXIT.                                                       CICSAUTH
00205                                                                   CICSAUTH
00206 ******************************************************************CICSAUTH
00207 *  EVALUATE QUERY RESULTS                                        *CICSAUTH
00208 ******************************************************************CICSAUTH
00209                                                                   CICSAUTH
00210  4000-EVALUATE-QUERY-RESULTS.                                     CICSAUTH
00211                                                                   CICSAUTH
00212      SET DP008-ACCESS-DENIED TO TRUE.                             CICSAUTH
00213 *                                                                 CICSAUTH
00214      IF PV-RESP EQUAL DFHRESP(NORMAL)                             CICSAUTH
00215          SET DP008-CALL-SUCCESSFUL TO TRUE                        CICSAUTH
00216          EVALUATE TRUE                                            CICSAUTH
00217            WHEN DP008-ACCESS-TYPE-READ                            CICSAUTH
00218                IF PV-AUTH-READ = DFHVALUE(READABLE)               CICSAUTH
00219                    SET DP008-ACCESS-ALLOWED TO TRUE               CICSAUTH
00220                END-IF                                             CICSAUTH
00221            WHEN DP008-ACCESS-TYPE-USE                             CICSAUTH
00222                IF PV-AUTH-READ = DFHVALUE(READABLE)               CICSAUTH
00223                    SET DP008-ACCESS-ALLOWED TO TRUE               CICSAUTH
00224                END-IF                                             CICSAUTH
00225            WHEN DP008-ACCESS-TYPE-UPDATE                          CICSAUTH
00226                IF PV-AUTH-UPDATE = DFHVALUE(UPDATABLE)            CICSAUTH
00227                    SET DP008-ACCESS-ALLOWED TO TRUE               CICSAUTH
00228                END-IF                                             CICSAUTH
00229            WHEN DP008-ACCESS-TYPE-ADD                             CICSAUTH
00230                IF PV-AUTH-UPDATE = DFHVALUE(UPDATABLE)            CICSAUTH
00231                    SET DP008-ACCESS-ALLOWED TO TRUE               CICSAUTH
00232                END-IF                                             CICSAUTH
00233            WHEN DP008-ACCESS-TYPE-DELETE                          CICSAUTH
00234                IF PV-AUTH-ALTER = DFHVALUE(ALTERABLE)             CICSAUTH
00235                    SET DP008-ACCESS-ALLOWED TO TRUE               CICSAUTH
00236                END-IF                                             CICSAUTH
00237            WHEN OTHER                                             CICSAUTH
00238               SET DP008-RESOURCE-UNDEFINED TO TRUE                CICSAUTH
00239               SET DP008-ACCESS-DENIED TO TRUE                     CICSAUTH
00240          END-EVALUATE                                             CICSAUTH
00241      ELSE                                                         CICSAUTH
00242         IF PV-RESP EQUAL DFHRESP(NOTFND)                          CICSAUTH
00243             SET DP008-RESOURCE-UNDEFINED TO TRUE                  CICSAUTH
00244             SET DP008-ACCESS-DENIED TO TRUE                       CICSAUTH
00245         ELSE                                                      CICSAUTH
00246             SET DP008-NOT-AUTHORIZED TO TRUE                      CICSAUTH
00247             SET DP008-ACCESS-DENIED TO TRUE                       CICSAUTH
00248         END-IF                                                    CICSAUTH
00249      END-IF.                                                      CICSAUTH
00250                                                                   CICSAUTH
00251 *4000-EXIT.                                                       CICSAUTH
00252                                                                   CICSAUTH
00253                                                                   CICSAUTH
00254 ******************************************************************CICSAUTH
00255 *  SELF EXPLANATORY                                              *CICSAUTH
00256 ******************************************************************CICSAUTH
00257                                                                   CICSAUTH
00258  9000-RETURN-TO-CALLING-TRANS.                                    CICSAUTH
00259                                                                   CICSAUTH
00260      MOVE DP008-SECURITY-COMMAREA TO DFHCOMMAREA.                 CICSAUTH
00261                                                                   CICSAUTH
00262      EXEC CICS                                                    CICSAUTH
00263          RETURN                                                   CICSAUTH
00264      END-EXEC.                                                    CICSAUTH
00265                                                                   CICSAUTH
00266 *9000-EXIT.                                                       CICSAUTH
00267                                                                   CICSAUTH

