      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    DPKCS037.                                                 
       AUTHOR.        CAL BUSH.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03-10-93.                                                 
       DATE-COMPILED.                                                           
      *----------------------------------------------------------------*        
      *    Z037 - ADD SHORT MESSAGE                                    *        
      *                                                                *        
      *    THIS PROGRAM ALLOWS THE USER TO ADD A NEW SYSTEM MESSAGE.   *        
      *    THE MESSAGE NUMBER IS A PROTECTED FIELD AND WILL BE GENER-  *        
      *    ATED BY THIS PROGRAM.  SYSTEM ID MUST BE ENTERED AND MUST BE*        
      *    ON TABLE TAPLSYS.  MESSAGE TEXT MUST BE ENTERED AND MUST    *        
      *    NOT HAVE A SPACE AS THE FIRST BYTE.                         *        
      *----------------------------------------------------------------*        
      *    05/15/2000  JEFF ROUBIK                                     *        
      *                ALLOW MESSAGE NUMBER FIELD TO BE UPDATED IF     *        
      *                RACF PERMITS.                                   *        
      *----------------------------------------------------------------*        
262023*  CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023       *        
262023*  CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE   *        
262023*  MAKING THE CALL DYNAMIC VS STATIC                             *        
      *----------------------------------------------------------------*        
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-OPERATING-COMPANY           PIC  X(02)  VALUE SPACE.          
           05  DK-APLSYS-CDE                  PIC  X(02)  VALUE SPACE.          
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'DP037A  '.          
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'DPKM037 '.          
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'DPKCS037'.          
           05  PC-UPDATE-MSG-NBR        PIC  X(08)  VALUE  'DPMSGNBR'.          
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-MAX-LOCK-RETRIES      PIC S9(04)  VALUE  +3                   
                                                    COMP.                       
           05  PC-MAX-MSG-VAR           PIC S9(04)  VALUE +1                    
                                                    COMP.                       
           05  PC-UPD-VSAM              PIC  X(01)  VALUE 'Y'.                  
           05  PC-MSG-VAR-SYMBOLIC      PIC  X(04)  VALUE '&VAR'.               
           05  PC-TSYMSG-NUMBERS.                                               
               10  PC-TSYMSG-00007      PIC  9(05)  VALUE  00007.               
               10  PC-TSYMSG-00008      PIC  9(05)  VALUE  00008.               
               10  PC-TSYMSG-00068      PIC  9(05)  VALUE  00068.               
               10  PC-TSYMSG-00095      PIC  9(05)  VALUE  00095.               
               10  PC-TSYMSG-00223      PIC  9(05)  VALUE  00223.               
               10  PC-TSYMSG-00224      PIC  9(05)  VALUE  00224.               
               10  PC-TSYMSG-00388      PIC  9(05)  VALUE  00388.               
               10  PC-TSYMSG-02482      PIC  9(05)  VALUE  02482.               
               10  PC-TSYMSG-02488      PIC  9(05)  VALUE  02488.               
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.                 
               88  PS-ERROR                         VALUE  'Y'.                 
               88  PS-NO-ERROR                      VALUE  'N'.                 
           05  PS-NUMBER-LOCK-SW        PIC  X(01)  VALUE  'N'.                 
               88  PS-NUMBER-LOCKED                 VALUE  'Y'.                 
               88  PS-NUMBER-NOT-LOCKED             VALUE  'N'.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CICS-RESPONSE         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-LOCK-RETRY-COUNTER    PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-MSG-VAR-CNT           PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    SECURITY SUBROUTINE PARAMETERS                              *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS008.                                                        
      *----------------------------------------------------------------*        
      *    CALLING PARAMETERS FOR LOCKING MODULE DPKCS044              *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS044.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT   *        
      *    SUB-ROUTINE (DPKUT100).                                     *        
      *----------------------------------------------------------------*        
           COPY DPWS010I.                                                       
                                                                                
      *----------------------------------------------------------------*        
      *    MAP LAYOUT                                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPKM037.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS015.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    FUNCTION KEYS COPYBOOK                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS016.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
                                                                                
      *****************************************************************         
      *    STANDARD COMMAREA.                                                   
      *****************************************************************         
                                                                                
           COPY DPWS020.                                                        
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
                                                                                
      *****************************************************************         
      *    INTER-APPLICATION COMMAREA.                                          
      *****************************************************************         
                                                                                
               10  INTER-APPL-COMM-AREA PIC X(200).                             
           COPY DPWS054.                                                        
           COPY DPWS058.                                                        
      *****************************************************************         
      *    SPECIFIC COMMMAREA FOR DPKCS037.                                     
      *****************************************************************         
               10  ASC-SPECIFIC-COMMAREA.                                       
                   15  ASC-ADD-COMPLETE-SW         PIC  X(01).                  
                       88  ASC-ADD-COMPLETE                   VALUE 'Y'.        
                       88  ASC-ADD-PENDING                    VALUE 'N'.        
                   15  ASC-DEFAULT-MSG.                                         
                       20  ASC-DEFAULT-MSG-N       PIC  9(05).                  
                   15  ASC-MSG-NUMBER.                                          
                       20  ASC-MSG-NUMBER-N        PIC  9(05).                  
                   15  ASC-SYSTEM-ID               PIC  X(02).                  
                   15  ASC-MSG-TEXT                PIC  X(70).                  
                   15  ASC-UPDATE-MSG-NBR-SW       PIC  X(01).                  
                       88  ASC-UPDATE-MSG-NBR                 VALUE 'Y'.        
               10  FILLER                          PIC  X(2781).                
           EJECT                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSING COPYBOOK.                                  *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS013.                                                        
                                                                                
      *    DB2 AREA FOR TSYMSG  (SYSTEM MESSAGE - SHORT MESSAGE)                
                                                                                
           EXEC SQL                                                             
                INCLUDE TSYMSG                                                  
           END-EXEC.                                                            
                                                                                
      *    DB2 AREA FOR TAPLSYS (APPLICATION SYSTEM MASTER)                     
                                                                                
           EXEC SQL                                                             
                INCLUDE TAPLSYS                                                 
           END-EXEC.                                                            
                                                                                
      *    DB2 AREA FOR COMMUNICATIONS                                          
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC  X(01).                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
      **                                                             **         
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
      *****************************************************************         
       0000-MAIN-MODULE.                                                        
           INITIALIZE DP030-CICS-API-FIELDS.                                    
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
           MOVE LENGTH OF DP037AI        TO DP030-MAP-LENGTH (1).               
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
           PERFORM 1000-CONTROL-PROCESSING                                      
                                                                                
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
           PERFORM 0001-CALL-CICS-ARCH-API.                                     
                                                                                
       0001-CALL-CICS-ARCH-API.                                                 
           CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          DP037AI.                              
                                                                                
           IF  DP030-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-NO-ROLLBACK                                            
                   DP013-XCTL-DISPLAY-RESTART                                   
                   DP013-CICS-ABEND      TO TRUE                                
               MOVE 'BEFORE 0000-MAIN-MODULE'                                   
                                         TO DP013-PARAGRAPH                     
               MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
      -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                           
                                         TO DP013-MESSAGE-TEXT (3)              
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1000-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   SET ASC-ADD-PENDING   TO TRUE                                
                   PERFORM 1100-CHECK-SECURITY                                  
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN ASC-ADD-COMPLETE                                            
                   PERFORM 6000-UNLOCK-PREV-MESSAGE                             
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   SET ASC-ADD-PENDING   TO TRUE                                
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   PERFORM 2000-PROCESS-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                                                                                
               WHEN OTHER                                                       
                   SET DP013-LOGIC-ABEND                                        
                       DP013-NO-ROLLBACK TO TRUE                                
                   MOVE '1000-CONTROL-PROCESSING'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
                                         TO DP013-MESSAGE-TEXT(1)               
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                         TO DP013-MESSAGE-TEXT(2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
      *----------------------------------------------------------------*        
                                                                                
       1100-CHECK-SECURITY.                                                     
                                                                                
           SET DP008-RSRC-TYPE-FUNC       TO TRUE.                              
           SET DP008-ACCESS-TYPE-USE      TO TRUE.                              
           MOVE PC-UPDATE-MSG-NBR         TO DP008-FUNCTION-NAME.               
           PERFORM 1110-LINK-TO-SECURITY.                                       
           IF DP008-ACCESS-ALLOWED                                              
               SET ASC-UPDATE-MSG-NBR     TO TRUE                               
           END-IF.                                                              
                                                                                
                                                                                
       1110-LINK-TO-SECURITY.                                                   
                                                                                
           EXEC CICS LINK                                                       
                     PROGRAM  (DP008-SECURITY-SUBROUTINE)                       
                     COMMAREA (DP008-SECURITY-COMMAREA)                         
                     LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)                  
           END-EXEC                                                             
           IF  DP008-CALL-SUCCESSFUL                                            
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               SET DP013-SECURITY-ABEND      TO TRUE                            
               MOVE '1110-LINK-TO-SECURITY'  TO DP013-PARAGRAPH                 
               MOVE DP008-SECURITY-COMMAREA  TO DP013-SECURITY-INFO             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
      * EDITS.                                                         *        
      *----------------------------------------------------------------*        
       2000-PROCESS-PANEL.                                                      
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-SRC-AID = DP016-CLEAR                                 
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                                                                                
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
                   INITIALIZE ASC-SPECIFIC-COMMAREA                             
                   PERFORM 4000-BUILD-INITIAL-PANEL                             
                   PERFORM 3200-RESET-ATTRIBUTES                                
                                                                                
               WHEN OTHER                                                       
                   PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
                   PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
                   IF  PS-NO-ERROR                                              
                       PERFORM 2100-CHECK-FUNCTION-KEY                          
                       SET DP030-CONTINUE-GO TO TRUE                            
                   ELSE                                                         
                       SET DP030-OVERRIDE-GO TO TRUE                            
                   END-IF                                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      ** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
      ** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
      ** FROM THE CICS ARCHITECTURE API.                              **        
      ******************************************************************        
       2100-CHECK-FUNCTION-KEY.                                                 
           EVALUATE TRUE                                                        
               WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
                   PERFORM 5000-ADD-SYSTEM-MESSAGE                              
                   SET ASC-ADD-COMPLETE  TO TRUE                                
      *            -- OBJECT WAS ADDED --                                       
                   MOVE PC-TSYMSG-00223  TO DP020-MSG-NUMBER                    
                   SET DP020-MSG-INFORMATIONAL                                  
                                         TO TRUE                                
                   MOVE ASC-MSG-NUMBER   TO AMSGNBRO                            
                   PERFORM 2110-PREPARE-INTER-APPL-COMM                         
                   SET DP030-ERASE-UNPROT TO TRUE                               
               WHEN DP020-SRC-AID = DP016-ENTER                                 
                   CONTINUE                                                     
                                                                                
               WHEN OTHER                                                       
                   SET DP013-NO-ROLLBACK                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                       DP013-CICS-ABEND  TO TRUE                                
                   MOVE '2100-CHECK-FUNCTION-KEY'                               
                                         TO DP013-PARAGRAPH                     
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
                                         TO DP013-MESSAGE-TEXT (1)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
      ** INTER-APPLICATION COMMAREA.                                  **        
      ******************************************************************        
       2110-PREPARE-INTER-APPL-COMM.                                            
           MOVE ASC-MSG-NUMBER-N         TO DP058-MESSAGE-NUMBER.               
      *----------------------------------------------------------------*        
      * MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
      * THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
      * APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
      *----------------------------------------------------------------*        
                                                                                
       2200-MOVE-SCREEN-TO-COMMAREA.                                            
           IF AMSGNBRL > ZERO                                                   
               MOVE AMSGNBRI             TO ASC-MSG-NUMBER                      
           ELSE                                                                 
               IF AMSGNBRF = DP015-ERASE-EOF                                    
                   MOVE ASC-DEFAULT-MSG  TO ASC-MSG-NUMBER                      
                                            AMSGNBRO                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASYSIDL > ZERO                                                   
               MOVE ASYSIDI              TO ASC-SYSTEM-ID                       
           ELSE                                                                 
               IF  ASYSIDF = DP015-ERASE-EOF                                    
                   MOVE SPACE            TO ASC-SYSTEM-ID                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  AMSGTXTL > ZERO                                                  
               MOVE AMSGTXTI             TO ASC-MSG-TEXT                        
           ELSE                                                                 
               IF  AMSGTXTF = DP015-ERASE-EOF                                   
                   MOVE SPACE            TO ASC-MSG-TEXT                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * PERFORM EDITS ON EACH FIELD ENTERED BY THE USER.  IF AN ERROR  *        
      * IS FOUND THE CURSOR IS POSITIONED AT THAT FIELD AND AN         *        
      * APPROPRIATE ERROR MESSAGE IS DISPLAYED.                        *        
      *----------------------------------------------------------------*        
                                                                                
       3000-EDIT-DATA-IN-COMMAREA.                                              
           PERFORM 3200-RESET-ATTRIBUTES.                                       
                                                                                
           IF  ASC-MSG-TEXT (1:1) = SPACE                                       
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO TRUE                                
      *        ---- REQUIRED FIELD ----                                         
               MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                    
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO AMSGTXTA                            
               MOVE DP015-RED            TO AMSGTXTC                            
               MOVE DP015-REVERSE        TO AMSGTXTH                            
               MOVE -1                   TO AMSGTXTL                            
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           ELSE                                                                 
               INSPECT ASC-MSG-TEXT                                             
                   TALLYING PV-MSG-VAR-CNT                                      
                   FOR ALL PC-MSG-VAR-SYMBOLIC                                  
               IF  PV-MSG-VAR-CNT > PC-MAX-MSG-VAR                              
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
      *            ---- ONLY 1 SYMBOLIC IS PERMITED ----                        
                   MOVE PC-TSYMSG-00388  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AMSGTXTA                            
                   MOVE DP015-RED        TO AMSGTXTC                            
                   MOVE DP015-REVERSE    TO AMSGTXTH                            
                   MOVE -1               TO AMSGTXTL                            
                   SET DP030-SET-CURSOR-APPL-1                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  ASC-SYSTEM-ID = SPACE                                            
               SET PS-ERROR                                                     
                   DP020-MSG-FATAL       TO TRUE                                
      *        ---- REQUIRED FIELD -----                                        
               MOVE PC-TSYMSG-00007      TO DP020-MSG-NUMBER                    
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                         TO ASYSIDA                             
               MOVE DP015-RED            TO ASYSIDC                             
               MOVE DP015-REVERSE        TO ASYSIDH                             
               MOVE -1                   TO ASYSIDL                             
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           ELSE                                                                 
               MOVE DP020-OPERATING-COMPANY                                     
                                         TO DK-OPERATING-COMPANY                
               MOVE ASC-SYSTEM-ID        TO DK-APLSYS-CDE                       
      *        PERFORM 3300-SELECT-TAPLSYS                                      
               MOVE ZERO TO SQLCODE                                             
               IF SQLCODE = +100                                                
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
      *            ---- SYSTEM DOESN'T EXIST ----                               
                   MOVE PC-TSYMSG-00095  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO ASYSIDA                             
                   MOVE DP015-RED        TO ASYSIDC                             
                   MOVE DP015-REVERSE    TO ASYSIDH                             
                   MOVE -1               TO ASYSIDL                             
                   SET DP030-SET-CURSOR-APPL-1                                  
                                         TO TRUE                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF ASC-MSG-NUMBER > SPACE                                            
               MOVE ASC-MSG-NUMBER       TO DP010I-UNEDITED-FIELD               
               MOVE LENGTH OF ASC-MSG-NUMBER TO DP010I-MAXIMUM-DIGITS           
               MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
               SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
               IF  DP010I-ERROR-DETECTED                                        
                   SET PS-ERROR                                                 
                       DP020-MSG-FATAL   TO TRUE                                
      *            ---- NUMERIC FIELD ----                                      
                   MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                         TO AMSGNBRA                            
                   MOVE DP015-RED        TO AMSGNBRC                            
                   MOVE DP015-REVERSE    TO AMSGNBRH                            
                   MOVE -1               TO AMSGNBRL                            
                   SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
               ELSE                                                             
                   MOVE DP010I-NUMERIC-FIELD TO ASC-MSG-NUMBER-N                
                                                SYMSG-SYMSG-NBR                 
                   IF ASC-MSG-NUMBER-N  = ASC-DEFAULT-MSG-N                     
                   OR ASC-MSG-NUMBER-N >= 99900                                 
                       PERFORM 5200-READ-MESSAGE                                
                       IF SQLCODE = ZERO                                        
                           SET PS-ERROR                                         
                               DP020-MSG-FATAL   TO TRUE                        
      *                    ---- MESSAGE NUMBER ALREADY EXISTS ----              
                           MOVE PC-TSYMSG-02482  TO DP020-MSG-NUMBER            
                           MOVE DP015-UNP-ALP-BRT-OFF                           
                                                 TO AMSGNBRA                    
                           MOVE DP015-RED        TO AMSGNBRC                    
                           MOVE DP015-REVERSE    TO AMSGNBRH                    
                           MOVE -1               TO AMSGNBRL                    
                           SET DP030-SET-CURSOR-APPL-1 TO TRUE                  
                       END-IF                                                   
                   ELSE                                                         
                       SET PS-ERROR                                             
                           DP020-MSG-FATAL   TO TRUE                            
      *                ---- OVERRIDE MESSAGE NUMBER < 99900 ----                
                       MOVE PC-TSYMSG-02488  TO DP020-MSG-NUMBER                
                       MOVE DP015-UNP-ALP-BRT-OFF                               
                                             TO AMSGNBRA                        
                       MOVE DP015-RED        TO AMSGNBRC                        
                       MOVE DP015-REVERSE    TO AMSGNBRH                        
                       MOVE -1               TO AMSGNBRL                        
                       SET DP030-SET-CURSOR-APPL-1 TO TRUE                      
                   END-IF                                                       
           END-IF.                                                              
                                                                                
           IF  PS-NO-ERROR                                                      
               MOVE -1                   TO ASYSIDL                             
               SET DP030-SET-CURSOR-APPL-1                                      
                                         TO TRUE                                
           END-IF.                                                              
                                                                                
                                                                                
       3200-RESET-ATTRIBUTES.                                                   
           MOVE DP015-UNP-ALP-NOR-OFF    TO ASYSIDA                             
                                            AMSGTXTA.                           
           MOVE DP015-GREEN              TO ASYSIDC                             
                                            AMSGTXTC.                           
           MOVE DP015-UNDERLINE          TO ASYSIDH                             
                                            AMSGTXTH.                           
           IF ASC-UPDATE-MSG-NBR                                                
               MOVE DP015-GREEN          TO AMSGNBRC                            
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                         TO AMSGNBRA                            
               MOVE DP015-UNDERLINE      TO AMSGNBRH                            
      *        MOVE -1                   TO AMSGNBRL                            
           ELSE                                                                 
               MOVE DP015-BLUE           TO AMSGNBRC                            
               MOVE DP015-ASK-NOR-OFF    TO AMSGNBRA                            
               MOVE DP015-HL-OFF         TO AMSGNBRH                            
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * GET THE NEXT AVAILABLE MESSAGE NUMBER, MOVE IT TO THE SCREEN   *        
      * AND POSITION THE CURSOR FOR THE INITIAL PANEL.                 *        
      *----------------------------------------------------------------*        
                                                                                
       4000-BUILD-INITIAL-PANEL.                                                
                                                                                
           SET DP030-SET-CURSOR-APPL-1   TO TRUE.                               
           MOVE -1                       TO ASYSIDL.                            
           SET PS-NUMBER-NOT-LOCKED      TO TRUE.                               
           PERFORM 4100-GET-NEXT-NUMBER.                                        
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-LOCK-RETRY-COUNTER                                    
               FROM 1 BY 1                                                      
               UNTIL PV-LOCK-RETRY-COUNTER > PC-MAX-LOCK-RETRIES                
                         OR PS-NUMBER-LOCKED                                    
                                                                                
               PERFORM 4200-LOCK-NUMBER                                         
           END-PERFORM.                                                         
                                                                                
           IF PS-NUMBER-LOCKED                                                  
               MOVE SYMSG-SYMSG-NBR      TO ASC-MSG-NUMBER-N                    
                                            ASC-DEFAULT-MSG-N                   
               MOVE ASC-MSG-NUMBER       TO AMSGNBRO                            
           ELSE                                                                 
               MOVE ZERO                 TO ASC-MSG-NUMBER-N                    
                                            ASC-DEFAULT-MSG-N                   
                                            AMSGNBRO                            
               MOVE PC-TSYMSG-00068      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-FATAL       TO TRUE                                
           END-IF.                                                              
                                                                                
                                                                                
       4100-GET-NEXT-NUMBER.                                                    
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
                                                                                
               EXEC SQL                                                         
                   SELECT MAX(SYMSG_NBR) + 1                                    
                   INTO   :SYMSG-SYMSG-NBR                                      
                   FROM   TSYMSG                                                
                   WHERE  SYMSG_NBR < 99900.                                    
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '4100-GET-NEXT-NUMBER'                              
                                         TO DP013-PARAGRAPH                     
                       MOVE 'SELECT NEXT AVAILABLE SYSTEM MSG NUMBER'           
                                         TO DP013-MESSAGE-TEXT (1)              
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSYMSG '    TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART                           
                                         TO TRUE                                
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '4100-GET-NEXT-NUMBER'                                      
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
                                         TO DP013-MESSAGE-TEXT (1)              
               MOVE 'NEXT AVAILABLE MESSAGE NUMBER'                             
                                         TO DP013-MESSAGE-TEXT (2)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSYMSG '            TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
       4200-LOCK-NUMBER.                                                        
                                                                                
           MOVE SYMSG-SYMSG-NBR          TO DP044-OBJECT-KEY.                   
           SET DP044-DP-SYSTEM-MESSAGE   TO TRUE.                               
           SET DP044-ACTION-LOCK-OBJECT  TO TRUE.                               
                                                                                
           PERFORM DP044-0000-LINK-TO-OBJ-LOCK.                                 
                                                                                
           IF  DP044-RC-CALL-SUCCESSFUL                                         
               SET PS-NUMBER-LOCKED      TO TRUE                                
           ELSE                                                                 
               IF  DP044-RC-OBJECT-ALREADY-HELD                                 
                   ADD 1 TO SYMSG-SYMSG-NBR                                     
               ELSE                                                             
                   MOVE '4200-LOCK-NUMBER'   TO DP013-PARAGRAPH                 
                   MOVE 'UNSUCCESSFUL ATTEMPT TO LOCK MESSAGE NBR:'             
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE SYMSG-SYMSG-NBR  TO DP013-MESSAGE-TEXT (2)              
                   SET DP013-LOGIC-ABEND                                        
                       DP013-XCTL-DISPLAY-RESTART                               
                                             TO TRUE                            
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
      *----------------------------------------------------------------*        
                                                                                
       4400-MOVE-COMMAREA-TO-SCREEN.                                            
           MOVE ASC-MSG-NUMBER           TO AMSGNBRO.                           
           MOVE ASC-SYSTEM-ID            TO ASYSIDO.                            
           MOVE ASC-MSG-TEXT             TO AMSGTXTO.                           
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * CREATE THE SHORT MESSAGE OBJECT ON THE TSYMSG TABLE.           *        
      *----------------------------------------------------------------*        
       5000-ADD-SYSTEM-MESSAGE.                                                 
           MOVE ASC-MSG-NUMBER-N         TO SYMSG-SYMSG-NBR.                    
           MOVE ASC-SYSTEM-ID            TO SYMSG-APLSYS-CDE.                   
           MOVE 'N'                      TO SYMSG-REV-IND.                      
           MOVE ASC-MSG-TEXT             TO SYMSG-SHT-TXT.                      
           MOVE PC-UPD-VSAM              TO SYMSG-UPD-VSAM-IND.                 
           PERFORM 5100-INSERT-MESSAGE.                                         
      *----------------------------------------------------------------*        
      * INSERT A ROW INTO THE TSYMSG TABLE.                            *        
      *----------------------------------------------------------------*        
       5100-INSERT-MESSAGE.                                                     
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNTER                                         
               FROM 1 BY 1                                                      
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
                         OR (SQLCODE = ZERO))                                   
               EXEC SQL                                                         
                   INSERT INTO TSYMSG                                           
                      (SYMSG_NBR,                                               
                       APLSYS_CDE,                                              
                       REV_IND,                                                 
                       SHT_TXT,                                                 
                       UPD_VSAM_IND)                                            
                   VALUES                                                       
                      (:SYMSG-SYMSG-NBR,                                        
                       :SYMSG-APLSYS-CDE,                                       
                       :SYMSG-REV-IND,                                          
                       :SYMSG-SHT-TXT,                                          
                       :SYMSG-UPD-VSAM-IND)                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = ZERO                                          
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -913                                          
                       CONTINUE                                                 
                   WHEN SQLWARN0 NOT = SPACES                                   
                   WHEN SQLCODE < ZERO                                          
                   WHEN SQLCODE > ZERO                                          
                       MOVE '5100-INSERT-MESSAGE'                               
                                         TO DP013-PARAGRAPH                     
                       MOVE 'INSERT A ROW INTO THE SYSTEM MESSAGE TABLE'        
                                         TO DP013-MESSAGE-TEXT(1)               
                       MOVE SQLCA        TO DP013-SQLCA                         
                       MOVE 'TSYMSG'     TO DP013-DB2-TABLE-NAME (1)            
                       SET DP013-DB2-ABEND                                      
                           DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       PERFORM DP013-0000-PROCESS-ABEND                         
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
               MOVE '5100-INSERT-MESSAGE'                                       
                                         TO DP013-PARAGRAPH                     
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO INSERT A NEW        
      -             ' SYSTEM MESSAGE'    TO DP013-MESSAGE-TEXT (1)              
               MOVE SQLCA                TO DP013-SQLCA                         
               MOVE 'TSYMSG '            TO DP013-DB2-TABLE-NAME (1)            
               SET DP013-DB2-ABEND                                              
                   DP013-XCTL-DISPLAY-RESTART                                   
                                         TO TRUE                                
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      * READ MESSAGE NUMBER FROM THE TSYMSG TABLE.                     *        
      *----------------------------------------------------------------*        
       5200-READ-MESSAGE.                                                       
           EXEC SQL                                                             
               SELECT SYMSG_NBR                                                 
                 INTO :SYMSG-SYMSG-NBR                                          
                 FROM TSYMSG                                                    
                WHERE SYMSG_NBR = :SYMSG-SYMSG-NBR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '5200-READ-MESSAGE'                                     
                                     TO DP013-PARAGRAPH                         
                   MOVE 'SELECT A ROW FROM THE SYSTEM MESSAGE TABLE'            
                                     TO DP013-MESSAGE-TEXT(1)                   
                   MOVE SQLCA        TO DP013-SQLCA                             
                   MOVE 'TSYMSG'     TO DP013-DB2-TABLE-NAME (1)                
                   SET DP013-DB2-ABEND                                          
                       DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
                                                                                
       6000-UNLOCK-PREV-MESSAGE.                                                
                                                                                
           MOVE ASC-MSG-NUMBER            TO DP044-OBJECT-KEY.                  
           SET DP044-DP-SYSTEM-MESSAGE    TO TRUE.                              
           SET DP044-ACTION-UNLOCK-OBJECT TO TRUE.                              
                                                                                
           PERFORM DP044-0000-LINK-TO-OBJ-LOCK.                                 
                                                                                
           IF  DP044-RC-CALL-SUCCESSFUL                                         
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-LINK-RETURN                                            
                   DP013-LOGIC-ABEND                                            
                   DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '6000-UNLOCK-PREV-MESSAGE'                                  
                                     TO DP013-PARAGRAPH                         
               MOVE 'UNSUCCESSFUL ATTEMPT TO UNLOCK MESSAGE NBR:'               
                                     TO DP013-MESSAGE-TEXT (1)                  
               MOVE ASC-MSG-NUMBER   TO DP013-MESSAGE-TEXT (2)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND PROCESSOR MODULE                                      *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD013.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    LINK TO CICS OBJECT LOCKING MODULE                          *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPPD044.                                                        
