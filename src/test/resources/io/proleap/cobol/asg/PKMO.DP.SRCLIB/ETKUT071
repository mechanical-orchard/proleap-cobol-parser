       IDENTIFICATION DIVISION.                                         ETKUT004
                                                                        ETKUT004
       PROGRAM-ID.              ETKUT071.                               ETKUT004
       AUTHOR.                  LINDA VOGT - COMPUWARE.                 ETKUT004
       DATE-WRITTEN.            10/12/98.                               ETKUT004
       DATE-COMPILED.                                                   ETKUT004
                                                                        ETKUT004
      ******************************************************************ETKUT004
      * THIS PROGRAM EXTRACTS ENTITY ID AND TRADING PARTNER NAME       *        
      * FROM A FILE CREATED FROM THE MAINFRAME TRADING PARTNER         *        
      * RELATIONAL DATABASE TABLES.  THE FILE CREATED WILL BE LOADED   *        
      * TO THE UNIX AND USED FOR CROSS-REFERENCE BETWEEN ENTITY ID     *        
      * AND NAME.                                                      *        
      ******************************************************************ETKUT004
                                                                        ETKUT004
       ENVIRONMENT DIVISION.                                            ETKUT004
                                                                        ETKUT004
       CONFIGURATION SECTION.                                           ETKUT004
                                                                        ETKUT004
       INPUT-OUTPUT SECTION.                                            ETKUT004
                                                                        ETKUT004
       FILE-CONTROL.                                                    ETKUT004
                                                                        ETKUT004
           SELECT TRADING-PARTNER-TPLOAD-FILE                           ETKUT004
              ASSIGN TO INP01.                                          ETKUT004
                                                                        ETKUT004
           SELECT TRADING-PARTNER-XREF                                  ETKUT004
              ASSIGN TO OUT01.                                          ETKUT004
                                                                                
       DATA DIVISION.                                                   ETKUT004
                                                                        ETKUT004
       FILE SECTION.                                                    ETKUT004
                                                                        ETKUT004
       FD  TRADING-PARTNER-TPLOAD-FILE                                  ETKUT004
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 406 CHARACTERS                               ETKUT004
           DATA RECORD IS TRADING-PARTNER-TPLOAD-RECORD.                ETKUT004
                                                                        ETKUT004
       01  TRADING-PARTNER-TPLOAD-RECORD.                               ETKUT004
           05  TP-DATA                      PIC X(406).                         
                                                                                
       FD  TRADING-PARTNER-XREF                                         ETKUT004
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                   ETKUT004
           RECORD CONTAINS 80 CHARACTERS                                ETKUT004
           DATA RECORD IS TRADING-PARTNER-XREF-RECORD.                  ETKUT004
                                                                        ETKUT004
       01  TRADING-PARTNER-XREF-RECORD      PIC X(80).                  ETKUT004
                                                                        ETKUT004
       WORKING-STORAGE SECTION.                                         ETKUT004
                                                                        ETKUT004
       01  WS-SWITCHES-AND-SUBSCRIPTS.                                  ETKUT004
           05  END-OF-FILE-SW                PIC X(01)   VALUE 'N'.     ETKUT004
               88  END-TPLOAD-FILE                       VALUE 'Y'.     ETKUT004
           05  ENT-SUB1          COMP-3      PIC S9(3)   VALUE +0.              
           05  ENT-SUB2          COMP-3      PIC S9(3)   VALUE +0.              
                                                                        ETKUT004
       01  WS-MISC-FIELDS.                                              ETKUT004
           05  WS-REC-HDR-DATA              PIC X(17)    VALUE          ETKUT004
               '"STD|X|TP_XREF|C|'.                                             
           05  WS-WORK-NAME                 PIC X(80)    VALUE SPACES.          
           05  WS-WORK-ENTITY-ID.                                               
               10  WS-WK-ENTITY-CHAR OCCURS 40 TIMES                            
                                            PIC X.                              
           05  WS-EDITED-ENTITY-ID.                                             
               10  WS-EDITED-ENTITY-CHAR OCCURS 40 TIMES                        
                                            PIC X.                              
           05  WS-SEPARATER                 PIC X(3)     VALUE '" "'.           
           05  WS-DOUBLE-QUOTE              PIC X        VALUE '"'.             
           05  WS-SAVE-ENTITY-ID            PIC X(40)    VALUE SPACES.          
                                                                                
       01  WS-TRADING-PART-XREF.                                                
           05  WS-REC-HEADER-DATA           PIC X(17)  VALUE SPACES.            
           05  WS-ENTITY-ID-NAME            PIC X(63)  VALUE SPACES.            
                                                                        ETKUT004
       PROCEDURE DIVISION.                                              ETKUT004
                                                                        ETKUT004
           OPEN INPUT TRADING-PARTNER-TPLOAD-FILE                               
                OUTPUT TRADING-PARTNER-XREF.                                    
                                                                                
           PERFORM 2000-PROCESS-TPLOAD-FILE THRU 2000-EXIT                      
              UNTIL END-TPLOAD-FILE.                                            
                                                                                
           CLOSE TRADING-PARTNER-TPLOAD-FILE                                    
                 TRADING-PARTNER-XREF.                                          
                                                                                
           STOP RUN.                                                    ETKUT004
                                                                        ETKUT004
      ****************************************************************          
      ** EXTRACT NAME AND ENTITY ID FROM INPUT RECORD.  NAME AND                
      ** ENTITY ID SEPARATED BY '~' (TILDA).  ELIMINATE THE LEADING             
      ** ZEROES ON THE ENTITY ID.                                               
      ****************************************************************          
                                                                                
       2000-PROCESS-TPLOAD-FILE.                                        ETKUT004
                                                                        ETKUT004
           PERFORM 8000-READ-TRADING-PTNR-TPLOAD THRU 8000-EXIT.                
           IF END-TPLOAD-FILE                                                   
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-EDITED-ENTITY-ID TO WS-SAVE-ENTITY-ID.                       
                                                                                
           INITIALIZE WS-WORK-NAME                                              
                      WS-WORK-ENTITY-ID                                         
                      WS-EDITED-ENTITY-ID                                       
                      ENT-SUB2                                                  
                      WS-TRADING-PART-XREF.                                     
                                                                                
           MOVE WS-REC-HDR-DATA TO WS-REC-HEADER-DATA.                          
           UNSTRING TP-DATA                                                     
              DELIMITED BY '~' OR ','                                           
              INTO WS-WORK-NAME                                                 
                   WS-WORK-ENTITY-ID                                            
           END-UNSTRING.                                                        
                                                                                
           INSPECT WS-WORK-ENTITY-ID REPLACING LEADING ZEROES BY '*'.           
           PERFORM 2010-EDIT-ENTITY-ID THRU 2010-EXIT                           
              VARYING ENT-SUB1 FROM 1 BY 1                                      
              UNTIL ENT-SUB1 > 40 OR                                            
                    WS-WK-ENTITY-CHAR (ENT-SUB1) = SPACE.                       
                                                                                
           STRING WS-EDITED-ENTITY-ID DELIMITED BY SPACE                        
                  WS-SEPARATER DELIMITED BY SIZE                                
                  WS-WORK-NAME DELIMITED BY '   '                               
                  WS-DOUBLE-QUOTE DELIMITED BY SIZE                             
                  INTO WS-ENTITY-ID-NAME                                        
           END-STRING.                                                          
                                                                                
           IF WS-EDITED-ENTITY-ID = WS-SAVE-ENTITY-ID                           
               GO TO 2000-EXIT.                                                 
                                                                                
           PERFORM 8010-WRITE-XREF THRU 8010-EXIT.                              
                                                                                
       2000-EXIT.                                                       ETKUT004
           EXIT.                                                                
                                                                                
      ******************************************************************        
      **  LEADING ZEROES ON ENTITY ID WERE REPLACED WITH ASTERISKS (*)          
      ******************************************************************        
       2010-EDIT-ENTITY-ID.                                                     
                                                                                
           IF WS-WK-ENTITY-CHAR (ENT-SUB1) = '*'                                
              GO TO 2010-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD +1 TO ENT-SUB2.                                                  
           MOVE WS-WK-ENTITY-CHAR (ENT-SUB1) TO                                 
                WS-EDITED-ENTITY-CHAR (ENT-SUB2).                               
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-READ-TRADING-PTNR-TPLOAD.                                   ETKUT004
                                                                        ETKUT004
           READ TRADING-PARTNER-TPLOAD-FILE                             ETKUT004
              AT END SET END-TPLOAD-FILE TO TRUE.                       ETKUT004
                                                                                
       8000-EXIT.                                                       ETKUT004
           EXIT.                                                                
                                                                                
                                                                                
       8010-WRITE-XREF.                                                         
                                                                                
           WRITE TRADING-PARTNER-XREF-RECORD                                    
              FROM WS-TRADING-PART-XREF.                                        
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
