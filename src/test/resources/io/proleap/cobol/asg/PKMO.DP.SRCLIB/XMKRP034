       IDENTIFICATION DIVISION.                                         00010011
       PROGRAM-ID.    XMKRP034                                          00020011
       AUTHOR.        BOB MCASEY.                                       00030011
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040011
       DATE-WRITTEN.  01/10/2003.                                       00050011
       DATE-COMPILED.                                                   00060011
                                                                        00070011
      ******************************************************************00080011
      *          DOMAIN WAREHOUSE DATA INTEGRITY                        00090011
      *                                                                 00100011
      *     XMKRP034 - WAREHOUSE DATA INTEGRITY REPORT                  00110011
      *                                                                 00120011
      * THIS PROGRAM READS AN EXTRACT OF RMS DATA AND COMPARES TO AN    00130011
      * EXTRACT OF DATA FROM THE XM DOMAIN TABLES.                      00140011
      * ANY DIFFERENCES ARE WRITTEN TO THE XM LOCATION DATA INTEGRITY   00150011
      * ERRORS REPORT.                                                  00160011
      *                                                                 00170011
      * THE INPUT FILES ARE FORMATTED TO THE HORD014-WH DATA            00180011
      *                                                                 00190011
      * THE MESSAGE TEXT FOR THE REPORT ERROR MESSAGES ARE FOUND IN     00200011
      * COPYBOOK XMWS001.                                               00210011
      *                                                                 00220011
      * IF THERE ARE ANY ERRORS TO BE REPORTED, PROGRAM WILL END WITH   00230011
      * RETURN CODE 4095 SO THAT AN E-MAIL CAN BE SENT IN A             00240011
      * SUBSEQUENT JOB STEP.                                            00250011
      *                                                                 00260011
      ******************************************************************00270011
      *---------------------------------------------------------------* 00280011
      *    WR/PITS#     DATE        DESCRIPTION                       * 00290011
      *    --------   ----------   ---------------------------------  * 00300011
      *    WR24442    08/20/2003   INITIAL CREATION.                  * 00310011
      *    WR26600    04/29/2004   PROCESS 10 CHARACTER WH NAME NOW   * 00311011
      *                            ON THE XMT_LOC EXTRACT.            * 00312011
0407PK*    WR31384    04/26/2007   USE NEW COPYBOOK XMRD016 FOR THE   * 00313011
0407PK*    XMT_LOC BMC UNLOAD INPUT FILE THAT IS NOW 463 BYTES.       * 00314011
      *---------------------------------------------------------------* 00320011
                                                                        00332011
                                                                        00340011
       ENVIRONMENT DIVISION.                                            00350011
       CONFIGURATION SECTION.                                           00360011
       SOURCE-COMPUTER.        IBM-3090.                                00370011
       OBJECT-COMPUTER.        IBM-3090.                                00380011
       INPUT-OUTPUT SECTION.                                            00390011
       FILE-CONTROL.                                                    00400011
                                                                        00410011
           SELECT RMS-LOC-EXTRACT-DATA-FILE                             00420011
               ASSIGN TO INP01.                                         00430011
                                                                        00440011
           SELECT XM-LOC-EXTRACT-DATA-FILE                              00450011
               ASSIGN TO INP02.                                         00460011
                                                                        00470011
           SELECT XM-LOC-DATA-INTEGRITY-RPT                             00480011
               ASSIGN TO RPT01.                                         00490011
                                                                        00500011
       DATA DIVISION.                                                   00510011
       FILE SECTION.                                                    00520011
                                                                        00530011
       FD  RMS-LOC-EXTRACT-DATA-FILE                                    00540011
           RECORDING MODE IS F                                          00550011
           LABEL RECORDS ARE STANDARD                                   00560011
           BLOCK CONTAINS 0 RECORDS.                                    00570011
                                                                        00580011
       01  RMS-EXTRACT-REC                  PIC  X(192).                00600011
                                                                        00610011
       FD  XM-LOC-EXTRACT-DATA-FILE                                     00620011
           RECORDING MODE IS F                                          00630011
           LABEL RECORDS ARE STANDARD                                   00640011
           BLOCK CONTAINS 0 RECORDS.                                    00650011
                                                                        00660011
      *01  XM-EXTRACT-REC                   PIC  X(456).                00682011
0407PK 01  XM-EXTRACT-REC                   PIC  X(463).                00683011
                                                                        00690011
       FD  XM-LOC-DATA-INTEGRITY-RPT                                    00700011
           RECORDING  MODE IS F                                         00710011
           LABEL RECORDS ARE STANDARD                                   00720011
           BLOCK CONTAINS 0 RECORDS.                                    00730011
                                                                        00740011
       01  XM-LOC-RPT-LINE                  PIC X(200).                 00750011
                                                                        00760011
                                                                        00770011
       WORKING-STORAGE SECTION.                                         00780011
                                                                        00790011
       01  PC-PROGRAM-CONSTANTS.                                        00800011
           05  PC-PGM-NAME             PIC  X(08)      VALUE 'XMKRP034'.00810011
           05  PC-MAX-DTL-LINES        PIC S9(04) COMP VALUE +70.       00820011
                                                                        00830011
       01  PV-PROGRAM-VARIABLES.                                        00840011
           05  PV-PAGE-CNT             PIC S9(05)        VALUE ZERO.    00850011
           05  PV-LINE-CNT             PIC S9(04) COMP   VALUE +71.     00860011
           05  PV-DISPLAY-CNT          PIC ZZZZ9.                       00870011
           05  PV-ADVANCE              PIC S9(03) COMP-3 VALUE ZERO.    00880011
           05  PV-NBR-ERRORS           PIC S9(04) COMP   VALUE ZERO.    00890011
           05  PV-DATE-YYMMDD.                                          00900011
               10  PV-YYMMDD-YY        PIC  X(02)        VALUE SPACE.   00910011
               10  PV-YYMMDD-MM        PIC  X(02)        VALUE SPACE.   00920011
               10  PV-YYMMDD-DD        PIC  X(02)        VALUE SPACE.   00930011
           05  PV-TIME-HHMMSS.                                          00940011
               10  PV-HHMMSS-HH        PIC  X(02)        VALUE SPACE.   00950011
               10  PV-HHMMSS-MM        PIC  X(02)        VALUE SPACE.   00960011
               10  PV-HHMMSS-SS        PIC  X(02)        VALUE SPACE.   00970011
           05  PV-MSG-NBR              PIC S9(03).                      00980011
           05  PV-XM-VALUE             PIC  X(30).                      00990011
           05  PV-RMS-VALUE            PIC  X(30).                      01000011
           05  PV-XM-EXTRACT-REC-CNT   PIC S9(04)  COMP                 01010011
                                                   VALUE ZERO.          01020011
           05  PV-RMS-EXTR-REC-CNT     PIC S9(04)  COMP                 01030011
                                                   VALUE ZERO.          01040011
           05  PV-REPORT-CNT                 PIC S9(04)  COMP           01050011
                                                         VALUE ZERO.    01060011
           05  ABEND-CODE                    PIC S9(04) VALUE +0.       01070011
           05  PV-RETRY-COUNTER              PIC  9(01)  VALUE ZEROS.   01080011
           05  PV-PARAGRAPH                  PIC  X(50).                01090011
           05  PV-MESSAGE                    PIC  X(50).                01100011
           05  PV-LOC-NBR                    PIC  9(04) VALUE 0.        01110011
           05  PV-REPLT-STR-NBR              PIC  9(04) VALUE 0.        01120011
           05  PV-DIST-NBR                   PIC  9(04) VALUE 0.        01130011
           05  PV-POS-RGST-BNK-QTY           PIC  9(03) VALUE 0.        01140011
           05  PV-FMT-CDE                    PIC  9(04) VALUE 0.        01150011
           05  PV-TOT-SLG-SQ-FTG-QTY         PIC  9(08) VALUE 0.        01160011
           05  PV-TOT-SQ-FTG-QTY             PIC  9(08) VALUE 0.        01170011
           05  PV-STP-ORD-DAYS-QTY           PIC  9(03) VALUE 0.        01180011
           05  PV-STRT-ORD-DAYS-QTY          PIC  9(03) VALUE 0.        01190011
           05  PV-MEDN-AGE-NBR               PIC  9(02)V9(02) VALUE 0.  01200011
           05  PV-EST-HH-INCM-AMT            PIC  9(11) VALUE 0.        01210011
                                                                        01220011
                                                                        01230011
       01  PS-PROGRAM-SWITCHES.                                         01240011
           05  PS-END-OF-RMS-FILE-SW PIC X   VALUE 'N'.                 01250011
               88 PS-END-OF-RMS-FILE         VALUE 'Y'.                 01260011
               88 PS-NOT-END-OF-RMS-FILE     VALUE 'N'.                 01270011
           05  PS-END-OF-XM-FILE-SW PIC X    VALUE 'N'.                 01280011
               88 PS-END-OF-XM-FILE          VALUE 'Y'.                 01290011
               88 PS-NOT-END-OF-XM-FILE      VALUE 'N'.                 01300011
                                                                        01310011
                                                                        01320011
      ******************************************************************01330011
      * STANDARD REPORT HEADING                                         01340011
      ******************************************************************01350011
                                                                        01360011
           COPY DPWS200O.                                               01370011
                                                                        01380011
       01  RPT-REPORT-HEADING.                                          01390011
           05  FILLER            PIC  X(85)    VALUE SPACES.            01400011
           05  FILLER            PIC  X(53)    VALUE                    01410011
             'XM WAREHOUSE DATA INTEGRITY ERRORS'.                      01420011
                                                                        01430011
       01  RPT-COL-HEADING-1.                                           01440011
           05  FILLER            PIC  X(01) VALUE SPACE.                01450011
           05  FILLER            PIC  X(17) VALUE ALL '-'.              01460011
           05  FILLER            PIC  X(05) VALUE ' KEY '.              01470011
           05  FILLER            PIC  X(18) VALUE ALL '-'.              01480011
                                                                        01490011
       01  RPT-COL-HEADING-2.                                           01500011
           05  FILLER            PIC  X(01) VALUE SPACE.                01510011
           05  FILLER            PIC  X(32) VALUE SPACE.                01520011
           05  FILLER            PIC  X(10) VALUE 'WAREHOUSE'.          01530011
           05  FILLER            PIC  X(07) VALUE 'TYPE'.               01540011
           05  FILLER            PIC  X(38) VALUE 'XM DOMAIN'.          01550011
           05  FILLER            PIC  X(40) VALUE                       01560011
              'RETEK MANAGEMENT SYSTENM - RMS'.                         01570011
           05  FILLER            PIC  X(08) VALUE 'MESSAGE'.            01580011
                                                                        01590011
       01  RPT-COL-HEADING-3.                                           01600011
           05  FILLER            PIC  X(01) VALUE SPACE.                01610011
           05  FILLER            PIC  X(42) VALUE SPACE.                01620011
           05  FILLER            PIC  X(07) VALUE 'CODE'.               01630011
           05  FILLER            PIC  X(38) VALUE 'VALUE'.              01640011
           05  FILLER            PIC  X(10) VALUE 'VALUE'.              01650011
                                                                        01660011
       01  DL-DETAIL-LINE.                                              01670011
           05  FILLER            PIC  X(01) VALUE SPACE.                01680011
           05  FILLER            PIC  X(34) VALUE SPACE.                01690011
           05  DL-LOCATION       PIC  ZZZZZ.                            01700011
           05  FILLER            PIC  X(04) VALUE SPACE.                01710011
           05  DL-TYP-CDE        PIC  X(03).                            01720011
           05  FILLER            PIC  X(03) VALUE SPACE.                01730011
           05  DL-XM-VALUE       PIC  X(35).                            01740011
           05  FILLER            PIC  X(03) VALUE SPACE.                01750011
           05  DL-RMS-VALUE      PIC  X(35).                            01760011
           05  FILLER            PIC  X(03) VALUE SPACE.                01770011
           05  DL-MESSAGE        PIC  X(75).                            01780011
                                                                        01790011
       01  RPT-TOTAL-LINE.                                              01800011
           05  FILLER            PIC  X(23) VALUE                       01810011
                                              'TOTAL NUMBER OF ERRORS '.01820011
           05  RPT-NBR-ERRORS    PIC  ZZZZZZ9.                          01830011
                                                                        01840011
       01  RPT-END-OF-REPORT-LINE.                                      01850011
           05  FILLER            PIC  X(21) VALUE                       01860011
                                                '*** END OF REPORT ***'.01870011
                                                                        01880011
      ***************************************************************** 01890011
      * RECORD LAYOUT FOR RMS WAREHOUSE EXTRACT FILE                    01900011
      ***************************************************************** 01910011
      *    COPY HORD007.                                                01920011
           COPY HORD014.                                                01930011
                                                                        01940011
      ***************************************************************** 01950011
      * RECORD LAYOUT FOR XMT_LOC EXTRACT FILE                          01960011
      * FILLER OF 01 BETWEEN COLUMNS INDICATES THAT THE PRIOR COLUMN    01970011
      * IS NULLABLE.                                                    01980011
      ***************************************************************** 01990011
0407PK     COPY XMRD016.                                                01991011
                                                                        02690011
      ***************************************************************** 02691011
      * COPYBOOK FOR THE LOCATION DATA INTEGRITY ERROR MESSAGE TABLE.   02692011
      ***************************************************************** 02693011
           COPY XMWS001.                                                02700011
                                                                        02710011
                                                                        02720011
      *----------------------------------------------------------------*02730011
       PROCEDURE DIVISION.                                              02740011
      *----------------------------------------------------------------*02750011
                                                                        02760011
           OPEN INPUT  RMS-LOC-EXTRACT-DATA-FILE                        02770011
                        XM-LOC-EXTRACT-DATA-FILE                        02780011
                OUTPUT XM-LOC-DATA-INTEGRITY-RPT.                       02790011
                                                                        02800011
           PERFORM 1000-INITIALIZE.                                     02810011
                                                                        02820011
           PERFORM 7000-READ-RMS-FILE.                                  02830011
           PERFORM 7100-READ-XM-FILE.                                   02840011
                                                                        02850011
                                                                        02860011
           PERFORM 2000-COMPARE-FILES                                   02870011
               UNTIL PS-END-OF-RMS-FILE AND                             02880011
                     PS-END-OF-XM-FILE.                                 02890011
                                                                        02900011
           PERFORM 8400-WRITE-TOTAL-LINE.                               02910011
                                                                        02920011
           CLOSE RMS-LOC-EXTRACT-DATA-FILE                              02930011
                  XM-LOC-EXTRACT-DATA-FILE                              02940011
                 XM-LOC-DATA-INTEGRITY-RPT.                             02950011
                                                                        02960011
           DISPLAY '***** ' PC-PGM-NAME ' STATISTICS *****'.            02970011
           MOVE PV-XM-EXTRACT-REC-CNT TO PV-DISPLAY-CNT.                02980011
           DISPLAY '   ** XM EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.    02990011
           MOVE PV-RMS-EXTR-REC-CNT    TO PV-DISPLAY-CNT.               03000011
           DISPLAY '   ** RMS    EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.03010011
           MOVE PV-NBR-ERRORS  TO PV-DISPLAY-CNT.                       03020011
                                                                        03030011
      *    IF THERE ARE ANY ERRORS TO BE REPORTED, ISSUE A              03040011
      *    RETURN CODE SO THAT AN E-MAIL WILL BE SENT IN A              03050011
      *    SUBSEQUENT JOB STEP.                                         03060011
                                                                        03070011
           IF PV-NBR-ERRORS > 0                                         03080011
               MOVE 4095 TO RETURN-CODE                                 03090011
           END-IF.                                                      03100011
                                                                        03110011
           DISPLAY '   ** ERRORS REPORTED: ' PV-DISPLAY-CNT.            03120011
           GOBACK.                                                      03130011
                                                                        03140011
                                                                        03150011
      ******************************************************************03160011
      * INITIALIZE AND WRITE REPORT HEADINGS                            03170011
      ******************************************************************03180011
       1000-INITIALIZE.                                                 03190011
                                                                        03200011
           ACCEPT PV-DATE-YYMMDD FROM DATE.                             03210011
           MOVE PV-YYMMDD-YY TO DP200O-RUN-YEAR.                        03220011
           MOVE PV-YYMMDD-MM TO DP200O-RUN-MONTH.                       03230011
           MOVE PV-YYMMDD-DD TO DP200O-RUN-DAY.                         03240011
                                                                        03250011
           ACCEPT PV-TIME-HHMMSS FROM TIME.                             03260011
           MOVE PV-HHMMSS-HH TO DP200O-RUN-HOUR.                        03270011
           MOVE PV-HHMMSS-MM TO DP200O-RUN-MINUTE.                      03280011
           MOVE PC-PGM-NAME  TO DP200O-PROGRAM-NAME.                    03290011
           MOVE 1            TO DP200O-REPORT-NUMBER.                   03300011
                                                                        03310011
           PERFORM 8100-WRITE-STD-RPT-HEADING.                          03320011
           PERFORM 8200-WRITE-REPORT-HEADING.                           03330011
           PERFORM 8300-WRITE-COLUMN-HEADING.                           03340011
                                                                        03350011
      ******************************************************************03360011
      * COMPARE RMS DATA TO XM DATA                                     03370011
      ******************************************************************03380011
       2000-COMPARE-FILES.                                              03390011
                                                                        03400011
           MOVE SPACES       TO PV-XM-VALUE                             03410011
                                PV-RMS-VALUE.                           03420011
                                                                        03430011
           IF PV-LOC-NBR = HO014-WH-NBR                                 03440011
      *****    LOCATION IS ON BOTH XM AND RMS.  *****                   03450011
               PERFORM 2100-COMPARE-FIELDS                              03460011
               PERFORM 7000-READ-RMS-FILE                               03470011
               PERFORM 7100-READ-XM-FILE                                03480011
           ELSE                                                         03490011
               IF PV-LOC-NBR < HO014-WH-NBR                             03500011
      *****        LOCATION IS ON XM ONLY.      *****                   03510011
                   MOVE 26 TO PV-MSG-NBR                                03520011
                   PERFORM 3200-WRITE-REPORT-REC-XM                     03530011
                   PERFORM 7100-READ-XM-FILE                            03540011
              ELSE                                                      03550011
      *****        LOCATION IS ON RMS ONLY.     *****                   03560011
                   MOVE 27 TO PV-MSG-NBR                                03570011
                   PERFORM 3100-WRITE-REPORT-REC-RMS                    03580011
                   PERFORM 7000-READ-RMS-FILE                           03590011
              END-IF                                                    03600011
           END-IF.                                                      03610011
                                                                        03620011
      ******************************************************************03630011
      * COMPARE RMS DATA TO XM DATA FIELD BY FIELD                      03640011
      ******************************************************************03650011
      * DUE TO NULL DATA, NEED TO CHECK FOR THE POSSIBILITY THAT THE    03660011
0407PK* RMS FILE HAS SPACES WHEN THE XM EXTRACT HAS LOW VALUES. THIS    03670012
      * IS OK AND SHOULD NOT BE REGARDED AS AN ERROR.                   03680011
      * ALSO IT IS POSSIBLE THAT FOR DATE FIELDS, XM EXTRACT WILL HAVE  03690011
0407PK* LOW VALUES WHEN RMS HAS 9999-09-09. THIS IS ALSO OK AND         03700012
      * SHOULD NOT BE REGARDED AS AN ERROR.                             03710011
      ******************************************************************03720011
       2100-COMPARE-FIELDS.                                             03730011
                                                                        03740012
           IF (HO014-WH-TYP-CDE NOT = XM016-LOC-TYP-CDE                 03741012
               AND NOT (HO014-WH-TYP-CDE      = SPACES                  03742012
                        AND XM016-LOC-TYP-CDE = LOW-VALUES))            03743012
               MOVE HO014-WH-TYP-CDE     TO PV-RMS-VALUE                03744012
               MOVE XM016-LOC-TYP-CDE    TO PV-XM-VALUE                 03745012
               MOVE 3                    TO PV-MSG-NBR                  03746012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03747012
           END-IF.                                                      03748012
           IF (HO014-WH-NM NOT = XM016-LOC-NM                           03749012
               AND NOT (HO014-WH-NM        = SPACES                     03750012
                        AND XM016-LOC-NM   = LOW-VALUES))               03760012
               MOVE HO014-WH-NM           TO PV-RMS-VALUE               03770012
               MOVE XM016-LOC-NM          TO PV-XM-VALUE                03780012
               MOVE 4                     TO PV-MSG-NBR                 03790012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03800012
           END-IF.                                                      03810012
           IF (HO014-WH-5-ABBR-NM NOT = XM016-LOC-5-ABBR-NM             03820012
               AND NOT (HO014-WH-5-ABBR-NM      = SPACES                03830012
                        AND XM016-LOC-5-ABBR-NM = LOW-VALUES))          03840012
               MOVE HO014-WH-5-ABBR-NM    TO PV-RMS-VALUE               03850012
               MOVE XM016-LOC-5-ABBR-NM TO PV-XM-VALUE                  03860012
               MOVE 5                     TO PV-MSG-NBR                 03870012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03880012
           END-IF.                                                      03881012
           IF (HO014-WH-LNE-1-ADDR NOT = XM016-LNE-1-ADDR               03882012
               AND NOT (HO014-WH-LNE-1-ADDR  = SPACES                   03883012
                        AND XM016-LNE-1-ADDR = LOW-VALUES))             03884012
               MOVE HO014-WH-LNE-1-ADDR     TO PV-RMS-VALUE             03885012
               MOVE XM016-LNE-1-ADDR        TO PV-XM-VALUE              03886012
               MOVE 6                       TO PV-MSG-NBR               03887012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03888012
           END-IF.                                                      03889012
           IF (HO014-WH-LNE-2-ADDR NOT = XM016-LNE-2-ADDR               03890012
               AND NOT (HO014-WH-LNE-2-ADDR  = SPACES                   03900012
                        AND XM016-LNE-2-ADDR = LOW-VALUES))             03910012
               MOVE HO014-WH-LNE-2-ADDR     TO PV-RMS-VALUE             03920012
               MOVE XM016-LNE-2-ADDR        TO PV-XM-VALUE              03930012
               MOVE 7                       TO PV-MSG-NBR               03940012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03950012
           END-IF.                                                      03951012
           IF (HO014-WH-CITY NOT = XM016-CTY-NM                         03952012
               AND NOT (HO014-WH-CITY      = SPACES                     03953012
                        AND XM016-CTY-NM   = LOW-VALUES))               03954012
               MOVE HO014-WH-CITY         TO PV-RMS-VALUE               03955012
               MOVE XM016-CTY-NM          TO PV-XM-VALUE                03956012
               MOVE 8                     TO PV-MSG-NBR                 03957012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03958012
           END-IF.                                                      03959012
           IF (HO014-WH-ST-CDE NOT = XM016-ST-CDE                       03960012
               AND NOT (HO014-WH-ST-CDE    = SPACES                     03970012
                        AND XM016-ST-CDE   = LOW-VALUES))               03980012
               MOVE HO014-WH-ST-CDE       TO PV-RMS-VALUE               03990012
               MOVE XM016-ST-CDE          TO PV-XM-VALUE                04000012
               MOVE 9                     TO PV-MSG-NBR                 04010012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04020012
           END-IF.                                                      04030012
           IF (HO014-WH-ZIP-10-CDE NOT = XM016-ZIP-10-CDE               04040012
               AND NOT (HO014-WH-ZIP-10-CDE  = SPACES                   04050012
                        AND XM016-ZIP-10-CDE = LOW-VALUES))             04060012
               MOVE HO014-WH-ZIP-10-CDE      TO PV-RMS-VALUE            04070012
               MOVE XM016-ZIP-10-CDE         TO PV-XM-VALUE             04080012
               MOVE 10                       TO PV-MSG-NBR              04090012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04100012
           END-IF.                                                      04110012
           IF (HO014-WH-OPEN-DATE NOT         = XM016-OPN-DTE           04120012
               AND NOT (HO014-WH-OPEN-DATE    = '9999-09-09'            04130012
                        AND XM016-OPN-DTE     = LOW-VALUES))            04140012
               MOVE HO014-WH-OPEN-DATE       TO PV-RMS-VALUE            04150012
               MOVE XM016-OPN-DTE            TO PV-XM-VALUE             04160012
               MOVE 11                       TO PV-MSG-NBR              04170012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04180012
           END-IF.                                                      04190012
           IF (HO014-WH-CLOSE-DATE       NOT = XM016-CLO-DTE            04200012
               AND NOT (HO014-WH-CLOSE-DATE  = '9999-09-09'             04210012
                        AND XM016-CLO-DTE    = LOW-VALUES))             04220012
               MOVE HO014-WH-CLOSE-DATE     TO PV-RMS-VALUE             04230012
               MOVE XM016-CLO-DTE           TO PV-XM-VALUE              04240012
               MOVE 12                      TO PV-MSG-NBR               04250012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04260012
           END-IF.                                                      04270012
           IF (HO014-WH-10-ABBR-NM NOT = XM016-LOC-10-ABBR-NM           04280012
               AND NOT (HO014-WH-10-ABBR-NM      = LOW-VALUES           04290012
                        AND XM016-LOC-10-ABBR-NM = SPACES))             04300012
               MOVE HO014-WH-10-ABBR-NM TO PV-RMS-VALUE                 04310012
               MOVE XM016-LOC-10-ABBR-NM TO PV-XM-VALUE                 04320012
               MOVE 13                    TO PV-MSG-NBR                 04330012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04340012
           END-IF.                                                      04350012
           IF (HO014-WH-OCPNC-DTE NOT = XM016-OCPNC-DTE                 04360012
               AND NOT (HO014-WH-OCPNC-DTE      = '9999-09-09'          04380012
                        AND XM016-OCPNC-DTE     = LOW-VALUES))          04390012
               MOVE HO014-WH-OCPNC-DTE         TO PV-RMS-VALUE          04400012
               MOVE XM016-OCPNC-DTE            TO PV-XM-VALUE           04410012
               MOVE 19                         TO PV-MSG-NBR            04420012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04430012
           END-IF.                                                      04440012
           IF (HO014-WH-DST-IND           NOT = XM016-DST-IND           04450012
               AND NOT (HO014-WH-DST-IND      = SPACES                  04460012
                        AND XM016-DST-IND     = LOW-VALUES))            04470012
               MOVE HO014-WH-DST-IND         TO PV-RMS-VALUE            04480012
               MOVE XM016-DST-IND            TO PV-XM-VALUE             04490012
               MOVE 20                       TO PV-MSG-NBR              04500012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04510012
           END-IF.                                                      04520012
           IF (HO014-WH-E-BUS-IND         NOT = XM016-E-BUS-IND         04530012
               AND NOT (HO014-WH-E-BUS-IND    = SPACES                  04540012
                        AND XM016-E-BUS-IND   = LOW-VALUES))            04550012
               MOVE HO014-WH-E-BUS-IND       TO PV-RMS-VALUE            04560012
               MOVE XM016-E-BUS-IND          TO PV-XM-VALUE             04570012
               MOVE 21                       TO PV-MSG-NBR              04580012
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04590012
           END-IF.                                                      04600012
                                                                        04700011
                                                                        04710011
      ******************************************************************04720011
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON BOTH RMS AND XM.    04730011
      ******************************************************************04740011
       3000-WRITE-REPORT-REC-BOTH.                                      04750011
                                                                        04760011
           MOVE HO014-WH-NBR         TO DL-LOCATION.                    04770011
           MOVE HO014-WH-TYP-CDE     TO DL-TYP-CDE.                     04780011
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    04790011
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   04800011
           SET  XMEM-IDX             TO PV-MSG-NBR.                     04810011
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 04820011
                                                                        04830011
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            04840011
               PERFORM 8100-WRITE-STD-RPT-HEADING                       04850011
               PERFORM 8200-WRITE-REPORT-HEADING                        04860011
               PERFORM 8300-WRITE-COLUMN-HEADING                        04870011
           END-IF.                                                      04880011
                                                                        04890011
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          04900011
                                                                        04910011
      ******************************************************************04920011
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON RMS ONLY.           04930011
      ******************************************************************04940011
       3100-WRITE-REPORT-REC-RMS.                                       04950011
                                                                        04960011
           MOVE HO014-WH-NBR         TO DL-LOCATION.                    04970011
           MOVE HO014-WH-TYP-CDE     TO DL-TYP-CDE.                     04980011
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    04990011
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05000011
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05010011
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05020011
                                                                        05030011
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05040011
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05050011
               PERFORM 8200-WRITE-REPORT-HEADING                        05060011
               PERFORM 8300-WRITE-COLUMN-HEADING                        05070011
           END-IF.                                                      05080011
                                                                        05090011
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          05100011
                                                                        05110011
      ******************************************************************05120011
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON XM ONLY.            05130011
      ******************************************************************05140011
       3200-WRITE-REPORT-REC-XM.                                        05150011
                                                                        05160011
           MOVE XM016-LOC-NBR        TO DL-LOCATION.                    05170011
           MOVE XM016-LOC-TYP-CDE    TO DL-TYP-CDE.                     05180011
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    05190011
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05200011
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05210011
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05220011
                                                                        05230011
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05240011
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05250011
               PERFORM 8200-WRITE-REPORT-HEADING                        05260011
               PERFORM 8300-WRITE-COLUMN-HEADING                        05270011
           END-IF.                                                      05280011
                                                                        05290011
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          05300011
                                                                        05310011
      ******************************************************************05320011
      * READ RMS INPUT FILE                                             05330011
      ******************************************************************05340011
       7000-READ-RMS-FILE.                                              05350011
           IF PS-NOT-END-OF-RMS-FILE                                    05360011
             READ RMS-LOC-EXTRACT-DATA-FILE                             05370011
               INTO HO014-WH-DATA                                       05380011
                 AT END SET PS-END-OF-RMS-FILE TO TRUE                  05390011
                 MOVE 9999 TO HO014-WH-NBR                              05400011
           END-IF.                                                      05410011
           IF PS-NOT-END-OF-RMS-FILE                                    05420011
              ADD +1 TO PV-RMS-EXTR-REC-CNT                             05430011
           END-IF.                                                      05440011
                                                                        05450011
      ******************************************************************05460011
      * READ XM INPUT FILE                                              05470011
      ******************************************************************05480011
       7100-READ-XM-FILE.                                               05490011
           IF PS-NOT-END-OF-XM-FILE                                     05500011
             READ XM-LOC-EXTRACT-DATA-FILE                              05510011
               INTO XM016-LOC-UNLOAD                                    05520011
                 AT END SET PS-END-OF-XM-FILE TO TRUE                   05530011
                 MOVE 9999 TO PV-LOC-NBR                                05540011
           END-IF.                                                      05550011
           IF PS-NOT-END-OF-XM-FILE                                     05560011
              ADD +1 TO PV-XM-EXTRACT-REC-CNT                           05570011
              PERFORM 7110-LOAD-XM-WORK-FIELDS                          05580011
           END-IF.                                                      05590011
                                                                        05600011
       7110-LOAD-XM-WORK-FIELDS.                                        05610011
           MOVE XM016-LOC-NBR              TO PV-LOC-NBR.               05620011
           MOVE XM016-REPLT-STR-NBR        TO PV-REPLT-STR-NBR.         05630011
           MOVE XM016-DIST-NBR             TO PV-DIST-NBR.              05640011
           MOVE XM016-POS-RGST-BNK-QTY     TO PV-POS-RGST-BNK-QTY.      05650011
           MOVE XM016-FMT-CDE              TO PV-FMT-CDE.               05660011
           MOVE XM016-TOT-SLG-SQ-FTG-QTY TO PV-TOT-SLG-SQ-FTG-QTY.      05670011
           MOVE XM016-TOT-SQ-FTG-QTY       TO PV-TOT-SQ-FTG-QTY.        05680011
           MOVE XM016-STP-ORD-DAYS-QTY     TO PV-STP-ORD-DAYS-QTY.      05690011
           MOVE XM016-STRT-ORD-DAYS-QTY    TO PV-STRT-ORD-DAYS-QTY.     05700011
           MOVE XM016-MEDN-AGE-NBR         TO PV-MEDN-AGE-NBR.          05710011
           MOVE XM016-EST-HH-INCM-AMT      TO PV-EST-HH-INCM-AMT.       05720011
      ******************************************************************05730011
      * WRITE REPORT DETAIL LINE                                        05740011
      ******************************************************************05750011
       8000-WRITE-RPT-DETAIL-LINE.                                      05760011
                                                                        05770011
           WRITE XM-LOC-RPT-LINE FROM DL-DETAIL-LINE                    05780011
               AFTER ADVANCING PV-ADVANCE.                              05790011
           ADD PV-ADVANCE TO PV-LINE-CNT.                               05800011
           MOVE +1        TO PV-ADVANCE.                                05810011
           ADD +1 TO PV-NBR-ERRORS.                                     05820011
                                                                        05830011
                                                                        05840011
      ******************************************************************05850011
      * PRINT THE KOHLS STANDARD REPORT HEADING.                        05860011
      ******************************************************************05870011
       8100-WRITE-STD-RPT-HEADING.                                      05880011
                                                                        05890011
           ADD 1            TO PV-PAGE-CNT.                             05900011
           MOVE PV-PAGE-CNT TO DP200O-PAGE-NUMBER.                      05910011
           WRITE XM-LOC-RPT-LINE FROM DP200O-STANDRD-HEADER-200-COLS    05920011
               AFTER ADVANCING PAGE.                                    05930011
           MOVE +1          TO PV-LINE-CNT.                             05940011
                                                                        05950011
                                                                        05960011
      ******************************************************************05970011
      * REPORT HEADING                                                  05980011
      ******************************************************************05990011
       8200-WRITE-REPORT-HEADING.                                       06000011
                                                                        06010011
           WRITE XM-LOC-RPT-LINE FROM RPT-REPORT-HEADING                06020011
               AFTER ADVANCING 1 LINES.                                 06030011
           ADD +1 TO PV-LINE-CNT.                                       06040011
                                                                        06050011
                                                                        06060011
      ******************************************************************06070011
      * REPORT COLUMN HEADING                                           06080011
      ******************************************************************06090011
       8300-WRITE-COLUMN-HEADING.                                       06100011
                                                                        06110011
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-1                 06120011
               AFTER ADVANCING 2 LINES.                                 06130011
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-2                 06140011
               AFTER ADVANCING 1 LINES.                                 06150011
           WRITE XM-LOC-RPT-LINE FROM RPT-COL-HEADING-3                 06160011
               AFTER ADVANCING 1 LINES.                                 06170011
           ADD  +4 TO PV-LINE-CNT.                                      06180011
           MOVE +2 TO PV-ADVANCE.                                       06190011
                                                                        06200011
                                                                        06210011
      ******************************************************************06220011
      * REPORT TOTAL LINE                                               06230011
      ******************************************************************06240011
       8400-WRITE-TOTAL-LINE.                                           06250011
                                                                        06260011
           MOVE PV-NBR-ERRORS TO RPT-NBR-ERRORS.                        06270011
                                                                        06280011
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            06290011
               PERFORM 8100-WRITE-STD-RPT-HEADING                       06300011
               PERFORM 8200-WRITE-REPORT-HEADING                        06310011
               PERFORM 8300-WRITE-COLUMN-HEADING                        06320011
           END-IF.                                                      06330011
                                                                        06340011
           WRITE XM-LOC-RPT-LINE FROM RPT-TOTAL-LINE                    06350011
               AFTER ADVANCING 2 LINES.                                 06360011
           ADD +2 TO PV-LINE-CNT.                                       06370011
                                                                        06380011
           WRITE XM-LOC-RPT-LINE FROM RPT-END-OF-REPORT-LINE            06390011
               AFTER ADVANCING 2 LINES.                                 06400011
                                                                        06410011
