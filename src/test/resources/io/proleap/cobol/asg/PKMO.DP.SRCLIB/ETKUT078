       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.              ETKUT078.                                       
       AUTHOR.              TIA JOHNSTON.                                       
       DATE-WRITTEN.            01/08/99.                                       
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS PROGRAM PACKS SPECIFIC NUMERIC FIELDS ON THE UNIX         *        
      * ASN FILE. AT THIS TIME, THE UNIX TRANSLATOR CANNOT HANDLE      *        
      * COMP FIELDS.                                                   *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT ASN-INPUT-FILE                                                
              ASSIGN TO INP01.                                                  
                                                                                
           SELECT ASN-OUTPUT-FILE                                               
              ASSIGN TO OUT01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  ASN-INPUT-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS ET017-RECORD.                                         
                                                                                
       01  ET017-RECORD.                                                        
           COPY ETRD017.                                                        
                                                                                
       FD  ASN-OUTPUT-FILE                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           DATA RECORD IS ADVANCE-SHIP-NOTICE-DTL-RECORD.                       
                                                                                
           COPY SNRD001I.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW                  PIC X(01)   VALUE 'N'.           
               88  END-OF-FILE                             VALUE 'Y'.           
                                                                                
       01  WS-HL-LEVEL                         PIC X(01).                       
                                                                                
       01  WS-MAN-QUAL                         PIC X(2).                        
                                                                                
       01  WS-STRUCT-CODE                      PIC X(4).                        
                                                                                
       01  WS-TP-ID                            PIC X(15).                       
                                                                                
       01  WS-PREV-TMST                        PIC X(26)  VALUE SPACES.         
                                                                                
       01  WS-CURR-TMST.                                                        
           05  WS-CURR-TMST-CHAR               PIC X(01).                       
           05  FILLER                          PIC X(25).                       
                                                                                
       01  WS-SLN-TABLE.                                                        
           05  WS-SLN-TABLE-ENTRY  OCCURS 10 TIMES.                             
                15 WS-PROD-ID-QUAL     PIC X(02).                               
                15 WS-PROD-ID          PIC X(30).                               
        01  WS-SUB                     PIC S9(4) COMP.                          
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT  ASN-INPUT-FILE                                           
                OUTPUT ASN-OUTPUT-FILE.                                         
                                                                                
           PERFORM 1300-READ-INPUT-FILE                                         
              THRU 1300-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           CLOSE ASN-INPUT-FILE                                                 
                 ASN-OUTPUT-FILE.                                               
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-FILE.                                                       
                                                                                
           MOVE ET017-HEADER-LEVEL-ID TO SN001I-REC-TYP-CDE.                    
                                                                                
           IF ID-IDENTIFICATION-REC                                             
               PERFORM 2000-PROCESS-ID-REC                                      
                  THRU 2000-PROCESS-ID-REC-EXIT.                                
                                                                                
           IF HL-HIERARCHICAL-REC                                               
               PERFORM 2010-PROCESS-HL-REC                                      
                  THRU 2010-PROCESS-HL-REC-EXIT.                                
                                                                                
           IF PO4-ITEM-PHYSICAL-DTL-REC                                         
               PERFORM 2020-PROCESS-PO4-REC                                     
                  THRU 2020-PROCESS-PO4-REC-EXIT.                               
                                                                                
           IF TD1-CARRIER-QTY-WGHT-REC                                          
               PERFORM 2030-PROCESS-TD1-REC                                     
                  THRU 2030-PROCESS-TD1-REC-EXIT.                               
                                                                                
           IF TD5-CARRIER-RTE-TRNST-REC                                         
               PERFORM 2040-PROCESS-TD5-REC                                     
                  THRU 2040-PROCESS-TD5-REC-EXIT.                               
                                                                                
           IF TD3-CARRIER-EQUIPMENT-REC                                         
               PERFORM 2045-PROCESS-TD3-REC                                     
                  THRU 2045-PROCESS-TD3-REC-EXIT.                               
                                                                                
SP0406     IF TD4-CARRIER-SPECIAL-HAND                                          
SP0406         PERFORM 2048-PROCESS-TD4-REC                                     
SP0406            THRU 2048-PROCESS-TD4-REC-EXIT.                               
                                                                                
           IF REF-REFERENCE-NUMBERS-REC                                         
               PERFORM 2050-PROCESS-REF-REC                                     
                  THRU 2050-PROCESS-REF-REC-EXIT.                               
                                                                                
           IF DTM-DTE-TME-REFERENCE-REC                                         
               PERFORM 2060-PROCESS-DTM-REC                                     
                  THRU 2060-PROCESS-DTM-REC-EXIT.                               
                                                                                
           IF N1-NAME-REC                                                       
               PERFORM 2070-PROCESS-N1-REC                                      
                  THRU 2070-PROCESS-N1-REC-EXIT.                                
                                                                                
           IF N3-ADDRESS-REC                                                    
               PERFORM 2080-PROCESS-N3-REC                                      
                  THRU 2080-PROCESS-N3-REC-EXIT.                                
                                                                                
           IF N4-GEOGRAPHIC-LOC-REC                                             
               PERFORM 2090-PROCESS-N4-REC                                      
                  THRU 2090-PROCESS-N4-REC-EXIT.                                
                                                                                
           IF PRF-PURCHASE-ORDER-REF-REC                                        
               PERFORM 2100-PROCESS-PRF-REC                                     
                  THRU 2100-PROCESS-PRF-REC-EXIT.                               
                                                                                
           IF PKG-MRKG-PCKG-LDG-REC                                             
               PERFORM 2110-PROCESS-PKG-REC                                     
                  THRU 2110-PROCESS-PKG-REC-EXIT.                               
                                                                                
           IF MAN-MARKS-NUMBERS-REC                                             
               PERFORM 2120-PROCESS-MAN-REC                                     
                  THRU 2120-PROCESS-MAN-REC-EXIT.                               
                                                                                
           IF LIN-ITEM-IDENTIFICATION-REC                                       
               PERFORM 2130-PROCESS-LIN-REC                                     
                  THRU 2130-PROCESS-LIN-REC-EXIT.                               
                                                                                
           IF SN1-ITEM-DETAIL-REC                                               
               PERFORM 2140-PROCESS-SN1-REC                                     
                  THRU 2140-PROCESS-SN1-REC-EXIT.                               
                                                                                
           IF SLN-SUBITEM-DETAIL-REC                                            
               PERFORM 2150-PROCESS-SLN-REC                                     
                  THRU 2150-PROCESS-SLN-REC-EXIT.                               
                                                                                
           PERFORM 1200-WRITE-OUTREC                                            
              THRU 1200-WRITE-OUTREC-EXIT.                                      
                                                                                
           PERFORM 1300-READ-INPUT-FILE                                         
              THRU 1300-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-ID-REC.                                                     
                                                                                
           MOVE ET017-HEADER-LEVEL-RECORD                                       
                       TO SN001I-HEADER-LEVEL-RECORD.                           
                                                                                
           MOVE SPACES TO SN001I-RECYCLE-DATE.                                  
           MOVE ET017-STRUCTURE-CODE TO SN001I-STRUC-CDE.                       
           MOVE ET017-ISA-CNTL-NBR   TO SN001I-ISA-CNTL-NBR.                    
           MOVE ET017-GS-CNTL-NBR    TO SN001I-GS-CNTL-NBR.                     
           MOVE ET017-ST-CNTL-NBR    TO SN001I-ST-CNTL-NBR.                     
           MOVE ET017-SESSION-NBR    TO SN001I-SESSION-NBR.                     
           MOVE ET017-ISA-ID-QUAL    TO SN001I-ISA-ID-QUAL.                     
           MOVE ET017-ISA-ID         TO SN001I-ISA-ID.                          
                                                                                
           MOVE ET017-ISA-TMST       TO WS-CURR-TMST.                           
           IF WS-CURR-TMST-CHAR = SPACE                                         
               MOVE WS-PREV-TMST     TO SN001I-ISA-TMST                         
           ELSE                                                                 
               MOVE ET017-ISA-TMST   TO WS-PREV-TMST                            
               MOVE ET017-ISA-TMST   TO SN001I-ISA-TMST                         
           END-IF.                                                              
                                                                                
           MOVE ET017-STRUCTURE-CODE TO WS-STRUCT-CODE.                         
           MOVE ET017-ISA-ID         TO WS-TP-ID.                               
                                                                                
       2000-PROCESS-ID-REC-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       2010-PROCESS-HL-REC.                                                     
                                                                                
           MOVE ET017-SHIP-NOTICE-HL-SEGMENT                                    
                       TO SN001I-SHIP-NOTICE-DTL-RECORD.                        
                                                                                
           MOVE ET017-HL-LEVEL-CODE TO WS-HL-LEVEL.                             
           MOVE SPACES TO WS-MAN-QUAL.                                          
                                                                                
       2010-PROCESS-HL-REC-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       2020-PROCESS-PO4-REC.                                                    
                                                                                
           INITIALIZE SN001I-PO4-SEGMENT.                                       
                                                                                
           MOVE ET017-PO4-ID         TO SN001I-PO4-ID.                          
                                                                                
           IF ET017-PO4-INNER-PACK-QTY NOT NUMERIC                              
               MOVE ZEROS            TO SN001I-PO401-PKG-SIZE-QTY               
           ELSE                                                                 
               MOVE ET017-PO4-INNER-PACK-QTY                                    
                                     TO SN001I-PO401-PKG-SIZE-QTY.              
                                                                                
           IF ET017-PO4-SKUS-PER-PACK NOT NUMERIC                               
               MOVE ZEROS            TO SN001I-PO402-SIZE-MSMT                  
           ELSE                                                                 
               MOVE ET017-PO4-SKUS-PER-PACK                                     
                                     TO SN001I-PO402-SIZE-MSMT.                 
                                                                                
           MOVE ET017-PO4-PACK-UM-CODE                                          
                                     TO SN001I-PO403-SIZE-UM-CDE.               
                                                                                
           IF ET017-PO4-GROSS-WEIGHT-PER-PCK NOT NUMERIC                        
               MOVE ZEROS            TO SN001I-PO406-GRO-WGHT-MSMT              
           ELSE                                                                 
               MOVE ET017-PO4-GROSS-WEIGHT-PER-PCK                              
                                     TO SN001I-PO406-GRO-WGHT-MSMT.             
                                                                                
           MOVE ET017-PO4-GROSS-WEIGHT-UM-CODE                                  
                                     TO SN001I-PO407-WGHT-UM-CDE.               
                                                                                
           IF ET017-PO4-GROSS-VOLUME-PER-PCK NOT NUMERIC                        
               MOVE ZEROS            TO SN001I-PO408-GRO-VOL-MSMT               
           ELSE                                                                 
               MOVE ET017-PO4-GROSS-VOLUME-PER-PCK                              
                                     TO SN001I-PO408-GRO-VOL-MSMT.              
                                                                                
           MOVE ET017-PO4-GROSS-VOLUME-UM-CODE                                  
                                     TO SN001I-PO409-VOL-UM-CDE.                
                                                                                
           IF ET017-PO4-LENGTH NOT NUMERIC                                      
               MOVE ZEROS            TO SN001I-PO410-LEN-MSMT                   
           ELSE                                                                 
               MOVE ET017-PO4-LENGTH TO SN001I-PO410-LEN-MSMT.                  
                                                                                
           IF ET017-PO4-WIDTH NOT NUMERIC                                       
               MOVE ZEROS            TO SN001I-PO411-WDTH-MSMT                  
           ELSE                                                                 
               MOVE ET017-PO4-WIDTH  TO SN001I-PO411-WDTH-MSMT.                 
                                                                                
           IF ET017-PO4-HEIGHT NOT NUMERIC                                      
               MOVE ZEROS            TO SN001I-PO412-HT-MSMT                    
           ELSE                                                                 
               MOVE ET017-PO4-HEIGHT TO SN001I-PO412-HT-MSMT.                   
                                                                                
           MOVE ET017-PO4-MEASUREMENT-UM-CODE                                   
                                     TO SN001I-PO413-UM-CDE.                    
           MOVE ET017-PO4-INNER-PACK TO SN001I-PO414-INPK-EA-QTY.               
                                                                                
       2020-PROCESS-PO4-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2030-PROCESS-TD1-REC.                                                    
                                                                                
           INITIALIZE SN001I-TD1-SEGMENT.                                       
                                                                                
           MOVE ET017-TD1-ID         TO SN001I-TD1-ID.                          
           MOVE ET017-TD1-PACKAGING-CODE                                        
                                     TO SN001I-TD101-PKG-CDE.                   
                                                                                
           IF ET017-TD1-SHIPMENT-QTY NOT NUMERIC                                
               MOVE ZEROS            TO SN001I-TD102-SHIPT-QTY                  
           ELSE                                                                 
               MOVE ET017-TD1-SHIPMENT-QTY                                      
                                     TO SN001I-TD102-SHIPT-QTY.                 
                                                                                
           MOVE ET017-TD1-WEIGHT-QUALIF                                         
                                     TO SN001I-TD106-WGHT-QUAL-CDE.             
                                                                                
           IF ET017-TD1-WEIGHT NOT NUMERIC                                      
               MOVE ZEROS            TO SN001I-TD107-WGHT-MSMT                  
           ELSE                                                                 
               MOVE ET017-TD1-WEIGHT TO SN001I-TD107-WGHT-MSMT.                 
                                                                                
           MOVE ET017-TD1-WEIGHT-UM-CODE                                        
                                     TO SN001I-TD108-UM-CDE.                    
                                                                                
       2030-PROCESS-TD1-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2040-PROCESS-TD5-REC.                                                    
                                                                                
           INITIALIZE SN001I-TD5-SEGMENT.                                       
                                                                                
           MOVE ET017-TD5-ID         TO SN001I-TD5-ID.                          
           MOVE ET017-TD5-ROUTING-SEQUENCE-CDE                                  
                                     TO SN001I-TD501-RTE-SEQ-CDE.               
           MOVE ET017-TD5-ID-CODE-QUALIF                                        
                                     TO SN001I-TD502-CARR-ID-QUAL-CDE.          
           MOVE ET017-TD5-ID-CODE    TO SN001I-TD503-CARR-ID-CDE.               
           MOVE ET017-TD5-TRANSPORT-TYPE-CODE                                   
                                     TO SN001I-TD504-TRANS-MTHD-CDE.            
           MOVE ET017-TD5-CARRIER-NAME                                          
                                     TO SN001I-TD505-CARR-NME.                  
           MOVE ET017-TD5-SHIPMENT-STATUS-CODE                                  
                                     TO SN001I-TD506-STATUS-CDE.                
SP0406     MOVE ET017-TD5-LOC-QUAL   TO SN001I-TD507-LOC-QUAL.                  
SP0406     MOVE ET017-TD5-LOC-ID     TO SN001I-TD508-LOC-ID.                    
           MOVE ET017-TD5-TRANSIT-TIME-QUALIF                                   
                                     TO SN001I-TD510-TRNS-TME-QUAL-CDE.         
                                                                                
           IF ET017-TD5-TRANSIT-TIME NOT NUMERIC                                
               MOVE ZEROS            TO SN001I-TD511-TRNST-TIME                 
           ELSE                                                                 
               MOVE ET017-TD5-TRANSIT-TIME                                      
                                     TO SN001I-TD511-TRNST-TIME.                
                                                                                
       2040-PROCESS-TD5-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2045-PROCESS-TD3-REC.                                                    
                                                                                
           MOVE ET017-TD3-SEGMENT                                               
                       TO SN001I-TD3-SEGMENT.                                   
                                                                                
       2045-PROCESS-TD3-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
SP0406 2048-PROCESS-TD4-REC.                                                    
SP0406                                                                          
SP0406     MOVE ET017-TD4-SEGMENT                                               
SP0406                 TO SN001I-TD4-SEGMENT.                                   
SP0406                                                                          
SP0406 2048-PROCESS-TD4-REC-EXIT.                                               
SP0406     EXIT.                                                                
                                                                                
                                                                                
       2050-PROCESS-REF-REC.                                                    
                                                                                
           MOVE ET017-REF-SEGMENT                                               
                       TO SN001I-REF-SEGMENT.                                   
                                                                                
       2050-PROCESS-REF-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2060-PROCESS-DTM-REC.                                                    
                                                                                
           MOVE ET017-DTM-SEGMENT                                               
                       TO SN001I-DTM-SEGMENT.                                   
                                                                                
       2060-PROCESS-DTM-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2070-PROCESS-N1-REC.                                                     
                                                                                
           MOVE ET017-N1-SEGMENT                                                
                       TO SN001I-N1-SEGMENT.                                    
                                                                                
       2070-PROCESS-N1-REC-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       2080-PROCESS-N3-REC.                                                     
                                                                                
           MOVE ET017-N3-SEGMENT                                                
                       TO SN001I-N3-SEGMENT.                                    
                                                                                
       2080-PROCESS-N3-REC-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       2090-PROCESS-N4-REC.                                                     
                                                                                
           MOVE ET017-N4-SEGMENT                                                
                       TO SN001I-N4-SEGMENT.                                    
                                                                                
       2090-PROCESS-N4-REC-EXIT.                                                
           EXIT.                                                                
                                                                                
                                                                                
       2100-PROCESS-PRF-REC.                                                    
                                                                                
           MOVE ET017-PRF-SEGMENT                                               
                       TO SN001I-PRF-SEGMENT.                                   
                                                                                
       2100-PROCESS-PRF-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2110-PROCESS-PKG-REC.                                                    
                                                                                
           MOVE ET017-PKG-SEGMENT                                               
                       TO SN001I-PKG-SEGMENT.                                   
                                                                                
       2110-PROCESS-PKG-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2120-PROCESS-MAN-REC.                                                    
                                                                                
           MOVE ET017-MAN-SEGMENT                                               
                       TO SN001I-MAN-SEGMENT.                                   
                                                                                
WG0514*    IF WS-HL-LEVEL = 'P' AND WS-STRUCT-CODE = '0001'                     
WG0514*      AND ET017-MAN-MARKS-NUMBERS-QUALIF = WS-MAN-QUAL                   
WG0514*        DISPLAY 'MULTIPLE MAN SEGMENTS FOUND: ' WS-TP-ID                 
WG0514*        MOVE 4095 TO RETURN-CODE                                         
WG0514*    END-IF.                                                              
                                                                                
           MOVE ET017-MAN-MARKS-NUMBERS-QUALIF TO WS-MAN-QUAL.                  
                                                                                
       2120-PROCESS-MAN-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2130-PROCESS-LIN-REC.                                                    
                                                                                
           MOVE ET017-LIN-SEGMENT                                               
                       TO SN001I-LIN-SEGMENT.                                   
                                                                                
       2130-PROCESS-LIN-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2140-PROCESS-SN1-REC.                                                    
                                                                                
           INITIALIZE SN001I-SN1-SEGMENT.                                       
                                                                                
           MOVE ET017-SN1-ID         TO SN001I-SN1-ID.                          
                                                                                
           IF ET017-SN1-UNITS-SHIPPED NOT NUMERIC                               
               MOVE ZEROS            TO SN001I-SN102-SHIP-QTY                   
           ELSE                                                                 
               MOVE ET017-SN1-UNITS-SHIPPED                                     
                                     TO SN001I-SN102-SHIP-QTY.                  
                                                                                
           MOVE ET017-SN1-SHIPPED-UM-CODE                                       
                                     TO SN001I-SN103-SHIP-UM-CDE.               
                                                                                
           IF ET017-SN1-QTY-SHIPPED-TO-DATE NOT NUMERIC                         
               MOVE ZEROS            TO SN001I-SN104-SHIPT-DTE-QTY              
           ELSE                                                                 
               MOVE ET017-SN1-QTY-SHIPPED-TO-DATE                               
                                     TO SN001I-SN104-SHIPT-DTE-QTY.             
                                                                                
           IF ET017-SN1-QTY-ORDERED NOT NUMERIC                                 
               MOVE ZEROS            TO SN001I-SN105-ORD-QTY                    
           ELSE                                                                 
               MOVE ET017-SN1-QTY-ORDERED                                       
                                     TO SN001I-SN105-ORD-QTY.                   
                                                                                
           MOVE ET017-SN1-ORDERED-UM-CODE                                       
                                     TO SN001I-SN106-ORD-UM-CDE.                
                                                                                
       2140-PROCESS-SN1-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       2150-PROCESS-SLN-REC.                                                    
                                                                                
           INITIALIZE SN001I-SLN-SEGMENT.                                       
                                                                                
           MOVE ET017-SLN-ID         TO SN001I-SLN-ID.                          
           MOVE ET017-SLN-ID-CODE    TO SN001I-SLN01-ID.                        
           MOVE ET017-SLN-CONFIGURATION-CODE                                    
                                     TO SN001I-SLN03-CONFIG-CDE.                
                                                                                
           IF ET017-SLN-QUANTITY NOT NUMERIC                                    
               MOVE ZEROS            TO SN001I-SLN04-QTY                        
           ELSE                                                                 
               MOVE ET017-SLN-QUANTITY                                          
                                     TO SN001I-SLN04-QTY.                       
                                                                                
           MOVE ET017-SLN-UM-CODE    TO SN001I-SLN05-UM-CDE.                    
                                                                                
           IF ET017-SLN-UNIT-PRICE NOT NUMERIC                                  
               MOVE ZEROS            TO SN001I-SLN06-UNIT-PRICE-AMT             
           ELSE                                                                 
               MOVE ET017-SLN-UNIT-PRICE                                        
                                     TO SN001I-SLN06-UNIT-PRICE-AMT.            
                                                                                
           MOVE ET017-SLN-UNIT-PRICE-CODE                                       
                                     TO SN001I-SLN07-UNIT-PRICE-CDE.            
           MOVE ET017-SLN-PRICE-CD   TO SN001I-SLN08-PRICE-CD.                  
                                                                                
           MOVE ET017-SLN-PRODUCT-ENTRIES                                       
                                     TO WS-SLN-TABLE.                           
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 10                 
              MOVE WS-PROD-ID-QUAL (WS-SUB)                                     
                           TO SN001I-SLN09-PROD-ID-QUAL-CDE (WS-SUB)            
              MOVE WS-PROD-ID (WS-SUB)                                          
                           TO SN001I-SLN10-PROD-ID (WS-SUB)                     
           END-PERFORM.                                                         
                                                                                
                                                                                
       2150-PROCESS-SLN-REC-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
       1200-WRITE-OUTREC.                                                       
                                                                                
           WRITE ADVANCE-SHIP-NOTICE-DTL-RECORD.                                
                                                                                
       1200-WRITE-OUTREC-EXIT.                                                  
           EXIT.                                                                
                                                                                
                                                                                
       1300-READ-INPUT-FILE.                                                    
                                                                                
           READ ASN-INPUT-FILE                                                  
              AT END                                                            
                 SET END-OF-FILE TO TRUE.                                       
                                                                                
       1300-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
                                                                                
