000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.              MRKUP133.                                       
000400 AUTHOR.                  DONALD TOMLINSON.                               
000500 INSTALLATION.            KOHLS DEPARTMENT STORES.                        
000600 DATE-WRITTEN.            03/29/02.                                       
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*                                                                         
001100*                                                                         
001200*  CHANGE LOG:                                                            
001300*  3/29/02 - DON TOMLINSON                                                
001400*            (WR #?????) NEW PROGRAM                                      
002410* 07/30/2004 - ANNE KINTOPF STORE NUMBER EXPANSION  WR#26363    *         
002410*              MOVED TO PICK UP DCLGEN CHANGES FOR TMRISTS AND  *         
002410*              TMRISFE                                          *         
001300*  4/18/22 - JEAN KURK                                                    
001400*            CHG0268937  CHANGE COMMIT TIMEFRAME                          
001300*  4/28/22 - JEAN KURK                                                    
001400*            CHG0269407  CHANGE COMMIT FREQUENCY - PER DBA, THIS          
001500*            PGM IS STILL GENERATING LARGE VOLUMES OF DB2 LOGS            
001300*  1/16/22 - JEAN KURK                                                    
001400*            CHG0278536  CHANGE COMMIT FREQ TO 1 - PER DBA                
002490*****************************************************************         
001700*                                                                         
001800 ENVIRONMENT DIVISION.                                                    
001900*                                                                         
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER. IBM-3090.                                               
002300 OBJECT-COMPUTER. IBM-3090.                                               
002400*                                                                         
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900*                                                                         
003000 DATA DIVISION.                                                           
003100*                                                                         
003200*                                                                         
003300 FILE SECTION.                                                            
003400                                                                          
003500*                                                                         
003600*                                                                         
003700 WORKING-STORAGE SECTION.                                                 
003800                                                                          
003900 01  PROGRAM-SWITCHES.                                                    
004000     05  PS-EOF-TMRITST-CURSOR       PIC  X(01)    VALUE 'N'.             
004100         88  EOF-TMRITST-CURSOR                    VALUE 'Y'.             
004200         88  NOT-EOF-TMRITST-CURSOR                VALUE 'N'.             
004300                                                                          
004400 01  PV-PROGRAM-VARIABLES.                                                
004500     05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
004600     05  PV-DB2-OPER-ATTEMPTED        PIC X(30)  VALUE SPACES.            
004700                                                                          
004800     05  PV-INPUT-REC-CNT             PIC S9(9)  VALUE +0 COMP-3.         
004900     05  PV-INSERT-CNT                PIC S9(9)  VALUE +0 COMP-3.         
005000                                                                          
005100     05  PV-SUB1                      PIC S9(4)  VALUE +0 COMP.           
005101     05  PV-SUB2                      PIC S9(4)  VALUE +0 COMP.           
005102                                                                          
005110     05  PV-INPUT-REC-CNT-DISP        PIC 9(9)   VALUE 0.                 
005120     05  PV-INSERT-CNT-DISP           PIC 9(9)   VALUE 0.                 
005121                                                                          
005130     05  PV-COMMIT-TIMESTAMP         PIC  X(26) VALUE SPACES.             
005140     05  PV-CURRENT-TIMESTAMP        PIC  X(26) VALUE SPACES.             
005200                                                                          
005300     05  PC-MAX-RETRIES               PIC S9(04) VALUE +003.              
005400     05  PC-0                         PIC S9(01) VALUE +0.                
005500     05  PC-1                         PIC S9(01) VALUE +1.                
005510*    05  PC-10                        PIC S99999V USAGE COMP-3            
005511*                                                VALUE +10.               
005510*    05  PC-5                         PIC S99999V USAGE COMP-3            
005511*                                                VALUE +5.                
005510*    05  PC-2                         PIC S99999V USAGE COMP-3            
005511*                                                VALUE +2.                
005510     05  PC-1-SEC                     PIC S99999V USAGE COMP-3            
005511                                                 VALUE +1.                
005512                                                                          
005520     05  PC-PROGRAM-NAME             PIC  X(08) VALUE 'MRKUP133'.         
005530     05  PC-JOB-NAME                 PIC  X(08) VALUE 'MRK013  '.         
005600                                                                          
005700                                                                          
005710******************************************************************        
005720* CR CHECKPOINT RESTART VARIABLES                                *        
005730******************************************************************        
005740 01  CR-CHECKPOINT-RESTART-AREA.                                          
005750     05  CR-AREA-LEN                 PIC S9(04) VALUE +54  COMP.          
005760                                                                          
005770     05  CR-CHECKPOINT-RESTART-VAR.                                       
005780         10  CR-STR-HDR-CNT          PIC  9(09) VALUE ZEROS.              
005790         10  CR-STR-DTL-CNT          PIC  9(09) VALUE ZEROS.              
005791         10  CR-LINES-NO-SKU-CNT     PIC  9(09) VALUE ZEROS.              
005792         10  CR-LINES-UPD-CNT        PIC  9(09) VALUE ZEROS.              
005793         10  CR-CORP-DTL-CNT         PIC  9(09) VALUE ZEROS.              
005794         10  CR-COMMIT-CNT           PIC  9(09) VALUE ZEROS.              
005795                                                                          
005796                                                                          
005800 01  ABEND-CODE                       PIC S9(04) VALUE +0                 
005900                                                 COMP SYNC.               
006000                                                                          
006100 01  PC-PROGRAM-CONSTANTS.                                                
006200     05  PC-PROGRAM                   PIC X(08)  VALUE                    
006300        'MRKUP133'.                                                       
006400                                                                          
006500                                                                          
006600****                                                                      
006700*  COPY BOOK OF WORKING STORAGE FOR SQL ABEND ROUTINE                     
006800****                                                                      
006900                                                                          
007000     COPY DPWS004.                                                        
007100                                                                          
007200     COPY SQLCA2.                                                         
007300                                                                          
007400     EXEC SQL                                                             
007500        INCLUDE SQLCA                                                     
007600     END-EXEC.                                                            
007700                                                                          
007710******************************************************************        
007720*  COBOL DECLARATIONS FOR THE TMRITST TABLE                               
007730******************************************************************        
007800     EXEC SQL                                                             
007900        INCLUDE TMRITST                                                   
008000     END-EXEC.                                                            
008100                                                                          
008110******************************************************************        
008120*  COBOL DECLARATIONS FOR THE TMRISFE TABLE                               
008130******************************************************************        
008200     EXEC SQL                                                             
008300        INCLUDE TMRISFE                                                   
008400     END-EXEC.                                                            
008500                                                                          
008510******************************************************************        
008520*  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART CONTROL TABLE            
008530******************************************************************        
008540*    EXEC SQL                                                             
008550*        INCLUDE TCKRSTCN                                                 
008560*    END-EXEC.                                                            
008600***************************                                               
008700* CURSOR FOR TMRITST                                                      
008800***************************                                               
008900     EXEC SQL                                                             
009000       DECLARE TMRITST_CSR CURSOR WITH HOLD FOR                           
009100       SELECT                                                             
009200             A.ITEM_NBR,                                                  
009300             A.STR_NBR                                                    
009400        FROM TMRITST A                                                    
009500       WHERE NOT EXISTS                                                   
009600         (SELECT ' '                                                      
009700         FROM TMRISFE B                                                   
009800             WHERE B.ITEM_NBR = A.ITEM_NBR                                
009900             AND B.STR_NBR    = A.STR_NBR)                                
010000       FOR FETCH ONLY                                                     
010100     END-EXEC.                                                            
010200*                                                                         
010300                                                                          
010400*                                                                         
010500 PROCEDURE DIVISION.                                                      
010600******************************************************************        
010700*   MAIN PROCESSING ROUTINE                                               
010800******************************************************************        
010900 0000-MAINLINE-PROCESSING.                                                
011000                                                                          
011100     PERFORM 1000-INITIALIZE.                                             
011200                                                                          
011300     PERFORM 3000-MAIN-PROCESSING.                                        
011400                                                                          
011500     PERFORM 9000-END-OF-JOB.                                             
011600                                                                          
011700     GOBACK.                                                              
011800                                                                          
011900******************************************************************        
012000*   INITIALIZATION ROUTINE                                                
012100******************************************************************        
012200*                                                                         
012300 1000-INITIALIZE.                                                         
012400                                                                          
012500     PERFORM 1100-OPEN-TMRITST-CURSOR.                                    
012600                                                                          
012610     PERFORM 4200-GET-COMMIT-TIMESTAMP.                                   
012700                                                                          
012800******************************************************************        
012900*   OPEN CURSOR                                                           
013000******************************************************************        
013100*                                                                         
013200 1100-OPEN-TMRITST-CURSOR.                                                
013300                                                                          
013400     MOVE '1100-OPEN-TMRITST-CURSOR' TO PV-PARAGRAPH-NAME.                
013500     MOVE 'OPEN TMRITST CURSOR'      TO PV-DB2-OPER-ATTEMPTED.            
013510     DISPLAY 'MRKUP133 OPEN CURSOR'.                                      
013600                                                                          
013700     EXEC SQL                                                             
013800         OPEN TMRITST_CSR                                                 
013900     END-EXEC.                                                            
014000                                                                          
014100     EVALUATE TRUE                                                        
014200         WHEN SQLCODE = ZERO                                              
014300             CONTINUE                                                     
014400         WHEN OTHER                                                       
014500             DISPLAY '1100-OPEN-TMRITST-CURSOR'                           
014600             PERFORM 9999-SQL-ERROR                                       
014700       END-EVALUATE.                                                      
014800                                                                          
014900                                                                          
015000******************************************************************        
015100*   MAIN PROCESSING ROUTINE                                               
015200******************************************************************        
015300 3000-MAIN-PROCESSING.                                                    
015400                                                                          
015500     PERFORM 4000-FETCH-TMRITST-CURSOR                                    
015600         UNTIL EOF-TMRITST-CURSOR.                                        
015700                                                                          
015800                                                                          
016000 4000-FETCH-TMRITST-CURSOR.                                               
016100                                                                          
016200     MOVE '4000-FETCH-CSR'       TO PV-PARAGRAPH-NAME.                    
016300     MOVE 'FETCH TMRITST CSR '   TO PV-DB2-OPER-ATTEMPTED.                
016400     PERFORM                                                              
016500         WITH TEST AFTER                                                  
016600         VARYING PV-SUB1                                                  
016700         FROM 1 BY 1                                                      
016800         UNTIL PV-SUB1 > PC-MAX-RETRIES                                   
016900            OR SQLCODE = ZERO                                             
017000            OR SQLCODE = +100                                             
017100                                                                          
017200         EXEC SQL                                                         
017300             FETCH TMRITST_CSR                                            
017400              INTO :MRITST-ITEM-NBR,                                      
017500                   :MRITST-STR-NBR                                        
017600         END-EXEC                                                         
017700         EVALUATE TRUE                                                    
017800             WHEN SQLCODE = ZERO                                          
017810                 ADD PC-1      TO PV-INPUT-REC-CNT                        
017900             WHEN SQLCODE = -904                                          
018000             WHEN SQLCODE = -913                                          
018100                 CONTINUE                                                 
018200             WHEN SQLCODE = +100                                          
018300                 SET EOF-TMRITST-CURSOR TO TRUE                           
018400             WHEN SQLWARN0 NOT = SPACES                                   
018500             WHEN SQLCODE  NOT = ZERO                                     
018600                  PERFORM 9999-SQL-ERROR                                  
018700         END-EVALUATE                                                     
018800     END-PERFORM.                                                         
018900                                                                          
019000     IF SQLCODE = ZERO                                                    
019010         MOVE MRITST-ITEM-NBR        TO MRISFE-ITEM-NBR                   
019020         MOVE MRITST-STR-NBR         TO MRISFE-STR-NBR                    
019040         MOVE PC-0                   TO MRISFE-LFWM-QTY                   
019050         MOVE PC-0                   TO MRISFE-FWM-QTY                    
019100         PERFORM 5000-INSERT-TO-TMRISFE                                   
019110         VARYING PV-SUB2 FROM +1 BY +1                                    
019120         UNTIL PV-SUB2 > +4.                                              
019200                                                                          
019210     PERFORM 4100-COMMIT-PROCESSING.                                      
019220                                                                          
019230                                                                          
019240******************************************************************        
019250*  ==> PROCESSES:                                                *        
019260*    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
019270*    - COMMIT THIS UNIT OF WORK.                                 *        
019280******************************************************************        
019290 4100-COMMIT-PROCESSING.                                                  
019291                                                                          
019292     PERFORM 4300-SET-CURRENT-TIMESTAMP.                                  
019293                                                                          
019294     IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
019300         PERFORM 5100-COMMIT                                              
019301         PERFORM 4200-GET-COMMIT-TIMESTAMP                                
019302     END-IF.                                                              
019303                                                                          
019304                                                                          
019305******************************************************************        
019306*  ==> PROCESSES:                                                *        
019307*    - GET THE COMMIT TIMESTAMP                                  *        
019309******************************************************************        
019310 4200-GET-COMMIT-TIMESTAMP.                                               
019311                                                                          
019312     EXEC SQL                                                             
019313       SET :PV-COMMIT-TIMESTAMP =                                         
019314              (CURRENT TIMESTAMP + :PC-1-SEC SECONDS)                     
019314*             (CURRENT TIMESTAMP + :PC-2 SECONDS)                         
019314*             (CURRENT TIMESTAMP + :PC-5 SECONDS)                         
019314*             (CURRENT TIMESTAMP + :PC-10 SECONDS)                        
019318     END-EXEC.                                                            
019319                                                                          
019320     IF SQLCODE = 0                                                       
019321         CONTINUE                                                         
019322     ELSE                                                                 
019323         MOVE '4200-GET-COMMIT-TIMESTAMP       '                          
019324                             TO PV-PARAGRAPH-NAME                         
019325         MOVE 'ERROR ON SELECT OF TIMESTAMP  '                            
019326                             TO PV-DB2-OPER-ATTEMPTED                     
019327         PERFORM 9999-SQL-ERROR                                           
019328     END-IF.                                                              
019329                                                                          
019330******************************************************************        
019331*  ==> PROCESSES:                                                *        
019332*    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
019333******************************************************************        
019334 4300-SET-CURRENT-TIMESTAMP.                                              
019335                                                                          
019336     EXEC SQL                                                             
019337          SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
019338     END-EXEC.                                                            
019339                                                                          
019340     IF SQLCODE = 0                                                       
019341         CONTINUE                                                         
019342     ELSE                                                                 
019343         MOVE '4300-SET-CURRENT-TIMESTAMP      '                          
019344                             TO PV-PARAGRAPH-NAME                         
019345         MOVE 'ERROR ON SET OF TIMESTAMP     '                            
019346                             TO PV-DB2-OPER-ATTEMPTED                     
019347         PERFORM 9999-SQL-ERROR                                           
019348     END-IF.                                                              
019349                                                                          
019350                                                                          
019360******************************************************************        
019400*   INSERT A ROW TO THE TMRISFE                                           
019500******************************************************************        
019600                                                                          
019700 5000-INSERT-TO-TMRISFE.                                                  
019800                                                                          
019900     MOVE '*5000-INSERT-TO-TMRISFE'  TO PV-PARAGRAPH-NAME.                
020000                                                                          
020210     MOVE PV-SUB2                    TO MRISFE-SEQ-NBR.                   
020300                                                                          
020400     EXEC SQL                                                             
020500         INSERT INTO TMRISFE                                              
020600                    (ITEM_NBR,                                            
020700                     STR_NBR,                                             
020800                     SEQ_NBR,                                             
020900                     LFWM_QTY,                                            
021000                     FWM_QTY)                                             
021100             VALUES (:MRISFE-ITEM-NBR,                                    
021200                     :MRISFE-STR-NBR,                                     
021300                     :MRISFE-SEQ-NBR,                                     
021400                     :MRISFE-LFWM-QTY,                                    
021500                     :MRISFE-FWM-QTY)                                     
021600     END-EXEC.                                                            
021700                                                                          
021800     EVALUATE SQLCODE                                                     
021900       WHEN +0                                                            
022000             ADD PC-1 TO PV-INSERT-CNT                                    
022100       WHEN -803                                                          
022200             DISPLAY 'INSERT DUP KEY - '                                  
022300                     'ITEM_NBR: ' MRISFE-ITEM-NBR                         
022400                     ' STR_NBR: ' MRISFE-STR-NBR                          
022410             PERFORM 9999-SQL-ERROR                                       
022500       WHEN OTHER                                                         
022600             MOVE 'INSERT TMRISFE' TO                                     
022700                  PV-DB2-OPER-ATTEMPTED                                   
022800             PERFORM 9999-SQL-ERROR                                       
022900     END-EVALUATE.                                                        
023000                                                                          
023001                                                                          
023010 5100-COMMIT.                                                             
023020                                                                          
023030     EXEC SQL                                                             
023040         COMMIT                                                           
023050     END-EXEC.                                                            
023106                                                                          
023107     IF SQLCODE = 0                                                       
023108         ADD PC-1            TO CR-COMMIT-CNT                             
023110     ELSE                                                                 
023111         MOVE '5100-COMMIT                     '                          
023112                             TO PV-PARAGRAPH-NAME                         
023113         MOVE 'ERROR ON COMMIT               '                            
023114                             TO PV-DB2-OPER-ATTEMPTED                     
023115         PERFORM 9999-SQL-ERROR                                           
023116     END-IF.                                                              
023120                                                                          
023230******************************************************************        
023300*   CLOSE FILES, DISPLAY COUNTERS                                         
023400******************************************************************        
023500 9000-END-OF-JOB.                                                         
023600                                                                          
023700     PERFORM 9100-CLOSE-TMRITST-CURSOR.                                   
023800                                                                          
023900     MOVE PV-INPUT-REC-CNT TO PV-INPUT-REC-CNT-DISP.                      
024000     MOVE PV-INSERT-CNT    TO PV-INSERT-CNT-DISP.                         
024100                                                                          
024200     DISPLAY ' '.                                                         
024300     DISPLAY '                 TOTAL INPUT RECS:  '                       
024400              PV-INPUT-REC-CNT-DISP.                                      
024500     DISPLAY 'NBR ROWS INSERTED TO FSTT_GRD_AVG:  '                       
024600              PV-INSERT-CNT-DISP.                                         
024700                                                                          
024810                                                                          
024900*****************************************************************         
025000 9100-CLOSE-TMRITST-CURSOR.                                               
025100     MOVE '9100-CLOSE-TMRITST-CURSOR'                                     
025200                                 TO PV-PARAGRAPH-NAME.                    
025300     MOVE 'CLOSE CURSOR 1'       TO PV-DB2-OPER-ATTEMPTED.                
025310     DISPLAY 'MRKUP133 CLOSE CURSOR'.                                     
025400                                                                          
025500     EXEC SQL                                                             
025600         CLOSE TMRITST_CSR                                                
025700     END-EXEC.                                                            
025800                                                                          
025900     EVALUATE TRUE                                                        
026000         WHEN SQLCODE = ZERO                                              
026100             CONTINUE                                                     
026200         WHEN OTHER                                                       
026300             DISPLAY '9100-CLOSE-TMRITST-CURSOR'                          
026400             PERFORM 9999-SQL-ERROR                                       
026500       END-EVALUATE.                                                      
026600                                                                          
026700                                                                          
027800                                                                          
028000******************************************************************        
028100*   SQL ABEND ROUTINE                                                     
028200******************************************************************        
028210 9999-SQL-ERROR.                                                          
028300                                                                          
028400     DISPLAY '**  ABEND     **'.                                          
028500     DISPLAY '**  PROGRAM   **  =  MRKUP133'.                             
028600     DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
028700     DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
028800*                                                                         
028900*    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
029000*    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
029100*                                                                         
029200     COPY DPPD004.                                                        
029300*                                                                         
029400                                                                          
029900     MOVE +4013 TO ABEND-CODE.                                            
030000     CALL 'ILBOABN0'  USING ABEND-CODE.                                   
030100                                                                          
