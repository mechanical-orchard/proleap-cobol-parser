       ID  DIVISION.                                                            
W34517 PROGRAM-ID.         VAKRP109                                             
       AUTHOR.             JULIE SEIDLER.                                       
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       6/00.                                                
       DATE-COMPILED.                                                           
                                                                                
W34517****IMPORTANT*****************************************************        
W34517* THIS IS A CLONE OF THE INKRP109 CREATED FOR THE VA PROJECT     *        
W34517* THE MAJOR CHANGES ARE .....                                    *        
W34517******************************************************************        
      ******************************************************************        
      *   ADJUSTED PHYSICAL INVENTORY SUMMARY BY UNITS COUNT REPORT             
      *                                                                         
      *   WHEN REPORTING PHYSICAL INVENTORY ON UNITS:                           
      *     .. GWP WILL BE IGNORED FROM TSUMINV, TSTKRMW, TADJADD               
      *        (NOTE: TADJDTL MAINTAINS "ADJUSTED RTL AMT" AND                  
      *               CHANGES TO GWP ITEMS CANNOT BE IDENTIFIED)                
      *     .. 000/999 SEQ WILL BE IGNORED FROM TSUMINV, TSTKRMW,               
      *               TADJADD, AND TADJDTL                                      
      ******************************************************************        
      *                                                                         
      * WARNING   **   WARNING   **   WARNING   **   WARNING   **               
      * WARNING   **   WARNING   **   WARNING   **   WARNING   **               
      *                                                                         
      *       SINCE GWP CANNOT BE IDENTIFIED ON THE TADJDTL TABLE THIS          
      *       REPORT WILL BE A "VERY CLOSE" REFLECTION OF THE PHYSICAL          
      *       INVENTORY UNIT COUNTS.                                            
      *                                                                         
      * WARNING   **   WARNING   **   WARNING   **   WARNING   **               
      * WARNING   **   WARNING   **   WARNING   **   WARNING   **               
      *                                                                         
RWJ00 *PROJECT HISTORY:                                                         
RWJ00 *                                                                         
RWJ00 *    WR# 99999  11/27/2000   ROD JANSSEN                                  
RWJ00 *    PROVIDE A STORE TOTAL PAGE AT THE END OF THE REPORT.                 
RWJ00 *                                                                         
W21732*    WR# 21732  02/06/2002   SUDHARSHAN ADDURI,IGSI                       
W21732*    INCREASED LENGTH OF DEPT IN THE REPORT TO 4 BYTES. ALSO              
W21732*    REMOVED THE LOGIC FOR FETCHING RECORDS FROM DIFFERENT TABLES         
W21732*    USING CURSORS. THIS LOGIC IS IMPLEMENTED IN THE NEW INKUT109         
W21732*    PROGRAM. NEW CHANGES ARE DONE TO READ THE FILE THAT GENERATED        
W21732*    FROM INKUT109 PROGRAM AND TO CREATE THE REPORT.                      
RLB312*                                                                         
RLB312*    FIX ERRORS FROM OFFSHORE CHANGES.                                    
RLB312*    MOVE CHECK FOR NON DUMMY SKU AND DS AND CS TYPES TO TOP              
RLB312*    OF B300 PARAGRAPH, ALSO, WHEN THE STORE CHANGES, PROCESS             
RLB312*    THE RECORD.                                                          
RLB312*    ALSO, MODIFIED LOGIC IN PARAGRAPH B300 TO MAKE THE IF THEN           
RLB312*    EASIER TO UNDERSTAND.....I DON'T THINK IT WAS WORKING                
RLB312*    CORRECTLY.                                                           
RLB312*                                                                         
21732 *    WR# 21732  04/28/2003   ROD JANSSEN                                  
21732 *  DAILY DETAIL STOCKROOM WITHDRAWAL & STORE CORRECTIONS REPORT           
21732 *  CREATION.                                                              
W26600*                                                                         
W26600*    WR# 26600  04/28/2004   PAUL KEBER                                   
W26600*  STORE NUMBER EXPANSION PROJECT CHANGES TO REPLACE TSTR000 WITH         
W26600*  XMT_LOC AND TO ALLOW A 5 DIGIT STORE.                                  
W27710*                                                                         
W27710*    WR# 27710  01/28/2005   ROD JANSSEN                                  
W27710*  REMOVE CONTENTION ISSUES WITH OTHER INVENTORY JOBS.                    
W27710*                                                                         
W28543* W28543  05-02-2005 ROD JANSSEN MISSED MARKDOWN/MISSED MARKUP            
W28543*                                ENHANCEMENTS                             
      ******************************************************************        
W34517* W34517  10-14-2008 IN-VA INVENTORY STREAM SPLIT                         
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
W21732     SELECT PHY-INVENTORY-SUMMARY-FILE                                    
W21732         ASSIGN TO INP01.                                                 
W21732                                                                          
           SELECT ADJ-PHY-INVENTORY-REPORT                                      
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
W21732                                                                          
W21732 FD  PHY-INVENTORY-SUMMARY-FILE                                           
W21732     RECORDING MODE IS F                                          INKUT115
W21732     BLOCK CONTAINS 0 CHARACTERS                                  INKUT115
W21732     LABEL RECORDS ARE STANDARD.                                  INKUT115
W21732                                                                  INKUT115
21732  01  PHY-INVENTORY-SUMMARY-REC     PIC X(100).                       CL*12
W21732                                                                  INKUT115
       FD  ADJ-PHY-INVENTORY-REPORT                                             
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE.                                                         
           05  REPORT-LINE-CC    PIC X(001).                                    
           05  REPORT-LINE-DATA  PIC X(132).                                    
           05  FILLER REDEFINES  REPORT-LINE-DATA.                              
               10  FILLER        PIC X(131).                                    
               10  REPORT-HEADER-FONT-INDICATOR                                 
                                 PIC X(001).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PC-PRINT-CONTROL.                                                    
           05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO.                  
           05  PC-LINE-COUNT           PIC S9(03)  VALUE ZERO COMP-3.           
           05  PC-REPORT-CC            PIC X(01)  VALUE SPACES.                 
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W21732     05  PV-PREV-DEPT            PIC X(4).                                
W26600     05  PV-PREV-STORE           PIC X(5)     VALUE SPACES.               
W26600     05  PV-RPT-STORE            PIC X(5)     VALUE SPACES.               
W26600     05  PV-STORE-NUM            PIC 9(5)     VALUE ZERO.                 
           05  PV-PARA                 PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
           05  PV-DEFAULT-DATE         PIC X(10)    VALUE '9999-09-09'.         
           05  PV-TEST-DATE            PIC X(10)    VALUE '2000-06-06'.         
           05  PC-MAX-RETRIES          PIC S9(4)                                
                                           COMP SYNC VALUE +2.                  
           05  PV-RETRY-COUNT          PIC S9(4) COMP SYNC VALUE +0.            
W26600     05  MAX-STORES              PIC S9(4) COMP SYNC VALUE +9999.         
W26600     05  PV-STR-NDX              PIC S9(4) COMP SYNC VALUE +0.            
           05  PV-DEPT-X               PIC X(03).                               
           05  PV-DEPT-NUM REDEFINES PV-DEPT-X                                  
                                       PIC 9(03).                               
           05  PV-DEPT-SUB             PIC 9(04)    VALUE ZEROES.               
           05  PV-SYSTEM-DATE.                                                  
               10  PV-SYS-YY           PIC 9(2)    VALUE ZEROES.                
               10  PV-SYS-MM           PIC 9(2)    VALUE ZEROES.                
               10  PV-SYS-DD           PIC 9(2)    VALUE ZEROES.                
           05  PV-DB2-DATE.                                                     
               10  PV-DB2-CC           PIC 9(2)    VALUE ZEROES.                
               10  PV-DB2-YY           PIC 9(2)    VALUE ZEROES.                
               10  FILLER              PIC X(1)    VALUE '-'.                   
               10  PV-DB2-MM           PIC 9(2)    VALUE ZEROES.                
               10  FILLER              PIC X(1)    VALUE '-'.                   
               10  PV-DB2-DD           PIC 9(2)    VALUE ZEROES.                
W34517     05  PV-JOB-INFO.                                                     
W34517         10  FILLER                  PIC X(01)       VALUE '('.           
W34517         10  PV-JOB-NAME             PIC X(08)       VALUE SPACES.        
W34517         10  FILLER                  PIC X(01)       VALUE ')'.           
                                                                                
           05  PV-COMPUTED-FIELDS.                                              
               10  PV-TSUMINV-AMT                PIC S9(15)    VALUE +0         
                                                 COMP-3.                        
               10  PV-TSTKRMW-AMT                PIC S9(15)    VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-AUDIT-AMT          PIC S9(15)    VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-STORE-AMT          PIC S9(15)    VALUE +0         
                                                 COMP-3.                        
               10  PV-TADJDTL-SALE-ADJMT-AMT     PIC S9(15)    VALUE +0         
                                                 COMP-3.                        
W21732     05  PV-TOT-TSUMINV-AMT            PIC S9(15)    VALUE +0             
                                                 COMP-3.                        
W21732     05  PV-TOT-TSTKRMW-AMT            PIC S9(15)    VALUE +0             
                                                 COMP-3.                        
W21732     05  PV-TOT-TADJDTL-AUDIT-AMT      PIC S9(15)    VALUE +0             
                                                 COMP-3.                        
W21732     05  PV-TOT-TADJDTL-SALE-ADJMT-AMT PIC S9(15)    VALUE +0             
                                                 COMP-3.                        
W21732     05  PV-TOT-TADJDTL-STORE-AMT      PIC S9(15)    VALUE +0             
                                                 COMP-3.                        
W21732     05  PV-TOT-ADJUSTED-PHYSICAL      PIC S9(15)    VALUE +0             
W21732                                           COMP-3.                        
W21732     05  PV-ADJUSTED-PHYSICAL          PIC S9(15)    VALUE +0             
W21732                                           COMP-3.                        
                                                                                
       01  SWITCHES.                                                            
W21732     05  PS-END-OF-PHY-INV-IND      PIC X       VALUE 'N'.                
W21732         88  PS-END-OF-PHY-INV-FILE          VALUE 'Y'.                   
                                                                                
       01  TB-DEPT-TABLE.                                                       
W21732        10  TB-DEPT-NBR                   PIC X(04) VALUE SPACES.         
              10  TB-TSUMINV-AMT                PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TSTKRMW-AMT                PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TADJDTL-AUDIT-AMT          PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TADJDTL-STORE-AMT          PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TADJDTL-SALE-ADJMT-AMT     PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-ADJUSTED-PHYSICAL          PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-TSUMINV-AMT            PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-TSTKRMW-AMT            PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-TADJDTL-AUDIT-AMT      PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-TADJDTL-SALE-ADJMT-AMT PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-TADJDTL-STORE-AMT      PIC S9(15)    VALUE +0          
                                                COMP-3.                         
              10  TB-TOT-ADJUSTED-PHYSICAL      PIC S9(15)    VALUE +0          
                                                COMP-3.                         
                                                                                
RWJ00  01  WS-STORE-TOTAL-TABLE.                                                
W26600     05 WS-STR-ELEMENTS                   OCCURS 9999 TIMES.              
W26600        10  WS-STR-NBR                    PIC 9(05)  VALUE ZERO.          
RWJ00         10  WS-TOT-TSUMINV-AMT            PIC S9(15) VALUE ZERO           
  |                                             COMP-3.                         
  |           10  WS-TOT-TSTKRMW-AMT            PIC S9(15) VALUE ZERO           
  |                                             COMP-3.                         
  |           10  WS-TOT-TADJDTL-STORE-AMT      PIC S9(15) VALUE ZERO           
  |                                             COMP-3.                         
  |           10  WS-TOT-TADJDTL-AUDIT-AMT      PIC S9(15) VALUE ZERO           
  |                                             COMP-3.                         
  |           10  WS-TOT-TADJDTL-SALE-ADJMT-AMT PIC S9(15) VALUE ZERO           
  |                                             COMP-3.                         
RWJ00                                                                           
       01  CPU-DATE.                                                            
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.          
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.          
                                                                                
       01  CPU-TIME.                                                            
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.          
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.          
           05  FILLER                      PIC X(02)     VALUE SPACES.          
       01  H1-HEADING.                                                          
           05  FILLER              PIC X(44)   VALUE SPACES.                    
           05  FILLER              PIC X(97)   VALUE                            
              'SUMMARIZED ADJUSTED PHYSICAL INVENTORY REPORT - UNITS'.          
                                                                                
       01  H1A-HEADING.                                                         
           05  FILLER              PIC X(44)   VALUE SPACES.                    
           05  FILLER              PIC X(97)   VALUE                            
              '          (OMITTING GWP AND 000/999 ITEMS)           '.          
                                                                                
       01  H1B-HEADING.                                                         
W26600     05  FILLER              PIC X(58)   VALUE SPACES.                    
           05  FILLER              PIC X(06)   VALUE 'STORE '.                  
           05  FILLER              PIC X(01)   VALUE SPACES.                    
W26600     05  H1B-STORE           PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
W26600     05  H1B-STORE-NAME      PIC X(40)   VALUE SPACES.                    
                                                                                
       01  H2-HEADING.                                                          
           05  FILLER              PIC X(01)   VALUE   '-'.                     
           05  FILLER              PIC X(53)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
               'DEPT                         PHYSICAL        STOCKROO'.         
           05  FILLER              PIC X(53)   VALUE                            
               'M           STORE              AUDIT               SA'.         
           05  FILLER              PIC X(26)   VALUE                            
               'LE              ADJUSTED  '.                                    
                                                                                
RWJ00  01  H2B-HEADING.                                                         
  |        05  FILLER              PIC X(01)   VALUE   '-'.                     
  |        05  FILLER              PIC X(53)   VALUE                            
  |   **        ----5----0----5----0----5----0----5----0----5----0----5         
  |            'STORE                        PHYSICAL        STOCKROO'.         
  |        05  FILLER              PIC X(53)   VALUE                            
  |            'M           STORE              AUDIT               SA'.         
  |        05  FILLER              PIC X(26)   VALUE                            
  |            'LE              ADJUSTED  '.                                    
RWJ00                                                                           
       01  H3-HEADING.                                                          
           05  FILLER              PIC X(01)   VALUE   ' '.                     
           05  FILLER              PIC X(53)   VALUE                            
      **        ----5----0----5----0----5----0----5----0----5----0----5         
               '                             INVENTORY       WITHDRAW'.         
           05  FILLER              PIC X(53)   VALUE                            
               'L        CORRECTIONS        CORRECTIONS        ADJUST'.         
           05  FILLER              PIC X(26)   VALUE                            
               'MENTS           PHYSICAL  '.                                    
           EJECT                                                                
                                                                                
       01  C1-COMMENT.                                                          
W26600     05  C1-STORE-NBR        PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(01)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE                            
               'STR TTL **'.                                                    
                                                                                
       01  D1-DETAIL-LINE.                                                      
           05  D1-CC                    PIC X(01).                              
               88  D1-SINGLE-SPACE                       VALUE ' '.             
               88  D1-DOUBLE-SPACE                       VALUE '0'.             
           05  D1-DETAIL-DATA.                                                  
W21732         10  D1-DEPT-NBR          PIC X(04)        VALUE SPACES.          
               10  FILLER               PIC X(01)        VALUE SPACES.          
W26600         10  D1-COMMENT           PIC X(16)        VALUE SPACES.          
W26600         10  FILLER               PIC X(03)        VALUE SPACES.          
               10  D1-PHYSICAL-INVEN    PIC ZZ,ZZZ,ZZZ,ZZ9-.                    
               10  FILLER               PIC X(05)        VALUE SPACES.          
               10  D1-STOCKROOM-WDRAWL  PIC ZZ,ZZZ,ZZ9-.                        
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  D1-STORE-CORRECTIONS PIC ZZ,ZZZ,ZZZ,ZZ9-.                    
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  D1-AUDIT-CORRECTIONS PIC ZZ,ZZZ,ZZZ,ZZ9-.                    
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  D1-SALE-ADJUST       PIC ZZ,ZZZ,ZZZ,ZZ9-.                    
               10  FILLER               PIC X(04)        VALUE SPACES.          
               10  D1-ADJUSTED-PHYSICAL PIC ZZ,ZZZ,ZZZ,ZZ9-.                    
                                                                                
      * COPY FOR STANDARD HEADING                                               
           COPY DPWS133O.                                                       
                                                                                
W34517******************************************************************        
W34517*  HEADER LINE                                                            
W34517******************************************************************        
W34517*01  DP085-GET-CURR-JOB-INFO-PGM.                                         
W34517*01  DP085-GET-CURR-JOB-INFO-PARMS.                                       
W34517     COPY DPWS085.                                                        
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
W21732*****************************************************************         
W21732*  COPYBOOK FOR INPUT                                                     
W21732*****************************************************************         
W21732                                                                          
21732      COPY INRD109.                                                        
W21732                                                                          
      *****************************************************************         
W26600* DB2 XMT LOCATION TABLE DCLGEN                                           
      *****************************************************************         
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
      *****************************************************************         
      * DB2 MERCHANDISE DEPARTMENT                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
           EJECT                                                                
                                                                                
           EJECT                                                                
       LINKAGE SECTION.                                                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
W21732     PERFORM B200-PROCESS-PHY-INV-FILE                                    
W21732          UNTIL PS-END-OF-PHY-INV-FILE.                                   
                                                                                
           PERFORM B600-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
           EJECT                                                                
       B100-HOUSEKEEPING.                                                       
           MOVE '*B100-HOUSEKEEPING.              *' TO PV-PARA                 
                                                                                
W21732     OPEN INPUT  PHY-INVENTORY-SUMMARY-FILE                               
W21732          OUTPUT ADJ-PHY-INVENTORY-REPORT.                                
                                                                                
W34517*  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
W34517     CALL DP085-GET-CURR-JOB-INFO-PGM                                     
W34517         USING DP085-GET-CURR-JOB-INFO-PARMS.                             
W34517     MOVE DP085-JOB-NAME     TO PV-JOB-NAME.                              
W34517                                                                          
W34517     MOVE PV-JOB-INFO                                                     
W34517         TO DP133O-STANDRD-HEADER-133-COLS(20:10).                        
                                                                                
           ACCEPT CPU-DATE FROM DATE.                                           
           MOVE CPU-YEAR   TO DP133O-RUN-YEAR.                                  
           MOVE CPU-MONTH  TO DP133O-RUN-MONTH.                                 
           MOVE CPU-DAY    TO DP133O-RUN-DAY.                                   
                                                                                
           ACCEPT CPU-TIME FROM TIME.                                           
           MOVE CPU-HOUR   TO DP133O-RUN-HOUR.                                  
           MOVE CPU-MINUTE TO DP133O-RUN-MINUTE.                                
W34517     MOVE 'VAKRP109' TO DP133O-PROGRAM-NAME.                              
                                                                                
           MOVE +0         TO PC-PAGE-COUNT.                                    
                                                                                
           INITIALIZE TB-DEPT-TABLE.                                            
RWJ00      INITIALIZE WS-STORE-TOTAL-TABLE.                                     
                                                                                
W21732     PERFORM B150-READ-PHY-INV-FILE.                                      
W21732     IF NOT PS-END-OF-PHY-INV-FILE                                        
W21732        MOVE IN109-STORE-NBR TO PV-PREV-STORE                             
W21732        MOVE IN109-DEPT      TO PV-PREV-DEPT                              
W26600        PERFORM Z100-READ-XMTLOC                                          
W21732        PERFORM Z800-PRINT-HEADINGS                                       
W21732     END-IF.                                                              
W21732                                                                          
W21732                                                                          
W21732 B150-READ-PHY-INV-FILE.                                                  
W21732                                                                          
W21732     READ PHY-INVENTORY-SUMMARY-FILE                                      
W21732            INTO IN109-PHY-IN-SUMMARY-RECORD                              
W21732            AT END                                                        
W21732                 SET PS-END-OF-PHY-INV-FILE TO TRUE                       
W21732     END-READ.                                                            
W21732                                                                          
W21732 B200-PROCESS-PHY-INV-FILE.                                               
W21732                                                                          
W21732     IF IN109-STORE-NBR EQUAL TO PV-PREV-STORE                            
RLB312*        STORE IS THE SAME                                                
W21732         IF IN109-DEPT EQUAL TO PV-PREV-DEPT                              
RLB312*           DEPT IS THE SAME                                              
RLB312*           IF IN109-SKU (6:3) NOT EQUAL TO '000' OR '999' AND            
RLB312*              IN109-STORE-TYPE = 'DS' OR 'CS'                            
W21732               PERFORM B300-PROCESS-RECORD                                
RLB312*           END-IF                                                        
W21732         ELSE                                                             
RLB312*           DEPT HAS CHANGED                                              
W21732            PERFORM B400-PRINT-DETAIL-LINE                                
W21732            INITIALIZE TB-DEPT-TABLE                                      
W21732            PERFORM B300-PROCESS-RECORD                                   
W21732         END-IF                                                           
W21732     ELSE                                                                 
RLB312*       STORE HAS CHANGED                                                 
W21732        IF (PV-TSUMINV-AMT       NOT = +0) OR                             
W21732           (PV-TSTKRMW-AMT       NOT = +0) OR                             
W21732           (PV-TADJDTL-AUDIT-AMT NOT = +0) OR                             
W21732           (PV-TADJDTL-STORE-AMT NOT = +0)                                
W21732            PERFORM B400-PRINT-DETAIL-LINE                                
W21732            PERFORM B410-PRINT-STORE-TOTAL                                
W26600            PERFORM Z100-READ-XMTLOC                                      
W21732            PERFORM Z800-PRINT-HEADINGS                                   
W21732            INITIALIZE TB-DEPT-TABLE                                      
W21732            INITIALIZE PV-COMPUTED-FIELDS                                 
W21732        END-IF                                                            
RLB312        PERFORM B300-PROCESS-RECORD                                       
W21732     END-IF.                                                              
W21732                                                                          
W21732     MOVE IN109-DEPT       TO PV-PREV-DEPT.                               
W21732     MOVE IN109-STORE-NBR  TO PV-PREV-STORE.                              
W21732                                                                          
W21732     PERFORM B150-READ-PHY-INV-FILE.                                      
W21732                                                                          
W21732 B300-PROCESS-RECORD.                                                     
W21732                                                                          
RLB312*    DON NOT INCLUDE DUMMY SKUS AND ONLY INCLUDE DS AND CS                
RLB312*    LOCATIONS                                                            
RLB312     IF (IN109-SKU (6:3) > '000'  AND                                     
RLB312         IN109-SKU (6:3) < '999') AND                                     
RLB312         IN109-STORE-TYPE = 'DS'  OR 'CS'                                 
RLB312        EVALUATE TRUE                                                     
RBL312           WHEN IN109-TABLE-NAME = 'SUMINV'                               
RLB312*              DO NOT PROCESS GWP                                         
W21732               IF IN109-UNIT-PR-AMT > .02                                 
W21732                  ADD IN109-INV-QTY     TO PV-TSUMINV-AMT                 
W21732                                           PV-TOT-TSUMINV-AMT             
W21732                                           TB-TSUMINV-AMT                 
W21732                                           TB-TOT-TSUMINV-AMT             
W21732               END-IF                                                     
RBL312           WHEN IN109-TABLE-NAME = 'STKRMW'                               
RLB312*              DO NOT PROCESS GWP                                         
W21732               IF IN109-UNIT-PR-AMT > .02                                 
W21732                  ADD IN109-INV-QTY TO PV-TSTKRMW-AMT                     
W21732                                       PV-TOT-TSTKRMW-AMT                 
W21732                                       TB-TSTKRMW-AMT                     
W21732                                       TB-TOT-TSTKRMW-AMT                 
W21732               END-IF                                                     
RBL312           WHEN IN109-TABLE-NAME = 'ADJDTL'                               
RLB312*              CORRECTIONS                                                
W21732               IF IN109-ADJUST-TYPE-CODE = 'CO'                           
W21732                  ADD IN109-INV-QTY TO  PV-TADJDTL-STORE-AMT              
W21732                                        PV-TOT-TADJDTL-STORE-AMT          
W21732                                        TB-TADJDTL-STORE-AMT              
W21732                                        TB-TOT-TADJDTL-STORE-AMT          
RLB312               END-IF                                                     
RLB312*              SALES ADJUSTMENTS                                          
W21732               IF IN109-ADJUST-TYPE-CODE = 'SA'                           
W21732                  ADD IN109-INV-QTY TO                                    
W21732                                 PV-TADJDTL-SALE-ADJMT-AMT                
W21732                                 PV-TOT-TADJDTL-SALE-ADJMT-AMT            
W21732                                 TB-TADJDTL-SALE-ADJMT-AMT                
W21732                                 TB-TOT-TADJDTL-SALE-ADJMT-AMT            
RLB312*              ELSE                                                       
RLB312               END-IF                                                     
RLB312*              ADJUSTMENTS TO EITHER SUMINV OR ADJ ADDS                   
W28543               IF IN109-ADJUST-TYPE-CODE = 'AD' OR 'SI' OR 'MM'           
W21732                  ADD IN109-INV-QTY TO  PV-TADJDTL-AUDIT-AMT              
W21732                                        PV-TOT-TADJDTL-AUDIT-AMT          
W21732                                        TB-TADJDTL-AUDIT-AMT              
W21732                                        TB-TOT-TADJDTL-AUDIT-AMT          
W21732               END-IF                                                     
RBL312           WHEN IN109-TABLE-NAME = 'ADJADD'                               
RLB312*              MASS ADDS OR ADJUSTMENT ADDS                               
W21732               ADD IN109-INV-QTY TO  PV-TADJDTL-AUDIT-AMT                 
W21732                                     PV-TOT-TADJDTL-AUDIT-AMT             
W21732                                     TB-TADJDTL-AUDIT-AMT                 
W21732                                     TB-TOT-TADJDTL-AUDIT-AMT             
RLB312           WHEN OTHER                                                     
RLB312               CONTINUE                                                   
RLB312        END-EVALUATE                                                      
RLB312     END-IF.                                                              
RLB312                                                                          
                                                                                
                                                                                
       B400-PRINT-DETAIL-LINE.                                                  
                                                                                
           MOVE SPACES                    TO D1-DETAIL-DATA.                    
                                                                                
W21732         MOVE PV-PREV-DEPT                                                
                    TO D1-DEPT-NBR                                              
W21732         MOVE TB-TSUMINV-AMT                                              
                    TO D1-PHYSICAL-INVEN                                        
W21732         MOVE TB-TSTKRMW-AMT                                              
                    TO D1-STOCKROOM-WDRAWL                                      
W21732         MOVE TB-TADJDTL-STORE-AMT                                        
                    TO D1-STORE-CORRECTIONS                                     
W21732         MOVE TB-TADJDTL-AUDIT-AMT                                        
                    TO D1-AUDIT-CORRECTIONS                                     
W21732         MOVE TB-TADJDTL-SALE-ADJMT-AMT                                   
                    TO D1-SALE-ADJUST                                           
                                                                                
               COMPUTE PV-ADJUSTED-PHYSICAL =                                   
W21732                 TB-TSUMINV-AMT          -                                
W21732                 TB-TSTKRMW-AMT          +                                
W21732                 TB-TADJDTL-STORE-AMT    +                                
W21732                 TB-TADJDTL-AUDIT-AMT    +                                
W21732                 TB-TADJDTL-SALE-ADJMT-AMT                                
                                                                                
                                                                                
               IF PC-LINE-COUNT > +59                                           
                   PERFORM Z800-PRINT-HEADINGS                                  
               END-IF                                                           
                                                                                
               MOVE PV-ADJUSTED-PHYSICAL     TO D1-ADJUSTED-PHYSICAL            
               MOVE D1-DETAIL-LINE           TO REPORT-LINE                     
               PERFORM Z700-WRITE-REPORT-LINE                                   
                                                                                
W21732         SET  D1-SINGLE-SPACE          TO TRUE.                           
                                                                                
       B410-PRINT-STORE-TOTAL.                                                  
                                                                                
           MOVE SPACES                    TO D1-DETAIL-DATA.                    
RWJ00      SET  D1-DOUBLE-SPACE           TO TRUE.                              
                                                                                
W26600     MOVE PV-RPT-STORE              TO C1-STORE-NBR                       
           MOVE C1-COMMENT                TO D1-COMMENT                         
                                                                                
           MOVE PV-TSUMINV-AMT            TO D1-PHYSICAL-INVEN                  
           MOVE PV-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL                
           MOVE PV-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS               
           MOVE PV-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS               
           MOVE PV-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST                     
                                                                                
           COMPUTE PV-ADJUSTED-PHYSICAL =                                       
                   PV-TSUMINV-AMT         -                                     
                   PV-TSTKRMW-AMT         +                                     
                   PV-TADJDTL-STORE-AMT   +                                     
                   PV-TADJDTL-AUDIT-AMT   +                                     
                   PV-TADJDTL-SALE-ADJMT-AMT.                                   
                                                                                
           IF PC-LINE-COUNT > +59                                               
               PERFORM Z800-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE PV-ADJUSTED-PHYSICAL     TO D1-ADJUSTED-PHYSICAL.               
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           SET  D1-SINGLE-SPACE          TO TRUE.                               
                                                                                
RWJ00      MOVE ZEROS                    TO PV-STR-NDX.                         
 |         PERFORM C100-ACCUM-STORE-TOTALS                                      
 |           VARYING PV-STR-NDX FROM 1 BY 1                                     
 |             UNTIL PV-STR-NDX IS GREATER THAN MAX-STORES.                     
RWJ00                                                                           
       B600-EOJ.                                                                
      *** NORMAL END OF JOB. PRINT STORE/DEPT TOTALS....                        
                                                                                
W21732        IF (PV-TSUMINV-AMT       NOT = +0) OR                             
W21732           (PV-TSTKRMW-AMT       NOT = +0) OR                             
W21732           (PV-TADJDTL-AUDIT-AMT NOT = +0) OR                             
W21732           (PV-TADJDTL-STORE-AMT NOT = +0)                                
W21732            PERFORM B400-PRINT-DETAIL-LINE                                
W21732            PERFORM B410-PRINT-STORE-TOTAL                                
W21732            INITIALIZE TB-DEPT-TABLE                                      
W21732            INITIALIZE PV-COMPUTED-FIELDS                                 
W21732        END-IF                                                            
                                                                                
W26600     MOVE 'TOTAL'  TO PV-RPT-STORE.                                       
W26600     MOVE SPACES TO XMT00001-LOC-NM.                                      
           PERFORM Z800-PRINT-HEADINGS.                                         
                                                                                
           MOVE SPACES                        TO D1-DETAIL-LINE.                
           SET  D1-DOUBLE-SPACE               TO TRUE.                          
                                                                                
           MOVE 'TOTAL'                       TO D1-COMMENT.                    
           MOVE PV-TOT-TSUMINV-AMT            TO D1-PHYSICAL-INVEN.             
           MOVE PV-TOT-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL.           
           MOVE PV-TOT-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS.          
           MOVE PV-TOT-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS.          
           MOVE PV-TOT-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST.                
                                                                                
           COMPUTE PV-TOT-ADJUSTED-PHYSICAL =                                   
                   PV-TOT-TSUMINV-AMT       -                                   
                   PV-TOT-TSTKRMW-AMT       +                                   
                   PV-TOT-TADJDTL-STORE-AMT +                                   
                   PV-TOT-TADJDTL-AUDIT-AMT +                                   
                   PV-TOT-TADJDTL-SALE-ADJMT-AMT.                               
                                                                                
           MOVE PV-TOT-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL.               
           MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
RWJ00 *** PRINT A STORE TOTALS SUMMARY PAGE AFTER THE DETAIL TOTALS.            
  |                                                                             
  |        MOVE ZEROS          TO PV-TOT-TSUMINV-AMT.                           
  |        MOVE ZEROS          TO PV-TOT-TSTKRMW-AMT.                           
  |        MOVE ZEROS          TO PV-TOT-TADJDTL-STORE-AMT.                     
  |        MOVE ZEROS          TO PV-TOT-TADJDTL-AUDIT-AMT.                     
  |        MOVE ZEROS          TO PV-TOT-TADJDTL-SALE-ADJMT-AMT.                
  |        MOVE ZEROS          TO PV-TOT-ADJUSTED-PHYSICAL.                     
  |                                                                             
W26600     MOVE 'TOTAL'        TO PV-RPT-STORE.                                 
W26600     MOVE 'SUMMARY'      TO XMT00001-LOC-NM.                              
  |        PERFORM Z805-PRINT-HEADINGS.                                         
  |                                                                             
  |        MOVE SPACES          TO REPORT-LINE.                                 
  |        SET  D1-DOUBLE-SPACE TO TRUE.                                        
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |                                                                             
  |        MOVE ZEROS          TO PV-STR-NDX.                                   
  |        PERFORM C200-PRINT-STORE-TOTALS                                      
  |          VARYING PV-STR-NDX FROM 1 BY 1                                     
  |            UNTIL PV-STR-NDX IS GREATER THAN MAX-STORES.                     
  |                                                                             
  |   *** PRINT A STORE TOTALS SUMMARY PAGE TOTAL AFTER UNLOADING TBL.          
  |                                                                             
  |        IF PC-LINE-COUNT > +59                                               
  |           PERFORM Z805-PRINT-HEADINGS                                       
  |        END-IF.                                                              
  |                                                                             
  |        MOVE SPACES                        TO D1-DETAIL-LINE.                
  |        SET  D1-DOUBLE-SPACE               TO TRUE.                          
  |                                                                             
  |        MOVE 'TOTAL'                       TO D1-COMMENT.                    
  |        MOVE PV-TOT-TSUMINV-AMT            TO D1-PHYSICAL-INVEN.             
  |        MOVE PV-TOT-TSTKRMW-AMT            TO D1-STOCKROOM-WDRAWL.           
  |        MOVE PV-TOT-TADJDTL-STORE-AMT      TO D1-STORE-CORRECTIONS.          
  |        MOVE PV-TOT-TADJDTL-AUDIT-AMT      TO D1-AUDIT-CORRECTIONS.          
  |        MOVE PV-TOT-TADJDTL-SALE-ADJMT-AMT TO D1-SALE-ADJUST.                
  |                                                                             
  |        COMPUTE PV-TOT-ADJUSTED-PHYSICAL =                                   
  |                PV-TOT-TSUMINV-AMT       -                                   
  |                PV-TOT-TSTKRMW-AMT       +                                   
  |                PV-TOT-TADJDTL-STORE-AMT +                                   
  |                PV-TOT-TADJDTL-AUDIT-AMT +                                   
  |                PV-TOT-TADJDTL-SALE-ADJMT-AMT.                               
  |                                                                             
  |        MOVE PV-TOT-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL.               
  |        MOVE D1-DETAIL-LINE           TO REPORT-LINE.                        
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
RWJ00                                                                           
W21732     CLOSE PHY-INVENTORY-SUMMARY-FILE                                     
W21732           ADJ-PHY-INVENTORY-REPORT.                                      
                                                                                
RWJ00  C100-ACCUM-STORE-TOTALS.                                                 
  |                                                                             
  |        IF WS-STR-NBR (PV-STR-NDX) IS EQUAL TO PV-STORE-NUM                  
  |            ADD PV-TSUMINV-AMT                                               
  |              TO WS-TOT-TSUMINV-AMT (PV-STR-NDX)                             
  |            ADD PV-TSTKRMW-AMT                                               
  |              TO WS-TOT-TSTKRMW-AMT (PV-STR-NDX)                             
  |            ADD PV-TADJDTL-STORE-AMT                                         
  |              TO WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX)                       
  |            ADD PV-TADJDTL-AUDIT-AMT                                         
  |              TO WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX)                       
  |            ADD PV-TADJDTL-SALE-ADJMT-AMT                                    
  |              TO WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX)                  
  |            MOVE 9999       TO PV-STR-NDX                                    
  |        ELSE                                                                 
  |        IF WS-STR-NBR (PV-STR-NDX) IS EQUAL TO ZEROS                         
  |            MOVE PV-STORE-NUM                                                
  |              TO WS-STR-NBR (PV-STR-NDX)                                     
  |            MOVE PV-TSUMINV-AMT                                              
  |              TO WS-TOT-TSUMINV-AMT (PV-STR-NDX)                             
  |            MOVE PV-TSTKRMW-AMT                                              
  |              TO WS-TOT-TSTKRMW-AMT (PV-STR-NDX)                             
  |            MOVE PV-TADJDTL-STORE-AMT                                        
  |              TO WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX)                       
  |            MOVE PV-TADJDTL-AUDIT-AMT                                        
  |              TO WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX)                       
  |            MOVE PV-TADJDTL-SALE-ADJMT-AMT                                   
  |              TO WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX)                  
  |            MOVE 9999       TO PV-STR-NDX                                    
  |        END-IF.                                                              
  |                                                                             
  |                                                                             
  |    C200-PRINT-STORE-TOTALS.                                                 
  |                                                                             
  |        IF WS-STR-NBR (PV-STR-NDX) NOT = ZEROS               OR              
  |           WS-TOT-TSUMINV-AMT (PV-STR-NDX) NOT = ZEROS       OR              
  |           WS-TOT-TSTKRMW-AMT (PV-STR-NDX) NOT = ZEROS       OR              
  |           WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX) NOT = ZEROS OR              
  |           WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX) NOT = ZEROS OR              
  |           WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX) NOT = ZEROS            
  |                                                                             
  |            IF PC-LINE-COUNT > +59                                           
  |                PERFORM Z805-PRINT-HEADINGS                                  
  |            END-IF                                                           
  |                                                                             
  |            SET  D1-SINGLE-SPACE          TO TRUE                            
  |                                                                             
  |            MOVE WS-STR-NBR (PV-STR-NDX) TO D1-COMMENT                       
  |            MOVE WS-TOT-TSUMINV-AMT (PV-STR-NDX)                             
  |                                         TO D1-PHYSICAL-INVEN                
  |            MOVE WS-TOT-TSTKRMW-AMT (PV-STR-NDX)                             
  |                                         TO D1-STOCKROOM-WDRAWL              
  |            MOVE WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX)                       
  |                                         TO D1-STORE-CORRECTIONS             
  |            MOVE WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX)                       
  |                                         TO D1-AUDIT-CORRECTIONS             
  |            MOVE WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX)                  
  |                                         TO D1-SALE-ADJUST                   
  |                                                                             
  |            COMPUTE PV-TOT-ADJUSTED-PHYSICAL              =                  
  |                    WS-TOT-TSUMINV-AMT (PV-STR-NDX)       -                  
  |                    WS-TOT-TSTKRMW-AMT (PV-STR-NDX)       +                  
  |                    WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX) +                  
  |                    WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX) +                  
  |                    WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX)               
  |                                                                             
  |            MOVE PV-TOT-ADJUSTED-PHYSICAL TO D1-ADJUSTED-PHYSICAL            
  |            MOVE D1-DETAIL-LINE           TO REPORT-LINE                     
  |            PERFORM Z700-WRITE-REPORT-LINE                                   
  |                                                                             
  |            ADD WS-TOT-TSUMINV-AMT (PV-STR-NDX)                              
  |                            TO PV-TOT-TSUMINV-AMT                            
  |            ADD WS-TOT-TSTKRMW-AMT (PV-STR-NDX)                              
  |                            TO PV-TOT-TSTKRMW-AMT                            
  |            ADD WS-TOT-TADJDTL-STORE-AMT (PV-STR-NDX)                        
  |                            TO PV-TOT-TADJDTL-STORE-AMT                      
  |            ADD WS-TOT-TADJDTL-AUDIT-AMT (PV-STR-NDX)                        
  |                            TO PV-TOT-TADJDTL-AUDIT-AMT                      
  |            ADD WS-TOT-TADJDTL-SALE-ADJMT-AMT (PV-STR-NDX)                   
  |                            TO PV-TOT-TADJDTL-SALE-ADJMT-AMT                 
  |                                                                             
  |        END-IF.                                                              
  |                                                                             
RWJ00                                                                           
W26600 Z100-READ-XMTLOC.                                                        
W21732                                                                          
W26600     MOVE '*Z100-READ-XMTLOC.                *' TO PV-PARA.               
W26600     MOVE IN109-STORE-NBR           TO PV-RPT-STORE                       
W26600                                       PV-STORE-NUM.                      
W26600     MOVE PV-STORE-NUM              TO XMT00001-LOC-NBR.                  
W21732                                                                          
W21732     EXEC SQL                                                             
W26600         SELECT LOC_NM                                                    
W26600          INTO :XMT00001-LOC-NM                                           
W26600          FROM XMT_LOC                                                    
W26600         WHERE LOC_NBR = :XMT00001-LOC-NBR                                
W27710         WITH UR                                                          
W21732     END-EXEC                                                             
W21732                                                                          
W21732     EVALUATE TRUE                                                        
W21732         WHEN SQLCODE = +0                                                
W21732             CONTINUE                                                     
W21732         WHEN OTHER                                                       
W26600             MOVE 'READING XMT_LOC TABLE'                                 
W21732                           TO  PV-DB2-OPER-ATTEMPTED                      
W21732             PERFORM Z900-SQL-ABEND                                       
W21732     END-EVALUATE.                                                        
                                                                                
                                                                                
       Z700-WRITE-REPORT-LINE.                                                  
                                                                                
           MOVE REPORT-LINE-CC  TO  PC-REPORT-CC.                               
                                                                                
           WRITE REPORT-LINE.                                                   
                                                                                
           EVALUATE TRUE                                                        
               WHEN PC-REPORT-CC   = ' '                                        
                   ADD +1 TO  PC-LINE-COUNT                                     
               WHEN PC-REPORT-CC   = '0'                                        
                   ADD +2 TO  PC-LINE-COUNT                                     
               WHEN PC-REPORT-CC   = '-'                                        
                   ADD +3 TO  PC-LINE-COUNT                                     
           END-EVALUATE.                                                        
                                                                                
       Z800-PRINT-HEADINGS.                                                     
                                                                                
           ADD 1              TO PC-PAGE-COUNT.                                 
                                                                                
           MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
           MOVE '1'           TO REPORT-LINE-CC                                 
           MOVE ' '           TO REPORT-HEADER-FONT-INDICATOR                   
                                                                                
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE H1-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE H1A-HEADING TO REPORT-LINE.                                     
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
W26600     MOVE PV-RPT-STORE TO H1B-STORE.                                      
W26600     MOVE XMT00001-LOC-NM TO H1B-STORE-NAME.                              
           MOVE H1B-HEADING TO REPORT-LINE.                                     
                                                                                
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE H2-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
           MOVE H3-HEADING TO REPORT-LINE.                                      
           PERFORM Z700-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE +6         TO PC-LINE-COUNT.                                    
                                                                                
           SET  D1-DOUBLE-SPACE          TO TRUE.                               
                                                                                
RWJ00  Z805-PRINT-HEADINGS.                                                     
  |                                                                             
  |        ADD 1              TO PC-PAGE-COUNT.                                 
  |                                                                             
  |        MOVE PC-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
  |        MOVE 1             TO DP133O-REPORT-NUMBER.                          
  |        MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
  |        MOVE '1'           TO REPORT-LINE-CC                                 
  |        MOVE ' '           TO REPORT-HEADER-FONT-INDICATOR                   
  |                                                                             
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |                                                                             
  |        MOVE H1-HEADING TO REPORT-LINE.                                      
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |                                                                             
  |        MOVE H1A-HEADING TO REPORT-LINE.                                     
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |                                                                             
W26600     MOVE PV-RPT-STORE    TO H1B-STORE.                                   
W26600     MOVE XMT00001-LOC-NM TO H1B-STORE-NAME.                              
  |        MOVE H1B-HEADING TO REPORT-LINE.                                     
  |                                                                             
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |        MOVE H2B-HEADING TO REPORT-LINE.                                     
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |        MOVE H3-HEADING TO REPORT-LINE.                                      
  |        PERFORM Z700-WRITE-REPORT-LINE.                                      
  |                                                                             
  |        MOVE +6         TO PC-LINE-COUNT.                                    
  |                                                                             
  |        SET  D1-DOUBLE-SPACE          TO TRUE.                               
RWJ00                                                                           
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       Z900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
W34517     DISPLAY '**  PROGRAM   **  =  VAKRP109'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                              
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
