       IDENTIFICATION DIVISION.                                         00010037
       PROGRAM-ID.    IMKRP004                                          00020037
       AUTHOR.        PAUL KEBER.                                       00030037
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040037
       DATE-WRITTEN.  03/29/2005.                                       00050037
       DATE-COMPILED.                                                   00060037
                                                                        00070037
      ******************************************************************00080037
      *   IMKRP004 - NON-CONTINUED CLEARANCE PERM PRICE CHANGES REPORT  00090037
      ******************************************************************00100037
      *     THIS PROGRAM WILL CREATE A REPORT BY EFFECTIVE DATE OF THE  00110037
      * NUMBER OF INSERTS TO TUPCPLS AND TPPCPRC FOR NON-CONTINUED      00120037
      * CLEARANCE PERMANENT PRICE CHANGES.  IT WILL LIST EACH SPECIFIC  00130037
      * PRICE CHANGE ID FOR AN EFFECTIVE DATE WHERE THE NUMBER OF       00140037
      * TUPCPLS INSERTS REACHES THE INSERTS WARNING COUNT LIMIT.  THE   00150037
      * PROGRAM WILL END WITH A 4095 RETURN CODE IF THE NUMBER OF       00160037
      * TUPCPLS INSERTS FOR AN EFFECTIVE DATE REACHES THE INSERTS       00170037
      * WARNING COUNT LIMIT.  A CORPORATE LEVEL TUPCPLS INSERT IS       00180037
      * ASSUMED EACH TIME AN RCP SKU IS FOUND EVEN THOUGH AN INSERT     00190037
      * WILL NOT BE DONE IF THE STORE ZERO ROW STATUS HAD PREVIOUSLY    00200037
      * BEEN SET TO STATUS 25 (RCP) AND THE ALL REMAINING STORES        00210037
      * INDICATOR IS NOT SET FOR THE RCP SKU RECORD.                    00220037
      *                                                                 00230037
      * WR/PITS#     DATE     DESCRIPTION                               00240037
      * --------  ----------  ------------------------------------      00250037
      * WR#28566  03/29/2005  P. KEBER - INITIAL IMPLEMENTATION.        00260037
      * WR#29434  10/03/2005  P. KEBER - ADDED LOGIC TO PROCESS THE NEW 00270037
      *                       "MK2" AND "FNL" PRICE CHANGES AS TUPCPLS  00280037
      *                       RCP AND TPPCPRC INSERTS ONLY.             00290037
      * WR#29434  12/28/2006  P. KEBER - TREAT MK2 OR FNL PRICE CHANGES 00300037
      *           LIKE RCP PRICE CHANGES.  ADD ALL REMAINING STORES     00310037
      *           LOGIC TO THE FNL PRICE CHANGES AS TUPCPLS CORPORATE   00320037
      *           LEVEL INSERTS.  MAINTAIN A NEW "RCP EXPIRE SKUS" COUNT00330037
      *           FOR MK2 AND FNL PRICE CHANGES WITH ALL STORES HAVING  00340037
      *           THE SAME PRICE IF ALL-RMNG-STR-IND IS SET TO "Y".     00350037
      *           CHECK EXISTING TUPCPLS ROWS FOR THE SAME PRICE ON FNL 00360037
      *           PRICE CHANGES.  MAINTAIN A NEW "POSSIBLE RCP INSERTS" 00370037
      *           COUNT FOR MK2 AND FNL PRICE CHANGES WITH ALL-RMNG-STR-00380037
      *           IND IS SET TO "Y" AND NOT ALL THE SAME PRICE.         00390037
      *           NON-STATUS 25 OR 30 RCP Y WITH ALL THE SAME PRICE WILL00400037
      *           ONLY HAVE CORPORATE LEVEL INSERTS.                    00410037
      * PKE10354  02/24/2012  COGNIZANT- CHANGES FOR NEW INSERT WARNING 00420038
      *                                  LIMIT                          00430037
      * PKE11778  09/14/2012  COGNIZANT- CODE HAS BEEN MODIFIED TO      00431040
      *                                  RETURN 4095 OR 4094 BASED ON   00432040
      *                                  TUPCPLS INSERT COUNT INSTEAD   00433040
      *                                  OF TPPCPRC.                    00434040
      ******************************************************************00440037
                                                                        00450037
                                                                        00460037
       ENVIRONMENT DIVISION.                                            00470037
       CONFIGURATION SECTION.                                           00480037
       SOURCE-COMPUTER.        IBM-3090.                                00490037
       OBJECT-COMPUTER.        IBM-3090.                                00500037
                                                                        00510037
       INPUT-OUTPUT SECTION.                                            00520037
       FILE-CONTROL.                                                    00530037
                                                                        00540037
           SELECT PRICE-CHGS-RFMT-FILE                                  00550037
               ASSIGN TO INP01.                                         00560037
                                                                        00570037
           SELECT CONTROL-CARD-FILE                                     00580037
               ASSIGN TO INP02.                                         00590037
                                                                        00600037
           SELECT DATES-CTLCARD-FILE                                    00610037
               ASSIGN TO INP03.                                         00620037
                                                                        00630037
           SELECT PRICE-CHGS-REPORT                                     00640037
               ASSIGN TO RPT01.                                         00650037
                                                                        00660037
       DATA DIVISION.                                                   00670037
       FILE SECTION.                                                    00680037
                                                                        00690037
       FD  PRICE-CHGS-RFMT-FILE                                         00700037
           RECORDING MODE IS F                                          00710037
           BLOCK CONTAINS 0 RECORDS.                                    00720037
       01  PRICE-CHGS-RFMT-RECORD           PIC  X(30).                 00730037
                                                                        00740037
       FD  CONTROL-CARD-FILE                                            00750037
           RECORDING MODE IS F                                          00760037
           BLOCK CONTAINS 0 RECORDS.                                    00770037
       01  CONTROL-CARD-RECORD              PIC X(80).                  00780037
                                                                        00790037
       FD  DATES-CTLCARD-FILE                                           00800037
           RECORDING MODE IS F                                          00810037
           BLOCK CONTAINS 0 RECORDS.                                    00820037
       01  DATES-CTLCARD-RECORD             PIC X(80).                  00830037
                                                                        00840037
       FD  PRICE-CHGS-REPORT                                            00850037
           RECORDING MODE IS F                                          00860037
           BLOCK CONTAINS 0 RECORDS.                                    00870037
       01  PRICE-CHGS-REPORT-LINE           PIC X(132).                 00880037
                                                                        00890037
       WORKING-STORAGE SECTION.                                         00900037
                                                                        00910037
       01  PA-PROGRAM-ACCUMULATORS.                                     00920037
           05  PA-RETRY-COUNT               PIC S9(04)    VALUE +0 COMP.00930037
           05  PA-INPUT-RECS-READ           PIC S9(09)    VALUE +0      00940037
                                                          COMP-3.       00950037
           05  PA-TUPCPLS-CORP-INSERTS      PIC S9(09)    VALUE +0      00960037
                                                          COMP-3.       00970037
           05  PA-TUPCPLS-RCP-INSERTS       PIC S9(09)    VALUE +0      00980037
                                                          COMP-3.       00990037
           05  PA-POSSIBLE-RCP-INSERTS      PIC S9(09)    VALUE +0      01000037
                                                          COMP-3.       01010037
           05  PA-TUPCPLS-INSERTS           PIC S9(09)    VALUE +0      01020037
                                                          COMP-3.       01030037
           05  PA-TPPCPRC-INSERTS           PIC S9(09)    VALUE +0      01040037
                                                          COMP-3.       01050037
           05  PA-RCP-EXPIRE-SKUS           PIC S9(09)    VALUE +0      01060037
                                                          COMP-3.       01070037
           05  PA-EFFDTE-TUPCPLS-CORP-ISRTS PIC S9(09)    VALUE +0      01080037
                                                          COMP-3.       01090037
           05  PA-EFFDTE-TUPCPLS-RCP-ISRTS  PIC S9(09)    VALUE +0      01100037
                                                          COMP-3.       01110037
           05  PA-EFFDTE-POSSIBLE-RCP-ISRTS PIC S9(09)    VALUE +0      01120037
                                                          COMP-3.       01130037
           05  PA-EFFDTE-TUPCPLS-INSERTS    PIC S9(09)    VALUE +0      01140037
                                                          COMP-3.       01150037
           05  PA-EFFDTE-TPPCPRC-INSERTS    PIC S9(09)    VALUE +0      01160037
                                                          COMP-3.       01170037
           05  PA-EFFDTE-RCP-EXPIRE-SKUS    PIC S9(09)    VALUE +0      01180037
                                                          COMP-3.       01190037
           05  PA-ALL-TUPCPLS-CORP-ISRTS    PIC S9(09)    VALUE +0      01200037
                                                          COMP-3.       01210037
           05  PA-ALL-TUPCPLS-RCP-ISRTS     PIC S9(09)    VALUE +0      01220037
                                                          COMP-3.       01230037
           05  PA-ALL-POSSIBLE-RCP-ISRTS    PIC S9(09)    VALUE +0      01240037
                                                          COMP-3.       01250037
           05  PA-ALL-TUPCPLS-INSERTS       PIC S9(09)    VALUE +0      01260037
                                                          COMP-3.       01270037
           05  PA-ALL-TPPCPRC-INSERTS       PIC S9(09)    VALUE +0      01280037
                                                          COMP-3.       01290037
           05  PA-ALL-RCP-EXPIRE-SKUS       PIC S9(09)    VALUE +0      01300037
                                                          COMP-3.       01310037
           05  PA-SKU-STORES-COUNT          PIC S9(09)    VALUE +0      01320037
                                                          COMP-3.       01330037
                                                                        01340037
       01  PC-PROGRAM-CONSTANTS.                                        01350037
           05  PC-LINE-LIMIT-FOR-DTL        PIC S9(04)    VALUE +62     01360037
                                                          COMP SYNC.    01370037
           05  PC-MAX-RETRIES               PIC S9(04)    VALUE +3 COMP.01380037
           05  PC-PROGRAM-NAME              PIC X(08)     VALUE         01390037
               'IMKRP004'.                                              01400037
           05  PC-CLEARANCE                 PIC X(02)     VALUE '30'.   01410037
           05  PC-MIXED                     PIC X(02)     VALUE '25'.   01420037
                                                                        01430037
       01  PS-SWITCHES.                                                 01440037
           05  PS-END-OF-INPUT-SW           PIC X(01)     VALUE 'N'.    01450037
               88  PS-END-OF-INPUT                        VALUE 'Y'.    01460037
               88  PS-NOT-END-OF-INPUT                    VALUE 'N'.    01470037
P10354*    05  PS-INSERTS-WARNING-HIT-SW    PIC X(01)     VALUE 'N'.    01480037
P10354*        88  PS-INSERTS-WARNING-HIT                 VALUE 'Y'.    01490037
P10354     05  PS-INSERTS-WARNING-HIT-SW    PIC X(01)     VALUE '0'.    01500037
P10354         88  PS-LOW-INT-WARNING-HIT                 VALUE '1'.    01510037
P10354         88  PS-HIH-INT-WARNING-HIT                 VALUE '2'.    01520037
           05  PS-ALL-STORES-SAME-PRICE-SW  PIC X(01)     VALUE 'N'.    01530037
               88  PS-ALL-STORES-HAVE-SAME-PRICE          VALUE 'Y'.    01540037
               88  PS-ALL-STORES-NOT-SAME-PRICE           VALUE 'N'.    01550037
                                                                        01560037
       01  PV-PROGRAM-VARIABLES.                                        01570037
           05  PV-ONE                       PIC S9(04)    VALUE +0 COMP.01580037
           05  PV-PREV-PRCHG-ID             PIC S9(9)     VALUE +0 COMP.01590037
           05  PV-LINE-COUNT                PIC S9(04)    VALUE +63     01600037
                                                          COMP.         01610037
           05  PV-PREV-PRCHG-EFF-DTE        PIC X(10)     VALUE SPACES. 01620037
           05  PV-PREV-SKU                  PIC S9(08)    VALUE +0      01630037
                                                          COMP-3.       01640037
           05  PV-NEW-UNT-RTL-AMT           PIC S9(08)    VALUE +0      01650037
                                                          COMP-3.       01660037
           05  PV-SAVE-PUR-NEW-UNT-RTL-AMT  PIC S9(07)V99 VALUE +0      01670037
                                                          COMP-3.       01680037
           05  PV-ADVANCE                   PIC S9(03)    VALUE +0      01690037
                                                          COMP-3.       01700037
           05  PV-SAVE-PRCHG-TYP-CDE        PIC X(03)     VALUE SPACES. 01710037
               88  PV-SAVE-PRCHG-2ND-CLR-MRKDWN           VALUE 'MK2'.  01720037
               88  PV-SAVE-PRCHG-FINL-CLR-MRKDWN          VALUE 'FNL'.  01730037
           05  PV-PAGE-COUNT                PIC S9(05)    VALUE ZERO.   01740037
           05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.  01750037
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(35)     VALUE SPACES. 01760037
           05  PV-SQL-SUB                   PIC 9(03)     VALUE ZERO.   01770037
           05  PV-SQLCODE                   PIC S9(9)     VALUE +0.     01780037
           05  PV-SQLCODE-DISPLAY           PIC +(8)9     VALUE ZEROES. 01790037
P10354     05  PV-DISP-HI-EFF-DT            PIC  X(10)    VALUE SPACES. 01800037
P10354     05  PV-DISP-LW-EFF-DT            PIC  X(10)    VALUE SPACES. 01810037
P10354     05  PV-DISP-HI-INST-CNT          PIC  Z(09)    VALUE ZERO.   01820037
P10354     05  PV-DISP-LW-INST-CNT          PIC  Z(09)    VALUE ZERO.   01830037
P10354     05  PV-DISP-STAR-SEPT            PIC  X(80)    VALUE ALL '*'.01840037
                                                                        01850037
                                                                        01860037
       01  ABEND-CODE                       PIC S9(04)    VALUE +0 COMP.01870037
           88  AC-ABEND-4013                           VALUE +4013.     01880037
      *        SQL ERROR                                                01890037
                                                                        01900037
      ******************************************************************01910037
      *  PERM PRICE CHANGES REFORMAT INPUT FILE RECORD LAYOUT           01920037
      ******************************************************************01930037
           COPY IMRD044.                                                01940037
                                                                        01950037
      ****************************************************************  01960037
      * RECORD LAYOUT FOR INPUT INSERTS LIMIT CONTROL CARD              01970037
      ****************************************************************  01980037
       01  CC-CONTROL-CARD.                                             01990037
           05  CC-INSERTS-WARNING-CNT-LBL   PIC X(23)   VALUE           02000037
                 'INSERTS WARNING COUNT: '.                             02010037
P10354*    05  CC-INSERTS-WARNING-COUNT     PIC 9(09)   VALUE ZERO.     02020037
P10354*    05  FILLER                       PIC X(48)   VALUE SPACES.   02030037
P10354     05  CC-LOW-INT-WARNING-COUNT     PIC 9(09)   VALUE ZERO.     02040037
P10354     05  FILLER                       PIC X(01)   VALUE SPACES.   02050037
P10354     05  CC-UP-INT-WARNING-COUNT      PIC 9(09)   VALUE ZERO.     02060037
P10354     05  FILLER                       PIC X(38)   VALUE SPACES.   02070037
                                                                        02080037
      ****************************************************************  02090037
      * RECORD LAYOUT FOR INPUT EFFECTIVE DATES CONTROL CARD            02100037
      ****************************************************************  02110037
       01  CC-EFFDATES-REC.                                             02120037
           05  FILLER                       PIC X(31)   VALUE SPACES.   02130037
           05  CC-EFFDATES-EFF-DATE-1       PIC X(10)   VALUE ZERO.     02140037
           05  FILLER                       PIC X(07)   VALUE SPACES.   02150037
           05  CC-EFFDATES-EFF-DATE-2       PIC X(10)   VALUE ZERO.     02160037
           05  FILLER                       PIC X(22)   VALUE SPACES.   02170037
                                                                        02180037
      ******************************************************************02190037
      * STANDARD REPORT HEADING                                         02200037
      ******************************************************************02210037
           COPY DPWS132O.                                               02220037
                                                                        02230037
       01  HD-REPORT-TITLE-HEADING1.                                    02240037
           05  FILLER                       PIC  X(41)    VALUE SPACES. 02250037
           05  FILLER                       PIC  X(49)    VALUE         02260037
               'NON-CONTINUED CLEARANCE PERM PRICE CHANGES REPORT'.     02270037
           05  FILLER                       PIC  X(42)    VALUE SPACES. 02280037
                                                                        02290037
       01  HD-REPORT-TITLE-HEADING2.                                    02300037
           05  FILLER                       PIC  X(44)    VALUE SPACES. 02310037
           05  FILLER                       PIC  X(17)    VALUE         02320037
               'EFFECTIVE DATES: '.                                     02330037
           05  HD-RTH2-EFF-DATE-1           PIC  X(10)    VALUE SPACES. 02340037
           05  FILLER                       PIC  X(06)    VALUE         02350037
               ' THRU '.                                                02360037
           05  HD-RTH2-EFF-DATE-2           PIC  X(10)    VALUE SPACES. 02370037
           05  FILLER                       PIC  X(45)    VALUE SPACES. 02380037
                                                                        02390037
       01  HD-COLUMN-HEADING-1.                                         02400037
           05  FILLER                       PIC  X(01)    VALUE SPACE.  02410037
           05  FILLER                       PIC  X(10)                  02420037
                                  VALUE 'EFFECTIVE '.                   02430037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02440037
           05  FILLER                       PIC  X(09)                  02450037
                                  VALUE '  PRICE  '.                    02460037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02470037
           05  FILLER                       PIC  X(11)                  02480037
                                  VALUE '  TUPCPLS  '.                  02490037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02500037
           05  FILLER                       PIC  X(11)                  02510037
                                  VALUE '  TUPCPLS  '.                  02520037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02530037
           05  FILLER                       PIC  X(11)                  02540037
                                  VALUE ' POSSIBLE  '.                  02550037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02560037
           05  FILLER                       PIC  X(11)                  02570037
                                  VALUE '  TUPCPLS  '.                  02580037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02590037
           05  FILLER                       PIC  X(11)                  02600037
                                  VALUE '  TPPCPRC  '.                  02610037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02620037
           05  FILLER                       PIC  X(11)                  02630037
                                  VALUE 'RCP EXPIRE '.                  02640037
           05  FILLER                       PIC  X(25)    VALUE SPACES. 02650037
                                                                        02660037
       01  HD-COLUMN-HEADING-2.                                         02670037
           05  FILLER                       PIC  X(01)    VALUE SPACE.  02680037
           05  FILLER                       PIC  X(10)                  02690037
                                  VALUE '   DATE   '.                   02700037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02710037
           05  FILLER                       PIC  X(09)                  02720037
                                  VALUE 'CHANGE ID'.                    02730037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02740037
           05  FILLER                       PIC  X(11)                  02750037
                                  VALUE 'CORP INSERT'.                  02760037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02770037
           05  FILLER                       PIC  X(11)                  02780037
                                  VALUE 'RCP INSERTS'.                  02790037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02800037
           05  FILLER                       PIC  X(11)                  02810037
                                  VALUE 'RCP INSERTS'.                  02820037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02830037
           05  FILLER                       PIC  X(11)                  02840037
                                  VALUE '  INSERTS  '.                  02850037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02860037
           05  FILLER                       PIC  X(11)                  02870037
                                  VALUE '  INSERTS  '.                  02880037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02890037
           05  FILLER                       PIC  X(11)                  02900037
                                  VALUE '   SKUS    '.                  02910037
           05  FILLER                       PIC  X(25)    VALUE SPACES. 02920037
                                                                        02930037
       01  HD-COLUMN-HEADING-3.                                         02940037
           05  FILLER                       PIC  X(01)    VALUE SPACE.  02950037
           05  FILLER                       PIC  X(10)                  02960037
                                  VALUE '----------'.                   02970037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 02980037
           05  FILLER                       PIC  X(09)                  02990037
                                  VALUE '---------'.                    03000037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03010037
           05  FILLER                       PIC  X(11)                  03020037
                                  VALUE '-----------'.                  03030037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03040037
           05  FILLER                       PIC  X(11)                  03050037
                                  VALUE '-----------'.                  03060037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03070037
           05  FILLER                       PIC  X(11)                  03080037
                                  VALUE '-----------'.                  03090037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03100037
           05  FILLER                       PIC  X(11)                  03110037
                                  VALUE '-----------'.                  03120037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03130037
           05  FILLER                       PIC  X(11)                  03140037
                                  VALUE '-----------'.                  03150037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03160037
           05  FILLER                       PIC  X(11)                  03170037
                                  VALUE '-----------'.                  03180037
           05  FILLER                       PIC  X(25)    VALUE SPACES. 03190037
                                                                        03200037
       01  DL-DETAIL-LINE.                                              03210037
           05  FILLER                       PIC  X(01)    VALUE SPACE.  03220037
           05  DL-EFF-DATE                  PIC  X(10)    VALUE SPACES. 03230037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03240037
           05  DL-PRCHG-ID                  PIC  Z(8)9    VALUE ZERO.   03250037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03260037
           05  DL-TUPCPLS-CORP-ISRTS        PIC  ZZZ,ZZZ,ZZ9            03270037
                                                          VALUE ZERO.   03280037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03290037
           05  DL-TUPCPLS-RCP-ISRTS         PIC  ZZZ,ZZZ,ZZ9            03300037
                                                          VALUE ZERO.   03310037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03320037
           05  DL-POSSIBLE-RCP-ISRTS        PIC  ZZZ,ZZZ,ZZ9            03330037
                                                          VALUE ZERO.   03340037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03350037
           05  DL-TUPCPLS-INSERTS           PIC  ZZZ,ZZZ,ZZ9            03360037
                                                          VALUE ZERO.   03370037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03380037
           05  DL-TPPCPRC-INSERTS           PIC  ZZZ,ZZZ,ZZ9            03390037
                                                          VALUE ZERO.   03400037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03410037
           05  DL-RCP-EXPIRE-SKUS           PIC  ZZZ,ZZZ,ZZ9            03420037
                                                          VALUE ZERO.   03430037
           05  FILLER                       PIC  X(25)    VALUE SPACES. 03440037
                                                                        03450037
       01  DL-EFFDTE-TTLS-DETAIL-LINE.                                  03460037
           05  FILLER                       PIC  X(01)    VALUE SPACE.  03470037
           05  DL-EFFDTE-TTLS-HEADING.                                  03480037
               10  DL-EFFDTE-TTLS-EFF-DATE  PIC  X(10)    VALUE SPACES. 03490037
               10  FILLER                   PIC  X(05)    VALUE SPACES. 03500037
               10  FILLER                   PIC  X(07)    VALUE         03510037
                   'TOTALS:'.                                           03520037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03530037
           05  DL-EFFDTE-TUPCPLS-CORP-ISRTS PIC  ZZZ,ZZZ,ZZ9            03540037
                                                          VALUE ZERO.   03550037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03560037
           05  DL-EFFDTE-TUPCPLS-RCP-ISRTS  PIC  ZZZ,ZZZ,ZZ9            03570037
                                                          VALUE ZERO.   03580037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03590037
           05  DL-EFFDTE-POSSIBLE-RCP-ISRTS PIC  ZZZ,ZZZ,ZZ9            03600037
                                                          VALUE ZERO.   03610037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03620037
           05  DL-EFFDTE-TUPCPLS-INSERTS    PIC  ZZZ,ZZZ,ZZ9            03630037
                                                          VALUE ZERO.   03640037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03650037
           05  DL-EFFDTE-TPPCPRC-INSERTS    PIC  ZZZ,ZZZ,ZZ9            03660037
                                                          VALUE ZERO.   03670037
           05  FILLER                       PIC  X(03)    VALUE SPACES. 03680037
           05  DL-EFFDTE-RCP-EXPIRE-SKUS    PIC  ZZZ,ZZZ,ZZ9            03690037
                                                          VALUE ZERO.   03700037
           05  FILLER                       PIC  X(25)    VALUE SPACES. 03710037
                                                                        03720037
       01  HD-END-OF-REPORT-LINE.                                       03730037
           05  FILLER                       PIC  X(132)   VALUE         03740037
                                                '*** END OF REPORT ***'.03750037
                                                                        03760037
      ***************************************************************** 03770037
      *  TSKXREF DCLGEN                                                 03780037
      ***************************************************************** 03790037
           EXEC SQL                                                     03800037
               INCLUDE TSKXREF                                          03810037
           END-EXEC.                                                    03820037
                                                                        03830037
      ***************************************************************** 03840037
      *  TUPCPLS DCLGEN                                                 03850037
      ***************************************************************** 03860037
           EXEC SQL                                                     03870037
               INCLUDE TUPCPLS                                          03880037
           END-EXEC.                                                    03890037
                                                                        03900037
      *                                                                 03910037
           EXEC SQL                                                     03920037
                INCLUDE SQLCA                                           03930037
           END-EXEC.                                                    03940037
                                                                        03950037
           COPY SQLCA2.                                                 03960037
                                                                        03970037
      ******************************************************************03980037
      *  DB2 ERROR MESSAGES FORMAT AREA.                                03990037
      ******************************************************************04000037
           COPY DPWS004.                                                04010037
                                                                        04020037
       PROCEDURE DIVISION.                                              04030037
                                                                        04040037
           PERFORM 1000-INITIALIZE.                                     04050037
                                                                        04060037
           PERFORM 2000-PROCESS-PRICE-CHANGES                           04070037
               UNTIL PS-END-OF-INPUT.                                   04080037
                                                                        04090037
           PERFORM 9000-EOJ-ROUTINE.                                    04100037
                                                                        04110037
P10354*    IF PS-INSERTS-WARNING-HIT                                    04120037
P10354     IF PS-HIH-INT-WARNING-HIT                                    04130037
P10354         DISPLAY 'ON ' PV-DISP-HI-EFF-DT ' TUPCPLS TABLE WILL BE  04140037
P10354-        'INSERTED WITH ' PV-DISP-HI-INST-CNT ' RECORDS'          04150037
P10354         DISPLAY PV-DISP-STAR-SEPT                                04160037
P10354         MOVE 4095 TO RETURN-CODE                                 04170037
P10354     ELSE                                                         04180037
P10354         IF PS-LOW-INT-WARNING-HIT                                04190037
P10354         DISPLAY 'ON ' PV-DISP-LW-EFF-DT ' TUPCPLS TABLE WILL BE  04200037
P10354-        'INSERTED WITH ' PV-DISP-LW-INST-CNT ' RECORDS'          04210037
P10354         DISPLAY PV-DISP-STAR-SEPT                                04220037
P10354             MOVE 4094 TO RETURN-CODE                             04230037
P10354         END-IF                                                   04240037
           END-IF.                                                      04250037
                                                                        04260037
           GOBACK.                                                      04270037
                                                                        04280037
                                                                        04290037
       1000-INITIALIZE.                                                 04300037
                                                                        04310037
           INITIALIZE PV-PAGE-COUNT.                                    04320037
                                                                        04330037
           MOVE FUNCTION CURRENT-DATE (3:2)  TO DP132O-RUN-YEAR.        04340037
           MOVE FUNCTION CURRENT-DATE (5:2)  TO DP132O-RUN-MONTH.       04350037
           MOVE FUNCTION CURRENT-DATE (7:2)  TO DP132O-RUN-DAY.         04360037
           MOVE FUNCTION CURRENT-DATE (9:2)  TO DP132O-RUN-HOUR.        04370037
           MOVE FUNCTION CURRENT-DATE (11:2) TO DP132O-RUN-MINUTE.      04380037
                                                                        04390037
           MOVE PC-PROGRAM-NAME       TO DP132O-PROGRAM-NAME.           04400037
           MOVE 1                     TO DP132O-REPORT-NUMBER.          04410037
                                                                        04420037
           OPEN INPUT  PRICE-CHGS-RFMT-FILE                             04430037
                       CONTROL-CARD-FILE                                04440037
                       DATES-CTLCARD-FILE.                              04450037
           OPEN OUTPUT PRICE-CHGS-REPORT.                               04460037
                                                                        04470037
           PERFORM 7100-READ-CONTROL-CARD.                              04480037
           PERFORM 7200-READ-DATES-CTLCARD.                             04490037
                                                                        04500037
           PERFORM 8100-WRITE-REPORT-HEADINGS.                          04510037
                                                                        04520037
           PERFORM 7000-READ-PRICE-CHGS-REC.                            04530037
           IF PS-NOT-END-OF-INPUT                                       04540037
               MOVE IM044-PCRR-EFF-DTE  TO PV-PREV-PRCHG-EFF-DTE        04550037
                                           DL-EFF-DATE                  04560037
                                           DL-EFFDTE-TTLS-EFF-DATE      04570037
               MOVE IM044-PCRR-PRCHG-ID TO PV-PREV-PRCHG-ID             04580037
           END-IF.                                                      04590037
                                                                        04600037
      ******************************************************************04610037
      *  CREATE PERM PRICING REPORT RECS                                04620037
      ******************************************************************04630037
       2000-PROCESS-PRICE-CHANGES.                                      04640037
                                                                        04650037
           IF IM044-PCRR-SKU NOT = PV-PREV-SKU                          04660037
               PERFORM 2100-SAME-PRICE-STORES-CHECK                     04670037
               MOVE ZERO TO PA-SKU-STORES-COUNT                         04680037
           END-IF.                                                      04690037
                                                                        04700037
      *-- BREAK ON A NEW PRICE CHANGE EFFECTIVE DATE                    04710037
           IF IM044-PCRR-EFF-DTE NOT = PV-PREV-PRCHG-EFF-DTE            04720037
               PERFORM 3100-CHECK-PRCCHGID-TOTALS                       04730037
               PERFORM 3000-WRITE-EFF-DATE-TOTALS                       04740037
               MOVE IM044-PCRR-EFF-DTE  TO PV-PREV-PRCHG-EFF-DTE        04750037
                                           DL-EFF-DATE                  04760037
                                           DL-EFFDTE-TTLS-EFF-DATE      04770037
               MOVE IM044-PCRR-PRCHG-ID TO PV-PREV-PRCHG-ID             04780037
           END-IF.                                                      04790037
                                                                        04800037
      *-- BREAK ON A NEW PRICE CHANGE ID                                04810037
           IF IM044-PCRR-PRCHG-ID NOT = PV-PREV-PRCHG-ID                04820037
               PERFORM 3100-CHECK-PRCCHGID-TOTALS                       04830037
               MOVE IM044-PCRR-PRCHG-ID TO PV-PREV-PRCHG-ID             04840037
           END-IF.                                                      04850037
                                                                        04860037
      *--  WHEN SKU CHANGES AND TYPE CODE IS RCP, MK2, OR FNL, CREATE A 04870037
      *--  CORPORATE LEVEL STORE RECORD                                 04880037
           IF IM044-PRCHGTYP-REG-CLEARNCE                               04890037
             OR ((IM044-PRCHGTYP-2ND-CLR-MRKDWN                         04900037
                  OR IM044-PRCHGTYP-FINL-CLR-MRKDWN)                    04910037
                 AND IM044-PCRR-ALL-RMNG-STRS)                          04920037
               IF IM044-PCRR-SKU NOT = PV-PREV-SKU                      04930037
                   ADD +1 TO PA-TUPCPLS-CORP-INSERTS                    04940037
                             PA-EFFDTE-TUPCPLS-CORP-ISRTS               04950037
                             PA-ALL-TUPCPLS-CORP-ISRTS                  04960037
                             PA-TPPCPRC-INSERTS                         04970037
                             PA-EFFDTE-TPPCPRC-INSERTS                  04980037
                             PA-ALL-TPPCPRC-INSERTS                     04990037
                   MOVE IM044-PCRR-NEW-UNT-RTL-AMT                      05000037
                          TO PV-SAVE-PUR-NEW-UNT-RTL-AMT                05010037
                   MOVE IM044-PCRR-PRCHG-TYP-CDE                        05020037
                          TO PV-SAVE-PRCHG-TYP-CDE                      05030037
                   IF (IM044-PRCHGTYP-REG-CLEARNCE                      05040037
                       AND IM044-PCRR-ALL-RMNG-STRS)                    05050037
                     OR IM044-PRCHGTYP-2ND-CLR-MRKDWN                   05060037
                     OR IM044-PRCHGTYP-FINL-CLR-MRKDWN                  05070037
                       SET PS-ALL-STORES-HAVE-SAME-PRICE TO TRUE        05080037
                   ELSE                                                 05090037
                       SET PS-ALL-STORES-NOT-SAME-PRICE  TO TRUE        05100037
                   END-IF                                               05110037
               END-IF                                                   05120037
               IF IM044-PCRR-ALL-RMNG-STRS                              05130037
                   IF IM044-PCRR-NEW-UNT-RTL-AMT <                      05140037
                                          PV-SAVE-PUR-NEW-UNT-RTL-AMT   05150037
                       ADD +1 TO PA-TUPCPLS-RCP-INSERTS                 05160037
                                 PA-EFFDTE-TUPCPLS-RCP-ISRTS            05170037
                                 PA-ALL-TUPCPLS-RCP-ISRTS               05180037
                       SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE         05190037
                   ELSE                                                 05200037
                       IF (IM044-PRCHGTYP-REG-CLEARNCE                  05210037
                           OR IM044-PRCHGTYP-2ND-CLR-MRKDWN             05220037
                           OR IM044-PRCHGTYP-FINL-CLR-MRKDWN)           05230037
                         AND IM044-PCRR-PRCHG-TYP-CDE =                 05240037
                                 PV-SAVE-PRCHG-TYP-CDE                  05250037
                           ADD +1 TO PA-SKU-STORES-COUNT                05260037
                       ELSE                                             05270037
                           SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE     05280037
                       END-IF                                           05290037
                   END-IF                                               05300037
               ELSE                                                     05310037
                   ADD +1 TO PA-TUPCPLS-RCP-INSERTS                     05320037
                             PA-EFFDTE-TUPCPLS-RCP-ISRTS                05330037
                             PA-ALL-TUPCPLS-RCP-ISRTS                   05340037
                   SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE             05350037
               END-IF                                                   05360037
               ADD +1 TO PA-TPPCPRC-INSERTS                             05370037
                         PA-EFFDTE-TPPCPRC-INSERTS                      05380037
                         PA-ALL-TPPCPRC-INSERTS                         05390037
           ELSE                                                         05400037
               SET PS-ALL-STORES-NOT-SAME-PRICE  TO TRUE                05410037
      *--      MK2 AND FNL PRICE CHANGES ARE TREATED AS NON-CORP RCP    05420037
               IF IM044-PRCHGTYP-2ND-CLR-MRKDWN                         05430037
                 OR IM044-PRCHGTYP-FINL-CLR-MRKDWN                      05440037
                   ADD +1 TO PA-TUPCPLS-RCP-INSERTS                     05450037
                             PA-EFFDTE-TUPCPLS-RCP-ISRTS                05460037
                             PA-ALL-TUPCPLS-RCP-ISRTS                   05470037
                   ADD +1 TO PA-TPPCPRC-INSERTS                         05480037
                             PA-EFFDTE-TPPCPRC-INSERTS                  05490037
                             PA-ALL-TPPCPRC-INSERTS                     05500037
               ELSE                                                     05510037
      *--          EVERYTHING ELSE IS A CORPORATE LEVEL PRICE CHANGE    05520037
                   ADD +1 TO PA-TUPCPLS-CORP-INSERTS                    05530037
                             PA-EFFDTE-TUPCPLS-CORP-ISRTS               05540037
                             PA-ALL-TUPCPLS-CORP-ISRTS                  05550037
                             PA-TPPCPRC-INSERTS                         05560037
                             PA-EFFDTE-TPPCPRC-INSERTS                  05570037
                             PA-ALL-TPPCPRC-INSERTS                     05580037
               END-IF                                                   05590037
           END-IF.                                                      05600037
                                                                        05610037
           MOVE IM044-PCRR-SKU             TO PV-PREV-SKU.              05620037
                                                                        05630037
           PERFORM 7000-READ-PRICE-CHGS-REC.                            05640037
                                                                        05650037
      ******************************************************************05660037
      *  CHECK IF ALL EXISTING STORES HAVE SAME PRICE.                  05670037
      ******************************************************************05680037
       2100-SAME-PRICE-STORES-CHECK.                                    05690037
                                                                        05700037
           IF PS-ALL-STORES-HAVE-SAME-PRICE                             05710037
               PERFORM 6000-SELECT-PC-SKU                               05720037
               IF SKXREF-SKU-STAT-CDE = PC-CLEARANCE OR PC-MIXED        05730037
      *--          MK2 AND FNL PRICE CHANGES ARE TREATED AS NON-CORP RCP05740037
                   IF PV-SAVE-PRCHG-2ND-CLR-MRKDWN                      05750037
                       ADD +1 TO PA-RCP-EXPIRE-SKUS                     05760037
                                 PA-EFFDTE-RCP-EXPIRE-SKUS              05770037
                                 PA-ALL-RCP-EXPIRE-SKUS                 05780037
                       SUBTRACT PA-SKU-STORES-COUNT                     05790037
                            FROM PA-TPPCPRC-INSERTS                     05800037
                                 PA-EFFDTE-TPPCPRC-INSERTS              05810037
                                 PA-ALL-TPPCPRC-INSERTS                 05820037
                   ELSE                                                 05830037
                       IF PV-SAVE-PRCHG-FINL-CLR-MRKDWN                 05840037
                           SET PS-ALL-STORES-NOT-SAME-PRICE TO TRUE     05850037
                           PERFORM 6100-SELECT-TUPCPLS-FNL-PRICES       05860037
                           IF PS-ALL-STORES-HAVE-SAME-PRICE             05870037
                               ADD +1 TO PA-RCP-EXPIRE-SKUS             05880037
                                         PA-EFFDTE-RCP-EXPIRE-SKUS      05890037
                                         PA-ALL-RCP-EXPIRE-SKUS         05900037
                               SUBTRACT PA-SKU-STORES-COUNT             05910037
                                    FROM PA-TPPCPRC-INSERTS             05920037
                                         PA-EFFDTE-TPPCPRC-INSERTS      05930037
                                         PA-ALL-TPPCPRC-INSERTS         05940037
                           END-IF                                       05950037
                       END-IF                                           05960037
                   END-IF                                               05970037
               ELSE                                                     05980037
      *            RCP FOR NON-CLEARANCE SKU WILL BECOME NON-RCP SKU.   05990037
                   SUBTRACT PA-SKU-STORES-COUNT                         06000037
                        FROM PA-TPPCPRC-INSERTS                         06010037
                             PA-EFFDTE-TPPCPRC-INSERTS                  06020037
                             PA-ALL-TPPCPRC-INSERTS                     06030037
               END-IF                                                   06040037
           END-IF.                                                      06050037
                                                                        06060037
           IF PS-ALL-STORES-NOT-SAME-PRICE                              06070037
               IF PA-SKU-STORES-COUNT > +0                              06080037
                 AND (PV-SAVE-PRCHG-2ND-CLR-MRKDWN                      06090037
                      OR PV-SAVE-PRCHG-FINL-CLR-MRKDWN)                 06100037
      *--          MK2 AND FNL PRICE CHANGE POSSIBLE INSERTS            06110037
                   ADD PA-SKU-STORES-COUNT TO                           06120037
                             PA-POSSIBLE-RCP-INSERTS                    06130037
                             PA-EFFDTE-POSSIBLE-RCP-ISRTS               06140037
                             PA-ALL-POSSIBLE-RCP-ISRTS                  06150037
               END-IF                                                   06160037
           END-IF.                                                      06170037
                                                                        06180037
      ******************************************************************06190037
      *  WRITE PERM PRICING EFFECTIVE DATE TOTALS                       06200037
      ******************************************************************06210037
       3000-WRITE-EFF-DATE-TOTALS.                                      06220037
                                                                        06230037
           COMPUTE PA-EFFDTE-TUPCPLS-INSERTS =                          06240037
                                        PA-EFFDTE-TUPCPLS-CORP-ISRTS    06250037
                                      + PA-EFFDTE-TUPCPLS-RCP-ISRTS     06260037
                                      + PA-EFFDTE-POSSIBLE-RCP-ISRTS.   06270037
                                                                        06280037
P10354*    IF PA-EFFDTE-TPPCPRC-INSERTS >= CC-INSERTS-WARNING-COUNT     06290037
P10354*        SET PS-INSERTS-WARNING-HIT TO TRUE                       06300037
P11778     IF PA-EFFDTE-TUPCPLS-INSERTS >= CC-UP-INT-WARNING-COUNT      06310040
P10354         SET PS-HIH-INT-WARNING-HIT     TO TRUE                   06320037
P10354         MOVE DL-EFFDTE-TTLS-EFF-DATE   TO PV-DISP-HI-EFF-DT      06330037
P10354         MOVE PA-EFFDTE-TUPCPLS-INSERTS TO PV-DISP-HI-INST-CNT    06340037
P10354     ELSE                                                         06350037
P11778         IF PA-EFFDTE-TUPCPLS-INSERTS >= CC-LOW-INT-WARNING-COUNT 06360040
P10354            SET PS-LOW-INT-WARNING-HIT     TO TRUE                06370037
P10354            MOVE DL-EFFDTE-TTLS-EFF-DATE   TO PV-DISP-LW-EFF-DT   06380037
P10354            MOVE PA-EFFDTE-TUPCPLS-INSERTS TO PV-DISP-LW-INST-CNT 06390037
P10354         END-IF                                                   06400037
           END-IF.                                                      06410037
                                                                        06420037
           MOVE PA-EFFDTE-TUPCPLS-CORP-ISRTS                            06430037
                                      TO DL-EFFDTE-TUPCPLS-CORP-ISRTS.  06440037
           MOVE PA-EFFDTE-TUPCPLS-RCP-ISRTS                             06450037
                                      TO DL-EFFDTE-TUPCPLS-RCP-ISRTS.   06460037
           MOVE PA-EFFDTE-POSSIBLE-RCP-ISRTS                            06470037
                                      TO DL-EFFDTE-POSSIBLE-RCP-ISRTS.  06480037
           MOVE PA-EFFDTE-TUPCPLS-INSERTS  TO DL-EFFDTE-TUPCPLS-INSERTS.06490037
           MOVE PA-EFFDTE-TPPCPRC-INSERTS  TO DL-EFFDTE-TPPCPRC-INSERTS.06500037
           MOVE PA-EFFDTE-RCP-EXPIRE-SKUS TO                            06510037
                DL-EFFDTE-RCP-EXPIRE-SKUS.                              06520037
           MOVE DL-EFFDTE-TTLS-DETAIL-LINE TO PRICE-CHGS-REPORT-LINE.   06530037
           PERFORM 8000-WRITE-REPORT-REC.                               06540037
                                                                        06550037
           MOVE SPACES TO PRICE-CHGS-REPORT-LINE.                       06560037
           PERFORM 8000-WRITE-REPORT-REC.                               06570037
                                                                        06580037
           MOVE +0 TO PA-TUPCPLS-CORP-INSERTS                           06590037
                      PA-TUPCPLS-RCP-INSERTS                            06600037
                      PA-POSSIBLE-RCP-INSERTS                           06610037
                      PA-TUPCPLS-INSERTS                                06620037
                      PA-TPPCPRC-INSERTS                                06630037
                      PA-RCP-EXPIRE-SKUS                                06640037
                      PA-EFFDTE-TUPCPLS-CORP-ISRTS                      06650037
                      PA-EFFDTE-TUPCPLS-RCP-ISRTS                       06660037
                      PA-EFFDTE-POSSIBLE-RCP-ISRTS                      06670037
                      PA-EFFDTE-TUPCPLS-INSERTS                         06680037
                      PA-EFFDTE-TPPCPRC-INSERTS                         06690037
                      PA-EFFDTE-RCP-EXPIRE-SKUS.                        06700037
                                                                        06710037
                                                                        06720037
      ******************************************************************06730037
      *  WRITE PERM PRICING PRICE CHANGE ID TOTALS IF INSERTS WARNING   06740037
      *  LIMIT IS HIT                                                   06750037
      ******************************************************************06760037
       3100-CHECK-PRCCHGID-TOTALS.                                      06770037
                                                                        06780037
           COMPUTE PA-TUPCPLS-INSERTS = PA-TUPCPLS-CORP-INSERTS         06790037
                                      + PA-TUPCPLS-RCP-INSERTS          06800037
                                      + PA-POSSIBLE-RCP-INSERTS.        06810037
                                                                        06820037
           IF PA-TUPCPLS-INSERTS >= CC-LOW-INT-WARNING-COUNT            06830037
               MOVE PV-PREV-PRCHG-ID        TO DL-PRCHG-ID              06840037
               MOVE PA-TUPCPLS-CORP-INSERTS TO DL-TUPCPLS-CORP-ISRTS    06850037
               MOVE PA-TUPCPLS-RCP-INSERTS  TO DL-TUPCPLS-RCP-ISRTS     06860037
               MOVE PA-POSSIBLE-RCP-INSERTS TO DL-POSSIBLE-RCP-ISRTS    06870037
               MOVE PA-TUPCPLS-INSERTS      TO DL-TUPCPLS-INSERTS       06880037
               MOVE PA-TPPCPRC-INSERTS      TO DL-TPPCPRC-INSERTS       06890037
               MOVE PA-RCP-EXPIRE-SKUS      TO DL-RCP-EXPIRE-SKUS       06900037
               MOVE DL-DETAIL-LINE          TO PRICE-CHGS-REPORT-LINE   06910037
               PERFORM 8000-WRITE-REPORT-REC                            06920037
               MOVE SPACES                  TO DL-EFF-DATE              06930037
                                               DL-EFFDTE-TTLS-EFF-DATE  06940037
           END-IF.                                                      06950037
                                                                        06960037
           MOVE +0 TO PA-TUPCPLS-CORP-INSERTS                           06970037
                      PA-TUPCPLS-RCP-INSERTS                            06980037
                      PA-POSSIBLE-RCP-INSERTS                           06990037
                      PA-TUPCPLS-INSERTS                                07000037
                      PA-TPPCPRC-INSERTS                                07010037
                      PA-RCP-EXPIRE-SKUS.                               07020037
                                                                        07030037
      ************************************************************      07040037
      *  SELECT THE PRICE CHANGE SKU                            **      07050037
      ************************************************************      07060037
       6000-SELECT-PC-SKU.                                              07070037
           MOVE '*6000-SELECT-PC-SKU*' TO PV-PARAGRAPH-NAME.            07080037
                                                                        07090037
           MOVE PV-PREV-SKU TO SKXREF-SKU.                              07100037
                                                                        07110037
           INITIALIZE PA-RETRY-COUNT.                                   07120037
                                                                        07130037
           PERFORM                                                      07140037
               WITH TEST AFTER                                          07150037
               VARYING PV-SQL-SUB                                       07160037
               FROM 1 BY 1                                              07170037
               UNTIL SQLCODE = 0                                        07180037
                 OR  SQLCODE = +100                                     07190037
                 OR  PV-SQL-SUB > PC-MAX-RETRIES                        07200037
                                                                        07210037
               EXEC SQL                                                 07220037
                  SELECT  SKU_STAT_CDE                                  07230037
                         ,INTR_UPC_ID                                   07240037
                    INTO :SKXREF-SKU-STAT-CDE                           07250037
                        ,:SKXREF-INTR-UPC-ID                            07260037
                    FROM TSKXREF                                        07270037
                   WHERE SKU = :SKXREF-SKU                              07280037
               END-EXEC                                                 07290037
                                                                        07300037
               EVALUATE TRUE                                            07310037
                   WHEN SQLCODE = 0                                     07320037
                        CONTINUE                                        07330037
                   WHEN SQLCODE = +100                                  07340037
                        MOVE SPACES TO SKXREF-SKU-STAT-CDE              07350037
                   WHEN SQLCODE = -911                                  07360037
                   WHEN SQLCODE = -913                                  07370037
                        CONTINUE                                        07380037
                   WHEN SQLWARN NOT = SPACES                            07390037
                   WHEN SQLCODE NOT = ZERO                              07400037
                        MOVE 'SELECT PRICE CHANGE SKU'                  07410037
                                   TO PV-DB2-OPERATION-ATTEMPTED        07420037
                        PERFORM 9980-SQL-ERROR                          07430037
               END-EVALUATE                                             07440037
           END-PERFORM.                                                 07450037
                                                                        07460037
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        07470037
               MOVE 'SELECT PC SKU RETRIES EXCEEDED'                    07480037
                                   TO  PV-DB2-OPERATION-ATTEMPTED       07490037
               PERFORM 9980-SQL-ERROR                                   07500037
           END-IF.                                                      07510037
                                                                        07520037
      ************************************************************      07530037
      *  VERIFY THERE ARE NO DIFFERENT PRICES FOR A FINAL MARKDOWN      07540037
      *  PRICE CHANGE WITH ALL STORES HAVING THE SAME PRICE.            07550037
      ************************************************************      07560037
       6100-SELECT-TUPCPLS-FNL-PRICES.                                  07570037
           MOVE '*6100-SELECT-TUPCPLS-FNL-PRICES*' TO PV-PARAGRAPH-NAME.07580037
                                                                        07590037
           MOVE SKXREF-INTR-UPC-ID TO UPCPLS-UPC-ID.                    07600037
           MOVE PV-SAVE-PUR-NEW-UNT-RTL-AMT TO UPCPLS-UNT-RTL-AMT.      07610037
                                                                        07620037
           INITIALIZE PA-RETRY-COUNT.                                   07630037
                                                                        07640037
           PERFORM                                                      07650037
               WITH TEST AFTER                                          07660037
               VARYING PV-SQL-SUB                                       07670037
               FROM 1 BY 1                                              07680037
               UNTIL SQLCODE = 0                                        07690037
                 OR  SQLCODE = +100                                     07700037
                 OR  PV-SQL-SUB > PC-MAX-RETRIES                        07710037
                                                                        07720037
               EXEC SQL                                                 07730037
                  SELECT 1                                              07740037
                    INTO :PV-ONE                                        07750037
                    FROM TUPCPLS                                        07760037
                   WHERE UPC_ID = :UPCPLS-UPC-ID                        07770037
                     AND STR_NBR > SMALLINT(0)                          07780037
                     AND EFF_END_TMST IS NULL                           07790037
                     AND UNT_RTL_AMT <> :UPCPLS-UNT-RTL-AMT             07800037
                     AND MKDN_CDE = 'F'                                 07810037
                     AND MKDN_CDE = 'F'                                 07820037
                   FETCH FIRST 1 ROW ONLY                               07830037
                   WITH UR                                              07840037
               END-EXEC                                                 07850037
                                                                        07860037
               EVALUATE TRUE                                            07870037
                   WHEN SQLCODE = 0                                     07880037
                        CONTINUE                                        07890037
                   WHEN SQLCODE = +100                                  07900037
                        SET PS-ALL-STORES-HAVE-SAME-PRICE TO TRUE       07910037
                   WHEN SQLCODE = -911                                  07920037
                   WHEN SQLCODE = -913                                  07930037
                        CONTINUE                                        07940037
                   WHEN SQLWARN NOT = SPACES                            07950037
                   WHEN SQLCODE NOT = ZERO                              07960037
                        MOVE 'SELECT TUPCPLS FNL PRICES'                07970037
                                   TO PV-DB2-OPERATION-ATTEMPTED        07980037
                        PERFORM 9980-SQL-ERROR                          07990037
               END-EVALUATE                                             08000037
           END-PERFORM.                                                 08010037
                                                                        08020037
           IF  PV-SQL-SUB GREATER PC-MAX-RETRIES                        08030037
               MOVE 'SELECT TUPCPLS RETRIES EXCEEDED'                   08040037
                                   TO  PV-DB2-OPERATION-ATTEMPTED       08050037
               PERFORM 9980-SQL-ERROR                                   08060037
           END-IF.                                                      08070037
                                                                        08080037
      ******************************************************************08090037
      *  7000-READ-PRICE-CHGS-REC                                       08100037
      ******************************************************************08110037
       7000-READ-PRICE-CHGS-REC.                                        08120037
                                                                        08130037
           READ PRICE-CHGS-RFMT-FILE INTO IM044-PRICE-CHGS-RFMT-RECORD  08140037
               AT END                                                   08150037
                   SET PS-END-OF-INPUT TO TRUE                          08160037
               NOT AT END                                               08170037
                   ADD +1 TO PA-INPUT-RECS-READ                         08180037
           END-READ.                                                    08190037
                                                                        08200037
                                                                        08210037
      ******************************************************************08220037
      *  READ INSERTS LIMIT CONTROL CARD FILE.                          08230037
      ******************************************************************08240037
       7100-READ-CONTROL-CARD.                                          08250037
                                                                        08260037
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  08270037
               AT END                                                   08280037
                   DISPLAY '** NO INSERTS WARNING COUNT CONTROL CARD **'08290037
                   MOVE '7100-READ-CONTROL-CARD' TO PV-PARAGRAPH-NAME   08300037
                   MOVE 4001 TO ABEND-CODE                              08310037
                   PERFORM 9999-ABEND                                   08320037
               NOT AT END                                               08330037
                   DISPLAY 'INPUT CONTROL CARD: ' CC-CONTROL-CARD.      08340037
                                                                        08350037
      ******************************************************************08360037
      *  READ EFFECTIVE DATES CONTROL CARD FILE.                        08370037
      ******************************************************************08380037
       7200-READ-DATES-CTLCARD.                                         08390037
                                                                        08400037
           READ DATES-CTLCARD-FILE INTO CC-EFFDATES-REC                 08410037
               AT END                                                   08420037
                   DISPLAY '** NO EFFECTIVE DATES CONTROL CARD **'      08430037
                   MOVE '7200-READ-DATES-CTLCARD' TO PV-PARAGRAPH-NAME  08440037
                   MOVE 4002 TO ABEND-CODE                              08450037
                   PERFORM 9999-ABEND                                   08460037
               NOT AT END                                               08470037
                   MOVE CC-EFFDATES-EFF-DATE-1 TO HD-RTH2-EFF-DATE-1    08480037
                   MOVE CC-EFFDATES-EFF-DATE-2 TO HD-RTH2-EFF-DATE-2.   08490037
                                                                        08500037
      ******************************************************************08510037
      *  WRITE OUT A DETAIL LINE TO THE PERM PRICE CHANGES REPORT.      08520037
      ******************************************************************08530037
       8000-WRITE-REPORT-REC.                                           08540037
                                                                        08550037
           IF PV-LINE-COUNT > PC-LINE-LIMIT-FOR-DTL                     08560037
               PERFORM 8100-WRITE-REPORT-HEADINGS                       08570037
           END-IF.                                                      08580037
                                                                        08590037
           MOVE 1                      TO PV-ADVANCE.                   08600037
           WRITE PRICE-CHGS-REPORT-LINE                                 08610037
               AFTER ADVANCING PV-ADVANCE.                              08620037
           ADD PV-ADVANCE TO PV-LINE-COUNT.                             08630037
                                                                        08640037
                                                                        08650037
      ******************************************************************08660037
      *  WRITE OUT STANDARD REPORT HEADING LINES.                       08670037
      ******************************************************************08680037
       8100-WRITE-REPORT-HEADINGS.                                      08690037
                                                                        08700037
           ADD 1 TO PV-PAGE-COUNT.                                      08710037
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    08720037
           MOVE DP132O-STANDRD-HEADER-132-COLS                          08730037
               TO PRICE-CHGS-REPORT-LINE                                08740037
           WRITE PRICE-CHGS-REPORT-LINE                                 08750037
               AFTER ADVANCING PAGE.                                    08760037
                                                                        08770037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-REPORT-TITLE-HEADING1   08780037
               AFTER ADVANCING 2 LINES.                                 08790037
                                                                        08800037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-REPORT-TITLE-HEADING2   08810037
               AFTER ADVANCING 1 LINES.                                 08820037
                                                                        08830037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-COLUMN-HEADING-1        08840037
               AFTER ADVANCING 2 LINES.                                 08850037
                                                                        08860037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-COLUMN-HEADING-2        08870037
               AFTER ADVANCING 1 LINES.                                 08880037
                                                                        08890037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-COLUMN-HEADING-3        08900037
               AFTER ADVANCING 1 LINES.                                 08910037
                                                                        08920037
           MOVE +8 TO PV-LINE-COUNT.                                    08930037
                                                                        08940037
                                                                        08950037
       9000-EOJ-ROUTINE.                                                08960037
                                                                        08970037
           IF PA-INPUT-RECS-READ > +0                                   08980037
               IF PS-ALL-STORES-HAVE-SAME-PRICE                         08990037
                   PERFORM 2100-SAME-PRICE-STORES-CHECK                 09000037
               END-IF                                                   09010037
               PERFORM 3100-CHECK-PRCCHGID-TOTALS                       09020037
               PERFORM 3000-WRITE-EFF-DATE-TOTALS                       09030037
           END-IF.                                                      09040037
                                                                        09050037
           MOVE 'ALL DATES GRAND TOTALS'                                09060037
                                       TO DL-EFFDTE-TTLS-HEADING.       09070037
           MOVE PA-ALL-TUPCPLS-CORP-ISRTS                               09080037
                                       TO DL-EFFDTE-TUPCPLS-CORP-ISRTS. 09090037
           MOVE PA-ALL-TUPCPLS-RCP-ISRTS                                09100037
                                       TO DL-EFFDTE-TUPCPLS-RCP-ISRTS.  09110037
           MOVE PA-ALL-POSSIBLE-RCP-ISRTS                               09120037
                                       TO DL-EFFDTE-POSSIBLE-RCP-ISRTS. 09130037
           COMPUTE PA-ALL-TUPCPLS-INSERTS = PA-ALL-TUPCPLS-CORP-ISRTS   09140037
                                          + PA-ALL-TUPCPLS-RCP-ISRTS    09150037
                                          + PA-ALL-POSSIBLE-RCP-ISRTS.  09160037
           MOVE PA-ALL-TUPCPLS-INSERTS TO DL-EFFDTE-TUPCPLS-INSERTS.    09170037
           MOVE PA-ALL-TPPCPRC-INSERTS TO DL-EFFDTE-TPPCPRC-INSERTS.    09180037
           MOVE PA-ALL-RCP-EXPIRE-SKUS TO DL-EFFDTE-RCP-EXPIRE-SKUS.    09190037
           MOVE DL-EFFDTE-TTLS-DETAIL-LINE TO PRICE-CHGS-REPORT-LINE.   09200037
           PERFORM 8000-WRITE-REPORT-REC.                               09210037
                                                                        09220037
           WRITE PRICE-CHGS-REPORT-LINE FROM HD-END-OF-REPORT-LINE      09230037
               AFTER ADVANCING 2 LINES.                                 09240037
                                                                        09250037
           CLOSE PRICE-CHGS-RFMT-FILE                                   09260037
                 CONTROL-CARD-FILE                                      09270037
                 DATES-CTLCARD-FILE                                     09280037
                 PRICE-CHGS-REPORT.                                     09290037
                                                                        09300037
P10354*    DISPLAY '****************************************'.          09310037
P10354     DISPLAY PV-DISP-STAR-SEPT                                    09320037
           DISPLAY 'IMKRP004 - INPUT RECORDS READ: ' PA-INPUT-RECS-READ.09330037
P10354*    DISPLAY '****************************************'.          09340037
                                                                        09350037
       9980-SQL-ERROR.                                                  09360037
      ******************************************************************09370037
      * ABEND ROUTINE FOR BAD SQL CALLS                                *09380037
      ******************************************************************09390037
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                          09400037
           DISPLAY '** ABEND **          = SQL ERROR'.                  09410037
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           09420037
           DISPLAY '** PARAGRAPH **      = ' PV-PARAGRAPH-NAME.         09430037
           DISPLAY '** OPERATION **      = ' PV-DB2-OPERATION-ATTEMPTED.09440037
           DISPLAY '** SQL CODE **       = ' PV-SQLCODE-DISPLAY.        09450037
           DISPLAY '**'                                                 09460037
      *                                                                 09470037
      ******************************************************************09480037
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    09490037
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         09500037
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     09510037
      * FORMAT.                                                         09520037
      ******************************************************************09530037
           COPY DPPD004.                                                09540037
                                                                        09550037
           SET AC-ABEND-4013 TO TRUE.                                   09560037
           CALL 'ILBOABN0' USING ABEND-CODE.                            09570037
                                                                        09580037
                                                                        09590037
      ******************************************************************09600037
      *  THIS ROUTINE FORCES AN ABEND DUE TO A PROGRAM PROBLEM.         09610037
      ******************************************************************09620037
       9999-ABEND.                                                      09630037
                                                                        09640037
           DISPLAY '**  ABEND     **'.                                  09650037
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              09660037
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09670037
                                                                        09680037
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09690037
                                                                        09700037
