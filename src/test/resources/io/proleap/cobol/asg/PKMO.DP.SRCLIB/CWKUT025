      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.     CWKUT025.                                                
       AUTHOR.         CARL RICHTER.                                            
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   05/15/03.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  ITEM SKU EXTRACT PROGRAM                                      *        
      *  THIS PROGRAM CREATES A CHANGE EXTRACT FILE BY COMPARING       *        
      *  TWO GENERATIONS OF THE RMS COMMON EXTRACT FILE.               *        
      *                                                                         
      *  CHANGE LOG:  10/01/2008  MARIBETH ZAMORA     PROJECT ID 1526           
      *                           AD ROTATION PROJECT  WR 32284                 
      *                                                                         
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT CURRENT-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PREVIOUS-FILE                                                 
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT CHANGE-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CURRENT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CURRENT-RECORD                  PIC X(395).                          
                                                                                
       FD  PREVIOUS-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PREVIOUS-RECORD                 PIC X(395).                          
                                                                                
       FD  CHANGE-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CHANGE-RECORD                   PIC X(499).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CR-CURRENT-REC.                                                      
           05  CR-CURR-KEY-9                  PIC   9(10).                      
           05  CR-CURR-KEY-X  REDEFINES                                         
               CR-CURR-KEY-9                  PIC   X(10).                      
                                                                                
           05  CR-CURR-DATA                   PIC   X(488).                     
                                                                                
       01  PR-PREVIOUS-REC.                                                     
           05  PR-PREV-KEY-9                  PIC   9(10).                      
           05  PR-PREV-KEY-X  REDEFINES                                         
               PR-PREV-KEY-9                  PIC   X(10).                      
                                                                                
           05  PR-PREV-DATA                   PIC   X(488).                     
                                                                                
       01  CHANGE-OUTPUT-REC.                                                   
           05  COR-CHANGE-KEY                 PIC   X(10).                      
           05  COR-CHANGE-DATA                PIC   X(488).                     
           05  COR-CHANGE-INDICATOR           PIC   X(01).                      
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME           PIC X(08) VALUE 'CWKUT025'.            
           05  PC-OPERCO                 PIC X(02) VALUE '03'.                  
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-INPUT-RECORDS          PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-PRE-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-INPUT-CUR-RECORDS      PIC  9(9)       VALUE ZERO.            
           05  PA-OUTPUT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-INSERT-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-UPDATE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-DELETE-RECORDS         PIC  9(9)       VALUE ZERO.            
           05  PA-NO-CHANGE-RECORDS      PIC  9(9)       VALUE ZERO.            
                                                                                
       01  WS-TEMP-REC.                                                         
           05  WS-TMP-INTR-UPC-ID            PIC   9(10).                       
           05  WS-TMP-RMS-STYL-ID            PIC   9(8).                        
           05  WS-TMP-ITM-NBR                PIC   9(15).                       
           05  WS-TMP-SKU                    PIC   9(8).                        
           05  WS-TMP-INTR-UPC-DESC          PIC   X(30).                       
           05  WS-TMP-SKU-STAT-CDE           PIC   X(2).                        
           05  WS-TMP-SKU-STAT-DESC          PIC   X(10).                       
           05  WS-TMP-INNR-PK-QTY            PIC  +9(5).                        
           05  WS-TMP-OUTR-PK-QTY            PIC  +9(5).                        
           05  WS-TMP-KOHLS-SIZE-CDE         PIC   9(10).                       
           05  WS-TMP-KOHLS-SIZE-DESC        PIC   X(10).                       
           05  WS-TMP-NRF-CLOR-CDE           PIC   X(3).                        
           05  WS-TMP-NRF-CLOR-DESC          PIC   X(10).                       
           05  WS-TMP-SHRT-CLOR-DESC         PIC   X(4).                        
           05  WS-TMP-CSTM-CLOR-DESC         PIC   X(10).                       
           05  WS-TMP-CUSTFACG-CLOR-DESC     PIC   X(22).                       
           05  WS-TMP-REGN-PRIC-IND          PIC   X(1).                        
           05  WS-TMP-RPLNMT-IND             PIC   X(1).                        
           05  WS-TMP-KY-ITM-IND             PIC   X(1).                        
           05  WS-TMP-POG-IND                PIC   X(1).                        
           05  WS-TMP-GP-CDE                 PIC   X(3).                        
           05  WS-TMP-SEA-DESC               PIC   X(20).                       
           05  WS-TMP-UNT-COST-1ST-AMT       PIC  +9(7).9(3).                   
           05  WS-TMP-TX-PRD-CDE-DESC        PIC   X(20).                       
           05  WS-TMP-WT-UM-CDE              PIC   X(2).                        
           05  WS-TMP-WT-UM-QTY              PIC  +9(5).9(3).                   
           05  WS-TMP-SIZE-UM-CDE            PIC   X(2).                        
           05  WS-TMP-WD-UM-QTY              PIC  +9(4).9(3).                   
           05  WS-TMP-HT-UM-QTY              PIC  +9(4).9(3).                   
           05  WS-TMP-DPTH-UM-QTY            PIC  +9(4).9(3).                   
           05  WS-TMP-E-BUS-ITM-IND          PIC   X(1).                        
           05  WS-TMP-ASRT-BEG-DTE           PIC   X(10).                       
           05  WS-TMP-ASRT-END-DTE           PIC   X(10).                       
           05  WS-TMP-STRG-AREA-DESC         PIC   X(20).                       
           05  WS-TMP-SHIP-WD-QTY            PIC  +9(4).9(3).                   
           05  WS-TMP-SHIP-DPTH-QTY          PIC  +9(4).9(3).                   
           05  WS-TMP-SHIP-HT-QTY            PIC  +9(4).9(3).                   
           05  WS-TMP-SHIP-WT-QTY            PIC  +9(5).9(3).                   
           05  WS-TMP-SHIP-DIM-SRC-CDE       PIC   X(1).                        
           05  WS-TMP-PKG-DESC               PIC   X(20).                       
           05  WS-TMP-SHIP-RSTRC-DESC        PIC   X(20).                       
           05  WS-TMP-GFT-WRAP-AVL-IND       PIC   X(1).                        
           05  WS-TMP-PRD-GP-NBR             PIC   9(8).                        
           05  WS-TMP-PRD-GP-IND             PIC   X(1).                        
           05  WS-TMP-SHIP-SRCHG-FEE-AMT     PIC  +9(3).9(2).                   
           05  WS-TMP-SHIP-SRVC-DESC         PIC   X(20).                       
           05  WS-TMP-SHIP-GP-DESC           PIC   X(20).                       
           05  WS-TMP-TRNSTN-IND             PIC   X(01).                       
           05  WS-TMP-WEB-EXCLV-IND          PIC   X(01).                       
           05  WS-TMP-EXTD-SZ-CDE            PIC   X(02).                       
           05  WS-TMP-PO-SHRT-CYC-IND        PIC   X(01).                       
           05  WS-TMP-EFF-BEG-DTE            PIC   X(10).                       
           05  WS-TMP-CORP-RTL-AMT           PIC  +9(7).9(2).                   
           05  WS-TMP-CORP-COST-AMT          PIC  +9(7).9(3).                   
           05  WS-TMP-CORP-GP-PRIC-ID        PIC   9(9).                        
           05  WS-TMP-ORIG-CLR-DTE           PIC   X(10).                       
           05  WS-TMP-MKDN-CDE               PIC   X(1).                        
                                                                                
                                                                                
      ******************************************************************        
      * INPUT FILE LAYOUT FOR EXPANDED SKU FILE                                 
      ******************************************************************        
           COPY CWRD032.                                                        
                                                                                
      ******************************************************************        
      * OUTPUT FILE LAYOUT FOR CW SKU EXTRACT FILE                              
      ******************************************************************        
           COPY CWRD005.                                                        
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-MAIN-ROUTINE                                            
               UNTIL CR-CURR-KEY-X = HIGH-VALUES                                
                     AND                                                        
                     PR-PREV-KEY-X = HIGH-VALUES.                               
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      *  INITIALIZE                                                             
      ******************************************************************        
                                                                                
       1000-INITIALIZE.                                                         
                                                                                
           OPEN INPUT  CURRENT-FILE                                             
                       PREVIOUS-FILE                                            
                OUTPUT CHANGE-FILE.                                             
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** PROGRAM: ' PC-PROGRAM-NAME.                              
           PERFORM 8000-READ-CURRENT.                                           
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      * MAIN PROCESSING.                                                        
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
                                                                                
           INITIALIZE CHANGE-OUTPUT-REC                                         
                                                                                
           IF CR-CURR-KEY-9  =  PR-PREV-KEY-9                                   
               PERFORM 2100-UPDATE-OR-NOCHANGE                                  
                                                                                
           ELSE                                                                 
                                                                                
               IF CR-CURR-KEY-9  <  PR-PREV-KEY-9                               
                   PERFORM 2200-INSERT-CURRENT                                  
                                                                                
               ELSE                                                             
                   PERFORM 2300-DELETE-PREVIOUS                                 
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      *  CHECKS RECORD FOR EQUALITY                                             
      *  IF DATA IS NOT EQUAL, THEN RECORD IS AN UPDATE                         
      ******************************************************************        
       2100-UPDATE-OR-NOCHANGE.                                                 
                                                                                
           IF CR-CURR-DATA  =  PR-PREV-DATA                                     
                                                                                
               ADD +1                 TO PA-NO-CHANGE-RECORDS                   
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
                                                                                
           ELSE                                                                 
                                                                                
               MOVE CR-CURR-KEY-9     TO COR-CHANGE-KEY                         
               MOVE CR-CURR-DATA      TO COR-CHANGE-DATA                        
               MOVE 'U'               TO COR-CHANGE-INDICATOR                   
                                                                                
               ADD +1                 TO PA-UPDATE-RECORDS                      
                                                                                
               PERFORM 7100-WRITE                                               
               PERFORM 8000-READ-CURRENT                                        
               PERFORM 8100-READ-PREVIOUS                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  INSERT CURRENT DATA                                                    
      ******************************************************************        
       2200-INSERT-CURRENT.                                                     
                                                                                
           MOVE CR-CURR-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE CR-CURR-DATA  TO COR-CHANGE-DATA                                
           MOVE 'I'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-INSERT-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8000-READ-CURRENT.                                           
                                                                                
                                                                                
      ******************************************************************        
      *  DELETE PREVIOUS DATA                                                   
      ******************************************************************        
       2300-DELETE-PREVIOUS.                                                    
                                                                                
           MOVE PR-PREV-KEY-9 TO COR-CHANGE-KEY                                 
           MOVE PR-PREV-DATA  TO COR-CHANGE-DATA                                
           MOVE 'D'           TO COR-CHANGE-INDICATOR                           
                                                                                
           ADD +1             TO PA-DELETE-RECORDS                              
                                                                                
           PERFORM 7100-WRITE                                                   
           PERFORM 8100-READ-PREVIOUS.                                          
                                                                                
                                                                                
      ******************************************************************        
      *  REFORMAT RMS DATA                                             *        
      ******************************************************************        
       3000-REFORMAT-RMS-DATA.                                                  
                                                                                
           INITIALIZE       CW005-SKU-RECORD.                                   
                                                                                
           MOVE CW032-INTR-UPC-ID        TO  CW005-INTR-UPC-ID-RED              
           MOVE CW032-RMS-STYL-ID        TO  CW005-RMS-STYL-ID-RED              
           MOVE CW032-ITM-NBR            TO  CW005-ITM-NBR-RED                  
           MOVE CW032-SKU                TO  CW005-SKU-RED                      
           MOVE CW032-INTR-UPC-DESC      TO  CW005-INTR-UPC-DESC                
           MOVE CW032-SKU-STAT-CDE       TO  CW005-SKU-STAT-CDE                 
           MOVE CW032-SKU-STAT-DESC      TO  CW005-SKU-STAT-DESC                
           MOVE CW032-INNR-PK-QTY        TO  CW005-INNR-PK-QTY                  
           MOVE CW032-OUTR-PK-QTY        TO  CW005-OUTR-PK-QTY                  
           MOVE CW032-KOHLS-SIZE-CDE     TO  CW005-KOHLS-SIZE-CDE-RED           
           MOVE CW032-KOHLS-SIZE-DESC    TO  CW005-KOHLS-SIZE-DESC              
           MOVE CW032-NRF-CLOR-CDE       TO  CW005-NRF-CLOR-CDE                 
           MOVE CW032-NRF-CLOR-DESC      TO  CW005-NRF-CLOR-DESC                
           MOVE CW032-SHRT-CLOR-DESC     TO  CW005-SHRT-CLOR-DESC               
           MOVE CW032-CSTM-CLOR-DESC     TO  CW005-CSTM-CLOR-DESC               
           MOVE CW032-CUSTFACG-CLOR-DESC TO  CW005-CUSTFACG-CLOR-DESC           
           MOVE CW032-REGN-PRIC-IND      TO  CW005-REGN-PRIC-IND                
           MOVE CW032-RPLNMT-IND         TO  CW005-RPLNMT-IND                   
           MOVE CW032-KY-ITM-IND         TO  CW005-KY-ITM-IND                   
           MOVE CW032-POG-IND            TO  CW005-POG-IND                      
           MOVE CW032-GP-CDE             TO  CW005-GP-CDE                       
           MOVE CW032-SEA-DESC           TO  CW005-SEA-DESC                     
           MOVE CW032-UNT-COST-1ST-AMT   TO  CW005-UNT-COST-1ST-AMT             
           MOVE CW032-TX-PRD-CDE-DESC    TO  CW005-TX-PRD-CDE-DESC              
           MOVE CW032-WT-UM-CDE          TO  CW005-WT-UM-CDE                    
           MOVE CW032-WT-UM-QTY          TO  CW005-WT-UM-QTY                    
           MOVE CW032-SIZE-UM-CDE        TO  CW005-SIZE-UM-CDE                  
           MOVE CW032-WD-UM-QTY          TO  CW005-WD-UM-QTY                    
           MOVE CW032-HT-UM-QTY          TO  CW005-HT-UM-QTY                    
           MOVE CW032-DPTH-UM-QTY        TO  CW005-DPTH-UM-QTY                  
           MOVE CW032-E-BUS-ITM-IND      TO  CW005-E-BUS-ITM-IND                
                                                                                
           MOVE CW032-SHIP-WD-QTY        TO  CW005-SHIP-WD-QTY                  
           MOVE CW032-SHIP-DPTH-QTY      TO  CW005-SHIP-DPTH-QTY                
           MOVE CW032-SHIP-HT-QTY        TO  CW005-SHIP-HT-QTY                  
           MOVE CW032-SHIP-WT-QTY        TO  CW005-SHIP-WT-QTY                  
           MOVE CW032-PRD-GP-NBR         TO  CW005-PRD-GP-NBR                   
           MOVE CW032-SHIP-SRCHG-FEE-AMT TO  CW005-SHIP-SRCHG-FEE-AMT           
                                                                                
           MOVE CW032-TRNSTN-IND         TO  CW005-TRNSTN-IND                   
           MOVE CW032-WEB-EXCLV-IND      TO  CW005-WEB-EXCLV-IND                
           MOVE CW032-EXTD-SZ-CDE        TO  CW005-EXTD-SZ-CDE                  
           MOVE CW032-PO-SHRT-CYC-IND    TO  CW005-PO-SHRT-CYC-IND              
           MOVE CW032-EFF-BEG-DTE        TO  CW005-EFF-BEG-DTE                  
           MOVE CW032-CORP-RTL-AMT       TO  CW005-CORP-RTL-AMT.                
           MOVE CW032-CORP-COST-AMT      TO  CW005-CORP-COST-AMT.               
           MOVE CW032-CORP-GP-PRIC-ID    TO  CW005-CORP-GP-PRIC-ID.             
                                                                                
           IF   CW032-E-BUS-ITM-IND  =  'N'                                     
                PERFORM 3100-MOVE-E-SKU-DEFAULTS                                
           ELSE                                                                 
                PERFORM 3200-MOVE-E-SKU-VALUES                                  
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      *  FORMAT THE E-BUS FIELDS WHEN THE                                       
      *  E-BUS-ITM-IND = 'N'.                                                   
      ******************************************************************        
       3100-MOVE-E-SKU-DEFAULTS.                                                
                                                                                
           MOVE '9999-09-09'          TO  CW005-ASRT-BEG-DTE                    
                                          CW005-ASRT-END-DTE.                   
                                                                                
           MOVE SPACES                TO  CW005-STRG-AREA-DESC                  
                                          CW005-SHIP-DIM-SRC-CDE                
                                          CW005-PKG-DESC                        
                                          CW005-SHIP-RSTRC-DESC                 
                                          CW005-GFT-WRAP-AVL-IND                
                                          CW005-PRD-GP-IND                      
                                          CW005-SHIP-SRVC-DESC                  
                                          CW005-SHIP-GP-DESC.                   
                                                                                
                                                                                
      ************************************************************              
      *  MOVE THE E-BUS FIELDS WHEN THE                                         
      *  E-BUS-ITM-IND = 'Y'.                                                   
      ************************************************************              
       3200-MOVE-E-SKU-VALUES.                                                  
                                                                                
           MOVE CW032-ASRT-BEG-DTE       TO  CW005-ASRT-BEG-DTE.                
           MOVE CW032-ASRT-END-DTE       TO  CW005-ASRT-END-DTE.                
           MOVE CW032-STRG-AREA-DESC     TO  CW005-STRG-AREA-DESC.              
           MOVE CW032-SHIP-DIM-SRC-CDE   TO  CW005-SHIP-DIM-SRC-CDE.            
           MOVE CW032-PKG-DESC           TO  CW005-PKG-DESC.                    
           MOVE CW032-SHIP-RSTRC-DESC    TO  CW005-SHIP-RSTRC-DESC.             
           MOVE CW032-GFT-WRAP-AVL-IND   TO  CW005-GFT-WRAP-AVL-IND.            
           MOVE CW032-PRD-GP-IND         TO  CW005-PRD-GP-IND.                  
           MOVE CW032-SHIP-SRVC-DESC     TO  CW005-SHIP-SRVC-DESC.              
           MOVE CW032-SHIP-GP-DESC       TO  CW005-SHIP-GP-DESC.                
                                                                                
                                                                                
      ******************************************************************        
      *  MOVE TO TEMP STORAGE                                          *        
      ******************************************************************        
       4000-MOVE-TO-TEMP.                                                       
                                                                                
           INITIALIZE       WS-TEMP-REC.                                        
                                                                                
           MOVE CW005-INTR-UPC-ID        TO  WS-TMP-INTR-UPC-ID                 
           MOVE CW005-RMS-STYL-ID        TO  WS-TMP-RMS-STYL-ID                 
           MOVE CW005-ITM-NBR            TO  WS-TMP-ITM-NBR                     
           MOVE CW005-SKU                TO  WS-TMP-SKU                         
           MOVE CW005-INTR-UPC-DESC      TO  WS-TMP-INTR-UPC-DESC               
           MOVE CW005-SKU-STAT-CDE       TO  WS-TMP-SKU-STAT-CDE                
           MOVE CW005-SKU-STAT-DESC      TO  WS-TMP-SKU-STAT-DESC               
           MOVE CW005-INNR-PK-QTY        TO  WS-TMP-INNR-PK-QTY                 
           MOVE CW005-OUTR-PK-QTY        TO  WS-TMP-OUTR-PK-QTY                 
           MOVE CW005-KOHLS-SIZE-CDE     TO  WS-TMP-KOHLS-SIZE-CDE              
           MOVE CW005-KOHLS-SIZE-DESC    TO  WS-TMP-KOHLS-SIZE-DESC             
           MOVE CW005-NRF-CLOR-CDE       TO  WS-TMP-NRF-CLOR-CDE                
           MOVE CW005-NRF-CLOR-DESC      TO  WS-TMP-NRF-CLOR-DESC               
           MOVE CW005-SHRT-CLOR-DESC     TO  WS-TMP-SHRT-CLOR-DESC              
           MOVE CW005-CSTM-CLOR-DESC     TO  WS-TMP-CSTM-CLOR-DESC              
           MOVE CW005-CUSTFACG-CLOR-DESC TO  WS-TMP-CUSTFACG-CLOR-DESC          
           MOVE CW005-REGN-PRIC-IND      TO  WS-TMP-REGN-PRIC-IND               
           MOVE CW005-RPLNMT-IND         TO  WS-TMP-RPLNMT-IND                  
           MOVE CW005-KY-ITM-IND         TO  WS-TMP-KY-ITM-IND                  
           MOVE CW005-POG-IND            TO  WS-TMP-POG-IND                     
           MOVE CW005-GP-CDE             TO  WS-TMP-GP-CDE                      
           MOVE CW005-SEA-DESC           TO  WS-TMP-SEA-DESC                    
           MOVE CW005-UNT-COST-1ST-AMT   TO  WS-TMP-UNT-COST-1ST-AMT            
           MOVE CW005-TX-PRD-CDE-DESC    TO  WS-TMP-TX-PRD-CDE-DESC             
           MOVE CW005-WT-UM-CDE          TO  WS-TMP-WT-UM-CDE                   
           MOVE CW005-WT-UM-QTY          TO  WS-TMP-WT-UM-QTY                   
           MOVE CW005-SIZE-UM-CDE        TO  WS-TMP-SIZE-UM-CDE                 
           MOVE CW005-WD-UM-QTY          TO  WS-TMP-WD-UM-QTY                   
           MOVE CW005-HT-UM-QTY          TO  WS-TMP-HT-UM-QTY                   
           MOVE CW005-DPTH-UM-QTY        TO  WS-TMP-DPTH-UM-QTY                 
           MOVE CW005-E-BUS-ITM-IND      TO  WS-TMP-E-BUS-ITM-IND               
                                                                                
           MOVE CW005-SHIP-WD-QTY        TO  WS-TMP-SHIP-WD-QTY                 
           MOVE CW005-SHIP-DPTH-QTY      TO  WS-TMP-SHIP-DPTH-QTY               
           MOVE CW005-SHIP-HT-QTY        TO  WS-TMP-SHIP-HT-QTY                 
           MOVE CW005-SHIP-WT-QTY        TO  WS-TMP-SHIP-WT-QTY                 
           MOVE CW005-PRD-GP-NBR         TO  WS-TMP-PRD-GP-NBR                  
           MOVE CW005-SHIP-SRCHG-FEE-AMT TO  WS-TMP-SHIP-SRCHG-FEE-AMT          
                                                                                
           MOVE CW005-ASRT-BEG-DTE       TO  WS-TMP-ASRT-BEG-DTE.               
           MOVE CW005-ASRT-END-DTE       TO  WS-TMP-ASRT-END-DTE.               
           MOVE CW005-STRG-AREA-DESC     TO  WS-TMP-STRG-AREA-DESC.             
           MOVE CW005-SHIP-DIM-SRC-CDE   TO  WS-TMP-SHIP-DIM-SRC-CDE.           
           MOVE CW005-PKG-DESC           TO  WS-TMP-PKG-DESC.                   
           MOVE CW005-SHIP-RSTRC-DESC    TO  WS-TMP-SHIP-RSTRC-DESC.            
           MOVE CW005-GFT-WRAP-AVL-IND   TO  WS-TMP-GFT-WRAP-AVL-IND.           
           MOVE CW005-PRD-GP-IND         TO  WS-TMP-PRD-GP-IND.                 
           MOVE CW005-SHIP-SRVC-DESC     TO  WS-TMP-SHIP-SRVC-DESC.             
           MOVE CW005-SHIP-GP-DESC       TO  WS-TMP-SHIP-GP-DESC.               
                                                                                
           MOVE CW005-TRNSTN-IND         TO  WS-TMP-TRNSTN-IND                  
           MOVE CW005-WEB-EXCLV-IND      TO  WS-TMP-WEB-EXCLV-IND               
           MOVE CW005-EXTD-SZ-CDE        TO  WS-TMP-EXTD-SZ-CDE                 
           MOVE CW005-PO-SHRT-CYC-IND    TO  WS-TMP-PO-SHRT-CYC-IND             
           MOVE CW005-EFF-BEG-DTE        TO  WS-TMP-EFF-BEG-DTE                 
           MOVE CW005-CORP-RTL-AMT       TO  WS-TMP-CORP-RTL-AMT.               
           MOVE CW005-CORP-COST-AMT      TO  WS-TMP-CORP-COST-AMT.              
           MOVE CW005-CORP-GP-PRIC-ID    TO  WS-TMP-CORP-GP-PRIC-ID.            
                                                                                
                                                                                
      ******************************************************************        
      *  WRITE THE OUTPUT RECORD                                                
      ******************************************************************        
       7100-WRITE.                                                              
                                                                                
           WRITE  CHANGE-RECORD FROM CHANGE-OUTPUT-REC.                         
           ADD +1            TO PA-OUTPUT-RECORDS.                              
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE CURRENT FILE.                                        *        
      ******************************************************************        
       8000-READ-CURRENT.                                                       
                                                                                
           READ CURRENT-FILE INTO CW032-RECORD                                  
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  CR-CURR-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-CUR-RECORDS                     
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  CR-CURRENT-REC                    
                                                                                
           END-READ.                                                            
                                                                                
                                                                                
      ******************************************************************        
      *  READ THE PREVIOUS FILE.                                                
      ******************************************************************        
       8100-READ-PREVIOUS.                                                      
                                                                                
           READ PREVIOUS-FILE INTO CW032-RECORD                                 
                                                                                
               AT END                                                           
                   MOVE HIGH-VALUES  TO  PR-PREV-KEY-X                          
                                                                                
               NOT AT END                                                       
                   ADD 1           TO  PA-INPUT-RECORDS                         
                   ADD 1           TO  PA-INPUT-PRE-RECORDS                     
                                                                                
                   PERFORM 3000-REFORMAT-RMS-DATA                               
                   PERFORM 4000-MOVE-TO-TEMP                                    
                   MOVE WS-TEMP-REC       TO  PR-PREVIOUS-REC                   
                                                                                
           END-READ.                                                            
                                                                                
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '** RECORDS COUNTS FOR PROGRAM: ' PC-PROGRAM-NAME.           
           DISPLAY '   '.                                                       
                                                                                
           DISPLAY 'INPUT RECS READ    : ' PA-INPUT-RECORDS.                    
           DISPLAY 'INPUT PREVIOUS RECS: ' PA-INPUT-PRE-RECORDS.                
           DISPLAY 'INPUT CURRENT RECS : ' PA-INPUT-CUR-RECORDS.                
           DISPLAY '   '.                                                       
           DISPLAY 'OUTPUT RECS WRITTEN: ' PA-OUTPUT-RECORDS.                   
           DISPLAY 'INSERT RECS WRITTEN: ' PA-INSERT-RECORDS.                   
           DISPLAY 'UPDATE RECS WRITTEN: ' PA-UPDATE-RECORDS.                   
           DISPLAY 'DELETE RECS WRITTEN: ' PA-DELETE-RECORDS.                   
           DISPLAY 'NO CHANGE RECORDS  : ' PA-NO-CHANGE-RECORDS.                
                                                                                
           CLOSE CURRENT-FILE                                                   
                 PREVIOUS-FILE                                                  
                 CHANGE-FILE.                                                   
                                                                                
      ******************************************************************        
      ******************************************************************        
