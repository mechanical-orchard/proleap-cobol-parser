       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.     INKUT026.                                                
       AUTHOR.         JIM HUNTER.                                              
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   JULY, 2013.                                              
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * INKUT026 - RE-FORMAT THE PARTIAL SHIP FILE FOR INVENTORY                
      *             LEDGER UPDATE.                                              
      * THIS PROGRAM READS THE OUTPUT FILE FROM INK122 (A COPY OF               
      * INK142) WHICH IS IN MLRD030 COPYBOOK FORMAT.                            
      * IT REFORMATS IT INTO COPYBOOK INRD012, WHICH IS THE LAYOUT              
      * OF THE INPUT FILE INTO INK215.                                          
      * IT NEEDS TO CALL TDTATTR TO GET THE 4 FINANCIAL DATE FIELDS             
      * NEEDED FOR THE INRD012 LAYOUT THAT ARE NOT ON THE MLRD030               
      * LAYOUT.                                                                 
      *                                                                         
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.         IBM-3090.                                       
       OBJECT-COMPUTER.         IBM-3090.                                       
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT PARL-SHIP-IN-FILE                                            
                ASSIGN TO INP01.                                                
                                                                                
            SELECT OPTIONAL DATE-CTLCD-FILE                                     
                ASSIGN TO INP02.                                                
                                                                                
            SELECT PARL-SHIP-OUT-FILE                                           
                ASSIGN TO OUT01.                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   PARL-SHIP-IN-FILE                                                   
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS.                                           
       01   PARL-SHIP-IN-RECORD             PIC X(200).                         
                                                                                
       FD   DATE-CTLCD-FILE                                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS.                                           
       01   DATE-CTLCD-RECORD               PIC X(80).                          
                                                                                
       FD   PARL-SHIP-OUT-FILE                                                  
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F                                                 
            BLOCK CONTAINS 0 RECORDS.                                           
       01   PARL-SHIP-OUT-RECORD            PIC X(120).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01   ABEND-CODE                      PIC S9(04) VALUE ZERO               
                                                       COMP SYNC.               
                                                                                
       01   CC-DATE-CONTROL-CARD.                                               
            05  CC-CALENDAR-TYPE                PIC X(02).                      
            05  FILLER                          PIC X.                          
            05  CC-PROCESS-DATE                 PIC X(10).                      
            05  CC-PROCESS-DATE-ISO  REDEFINES                                  
                CC-PROCESS-DATE.                                                
                10  CC-PROCESS-CC               PIC X(02).                      
                10  CC-PROCESS-YY               PIC X(02).                      
                10  CC-PROCESS-DASH-1           PIC X(01).                      
                10  CC-PROCESS-MM               PIC X(02).                      
                10  CC-PROCESS-DASH-2           PIC X(01).                      
                10  CC-PROCESS-DD               PIC X(02).                      
            05  FILLER                          PIC X.                          
            05  CC-PROCESSING-MODE              PIC X(04).                      
                88  CC-PROCESSING-MODE-VALID    VALUE 'ML' 'INV' 'BOTH'.        
                88  CC-PROCESSING-MODE-ML       VALUE 'ML' 'BOTH'.              
                88  CC-PROCESSING-MODE-ML-ONLY  VALUE 'ML'.                     
                88  CC-PROCESSING-MODE-INV      VALUE 'INV' 'BOTH'.             
                88  CC-PROCESSING-MODE-INV-ONLY VALUE 'INV'.                    
            05  FILLER                          PIC X.                          
            05  CC-CHECK-INV-DATES-SW           PIC X.                          
                88  CC-CHECK-INV-DATES-SW-VALID VALUE 'Y' 'N'.                  
                88  CC-CHECK-INV-DATES          VALUE 'Y'.                      
                88  CC-DONT-CHECK-INV-DATES     VALUE 'N'.                      
            05  FILLER                          PIC X(60).                      
                                                                                
       01   PC-PROGRAM-CONSTANTS.                                               
            05  PC-PROGRAM-NAME             PIC X(08)  VALUE 'INKUT026'.        
                                                                                
       01   PS-PROGRAM-SWITCHES.                                                
            05  PS-DATE-CTLCD-EOF-SW        PIC X(01)  VALUE 'N'.               
                88  PS-NOT-END-OF-DATE-CTLCD-FILE      VALUE 'Y'.               
                88  PS-END-OF-DATE-CTLCD-FILE          VALUE 'Y'.               
                                                                                
            05  PS-PARL-SHIP-IN-EOF-SW      PIC X(01)  VALUE 'N'.               
                88  PS-NO-MORE-PARL-SHIPS              VALUE 'Y'.               
                                                                                
       01   PV-PROGRAM-VARIABLES.                                               
            05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACES.            
            05  PV-OPERATION-ATTEMPTED      PIC X(40)  VALUE SPACES.            
                                                                                
       01   PA-PROGRAM-ACCUMULATORS.                                            
            05  PA-INPUT-COUNT              PIC 9(7)   VALUE ZEROES.            
            05  PA-OUTPUT-COUNT             PIC 9(7)   VALUE ZEROES.            
                                                                                
      ****************************************************                      
      *   COMMUNICATION AREAS FOR DB2                     *                     
      ****************************************************                      
            EXEC SQL                                                            
                INCLUDE SQLCA                                                   
            END-EXEC.                                                           
                                                                                
      ****************************************************                      
      *   ERROR MESSAGE AREA FOR DB2                      *                     
      ****************************************************                      
            COPY DPWS004.                                                       
                                                                                
      ****************************************************                      
      * M/L OPEN FISCAL DATE INFORMATION TABLE            *                     
      ****************************************************                      
            EXEC SQL                                                            
                INCLUDE TMLCNTL                                                 
            END-EXEC.                                                           
                                                                                
      ****************************************************                      
      *   M/L DATE TABLE                                  *                     
      ****************************************************                      
            EXEC SQL                                                            
                INCLUDE TDTATTR                                                 
            END-EXEC.                                                           
                                                                                
      ******************************************************************        
      * MLRD030 - PURCHASE AND TRANSFER EXTRACT RECORD LAYOUT                   
      *          - THIS IS THE PARTIAL SHIPMENT INPUT FILE                      
      ******************************************************************        
            COPY MLRD030.                                                       
      *                                                                         
      *                                                                         
      ******************************************************************        
      * INRD012 - INVENTORY LEDGER DATABASE UPDATE TRANSACTION                  
      *          - THIS IS THE PARTIAL SHIPMENT OUTPUT FILE                     
      ******************************************************************        
            COPY INRD012.                                                       
      *                                                                         
      *                                                                         
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * PROGRAM MAINLINE                                                        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
                                                                                
            PERFORM 1000-INITIALIZATION.                                        
                                                                                
            PERFORM 2000-READ-PARL-SHIP-IN-FILE.                                
                                                                                
            PERFORM 3000-PROCESS-PARL-SHIP-FILE                                 
                UNTIL PS-NO-MORE-PARL-SHIPS.                                    
                                                                                
                                                                                
            PERFORM 8000-EOJ-ROUTINE.                                           
                                                                                
            STOP RUN.                                                           
                                                                                
      ******************************************************************        
      * PROCESS CONTROL CARD, INITIALIZE VARIABLES                              
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
            OPEN INPUT  DATE-CTLCD-FILE                                         
                        PARL-SHIP-IN-FILE                                       
                 OUTPUT PARL-SHIP-OUT-FILE.                                     
                                                                                
            PERFORM 1100-READ-DATE-CTLCD-FILE.                                  
            DISPLAY 'CONTROL CARD: ' CC-DATE-CONTROL-CARD.                      
                                                                                
            IF PS-NOT-END-OF-DATE-CTLCD-FILE                                    
            IF  CC-PROCESS-CC       IS NUMERIC                                  
            AND CC-PROCESS-YY       IS NUMERIC                                  
            AND CC-PROCESS-DASH-1 = '-'                                         
            AND CC-PROCESS-MM       IS NUMERIC                                  
            AND CC-PROCESS-DASH-2 = '-'                                         
            AND CC-PROCESS-DD       IS NUMERIC                                  
                CONTINUE                                                        
            ELSE                                                                
                MOVE SPACES TO CC-PROCESS-DATE                                  
            END-IF.                                                             
                                                                                
            PERFORM 1200-READ-CONTROL-TABLE.                                    
                                                                                
                                                                                
      ******************************************************************        
      * 1100-READ--DATE-CTLCD-FILE.                                             
      * - GET THE 1ST AND LAST WEEK END DATES FOR 13 WEEK HISTORY USING         
      *    THE CONTROL CARD DATE.                                               
      ******************************************************************        
       1100-READ-DATE-CTLCD-FILE.                                               
                                                                                
            READ DATE-CTLCD-FILE INTO CC-DATE-CONTROL-CARD                      
                AT END                                                          
                    DISPLAY '** PROGRAM ABEND **'                               
                    DISPLAY 'INKUT026  - CONTROL DATE EMPTY'                    
                    PERFORM 9999-ABEND-ROUTINE.                                 
                                                                                
            DISPLAY '** INKUT026 CONTROL CARD:' CC-DATE-CONTROL-CARD.           
                                                                                
                                                                                
      ******************************************************************        
      * READ THE DB2 CONTROL TABLE CONTAINING OPEN FISCAL DATE INFO             
      ******************************************************************        
       1200-READ-CONTROL-TABLE.                                                 
                                                                                
            IF CC-PROCESS-DATE = SPACES                                         
                DISPLAY ' USING ML CONTROL DATE '                               
                EXEC SQL                                                        
                    SELECT A.PSTG_FSCL_MN_DTE                                   
                          ,A.PSTG_FSCL_MN_NBR                                   
                          ,A.PSTG_FSCL_WK_DTE                                   
                          ,A.PSTG_FSCL_WK_NBR                                   
                          ,B.FSCL_MN_BEG_DTE                                    
                     INTO  :MLCNTL-PSTG-FSCL-MN-DTE                             
                          ,:MLCNTL-PSTG-FSCL-MN-NBR                             
                          ,:MLCNTL-PSTG-FSCL-WK-DTE                             
                          ,:MLCNTL-PSTG-FSCL-WK-NBR                             
                          ,:DTATTR-FSCL-MN-BEG-DTE                              
                     FROM  TMLCNTL A                                            
                          ,TDTATTR B                                            
                     WHERE KY_DTE = OPN_FSCL_MN_DTE                             
                     WITH UR                                                    
                END-EXEC                                                        
            ELSE                                                                
                DISPLAY ' USING CONTROL CARD DATE OF: ' CC-PROCESS-DATE         
                EXEC SQL                                                        
                    SELECT B.FSCL_MN_END_DTE                                    
                          ,B.FSCL_MN_NBR                                        
                          ,B.FSCL_WK_END_DTE                                    
                          ,B.FSCL_WK_NBR                                        
                          ,B.FSCL_MN_BEG_DTE                                    
                     INTO  :MLCNTL-PSTG-FSCL-MN-DTE                             
                          ,:MLCNTL-PSTG-FSCL-MN-NBR                             
                          ,:MLCNTL-PSTG-FSCL-WK-DTE                             
                          ,:MLCNTL-PSTG-FSCL-WK-NBR                             
                          ,:DTATTR-FSCL-MN-BEG-DTE                              
                     FROM                                                       
                           TDTATTR B                                            
                     WHERE KY_DTE = :CC-PROCESS-DATE                            
                     WITH UR                                                    
                END-EXEC                                                        
            END-IF.                                                             
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 0                                                          
                    DISPLAY 'CTRL TABLE POSTING MONTH END DATE : ',             
                                               MLCNTL-PSTG-FSCL-MN-DTE          
                    DISPLAY 'CTRL TABLE POSTING MONTH NUMBER   : ',             
                                               MLCNTL-PSTG-FSCL-MN-NBR          
                    DISPLAY 'CTRL TABLE POSTING WEEK DATE      : '              
                                               MLCNTL-PSTG-FSCL-WK-DTE          
                    DISPLAY 'CTRL TABLE POSTING WEEK NUMBER    : '              
                                               MLCNTL-PSTG-FSCL-WK-NBR          
                    DISPLAY 'BEGINNING FISCAL MONTH DATE       : '              
                                               DTATTR-FSCL-MN-BEG-DTE           
                WHEN OTHER                                                      
                    MOVE '1200-READ-CONTROL-TABLE' TO PV-PARAGRAPH-NAME         
                    MOVE 'UNABLE TO READ FISCAL DATES'                          
                                        TO PV-OPERATION-ATTEMPTED               
                    PERFORM 9999-SQL-ABEND                                      
            END-EVALUATE.                                                       
                                                                                
                                                                                
      ******************************************************************        
      * READ PARTIAL SHIP RECORDS IN PURCHASE RECORD FORMAT                     
      ******************************************************************        
       2000-READ-PARL-SHIP-IN-FILE.                                             
                                                                                
            READ PARL-SHIP-IN-FILE     INTO ML030-PURCH-RECORD                  
                AT END SET PS-NO-MORE-PARL-SHIPS TO TRUE.                       
                                                                                
                                                                                
      ******************************************************************        
      * FORMAT THE OUTPUT PARTIAL SHIP FILE                                     
      ******************************************************************        
       3000-PROCESS-PARL-SHIP-FILE.                                             
                                                                                
            ADD 1 TO PA-INPUT-COUNT.                                            
                                                                                
            INITIALIZE IN012-UPDATE-REC.                                        
            MOVE MLCNTL-PSTG-FSCL-MN-DTE    TO IN012-FSCL-MN-END-DTE.           
            MOVE MLCNTL-PSTG-FSCL-MN-NBR    TO IN012-FSCL-MN-NBR.               
            MOVE MLCNTL-PSTG-FSCL-WK-DTE    TO IN012-FSCL-WK-END-DTE.           
            MOVE MLCNTL-PSTG-FSCL-WK-NBR    TO IN012-FSCL-WK-NBR.               
            MOVE ML030-PURCH-DEPT-NBR       TO IN012-DEPT-NBR.                  
            MOVE ML030-PURCH-CLASS-NBR      TO IN012-SUB-CL-NBR.                
            MOVE ML030-PURCH-STORE-NBR      TO IN012-STR-NBR.                   
            MOVE ML030-PURCH-ML-LWST-ID     TO IN012-ML-LWST-ID.                
            MOVE ML030-PURCH-FIN-LIAB-DTE   TO IN012-FIN-LIAB-DTE.              
            MOVE ML030-PURCH-TRANS-CDE      TO IN012-TRN-TYP-CDE.               
                                                                                
            MOVE ML030-PURCH-EXTD-RTL-AMT   TO                                  
                                           IN012-STR-PARL-SHIP-RTL-AMT.         
                                                                                
            PERFORM 4000-WRITE-PARL-SHIP-OUT-REC.                               
                                                                                
            PERFORM 2000-READ-PARL-SHIP-IN-FILE.                                
                                                                                
                                                                                
      ******************************************************************        
      * CONTROL WRITING OF ALL RECORDS TO THE OUTPUT FILE                       
      ******************************************************************        
       4000-WRITE-PARL-SHIP-OUT-REC.                                            
                                                                                
            WRITE PARL-SHIP-OUT-RECORD    FROM IN012-UPDATE-REC.                
            ADD 1 TO PA-OUTPUT-COUNT.                                           
                                                                                
                                                                                
      ******************************************************************        
      * PROCESS FINAL COUNTS FOR INPUT AND OUTPUT COUNTS                        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
            CLOSE DATE-CTLCD-FILE                                               
                  PARL-SHIP-IN-FILE                                             
                  PARL-SHIP-OUT-FILE.                                           
                                                                                
            DISPLAY '                        '.                                 
            DISPLAY 'TOTAL INPUT  RECORDS  = ', PA-INPUT-COUNT.                 
            DISPLAY 'TOTAL OUTPUT RECORDS  = ', PA-OUTPUT-COUNT.                
            DISPLAY '                        '.                                 
            DISPLAY '***** GOOD EOJ *****'.                                     
                                                                                
      *                                                                         
      ******************************************************************        
      *   STANDARD DB2 ERROR ABEND ROUTINE                                      
      *   THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                    
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
            MOVE +4013                 TO ABEND-CODE.                           
            DISPLAY '**  ABEND     **'.                                         
            DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                     
            DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                   
            DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.              
                                                                                
      *     THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                
      *     DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.               
                                                                                
            COPY DPPD004.                                                       
                                                                                
            PERFORM 9999-ABEND-ROUTINE.                                         
                                                                                
      ******************************************************************        
      * COMMON ABEND MODULE                                                     
      ******************************************************************        
       9999-ABEND-ROUTINE.                                                      
                                                                                
                CALL 'ILBOABN0' USING ABEND-CODE.                               
                                                                                
