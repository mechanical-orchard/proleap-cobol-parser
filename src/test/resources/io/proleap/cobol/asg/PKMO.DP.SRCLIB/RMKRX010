/* REXX */                                                                      
/* RMKRX010 - Prepare Infopac Summary XREF records for all dynamic    */        
/*      distribution types tied to merchandise analysts.  Skip all    */        
/*      comment records in the file.  Department/Analyst keys can     */        
/*      either be explicitly listed to only give an analyst specific  */        
/*      departments, or the the department can be left blank to       */        
/*      dynamically assign all the departments tied to the related    */        
/*      buyer.  Buyer/analyst keys and analyst keys are created tied  */        
/*      to the buyer number.                                          */        
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX010 - '                                                        
ANALYST_FILE_END_OF_FILE = 'Y'                                                  
ANALYST_FILE_NOT_END_OF_FILE = 'N'                                              
COMMENT_LINE = '*'                                                              
XREF_TYPE_DEPT_ANALYST = 'DEPTANL'                                              
XREF_TYPE_BUYER_ANALYST = 'BYRANL_'                                             
XREF_TYPE_ANALYST = 'ANALYST'                                                   
USERID_NOT_SPECIFIED = '        '                                               
UNKNOWN_USERID = 'TK?     '                                                     
DEPARTMENT_NOT_SPECIFIED = '   '                                                
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
ANALYST_FILE_EOF = ANALYST_FILE_NOT_END_OF_FILE                                 
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM                                                                     
"EXECIO * DISKR INP01  (STEM DEPTFILE. FINIS)"                                  
IF RC /=0 & RC /= 2 THEN                                                        
DO                                                                              
  SAY CURRPGM 'ERROR' RC 'READING INP01 DATASET.'                               
  EXIT 4                                                                        
END                                                                             
CALL READ_ANALYST_RECORD                                                        
DO WHILE (ANALYST_FILE_EOF = ANALYST_FILE_NOT_END_OF_FILE)                      
    PARSE PULL INP02_RECORD                                                     
    IF (SUBSTR(INP02_RECORD,1,1) ^= COMMENT_LINE) THEN DO                       
        ANALYST_DEPT_NBR = SUBSTR(INP02_RECORD,1,3)                             
        ANALYST_BUYER_NBR = SUBSTR(INP02_RECORD,5,3)                            
        ANALYST_USERID = SUBSTR(INP02_RECORD,10,8)                              
/*                                                                    */        
/*   Do not generate dynamic distribution keys for anyone without     */        
/*   the minimum acceptable constraints on a userid.                  */        
/*                                                                    */        
        IF ((ANALYST_USERID ^= USERID_NOT_SPECIFIED) &,                         
                (ANALYST_USERID ^= UNKNOWN_USERID)) THEN DO                     
/*                                                                    */        
/*   Department/Analyst keys can either be explicitly listed to only  */        
/*   give an analyst specific departments, or the department can be   */        
/*   left blank to dynamically assign all the departments tied to     */        
/*   the related buyer.                                               */        
/*                                                                    */        
            IF (ANALYST_DEPT_NBR = DEPARTMENT_NOT_SPECIFIED) THEN,              
                CALL CREATE_SUM_XREF_FOR_ALL_BYR_DPTS                           
            ELSE DO                                                             
                SUM_XREF_RECORD = XREF_TYPE_DEPT_ANALYST || '  ' ||,            
                          LEFT(ANALYST_DEPT_NBR,10) ||,                         
                          ANALYST_USERID                                        
                CALL WRITE_SUM_XREF_RECORD                                      
            END                                                                 
            SUM_XREF_RECORD = XREF_TYPE_BUYER_ANALYST || '  ' ||,               
                              LEFT(ANALYST_BUYER_NBR,10) ||,                    
                              ANALYST_USERID                                    
            CALL WRITE_SUM_XREF_RECORD                                          
            SUM_XREF_RECORD = XREF_TYPE_ANALYST || '  ' ||,                     
                              LEFT(ANALYST_BUYER_NBR,10) ||,                    
                              ANALYST_USERID                                    
            CALL WRITE_SUM_XREF_RECORD                                          
        CALL WRITE_SUM_XREF_RECORD                                              
        END                                                                     
    END                                                                         
    CALL READ_ANALYST_RECORD                                                    
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_ANALYST_RECORD                                               */         
/*                                                                   */         
/* Read an Infopac merchandise analyst record.                       */         
/* ----------------------------------------------------------------- */         
READ_ANALYST_RECORD:                                                            
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN                                                              
        ANALYST_FILE_EOF = ANALYST_FILE_END_OF_FILE                             
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_SUM_XREF_FOR_ALL_BYR_DPTS                                  */         
/*                                                                   */         
/* Create Infopac Summary XREF records for all departments for       */         
/* which the related buyer is responsible.                           */         
/* ----------------------------------------------------------------- */         
CREATE_SUM_XREF_FOR_ALL_BYR_DPTS:                                               
    SAY 'START CREATE'                                                          
    DO I=1 TO DEPTFILE.0                                                        
        DEPTFILE_BUYER_NBR = SUBSTR(DEPTFILE.I,5,3)                             
        IF (DEPTFILE_BUYER_NBR = ANALYST_BUYER_NBR) THEN DO                     
            DEPTFILE_DEPT_NBR = SUBSTR(DEPTFILE.I,1,3)                          
            SUM_XREF_RECORD = XREF_TYPE_DEPT_ANALYST || '  ' ||,                
                      LEFT(DEPTFILE_DEPT_NBR,10) ||,                            
                      ANALYST_USERID                                            
            CALL WRITE_SUM_XREF_RECORD                                          
        END                                                                     
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_SUM_XREF_RECORD                                             */         
/*                                                                   */         
/* Write an Infopac Summary XREF record.                             */         
/* ----------------------------------------------------------------- */         
WRITE_SUM_XREF_RECORD:                                                          
    OUT01_RECORD = LEFT(SUM_XREF_RECORD,150)                                    
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
