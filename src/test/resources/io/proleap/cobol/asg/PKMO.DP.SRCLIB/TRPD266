000100****************************************************************          
000200* THIS IS EXECUTABLE COPYBOOK WHICH REQUIRES VARIABLES FROM               
000210* COPYBOOK TRRD266.                                                       
000300****************************************************************          
000400* FOR THE DECON CONTROL PANEL, DETERMINE BEGINING AND ENDING              
000500* DATE OF THE WEEK FOR THE WORKING DECONSOLIDATOR WINDOW.                 
000600* AS OF JULY 2004, THE WINDOW IS THURSDAY THRU WEDNESDAY.                 
000700****************************************************************          
000800* MOVE A DB2 DATE FORMAT YYYY-MM-DD TO PV-266-DATE-ENTERED.               
000810* ROUTINE WILL RETURN THE FIRST AND LAST DATE OF WEEK DEPENDING           
000820* ON HOW THE WORK WEEK IS SET UP, I.E. SUNDAY THRU SATURDAY.              
000830* (SEE COPYBOOK TRRD266 TO SEE CURRENT WORK WEEK SETUP.                   
000900****************************************************************          
036050 D266-GET-BEGIN-END-DATES.                                                
036051                                                                          
036052     SET PS-266-ERRORS-NO TO TRUE.                                        
036053                                                                          
036054     EXEC SQL                                                             
036055       SET :PV-266-DAY-OF-WEEK = DAYOFWEEK(:PV-266-DATE-ENTERED)          
036056     END-EXEC.                                                            
036058     EVALUATE TRUE                                                        
036060        WHEN SQLCODE = ZERO                                               
036061           CONTINUE                                                       
036062        WHEN OTHER                                                        
036063           DISPLAY '****** DAY OF WEEK DATE ERROR ******'                 
036064           DISPLAY 'PARAGRAPH - D266-GET-BEGIN-END-DATES'                 
036065           DISPLAY 'FOR DATE ENTERED - ' PV-266-DATE-ENTERED              
036066           DISPLAY ' '                                                    
036067     END-EVALUATE.                                                        
036068                                                                          
036069     COMPUTE PV-266-DAYS-BACK = PV-266-DAY-OF-WEEK                        
036070                    - PC-266-START-DAY-VAL.                               
036071                                                                          
036072     IF PV-266-DAYS-BACK < ZERO                                           
036073        ADD +7 TO PV-266-DAYS-BACK                                        
036074     END-IF.                                                              
036075                                                                          
036076     EXEC SQL                                                             
036077       SET :PV-266-BEGIN-DATE =                                           
036078              DATE(:PV-266-DATE-ENTERED) - :PV-266-DAYS-BACK DAYS         
036079     END-EXEC.                                                            
036081     EVALUATE TRUE                                                        
036082        WHEN SQLCODE = ZERO                                               
036083           CONTINUE                                                       
036084        WHEN OTHER                                                        
036085           DISPLAY '********* BEGIN DATE ERROR *********'                 
036086           DISPLAY 'PARAGRAPH - D266-GET-BEGIN-END-DATES'                 
036087           DISPLAY 'FOR DATE ENTERED - ' PV-266-DATE-ENTERED              
036088           DISPLAY ' '                                                    
036089     END-EVALUATE.                                                        
036090                                                                          
036091     EXEC SQL                                                             
036092       SET :PV-266-END-DATE =                                             
036093              DATE(:PV-266-BEGIN-DATE) + 6 DAYS                           
036094     END-EXEC.                                                            
036095     EVALUATE TRUE                                                        
036096        WHEN SQLCODE = ZERO                                               
036097           CONTINUE                                                       
036098        WHEN OTHER                                                        
036099           DISPLAY '********** END DATE ERROR **********'                 
036100           DISPLAY 'PARAGRAPH - D266-GET-BEGIN-END-DATES'                 
036101           DISPLAY 'FOR DATE ENTERED - ' PV-266-DATE-ENTERED              
036102           DISPLAY ' '                                                    
036103           SET PS-266-ERRORS TO TRUE                                      
036110     END-EVALUATE.                                                        
036200                                                                          
