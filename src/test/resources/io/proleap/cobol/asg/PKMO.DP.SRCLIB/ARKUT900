      ******************************************************************00010014
       IDENTIFICATION DIVISION.                                         00020014
      ******************************************************************00030014
                                                                        00040014
       PROGRAM-ID.             ARKUT900.                                00050014
       AUTHOR.                 JIM HUNTER.                              00060014
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070014
       DATE-WRITTEN            04-05-2019.                              00080014
       DATE-COMPILED.                                                   00090014
                                                                        00100014
      ******************************************************************00110014
      *   THIS PROGRAM WILL CREATE A FILE TO BE STORED IN A SPREADSHEET 00120014
      *   THAT THE CREDIT DEPT WILL ACCESS WHEN SOMEONE THAT RETIRED    00130014
      *   BEFORE 2015 LOSES THEIR LIFETIME DISCOUNT.                    00140014
      *   ACCOUNT NUMBER MUST BE DETOKENIZED.                           00150014
      *   EMPLOYEE NAME MUST BE DECRYPTED.                              00160014
      *   SOCIAL SECURITY NUMBER MUST BE DETOKENIZED.                   00170014
      ******************************************************************00180014
      *   05/01/2019  -  JIM HUNTER   CHG0222869    INITIAL CREATION    00190014
      *                  INITIAL PROGRAM CREATION                       00200014
      *   06/16/2023  -  BOB MCASEY   CHG0289805                        00201015
      *                  CHANGE CURSOR TO TERM_DTE IS NOT NULL          00202015
      ******************************************************************00210014
       ENVIRONMENT DIVISION.                                            00220014
      ******************************************************************00230014
                                                                        00240014
       CONFIGURATION SECTION.                                           00250014
                                                                        00260014
       SOURCE-COMPUTER.        IBM-3090.                                00270014
       OBJECT-COMPUTER.        IBM-3090.                                00280014
                                                                        00290014
       INPUT-OUTPUT SECTION.                                            00300014
                                                                        00310014
       FILE-CONTROL.                                                    00320014
                                                                        00330014
           SELECT EMPLOYEE-DISCOUNT-TERMS-FILE                          00340014
               ASSIGN TO OUT01.                                         00350014
                                                                        00360014
      ******************************************************************00370014
       DATA DIVISION.                                                   00380014
      ******************************************************************00390014
                                                                        00400014
       FILE SECTION.                                                    00410014
                                                                        00420014
       FD EMPLOYEE-DISCOUNT-TERMS-FILE                                  00430014
           RECORDING MODE IS F                                          00440014
           BLOCK CONTAINS 0 RECORDS                                     00450014
           LABEL RECORDS ARE STANDARD.                                  00460014
                                                                        00470014
       01 EMPLOYEE-DISCOUNT-TERMS-REC      PIC X(250).                  00480014
                                                                        00490014
       WORKING-STORAGE SECTION.                                         00500014
                                                                        00510014
      * KOHL'S CALENDAR ROUTINE                                         00520014
           COPY DPWS500.                                                00530014
      ******************************************************************00540014
      *                        ABEND CODES                              00550014
      ******************************************************************00560014
           COPY ARWS052.                                                00570014
                                                                        00580014
      ******************************************************************00590014
      *                        SWITCHES                                 00600014
      ******************************************************************00610014
       01 PS-PROGRAM-SWITCHES.                                          00620014
          05 PS-PROCESSING-SW               PIC X      VALUE SPACES.    00630014
             88 PS-CONTINUE-PROCESSING                 VALUE 'Y'.       00640014
             88 PS-STOP-PROCESSING                     VALUE 'N'.       00650014
          05 PS-SQL-STATUS-FLAG             PIC X      VALUE 'N'.       00660014
             88 PS-SQL-STATUS-OK                       VALUE 'Y'.       00670014
             88 PS-SQL-STATUS-NOT-OK                   VALUE 'N'.       00680014
      ******************************************************************00690014
      *                        CONSTANTS                                00700014
      ******************************************************************00710014
       01 PC-PROGRAM-CONSTANTS.                                         00720014
          05 PC-SUCCESSFUL-SQLCODE          PIC S9(9)  VALUE ZEROES     00730014
                                                       COMP.            00740014
          05 PC-ROW-NOT-FOUND-SQLCODE       PIC S9(9)  VALUE +100       00750014
                                                       COMP.            00760014
          05 PC-DUPLICATE-ROW-SQLCODE       PIC S9(9)  VALUE -803       00770014
                                                       COMP.            00780014
          05 PC-PROGRAM-NAME                PIC X(8)   VALUE 'ARKUT900'.00790014
          05 PC-TERM-DISC-STAT-N            PIC X(1)   VALUE 'N'.       00800014
          05 PC-TERM-DISC-STAT-L            PIC X(1)   VALUE 'L'.       00810014
                                                                        00820014
      ******************************************************************00830014
      *                        VARIABLES                                00840014
      ******************************************************************00850014
       01 PV-PROGRAM-VARIABLES.                                         00860014
          05 PV-TOTAL-READS                PIC S9(9) VALUE ZEROES COMP. 00870014
          05 PV-CURR-ACCT-PRE              PIC X(3)   VALUE SPACES.     00880014
          05 PV-CURR-DTE                   PIC X(10)  VALUE SPACES.     00890014
          05 PV-CURR-TMST                  PIC X(26)  VALUE SPACES.     00900014
          05 PV-DB2-TABLE-NAME             PIC X(30)  VALUE SPACES.     00910014
          05 PV-DISPLAY-VALUE              PIC 9(10)  VALUE ZEROES.     00920014
          05 PV-KEY-VALUE-NAME             PIC X(30)  VALUE SPACES.     00930014
          05 PV-KEY-VALUE                  PIC X(30)  VALUE SPACES.     00940014
          05 PV-OPERATION-MSG              PIC X(30)  VALUE SPACES.     00950014
          05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.     00960014
          05 PV-PGM-BEG-TMST               PIC X(26)  VALUE SPACES.     00970014
          05 PV-PGM-END-TMST               PIC X(26)  VALUE SPACES.     00980014
          05 PV-ACCT-NBR-12.                                            00990014
             10 PV-ACCT-NBR-10             PIC X(10)  VALUE SPACES.     01000014
             10 FILLER                     PIC X(02)  VALUE '00'.       01010014
          05 PV-SSN-NBR                    PIC X(09)  VALUE SPACES.     01020014
          05 PV-SSN-NBR-FORMAT.                                         01030014
             10 PV-SSN-1-3                 PIC X(3)   VALUE SPACES.     01040014
             10 FILLER                     PIC X(1)   VALUE '-'.        01050014
             10 PV-SSN-4-5                 PIC X(2)   VALUE SPACES.     01060014
             10 FILLER                     PIC X(1)   VALUE '-'.        01070014
             10 PV-SSN-6-9                 PIC X(4)   VALUE SPACES.     01080014
          05 PV-EMP-NM                     PIC X(88)  VALUE SPACES.     01090014
          05 PV-INPUT-TEXT                 PIC X(88) VALUE SPACES.      01100014
          05 PV-OUTPUT-TEXT                PIC X(66) VALUE SPACES.      01110014
          05 PV-EMP-ID                     PIC X(7)   VALUE SPACES.     01120014
          05 PV-INPUT-LEN                  PIC 9(09)  VALUE ZEROES.     01130014
                                                                        01140014
       01 INDICATOR-VARIABLES.                                          01150014
          05 IND-ART00001-CMNT-TXT         PIC S9(4) VALUE ZEROS COMP.  01160014
          05 IND-ED-DISC-DATE              PIC S9(4) VALUE ZEROS COMP.  01170014
          05 IND-ED-TERM-DATE              PIC S9(4) VALUE ZEROS COMP.  01180014
                                                                        01190014
       01 ED-EMP-DISC-HEADER-DATA.                                      01200014
           05  ED-EMP-NM-H             PIC X(50)  VALUE 'EMP NAME'.     01210014
           05  FILLER                  PIC X      VALUE ','.            01220014
           05  ED-ACCT-NBR-H           PIC X(10)  VALUE 'ACCT NBR'.     01230014
           05  FILLER                  PIC X      VALUE ','.            01240014
           05  ED-SSN-NBR-H            PIC X(11)  VALUE 'SSN NBR'.      01250014
           05  FILLER                  PIC X      VALUE ','.            01260014
           05  ED-EMP-ID-H             PIC X(11)  VALUE 'EMP ID'.       01270014
           05  FILLER                  PIC X      VALUE ','.            01280014
           05  ED-DISC-STAT-CDE-H      PIC X(11)  VALUE 'DISC STATUS'.  01290014
           05  FILLER                  PIC X      VALUE ','.            01300014
           05  ED-CMNT-TXT-H           PIC X(80)  VALUE 'COMMENTS'.     01310014
           05  FILLER                  PIC X      VALUE ','.            01320014
           05  ED-EMP-STAT-CDE-H       PIC X(10)  VALUE 'EMP STATUS'.   01330014
           05  FILLER                  PIC X      VALUE ','.            01340014
           05  ED-EMP-TYP-CDE-H        PIC X(8)   VALUE 'EMP TYPE'.     01350014
           05  FILLER                  PIC X      VALUE ','.            01360014
           05  ED-LOC-NBR-H            PIC X(4)   VALUE 'STR'.          01370014
           05  FILLER                  PIC X      VALUE ','.            01380014
           05  ED-DISC-DTE-H           PIC X(10)  VALUE 'DISC DATE'.    01390014
           05  FILLER                  PIC X      VALUE ','.            01400014
           05  ED-TERM-DTE-H           PIC X(10)  VALUE 'TERM DATE'.    01410014
           05  FILLER                  PIC X      VALUE ','.            01420014
           05  ED-LAST-UPD-DTE-H       PIC X(10)  VALUE 'LAST UPDT'.    01430014
           05  FILLER                  PIC X(16)  VALUE SPACES.         01440014
                                                                        01450014
       01 ED-EMP-DISC-DETAIL-DATA.                                      01460014
           05  ED-EMP-NM                   PIC X(50)  VALUE SPACES.     01470014
           05  FILLER                      PIC X      VALUE ','.        01480014
           05  ED-ACCT-NBR                 PIC X(10)  VALUE ZEROES.     01490014
           05  FILLER                      PIC X      VALUE ','.        01500014
           05  ED-SSN-NBR                  PIC X(11)  VALUE SPACES.     01510014
           05  FILLER                      PIC X      VALUE ','.        01520014
           05  ED-EMP-ID                   PIC X(11)  VALUE SPACES.     01530014
           05  FILLER                      PIC X      VALUE ','.        01540014
           05  ED-DISC-STAT-CDE            PIC X(1)   VALUE SPACES.     01550014
           05  FILLER                      PIC X      VALUE ','.        01560014
           05  ED-CMNT-TXT                 PIC X(80)  VALUE SPACES.     01570014
           05  FILLER                      PIC X      VALUE ','.        01580014
           05  ED-EMP-STAT-CDE             PIC X(1)   VALUE SPACES.     01590014
           05  FILLER                      PIC X      VALUE ','.        01600014
           05  ED-EMP-TYP-CDE              PIC X(1)   VALUE SPACES.     01610014
           05  FILLER                      PIC X      VALUE ','.        01620014
           05  ED-LOC-NBR                  PIC X(4)   VALUE SPACES.     01630014
           05  FILLER                      PIC X      VALUE ','.        01640014
           05  ED-DISC-DTE                 PIC X(10)  VALUE SPACES.     01650014
           05  FILLER                      PIC X      VALUE ','.        01660014
           05  ED-TERM-DTE                 PIC X(10)  VALUE SPACES.     01670014
           05  FILLER                      PIC X      VALUE ','.        01680014
           05  ED-LAST-UPD-DTE             PIC X(10)  VALUE SPACES.     01690014
           05  FILLER                      PIC X(40)  VALUE SPACES.     01700014
                                                                        01710014
      *                        COPYBOOKS                                01720014
      ******************************************************************01730014
                                                                        01740014
      ******************************************************************01750014
      *  VARIABLES TO CONVERT FROM BASE64 TO BINARY & VICE VERSA       *01760014
      ******************************************************************01770014
           COPY DPWS022.                                                01780014
                                                                        01790014
      ******************************************************************01800014
      *  VARIABLES TO CONVERT FROM ASCII TO EBCDIC & VICE VERSA        *01810014
      ******************************************************************01820014
           COPY DPWS053.                                                01830014
                                                                        01840014
      ******************************************************************01850014
      * LAYOUT THAT DEFINES THE PARAMETERS NEEDED TO INVOKE PROTEGRITY *01860014
      * MODULE                                                         *01870014
      ******************************************************************01880014
           COPY PTYCCSIP.                                               01890014
                                                                        01900014
      ******************************************************************01910014
      * LAYOUT THAT DEFINES THE WORKING VARIABLES TO INVOKE PROTEGRITY *01920014
      * MODULE                                                         *01930014
      ******************************************************************01940014
           COPY DPWS031.                                                01950014
                                                                        01960014
      ******************************************************************01970014
      * DPWS004 VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004 01980014
      ******************************************************************01990014
           COPY DPWS004.                                                02000014
                                                                        02010014
      ******************************************************************02020014
      * DB2 MESSAGE AREA                                                02030014
      ******************************************************************02040014
           COPY SQLCA2.                                                 02050014
                                                                        02060014
      ******************************************************************02070014
      *                     SQL DECLARATIONS                            02080014
      ******************************************************************02090014
                                                                        02100014
      ******************************************************************02110014
      *  STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                02120014
      ******************************************************************02130014
                                                                        02140014
           EXEC SQL                                                     02150014
               INCLUDE SQLCA                                            02160014
           END-EXEC.                                                    02170014
      ******************************************************************02180014
      *                     ART_EMP_DISC                                02190014
      ******************************************************************02200014
           EXEC SQL                                                     02210014
               INCLUDE ART00001                                         02220014
           END-EXEC.                                                    02230014
                                                                        02240014
      ******************************************************************02250014
      *                     CURSORS                                     02260014
      ******************************************************************02270014
                                                                        02280014
      *EMPLOYEE DISCOUNT CURSOR                                         02290014
           EXEC SQL                                                     02300014
               DECLARE EMP_DISC_CSR CURSOR FOR                          02310014
                  SELECT  ACCT_NBR                                      02320014
                         ,EMP_ID                                        02330014
                         ,DISC_STAT_CDE                                 02340014
                         ,LAST_UPD_DTE                                  02350014
                         ,CMNT_TXT                                      02360014
                         ,SSN_NBR                                       02370014
                         ,EMP_NM                                        02380014
                         ,EMP_STAT_CDE                                  02390014
                         ,EMP_TYP_CDE                                   02400014
                         ,LOC_NBR                                       02410014
                         ,DISC_DTE                                      02420014
                         ,TERM_DTE                                      02430014
                    FROM ART_EMP_DISC                                   02440014
                   WHERE DISC_STAT_CDE = 'L'                            02450014
0625RM              AND TERM_DTE IS NOT NULL                            02461015
                ORDER BY ACCT_NBR                                       02470014
           END-EXEC.                                                    02480014
      ******************************************************************02490014
       PROCEDURE DIVISION.                                              02500014
      ******************************************************************02510014
                                                                        02520014
       0000-MAINLINE.                                                   02530014
                                                                        02540014
           PERFORM 1000-INITIALIZE.                                     02550014
                                                                        02560014
           PERFORM 8000-OPEN-EMP-DISC-CSR.                              02570014
           PERFORM 8025-FETCH-EMP-DISC-CSR.                             02580014
                                                                        02590014
           PERFORM 2000-PROCESS-EMP-DISC-TERMS                          02600014
              UNTIL PS-STOP-PROCESSING.                                 02610014
                                                                        02620014
           PERFORM 8050-CLOSE-EMP-DISC-CSR.                             02630014
                                                                        02640014
           PERFORM 9000-END-PROC.                                       02650014
                                                                        02660014
           STOP RUN.                                                    02670014
                                                                        02680014
                                                                        02690014
       1000-INITIALIZE.                                                 02700014
           OPEN OUTPUT EMPLOYEE-DISCOUNT-TERMS-FILE.                    02710014
                                                                        02720014
           MOVE PC-PROGRAM-NAME    TO DP031-PROGRAM-NAME.               02730014
                                                                        02740014
           PERFORM 8600-ACCEPT-CURR-TMST.                               02750014
           MOVE PV-CURR-TMST       TO PV-PGM-BEG-TMST.                  02760014
                                                                        02770014
           MOVE ZEROES             TO ART00001-ACCT-NBR.                02780014
                                                                        02790014
           WRITE EMPLOYEE-DISCOUNT-TERMS-REC                            02800014
              FROM ED-EMP-DISC-HEADER-DATA.                             02810014
                                                                        02820014
           SET PS-CONTINUE-PROCESSING TO TRUE.                          02830014
                                                                        02840014
                                                                        02850014
       2000-PROCESS-EMP-DISC-TERMS.                                     02860014
           MOVE  ART00001-ACCT-NBR       TO PV-DISPLAY-VALUE.           02870014
           PERFORM 3000-DETOKENIZE-ACCT-NBR.                            02880014
           MOVE  DP031-CLEAR-TEXT (1:10) TO ED-ACCT-NBR.                02890014
           MOVE  'XXXXXX'                TO ED-ACCT-NBR (1:6).          02900014
           MOVE  ART00001-EMP-ID         TO ED-EMP-ID.                  02910014
           MOVE  ART00001-DISC-STAT-CDE  TO ED-DISC-STAT-CDE.           02920014
           MOVE  ART00001-LAST-UPD-DTE   TO ED-LAST-UPD-DTE.            02930014
           MOVE  ART00001-CMNT-TXT-TEXT  TO ED-CMNT-TXT.                02940014
           INSPECT ED-CMNT-TXT REPLACING ALL ',' BY ' '.                02950014
           MOVE  ART00001-SSN-NBR        TO PV-SSN-NBR.                 02960014
           PERFORM 4000-DETOKENIZE-SSN-NBR.                             02970014
           MOVE  PV-SSN-NBR-FORMAT       TO ED-SSN-NBR.                 02980014
           MOVE  ART00001-EMP-NM         TO PV-EMP-NM.                  02990014
           PERFORM 5000-DECRYPT-EMP-NM.                                 03000014
           MOVE  DP053-OUTPUT-FIELD      TO ED-EMP-NM.                  03010014
           INSPECT ED-EMP-NM REPLACING ALL ',' BY ' '.                  03020014
           MOVE  ART00001-EMP-STAT-CDE   TO ED-EMP-STAT-CDE.            03030014
           MOVE  ART00001-EMP-TYP-CDE    TO ED-EMP-TYP-CDE.             03040014
           MOVE  ART00001-LOC-NBR        TO ED-LOC-NBR.                 03050014
           MOVE  ART00001-DISC-DTE       TO ED-DISC-DTE.                03060014
           MOVE  ART00001-TERM-DTE       TO ED-TERM-DTE.                03070014
                                                                        03080014
           WRITE EMPLOYEE-DISCOUNT-TERMS-REC                            03090014
              FROM ED-EMP-DISC-DETAIL-DATA.                             03100014
                                                                        03110014
           PERFORM 8025-FETCH-EMP-DISC-CSR.                             03120014
                                                                        03130014
                                                                        03140014
       3000-DETOKENIZE-ACCT-NBR.                                        03150014
      *    COPIED FROM ARKUT063                                         03160014
           INITIALIZE CSI-PARAMETERS                                    03170014
                      DP031-PROTEG-PGM-PARAMETERS                       03180014
                      PV-ACCT-NBR-12.                                   03190014
                                                                        03200014
           MOVE ART00001-ACCT-NBR          TO PV-ACCT-NBR-10.           03210014
                                                                        03220014
           SET CSI-DECRYPT-FUNCTION        TO TRUE.                     03230014
           SET DP031-ACCTNBR               TO TRUE.                     03240014
           MOVE 'ACCT-NBR'                 TO DP031-FIELD.              03250014
           MOVE PV-ACCT-NBR-12             TO DP031-CIPHER-TEXT.        03260014
           MOVE 12                         TO DP031-INPUT-LEN.          03270014
                                                                        03280014
           PERFORM DP031-0000-PROTG-TOKEN-DETOKEN.                      03290014
                                                                        03300014
           IF CSI-RETURN-CODE NOT = ZERO                                03310014
              DISPLAY 'ACCOUNT NBR      : ' PV-DISPLAY-VALUE            03320014
              PERFORM DP031-9999-ABEND                                  03330014
           ELSE                                                         03340014
              MOVE DP031-CLEAR-TEXT(1:3)  TO PV-CURR-ACCT-PRE           03350014
           END-IF.                                                      03360014
                                                                        03370014
                                                                        03380014
       4000-DETOKENIZE-SSN-NBR.                                         03390014
      *    COPIED FROM ARKCS001                                         03400014
           INITIALIZE CSI-PARAMETERS                                    03410014
                      DP031-PROTEG-PGM-PARAMETERS                       03420014
                      PV-INPUT-LEN.                                     03430014
           INSPECT FUNCTION REVERSE(PV-SSN-NBR)                         03440014
                 TALLYING PV-INPUT-LEN FOR LEADING SPACES.              03450014
           COMPUTE PV-INPUT-LEN = LENGTH OF PV-SSN-NBR - PV-INPUT-LEN.  03460014
           IF PV-SSN-NBR NOT = SPACES AND PV-INPUT-LEN = 9              03470014
              MOVE 'SSN-NUMBER'        TO DP031-FIELD                   03480014
              SET CSI-DECRYPT-FUNCTION TO TRUE                          03490014
              SET DP031-SSN            TO TRUE                          03500014
              MOVE PV-INPUT-LEN        TO DP031-INPUT-LEN               03510014
              MOVE PV-SSN-NBR          TO DP031-CIPHER-TEXT             03520014
              PERFORM DP031-0000-PROTG-TOKEN-DETOKEN                    03530014
              MOVE DP031-CLEAR-TEXT    TO PV-SSN-NBR                    03540014
              MOVE 'XXX'               TO PV-SSN-1-3                    03550014
              MOVE 'XX'                TO PV-SSN-4-5                    03560014
              MOVE PV-SSN-NBR (6:4)    TO PV-SSN-6-9                    03570014
           ELSE                                                         03580014
              MOVE SPACES              TO PV-SSN-1-3                    03590014
                                          PV-SSN-4-5                    03600014
                                          PV-SSN-6-9                    03610014
           END-IF.                                                      03620014
                                                                        03630014
                                                                        03640014
       5000-DECRYPT-EMP-NM.                                             03650014
      *    COPIED FROM ARKCS001                                         03660014
           IF PV-EMP-NM = SPACES                                        03670014
               MOVE PV-EMP-NM           TO ED-EMP-NM                    03680014
           ELSE                                                         03690014
               PERFORM 5600-BASE64-BINARY                               03700014
               PERFORM 5700-DECRYPT-EMPNAME                             03710014
               PERFORM 5800-ASCII-EBCDIC                                03720014
           END-IF.                                                      03730014
                                                                        03740014
                                                                        03750014
       5600-BASE64-BINARY.                                              03760014
      * CONVERTS THE DATA FROM BASE64 TO BINARY FORMAT.                *03770014
                                                                        03780014
           INITIALIZE PV-INPUT-TEXT                                     03790014
                      PV-OUTPUT-TEXT                                    03800014
                      PV-INPUT-LEN.                                     03810014
                                                                        03820014
           INSPECT PV-EMP-NM TALLYING PV-INPUT-LEN                      03830014
                   FOR CHARACTERS BEFORE INITIAL SPACE.                 03840014
           MOVE PV-EMP-NM           TO PV-INPUT-TEXT.                   03850014
           MOVE PV-INPUT-LEN        TO DP022-INLEN.                     03860014
           MOVE 66                  TO DP022-OUTLEN-IN.                 03870014
           CALL  DP022-MGCXR2B  USING DP022-PARMS,                      03880014
                                      PV-INPUT-TEXT,                    03890014
                                      PV-OUTPUT-TEXT.                   03900014
           IF RETURN-CODE NOT EQUAL 0                                   03910014
               MOVE '5600-BASE64-BINARY'     TO PV-PARAGRAPH-NAME       03920014
               MOVE 'EMPLOYEE NAME '         TO PV-KEY-VALUE-NAME       03930014
               MOVE ED-EMP-NM                TO PV-KEY-VALUE            03940014
               MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME       03950014
               MOVE RETURN-CODE              TO DP031-RETURN-CODE       03960014
               STRING 'RC = '                DELIMITED BY SIZE          03970014
                   DP031-RETURN-CODE         DELIMITED BY SIZE          03980014
                                           INTO PV-OPERATION-MSG        03990014
               SET ABEND-4019-CAL-SUB-ERROR  TO TRUE                    04000014
               PERFORM 9900-ABEND                                       04010014
           END-IF.                                                      04020014
                                                                        04030014
                                                                        04040014
       5700-DECRYPT-EMPNAME.                                            04050014
      * CALLS THE PROTEGRITY MODULE TO DECRYPT THE EMPLOYEE NAME.       04060014
      * RETURNED EMPLOYEE NAME VALUE WILL BE IN ASCII FORMAT.           04070014
                                                                        04080014
           INITIALIZE CSI-PARAMETERS                                    04090014
                      DP031-PROTEG-PGM-PARAMETERS.                      04100014
           MOVE 'EMP-NAME'              TO DP031-FIELD.                 04110014
           SET CSI-DECRYPT-FUNCTION     TO TRUE.                        04120014
           SET DP031-CUST-INFO          TO TRUE.                        04130014
           MOVE DP022-OUTLEN-OUT        TO CSI-CIPHER-TEXT-LENGTH.      04140014
           MOVE 50                      TO CSI-CLEAR-TEXT-LENGTH.       04150014
           MOVE PV-OUTPUT-TEXT          TO DP031-CIPHER-TEXT.           04160014
           PERFORM DP031-1000-PROTG-ENCRYP-DECRYP.                      04170014
                                                                        04180014
                                                                        04190014
       5800-ASCII-EBCDIC.                                               04200014
      * CONVERTS THE DATA FROM ASCII TO EBCDIC FORMAT                   04210014
                                                                        04220014
           INITIALIZE DP053-EBCDIC-ASCII-PARMS.                         04230014
                                                                        04240014
           SET  DP053-CT-ASCII-TO-EBCDIC TO TRUE.                       04250014
           MOVE DP031-CLEAR-TEXT      TO DP053-INPUT-FIELD.             04260014
           MOVE CSI-CLEAR-TEXT-LENGTH TO DP053-FIELD-LENGTH.            04270014
                                                                        04280014
           CALL DP053-EBCDIC-ASCII-ROUTINE USING DP053-CONVERSION-TYPE  04290014
                                                 DP053-FIELD-LENGTH     04300014
                                                 DP053-INPUT-FIELD      04310014
                                                 DP053-OUTPUT-FIELD     04320014
                                                 DP053-RETURN-CODE.     04330014
           IF NOT DP053-RC-SUCCESSFUL                                   04340014
               MOVE '5800-ASCII-EBCDIC'      TO PV-PARAGRAPH-NAME       04350014
               MOVE 'EMPLOYEE NAME '         TO PV-KEY-VALUE-NAME       04360014
               MOVE ED-EMP-NM                TO PV-KEY-VALUE            04370014
               MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME       04380014
               MOVE DP053-RETURN-CODE        TO DP031-RETURN-CODE       04390014
               STRING 'RC = '                DELIMITED BY SIZE          04400014
                   DP031-RETURN-CODE         DELIMITED BY SIZE          04410014
                                           INTO PV-OPERATION-MSG        04420014
               SET ABEND-4019-CAL-SUB-ERROR  TO TRUE                    04430014
               PERFORM 9900-ABEND                                       04440014
           END-IF.                                                      04450014
                                                                        04460014
                                                                        04470014
       8000-OPEN-EMP-DISC-CSR.                                          04480014
           EXEC SQL                                                     04490014
               OPEN EMP_DISC_CSR                                        04500014
           END-EXEC.                                                    04510014
                                                                        04520014
           EVALUATE TRUE                                                04530014
              WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                      04540014
                 CONTINUE                                               04550014
              WHEN OTHER                                                04560014
                 MOVE '8000-OPEN-EMP-DISC-CSR' TO PV-PARAGRAPH-NAME     04570014
                 MOVE SPACES                   TO PV-KEY-VALUE-NAME     04580014
                 MOVE SPACES                   TO PV-KEY-VALUE          04590014
                 MOVE 'ART_EMP_DISC'           TO PV-DB2-TABLE-NAME     04600014
                 MOVE 'CURSOR OPEN ERROR'      TO PV-OPERATION-MSG      04610014
                 SET ABEND-4013-DB2-ERROR      TO TRUE                  04620014
                 PERFORM 9900-ABEND                                     04630014
           END-EVALUATE.                                                04640014
                                                                        04650014
                                                                        04660014
       8025-FETCH-EMP-DISC-CSR.                                         04670014
           INITIALIZE ART00001-CMNT-TXT                                 04680014
                      IND-ART00001-CMNT-TXT.                            04690014
                                                                        04700014
           EXEC SQL                                                     04710014
              FETCH EMP_DISC_CSR                                        04720014
               INTO :ART00001-ACCT-NBR                                  04730014
                   ,:ART00001-EMP-ID                                    04740014
                   ,:ART00001-DISC-STAT-CDE                             04750014
                   ,:ART00001-LAST-UPD-DTE                              04760014
                   ,:ART00001-CMNT-TXT:IND-ART00001-CMNT-TXT            04770014
                   ,:ART00001-SSN-NBR                                   04780014
                   ,:ART00001-EMP-NM                                    04790014
                   ,:ART00001-EMP-STAT-CDE                              04800014
                   ,:ART00001-EMP-TYP-CDE                               04810014
                   ,:ART00001-LOC-NBR                                   04820014
                   ,:ART00001-DISC-DTE                                  04830014
                   ,:ART00001-TERM-DTE                                  04840014
           END-EXEC.                                                    04850014
                                                                        04860014
                                                                        04870014
           EVALUATE TRUE                                                04880014
              WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                      04890014
                 ADD 1                  TO PV-TOTAL-READS               04900014
              WHEN SQLCODE = PC-ROW-NOT-FOUND-SQLCODE                   04910014
                 SET PS-STOP-PROCESSING TO TRUE                         04920014
              WHEN OTHER                                                04930014
                 MOVE '8025-FETCH-EMP-DISC-CSR' TO PV-PARAGRAPH-NAME    04940014
                 MOVE 'ART00001-ACCT-NBR'       TO PV-KEY-VALUE-NAME    04950014
                 MOVE ART00001-ACCT-NBR         TO PV-DISPLAY-VALUE     04960014
                 MOVE PV-DISPLAY-VALUE          TO PV-KEY-VALUE         04970014
                 MOVE 'ART_EMP_DISC'            TO PV-DB2-TABLE-NAME    04980014
                 MOVE 'CURSOR FETCH ERROR'      TO PV-OPERATION-MSG     04990014
                 SET ABEND-4013-DB2-ERROR       TO TRUE                 05000014
                 PERFORM 9900-ABEND                                     05010014
           END-EVALUATE.                                                05020014
                                                                        05030014
                                                                        05040014
       8050-CLOSE-EMP-DISC-CSR.                                         05050014
           EXEC SQL                                                     05060014
               CLOSE EMP_DISC_CSR                                       05070014
           END-EXEC.                                                    05080014
                                                                        05090014
           EVALUATE TRUE                                                05100014
              WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                      05110014
                 CONTINUE                                               05120014
              WHEN OTHER                                                05130014
                 MOVE '8050-CLOSE-EMP-DISC-CSR' TO PV-PARAGRAPH-NAME    05140014
                 MOVE SPACES                    TO PV-KEY-VALUE-NAME    05150014
                 MOVE SPACES                    TO PV-KEY-VALUE         05160014
                 MOVE 'ART_EMP_DISC'            TO PV-DB2-TABLE-NAME    05170014
                 MOVE 'CURSOR CLOSE ERROR'      TO PV-OPERATION-MSG     05180014
                 SET ABEND-4013-DB2-ERROR       TO TRUE                 05190014
                 PERFORM 9900-ABEND                                     05200014
           END-EVALUATE.                                                05210014
                                                                        05220014
                                                                        05230014
       8600-ACCEPT-CURR-TMST.                                           05240014
           EXEC SQL                                                     05250014
               SET :PV-CURR-TMST = CURRENT TIMESTAMP                    05260014
           END-EXEC                                                     05270014
                                                                        05280014
           EVALUATE TRUE                                                05290014
              WHEN SQLCODE = PC-SUCCESSFUL-SQLCODE                      05300014
                 MOVE PV-CURR-TMST               TO PV-CURR-DTE         05310014
              WHEN SQLCODE NOT = PC-SUCCESSFUL-SQLCODE                  05320014
              WHEN SQLWARN0 = 'W'                                       05330014
                 MOVE '8600-ACCEPT-CURR-TMST' TO PV-PARAGRAPH-NAME      05340014
                 MOVE 'CURRENT TIMESTAMP'      TO PV-KEY-VALUE-NAME     05350014
                 MOVE PV-CURR-TMST             TO PV-KEY-VALUE          05360014
                 MOVE 'N/A'                    TO PV-DB2-TABLE-NAME     05370014
                 MOVE 'TMST RETRIEVAL ERROR'   TO PV-OPERATION-MSG      05380014
                 SET ABEND-4013-DB2-ERROR      TO TRUE                  05390014
                 PERFORM 9900-ABEND                                     05400014
           END-EVALUATE.                                                05410014
                                                                        05420014
                                                                        05430014
       9000-END-PROC.                                                   05440014
           CLOSE EMPLOYEE-DISCOUNT-TERMS-FILE.                          05450014
                                                                        05460014
           PERFORM 8600-ACCEPT-CURR-TMST.                               05470014
           MOVE PV-CURR-TMST      TO PV-PGM-END-TMST.                   05480014
                                                                        05490014
           DISPLAY ' '.                                                 05500014
           DISPLAY '**********************'.                            05510014
           DISPLAY '   ARKUT900 CONTROLS  '.                            05520014
           DISPLAY '**********************'.                            05530014
           DISPLAY 'BEG DATE/TMST = ' PV-PGM-BEG-TMST.                  05540014
           DISPLAY 'END DATE/TMST = ' PV-PGM-END-TMST.                  05550014
           DISPLAY '======================'.                            05560014
           DISPLAY ' '.                                                 05570014
           DISPLAY 'EMP DISC READS:   ' PV-TOTAL-READS.                 05580014
                                                                        05590014
                                                                        05600014
       9900-ABEND.                                                      05610014
           IF ABEND-4013-DB2-ERROR                                      05620014
                                                                        05630014
              PERFORM 9950-DISPLAY-SQLCA                                05640014
                                                                        05650014
              MOVE SQLCODE    TO SQLCODE2                               05660014
              MOVE SQLERRD(3) TO SQLERRD3                               05670014
                                                                        05680014
              DISPLAY '** ABEND           ** = SQLABEND'                05690014
              DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME        05700014
              DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME      05710014
              DISPLAY '** KEY VALUE NAME  ** = ' PV-KEY-VALUE-NAME      05720014
              DISPLAY '** KEY VALUE       ** = ' PV-KEY-VALUE           05730014
              DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME      05740014
              DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG       05750014
              DISPLAY '** SQLCODE         ** = ' SQLCODE2               05760014
              DISPLAY '** ROWS PROCESSED  ** = ' SQLERRD3               05770014
              DISPLAY '** SQLERRM         ** = ' SQLERRM                05780014
              DISPLAY '** SQLERRMC        ** = ' SQLERRMC               05790014
           ELSE                                                         05800014
              DISPLAY '** ABEND           ** = ABEND (NON-SQL)'         05810014
              DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME        05820014
              DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME      05830014
              DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG       05840014
              DISPLAY '** KEY VALUE NAME  ** = ' PV-KEY-VALUE-NAME      05850014
              DISPLAY '** KEY VALUE       ** = ' PV-KEY-VALUE           05860014
           END-IF.                                                      05870014
           CALL 'ILBOABN0' USING ABEND-CODE.                            05880014
                                                                        05890014
                                                                        05900014
       9950-DISPLAY-SQLCA.                                              05910014
      ******************************************************************05920014
      *  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   05930014
      *  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        05940014
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     05950014
      *  FORMAT.                                                        05960014
      ******************************************************************05970014
                                                                        05980014
              COPY DPPD004.                                             05990014
                                                                        06000014
      ******************************************************************06010014
      *  COPY MEMBER DPPD031 CONTAINS STATEMENTS NEEDED TO INVOKE       06020014
      *  PROTEGRITY MODULE FOR PROTECTION/UNPROTECTION                  06030014
      ******************************************************************06040014
              COPY DPPD031.                                             06050014
                                                                        06060014
      ******************************************************************06070014
