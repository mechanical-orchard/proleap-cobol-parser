       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    VAKUP313                                                  
       AUTHOR.        JEFF HARTIG/COMPUWARE.                                    
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  6-11-98.                                                  
       DATE-COMPILED.                                                           
                                                                                
W34517******************************************************************        
W34517*                                                                         
W34517*               !!!  I M P O R T A N T  !!!                               
W34517*                                                                         
W34517*                                                                         
W34517*    !!!  THIS PROGRAM MUST BE KEPT IN SYNC WITH INKUP313  !!!            
W34517*                                                                         
W34517*                                                                         
W34517*                                                                         
      ******************************************************************        
      * VAKUP313 - INVENTORY LEDGER SHRINK & END INV AMOUNT RECALC              
W34517* THIS PROGRAM PERFORMS UPDATES FOR THE BRAND VENDOR INVENTORY            
W34517* LEDGER FINANCIALS. ANY CHANGES MADE TO THIS PROGRAM MUST ALSO           
W34517* BY MADE TO THE BRAND VENDOR VERSION OF THIS PROGRAM INKUP313.           
W34517*                                                                         
W34517* CHANGE MADE: CREATE SPLIT PROCESS FOR VA VERSION OF INV LEDGER.         
W34517*              WR34517  PATTY JAEGER - OCT. 14, 2008 INSTALL DATE         
W34517******************************************************************        
      *  THIS PROGRAM WILL RECALCULATE THE FOLLOWING FIELDS ON TINSCLS *        
      *    SHNK_RTL_AMT     - RETAIL SHRINK                            *        
      *    END_INV_RTL_AMT  - RETAIL ENDING INVENTORY                  *        
      **                                                              **        
W33304* TINCNTL(OPN FSCL MTH) WILL BE USED TO DETERMINE WHICH MONTH'S *         
      *    ROW WILL BE UPDATED/RECALCULATED AND USED TO OBTAIN THE     *        
      *    PREVIOUS MONTH AND SEASON END DATES.                        *        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *          10/03/99    JULIE SEIDLER                                      
      *                      CHANGES MADE TO OBTAIN THE SHRINK RETAIL  *        
      *                      AMOUNT FROM THE TINSHNK DB2 TABLE INSTEAD *        
      *                      OF CALCULATING THE AMOUNT USING THE       *        
      *                      TDPTSHK TABLE.                            *        
      * 21591    03-01-2002  MANIFEST PROJECT CHANGE FOR IN-TRANSIT    *        
      *                      REVERSAL FROM INVENTORY LEDGER            *        
W26600* W26600  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT           
W27808* W27808  05-24-2005  S. JACKSON NEW MANIFESTING / STORE-EXPECTED         
W28582* W28582  12-27-2005  C. SCHWENN CHANGES FOR BRAND/VENDOR PROJECT         
W34517* W34517  07-29-2008  PATTY JAEGER BRAND VENDOR CHANGES - VA SPLIT        
W33304* W33304  09-24-2008  NIJESH/INFOSYS VIGNESH/INFOSYS                      
W33304*                     HOLDING INVENTORY LEDGER OPEN - HILO                
F33304* F33304  09-21-2009  FIX FOR PROD INCIDENT INC000000961180               
F33304*                     JILL LIN                                            
P33304* P33304  11-10-2009  PRITHIVIRAJ SUBRAMANIAN                             
P33304* P33304              FIX FOR PROD INCIDENT INC000001032396               
TA5801* TA5801  11-10-2010  THASLIM ARIFF                                       
TA5801*                     FIX FOR DATA INTEGRITY ERRORS                       
P42135* P42135  11-10-2013  COGNIZANT                                           
P42135*                     MODIFIED THE PROGRAM LOGIC TO FETCH THE             
P42135*                     SUM OF SHRINK DOLLARS WHEN THERE IS                 
P42135*                     CHANGE IN POSTING DATE ALSO.                        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.        
                                                                                
       01  PA-PROGRAM-ACCUMULATOR.                                              
           05  PA-DEADLOCK-COUNT               PIC  9(04) VALUE ZEROES.         
           05  PA-DEPTS-PROCESSED              PIC  9(09) VALUE ZEROES.         
           05  PA-COMMIT-COUNT                 PIC  9(09) VALUE ZEROES.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CONTROL-CARD-EMPTY-SW        PIC X     VALUE 'N'.             
               88  PS-EMPTY-CONTROL-FILE                 VALUE 'Y'.             
           05  PS-FIRST-TIME-SW                PIC X     VALUE 'N'.             
               88  PS-FIRST-TIME-THRU                    VALUE 'Y'.             
           05  PS-FIRST-COMMIT-SW              PIC X     VALUE 'N'.             
               88  PS-FIRST-COMMIT                       VALUE 'Y'.             
           05  PS-RECALC-CURSOR-SW             PIC X     VALUE 'N'.             
               88  PS-END-OF-RECALC-CURSOR               VALUE 'Y'.             
           05  PS-PROGRAM-DEADLOCK-SW          PIC X     VALUE 'N'.             
               88  PS-PROGRAM-DEADLOCKED                 VALUE 'Y'.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'VAK220'.            
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'VAKUP313'.          
           05  PC-MAX-DEADLOCKS        PIC S9(03)    VALUE +3.                  
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                'CONTROL CARD IS EMPTY        '.                                
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-06                       PIC X(30) VALUE                  
                'ERROR ON UPDATE OF TINCLS    '.                                
           05  PE-MSG-07                       PIC X(50) VALUE                  
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-08                       PIC X(50) VALUE                  
                'UPDATE WORK_STRG - TCKRSTINF '.                                
           05  PE-MSG-09                       PIC X(50) VALUE                  
                'PGM ABEND DURING A COMMIT    '.                                
           05  PE-MSG-10                       PIC X(50) VALUE                  
                'UPDATE OF TCKRSTCNTL FAILED  '.                                
           05  PE-MSG-11                       PIC X(50) VALUE                  
                'ERROR IN SELECT - TCKRSTINF  '.                                
           05  PE-MSG-12                       PIC X(50) VALUE                  
                'MAXIMUM DEADLOCK CNT EXCEEDED'.                                
           05  PE-MSG-13                       PIC X(50) VALUE                  
                'UNABLE TO OBTAIN TIMESTAMP   '.                                
           05  PE-MSG-14                       PIC X(50) VALUE                  
                'SELECT TO TINSHNK            '.                                
W20445     05  PE-MSG-16                       PIC X(30) VALUE                  
W20445          'ERROR SELECTING COUNT OF ROWS.'.                               
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-RETRY-COUNT               PIC S9(04) COMP SYNC                
                                                       VALUE ZERO.              
           05  PC-MAX-RETRIES               PIC S9(04) COMP SYNC                
                                                       VALUE +3.                
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES.         
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES.         
           05  PV-OPERATION-ATTEMPTED          PIC  X(30) VALUE SPACES.         
           05  PV-TINSHNK-ROW-NOT-FOUND        PIC  9(09) VALUE ZEROES.         
           05  PV-TINSCLS-UPDATE-VARIABLES.                                     
               10  PV-HOLD-FISCAL-NBR          PIC  X(02) VALUE SPACES.         
               10  PV-HOLD-FISCAL-MN-END-DTE   PIC  X(10) VALUE SPACES.         
               10  PV-HOLD-DEPT-NBR            PIC  X(3)  VALUE SPACES.         
               10  PV-HOLD-SUB-CL-NBR          PIC  X(02) VALUE SPACES.         
               10  PV-HOLD-STR-NBR             PIC  S9(04) COMP.                
W28582         10  PV-HOLD-IN-LWST-ID          PIC  S9(09) COMP.                
W26600     05  PV-SHNK-RTL-AMT          PIC S9(11)V99 COMP-3 VALUE +0.          
W20445     05  PV-INSCLS-SHNK-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE +0.          
W20445     05  PV-INSHNK-SHNK-RTL-AMT   PIC S9(11)V99 COMP-3 VALUE +0.          
W28582     05  PV-IN-LWST-PCT-SALES  PIC S9(04)V9(07) COMP-3 VALUE +0.          
W28582     05  PV-IN-LWST-COUNT         PIC S9(04)    COMP-3 VALUE +0.          
W28582     05  PV-IN-LWST-PROCESS-COUNT PIC S9(04)    COMP-3 VALUE +0.          
W28582     05  PV-IN-LWST-SALES-AMT     PIC S9(11)V99 COMP-3 VALUE +0.          
W20445     05  PV-INSCLS-SALES-AMT      PIC S9(11)V99 COMP-3 VALUE +0.          
W20445     05  PV-INSCLS-SHNK-DIFF      PIC S9(11)V99 COMP-3 VALUE +0.          
W20445     05  PV-SALES-NULL-IND        PIC S9(04) COMP VALUE ZEROS.            
W28582     05  PV-IN-LWST-NULL-IND      PIC S9(04) COMP VALUE ZEROS.            
W20445     05  PV-SHNK-NULL-IND        PIC S9(04) COMP VALUE ZEROS.             
W33304     05  PV-MIN-INCNTL-OPN-FSCL-MN-DTE   PIC X(10) VALUE SPACES.          
W33304     05  PV-MAX-INCNTL-OPN-FSCL-MN-DTE   PIC X(10) VALUE SPACES.          
W33304     05  PV-MIN-INCNTL-OPN-FSCL-MN-NBR   PIC X(02) VALUE SPACES.          
W33304     05  PV-MAX-INCNTL-OPN-FSCL-MN-NBR   PIC X(02) VALUE SPACES.          
W33304     05  PV-NULL                         PIC S9(04)  VALUE +0             
W33304                                                      COMP SYNC.          
P42135     05  PV-SAVE-ML-PSTG-MN-END-DTE      PIC X(10) VALUE SPACES.          
                                                                                
      ****************************************************                      
W33304*  DB2 TINCNTL TABLE                               *                      
      ****************************************************                      
           EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
W33304****************************************************                      
W33304*  DB2 TINVPAR TABLE                               *                      
W33304****************************************************                      
W33304     EXEC SQL                                                             
W33304         INCLUDE TINVPAR                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
F33304****************************************************                      
F33304*  DB2 TMLCNTL TABLE                               *                      
F33304****************************************************                      
P33304*    EXEC SQL                                                             
P33304*        INCLUDE TMLCNTL                                                  
P33304*    END-EXEC.                                                            
P33304                                                                          
      ****************************************************                      
      *  DB2 WEEKLY SHRINK                                                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TINSHNK                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  INVENTORY LEDGER TABLE                          *                      
      ****************************************************                      
           EXEC SQL                                                             
W20445         INCLUDE TINSCLS                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  M/L DATE TABLE                                  *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
      *********** THIS CURSOR WILL DRIVE PROGRAM PROCESSING ************        
           EXEC SQL                                                             
               DECLARE RECALC_UPDATE_CSR CURSOR WITH HOLD FOR                   
W33304          SELECT  A.FSCL_MN_NBR                                           
W33304                 ,A.FSCL_MN_END_DTE                                       
W33304                 ,A.DEPT_NBR                                              
W33304                 ,A.SUB_CL_NBR                                            
W33304                 ,A.STR_NBR                                               
W33304                 ,A.IN_LO_MDSE_LVL_ID                                     
W33304                 ,A.SLS_RTL_AMT                                           
W33304                 ,A.MKDN_RTL_AMT                                          
W33304                 ,A.MKUP_RTL_AMT                                          
W33304                 ,A.XFER_IN_RTL_AMT                                       
W33304                 ,A.XFER_OUT_RTL_AMT                                      
W33304                 ,A.RCPT_RTL_AMT                                          
W33304                 ,A.PADJ_RTL_AMT                                          
OCFSFS                 ,A.STR_PARL_SHIP_RTL_AMT                                 
W33304                 ,A.CTOFF_END_INV_AMT                                     
W33304                 ,A.STR_EXPTED_RTL_AMT                                    
W33304                 ,A.ML_PSTG_MN_END_DTE                                    
W33304           FROM TINSCLS A                                                 
W33304               ,TINVPAR B                                                 
W33304               ,TINCNTL C                                                 
F33304           WHERE A.FSCL_MN_NBR        = C.OPN_FSCL_MN_NBR                 
F33304             AND A.FSCL_MN_END_DTE    = C.OPN_FSCL_MN_DTE                 
      * CHECKPOINT RESTART AFTER LAST ROW PROCESSED                             
W33304             AND A.DEPT_NBR           > :PV-HOLD-DEPT-NBR                 
W33304             AND A.STR_NBR            = B.LOC_NBR                         
W33304             AND B.ACTL_FIN_BK_DTE    = '9999-09-09'                      
W33304             AND B.LOC_INV_STAT_CDE   = 'IN'                              
W33304             AND B.INV_ID             = C.INV_ID                          
W33304             AND C.ACTV_IND           = 'Y'                               
W33304       ORDER BY A.DEPT_NBR, A.SUB_CL_NBR,                                 
TA5801*               A.STR_NBR, A.IN_LO_MDSE_LVL_ID                            
P42135            A.STR_NBR, A.ML_PSTG_MN_END_DTE, A.IN_LO_MDSE_LVL_ID          
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
W20445     05  CR-AREA-LEN                  PIC S9(04) VALUE +34  COMP.         
           05  CR-CHECKPOINT-RESTART-VAR.                                       
W20445         10  CR-PREV-DTL-KEY          PIC  X(23) VALUE SPACES.            
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
                   15  CR-FISCAL-NBR        PIC  X(02).                         
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                         
                   15  CR-DEPT-NBR          PIC  X(03).                         
                   15  CR-SUB-CL-NBR        PIC  X(02).                         
                   15  CR-STR-NBR           PIC  S9(04) COMP.                   
W28582             15  CR-IN-LWST-ID        PIC  S9(09) COMP.                   
               10  CR-COMMIT-COUNT          PIC  9(09) VALUE ZEROES.            
                                                                                
      ****************************************************                      
      *  ERROR MESSAGE AREA FOR DB2                      *                      
      ****************************************************                      
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-END-OF-RECALC-CURSOR.                                  
                                                                                
           PERFORM 8000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
      * RETRIEVE DATES  / PREPARE RESTART INFORMATION                           
P33304*    PERFORM 3000-READ-TMLCNTL.                                           
           PERFORM 4000-READ-OPN-FSCL-MTH.                                      
           DISPLAY 'OPEN FISCAL MONTH: '                                        
W33304         PV-MIN-INCNTL-OPN-FSCL-MN-DTE ' TO '                             
W33304         PV-MAX-INCNTL-OPN-FSCL-MN-DTE                                    
                   '    OPEN FISCAL MONTH NBR:  '                               
W33304         PV-MIN-INCNTL-OPN-FSCL-MN-NBR ' TO '                             
W33304         PV-MAX-INCNTL-OPN-FSCL-MN-NBR.                                   
W33304     MOVE PV-MIN-INCNTL-OPN-FSCL-MN-DTE TO CR-FISCAL-MN-END-DTE.          
                                                                                
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
                PERFORM 7700-SELECT-RESTART-INFO                                
                MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                           
                                              CR-CHECKPOINT-RESTART-VAR         
                DISPLAY 'RESTART - LAST DEPT PROCESSED : ', CR-DEPT-NBR         
W20445          DISPLAY '          LAST SUB CLASS      : ',                     
W20445                                 CR-SUB-CL-NBR                            
W20445          DISPLAY '          LAST LOCATION       : ',                     
W20445                                 CR-STR-NBR                               
                MOVE CR-DEPT-NBR    TO PV-HOLD-DEPT-NBR                         
W20445          MOVE CR-SUB-CL-NBR  TO PV-HOLD-SUB-CL-NBR                       
W20445          MOVE CR-STR-NBR     TO PV-HOLD-STR-NBR                          
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'        
           END-IF.                                                              
                                                                                
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
                                                                                
      * OPEN RECALC CURSOR & PERFORM PRIMER READ                                
           PERFORM 7100-OPEN-RECALC-CURSOR.                                     
           PERFORM 7150-PROCESS-RECALC-CURSOR.                                  
                                                                                
           IF  PS-END-OF-RECALC-CURSOR                                          
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESSED AFTER RESTART'              
               ELSE                                                             
                   DISPLAY 'NO RECORDS TO SELECTED FOR RECALCULATION'           
               END-IF                                                           
           END-IF.                                                              
                                                                                
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
                                                                                
W28582     MOVE ZEROS             TO PV-IN-LWST-PCT-SALES                       
W28582                               PV-IN-LWST-COUNT                           
W28582                               PV-IN-LWST-PROCESS-COUNT                   
W20445                               PV-INSCLS-SALES-AMT                        
W20445                               PV-INSCLS-SHNK-DIFF                        
W28582                               PV-IN-LWST-SALES-AMT.                      
W20445                                                                          
      ******************************************************************        
      * MAIN PROCESSING PARAGRAPH - PERFORMED UNTIL ALL ROWS ON TINSCLS         
      *      HAVE BEEN UPDATED (IE. END OF CURSOR PROCESSING)                   
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
W28582     ADD 1                    TO PV-IN-LWST-PROCESS-COUNT.                
W20445                                                                          
           MOVE 'N' TO PS-PROGRAM-DEADLOCK-SW.                                  
                                                                                
W20445*    IF INSCLS-DEPT-NBR = PV-HOLD-DEPT-NBR                                
W20445*        CONTINUE                                                         
W20445*    ELSE                                                                 
W20445*       IF NOT PS-FIRST-TIME-THRU                                         
W20445*           PERFORM 7800-COMMIT-PROCESSING                                
W20445*       END-IF                                                            
W20445*       MOVE INSCLS-FSCL-MN-NBR       TO PV-HOLD-FISCAL-NBR               
W20445*       MOVE INSCLS-FSCL-MN-END-DTE   TO PV-HOLD-FISCAL-MN-END-DTE        
W20445*       MOVE INSCLS-DEPT-NBR          TO PV-HOLD-DEPT-NBR                 
W20445*       MOVE INSCLS-SUB-CL-NBR        TO PV-HOLD-SUB-CL-NBR               
W20445*       MOVE INSCLS-STR-NBR           TO PV-HOLD-STR-NBR                  
W20445*    END-IF.                                                              
W20445*                                                                         
W20445*    IF PS-FIRST-TIME-THRU                                                
W20445*        MOVE 'N' TO PS-FIRST-TIME-SW                                     
W20445*    END-IF.                                                              
W20445*                                                                         
      * RE-CALCULATE SHRINK AMOUNT AND END INVENTORY AMOUNT                     
           PERFORM 2700-RE-CALC-AMOUNTS.                                        
                                                                                
      * UPDATE TABLE                                                            
           PERFORM 7500-UPDATE-TINSCLS-TABLE.                                   
                                                                                
           IF PS-PROGRAM-DEADLOCKED                                             
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7150-PROCESS-RECALC-CURSOR                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FINAL CALCULATIONS OF VALUES TO UPDATE TINSCLS                 *        
      ******************************************************************        
       2700-RE-CALC-AMOUNTS.                                                    
                                                                                
W20445**   A KEY BREAK FOR TINSCLS, PRIME THE SHRINK RETAIL PRORATION           
W20445** LOGIC FOR THE NEXT DEPT/CLASS/STORE COMBINATION....                    
W20445     IF INSCLS-DEPT-NBR = PV-HOLD-DEPT-NBR     AND                        
W20445        INSCLS-SUB-CL-NBR = PV-HOLD-SUB-CL-NBR AND                        
W20445        INSCLS-STR-NBR = PV-HOLD-STR-NBR                                  
W20445         CONTINUE                                                         
W20445     ELSE                                                                 
W20445        IF NOT PS-FIRST-TIME-THRU                                         
W20445            PERFORM 7800-COMMIT-PROCESSING                                
W20445        END-IF                                                            
W20445                                                                          
W28582** COUNT THE NUMBER OF INV LWST FOR THE DEPT/CLASS/STORE COMBO...         
W28582        PERFORM 6000-GET-IN-LWST-COUNT                                    
W20445                                                                          
W20445** GET THE SHRINK RETAIL FOR THE DEPT/CLASS/STORE COMBO...                
W20445        PERFORM 7200-OBTAIN-SHRINK-AMT                                    
W20445                                                                          
W20445** RESET ACCUMULATORS/COUNTER/STC FOR THE KEY BREAK...                    
W28582        MOVE 1                      TO PV-IN-LWST-PROCESS-COUNT           
W20445        MOVE ZEROS                  TO PV-INSCLS-SHNK-RTL-AMT             
              MOVE INSCLS-FSCL-MN-NBR     TO PV-HOLD-FISCAL-NBR                 
              MOVE INSCLS-FSCL-MN-END-DTE TO PV-HOLD-FISCAL-MN-END-DTE          
              MOVE INSCLS-DEPT-NBR        TO PV-HOLD-DEPT-NBR                   
              MOVE INSCLS-SUB-CL-NBR      TO PV-HOLD-SUB-CL-NBR                 
              MOVE INSCLS-STR-NBR         TO PV-HOLD-STR-NBR                    
W28582        MOVE INSCLS-IN-LO-MDSE-LVL-ID TO PV-HOLD-IN-LWST-ID               
W20445                                                                          
           END-IF.                                                              
                                                                                
           IF PS-FIRST-TIME-THRU                                                
               MOVE 'N' TO PS-FIRST-TIME-SW                                     
           END-IF.                                                              
P42135                                                                          
P42135     IF PV-SAVE-ML-PSTG-MN-END-DTE NOT EQUAL                              
P42135                                   INSCLS-ML-PSTG-MN-END-DTE              
P42135         PERFORM 7200-OBTAIN-SHRINK-AMT                                   
P42135     END-IF.                                                              
                                                                                
W28582** SHRINK CALCULATIONS AT THE INV LWST LEVEL (IN_LO_MDSE_LVL_ID). .       
W20445     PERFORM 6100-PRORATE-SHRINK.                                         
W20445                                                                          
W20445*    IF INSCLS-SLS-RTL-AMT = ZERO                                         
W20445*        MOVE ZERO TO PV-SHNK-RTL-AMT                                     
W20445*    ELSE                                                                 
W20445*        PERFORM 7200-OBTAIN-SHRINK-AMT                                   
W20445*    END-IF.                                                              
W20445                                                                          
W20445**   ENDING INVENTORY RETAIL AMOUNT CALCULATIONS AT THE                   
W28582** DEPT/CLASS/STORE/IN_LO_MDSE_LVL_ID LEVEL.....                          
           COMPUTE INSCLS-END-INV-RTL-AMT    =                                  
                   INSCLS-CTOFF-END-INV-AMT  +                                  
                   INSCLS-RCPT-RTL-AMT       +                                  
                   INSCLS-PADJ-RTL-AMT       +                                  
                   INSCLS-MKUP-RTL-AMT       +                                  
                   INSCLS-XFER-IN-RTL-AMT    -                                  
                   INSCLS-SLS-RTL-AMT        -                                  
                   INSCLS-MKDN-RTL-AMT       -                                  
                   INSCLS-XFER-OUT-RTL-AMT   -                                  
W27808             PV-SHNK-RTL-AMT           -                                  
W27808             INSCLS-STR-EXPTED-RTL-AMT -                                  
OCFSFS             INSCLS-STR-PARL-SHIP-RTL-AMT.                                
                                                                                
F33304******************************************************************        
F33304* READ M/L OPEN FISCAL MONTH                                              
F33304******************************************************************        
                                                                                
      ******************************************************************        
      * READ M/L OPEN FISCAL MONTH                                              
      ******************************************************************        
       4000-READ-OPN-FSCL-MTH.                                                  
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                    OR (SQLCODE         = +100)                                 
                                                                                
               EXEC SQL                                                         
W33304              SELECT  MIN(OPN_FSCL_MN_DTE)                                
W33304                     ,MAX(OPN_FSCL_MN_DTE)                                
W33304                     ,MIN(OPN_FSCL_MN_NBR)                                
W33304                     ,MAX(OPN_FSCL_MN_NBR)                                
W33304              INTO    :PV-MIN-INCNTL-OPN-FSCL-MN-DTE :PV-NULL             
W33304                     ,:PV-MAX-INCNTL-OPN-FSCL-MN-DTE :PV-NULL             
W33304                     ,:PV-MIN-INCNTL-OPN-FSCL-MN-NBR :PV-NULL             
W33304                     ,:PV-MAX-INCNTL-OPN-FSCL-MN-NBR :PV-NULL             
W33304              FROM   TINCNTL                                              
W33304              WHERE  ACTV_IND = 'Y'                                       
W33304              WITH UR                                                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                   WHEN SQLCODE        = +100                                   
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE    '4000-READ-OPN-FSCL-MTH'                         
                                                TO PV-PARAGRAPH-NAME            
W33304                 MOVE    'SELECT ROW FROM TINCNTL'                        
                                          TO PV-OPERATION-ATTEMPTED             
                       PERFORM 9999-SQL-ABEND                                   
               END-EVALUATE                                                     
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
W33304         DISPLAY '** MAX RETRIES EXCEEDED ON TINCNTL **'                  
               MOVE    '4000-READ-OPN-FSCL-MTH'                                 
                                                TO PV-PARAGRAPH-NAME            
W33304         MOVE    'SELECT ROW FROM TINCNTL'                                
                                        TO PV-OPERATION-ATTEMPTED               
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
W20445/****************************************************************         
W28582* 6000-GET-IN-LWST-COUNT.                                                 
W28582*-- COUNT THE NUMBER OF INV LOWEST LEVEL KEY (DATALESS KEY)               
W28582*   WITHIN FSCL_MN_END_DTE.                                               
W20445*   THE COUNT IS USED TO ACCOMMODATE FOR ANY IMBALANCE IN THE             
W20445*   CALCULATED SHRINK VS. THE TINSHNK SHRINK.  ANY OUT OF BALANCE         
W28582*   AMOUNT IS LOADED INTO THE LAST DATALESS KEY OF A DEPT/SUB/STR         
W20445*****************************************************************         
W28582 6000-GET-IN-LWST-COUNT.                                                  
W20445                                                                          
W28582     MOVE ZEROS               TO PV-IN-LWST-COUNT.                        
W20445                                                                          
W20445     EXEC SQL                                                             
TA5801*        SELECT COUNT(DISTINCT IN_LO_MDSE_LVL_ID)                         
TA5801         SELECT COUNT(IN_LO_MDSE_LVL_ID)                                  
W28582         INTO :PV-IN-LWST-COUNT:PV-IN-LWST-NULL-IND                       
W20445         FROM  TINSCLS                                                    
W20445         WHERE DEPT_NBR = :INSCLS-DEPT-NBR                                
W20445           AND SUB_CL_NBR = :INSCLS-SUB-CL-NBR                            
W20445           AND STR_NBR = :INSCLS-STR-NBR                                  
W20445           AND FSCL_MN_NBR = :INSCLS-FSCL-MN-NBR                          
W20445           AND FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE                  
W20445         WITH UR                                                          
W20445     END-EXEC.                                                            
W20445                                                                          
W20445     EVALUATE SQLCODE                                                     
W20445         WHEN +0                                                          
W28582             IF PV-IN-LWST-NULL-IND IS < 0                                
W28582                MOVE ZEROS    TO PV-IN-LWST-COUNT                         
W20445             END-IF                                                       
W20445         WHEN OTHER                                                       
W28582             MOVE '6000-GET-IN-LWST-COUNT' TO PV-PARAGRAPH-NAME           
W20445             MOVE PE-MSG-16   TO PV-OPERATION-ATTEMPTED                   
W20445             PERFORM 9999-SQL-ABEND                                       
W20445     END-EVALUATE.                                                        
W20445                                                                          
W20445******************************************************************        
W20445* 6100-PRORATE-SHRINK.                                                    
W28582*-- AT THE DEPT/CLASS/STORE/IN_LO_MDSE_LVL_ID LEVEL, PRORATE EACH INV     
W28582*   LWST LVL SHRINK DOLLARS BASED ON THE LWST KEY'S PERCENTAGE OF TOTAL   
W28582*   DEPT/CLASS/STORE SALES VS. THE INV LWST LEVEL'S SALES.                
W20445*-- ANY REMAINING SHRINK RETAIL GOES TO THE LAST DEPT/CLASS/STORE         
W28582*   INVENTORY LOWEST LEVEL KEY.                                           
W20445******************************************************************        
W20445 6100-PRORATE-SHRINK.                                                     
W20445                                                                          
W20445***  THE TOTAL SALES FOR THE DEPT/CLASS/STORE FOR SHRINK RETAIL           
W20445***  PRORATION...                                                         
W28582     MOVE INSCLS-SLS-RTL-AMT  TO PV-IN-LWST-SALES-AMT.                    
W20445                                                                          
W20445     PERFORM 6200-SUM-SALES-DLRS.                                         
W20445                                                                          
W28582***  CALC % OF SALES FOR AN INV LWST KEY WITHIN A DEPT/CLASS/STORE        
W20445     IF PV-INSCLS-SALES-AMT IS EQUAL TO ZEROS                             
W28582         MOVE ZEROS           TO PV-IN-LWST-PCT-SALES                     
W20445     ELSE                                                                 
W28582         COMPUTE PV-IN-LWST-PCT-SALES ROUNDED                             
W28582                              = PV-IN-LWST-SALES-AMT                      
W20445                              / PV-INSCLS-SALES-AMT                       
W20445     END-IF.                                                              
W20445                                                                          
W28582***  CALC SHRINK RETAIL FOR AN INV LWST KEY WITHIN                        
W28582***  A DEPT/CLASS/STORE.                                                  
W20445     COMPUTE PV-SHNK-RTL-AMT ROUNDED                                      
W28582                              = PV-IN-LWST-PCT-SALES                      
W20445                              * PV-INSHNK-SHNK-RTL-AMT.                   
W20445                                                                          
W28582***  ACCUMULATOR FOR INV LWST LEVEL SHRINK AMOUNTS USED TO                
W20445***  BALANCE BACK TO TINSHNK SHRINK RETAIL AT A KEY BREAK...              
W20445     ADD PV-SHNK-RTL-AMT      TO PV-INSCLS-SHNK-RTL-AMT.                  
W20445                                                                          
W28582***  ON LAST LWST ID FOR THE DEPT/CLASS/STORE. DETERMINE IF ANY           
W20445***  REMAINING SHRINK RETAIL EXISTS. IF SO, APPLY THE DIFFERENCE          
W28582***  TO THE LAST INV LWST KEY FOR THE DEPT/CLASS/STORE.                   
W28582     IF PV-IN-LWST-COUNT = PV-IN-LWST-PROCESS-COUNT                       
W20445         IF PV-INSCLS-SHNK-RTL-AMT = PV-INSHNK-SHNK-RTL-AMT               
W20445             CONTINUE                                                     
W20445         ELSE                                                             
W20445             COMPUTE PV-INSCLS-SHNK-DIFF                                  
W20445                              = PV-INSHNK-SHNK-RTL-AMT                    
W20445                              - PV-INSCLS-SHNK-RTL-AMT                    
W20445             COMPUTE PV-SHNK-RTL-AMT                                      
W20445                              = PV-SHNK-RTL-AMT                           
W20445                              + PV-INSCLS-SHNK-DIFF                       
W20445         END-IF                                                           
W28582         MOVE ZEROES          TO PV-IN-LWST-PROCESS-COUNT                 
W20445                                 PV-INSCLS-SHNK-RTL-AMT                   
W20445     END-IF.                                                              
W20445                                                                          
W20445*****************************************************************         
W20445* 6200-SUM-SALES-DLRS.                                                    
W20445*-- SUM RETAIL SALES FROM TINSCLS FOR DEPT/CLASS/STORE.                   
W20445*****************************************************************         
W20445 6200-SUM-SALES-DLRS.                                                     
W20445                                                                          
W20445     EXEC SQL                                                             
W20445         SELECT SUM(SLS_RTL_AMT)                                          
W20445         INTO :PV-INSCLS-SALES-AMT:PV-SALES-NULL-IND                      
W20445         FROM TINSCLS                                                     
W20445         WHERE FSCL_MN_NBR = :INSCLS-FSCL-MN-NBR                          
W20445           AND FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE                  
W20445           AND DEPT_NBR = :INSCLS-DEPT-NBR                                
W20445           AND SUB_CL_NBR = :INSCLS-SUB-CL-NBR                            
W20445           AND STR_NBR = :INSCLS-STR-NBR                                  
W20445        WITH UR                                                           
W20445     END-EXEC                                                             
W20445                                                                          
W20445     EVALUATE SQLCODE                                                     
W20445         WHEN +0                                                          
W20445             IF PV-SALES-NULL-IND < 0                                     
W20445                MOVE ZEROS              TO PV-INSCLS-SALES-AMT            
W20445             END-IF                                                       
W20445         WHEN OTHER                                                       
W20445             MOVE '6200-SUM END ID SSL' TO PV-PARAGRAPH-NAME              
W20445             MOVE 'NEED MESSAGE'        TO PV-OPERATION-ATTEMPTED         
W20445             PERFORM 9999-SQL-ABEND                                       
W20445     END-EVALUATE.                                                        
W20445                                                                          
      *****************************************************************         
W20445* OPEN CURSOR FOR UPDATE OF RECALCULATED COLUMNS ON TINSCLS               
      *****************************************************************         
       7100-OPEN-RECALC-CURSOR.                                                 
                                                                                
           EXEC SQL                                                             
               OPEN RECALC_UPDATE_CSR                                           
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
      *             MOVE 'Y' TO  PS-SEASON-FIRST-FETCH-SW                       
               WHEN OTHER                                                       
                  MOVE '7100-OPEN-RECALC-CSR' TO PV-PARAGRAPH-NAME              
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * FETCH A ROW FOR UPDATE                                                  
      *****************************************************************         
       7150-PROCESS-RECALC-CURSOR.                                              
                                                                                
           EXEC SQL                                                             
                  FETCH RECALC_UPDATE_CSR                                       
                   INTO :INSCLS-FSCL-MN-NBR                                     
                       ,:INSCLS-FSCL-MN-END-DTE                                 
                       ,:INSCLS-DEPT-NBR                                        
                       ,:INSCLS-SUB-CL-NBR                                      
                       ,:INSCLS-STR-NBR                                         
W28582                 ,:INSCLS-IN-LO-MDSE-LVL-ID                               
                       ,:INSCLS-SLS-RTL-AMT                                     
                       ,:INSCLS-MKDN-RTL-AMT                                    
                       ,:INSCLS-MKUP-RTL-AMT                                    
                       ,:INSCLS-XFER-IN-RTL-AMT                                 
                       ,:INSCLS-XFER-OUT-RTL-AMT                                
                       ,:INSCLS-RCPT-RTL-AMT                                    
                       ,:INSCLS-PADJ-RTL-AMT                                    
OCFSFS                 ,:INSCLS-STR-PARL-SHIP-RTL-AMT                           
                       ,:INSCLS-CTOFF-END-INV-AMT                               
W27808                 ,:INSCLS-STR-EXPTED-RTL-AMT                              
W33304                 ,:INSCLS-ML-PSTG-MN-END-DTE                              
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   SET PS-END-OF-RECALC-CURSOR TO TRUE                          
               WHEN OTHER                                                       
                   MOVE '7150-PROCESS-RECALC'  TO PV-PARAGRAPH-NAME             
                   MOVE PE-MSG-04              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      *****************************************************************         
      * SELECTS THE SHRINK RETAIL AMOUNT SUM FROM TINSHNK                       
      *****************************************************************         
       7200-OBTAIN-SHRINK-AMT.                                                  
      *                                                                         
W20445     MOVE ZEROS          TO PV-INSHNK-SHNK-RTL-AMT.                       
W20445                                                                          
           EXEC SQL                                                             
                SELECT SUM(SHNK_RTL_AMT)                                        
W20445          INTO :PV-INSHNK-SHNK-RTL-AMT :PV-SHNK-NULL-IND                  
                FROM TINSHNK                                                    
                WHERE (FSCL_MN_END_DTE = :INSCLS-FSCL-MN-END-DTE)               
P33304           AND (ML_PSTG_MN_END_DTE = :INSCLS-ML-PSTG-MN-END-DTE)          
                 AND (DEPT_NBR        = :INSCLS-DEPT-NBR)                       
                 AND (SUB_CL_NBR      = :INSCLS-SUB-CL-NBR)                     
                 AND (STR_NBR         = :INSCLS-STR-NBR)                        
W20445          WITH UR                                                         
           END-EXEC                                                             
      *                                                                         
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
P42135             MOVE INSCLS-ML-PSTG-MN-END-DTE TO                            
P42135                                     PV-SAVE-ML-PSTG-MN-END-DTE           
                   CONTINUE                                                     
               WHEN -305                                                        
                   DISPLAY '***************************************   '         
                   DISPLAY '***      TINSHNK ROW NOT FOUND      ***   '         
                   DISPLAY '***************************************   '         
                   DISPLAY 'DEPT IN PROCESS : '  INSCLS-DEPT-NBR                
                   DISPLAY '    SUB CLASS   : '  INSCLS-SUB-CL-NBR              
                   DISPLAY '    STORE       : '  INSCLS-STR-NBR                 
                   DISPLAY '    OPEN MONTH  : '  INSCLS-FSCL-MN-END-DTE         
                   DISPLAY '    SLS RTL     : '  INSCLS-SLS-RTL-AMT             
                   DISPLAY '***************************************   '         
                   MOVE '7200-SELECT-SHRINK-AMT' TO PV-PARAGRAPH-NAME           
                   MOVE PE-MSG-14              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
               WHEN OTHER                                                       
                   DISPLAY '                                     '              
                   DISPLAY 'DEPT IN PROCESS : '  INSCLS-DEPT-NBR                
                   DISPLAY '    SUB CLASS   : '  INSCLS-SUB-CL-NBR              
                   DISPLAY '    STORE       : '  INSCLS-STR-NBR                 
                   DISPLAY '    OPEN MONTH  : '  INSCLS-FSCL-MN-END-DTE         
                   MOVE '7200-SELECT-SHRINK-AMT' TO PV-PARAGRAPH-NAME           
                   MOVE PE-MSG-14              TO PV-OPERATION-ATTEMPTED        
                   PERFORM 9999-SQL-ABEND                                       
             END-EVALUATE.                                                      
                                                                                
      ******************************************************************        
       7500-UPDATE-TINSCLS-TABLE.                                               
           EXEC SQL                                                             
               UPDATE TINSCLS                                                   
                  SET  SHNK_RTL_AMT     = :PV-SHNK-RTL-AMT                      
                      ,END_INV_RTL_AMT  = :INSCLS-END-INV-RTL-AMT               
                WHERE  FSCL_MN_NBR      = :INSCLS-FSCL-MN-NBR                   
                AND    FSCL_MN_END_DTE  = :INSCLS-FSCL-MN-END-DTE               
                AND    DEPT_NBR         = :INSCLS-DEPT-NBR                      
                AND    SUB_CL_NBR       = :INSCLS-SUB-CL-NBR                    
                AND    STR_NBR          = :INSCLS-STR-NBR                       
W28582          AND    IN_LO_MDSE_LVL_ID = :INSCLS-IN-LO-MDSE-LVL-ID            
W33304          AND    ML_PSTG_MN_END_DTE =:INSCLS-ML-PSTG-MN-END-DTE           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN -911                                                        
                   ADD +1             TO PA-DEADLOCK-COUNT                      
                   IF PA-DEADLOCK-COUNT > PC-MAX-DEADLOCKS                      
                       MOVE '7500-UPDATE-TINSCLS-TABLE' TO                      
                                                    PV-PARAGRAPH-NAME           
                       PERFORM 9000-DEADLOCK-ERROR                              
                   ELSE                                                         
                       PERFORM 7999-RESTART-PROCESS                             
                   END-IF                                                       
               WHEN OTHER                                                       
W33304           DISPLAY 'OPN-FSCL-MN-NBR = '                                   
W33304                    INSCLS-FSCL-MN-NBR                                    
W33304                  '   INV-MN-END-DTE = '                                  
W33304                      INSCLS-FSCL-MN-END-DTE                              
W33304                  '   OPN-FSCL-MN-DTE = '                                 
W33304                      INSCLS-ML-PSTG-MN-END-DTE                           
                        '   DEPT_NBR = ' PV-HOLD-DEPT-NBR                       
W20445                  '   SUB_CL_NBR = ' PV-HOLD-SUB-CL-NBR                   
W20445                  '   STR_NBR =  ' PV-HOLD-STR-NBR                        
W28582             '   IN_LO_MDSE_LVL_ID  =  ' INSCLS-IN-LO-MDSE-LVL-ID         
                        '   SHNK_RTL_AMT = ' PV-SHNK-RTL-AMT                    
                        '   END_INV_RTL_AMT  = ' INSCLS-END-INV-RTL-AMT         
                    MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED             
                    MOVE '7500-UPDATE-TINSCLS-TABLE'                            
                                          TO PV-PARAGRAPH-NAME                  
                    PERFORM 9999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
           IF PS-PROGRAM-DEADLOCKED                                             
               CONTINUE                                                         
           ELSE                                                                 
               ADD 1 TO PA-DEPTS-PROCESSED.                                     
                                                                                
      ******************************************************************        
      * PERFORMED BY END OF JOB PROCESSING                                      
      ******************************************************************        
       7550-CLOSE-RECALC-CURSOR.                                                
           EXEC SQL                                                             
               CLOSE RECALC_UPDATE_CSR                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE PE-MSG-05            TO PV-OPERATION-ATTEMPTED          
                   MOVE  '7550-CLOSE-RECALC_CURSOR' TO PV-PARAGRAPH-NAME        
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      * FOLLOWING PARAGRAPHS ARE FOR CHECKPOINT RESTART LOGIC                   
                                                                                
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *        
      *            CONTROL TABLE                                       *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
                                                                                
           EXEC SQL                                                             
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
                   MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED         
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *        
      *            IF WE ARE IN A RESTART CONDITION.                   *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
                                                                                
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *        
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
                                                                                
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
      *                                                                         
      *    DISPLAY 'CURRENT TIMESTAMP ', PV-CURRENT-TIMESTAMP                   
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-13             TO PV-OPERATION-ATTEMPTED             
               MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *        
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
                                                                                
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
                                                                                
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
      *        PREPARE COMMIT RECORD FOR UPDATE                                 
               ADD 1                           TO PA-COMMIT-COUNT               
W33304         MOVE INSCLS-FSCL-MN-NBR         TO CR-FISCAL-NBR                 
W33304         MOVE INSCLS-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE          
               MOVE PV-HOLD-DEPT-NBR           TO CR-DEPT-NBR                   
               MOVE PV-HOLD-SUB-CL-NBR         TO CR-SUB-CL-NBR                 
               MOVE PV-HOLD-STR-NBR            TO CR-STR-NBR                    
W28582         MOVE PV-HOLD-IN-LWST-ID         TO CR-IN-LWST-ID                 
               MOVE PA-COMMIT-COUNT            TO CR-COMMIT-COUNT               
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                          
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                             TCKRSTINF-WORK-STRG-DESC           
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
               PERFORM 7850-COMMIT-CHANGES                                      
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.              
      *            TAKE A COMMIT.                                               
      *  ==> PERFORMED FROM: 7800-COMMIT-PROCESSING.                            
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :PC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
      ******   DISPLAY 'COMMIT ==>> ' TCKRSTINF-WORK-STRG-DESC 1:30             
      ******            ' <<=='                                                 
           ELSE                                                                 
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7900-UPDATE-CHECKPOINT-STATUS                                           
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE           
      *                                                                         
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED        
               PERFORM 9999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TINSCLS ROW ROR UPDATE        
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.          
      ******************************************************************        
       7999-RESTART-PROCESS.                                                    
                                                                                
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                                   
                                                                                
           PERFORM 7700-SELECT-RESTART-INFO.                                    
                                                                                
           DISPLAY '**** RESTART **** '.                                        
           DISPLAY '** SQLCODE -911 ** ', PA-DEADLOCK-COUNT, 'TIMES'.           
           DISPLAY 'TCKRSTINF-LAST CKPT ',                                      
                                        TCKRSTINF-WORK-STRG-DESC(1:30).         
                                                                                
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN              
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE                
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                               
           IF PS-FIRST-COMMIT                                                   
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)                       
               MOVE ZERO       TO PV-HOLD-DEPT-NBR                              
W20445         MOVE ZEROS      TO PV-HOLD-SUB-CL-NBR                            
W20445         MOVE ZEROS      TO PV-HOLD-STR-NBR                               
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'             
                      ' BEGINNING'                                              
           ELSE                                                                 
               DISPLAY 'TCKRSTINF-LAST CKPT '                                   
                        TCKRSTINF-WORK-STRG-DESC (1:30)                         
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA          
               MOVE TCKRSTINF-WORK-STRG-DESC TO                                 
                    CR-CHECKPOINT-RESTART-AREA                                  
               MOVE CR-DEPT-NBR     TO PV-HOLD-DEPT-NBR                         
W20445         MOVE CR-SUB-CL-NBR   TO PV-HOLD-SUB-CL-NBR                       
W20445         MOVE CR-STR-NBR      TO PV-HOLD-STR-NBR                          
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                        
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                         
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE                  
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                           
               DISPLAY 'SUB CLASS NBR   ' CR-SUB-CL-NBR                         
               DISPLAY 'STORE NBR       ' CR-STR-NBR                            
W28582         DISPLAY 'INV LOWEST KEY  ' CR-IN-LWST-ID                         
           END-IF.                                                              
                                                                                
      * POSITION CURSOR                                                         
           PERFORM 7100-OPEN-RECALC-CURSOR.                                     
                                                                                
      ******************************************************************        
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *        
      ******************************************************************        
       8000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 7550-CLOSE-RECALC-CURSOR.                                    
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
                                                                                
           DISPLAY '                                             '.             
W20445     DISPLAY 'ROWS  PROCESSED ', PA-DEPTS-PROCESSED.                      
           DISPLAY '                                             '.             
           DISPLAY 'COMMITS TAKEN   ', PA-COMMIT-COUNT.                         
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
                                                                                
      ******************************************************************        
      *  PERFORMED BY      PROCESS-RECALC-CURSOR                                
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)           
      ******************************************************************        
       9000-DEADLOCK-ERROR.                                                     
                                                                                
               MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9999-SQL-ABEND.                                          
                                                                                
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
