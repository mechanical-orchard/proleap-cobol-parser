       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    ICKUP056.                                                 
       AUTHOR.        R. KELLEN.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  02/01/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * ICKUP056 - MAINAINT QTYS ON ICT_SKU_LOC_CNT TABLE                       
      *****************************************************************         
      *        THIS PROGRAM WILL READ A FILE CONTAINING COUNT/SKU/STORE         
      *        QTY DATA AND MAINTAIN THE UNT_RTL_AMT,LOC_EXPTED_UNT_QTY         
      *        ,IN_TRNST_UNT_QTY,ACPTD_TRUK_UNT_QTY,RTL_SAL_UNT_QTY             
      *        ,RTL_RET_UNT_QTY,XFER_IN_UNT_QTY,XFER_OUT_UNT_QTY                
      *        ,SCND_UPC_NBR,SCND_UNT_QTY,CNT_GP_CDE,MDSE_EXPRN_DTE ON          
      *        THE ICT_SKU_LOC_CNT TABLE. AN 'UPD' ROW IS ADDED TO THE          
      *        ICT_LOC_CNT_STAT TABLE FOR EACH LOCATION PROCESSED.              
      *****************************************************************         
      *                                                                         
      *  TABLES UPDATED ARE:                                                    
      *      ICT_SKU_LOC_CNT  - IC COUNT EXECUTION TABLE                        
      *  FILES READ ARE:                                                        
      *      NONE                                                               
      *****************************************************************         
      * 02/02/06 - R KELLEN         -  WR NO. WR30388                           
      *    - NEW PROGRAM.                                                       
      *****************************************************************         
CL3132* 05/21/10 - COGNIZANT        - CLARITY ID: 3132C, WR37582                
CL3132*    - MODIFIED 8600-DELETE-SKU-LOC PARA TO CHECK LAST_UPD_USR_ID         
CL3132*    - ID  = 'SYSTEM' TO ENSURE ROWS INSERTED BY THE RF DEVICE            
CL3132*    - ARE NOT DELETED.                                                   
CL3132*****************************************************************         
P61756*****************************************************************         
P61756* CHANGE MADE: CORRECTED THE -180 ISSUE WITH                              
P61756* ICT00011-MDSE-EXPRN-DTE.CHANGES ARE TAGGED WITH P61756.                 
P61756* MADE BY: COGNIZANT               WR/IR #: CHG0160570                    
      *****************************************************************         
201429* JUNE, 2018  CHANGE MADE: MAINFRAME REFACTORING -                        
201429*             DELETE DUPLICATE DEFINITIONS OF                             
201429*             PV-EXTR-PRC-TMST-NI AND PV-EXTR-PRC-TMST-IS-NULL            
201429* MADE BY: JIM HUNTER              WR/IR #: CHG0201429                    
201429*****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT IC-COUNT-QTY-MAINT-FILE    ASSIGN TO INP01.                   
                                                                                
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  IC-COUNT-QTY-MAINT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS IC-COUNT-QTY-MAINT-REC.                               
       01  IC-COUNT-QTY-MAINT-REC     PIC  X(60).                               
                                                                                
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME                PIC X(08)    VALUE                
                                                 'ICKUP056'.                    
           05  PC-JOB-NAME               PIC X(08)     VALUE 'ICK056  '.        
           05  PC-MAXIMUM-RETRY-COUNT         PIC S9(04)   VALUE +2             
                                                           COMP SYNC.           
           05  PC-DEFAULT-TMST                PIC X(26)    VALUE                
               '2007-01-01-00.00.00.000000'.                                    
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-RETRIES                     PIC S9(04)   VALUE +0             
                                                           COMP.                
           05  PV-PARAGRAPH-NAME              PIC  X(30)   VALUE SPACES.        
           05  PV-DB2-OPERATION-ATTEMPTED     PIC  X(30)   VALUE SPACES.        
           05  PV-SQLCODE-DISPLAY             PIC +(8)9    VALUE ZEROES.        
           05  PV-STD-TM-ZN-CDE-NI            PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-STD-TM-ZN-CDE-IS-NULL    VALUE -1.                         
           05  PV-DST-IND-NI                  PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-DST-IND-IS-NULL          VALUE -1.                         
           05  PV-COMMITS-TAKEN                PIC S9(09)  COMP-3               
                                                         VALUE ZERO.            
           05  PV-COMMIT-INTERVAL              PIC S9(09)  COMP-3               
                                                       VALUE +5000.             
           05  PV-COMMIT-CNTR                 PIC S9(04)   VALUE +1             
                                                           COMP-3.              
           05  PV-CURRENT-DATE             PIC X(10).                           
           05  PV-CURRENT-TMST             PIC X(26).                           
           05  PV-LAST-RUN-TMST            PIC X(26).                           
           05  PV-EXTR-PRC-TMST-NI            PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-EXTR-PRC-TMST-IS-NULL    VALUE -1.                         
               88 PV-EXTR-PRC-TMST-NOT-NULL   VALUE 0.                          
           05  PV-EXEC-TMST-NI                PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-EXTR-TMST-IS-NULL        VALUE -1.                         
               88 PV-EXTR-TMST-NOT-NULL       VALUE 0.                          
           05  PV-IN-TRNST-UNT-QTY-NI         PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-IN-TRNST-UNT-QTY-NULL    VALUE -1.                         
               88 PV-IN-TRNST-UNT-QTY-NOT-NULL VALUE 0.                         
           05  PV-ACPTD-TRUK-UNT-QTY-NI       PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-ACPTD-TRUK-UNT-QTY-IS-NULL VALUE -1.                       
               88 PV-ACPTD-TRUK-UNT-QTY-NOT-NULL VALUE 0.                       
           05  PV-RTL-SAL-UNT-QTY-NI          PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-SAL-UNT-QTY-IS-NULL     VALUE -1.                          
               88 PV-SAL-UNT-QTY-NOT-NULL    VALUE 0.                           
           05  PV-RTL-RET-UNT-QTY-NI          PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 RTL-RET-UNT-QTY-IS-NULL     VALUE -1.                         
               88 RTL-RET-UNT-QTY-NOT-NULL    VALUE 0.                          
           05  PV-XFER-IN-UNT-QTY-NI          PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-XFER-IN-UNT-QTY-IS-NULL VALUE -1.                          
               88 PV-XFER-IN-UNT-QTY-NOT-NULL VALUE 0.                          
           05  PV-XFER-OUT-UNT-QTY-NI         PIC S9(04)  COMP                  
                                                         VALUE ZERO.            
               88 PV-XFER-OUT-UNT-QTY-IS-NULL VALUE -1.                         
               88 PV-XFER-OUT-UNT-QTY-NOT-NULL VALUE 0.                         
           05  PV-SCND-UPC-NBR-NI              PIC S9(04)  COMP                 
                                                         VALUE ZERO.            
               88 PV-SCND-UPC-NBR-IS-NULL     VALUE -1.                         
               88 PV-SCND-UPC-NBR-NOT-NULL    VALUE 0.                          
           05  PV-SCND-UNT-QTY-NI              PIC S9(04)  COMP                 
                                                         VALUE ZERO.            
               88 PV-SCND-UNT-QTY-IS-NULL     VALUE -1.                         
               88 PV-SCND-UNT-QTY-NOT-NULL    VALUE 0.                          
           05  PV-CNT-GP-CDE-NI                PIC S9(04)  COMP                 
                                                         VALUE ZERO.            
               88 PV-CNT-GP-CDE-IS-NULL       VALUE -1.                         
               88 PV-CNT-GP-CDE-NOT-NULL      VALUE 0.                          
           05  PV-MDSE-EXPRN-DTE-NI            PIC S9(04)  COMP                 
                                                         VALUE ZERO.            
               88 PV-MDSE-EXPRN-DTE-IS-NULL   VALUE -1.                         
               88 PV-MDSE-EXPRN-DTE-NOT-NULL  VALUE 0.                          
           05  PV-ORIG-CLR-DTE-DTE-NI          PIC S9(04)  COMP                 
                                                         VALUE ZERO.            
               88 PV-ORIG-CLR-DTE-IS-NULL      VALUE -1.                        
               88 PV-ORIG-CLR-DTE-NOT-NULL     VALUE 0.                         
                                                                                
P61756     05  PV-ORIG-CLR-DTE-NULL-BLNK   PIC S9(04)  COMP                CL*15
P61756                                                   VALUE ZERO.       CL*15
P61756         88 PV-ORIG-CLR-DTE-IS-BLNK  VALUE -1.                            
P61756         88 PV-ORIG-CLR-DTEIS-NOT-BLNK VALUE 0.                           
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-MNT-INPUT-FL-SW      PIC  X(01)   VALUE 'N'.           
               88  PS-END-OF-MNT-INPUT-FL                  VALUE 'Y'.           
           05  PS-PROCESS-STOP-SW             PIC  X(01)   VALUE 'N'.           
               88  PS-PROCESS-STOP                         VALUE 'Y'.           
           05  PS-FIRST-COMMIT-SW             PIC  X(01)   VALUE 'Y'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-INPUT-SKU-FOUND-SW          PIC  X(01)   VALUE 'Y'.           
               88  PS-INPUT-SKU-FOUND                      VALUE 'Y'.           
               88  PS-INPUT-SKU-NOT-FOUND                  VALUE 'N'.           
                                                                                
       01  ABEND-CODE                         PIC S9(04)   VALUE +0             
                                                           COMP SYNC.           
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN               PIC S9(04)    VALUE +5  COMP.          
                                                                                
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-MNT-READ-CNT       PIC 9(09)    VALUE 0 COMP-3.           
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-MNT-CNTR                  PIC  9(09)  VALUE ZEROS.            
           05  DI-COMMIT-CNTR               PIC  9(09)  VALUE ZEROS.            
                                                                                
      *****************************************************************         
      *    TIMEZONE CONVERSION ROUTINE COPYBOOK                                 
      *****************************************************************         
           COPY ICWS010.                                                        
      *****************************************************************         
      *                                                                         
      *****************************************************************         
      *    INPUT/OUTPUT COPYBOOKS                                               
      *****************************************************************         
           COPY ICRD057.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART CONTROL TABLE                            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART INFORMATION TABLE                        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
      *****************************************************************         
      *  XST_SKU_LOC TABLE                                                      
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00008                                                
           END-EXEC.                                                            
P61756*****************************************************************         
P61756*  XST_SKU TABLE                                                          
P61756*****************************************************************         
P61756     EXEC SQL                                                             
P61756          INCLUDE XST00010                                                
P61756     END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_SKU_LOC_CNT TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00011                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  ICT_LOC_CNT_STAT TABLE                                                 
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE ICT00020                                                
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  XST_LOC TABLE                                                          
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE XST00001                                                
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE' TO PV-PARAGRAPH-NAME.                        
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL.                                
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
            DISPLAY 'STARTING RECORD - '                                        
              ICRD057-IC-CNT-QTY-MAINT-FILE                                     
           END-IF                                                               
           PERFORM 2000-PROCESS-COUNT-MNT                                       
             UNTIL PS-END-OF-MNT-INPUT-FL.                                      
           PERFORM 8000-SQL-COMMIT.                                             
           DISPLAY 'COUNT MAINT PROCESSING DONE'.                               
                                                                                
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           PERFORM 8100-INIT-CHECKPOINT-SELECT.                                 
           MOVE TCKRSTCNTL-CKPT-FRQNCY-QTY TO PV-COMMIT-INTERVAL.               
      ******************************************************************        
      *  SELECT SAVED DATA TO SEE THE PROGRAM IS IN A RESTART STATUS            
      *  FROM THE RESTART INFO TABLE AND THEN ONLY PROCESS NON-COMPLETED        
      *  RECORDS AS OF THE LAST COMMIT. IF NOT, THEN THE VARIABLES WILL         
      *  CONTAIN THEIR INITIAL VALUES AND THE PROGRAM WILL PROCESS ALL          
      *  RECORDS ON THE INPUT FILE.                                             
      ******************************************************************        
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
               PERFORM 8150-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                 TO CR-CHECKPOINT-RESTART-VAR                                   
               MOVE CR-MNT-READ-CNT TO DI-MNT-CNTR                              
               SUBTRACT 1 FROM CR-MNT-READ-CNT                                  
               DISPLAY SPACES                                                   
               DISPLAY '** ICKUP056 IS BEING RESTARTED FROM RECORD# '           
                 CR-MNT-READ-CNT.                                               
                                                                                
           OPEN INPUT  IC-COUNT-QTY-MAINT-FILE.                                 
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL CR-MNT-READ-CNT TIMES.          
           IF CR-MNT-READ-CNT > 0                                               
             DISPLAY 'RESTART ON RECORD KEY - ' ICRD057-CNT-STUP-ID             
                                          ' - ' ICRD057-LOC-NBR                 
                                          ' - ' ICRD057-SKU-NBR.                
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-DATE = CURRENT DATE                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
           PERFORM 9000-TIMEZONE-CONVERSION-SETUP.                              
                                                                                
      ******************************************************************        
      * 2000-PROCESS-COUNT-MNT.                                        *        
      *  ==> INSERT  ROWS FOR EACH ACTIVE COUNT/LOC/SKU RECORD         *        
      ******************************************************************        
       2000-PROCESS-COUNT-MNT.                                                  
P61756     INITIALIZE  PV-MDSE-EXPRN-DTE-NI                                     
P61756                 PV-ORIG-CLR-DTE-DTE-NI                                   
P61756                 PV-ORIG-CLR-DTE-NULL-BLNK                                
P61756                 DCLICT00011                                              
P61756                 DCLXST00008.                                             
           MOVE '2000-PROCESS-COUNT-MNT' TO PV-PARAGRAPH-NAME.                  
           MOVE  ICRD057-CNT-STUP-ID        TO  ICT00011-CNT-STUP-ID.           
           MOVE  ICRD057-LOC-NBR            TO  ICT00011-LOC-NBR.               
           MOVE  ICRD057-SKU-NBR            TO  ICT00011-SKU-NBR.               
           MOVE  ICRD057-CNT-SEQ-NBR        TO  ICT00011-CNT-SEQ-NBR.           
           PERFORM 8300-SEL-SKU-LOC-CNT.                                        
           IF ICRD057-CNT-STUP-ID   NOT =  ICT00020-CNT-STUP-ID                 
            OR ICRD057-LOC-NBR      NOT =  ICT00020-LOC-NBR                     
            OR ICRD057-CNT-SEQ-NBR  NOT =  ICT00020-CNT-SEQ-NBR                 
             MOVE  ICRD057-LOC-NBR            TO  ICT00020-LOC-NBR              
             MOVE  ICRD057-CNT-STUP-ID        TO  ICT00020-CNT-STUP-ID          
             MOVE  ICRD057-CNT-SEQ-NBR        TO  ICT00020-CNT-SEQ-NBR          
      *** ADJUST TO STORE LOCAL TIME ZONE.                                      
             MOVE ICRD057-LOC-NBR             TO XST00001-LOC-NBR               
             PERFORM 8500-SELECT-XST-LOC                                        
             MOVE XST00001-STD-TM-ZN-CDE TO                                     
                  ICWS010-TZC-TZ-IN-TM-ZN-CDE                                   
             MOVE XST00001-DST-IND       TO ICWS010-TZC-TZ-IND-ST-IND           
             PERFORM 9000-TIMEZONE-CONVERSION                                   
             MOVE ICWS010-TZC-LOCATION-TMST TO ICT00020-LAST-UPD-TMST           
      ***                                                                       
             PERFORM 8400-INS-SKU-LOC-CNT-ST.                                   
           MOVE  ICRD057-UNT-RTL-AMT        TO ICT00011-UNT-RTL-AMT.            
           MOVE  ICRD057-LOC-EXPTED-UNT-QTY TO                                  
                         ICT00011-LOC-EXPTED-UNT-QTY.                           
           MOVE  ICRD057-ACPTD-TRUK-UNT-QTY TO                                  
                         ICT00011-ACPTD-TRUK-UNT-QTY.                           
           SET PV-ACPTD-TRUK-UNT-QTY-NOT-NULL TO TRUE.                          
           SET PV-SCND-UPC-NBR-IS-NULL TO TRUE.                                 
           SET PV-IN-TRNST-UNT-QTY-NULL TO TRUE.                                
           SET PV-SCND-UNT-QTY-IS-NULL TO TRUE.                                 
           SET PV-CNT-GP-CDE-IS-NULL   TO TRUE.                                 
           SET PV-SAL-UNT-QTY-NOT-NULL TO TRUE.                                 
           MOVE  ICRD057-SALES-QTY  TO                                          
                         ICT00011-RTL-SAL-UNT-QTY.                              
           SET RTL-RET-UNT-QTY-NOT-NULL TO TRUE.                                
           MOVE  ICRD057-RETURN-QTY TO                                          
                         ICT00011-RTL-RET-UNT-QTY.                              
           SET PV-XFER-IN-UNT-QTY-NOT-NULL TO TRUE.                             
           MOVE  ICRD057-XFER-IN-QTY TO                                         
                         ICT00011-XFER-IN-UNT-QTY.                              
           SET PV-XFER-OUT-UNT-QTY-NOT-NULL TO TRUE.                            
           MOVE  ICRD057-XFER-OUT-QTY TO                                        
                         ICT00011-XFER-OUT-UNT-QTY.                             
           SET PV-CNT-GP-CDE-IS-NULL TO TRUE.                                   
           MOVE  ICRD057-INTRANSIT-QTY      TO                                  
                         ICT00011-IN-TRNST-UNT-QTY.                             
           SET PV-IN-TRNST-UNT-QTY-NOT-NULL TO TRUE.                            
      ***                                                                       
      * IF THE SKU IS ACTIVE, THE ORIG CLR DATE SHOULD BE NULL                  
      ***                                                                       
           IF ICRD057-ACTIVE-IND   = 'Y'                                        
             SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                              
             SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                              
           ELSE                                                                 
      ***                                                                       
      * IF THE SKU IS CLEARANCE, CHECK TO SEE IF ORIG CLR DATE HAS NOT          
      * ALREADY BEEN SET. IF IT HAS BEEN, LEAVE IT, ELSE SET IT TO              
      * CLEARANCE DATE FROM INPUT FILE.                                         
      ***                                                                       
P61756       IF (ICRD057-ACTIVE-IND = 'N' AND                                   
P61756            ICRD057-ORIG-CLR-DTE EQUAL SPACES)                            
P61756              PERFORM 8700-NO-IND-STAT-CDE-FND                            
P61756*      MOVE  PV-CURRENT-DATE  TO ICT00011-MDSE-EXPRN-DTE                  
P61756*           SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                        
P61756*           SET PV-MDSE-EXPRN-DTE-NOT-NULL   TO TRUE                      
P61756       ELSE                                                               
P61756           IF  ICRD057-ACTIVE-IND = ' '                                   
P61756              PERFORM 8700-NO-IND-STAT-CDE-FND                            
P61756       ELSE                                                               
P61756         MOVE  ICRD057-ORIG-CLR-DTE  TO ICT00011-MDSE-EXPRN-DTE           
P61756         SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                           
P61756         SET PV-MDSE-EXPRN-DTE-NOT-NULL   TO TRUE.                        
      *** THIS TMST HAS BEEN ADJUSTED (ABOVE) TO STORE LOCAL TIME ZONE.         
           MOVE ICT00020-LAST-UPD-TMST TO ICT00011-LAST-UPD-TMST.               
           MOVE  ICRD057-CNT-SEQ-NBR          TO ICT00011-CNT-SEQ-NBR.          
           IF PS-INPUT-SKU-FOUND                                                
             PERFORM 8600-DELETE-SKU-LOC                                        
             PERFORM 8200-INS-SKU-LOC-CNT.                                      
           ADD 1 TO PV-COMMIT-CNTR.                                             
           IF PV-COMMIT-CNTR > PV-COMMIT-INTERVAL                               
             ADD PV-COMMIT-INTERVAL TO CR-MNT-READ-CNT                          
             PERFORM 8000-SQL-COMMIT.                                           
           PERFORM 7000-READ-COUNT-QTY-MAINT-FL.                                
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILE THAT WAS OPENED IN PARA 1000.                  *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - SETS CHEKPOINT STATUS CODE TO 0 (PROGRAM NOT IN PROCESS OR*        
      *      IN RESTART STATUS.                                        *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING' TO PV-PARAGRAPH-NAME.                     
                                                                                
           CLOSE IC-COUNT-QTY-MAINT-FILE                                        
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 8160-UPDATE-CHECKPOINT-STATUS.                               
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 7000-READ-COUNT-QTY-MAINT-FL.                                           
      *----------------------------------------------------------               
       7000-READ-COUNT-QTY-MAINT-FL.                                            
           MOVE '7000-READ-COUNT-QTY-MAINT-FL'   TO PV-PARAGRAPH-NAME.          
           READ IC-COUNT-QTY-MAINT-FILE                                         
             INTO ICRD057-IC-CNT-QTY-MAINT-FILE                                 
           AT END SET PS-END-OF-MNT-INPUT-FL TO TRUE.                           
           ADD +1 TO DI-MNT-CNTR.                                               
                                                                                
TEST  **   IF DI-MNT-CNTR > 500                                                 
TEST  **     DISPLAY 'INTENTIONAL ABEND - ' DI-MNT-CNTR                         
TEST  **     DISPLAY ICRD054-IC-COUNTS-FILE                                     
TEST  **       PERFORM 9999-SQL-ABEND.                                          
                                                                                
      *----------------------------------------------------------               
      * 8000-SQL-COMMIT.                                                        
      *----------------------------------------------------------               
       8000-SQL-COMMIT.                                                         
           MOVE '8000-SQL-COMMIT' TO PV-PARAGRAPH-NAME.                         
           MOVE 'COMMIT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           MOVE DI-MNT-CNTR   TO CR-MNT-READ-CNT.                               
           MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
                                                                                
           IF PS-FIRST-COMMIT                                                   
               MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                     
               PERFORM 8160-UPDATE-CHECKPOINT-STATUS                            
               MOVE 'N'        TO PS-FIRST-COMMIT-SW                            
           END-IF.                                                              
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 CHECKPOINT'                                 
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               COMMIT WORK                                                      
           END-EXEC.                                                            
           DISPLAY 'COMMIT COMPLETED'.                                          
           ADD +1 TO DI-COMMIT-CNTR.                                            
           MOVE 1 TO PV-COMMIT-CNTR.                                            
                                                                                
      *----------------------------------------------------------               
      *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF             
      *  THE PROGRAM IS IN A RESTART CONDITION.                                 
      *----------------------------------------------------------               
       8100-INIT-CHECKPOINT-SELECT.                                             
           MOVE '8100-INIT-CHECKPOINT-SELECT' TO  PV-PARAGRAPH-NAME.            
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                             
             SELECT                                                             
                  CKPT_STAT_CODE                                                
                 ,CKPT_FRQNCY_QTY                                               
               INTO                                                             
                 :TCKRSTCNTL-CKPT-STAT-CODE                                     
                ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                    
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 SELECT CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO                
      *  OBTAIN THE PROGRAMS SAVED VARIABLES.                                   
      ******************************************************************        
       8150-SELECT-RESTART-INFO.                                                
           MOVE '8150-SELECT-RESTART-INFO ' TO  PV-PARAGRAPH-NAME.              
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                  WORK_STRG_DESC                                                
               INTO                                                             
                 :TCKRSTINF-WORK-STRG-DESC                                      
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 SELECT XRST'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE                      
      ******************************************************************        
       8160-UPDATE-CHECKPOINT-STATUS.                                           
           MOVE '8160-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME.           
           MOVE 'UPDATE' TO PV-DB2-OPERATION-ATTEMPTED.                         
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 UPDATE CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               DISPLAY ' KEY - '   PC-JOB-NAME                                  
                                   PC-PROGRAM-NAME                              
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * 8200-INS-SKU-LOC-CNT.                                                   
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE            
      *-------------------------------------------------------------            
       8200-INS-SKU-LOC-CNT.                                                    
           MOVE '8200-INS-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.                    
           MOVE 'INSERT' TO PV-DB2-OPERATION-ATTEMPTED.                         
           EXEC SQL                                                             
            INSERT INTO ICT_SKU_LOC_CNT                                         
                  (LOC_NBR                                                      
                  ,SKU_NBR                                                      
                  ,CNT_STUP_ID                                                  
                  ,LAST_UPD_TMST                                                
                  ,CNT_SEQ_NBR                                                  
                  ,LAST_UPD_USR_ID                                              
                  ,LAST_ACTN_CDE                                                
                  ,UNT_RTL_AMT                                                  
                  ,LOC_EXPTED_UNT_QTY                                           
                  ,IN_TRNST_UNT_QTY                                             
                  ,ACPTD_TRUK_UNT_QTY                                           
                  ,RTL_SAL_UNT_QTY                                              
                  ,RTL_RET_UNT_QTY                                              
                  ,XFER_IN_UNT_QTY                                              
                  ,XFER_OUT_UNT_QTY                                             
                  ,SCND_UPC_NBR                                                 
                  ,SCND_UNT_QTY                                                 
                  ,CNT_GP_CDE                                                   
                  ,EXEC_TMST                                                    
                  ,MDSE_EXPRN_DTE)                                              
            VALUES                                                              
                (:ICT00011-LOC-NBR                                              
                ,:ICT00011-SKU-NBR                                              
                ,:ICT00011-CNT-STUP-ID                                          
                ,:ICT00011-LAST-UPD-TMST                                        
                ,:ICT00011-CNT-SEQ-NBR                                          
                ,'SYSTEM'                                                       
                ,:ICT00011-LAST-ACTN-CDE                                        
                ,:ICT00011-UNT-RTL-AMT                                          
                ,:ICT00011-LOC-EXPTED-UNT-QTY                                   
                ,:ICT00011-IN-TRNST-UNT-QTY:PV-IN-TRNST-UNT-QTY-NI              
                ,:ICT00011-ACPTD-TRUK-UNT-QTY:PV-ACPTD-TRUK-UNT-QTY-NI          
                ,:ICT00011-RTL-SAL-UNT-QTY:PV-RTL-SAL-UNT-QTY-NI                
                ,:ICT00011-RTL-RET-UNT-QTY:PV-RTL-RET-UNT-QTY-NI                
                ,:ICT00011-XFER-IN-UNT-QTY:PV-XFER-IN-UNT-QTY-NI                
                ,:ICT00011-XFER-OUT-UNT-QTY:PV-XFER-OUT-UNT-QTY-NI              
                ,:ICT00011-SCND-UPC-NBR:PV-SCND-UPC-NBR-NI                      
                ,:ICT00011-SCND-UNT-QTY:PV-SCND-UNT-QTY-NI                      
                ,:ICT00011-CNT-GP-CDE:PV-CNT-GP-CDE-NI                          
                ,:ICT00011-EXEC-TMST:PV-EXEC-TMST-NI                            
                ,:ICT00011-MDSE-EXPRN-DTE:PV-MDSE-EXPRN-DTE-NI)                 
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 INSERT ICT_SKU_LOC_CNT'                     
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *-------------------------------------------------------------            
      * 8300-SEL-SKU-LOC-CNT.                                                   
      *      SELECT CURRENT ICT_SKU_LOC_CNT ROW.                                
      *-------------------------------------------------------------            
       8300-SEL-SKU-LOC-CNT.                                                    
           MOVE '8300-SEL-SKU-LOC-CNT' TO PV-PARAGRAPH-NAME.                    
           MOVE 'SELECT' TO PV-DB2-OPERATION-ATTEMPTED.                         
           EXEC SQL                                                             
            SELECT LOC_NBR                                                      
                  ,SKU_NBR                                                      
                  ,CNT_STUP_ID                                                  
                  ,LAST_UPD_TMST                                                
      *           ,CNT_SEQ_NBR                                                  
                  ,LAST_UPD_USR_ID                                              
                  ,LAST_ACTN_CDE                                                
                  ,UNT_RTL_AMT                                                  
                  ,LOC_EXPTED_UNT_QTY                                           
                  ,IN_TRNST_UNT_QTY                                             
                  ,ACPTD_TRUK_UNT_QTY                                           
                  ,RTL_SAL_UNT_QTY                                              
                  ,RTL_RET_UNT_QTY                                              
                  ,XFER_IN_UNT_QTY                                              
                  ,XFER_OUT_UNT_QTY                                             
                  ,SCND_UPC_NBR                                                 
                  ,SCND_UNT_QTY                                                 
                  ,CNT_GP_CDE                                                   
                  ,EXEC_TMST                                                    
                  ,MDSE_EXPRN_DTE                                               
            INTO                                                                
                 :ICT00011-LOC-NBR                                              
                ,:ICT00011-SKU-NBR                                              
                ,:ICT00011-CNT-STUP-ID                                          
                ,:ICT00011-LAST-UPD-TMST                                        
      *         ,:ICT00011-CNT-SEQ-NBR                                          
                ,:ICT00011-LAST-UPD-USR-ID                                      
                ,:ICT00011-LAST-ACTN-CDE                                        
                ,:ICT00011-UNT-RTL-AMT                                          
                ,:ICT00011-LOC-EXPTED-UNT-QTY                                   
                ,:ICT00011-IN-TRNST-UNT-QTY:PV-IN-TRNST-UNT-QTY-NI              
                ,:ICT00011-ACPTD-TRUK-UNT-QTY:PV-ACPTD-TRUK-UNT-QTY-NI          
                ,:ICT00011-RTL-SAL-UNT-QTY:PV-RTL-SAL-UNT-QTY-NI                
                ,:ICT00011-RTL-RET-UNT-QTY:PV-RTL-RET-UNT-QTY-NI                
                ,:ICT00011-XFER-IN-UNT-QTY:PV-XFER-IN-UNT-QTY-NI                
                ,:ICT00011-XFER-OUT-UNT-QTY:PV-XFER-OUT-UNT-QTY-NI              
                ,:ICT00011-SCND-UPC-NBR:PV-SCND-UPC-NBR-NI                      
                ,:ICT00011-SCND-UNT-QTY:PV-SCND-UNT-QTY-NI                      
                ,:ICT00011-CNT-GP-CDE:PV-CNT-GP-CDE-NI                          
                ,:ICT00011-EXEC-TMST:PV-EXEC-TMST-NI                            
                ,:ICT00011-MDSE-EXPRN-DTE:PV-MDSE-EXPRN-DTE-NI                  
            FROM ICT_SKU_LOC_CNT A                                              
            WHERE A.LOC_NBR       = :ICT00011-LOC-NBR                           
              AND A.SKU_NBR       = :ICT00011-SKU-NBR                           
              AND A.CNT_STUP_ID   = :ICT00011-CNT-STUP-ID                       
              AND A.CNT_SEQ_NBR   = :ICT00011-CNT-SEQ-NBR                       
              AND A.LAST_UPD_TMST =                                             
              (SELECT MAX(B.LAST_UPD_TMST)                                      
               FROM ICT_SKU_LOC_CNT    B                                        
               WHERE B.LOC_NBR     = :ICT00011-LOC-NBR                          
                 AND B.SKU_NBR     = :ICT00011-SKU-NBR                          
                 AND B.CNT_SEQ_NBR = A.CNT_SEQ_NBR                              
                 AND B.CNT_STUP_ID = :ICT00011-CNT-STUP-ID)                     
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
               SET PS-INPUT-SKU-FOUND TO TRUE                                   
              WHEN SQLCODE = +100                                               
               SET PS-INPUT-SKU-NOT-FOUND TO TRUE                               
               DISPLAY 'KEY NF IS - ' ICT00011-LOC-NBR                          
                             ' - '      ICT00011-SKU-NBR                        
                             ' - '      ICT00011-CNT-STUP-ID                    
                             ' - '      ICT00011-CNT-SEQ-NBR                    
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP056 SELECT ICT_SKU_LOC_CNT'                     
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *-------------------------------------------------------------            
      * 8400-INS-SKU-LOC-CNT-ST.                                                
      *      INSERT LAST RUN TIMESTAMP OF TO ICT_CNT_STUP_STAT TABLE            
      *-------------------------------------------------------------            
       8400-INS-SKU-LOC-CNT-ST.                                                 
           MOVE '8400-INS-SKU-LOC-CNT-ST' TO PV-PARAGRAPH-NAME.                 
           MOVE 'INSERT' TO PV-DB2-OPERATION-ATTEMPTED.                         
           EXEC SQL                                                             
             INSERT INTO ICT_LOC_CNT_STAT                                       
               (CNT_STAT_CDE                                                    
               ,LOC_NBR                                                         
               ,CNT_STUP_ID                                                     
               ,CNT_SEQ_NBR                                                     
               ,LAST_UPD_TMST                                                   
               ,LAST_UPD_USR_ID                                                 
               ,LAST_ACTN_CDE)                                                  
             VALUES                                                             
               ('UPD'                                                           
               ,:ICT00020-LOC-NBR                                               
               ,:ICT00020-CNT-STUP-ID                                           
               ,:ICT00020-CNT-SEQ-NBR                                           
               ,:ICT00020-LAST-UPD-TMST                                         
               ,'SYSTEM'                                                        
               ,'A')                                                            
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN SQLCODE = -803                                               
               CONTINUE                                                         
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP054 INSERT ICT_LOC_CNT_STAT'                    
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------               
      *  SELECT FROM XST_LOC TO GET A STORE TIMEZONE.                           
      *----------------------------------------------------------               
       8500-SELECT-XST-LOC.                                                     
           MOVE '8500-SELECT-XST-LOC' TO  PV-PARAGRAPH-NAME.                    
                                                                                
           EXEC SQL                                                             
             SELECT                                                             
                  STD_TM_ZN_CDE                                                 
                 ,DST_IND                                                       
               INTO                                                             
                  :XST00001-STD-TM-ZN-CDE:PV-STD-TM-ZN-CDE-NI                   
                 ,:XST00001-DST-IND:PV-DST-IND-NI                               
               FROM  XST_LOC                                                    
              WHERE  LOC_NBR   = :XST00001-LOC-NBR                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                IF PV-DST-IND-IS-NULL                                           
                  MOVE 'Y' TO XST00001-DST-IND                                  
                END-IF                                                          
                IF PV-STD-TM-ZN-CDE-IS-NULL                                     
                  MOVE 'CST' TO XST00001-STD-TM-ZN-CDE                          
                END-IF                                                          
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP    TO TRUE                                   
               DISPLAY '** ICKUP055 SELECT XST_LOC'                             
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  ==> PROCESSES:                                                *        
      *    - DELETE ALL ROWS WITH SPECIFIED CNT-STUP-ID, LOC-NBR,      *        
      *      CNT-SEQ-NBR AND SKU-NBR.                                  *        
      ******************************************************************        
       8600-DELETE-SKU-LOC.                                                     
           MOVE '8600-DELETE-SKU-LOC' TO PV-PARAGRAPH-NAME.                     
                                                                                
           EXEC SQL                                                             
              DELETE                                                            
              FROM ICT_SKU_LOC_CNT                                              
              WHERE CNT_STUP_ID      = :ICT00011-CNT-STUP-ID                    
                AND LOC_NBR          = :ICT00011-LOC-NBR                        
                AND CNT_SEQ_NBR      = :ICT00011-CNT-SEQ-NBR                    
                AND SKU_NBR          = :ICT00011-SKU-NBR                        
CL3132          AND LAST_UPD_USR_ID  = 'SYSTEM'                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                 DISPLAY 'NOT FOUND - ' ICT00011-CNT-STUP-ID                    
                                  ' - ' ICT00011-LOC-NBR                        
                                  ' - ' ICT00011-CNT-SEQ-NBR                    
                                  ' - ' ICT00011-SKU-NBR                        
               WHEN OTHER                                                       
                   MOVE 'DELETE ICT_SKU_LOC'                                    
                               TO PV-DB2-OPERATION-ATTEMPTED                    
                   PERFORM 9999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
P61756******************************************************************        
P61756*  8700-NO-IND-STAT-CDE-FND                                               
P61756******************************************************************        
P61756 8700-NO-IND-STAT-CDE-FND.                                                
P61756     MOVE '8700-NO-IND-STAT-CDE-FND ' TO PV-PARAGRAPH-NAME.               
P61756     EXEC SQL                                                             
P61756       SELECT                                                             
P61756              LOC_SKU_STAT_CDE                                            
P61756             ,A.LOC_NBR                                                   
P61756             ,ORIG_CLR_DTE                                                
P61756       INTO   :XST00008-LOC-SKU-STAT-CDE                                  
P61756             ,:XST00008-LOC-NBR                                           
P61756             ,:XST00008-ORIG-CLR-DTE:PV-ORIG-CLR-DTE-NULL-BLNK            
P61756       FROM   XST_SKU_LOC  A                                              
P61756             ,XST_SKU      B                                              
P61756       WHERE A.LOC_NBR     = :ICT00011-LOC-NBR                            
P61756        AND  A.BEG_TMST   <= CURRENT TIMESTAMP                            
P61756        AND (A.END_TMST    > CURRENT TIMESTAMP                            
P61756         OR  A.END_TMST    IS NULL)                                       
P61756        AND  B.SKU_NBR     = :ICT00011-SKU-NBR                            
P61756        AND  A.INTR_UPC_ID = B.INTR_UPC_ID                                
P61756     END-EXEC.                                                            
P61756     EVALUATE TRUE                                                        
P61756        WHEN SQLCODE = 0                                                  
P61756     IF XST00008-LOC-SKU-STAT-CDE = '20' OR '90'                          
P61756           SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                          
P61756           SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                          
P61756     ELSE                                                                 
P61756         IF XST00008-LOC-SKU-STAT-CDE = '30'                              
P61756            IF PV-ORIG-CLR-DTE-IS-BLNK                                    
P61756             MOVE PV-CURRENT-DATE TO                                      
P61756                             ICT00011-MDSE-EXPRN-DTE                      
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL TO TRUE                       
P61756            ELSE                                                          
P61756             MOVE XST00008-ORIG-CLR-DTE TO                                
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL  TO TRUE                      
P61756            END-IF                                                        
P61756     ELSE                                                                 
P61756         IF XST00008-LOC-SKU-STAT-CDE = '25'                              
P61756            AND XST00008-LOC-NBR  NOT = 0                                 
P61756            IF PV-ORIG-CLR-DTE-IS-BLNK                                    
P61756             MOVE PV-CURRENT-DATE TO                                      
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL TO TRUE                       
P61756            ELSE                                                          
P61756             MOVE XST00008-ORIG-CLR-DTE  TO                               
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL TO TRUE                       
P61756            END-IF                                                        
P61756     ELSE                                                                 
P61756         MOVE SPACES TO ICT00011-MDSE-EXPRN-DTE                           
P61756         SET PV-ORIG-CLR-DTE-IS-NULL TO TRUE                              
P61756         SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                            
P61756        END-IF                                                            
P61756      END-IF                                                              
P61756     END-IF                                                               
P61756        WHEN SQLCODE = +100                                               
P61756          PERFORM 8800-CHECK-FOR-ACT-LOC-0                                
P61756        WHEN OTHER                                                        
P61756         MOVE SPACES TO ICT00011-MDSE-EXPRN-DTE                           
P61756         SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                            
P61756         SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                            
P61756         DISPLAY '** UNALBE TO FIND SKU STATUS'                           
P61756     END-EVALUATE.                                                        
P61756                                                                          
P61756*-------------------------------------------------------------            
P61756* 8800-CHECK-FOR-ACT-LOC-0                                                
P61756*      CHECK TO SEE IF A SKU IS ACTIVE OR CLEARANCE                       
P61756*-------------------------------------------------------------            
P61756 8800-CHECK-FOR-ACT-LOC-0.                                                
P61756     MOVE '8800-CHECK-FOR-ACT-LOC-0' TO PV-PARAGRAPH-NAME.                
P61756     EXEC SQL                                                             
P61756       SELECT                                                             
P61756              LOC_SKU_STAT_CDE                                            
P61756             ,A.LOC_NBR                                                   
P61756             ,ORIG_CLR_DTE                                                
P61756       INTO   :XST00008-LOC-SKU-STAT-CDE                                  
P61756             ,:XST00008-LOC-NBR                                           
P61756             ,:XST00008-ORIG-CLR-DTE:PV-ORIG-CLR-DTE-NULL-BLNK            
P61756       FROM   XST_SKU_LOC  A                                              
P61756             ,XST_SKU      B                                              
P61756       WHERE A.LOC_NBR     = 0                                            
P61756        AND  A.BEG_TMST   <= CURRENT TIMESTAMP                            
P61756        AND (A.END_TMST    > CURRENT TIMESTAMP                            
P61756         OR  A.END_TMST    IS NULL)                                       
P61756        AND  B.SKU_NBR     = :ICT00011-SKU-NBR                            
P61756        AND  A.INTR_UPC_ID = B.INTR_UPC_ID                                
P61756     END-EXEC.                                                            
P61756     EVALUATE TRUE                                                        
P61756        WHEN SQLCODE = 0                                                  
P61756     IF XST00008-LOC-SKU-STAT-CDE = '20' OR '90'                          
P61756           SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                          
P61756           SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                          
P61756     ELSE                                                                 
P61756         IF XST00008-LOC-SKU-STAT-CDE = '30'                              
P61756            IF PV-ORIG-CLR-DTE-IS-BLNK                                    
P61756             MOVE PV-CURRENT-DATE TO                                      
P61756                             ICT00011-MDSE-EXPRN-DTE                      
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL TO TRUE                       
P61756            ELSE                                                          
P61756             MOVE XST00008-ORIG-CLR-DTE TO                                
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL  TO TRUE                      
P61756            END-IF                                                        
P61756     ELSE                                                                 
P61756         IF XST00008-LOC-SKU-STAT-CDE = '25'                              
P61756            AND XST00008-LOC-NBR = ZERO                                   
P61756            IF PV-ORIG-CLR-DTE-IS-BLNK                                    
P61756             MOVE SPACES TO                                               
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-IS-NULL TO TRUE                          
P61756             SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                        
P61756            ELSE                                                          
P61756             MOVE XST00008-ORIG-CLR-DTE  TO                               
P61756                         ICT00011-MDSE-EXPRN-DTE                          
P61756             SET PV-ORIG-CLR-DTE-NOT-NULL   TO TRUE                       
P61756             SET PV-MDSE-EXPRN-DTE-NOT-NULL TO TRUE                       
P61756            END-IF                                                        
P61756     ELSE                                                                 
P61756         MOVE SPACES TO ICT00011-MDSE-EXPRN-DTE                           
P61756         SET PV-ORIG-CLR-DTE-IS-NULL TO TRUE                              
P61756         SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                            
P61756        END-IF                                                            
P61756      END-IF                                                              
P61756     END-IF                                                               
P61756        WHEN SQLCODE = +100                                               
P61756         MOVE SPACES TO ICT00011-MDSE-EXPRN-DTE                           
P61756         SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                            
P61756         SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                            
P61756        WHEN OTHER                                                        
P61756         MOVE SPACES TO ICT00011-MDSE-EXPRN-DTE                           
P61756         SET PV-ORIG-CLR-DTE-IS-NULL   TO TRUE                            
P61756         SET PV-MDSE-EXPRN-DTE-IS-NULL TO TRUE                            
P61756         DISPLAY '** UNALBE TO FIND SKU STATUS'                           
P61756     END-EVALUATE.                                                        
      ******************************************************************        
           COPY ICPD010.                                                        
      ******************************************************************        
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           SUBTRACT 1 FROM DI-MNT-CNTR.                                         
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM ICKUP056 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'MAINTENANCE RECORDS PROCESSED:' DI-MNT-CNTR.                
           DISPLAY 'COMMITS TAKEN                :' DI-COMMIT-CNTR.             
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM ICKUP056  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'ICKUP056 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
