      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.   PXKUT100.                                          00050001
       AUTHOR.       RICH KELLEN.                                       00060000
       INSTALLATION. KOHLS DEPARTMENT STORES.                           00070000
       DATE-WRITTEN. JUNE 2010.                                         00080001
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ***************************************************************** 00110000
      * WR36056 - MISSING SKU/STORE DATA LOOKUP.                      * 00120024
      * THIS PROGRAM READS OF FILE OF SKUS/STORES UNLOADED FROM THE   * 00130001
      * PXTW_MSSG_STR_MDSE TABLE AND LOOKS UP DATA NEEDED TO INSERT   * 00140001
      * THOSE SKU/STORES INTO THE PXT_EVNT_STR_MDSE TABLE AND WRITES  * 00150004
      * IT TO AN OUTPUT FILE. 2 OTHER FILES ARE ALSO CREATED TO ALLOW * 00160013
      * THE UNIX PROCESS TO CHECK THE INTEGRITY OF DATA FILES SENT TO * 00161013
      * UNIX. THESE FILES CONTAIN A SINGLE RECORD, ONE WITH THE NBR   * 00162013
      * OF ROWS ON THE PRICE CHANGE LOOKUP FILE AND THE OTHER WITH    * 00163013
      * THE NUMBER OF RECORDS ON THE INPUT DRIVER FILE.               * 00164013
      ***************************************************************** 00170000
      ******************** HISTORY OF CHANGES **************************00180000
      * CHG ID/                                                        *00190000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *00200000
      * -------  ----------  ----------------------------------------  *00210000
      * WR36056  07/01/2010  INITIAL IMPLEMENTATION                    *00220013
      ******************************************************************00230000
      ******************************************************************00240000
       ENVIRONMENT DIVISION.                                            00250000
      ******************************************************************00260000
      *                                                                 00270000
       CONFIGURATION SECTION.                                           00280000
       SOURCE-COMPUTER.   IBM-3090.                                     00290000
       OBJECT-COMPUTER.   IBM-3090.                                     00300000
      *                                                                 00310000
       INPUT-OUTPUT SECTION.                                            00320000
       FILE-CONTROL.                                                    00330000
           SELECT MISSING-SKU-UNLOAD-FILE                               00340001
               ASSIGN                       TO INP01.                   00350000
                                                                        00360013
            SELECT SKU-ADD-FILE                                         00370010
              ASSIGN                        TO OUT01.                   00380001
                                                                        00390000
            SELECT SKU-ADD-COUNT-FILE                                   00391010
              ASSIGN                        TO OUT02.                   00392010
                                                                        00393010
            SELECT MISSING-SKU-COUNT-FILE                               00394010
              ASSIGN                        TO OUT03.                   00395010
                                                                        00396010
      ******************************************************************00400000
       DATA DIVISION.                                                   00410000
      ******************************************************************00420000
      *                                                                 00430000
       FILE SECTION.                                                    00440000
       FD  MISSING-SKU-UNLOAD-FILE                                      00450001
           RECORDING MODE F                                             00460000
           BLOCK CONTAINS 0 RECORDS                                     00470000
           RECORD CONTAINS 047 CHARACTERS                               00480012
           LABEL RECORDS ARE STANDARD                                   00490000
           DATA RECORD IS MISSING-SKU-UNLOAD-REC.                       00500001
       01  MISSING-SKU-UNLOAD-REC                 PIC X(047).           00510012
      *                                                                 00520000
       FD SKU-ADD-FILE                                                  00530010
           RECORDING MODE IS F                                          00540000
           BLOCK CONTAINS 0 RECORDS                                     00550000
           RECORD CONTAINS 088 CHARACTERS                               00551008
           LABEL RECORDS ARE STANDARD                                   00552008
           DATA RECORD IS SKU-ADD-RECORD.                               00560010
      *                                                                 00570000
       01 SKU-ADD-RECORD                         PIC X(088).            00580010
      *                                                                 00590000
       FD SKU-ADD-COUNT-FILE                                            00591010
           RECORDING MODE IS F                                          00592010
           BLOCK CONTAINS 0 RECORDS                                     00593010
           RECORD CONTAINS 015 CHARACTERS                               00594010
           LABEL RECORDS ARE STANDARD                                   00595010
           DATA RECORD IS SKU-ADD-COUNT-RECORD.                         00596010
      *                                                                 00597010
       01 SKU-ADD-COUNT-RECORD.                                         00598010
          05  SKU-ADD-COUNT                      PIC 9(7).              00598110
          05  FILLER                             PIC X(8).              00598210
      *                                                                 00599010
       FD MISSING-SKU-COUNT-FILE                                        00599110
           RECORDING MODE IS F                                          00599210
           BLOCK CONTAINS 0 RECORDS                                     00599310
           RECORD CONTAINS 015 CHARACTERS                               00599410
           LABEL RECORDS ARE STANDARD                                   00599510
           DATA RECORD IS MISSING-SKU-COUNT-RECORD.                     00599610
      *                                                                 00599710
       01 MISSING-SKU-COUNT-RECORD.                                     00599910
          05 MISSING-SKU-COUNT                   PIC 9(07).             00600010
          05 FILLER                              PIC X(8).              00600110
      *                                                                 00600210
       WORKING-STORAGE SECTION.                                         00601010
                                                                        00610000
       01 ABEND-CODE                      PIC S9(04)    VALUE ZERO      00620000
                                                        COMP SYNC.      00630000
           88  ABEND-4013-DB2-ERROR                        VALUE +4013. 00640000
      *                                                                 00650000
                                                                        00700000
       01 PC-PROGRAM-CONSTANTS.                                         00930000
          05  PC-PXKUT100                 PIC X(10)      VALUE          00940001
              'PXKUT100  '.                                             00950001
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)                     00960000
               VALUE 'PXKUT100'.                                        00970001
          05  PC-PROGRAM-NAME             PIC X(08)  VALUE              00980000
              'PXKUT100'.                                               00990001
           05  PC-MAX-RETRIES            PIC S9(04) VALUE +5 COMP.      00991026
       01 PV-PROGRAM-VARIABLES.                                         01000000
          05  PV-SQLCODE                  PIC +9(04).                   01010000
          05  PV-STR-NBR                  PIC 9(5)    VALUE ZEROS.      01011017
          05  PV-SKU-NBR                  PIC 9(8)    VALUE ZEROS.      01012017
          05  PV-OPERATION-MSG            PIC X(60)  VALUE SPACES.      01020000
          05  PV-PRCHG-ID                 PIC X(10)  VALUE SPACES.      01021009
          05  PV-UNT-RTL-AMT              PIC X(09)  VALUE SPACES.      01022015
          05  PV-UNT-RTL-AMT-N REDEFINES PV-UNT-RTL-AMT                 01022116
                                          PIC 9(07)V99.                 01022216
          05  PV-GP-RTL-QTY               PIC X(10)  VALUE SPACES.      01022316
          05  PV-GP-RTL-AMT               PIC X(10)  VALUE SPACES.      01023009
          05  PV-WRITE-CNT                PIC 9(7)   VALUE ZERO         01030000
                                                     COMP-3.            01040000
          05  PV-READ-CNT                 PIC 9(7)      VALUE ZERO      01050000
                                                        COMP-3.         01060000
          05  PV-EVNT-NOT-FOUND-CNT       PIC 9(7)      VALUE ZERO      01070002
                                                        COMP-3.         01080002
          05  PV-RETRY-COUNT              PIC S9(04)    VALUE +0        01090026
                                                        COMP SYNC.      01100000
          05  PV-ABEND-CODE        PIC S9(04)     VALUE +0              01150000
                                                  COMP SYNC.            01160000
          05  PV-PARA-NAME         PIC X(35).                           01170000
          05  PV-DISPLAY-SKU-NBR        PIC 9(10)  VALUE ZERO.          01180026
          05  PV-DISPLAY-LOC-NBR        PIC 9(4)   VALUE ZERO.          01181026
                                                                        01190000
      *                                                                 01220000
       01 PS-PROGRAM-SWITCHES.                                          01230000
          05  END-OF-MSSG-SKU-UNLD-FILE-SW PIC X(1) VALUE 'N'.          01240002
              88  PS-END-OF-MSSG-SKU-UNLD-FILE VALUE 'Y'.               01250002
          05  PS-INPUT-FILE-EMPTY-SW       PIC X(1) VALUE 'N'.          01260020
              88  PS-INPUT-FILE-EMPTY               VALUE 'Y'.          01270020
              88  PS-INPUT-FILE-NOT-EMPTY           VALUE 'N'.          01280020
          05  PS-EVENT-FOUND-SW            PIC X(1) VALUE 'N'.          01281021
              88  PS-EVENT-FOUND                    VALUE 'Y'.          01282021
              88  PS-EVENT-NOT-FOUND                VALUE 'N'.          01283021
      *                                                                 01290000
      ******************************************************************01300000
      **TIMESTAMP VARIABLES                                             01310000
      ******************************************************************01320000
      ******************************************************************01480000
      * DB2 ERROR MESSAGES FORMAT AREA.                                 01490000
      ******************************************************************01500000
           COPY DPWS004.                                                01510000
                                                                        01520000
      *                                                                 01570000
      ******************************************************************01580000
      * COPY MEMBER FOR THE MISSING SKUS INPUT FILE (INPUT)             01590001
      ******************************************************************01600000
      *                                                                 01610000
           COPY PXRD120.                                                01620002
      *                                                                 01630000
      ******************************************************************01640000
      * COPY MEMBER FOR THE MISSING SKU ADD TRANS FILE (OUTPUT)         01650001
      ******************************************************************01660000
      *                                                                 01670000
           COPY PXRD121.                                                01680002
                                                                        01690000
      *---------------------------------------------                    01700002
      *  MB2 COMMUNICATION AREA.                                        01710014
      *---------------------------------------------                    01720002
           EXEC SQL                                                     01730002
                INCLUDE SQLCA                                           01740002
           END-EXEC.                                                    01750002
                                                                        01760002
           COPY SQLCA2.                                                 01770002
      ******************************************************************01780002
      *  DB2 TABLE XST00001 (XST_LOC)                                   01790003
      ******************************************************************01800002
           EXEC SQL                                                     01810002
               INCLUDE XST00001                                         01820003
           END-EXEC.                                                    01830002
      ******************************************************************01900003
      *  DB2 TABLE XST00010 (XST_SKU)                                   01910003
      ******************************************************************01920003
           EXEC SQL                                                     01930003
               INCLUDE XST00010                                         01940003
           END-EXEC.                                                    01950003
      ******************************************************************01960003
      *  DB2 TABLE XST00011 (XST_SKU_ATTR)                              01970003
      ******************************************************************01980003
           EXEC SQL                                                     01990003
               INCLUDE XST00011                                         02000003
           END-EXEC.                                                    02010003
      ******************************************************************02020003
      *  DB2 TABLE XST00013 (XST_VND_STYL)                              02030003
      ******************************************************************02040003
           EXEC SQL                                                     02050003
               INCLUDE XST00013                                         02060003
           END-EXEC.                                                    02070003
      ******************************************************************02080003
      *  DB2 TABLE XST00015 (XST_DEPT)                                  02090003
      ******************************************************************02100003
           EXEC SQL                                                     02110003
               INCLUDE XST00015                                         02120003
           END-EXEC.                                                    02130003
      ******************************************************************02140003
      *  DB2 TABLE XST00016 (XST_BUS_GP)                                02150003
      ******************************************************************02160003
           EXEC SQL                                                     02170003
               INCLUDE XST00016                                         02180003
           END-EXEC.                                                    02190003
      ******************************************************************02200003
      *  DB2 TABLE XST00023 (XST_STR_GP_MBSHP)                          02210003
      ******************************************************************02220003
           EXEC SQL                                                     02230003
               INCLUDE XST00023                                         02240003
           END-EXEC.                                                    02250003
      ******************************************************************02260003
      *  DB2 TABLE XST00063 (XST_PRCHG)                                 02270003
      ******************************************************************02280003
           EXEC SQL                                                     02290003
               INCLUDE XST00063                                         02300003
           END-EXEC.                                                    02310003
      ******************************************************************02320003
      *  DB2 TABLE XST00064 (XST_STR_GP_MBSHP)                          02330003
      ******************************************************************02340003
           EXEC SQL                                                     02350003
               INCLUDE XST00064                                         02360003
           END-EXEC.                                                    02370003
      ******************************************************************02380003
      *  DB2 TABLE XST00065 (XST_PRCHG_MDSE)                            02390003
      ******************************************************************02400003
           EXEC SQL                                                     02410003
               INCLUDE XST00065                                         02420003
           END-EXEC.                                                    02430003
      ******************************************************************02440003
       PROCEDURE DIVISION.                                              02450000
      ******************************************************************02460000
      *                                                                 02470000
          EJECT                                                         02480000
       0000-MAINLINE-PROCESSING.                                        02490000
      ******************************************************************02500000
      * MAIN PROCESSING ROUTINE                                       * 02510000
      ******************************************************************02520000
                                                                        02530000
            PERFORM 1000-INIT.                                          02540000
            PERFORM 2000-PROCESS-MSSG-SKU-LOOKUP                        02550002
              UNTIL PS-END-OF-MSSG-SKU-UNLD-FILE.                       02560002
      * THIS WRITES THE LAST RECORD PROVIDED THERE WAS DATA ON THE      02560120
      * INPUT FILE, OTHERWISE, NO LAST RECORD WILL BE WRITTEN.THE       02560220
            IF PS-INPUT-FILE-NOT-EMPTY                                  02560320
              AND PS-EVENT-FOUND                                        02560421
              PERFORM 7100-WRITE-SKU-ADD-FILE.                          02561020
                                                                        02570000
            PERFORM 9000-END-OF-JOB.                                    02580000
                                                                        02590000
            GOBACK.                                                     02600000
                                                                        02610000
      ******************************************************************02620000
      * OPEN FILES AND GET CURRENT DATE AND TIME FROM DB2.             *02630000
      ******************************************************************02640000
      *                                                                 02650000
       1000-INIT.                                                       02660000
            MOVE '*1000-INIT.              *' TO PV-PARA-NAME.          02670000
            OPEN INPUT  MISSING-SKU-UNLOAD-FILE                         02680001
                 OUTPUT SKU-ADD-FILE                                    02690010
                        SKU-ADD-COUNT-FILE                              02691010
                        MISSING-SKU-COUNT-FILE.                         02700010
           PERFORM 7000-READ-MSSG-SKU-UNLD-FILE.                        02790005
           IF PS-END-OF-MSSG-SKU-UNLD-FILE                              02790120
             MOVE 'Y' TO PS-INPUT-FILE-EMPTY-SW.                        02790220
           MOVE PXRD120-STR-NBR TO PV-STR-NBR.                          02791019
           MOVE PXRD120-SKU-NBR TO PV-SKU-NBR.                          02792019
                                                                        02800000
                                                                        02810000
      ******************************************************************02820000
      * PROCESS MISSING SKU PRICE CHANGE LOOKUP.                        02830013
      ******************************************************************02840000
       2000-PROCESS-MSSG-SKU-LOOKUP.                                    02850002
      * THIS LOGIC WILL SUM THE SCANNED QTY FOR EACH UNIQUE SKU/STORE   02850120
      * COMBINATION.                                                    02850219
           IF PV-STR-NBR NOT = PXRD120-STR-NBR                          02850319
             OR PV-SKU-NBR NOT = PXRD120-SKU-NBR                        02850419
               IF PS-EVENT-FOUND                                        02850822
                 PERFORM 7100-WRITE-SKU-ADD-FILE                        02850922
               END-IF                                                   02851022
               MOVE PXRD120-SCND-UNT-QTY TO PXRD121-SCND-UNT-QTY        02851123
               MOVE PXRD120-STR-NBR TO PV-STR-NBR                       02851223
               MOVE PXRD120-SKU-NBR TO PV-SKU-NBR                       02851323
           ELSE                                                         02851419
               ADD PXRD120-SCND-UNT-QTY TO PXRD121-SCND-UNT-QTY.        02851519
           MOVE PXRD120-STR-NBR           TO XST00023-STR-NBR           02852019
                                             PXRD121-STR-NBR.           02852119
           MOVE PXRD120-SKU-NBR           TO XST00065-SKU-NBR           02853019
                                             PXRD121-SKU-NBR.           02854019
           PERFORM 8000-MSSG-SKU-LOOKUP-SELECT.                         02856005
           IF PS-EVENT-FOUND                                            02856128
      *** ONLY SKU/STORES WITH A MATCHING CURRENT EVENT FOUND WILL HAVE 02857027
      *** A RECORD WRITTEN. ALL OTHERS WILL BE DISCARDED.               02858027
             MOVE PV-PRCHG-ID            TO PXRD121-PRCHG-ID            02859028
             MOVE XST00063-EVNT-CTG-CDE  TO PXRD121-CTG-CDE             02859128
             MOVE XST00063-PRCHG-TYP-CDE TO PXRD121-EVNT-TYPE-CDE       02859228
             MOVE XST00064-PRCHG-EFF-DTE TO PXRD121-EVNT-DTE            02859328
      *** CURRENTLY WE WANT TO POPULATE OH-QTY WITH ZERO.               02859427
      *** IF, IN THE FUTURE, IT IS DESIRED TO HAVE THE ACTUAL ONHAND    02859527
      *** QTY, THEN CHANGE SQL ABOVE TO SELECT STR_EXPTED_QTY FROM      02859627
      *** THE PXT_PRCHG_PRCG TABLE. THE DOWNSTREAM LOAD JOBS ARE SETUP  02859727
      *** TO HANDLE IT.                                                 02859827
             MOVE ZERO                   TO PXRD121-OH-QTY              02859928
             MOVE PV-UNT-RTL-AMT-N       TO PXRD121-NW-UNT-RTL-AMT      02860028
             MOVE PV-GP-RTL-QTY          TO PXRD121-GP-RTL-QTY          02860128
             MOVE PV-GP-RTL-AMT          TO PXRD121-GP-RTL-AMT          02860228
           END-IF.                                                      02860328
           PERFORM 7000-READ-MSSG-SKU-UNLD-FILE.                        02861005
                                                                        02870000
                                                                        02980000
      ******************************************************************02990000
      *  READ INPUT FILE                                               *03000002
      ******************************************************************03010000
       7000-READ-MSSG-SKU-UNLD-FILE.                                    03020002
           ADD 1 TO PV-READ-CNT.                                        03030002
           READ MISSING-SKU-UNLOAD-FILE INTO PXRD120-MSSG-SKU-FILE      03040002
             AT END                                                     03050000
               SET PS-END-OF-MSSG-SKU-UNLD-FILE TO TRUE.                03060002
                                                                        03070000
      ******************************************************************03080000
      *  WRITE OUTPUT FILE                                             *03090002
      ******************************************************************03100000
       7100-WRITE-SKU-ADD-FILE.                                         03110010
           WRITE SKU-ADD-RECORD FROM                                    03130010
             PXRD121-MSSG-SKU-TRANS-FILE.                               03140006
           ADD 1 TO PV-WRITE-CNT.                                       03143019
                                                                        03150000
      *------------------------------------------------------           03160002
      * 8000-MSS-SKU-LOOKUP-SELECT.                                     03170002
      *     LOOKS UP DATA NEEDED TO INSERT COMPLETE ROWS TO             03180003
      *     THE PXT_EVNT_STR_MDSE TABLE.                                03190003
      *------------------------------------------------------           03200003
                                                                        03210003
       8000-MSSG-SKU-LOOKUP-SELECT.                                     03220003
                                                                        03230003
           PERFORM WITH TEST AFTER                                      03231026
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       03232026
                 UNTIL SQLCODE = +0                                     03233026
                    OR SQLCODE = +100                                   03234026
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  03235026
               EXEC SQL                                                 03240026
                  SELECT DIGITS(A.PRCHG_ID) PRCHG_ID                    03250026
                         ,B.PRCHG_EFF_DTE                               03270026
                         ,DIGITS(E.UNT_RTL_AMT) UNT_RTL_AMT             03280026
                         ,A.EVNT_CTG_CDE                                03300026
                         ,A.PRCHG_TYP_CDE                               03310026
                         ,DIGITS(COALESCE(E.GP_RTL_QTY,0))              03330026
                         ,DIGITS(COALESCE(E.GP_RTL_AMT,0))              03340026
                  INTO    :PV-PRCHG-ID                                  03341026
                         ,:XST00064-PRCHG-EFF-DTE                       03343026
                         ,:PV-UNT-RTL-AMT                               03344026
                         ,:XST00063-EVNT-CTG-CDE                        03346026
                         ,:XST00063-PRCHG-TYP-CDE                       03347026
                         ,:PV-GP-RTL-QTY                                03349026
                         ,:PV-GP-RTL-AMT                                03349126
                   FROM   XST_PRCHG         A                           03350026
                         ,XST_STR_GP_PRCHG  B                           03360026
                         ,XST_STR_GP_MBSHP  C                           03370026
                         ,XST_LOC           D                           03380026
                         ,XST_PRCHG_MDSE    E                           03390026
                         ,XST_SKU           F                           03400026
                         ,XST_VND_STYL      G                           03410026
                         ,XST_DEPT          H                           03420026
                         ,XST_BUS_GP        I                           03430026
                   WHERE A.PRCHG_ID       = B.PRCHG_ID                  03450026
                     AND B.STR_GP_TYP_CDE = C.STR_GP_TYP_CDE            03460026
                     AND B.STR_GP_ID      = C.STR_GP_ID                 03470026
                     AND C.STR_NBR        = D.LOC_NBR                   03480026
                     AND B.STR_GP_TYP_CDE = E.STR_GP_TYP_CDE            03490026
                     AND B.STR_GP_ID      = E.STR_GP_ID                 03500026
                     AND B.PRCHG_ID       = E.PRCHG_ID                  03510026
                     AND E.SKU_NBR        = F.SKU_NBR                   03520026
                     AND F.STYL_ID        = G.STYL_ID                   03530026
                     AND G.DEPT_NBR       = H.DEPT_NBR                  03540026
                     AND H.BUS_GP_NBR     = I.BUS_GP_NBR                03550026
                     AND (C.EFF_END_DTE   >= B.PRCHG_EFF_DTE            03570026
                       OR C.EFF_END_DTE   IS NULL)                      03580026
                     AND C.EFF_BEG_DTE    <= B.PRCHG_EFF_DTE            03590026
                     AND B.PRCHG_EFF_DTE <= CURRENT DATE + 1 DAY        03600026
72507                AND B.PRCHG_EFF_DTE >  CURRENT DATE - 7 DAYS       03610032
                     AND A.PRCHG_TYP_CDE  IN                            03620026
                      ('RCP', 'MK1','MK2', 'FNL', 'CTA', 'MUA', 'MDA')  03621026
                     AND A.EVNT_CTG_CDE   IN ('CLR','RCP', 'ACT')       03630026
                     AND A.PRCHG_ST_CDE   = 'APR'                       03640026
                     AND A.LAST_ACTN_CDE  <> 'D'                        03650026
                     AND B.LAST_ACTN_CDE  <> 'D'                        03660026
                     AND E.LAST_ACTN_CDE  <> 'D'                        03670026
                     AND E.SKU_NBR        = :XST00065-SKU-NBR           03680026
                     AND C.STR_NBR        = :XST00023-STR-NBR           03690026
                     AND B.PRCHG_EFF_DTE  =                             03691026
                       (SELECT MAX(M.PRCHG_EFF_DTE)                     03692026
                        FROM XST_STR_GP_PRCHG  M                        03693026
                        WHERE M.STR_GP_TYP_CDE = B.STR_GP_TYP_CDE       03694026
                          AND M.PRCHG_EFF_DTE <= CURRENT DATE + 1 DAY   03695026
72507                     AND M.PRCHG_EFF_DTE >  CURRENT DATE - 7 DAYS  03695132
                          AND M.STR_GP_ID      = B.STR_GP_ID)           03696026
                   FETCH FIRST ROW ONLY                                 03697026
                   WITH UR                                              03710026
               END-EXEC                                                 03720026
                                                                        03730003
               EVALUATE TRUE                                            03740026
                  WHEN SQLCODE = -904                                   03741026
                  WHEN SQLCODE = -913                                   03742026
P10171            WHEN SQLCODE = -911                                   03742130
                    CONTINUE                                            03743026
                  WHEN SQLCODE = +0                                     03750026
                   SET PS-EVENT-FOUND          TO TRUE                  03823026
                  WHEN SQLCODE = +100                                   03850026
                       SET PS-EVENT-NOT-FOUND  TO TRUE                  03851026
                       ADD 1 TO PV-EVNT-NOT-FOUND-CNT                   03860026
                  WHEN OTHER                                            03870026
                    DISPLAY 'SELECT FAILED IN PARA 8000'                03880026
                    DISPLAY 'SKU-NBR:     ' PXRD120-SKU-NBR             03890026
                    DISPLAY 'LOC-NBR:     ' PXRD120-STR-NBR             03900026
                    DISPLAY 'CREATE-TMST: ' PXRD120-CRE-TMST            03910026
                    DISPLAY 'SCND-UNT-QTY:' PXRD120-SCND-UNT-QTY        03920026
                    PERFORM 9980-SQL-ERROR                              03930026
               END-EVALUATE                                             03940026
           END-PERFORM.                                                 03941026
                                                                        03942026
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          03943026
               MOVE XST00065-SKU-NBR              TO PV-DISPLAY-SKU-NBR 03944026
               MOVE XST00023-STR-NBR              TO PV-DISPLAY-LOC-NBR 03945026
               DISPLAY '** '                                            03946026
                       PC-CURRENT-PROGRAM-NAME                          03947026
                       ' - SKU NBR: '                                   03948026
                       PV-DISPLAY-SKU-NBR                               03949026
               DISPLAY '** '                                            03949126
                       PC-CURRENT-PROGRAM-NAME                          03949226
                       ' - LOC NBR: '                                   03949326
                       PV-DISPLAY-LOC-NBR                               03949426
               MOVE '8000-MSSG-SKU-LOOKUP-SELECT' TO PV-PARA-NAME       03949526
               MOVE 'SELECT EVENT DATA'           TO PV-OPERATION-MSG   03949626
               PERFORM 9980-SQL-ERROR                                   03949726
           END-IF.                                                      03949826
                                                                        03950003
      ******************************************************************03960000
      *  WRITE COUNTER FILES, CLOSE FILES, DISPLAY COUNTERS            *03970013
      ******************************************************************03980000
       9000-END-OF-JOB.                                                 03990000
           COMPUTE MISSING-SKU-COUNT = PV-READ-CNT - 1.                 03991010
           MOVE  PV-WRITE-CNT        TO SKU-ADD-COUNT                   03992010
           DISPLAY 'NBR OF RECS READ        -   '                       04000002
                   MISSING-SKU-COUNT.                                   04010010
           DISPLAY 'NBR OF EVENTS NOT FOUND -   '                       04020002
                   PV-EVNT-NOT-FOUND-CNT.                               04030002
           DISPLAY 'NBR OF RECORDS WRITTEN  -   '                       04040002
                   PV-WRITE-CNT.                                        04050002
           WRITE SKU-ADD-COUNT-RECORD.                                  04051010
           WRITE MISSING-SKU-COUNT-RECORD.                              04052010
           CLOSE MISSING-SKU-UNLOAD-FILE                                04060001
                 SKU-ADD-FILE                                           04070010
                 SKU-ADD-COUNT-FILE                                     04071010
                 MISSING-SKU-COUNT-FILE.                                04072010
                                                                        04080000
      *----------------------------------------------------------       04090002
       9980-SQL-ERROR.                                                  04100002
      *----------------------------------------------------------       04110002
                                                                        04120002
           MOVE SQLCODE    TO SQLCODE2.                                 04130002
           MOVE SQLERRD(3) TO SQLERRD3.                                 04140002
           DISPLAY '** PROGRAM        ** = PXKUT100'.                   04160002
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                  04170002
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                  04180002
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                   04190002
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                  04200002
                                                                        04210002
           SET ABEND-4013-DB2-ERROR TO TRUE.                            04220004
           CALL 'ILBOABN0' USING ABEND-CODE.                            04230002
                                                                        04240002
      *                                                                 04250000
