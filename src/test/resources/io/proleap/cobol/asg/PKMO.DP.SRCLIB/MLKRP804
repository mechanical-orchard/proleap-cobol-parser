       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         MLKRP804                                             
       AUTHOR.             PAUL JORGENSEN.                                      
       INSTALLATION.       KOHLS DEPARTMENT STORES.                             
       DATE-WRITTEN.       10/06.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *  THIS IS A REPORT SHOWING EIR, EIC, CMU%, GM DOLLORS AND %,             
      *  BOTH ORIGINAL AND ADJUSTED AMTS FOR DATA SMOOTHING OUTLYER             
      *  REPORTING                                                              
      ******************************************************************        
      *                     PROGRAM LOG                                         
      ******************************************************************        
      * 01/01/07  P JORGENSEN                                                   
      *        INITIAL IMPLEMENTATION                                           
      ******************************************************************        
      ******************************************************************        
                                                                        MLKRP804
       ENVIRONMENT DIVISION.                                            MLKRP804
       CONFIGURATION SECTION.                                           MLKRP804
       INPUT-OUTPUT SECTION.                                            MLKRP804
       FILE-CONTROL.                                                    MLKRP804
                                                                        MLKRP804
           SELECT CONTROL-CARD-FILE                                     MLKRP804
               ASSIGN TO INP01.                                         MLKRP804
                                                                        MLKRP804
           SELECT OUTLYER-RPT-DATA-FILE                                 MLKRP804
               ASSIGN TO INP02.                                         MLKRP804
                                                                        MLKRP804
           SELECT OUTLYER-REPORT-FILE                                   MLKRP804
               ASSIGN TO RPT01.                                         MLKRP804
                                                                        MLKRP804
       DATA DIVISION.                                                   MLKRP804
       FILE SECTION.                                                    MLKRP804
                                                                        MLKRP804
       FD  CONTROL-CARD-FILE                                            MLKRP804
           LABEL RECORDS ARE OMITTED                                    MLKRP804
           RECORDING MODE IS F                                          MLKRP804
           BLOCK CONTAINS 0 RECORDS.                                    MLKRP804
       01  CONTROL-CARD-REC        PIC X(080).                          MLKRP804
                                                                        MLKRP804
       FD  OUTLYER-RPT-DATA-FILE                                        MLKRP804
           LABEL RECORDS ARE OMITTED                                    MLKRP804
           RECORDING MODE IS F                                          MLKRP804
           BLOCK CONTAINS 0 RECORDS.                                    MLKRP804
       01  OUTLYER-RPT-DATA-REC    PIC X(350).                          MLKRP804
                                                                        MLKRP804
       FD  OUTLYER-REPORT-FILE                                          MLKRP804
           LABEL RECORDS ARE OMITTED                                    MLKRP804
           RECORD CONTAINS 200 CHARACTERS                               MLKRP804
           RECORDING  MODE IS F                                         MLKRP804
           BLOCK CONTAINS 0 RECORDS.                                    MLKRP804
       01  OUTLYER-REPORT-REC      PIC X(200).                          MLKRP804
                                                                        MLKRP804
       WORKING-STORAGE SECTION.                                         MLKRP804
                                                                        MLKRP804
       01  CC-CONTROL-CARD.                                             MLKRP804
           05  FILLER                      PIC X(01)       VALUE SPACE. MLKRP804
               88  CC-COMMENT-RECORD                       VALUE '*'.           
               88  CC-VALID-RECORD                         VALUE 'V'.           
               88  CC-NOT-COMMENT-RECORD                   VALUE SPACE.         
           05  CC-REPORT-TITLE             PIC X(55)       VALUE SPACES.MLKRP804
           05  FILLER                      PIC X(39)       VALUE SPACES.        
                                                                        MLKRP804
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.MLKRP804
           88  ABEND-4003                          VALUE +4003.         MLKRP804
                                                                        MLKRP804
       01  PC-PROGRAM-CONSTANTS.                                        00770000
      *    05  PC-MAX-LINE-COUNT       PIC S9(3) COMP-3 VALUE +60.      00780023
           05  PC-MAX-LINE-COUNT       PIC S9(3) COMP-3 VALUE +75.      00780023
           05  PC-BLANK-LINE           PIC X(200) VALUE SPACES.         MLKRP804
                                                                        MLKRP804
       01  PV-PROGRAM-VARIABLES.                                        MLKRP804
           05  PV-PAGE-COUNT           PIC S9(05)  VALUE ZERO.          MLKRP804
           05  PV-NBR-OF-LINES         PIC S9(03)  VALUE ZERO.          MLKRP804
           05  PV-LINE-COUNT           PIC S9(03)  VALUE ZERO COMP-3.   MLKRP804
           05  PV-PREV-GMM             PIC X(02)   VALUE SPACES.                
           05  PV-PREV-DMM             PIC X(02)   VALUE SPACES.                
           05  PV-PREV-BUYER           PIC X(03)   VALUE SPACES.                
           05  PV-PREV-DEPT-NBR        PIC S9(04)  COMP-3                       
                                                   VALUE ZERO.                  
           05  PV-ADJ-EIC-ADJUSTS-AMT  PIC S9(04)V99                            
                                                    COMP-3.                     
           05  PV-ADJ-END-INV-COST-AMT PIC S9(11)V99                            
                                                    COMP-3.                     
           05  PV-WORK-PERCENT         PIC S9(04)V9(02)                         
                                                    COMP-3.                     
           05  PV-DEPT-NBR-3X          PIC X(03).                               
           05  PV-DEPT-NBR-3N REDEFINES PV-DEPT-NBR-3X                          
                                       PIC 9(03).                               
           05  PV-OPER-ATTEMPTED       PIC X(30)   VALUE SPACES.        MLKRP804
           05  PV-PARA                 PIC X(30)   VALUE SPACES.        MLKRP804
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)   VALUE SPACES.        MLKRP804
           05  PV-FSCL-END-DTE.                                         MLKRP804
               10  PV-FSCL-YY          PIC X(4).                        MLKRP804
               10  FILLER              PIC X.                           MLKRP804
               10  PV-FSCL-MM          PIC X(2).                        MLKRP804
               10  FILLER              PIC X.                           MLKRP804
               10  PV-FSCL-DD          PIC X(2).                        MLKRP804
           05  PV-FSCL-END-DTE-HEADINGS.                                        
               10  PV-FSCL-END-DTE-JAN.                                         
                   15  FILLER           PIC X(10) VALUE ' JANUARY, '.           
                   15  PV-FSCL-JAN-YEAR PIC X(05).                              
               10  PV-FSCL-END-DTE-FEB.                                         
                   15  FILLER           PIC X(10) VALUE 'FEBRUARY, '.           
                   15  PV-FSCL-FEB-YEAR PIC X(05).                              
               10  PV-FSCL-END-DTE-MAR.                                         
                   15  FILLER           PIC X(09) VALUE '  MARCH, '.            
                   15  PV-FSCL-MAR-YEAR PIC X(06).                              
               10  PV-FSCL-END-DTE-APR.                                         
                   15  FILLER           PIC X(09) VALUE '  APRIL, '.            
                   15  PV-FSCL-APR-YEAR PIC X(06).                              
               10  PV-FSCL-END-DTE-MAY.                                         
                   15  FILLER           PIC X(08) VALUE '   MAY, '.             
                   15  PV-FSCL-MAY-YEAR PIC X(07).                              
               10  PV-FSCL-END-DTE-JUN.                                         
                   15  FILLER           PIC X(08) VALUE '  JUNE, '.             
                   15  PV-FSCL-JUN-YEAR PIC X(07).                              
               10  PV-FSCL-END-DTE-JUL.                                         
                   15  FILLER           PIC X(08) VALUE '  JULY, '.             
                   15  PV-FSCL-JUL-YEAR PIC X(07).                              
               10  PV-FSCL-END-DTE-AUG.                                         
                   15  FILLER           PIC X(09) VALUE ' AUGUST, '.            
                   15  PV-FSCL-AUG-YEAR PIC X(06).                              
               10  PV-FSCL-END-DTE-SEP.                                         
                   15  FILLER           PIC X(11) VALUE 'SEPTEMBER, '.          
                   15  PV-FSCL-SEP-YEAR PIC X(04).                              
               10  PV-FSCL-END-DTE-OCT.                                         
                   15  FILLER           PIC X(10) VALUE ' OCTOBER, '.           
                   15  PV-FSCL-OCT-YEAR PIC X(05).                              
               10  PV-FSCL-END-DTE-NOV.                                         
                   15  FILLER           PIC X(10) VALUE 'NOVEMBER, '.           
                   15  PV-FSCL-NOV-YEAR PIC X(05).                              
               10  PV-FSCL-END-DTE-DEC.                                         
                   15  FILLER           PIC X(10) VALUE 'DECEMBER, '.           
                   15  PV-FSCL-DEC-YEAR PIC X(05).                              
                                                                        MLKRP804
           05  PV-COMPUTED-FIELDS.                                      MLKRP804
               10  PV-RECORD-FETCHED     PIC S9(15)      VALUE ZEROES           
                               COMP-3.                                          
               10  PV-TOT-QTY            PIC S9(15)      VALUE ZEROES           
                               COMP-3.                                          
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-CO-EIR-AMT                PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-CO-ORIG-EIC-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-CO-ORIG-GM-AMT            PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-CO-ADJ-EIC-AMT            PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-CO-ADJ-EIC-ADJUSTS-AMT    PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-CO-ADJ-GM-AMT             PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-EIR-AMT               PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-ORIG-EIC-AMT          PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-ORIG-GM-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-ADJ-EIC-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-ADJ-EIC-ADJUSTS-AMT   PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-GMM-ADJ-GM-AMT            PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-EIR-AMT               PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-ORIG-EIC-AMT          PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-ORIG-GM-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-ADJ-EIC-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-ADJ-EIC-ADJUSTS-AMT   PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DMM-ADJ-GM-AMT            PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-EIR-AMT             PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-ORIG-EIC-AMT        PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-ORIG-GM-AMT         PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-ADJ-EIC-AMT         PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-ADJ-EIC-ADJUSTS-AMT PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-BUYER-ADJ-GM-AMT          PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-EIR-AMT              PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-ORIG-EIC-AMT         PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-ORIG-GM-AMT          PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-ADJ-EIC-AMT          PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-ADJ-EIC-ADJUSTS-AMT  PIC S9(9)V99 COMP-3.        MLKRP804
           05  PA-DEPT-ADJ-GM-AMT           PIC S9(9)V99 COMP-3.        MLKRP804
                                                                        MLKRP804
       01  SWITCHES.                                                    MLKRP804
           05  END-OF-INPUT-SW         PIC X       VALUE 'N'.           MLKRP804
               88  END-OF-INPUT                    VALUE 'Y'.           MLKRP804
                                                                        MLKRP804
           05  END-OF-CONTROL-CARD-SW  PIC X       VALUE 'N'.           MLKRP804
               88  END-OF-CONTROL-CARD             VALUE 'Y'.           MLKRP804
                                                                        MLKRP804
           05  ADD-TO-LINE-CNT-SW      PIC X       VALUE 'Y'.           MLKRP804
               88  ADD-TO-LINE-CNT                 VALUE 'Y'.           MLKRP804
               88  DO-NOT-ADD-TO-LINE-CNT          VALUE 'N'.           MLKRP804
                                                                        MLKRP804
       01  CPU-DATE.                                                    MLKRP804
           05  CPU-YEAR                    PIC X(02)     VALUE SPACES.  MLKRP804
           05  CPU-MONTH                   PIC X(02)     VALUE SPACES.  MLKRP804
           05  CPU-DAY                     PIC X(02)     VALUE SPACES.  MLKRP804
                                                                        MLKRP804
       01  CPU-TIME.                                                    MLKRP804
           05  CPU-HOUR                    PIC X(02)     VALUE SPACES.  MLKRP804
           05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.  MLKRP804
           05  FILLER                      PIC X(02)     VALUE SPACES.  MLKRP804
                                                                        MLKRP804
       01  H1-HEADING.                                                  MLKRP804
           05  FILLER              PIC X(73)   VALUE SPACES.            MLKRP804
           05  H1-REPORT-TITLE     PIC X(55).                           MLKRP804
           05  FILLER              PIC X(72)   VALUE SPACE.             MLKRP804
                                                                                
       01  H2-GMM-HEADING.                                              MLKRP804
           05  FILLER              PIC X(01)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(07)   VALUE 'GMM..: '.         MLKRP804
           05  H2-GMM-ID           PIC X(02).                           MLKRP804
           05  FILLER              PIC X(03)   VALUE ' - '.             MLKRP804
           05  H2-GMM-NAME         PIC X(30).                           MLKRP804
           05  FILLER              PIC X(50)   VALUE SPACES.            MLKRP804
           05  H2-FSCL-END-DTE     PIC X(15).                           MLKRP804
           05  FILLER              PIC X(92)   VALUE SPACES.            MLKRP804
                                                                                
       01  H3-DMM-HEADING.                                              MLKRP804
           05  FILLER              PIC X(01)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(07)   VALUE 'DMM..: '.         MLKRP804
           05  H3-DMM-ID           PIC X(02).                           MLKRP804
           05  FILLER              PIC X(03)   VALUE ' - '.             MLKRP804
           05  H3-DMM-NAME         PIC X(30).                           MLKRP804
           05  FILLER              PIC X(157)  VALUE SPACES.            MLKRP804
                                                                                
       01  H4-BUYER-HEADING.                                            MLKRP804
           05  FILLER              PIC X(01)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(07)   VALUE 'BUYER: '.         MLKRP804
           05  H4-BUYER-ID         PIC X(03).                           MLKRP804
           05  FILLER              PIC X(03)   VALUE ' - '.             MLKRP804
           05  H4-BUYER-NAME       PIC X(30).                           MLKRP804
           05  FILLER              PIC X(67)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(05)   VALUE 'OVER-'.           MLKRP804
           05  FILLER              PIC X(85)   VALUE SPACES.            MLKRP804
                                                                                
       01  H5-DEPT-HEADING.                                             MLKRP804
           05  FILLER              PIC X(01)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(07)   VALUE 'DEPT.: '.         MLKRP804
           05  H5-DEPT-NBR         PIC 9(04).                           MLKRP804
           05  FILLER              PIC X(03)   VALUE ' - '.             MLKRP804
           05  H5-DEPT-NAME        PIC X(25).                           MLKRP804
           05  FILLER              PIC X(11)   VALUE '     CMU%: '.     MLKRP804
           05  H5-DEPT-CMU-PCT     PIC ZZZZ.9999999-.                   MLKRP804
           05  H5-DEPT-CMU-PCT-X REDEFINES H5-DEPT-CMU-PCT              MLKRP804
                                   PIC X(13).                                   
           05  FILLER              PIC X(47)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(04)   VALUE 'RIDE'.            MLKRP804
           05  FILLER              PIC X(86)   VALUE SPACES.            MLKRP804
                                                                                
       01  H6-HEADING.                                                  MLKRP804
           05  FILLER              PIC X(38)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(40)   VALUE                    MLKRP804
              '---------------------------- ORIGINAL --'.                       
           05  FILLER              PIC X(40)   VALUE                    MLKRP804
              '-----------------------------|----------'.                       
           05  FILLER              PIC X(40)   VALUE                    MLKRP804
              '---------------------------- ADJUSTED --'.                       
           05  FILLER              PIC X(42)   VALUE                    MLKRP804
              ' -----------------------------------------'.                     
                                                                                
       01  H7-HEADING.                                                  MLKRP804
           05  FILLER              PIC X(01)   VALUE SPACE.             MLKRP804
           05  FILLER              PIC X(06)   VALUE 'VENDOR'.          MLKRP804
           05  FILLER              PIC X(15)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(05)   VALUE 'BRAND'.           MLKRP804
           05  FILLER              PIC X(23)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(21)   VALUE                    MLKRP804
                                             'EIR               EIC'.   MLKRP804
           05  FILLER              PIC X(05)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(21)   VALUE                    MLKRP804
                                             'CMU%              GM$'.   MLKRP804
           05  FILLER              PIC X(06)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(03)   VALUE 'GM%'.             MLKRP804
           05  FILLER              PIC X(09)   VALUE ' |   CMU%'.       MLKRP804
           05  FILLER              PIC X(09)   VALUE ' CMU% VAR'.       MLKRP804
           05  FILLER              PIC X(05)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(13)   VALUE '         EIC '.   MLKRP804
           05  FILLER              PIC X(08)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(09)   VALUE '  EIC ADJ'.       MLKRP804
           05  FILLER              PIC X(14)   VALUE SPACES.            MLKRP804
           05  FILLER              PIC X(13)   VALUE ' GM$      GM%'.   MLKRP804
           05  FILLER              PIC X(14)   VALUE '  ACTION      '.  MLKRP804
                                                                        MLKRP804
       01  D1-DETAIL-LINE.                                              MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACE.          MLKRP804
           05  D1-VENDOR-NAME           PIC X(19).                      MLKRP804
           05  FILLER                   PIC X(02) VALUE SPACE.          MLKRP804
           05  D1-BRAND-NAME            PIC X(15).                      MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-EIR-AMT               PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(02) VALUE SPACES.         MLKRP804
           05  D1-ORIG-EIC-AMT          PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ORIG-CMU-PCT          PIC ZZZZ.99-.                   MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ORIG-GM-AMT           PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ORIG-GM-PCT           PIC ZZZZ.99-.                   MLKRP804
           05  D1-VERTICAL-LINE         PIC X(01) VALUE '|'.            MLKRP804
           05  D1-GAUGED-CMU-PCT        PIC ZZZZ.99-.                   MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-CMU-VARIANCE-PCT      PIC ZZZZ.99-.                   MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ADJ-EIC-AMT           PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(02) VALUE SPACES.         MLKRP804
           05  D1-ADJ-EIC-ADJUSTS-AMT   PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(02) VALUE SPACES.         MLKRP804
           05  D1-ADJ-GM-AMT            PIC $$$$,$$$,$$9.99-.           MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ADJ-GM-PCT            PIC ZZZZ.99-.                   MLKRP804
           05  FILLER                   PIC X(01) VALUE SPACES.         MLKRP804
           05  D1-ACTION                PIC X(12).                      MLKRP804
                                                                        MLKRP804
       01  T1-TOTAL-LINE.                                               MLKRP804
           05  FILLER                   PIC X(16) VALUE SPACES.         MLKRP804
           05  T1-TOTAL-LEVEL           PIC X(20) VALUE SPACES.         MLKRP804
           05  T1-EIR-AMT               PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  T1-ORIG-EIC-AMT          PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  FILLER                   PIC X(08) VALUE SPACES.         MLKRP804
           05  T1-ORIG-GM-AMT           PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  FILLER                   PIC X(26) VALUE SPACES.         MLKRP804
           05  T1-ADJ-EIC-AMT           PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  T1-ADJ-EIC-ADJUSTS-AMT   PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  T1-ADJ-GM-AMT            PIC $$,$$$,$$$,$$9.99-.         MLKRP804
           05  FILLER                   PIC X(55) VALUE SPACES.         MLKRP804
                                                                                
      ******************************************************************02620000
      ***  REPORT NO DATA LINE                                          02630000
      ******************************************************************02640000
       01  ER-REPORT-NO-DATA-LINE.                                      02650000
           05  FILLER                  PIC X(34)   VALUE                02660000
                                  '*** NO DATA ON WHICH TO REPORT ***'. 02670000
      ******************************************************************02620000
      ***  REPORT END OF REPORT LINE                                    02630000
      ******************************************************************02640000
       01  ER-END-OF-REPORT-LINE.                                       02650000
           05  FILLER                  PIC X(21)   VALUE                02660000
                                          '*** END OF REPORT ***'.      02670000
           05  FILLER                  PIC X(179)  VALUE SPACES.        02660000
                                                                        MLKRP804
      * COPY FOR STANDARD HEADING                                       MLKRP804
           COPY DPWS200O.                                               MLKRP804
                                                                        MLKRP804
      ***************************************************************** INKRP104
      *  SQL ABEND ROUTINE WORKING STORAGE                              INKRP104
      ***************************************************************** INKRP104
                                                                        INKRP104
           COPY DPWS004.                                                INKRP104
                                                                        INKRP104
      ****************************************************************          
      * RECORD LAYOUT FOR SMOOTHING OUTLYER REPORT DATA RECORD                  
      ****************************************************************          
           COPY MLRD204.                                                        
                                                                        MLKRP804
      ***************************************************************** INKRP104
      * DB2 VENDOR STYLE CHARACTERISTIC VALUE TABLE                     INKRP104
      ***************************************************************** INKRP104
           EXEC SQL                                                     INKRP104
               INCLUDE TVSCHVL                                          INKRP104
           END-EXEC.                                                    INKRP104
                                                                                
      ***************************************************************** INKRP104
      * DB2 MONTHLY DEPARTMENT TABLE                                    INKRP104
      ***************************************************************** INKRP104
           EXEC SQL                                                     INKRP104
               INCLUDE TMNDPT                                           INKRP104
           END-EXEC.                                                    INKRP104
                                                                                
      *****************************************************************         
      * SQL COMMON AREA                                                         
      *****************************************************************         
           EXEC SQL                                                     INKRP104
               INCLUDE SQLCA                                            INKRP104
           END-EXEC.                                                    INKRP104
                                                                        INKRP104
           COPY SQLCA2.                                                 INKRP104
                                                                        MLKRP804
       LINKAGE SECTION.                                                 MLKRP804
                                                                        MLKRP804
       PROCEDURE DIVISION.                                              MLKRP804
                                                                        MLKRP804
       0000-MAIN-MODULE.                                                MLKRP804
                                                                        MLKRP804
           PERFORM 1000-INITIALIZATION.                                 MLKRP804
                                                                        MLKRP804
           PERFORM 7100-READ-INPUT-FILE                                         
           IF NOT END-OF-INPUT                                                  
               PERFORM 1200-FSCL-END-DTE-HEADING                                
               MOVE ML204-GMM           TO PV-PREV-GMM                          
                                           H2-GMM-ID                            
               MOVE ML204-NAME-GMM      TO H2-GMM-NAME                          
               MOVE ML204-DMM           TO PV-PREV-DMM                          
                                           H3-DMM-ID                            
               MOVE ML204-NAME-DMM      TO H3-DMM-NAME                          
               MOVE ML204-BUYER         TO PV-PREV-BUYER                        
                                           H4-BUYER-ID                          
               MOVE ML204-NAME-BUYER    TO H4-BUYER-NAME                        
               MOVE ML204-DEPT-NBR      TO PV-DEPT-NBR-3N                       
                                           H5-DEPT-NBR                          
                                           PV-PREV-DEPT-NBR                     
               MOVE ML204-NAME-DEPT     TO H5-DEPT-NAME                         
               PERFORM 3010-DEPT-CMU-PCT                                        
               PERFORM 8100-PRINT-HEADINGS                                      
               PERFORM 2000-PROCESS-INPUT                               MLKRP804
                   UNTIL END-OF-INPUT                                   MLKRP804
               PERFORM 2100-COMPANY-BREAK                                       
           ELSE                                                                 
               WRITE OUTLYER-REPORT-REC FROM ER-REPORT-NO-DATA-LINE             
               AFTER ADVANCING 3 LINES                                          
           END-IF.                                                              
           WRITE OUTLYER-REPORT-REC FROM ER-END-OF-REPORT-LINE                  
               AFTER ADVANCING 1 LINES.                                         
                                                                        MLKRP804
           CLOSE CONTROL-CARD-FILE                                      MLKRP804
                 OUTLYER-RPT-DATA-FILE                                          
                 OUTLYER-REPORT-FILE.                                   MLKRP804
                                                                        MLKRP804
           STOP RUN.                                                    MLKRP804
      *                                                                 MLKRP804
       1000-INITIALIZATION.                                             MLKRP804
           MOVE '*1000-INITIALIZATION             *' TO PV-PARA         MLKRP804
                                                                        MLKRP804
           OPEN INPUT  CONTROL-CARD-FILE                                MLKRP804
                       OUTLYER-RPT-DATA-FILE                                    
                OUTPUT OUTLYER-REPORT-FILE.                             MLKRP804
                                                                        MLKRP804
           MOVE 1          TO DP200O-REPORT-NUMBER.                     MLKRP804
           ACCEPT CPU-DATE FROM DATE.                                   MLKRP804
           MOVE CPU-YEAR   TO DP200O-RUN-YEAR                           MLKRP804
           MOVE CPU-MONTH  TO DP200O-RUN-MONTH                          MLKRP804
           MOVE CPU-DAY    TO DP200O-RUN-DAY                            MLKRP804
           ACCEPT CPU-TIME FROM TIME.                                   MLKRP804
           MOVE CPU-HOUR   TO DP200O-RUN-HOUR.                          MLKRP804
           MOVE CPU-MINUTE TO DP200O-RUN-MINUTE.                        MLKRP804
           MOVE 'MLKRP804' TO DP200O-PROGRAM-NAME.                      MLKRP804
                                                                        MLKRP804
           PERFORM                                                              
               UNTIL END-OF-CONTROL-CARD                                        
                  OR CC-VALID-RECORD                                            
                  PERFORM 7000-READ-CONTROL-CARD                                
           END-PERFORM.                                                         
           IF CC-VALID-RECORD                                                   
               PERFORM 1100-EDIT-CONTROL-CARDS                                  
           END-IF.                                                              
           IF END-OF-CONTROL-CARD                                               
               MOVE 'NO VALID CONTROL CARD'                             MLKRP804
                                        TO PV-DB2-OPER-ATTEMPTED                
               PERFORM 9900-ABEND                                               
           END-IF.                                                              
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
                                                                        MLKRP804
       1100-EDIT-CONTROL-CARDS.                                         MLKRP804
                                                                        MLKRP804
           DISPLAY 'REPORT TITLE CONTROL CARD: '                        MLKRP804
           DISPLAY  CC-CONTROL-CARD                                     MLKRP804
           MOVE CC-REPORT-TITLE         TO H1-REPORT-TITLE.                     
      *    IF                                                                   
      *        MOVE '*1100-EDIT-CONTROL-CARDS      *'                   MLKRP804
      *                                 TO PV-PARA                      MLKRP804
      *        MOVE 'NO VALID CONTROL CARD VALUE'                       MLKRP804
      *                                 TO PV-DB2-OPER-ATTEMPTED                
      *        PERFORM 9900-ABEND                                               
      *    END-IF.                                                              
                                                                                
       1200-FSCL-END-DTE-HEADING.                                               
           INITIALIZE H2-FSCL-END-DTE                                           
           MOVE ML204-FSCL-END-DTE      TO PV-FSCL-END-DTE                      
           EVALUATE PV-FSCL-MM                                                  
               WHEN '01'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-JAN-YEAR                     
                   MOVE PV-FSCL-END-DTE-JAN                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '02'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-FEB-YEAR                     
                   MOVE PV-FSCL-END-DTE-FEB                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '03'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-MAR-YEAR                     
                   MOVE PV-FSCL-END-DTE-MAR                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '04'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-APR-YEAR                     
                   MOVE PV-FSCL-END-DTE-APR                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '05'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-MAY-YEAR                     
                   MOVE PV-FSCL-END-DTE-MAY                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '06'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-JUN-YEAR                     
                   MOVE PV-FSCL-END-DTE-JUN                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '07'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-JUL-YEAR                     
                   MOVE PV-FSCL-END-DTE-JUL                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '08'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-AUG-YEAR                     
                   MOVE PV-FSCL-END-DTE-AUG                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '09'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-SEP-YEAR                     
                   MOVE PV-FSCL-END-DTE-SEP                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '10'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-OCT-YEAR                     
                   MOVE PV-FSCL-END-DTE-OCT                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '11'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-NOV-YEAR                     
                   MOVE PV-FSCL-END-DTE-NOV                                     
                                        TO H2-FSCL-END-DTE                      
               WHEN '12'                                                        
                   MOVE PV-FSCL-YY      TO PV-FSCL-DEC-YEAR                     
                   MOVE PV-FSCL-END-DTE-DEC                                     
                                        TO H2-FSCL-END-DTE                      
           END-EVALUATE.                                                        
                                                                                
       2000-PROCESS-INPUT.                                              MLKRP804
                                                                                
           IF ML204-GMM NOT = PV-PREV-GMM                                       
      *        ADD +13                      TO PV-LINE-COUNT                    
               ADD +8                       TO PV-LINE-COUNT                    
               IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                             
                   PERFORM 8100-PRINT-HEADINGS                                  
      *            ADD +13                  TO PV-LINE-COUNT                    
                   ADD +8                   TO PV-LINE-COUNT                    
               END-IF                                                           
               PERFORM 2200-GMM-BREAK                                           
               PERFORM 2300-DMM-BREAK                                           
               PERFORM 2400-BUYER-BREAK                                         
               PERFORM 2500-DEPARTMENT-BREAK                                    
      *        MOVE +2                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8010-GMM-HEADING-LINE                                    
      *        MOVE +1                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8020-DMM-HEADING-LINE                                    
      *        PERFORM 8030-BUYER-HEADING-LINE                                  
      *        PERFORM 8040-DEPT-HEADING-LINE                                   
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           IF ML204-DMM NOT = PV-PREV-DMM                                       
      *        ADD +10                      TO PV-LINE-COUNT                    
               ADD +6                       TO PV-LINE-COUNT                    
               IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                             
                   PERFORM 8100-PRINT-HEADINGS                                  
      *            ADD +10                  TO PV-LINE-COUNT                    
                   ADD +6                   TO PV-LINE-COUNT                    
               END-IF                                                           
               PERFORM 2300-DMM-BREAK                                           
               PERFORM 2400-BUYER-BREAK                                         
               PERFORM 2500-DEPARTMENT-BREAK                                    
      *        MOVE +2                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8020-DMM-HEADING-LINE                                    
      *        MOVE +1                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8030-BUYER-HEADING-LINE                                  
      *        PERFORM 8040-DEPT-HEADING-LINE                                   
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           IF ML204-BUYER NOT = PV-PREV-BUYER                                   
      *        ADD +7                       TO PV-LINE-COUNT                    
               ADD +4                       TO PV-LINE-COUNT                    
               IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                             
                   PERFORM 8100-PRINT-HEADINGS                                  
      *            ADD +7                   TO PV-LINE-COUNT                    
                   ADD +4                   TO PV-LINE-COUNT                    
               END-IF                                                           
               PERFORM 2400-BUYER-BREAK                                         
               PERFORM 2500-DEPARTMENT-BREAK                                    
      *        MOVE +2                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8030-BUYER-HEADING-LINE                                  
      *        MOVE +1                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8040-DEPT-HEADING-LINE                                   
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           IF ML204-DEPT-NBR NOT = PV-PREV-DEPT-NBR                             
      *        ADD +4                       TO PV-LINE-COUNT                    
               ADD +2                       TO PV-LINE-COUNT                    
               IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                             
                   PERFORM 8100-PRINT-HEADINGS                                  
      *            ADD +4                   TO PV-LINE-COUNT                    
                   ADD +2                   TO PV-LINE-COUNT                    
               END-IF                                                           
               PERFORM 2500-DEPARTMENT-BREAK                                    
      *        MOVE +2                      TO PV-NBR-OF-LINES                  
      *        PERFORM 8040-DEPT-HEADING-LINE                                   
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           PERFORM 3000-GET-BRAND-NAME                                          
           MOVE ML204-NAME-VENDOR           TO D1-VENDOR-NAME                   
           MOVE ML204-ORIG-END-INV-RTL-AMT  TO D1-EIR-AMT                       
           MOVE ML204-ORIG-GRO-MRGN-AMT     TO D1-ORIG-GM-AMT                   
           MOVE ML204-TGT-CUM-MKUP-PCT      TO D1-GAUGED-CMU-PCT                
           MOVE ML204-ADJ-GRO-MRGN-AMT      TO D1-ADJ-GM-AMT                    
           IF ML204-NONOUTLYER-CANDIDATE                                        
               MOVE ML204-ADJ-END-INV-COST-AMT1                                 
                                            TO D1-ORIG-EIC-AMT                  
           ELSE                                                                 
               MOVE ML204-ORIG-END-INV-COST-AMT                                 
                                            TO D1-ORIG-EIC-AMT                  
           END-IF.                                                              
                                                                                
           IF ML204-ORIG-END-INV-RTL-AMT = ZERO                                 
               IF ML204-ORIG-END-INV-COST-AMT = ZERO                            
                   MOVE ZERO TO ML204-ORIG-CUM-MKUP-PCT                         
               ELSE                                                             
                   MOVE 100  TO ML204-ORIG-CUM-MKUP-PCT                         
               END-IF                                                           
           ELSE                                                                 
               COMPUTE ML204-ORIG-CUM-MKUP-PCT =                                
                      (ML204-ORIG-END-INV-COST-AMT /                            
                       ML204-ORIG-END-INV-RTL-AMT) * 100                        
           END-IF.                                                              
           MOVE ML204-ORIG-CUM-MKUP-PCT     TO D1-ORIG-CMU-PCT                  
                                                                                
           IF ML204-ORIG-SLS-RTL-AMT = ZERO                                     
               IF ML204-ORIG-GRO-MRGN-AMT = ZERO                                
                   MOVE ZERO TO ML204-ORIG-GRO-MRGN-PCT                         
               ELSE                                                             
                   MOVE 100  TO ML204-ORIG-GRO-MRGN-PCT                         
               END-IF                                                           
           ELSE                                                                 
               COMPUTE ML204-ORIG-GRO-MRGN-PCT =                                
                      (ML204-ORIG-GRO-MRGN-AMT /                                
                       ML204-ORIG-SLS-RTL-AMT) * 100                            
           END-IF.                                                              
           MOVE ML204-ORIG-GRO-MRGN-PCT     TO D1-ORIG-GM-PCT                   
                                                                                
           IF ML204-ORIG-SLS-RTL-AMT = ZERO                                     
               IF ML204-ADJ-GRO-MRGN-AMT = ZERO                                 
                   MOVE ZERO TO ML204-ADJ-GRO-MRGN-PCT                          
               ELSE                                                             
                   MOVE 100  TO ML204-ADJ-GRO-MRGN-PCT                          
               END-IF                                                           
           ELSE                                                                 
               COMPUTE ML204-ADJ-GRO-MRGN-PCT =                                 
                      (ML204-ADJ-GRO-MRGN-AMT /                                 
                       ML204-ORIG-SLS-RTL-AMT) * 100                            
           END-IF.                                                              
           MOVE ML204-ADJ-GRO-MRGN-PCT  TO D1-ADJ-GM-PCT                        
                                                                                
           IF ML204-ORIG-END-INV-RTL-AMT = ZERO                                 
               IF ML204-ADJ-END-INV-COST-AMT2 = ZERO                            
                   MOVE ZERO TO ML204-SMOOTHED-CUM-MKUP-PCT                     
               ELSE                                                             
                   MOVE 100  TO ML204-SMOOTHED-CUM-MKUP-PCT                     
               END-IF                                                           
           ELSE                                                                 
               COMPUTE ML204-SMOOTHED-CUM-MKUP-PCT =                            
                      (ML204-ADJ-END-INV-COST-AMT2 /                            
                       ML204-ORIG-END-INV-RTL-AMT) * 100                        
           END-IF.                                                              
                                                                                
           COMPUTE PV-WORK-PERCENT =                                            
                   ML204-ORIG-CUM-MKUP-PCT -                                    
                   ML204-SMOOTHED-CUM-MKUP-PCT.                                 
           MOVE PV-WORK-PERCENT             TO D1-CMU-VARIANCE-PCT.             
           MOVE SPACES                      TO D1-ACTION                        
           IF ML204-OUTLYER-CANDIDATES                                          
               MOVE 'LEVEL ONE'             TO D1-ACTION                        
               MOVE ML204-INV-COST-ADJ1     TO D1-ADJ-EIC-ADJUSTS-AMT           
                                               PV-ADJ-EIC-ADJUSTS-AMT           
               MOVE ML204-ADJ-END-INV-COST-AMT1                                 
                                            TO D1-ADJ-EIC-AMT                   
                                               PV-ADJ-END-INV-COST-AMT          
               PERFORM 8000-PRINT-DETAIL                                        
               PERFORM 2010-LEVEL1-LEVEL2-TOT                                   
           END-IF.                                                              
           IF ML204-NONOUTLYER-CANDIDATE                                        
             OR (ML204-ADJ-END-INV-COST-AMT1 NOT =                              
                 ML204-ADJ-END-INV-COST-AMT2)                                   
               IF D1-ACTION = 'LEVEL ONE'                                       
                   MOVE PC-BLANK-LINE       TO D1-DETAIL-LINE                   
                   MOVE '|'                 TO D1-VERTICAL-LINE                 
                   MOVE ML204-NAME-VENDOR   TO D1-VENDOR-NAME                   
                   PERFORM 3000-GET-BRAND-NAME                                  
               END-IF                                                           
               MOVE 'LEVEL TWO'             TO D1-ACTION                        
               MOVE ML204-INV-COST-ADJ2     TO D1-ADJ-EIC-ADJUSTS-AMT           
                                               PV-ADJ-EIC-ADJUSTS-AMT           
               MOVE ML204-ADJ-END-INV-COST-AMT2                                 
                                            TO D1-ADJ-EIC-AMT                   
                                               PV-ADJ-END-INV-COST-AMT          
               PERFORM 8000-PRINT-DETAIL                                        
               PERFORM 2010-LEVEL1-LEVEL2-TOT                                   
           END-IF.                                                              
                                                                                
           ADD ML204-ORIG-END-INV-RTL-AMT                                       
                                        TO PA-CO-EIR-AMT                        
                                           PA-GMM-EIR-AMT                       
                                           PA-DMM-EIR-AMT                       
                                           PA-BUYER-EIR-AMT                     
                                           PA-DEPT-EIR-AMT                      
           ADD ML204-ORIG-END-INV-COST-AMT                                      
                                        TO PA-CO-ORIG-EIC-AMT                   
                                           PA-GMM-ORIG-EIC-AMT                  
                                           PA-DMM-ORIG-EIC-AMT                  
                                           PA-BUYER-ORIG-EIC-AMT                
                                           PA-DEPT-ORIG-EIC-AMT                 
           ADD ML204-ORIG-GRO-MRGN-AMT  TO PA-CO-ORIG-GM-AMT                    
                                           PA-GMM-ORIG-GM-AMT                   
                                           PA-DMM-ORIG-GM-AMT                   
                                           PA-BUYER-ORIG-GM-AMT                 
                                           PA-DEPT-ORIG-GM-AMT                  
           ADD ML204-ADJ-GRO-MRGN-AMT   TO PA-CO-ADJ-GM-AMT                     
                                           PA-GMM-ADJ-GM-AMT                    
                                           PA-DMM-ADJ-GM-AMT                    
                                           PA-BUYER-ADJ-GM-AMT                  
                                           PA-DEPT-ADJ-GM-AMT                   
                                                                                
           PERFORM 7100-READ-INPUT-FILE.                                        
                                                                                
       2010-LEVEL1-LEVEL2-TOT.                                                  
           ADD PV-ADJ-END-INV-COST-AMT  TO PA-CO-ADJ-EIC-AMT                    
                                           PA-GMM-ADJ-EIC-AMT                   
                                           PA-DMM-ADJ-EIC-AMT                   
                                           PA-BUYER-ADJ-EIC-AMT                 
                                           PA-DEPT-ADJ-EIC-AMT                  
           ADD PV-ADJ-EIC-ADJUSTS-AMT   TO PA-CO-ADJ-EIC-ADJUSTS-AMT            
                                           PA-GMM-ADJ-EIC-ADJUSTS-AMT           
                                           PA-DMM-ADJ-EIC-ADJUSTS-AMT           
                                           PA-BUYER-ADJ-EIC-ADJUSTS-AMT         
                                           PA-DEPT-ADJ-EIC-ADJUSTS-AMT.         
                                                                                
       2100-COMPANY-BREAK.                                                      
           ADD +10                      TO PV-LINE-COUNT                        
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF                                                               
           MOVE 'COMPANY TOTAL'     TO T1-TOTAL-LEVEL                           
           MOVE PA-CO-EIR-AMT       TO T1-EIR-AMT                       MLKRP804
           MOVE PA-CO-ORIG-EIC-AMT  TO T1-ORIG-EIC-AMT                  MLKRP804
           MOVE PA-CO-ORIG-GM-AMT   TO T1-ORIG-GM-AMT                   MLKRP804
           MOVE PA-CO-ADJ-EIC-AMT   TO T1-ADJ-EIC-AMT                   MLKRP804
           MOVE PA-CO-ADJ-EIC-ADJUSTS-AMT                               MLKRP804
                                    TO T1-ADJ-EIC-ADJUSTS-AMT                   
           MOVE PA-CO-ADJ-GM-AMT    TO T1-ADJ-GM-AMT                    MLKRP804
           WRITE OUTLYER-REPORT-REC FROM T1-TOTAL-LINE                          
               AFTER ADVANCING 2 LINES                                          
           PERFORM 2200-GMM-BREAK                                               
           PERFORM 2300-DMM-BREAK                                               
           PERFORM 2400-BUYER-BREAK                                             
           PERFORM 2500-DEPARTMENT-BREAK.                                       
                                                                                
       2200-GMM-BREAK.                                                          
           MOVE 'GMM TOTAL'         TO T1-TOTAL-LEVEL                           
           MOVE PA-GMM-EIR-AMT      TO T1-EIR-AMT                       MLKRP804
           MOVE PA-GMM-ORIG-EIC-AMT TO T1-ORIG-EIC-AMT                  MLKRP804
           MOVE PA-GMM-ORIG-GM-AMT  TO T1-ORIG-GM-AMT                   MLKRP804
           MOVE PA-GMM-ADJ-EIC-AMT  TO T1-ADJ-EIC-AMT                   MLKRP804
           MOVE PA-GMM-ADJ-EIC-ADJUSTS-AMT                              MLKRP804
                                    TO T1-ADJ-EIC-ADJUSTS-AMT                   
           MOVE PA-GMM-ADJ-GM-AMT   TO T1-ADJ-GM-AMT                    MLKRP804
           WRITE OUTLYER-REPORT-REC FROM T1-TOTAL-LINE                          
               AFTER ADVANCING 2 LINES                                          
           MOVE ML204-GMM           TO PV-PREV-GMM                              
                                       H2-GMM-ID                                
           MOVE ML204-NAME-GMM      TO H2-GMM-NAME                              
           MOVE ZERO                TO PA-GMM-EIR-AMT                   MLKRP804
                                       PA-GMM-ORIG-EIC-AMT              MLKRP804
                                       PA-GMM-ORIG-GM-AMT               MLKRP804
                                       PA-GMM-ADJ-EIC-AMT               MLKRP804
                                       PA-GMM-ADJ-EIC-ADJUSTS-AMT       MLKRP804
                                       PA-GMM-ADJ-GM-AMT.               MLKRP804
                                                                                
       2300-DMM-BREAK.                                                          
           MOVE 'DMM TOTAL'         TO T1-TOTAL-LEVEL                           
           MOVE PA-DMM-EIR-AMT      TO T1-EIR-AMT                       MLKRP804
           MOVE PA-DMM-ORIG-EIC-AMT TO T1-ORIG-EIC-AMT                  MLKRP804
           MOVE PA-DMM-ORIG-GM-AMT  TO T1-ORIG-GM-AMT                   MLKRP804
           MOVE PA-DMM-ADJ-EIC-AMT  TO T1-ADJ-EIC-AMT                   MLKRP804
           MOVE PA-DMM-ADJ-EIC-ADJUSTS-AMT                              MLKRP804
                                    TO T1-ADJ-EIC-ADJUSTS-AMT                   
           MOVE PA-DMM-ADJ-GM-AMT   TO T1-ADJ-GM-AMT                    MLKRP804
           WRITE OUTLYER-REPORT-REC FROM T1-TOTAL-LINE                          
               AFTER ADVANCING 2 LINES                                          
           MOVE ML204-DMM           TO PV-PREV-DMM                              
                                       H3-DMM-ID                                
           MOVE ML204-NAME-DMM      TO H3-DMM-NAME                              
           MOVE ZERO                TO PA-DMM-EIR-AMT                   MLKRP804
                                       PA-DMM-ORIG-EIC-AMT              MLKRP804
                                       PA-DMM-ORIG-GM-AMT               MLKRP804
                                       PA-DMM-ADJ-EIC-AMT               MLKRP804
                                       PA-DMM-ADJ-EIC-ADJUSTS-AMT       MLKRP804
                                       PA-DMM-ADJ-GM-AMT.               MLKRP804
                                                                                
       2400-BUYER-BREAK.                                                        
           MOVE 'BUYER TOTAL'       TO T1-TOTAL-LEVEL                           
           MOVE PA-BUYER-EIR-AMT    TO T1-EIR-AMT                       MLKRP804
           MOVE PA-BUYER-ORIG-EIC-AMT                                   MLKRP804
                                    TO T1-ORIG-EIC-AMT                          
           MOVE PA-BUYER-ORIG-GM-AMT                                    MLKRP804
                                    TO T1-ORIG-GM-AMT                   MLKRP804
           MOVE PA-BUYER-ADJ-EIC-AMT                                    MLKRP804
                                    TO T1-ADJ-EIC-AMT                   MLKRP804
           MOVE PA-BUYER-ADJ-EIC-ADJUSTS-AMT                            MLKRP804
                                    TO T1-ADJ-EIC-ADJUSTS-AMT                   
           MOVE PA-BUYER-ADJ-GM-AMT TO T1-ADJ-GM-AMT                    MLKRP804
           WRITE OUTLYER-REPORT-REC FROM T1-TOTAL-LINE                          
               AFTER ADVANCING 2 LINES                                          
           MOVE ML204-BUYER         TO PV-PREV-BUYER                            
                                       H4-BUYER-ID                              
           MOVE ML204-NAME-BUYER    TO H4-BUYER-NAME                            
           MOVE ZERO                TO PA-BUYER-EIR-AMT                 MLKRP804
                                       PA-BUYER-ORIG-EIC-AMT            MLKRP804
                                       PA-BUYER-ORIG-GM-AMT             MLKRP804
                                       PA-BUYER-ADJ-EIC-AMT             MLKRP804
                                       PA-BUYER-ADJ-EIC-ADJUSTS-AMT     MLKRP804
                                       PA-BUYER-ADJ-GM-AMT.             MLKRP804
                                                                                
       2500-DEPARTMENT-BREAK.                                                   
           MOVE 'DEPT TOTAL'        TO T1-TOTAL-LEVEL                           
           MOVE PA-DEPT-EIR-AMT     TO T1-EIR-AMT                       MLKRP804
           MOVE PA-DEPT-ORIG-EIC-AMT                                    MLKRP804
                                    TO T1-ORIG-EIC-AMT                          
           MOVE PA-DEPT-ORIG-GM-AMT TO T1-ORIG-GM-AMT                   MLKRP804
           MOVE PA-DEPT-ADJ-EIC-AMT TO T1-ADJ-EIC-AMT                   MLKRP804
           MOVE PA-DEPT-ADJ-EIC-ADJUSTS-AMT                             MLKRP804
                                    TO T1-ADJ-EIC-ADJUSTS-AMT                   
           MOVE PA-DEPT-ADJ-GM-AMT                                      MLKRP804
                                    TO T1-ADJ-GM-AMT                            
           WRITE OUTLYER-REPORT-REC FROM T1-TOTAL-LINE                          
               AFTER ADVANCING 2 LINES                                          
           MOVE ML204-DEPT-NBR      TO PV-DEPT-NBR-3N                           
                                       H5-DEPT-NBR                              
                                       PV-PREV-DEPT-NBR                         
           MOVE ML204-NAME-DEPT     TO H5-DEPT-NAME                             
           PERFORM 3010-DEPT-CMU-PCT                                            
           MOVE ZERO                TO PA-DEPT-EIR-AMT                  MLKRP804
                                       PA-DEPT-ORIG-EIC-AMT             MLKRP804
                                       PA-DEPT-ORIG-GM-AMT              MLKRP804
                                       PA-DEPT-ADJ-EIC-AMT              MLKRP804
                                       PA-DEPT-ADJ-EIC-ADJUSTS-AMT      MLKRP804
                                       PA-DEPT-ADJ-GM-AMT.              MLKRP804
                                                                                
       3000-GET-BRAND-NAME.                                                     
      *                                                                         
           EXEC SQL                                                             
             SELECT CHARTC_VAL_DESC                                             
               INTO :VSCHVL-CHARTC-VAL-DESC                                     
               FROM TVSCHVL                                                     
              WHERE CHARTC_SEQ_NBR = :ML204-LBL-SEQ-NBR                         
                AND CHARTC_CDE     = 3                                          
              WITH UR                                                           
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE VSCHVL-CHARTC-VAL-DESC TO D1-BRAND-NAME                 
               WHEN OTHER                                                       
                   MOVE 'NOT FOUND '           TO D1-BRAND-NAME                 
           END-EVALUATE.                                                        
                                                                                
       3010-DEPT-CMU-PCT.                                                       
      *                                                                         
           EXEC SQL                                                             
             SELECT CUM_MKUP_PCT                                                
               INTO :MNDPT-CUM-MKUP-PCT                                         
               FROM TMNDPT                                                      
              WHERE FSCL_MN_NBR     = :ML204-FSCL-PERIOD                        
                AND FSCL_MN_END_DTE = :ML204-FSCL-END-DTE                       
                AND DEPT_NBR        = :PV-DEPT-NBR-3X                           
              WITH UR                                                           
           END-EXEC.                                                            
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN SQLCODE = +0                                                
                   MOVE MNDPT-CUM-MKUP-PCT TO H5-DEPT-CMU-PCT                   
               WHEN OTHER                                                       
                   MOVE 'NOT FOUND '       TO H5-DEPT-CMU-PCT-X                 
           END-EVALUATE.                                                        
                                                                        MLKRP804
       7000-READ-CONTROL-CARD.                                          MLKRP804
                                                                        MLKRP804
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                  INKR    
               AT END                                                   INKR    
                   SET END-OF-CONTROL-CARD TO TRUE.                     INKR    
                                                                        MLKRP804
       7100-READ-INPUT-FILE.                                            MLKRP804
                                                                        MLKRP804
           READ OUTLYER-RPT-DATA-FILE INTO ML204-OUTLYER-EXTRACT        INKR    
               AT END                                                   INKR    
                   SET END-OF-INPUT TO TRUE.                            INKR    
                                                                                
       8000-PRINT-DETAIL.                                                       
           ADD +1                  TO PV-LINE-COUNT.                            
           IF PV-LINE-COUNT > PC-MAX-LINE-COUNT                                 
               PERFORM 8100-PRINT-HEADINGS                                      
           END-IF.                                                              
           WRITE OUTLYER-REPORT-REC FROM D1-DETAIL-LINE                         
               AFTER ADVANCING 1 LINES.                                         
                                                                                
       8100-PRINT-HEADINGS.                                             MLKRP804
                                                                        MLKRP804
           ADD 1              TO PV-PAGE-COUNT.                         MLKRP804
                                                                        MLKRP804
           MOVE PV-PAGE-COUNT TO DP200O-PAGE-NUMBER.                    MLKRP804
           MOVE +9         TO PV-LINE-COUNT.                            MLKRP804
           WRITE OUTLYER-REPORT-REC FROM                                        
                                        DP200O-STANDRD-HEADER-200-COLS  MLKRP804
               AFTER ADVANCING PAGE.                                            
           WRITE OUTLYER-REPORT-REC FROM H1-HEADING                             
               AFTER ADVANCING 2 LINES.                                         
           MOVE +1          TO PV-NBR-OF-LINES.                                 
           PERFORM 8010-GMM-HEADING-LINE                                        
           PERFORM 8020-DMM-HEADING-LINE                                        
           PERFORM 8030-BUYER-HEADING-LINE                                      
           PERFORM 8040-DEPT-HEADING-LINE                                       
           WRITE OUTLYER-REPORT-REC FROM H6-HEADING                             
               AFTER ADVANCING 1 LINES.                                         
           WRITE OUTLYER-REPORT-REC FROM H7-HEADING                             
               AFTER ADVANCING 1 LINES.                                         
                                                                        MLKRP804
       8010-GMM-HEADING-LINE.                                                   
           WRITE OUTLYER-REPORT-REC FROM H2-GMM-HEADING                         
               AFTER ADVANCING PV-NBR-OF-LINES.                                 
       8020-DMM-HEADING-LINE.                                                   
           WRITE OUTLYER-REPORT-REC FROM H3-DMM-HEADING                         
               AFTER ADVANCING PV-NBR-OF-LINES.                                 
       8030-BUYER-HEADING-LINE.                                                 
           WRITE OUTLYER-REPORT-REC FROM H4-BUYER-HEADING                       
               AFTER ADVANCING PV-NBR-OF-LINES.                                 
       8040-DEPT-HEADING-LINE.                                                  
           WRITE OUTLYER-REPORT-REC FROM H5-DEPT-HEADING                        
               AFTER ADVANCING PV-NBR-OF-LINES.                                 
                                                                        MLKRP804
      ******************************************************************MLKRP804
      *  THIS ROUTINE FORCES AN ABEND                                   MLKRP804
      ******************************************************************MLKRP804
       9900-ABEND.                                                      MLKRP804
                                                                        MLKRP804
           MOVE +4013                 TO ABEND-CODE.                    MLKRP804
           DISPLAY '**  ABEND     **'.                                  MLKRP804
           DISPLAY '**  PROGRAM   **  =  MLKRP804'.                     MLKRP804
           DISPLAY '**  PARAGRAPH **  = ' PV-PARA.                      MLKRP804
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        MLKRP804
                                                                        MLKRP804
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         MLKRP804
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        MLKRP804
                                                                        MLKRP804
           COPY DPPD004.                                                MLKRP804
                                                                        MLKRP804
           CALL 'ILBOABN0'  USING ABEND-CODE.                           MLKRP804
