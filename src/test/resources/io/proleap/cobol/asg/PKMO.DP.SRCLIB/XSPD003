      ******************************************************************00000100
      *   PROCEDURE DIVISION FOR THE REAL-TIME PDE SQL PROGRAMS         00000200
      ******************************************************************00000400
                                                                        00001000
      ******************************************************************00001100
      * IF PROMOTIONAL PRICING HAS BEEN FOUND OR THE LOWEST SIX WEEK    00001200
      * INDICATOR IS YES, CALL THE RULES ENGINE.  OTHERWISE, MOVE THE   00001300
      * RETAIL PRICE TO THE PRICE DETERMINED PRICE.                     00001400
      ******************************************************************00001500
       3000-PROCESS-RULES.                                              00002000
                                                                        00003000
           IF PS-PROMO-PRICG-FOUND OR                                   00020000
              XS047-INCLD-LST-SIX-WK-PRIC-SW = 'Y'                      00030000
               MOVE PV-INDEX TO XS045-ELIG-PROMO-TBL-SZ                 00040000
               PERFORM 8100-CALL-BUS-RULES-MOD                          00050000
               IF PS-NO-PROMO-PRICG-FOUND                               00060000
                   PERFORM 8200-INIT-XS045-TIER                         00070001
                       VARYING PV-SUB                                   00080000
                       FROM 1 BY 1                                      00090000
                       UNTIL PV-SUB > PC-NBR-OF-TIERS                   00100000
               END-IF                                                   00110000
               PERFORM 3100-POPULATE-PDE-LINKAGE                        00120001
           ELSE                                                         00130000
               MOVE 1 TO XS045-ELIG-PROMO-TBL-SZ                        00140000
               PERFORM 3100-POPULATE-PDE-LINKAGE                        00150001
      ************** IF THE BUSINESS RULES IS NOT CALLED  ************* 00160000
      **************   THEN MOVE RTL PRICE TO DET-PRICE   ************* 00170000
               MOVE HV-UNT-RTL-AMT  TO XS047-PRICE-DET-PRICE            00180000
           END-IF.                                                      00190000
                                                                        00210000
           IF XS045-ELIG-PROMO-TBL-SZ = 0                               00220000
               MOVE 1 TO XS045-ELIG-PROMO-TBL-SZ                        00230000
           END-IF.                                                      00240000
                                                                        00250000
           PERFORM 3200-INIT-ELIG-PROMO-TBL                             00260001
               VARYING PV-INDEX                                         00270000
               FROM 1 BY 1                                              00280000
               UNTIL PV-INDEX > XS045-ELIG-PROMO-TBL-SZ.                00290000
                                                                        00300000
      ******************************************************************00310001
      * INITIALIZE THE ELIG PROMO TABLE BEFORE RETURNING CONTROL BACK   00320001
      * TO THE CALLING PROGRAM.  OTHERWISE, THE NEXT TIME THIS MODULE   00330001
      * IS CALLED INCORRECT VALUES MAY BE SENT TO THE RULES ENGINE.     00340001
      ******************************************************************00350001
       3200-INIT-ELIG-PROMO-TBL.                                        00360001
                                                                        00370001
           INITIALIZE XS045-ELIG-PROMO-TBL-ELMT(PV-INDEX).              00380001
                                                                        00390001
           PERFORM 8011-INITIALIZE-TIER-TABLE                           00400001
               VARYING PV-SUB                                           00410001
               FROM 1 BY 1                                              00420001
               UNTIL PV-SUB > PC-NBR-OF-TIERS.                          00430001
                                                                        00440001
      ******************************************************************00450001
      * MOVE VALUES TO PRICE DETERMINATION ENGINE LINKAGE SECTION FROM  00460001
      * PRICE DETERMINATION BUSINESS RULE PROGRAM.                      00470001
      ******************************************************************00480001
       3100-POPULATE-PDE-LINKAGE.                                       00490001
                                                                        00500001
           MOVE XS045-ERROR-SECT-NAME  TO XS047-ERROR-SECTION-NAME.     00510001
           MOVE XS045-SQL-CODE         TO XS047-ERROR-SQL-CODE.         00520001
           IF XS045-SQL-CODE = 0                                        00530001
               MOVE 'Y'                TO XS047-SUCCESSFUL-COMPLETE-IND 00540001
           ELSE                                                         00550001
               MOVE 'N'                TO XS047-SUCCESSFUL-COMPLETE-IND 00560001
           END-IF.                                                      00570001
                                                                        00580001
           MOVE XS045-RGLR-PRICE       TO XS047-RGLR-PRICE.             00590001
           MOVE XS045-PD-PRICE         TO XS047-PRICE-DET-PRICE.        00600001
           MOVE XS045-PD-PRC-PRC-TYP-CDE                                00610001
                                       TO XS047-PRICE-DET-PRICG-TYP-CDE.00620001
           MOVE XS045-PD-PROMO-ID      TO XS047-PRICE-DET-PROMO-ID.     00630001
           MOVE XS045-PD-STRT-TMST     TO XS047-PRICE-DET-STRT-TMST.    00631002
           MOVE XS045-PD-END-TMST      TO XS047-PRICE-DET-END-TMST.     00632002
                                                                        00640001
           MOVE XS045-REG-GRP-PRICG-QTY                                 00650001
                                       TO XS047-REG-GRP-PRICG-QTY.      00660001
           MOVE XS045-REG-GRP-PRICG-DLR                                 00670001
                                       TO XS047-REG-GRP-PRICG-DLR.      00680001
                                                                        00690001
           MOVE XS045-PCT-OFF-REG-GP-PRC-PCT                            00700001
                                    TO XS047-PCT-OFF-REG-GRP-PRIC-PCT.  00710001
           MOVE XS045-PCT-OFF-REG-GP-PRC-AMT                            00720001
                                    TO XS047-PCT-OFF-REG-GRP-PRIC-AMT.  00730001
                                                                        00740001
           MOVE XS045-PROMO-GP-PRICG-QTY                                00750001
                                       TO XS047-PROMO-GRP-PRICG-QTY.    00760001
           MOVE XS045-PROMO-GP-PRICG-DLR                                00770001
                                       TO XS047-PROMO-GRP-PRICG-DLR.    00780001
           MOVE XS045-PROMO-GP-PRICG-TYP-CDE                            00790001
                                       TO XS047-PROMO-GRP-PRICG-TYP-CDE.00800001
           MOVE XS045-PROMO-GP-PROMO-ID                                 00810001
                                       TO XS047-PROMO-GRP-PROMO-ID.     00820001
           MOVE XS045-PROMO-GP-STRT-TMST                                00821002
                                       TO XS047-PROMO-GRP-STRT-TMST.    00822002
           MOVE XS045-PROMO-GP-END-TMST                                 00823002
                                       TO XS047-PROMO-GRP-END-TMST.     00823102
                                                                        00830001
           MOVE XS045-LWST-SIX-WEEK-PRICE                               00840001
                                       TO XS047-LWST-SIX-WEEK-PRICE.    00850001
                                                                        00860001
           MOVE XS045-ADDL-PCT-OFF-DISC-PCT                             00870001
                                       TO XS047-ADDL-PCT-OFF-DISC-PCT.  00880001
           MOVE XS045-ADDL-DLR-OFF-AMT TO XS047-ADDL-DLR-OFF-AMT.       00890001
           MOVE XS045-ADDL-PRICG-TYP-CDE                                00900001
                                    TO XS047-ADD-PCT-DLR-PRIC-TYP-CDE.  00910001
           MOVE XS045-ADDL-PROMO-ID    TO XS047-ADDL-PCT-DLR-PROMO-ID.  00920001
           MOVE XS045-ADDL-STRT-TMST   TO XS047-ADDL-PCT-DLR-STRT-TMST. 00921002
           MOVE XS045-ADDL-END-TMST    TO XS047-ADDL-PCT-DLR-END-TMST.  00922002
                                                                        00930001
           MOVE XS045-BWP-IND          TO XS047-GWP-PWP-IND.            00940001
           MOVE XS045-BWP-AMT          TO XS047-GWP-PWP-AMT.            00950001
           MOVE XS045-BWP-PRICG-TYP-CDE                                 00960001
                                       TO XS047-GWP-PWP-PRICG-TYP-CDE.  00970001
           MOVE XS045-BWP-PROMO-ID     TO XS047-GWP-PWP-PROMO-ID.       00980001
           MOVE XS045-BWP-STRT-TMST    TO XS047-GWP-PWP-STRT-TMST.      00981002
           MOVE XS045-BWP-END-TMST     TO XS047-GWP-PWP-END-TMST.       00982002
                                                                        00990001
           MOVE XS045-TIERED-PRICG-TYP-CDE                              01000001
                                       TO XS047-TIERED-PRICG-TYP-CDE.   01010001
           MOVE XS045-TIERED-PROMO-ID  TO XS047-TIERED-PROMO-ID.        01020001
           MOVE XS045-TIERED-STRT-TMST TO XS047-TIERED-STRT-TMST.       01021002
           MOVE XS045-TIERED-END-TMST  TO XS047-TIERED-END-TMST.        01022002
                                                                        01030001
           PERFORM 3110-POPULATE-PDE-TIER                               01040001
               VARYING PV-SUB                                           01050001
               FROM 1 BY 1                                              01060001
               UNTIL PV-SUB > PC-NBR-OF-TIERS.                          01070001
                                                                        01080001
           MOVE XS045-EBOGO-PRICG-TYP-CDE                               01081003
                                       TO XS047-EBOGO-PRICG-TYP-CDE.    01082003
           MOVE XS045-EBOGO-PROMO-ID   TO XS047-EBOGO-PROMO-ID.         01083003
           MOVE XS045-EBOGO-STRT-TMST  TO XS047-EBOGO-STRT-TMST.        01084003
           MOVE XS045-EBOGO-END-TMST   TO XS047-EBOGO-END-TMST.         01085003
           MOVE XS045-EBOGO-DISC-QTY   TO XS047-EBOGO-PRICG-QTY.        01085104
           MOVE XS045-EBOGO-DISC-AMT   TO XS047-EBOGO-DISC-PRICG-AMT.   01085204
                                                                        01086003
      ******************************************************************01090001
      * MOVE VALUES IN ARARY                                            01100001
      ******************************************************************01110001
       3110-POPULATE-PDE-TIER.                                          01120001
                                                                        01130001
           SET XS047-TIER-INDEX    TO PV-SUB.                           01140001
           SET TIER-INDEX          TO PV-SUB.                           01150001
                                                                        01160001
           MOVE XS045-TIER-BUY-QTY(TIER-INDEX)                          01170001
                                   TO XS047-BUY-QTY(XS047-TIER-INDEX).  01180001
                                                                        01190001
           MOVE XS045-TIER-BUY-DLR(TIER-INDEX)                          01200001
                                   TO XS047-BUY-DLR(XS047-TIER-INDEX).  01210001
                                                                        01220001
           MOVE XS045-TIER-GET-PCT(TIER-INDEX)                          01230001
                                   TO XS047-GET-PCT(XS047-TIER-INDEX).  01240001
                                                                        01250001
           MOVE XS045-TIER-GET-DLR(TIER-INDEX)                          01260001
                                   TO XS047-GET-DLR(XS047-TIER-INDEX).  01270001
                                                                        01280001
