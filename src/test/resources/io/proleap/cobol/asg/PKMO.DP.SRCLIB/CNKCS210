000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    CNKCS210.                                                 
000300 AUTHOR.        DENNIS STEFFEN.                                           
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  APRIL 1998.                                               
000600 DATE-COMPILED.                                                           
000700*----------------------------------------------------------------*        
000800*    Y210 - STORE ZONE INFO CRITERIA                                      
000900*                                                                         
001000*    THIS PROGRAM ALLOWS THE USER TO ENTER DATA TO BE USED BY             
001100*    SUBSEQUENT PROGRAMS TO CREATE UPDATABLE LIST SCREENS.                
001200*    THE OPER UNIT IS A REQUIRED FIELD AND MUST EXIST ON THE              
001300*    FACLTY TABLE.  ALL OTHER FIELDS ARE OPTIONAL, BUT WILL BE            
001400*    VALIDATED.                                                           
001500*----------------------------------------------------------------*        
263455* CHANGED 11/02/2021    BY JIM HUNTER      WR: CHG0263455                 
263455* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263455* MAKING THE CALL DYNAMIC VS STATIC.                                      
263455*----------------------------------------------------------------*        
CICS  * CHANGED 10/25/2022    BY JIM HUNTER      WR: CHG0276961                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
001600                                                                          
001700                                                                          
001800 ENVIRONMENT DIVISION.                                                    
001900 DATA DIVISION.                                                           
002000                                                                          
002100 WORKING-STORAGE SECTION.                                                 
002200                                                                          
002300 01  DK-DB2-KEYS.                                                         
002400     05  DK-OPER-UNT-ID            PIC  S9(04) COMP.                      
002500     05  DK-FCLTY-ID               PIC  S9(04) COMP.                      
002600                                                                          
002700 01  PC-PROGRAM-CONSTANTS.                                                
002800     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'CN210A  '.         
002900     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'CNKM210 '.         
003000     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'CNKCS210'.         
003100     05  PC-MAX-RETRIES            PIC  9(01)  VALUE  3.                  
003200     05  PC-TSYMSG-NUMBERS.                                               
003300         10  PC-TSYMSG-00005       PIC  9(05)  VALUE  00005.              
003400*            INVALID VALUE                                                
003500         10  PC-TSYMSG-00007       PIC  9(05)  VALUE  00007.              
003600*            REQUIRED ENTRY                                               
003700         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
003800*            MUST BE NUMERIC                                              
003900         10  PC-TSYMSG-01004       PIC  9(05)  VALUE  01004.              
004000*            INVALID OPERATING UNIT/FACILTY ID ENTERED                    
004100                                                                          
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
004400         88  PS-ERROR                          VALUE  'Y'.                
004500         88  PS-NO-ERROR                       VALUE  'N'.                
004600                                                                          
004700*----------------------------------------------------------------*        
004800*    MAP LAYOUT                                                  *        
004900*----------------------------------------------------------------*        
005000                                                                          
005100     COPY CNKM210.                                                        
005200     EJECT                                                                
005300*----------------------------------------------------------------*        
005400*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
005500*----------------------------------------------------------------*        
005600                                                                          
005700     COPY DPWS015.                                                        
005800                                                                          
005900*----------------------------------------------------------------*        
006000*    FUNCTION KEYS COPYBOOK                                      *        
006100*----------------------------------------------------------------*        
006200                                                                          
006300     COPY DPWS016.                                                        
006400 EJECT                                                                    
006500******************************************************************        
006600** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
006700** SUB-ROUTINE (DPKUT100).                                      **        
006800******************************************************************        
006900     COPY DPWS010I.                                                       
007000 EJECT                                                                    
007100******************************************************************        
007200** THE CALLING PARAMETER LIST (DPWS030) FOR THE CICS            **        
007300** ARCHITECTURE API (BLACK BOX) SUB-ROUTINE.                    **        
007400******************************************************************        
007500     COPY DPWS030.                                                        
CICS       COPY DPWS930.                                                        
007600 EJECT                                                                    
007700*****************************************************************         
007800*    STANDARD COMMAREA.                                                   
007900*****************************************************************         
008000                                                                          
008100     COPY DPWS020.                                                        
008200     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
008300                                                                          
008400*****************************************************************         
008500*    INTER-APPLICATION COMMAREA.                                          
008600*****************************************************************         
008700                                                                          
008800         10  INTER-APPL-COMM-AREA PIC X(200).                             
008900     COPY CNWS001.                                                        
009000*****************************************************************         
009100*    SPECIFIC COMMMAREA FOR CNKCS210.                                     
009200*****************************************************************         
009300         10  ASC-SPECIFIC-COMMAREA.                                       
009400             15  ASC-OPER-UNT-ID             PIC  X(04)                   
009500                                 JUSTIFIED RIGHT.                         
009600             15  ASC-OPER-UNT-ID-N REDEFINES ASC-OPER-UNT-ID              
009700                                             PIC  9(04).                  
009800             15  ASC-FCLTY-ID                PIC  X(04)                   
009900                                 JUSTIFIED RIGHT.                         
010000             15  ASC-FCLTY-ID-N  REDEFINES  ASC-FCLTY-ID                  
010100                                             PIC  9(04).                  
010200             15  ASC-FCLTY-NME               PIC  X(25).                  
010300     EJECT                                                                
010400*----------------------------------------------------------------*        
010500*    ABEND PROCESSING COPYBOOK.                                  *        
010600*----------------------------------------------------------------*        
010700                                                                          
010800     COPY DPWS013.                                                        
010900                                                                          
011700                                                                          
011800*----------------------------------------------------------------*        
011900*    COMMAREA USED TO CALL XLKUT071                              *        
012000*----------------------------------------------------------------*        
012100                                                                          
012200     COPY XLWS071.                                                        
012500                                                                          
013300                                                                          
013500 LINKAGE SECTION.                                                         
013600                                                                          
013700 01  DFHCOMMAREA.                                                         
013800     05  FILLER                         OCCURS  1 TO 4072 TIMES           
013900                                        DEPENDING ON EIBCALEN.            
014000         10  FILLER                     PIC X.                            
014100                                                                          
014200                                                                          
014300 PROCEDURE DIVISION.                                                      
014400                                                                          
014500*****************************************************************         
014600** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
014700** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
014800** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
014900**                                                             **         
015000** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
015100** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
015200*****************************************************************         
015300                                                                          
015400 0000-MAIN-MODULE.                                                        
015500                                                                          
015600     INITIALIZE DP030-CICS-API-FIELDS.                                    
015700     MOVE +1                     TO DP030-NUMBER-OF-MAPS.                 
015800     MOVE PC-CURRENT-MAPSET-NAME TO DP030-MAPSET-NAME.                    
015900     MOVE PC-CURRENT-MAP-NAME    TO DP030-MAP-NAME (1).                   
016000     SET DP030-RECEIVE-APPL-MAP  TO TRUE.                                 
016100     MOVE LENGTH OF CN210AI      TO DP030-MAP-LENGTH (1).                 
016200     MOVE 'PROGRAM ENTRY CALL'   TO DP013-MESSAGE-TEXT (1).               
016300                                                                          
016400     PERFORM 0001-CALL-CICS-ARCH-API.                                     
016500     PERFORM 1000-CONTROL-PROCESSING                                      
016600                                                                          
016700     MOVE 'PROGRAM EXIT CALL'    TO DP013-MESSAGE-TEXT (1).               
016800     PERFORM 0001-CALL-CICS-ARCH-API.                                     
016900                                                                          
017000                                                                          
017100 0001-CALL-CICS-ARCH-API.                                                 
017200                                                                          
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
017400                                    DFHCOMMAREA                           
017500                                    DP030-CICS-API-FIELDS                 
017600                                    DP020-STANDARD-COMMAREA               
017700                                    CN210AI.                              
017800                                                                          
017900     IF  DP030-RC-CALL-SUCCESSFUL                                         
018000         CONTINUE                                                         
018100     ELSE                                                                 
018200         SET DP013-NO-ROLLBACK                                            
018300             DP013-XCTL-DISPLAY-RESTART                                   
018400             DP013-CICS-ABEND    TO TRUE                                  
018500         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
018600                                 TO DP013-PARAGRAPH                       
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
018900         MOVE DP030-RETURN-CODE  TO DP013-MESSAGE-TEXT (3)                
019000         PERFORM DP013-0000-PROCESS-ABEND                                 
019100     END-IF.                                                              
019200 EJECT                                                                    
019300                                                                          
019400*----------------------------------------------------------------*        
019500*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
019600*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
019700*----------------------------------------------------------------*        
019800                                                                          
019900 1000-CONTROL-PROCESSING.                                                 
020000                                                                          
020100     EVALUATE TRUE                                                        
020200         WHEN DP020-NEXT-ACT-INITIAL                                      
020300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
020400             PERFORM 4000-BUILD-INITIAL-PANEL                             
020500                                                                          
020600         WHEN DP020-NEXT-ACT-READ-MAP                                     
020700             PERFORM 2000-FKEY-PROCESSING                                 
020800                                                                          
020900         WHEN DP020-NEXT-ACT-RETURN                                       
021000             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
021100             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
021200                                                                          
021300         WHEN OTHER                                                       
021400             SET DP013-LOGIC-ABEND                                        
021500                 DP013-NO-ROLLBACK                                        
021600                                 TO TRUE                                  
021700             MOVE '1000-CONTROL-PROCESSING'                               
021800                                 TO  DP013-PARAGRAPH                      
021900             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
022000                                 TO  DP013-MESSAGE-TEXT(1)                
022100             MOVE DP020-NEXT-APPL-ACTIVITY                                
022200                                 TO DP013-MESSAGE-TEXT(2)                 
022300             PERFORM DP013-0000-PROCESS-ABEND                             
022400     END-EVALUATE.                                                        
022500                                                                          
022600*----------------------------------------------------------------*        
022700* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
022800* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
022900* EDITS.                                                         *        
023000*----------------------------------------------------------------*        
023100                                                                          
023200 2000-FKEY-PROCESSING.                                                    
023300                                                                          
023400     EVALUATE TRUE                                                        
023500                                                                          
023600         WHEN DP020-SRC-AID = DP016-CLEAR                                 
023700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
023800             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
023900                                                                          
024000         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
024100             INITIALIZE ASC-SPECIFIC-COMMAREA                             
024200             PERFORM 4000-BUILD-INITIAL-PANEL                             
024300             PERFORM 3100-RESET-ATTRIBUTES                                
024400                                                                          
024500         WHEN OTHER                                                       
024600             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
024700             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
024800             IF  PS-NO-ERROR                                              
024900                 SET DP030-CONTINUE-GO                                    
025000                                 TO TRUE                                  
025100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
025200             ELSE                                                         
025300                 SET DP030-OVERRIDE-GO                                    
025400                                 TO TRUE                                  
025500             END-IF                                                       
025600     END-EVALUATE.                                                        
025700                                                                          
025800******************************************************************        
025900** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
026000** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
026100** FROM THE CICS ARCHITECTURE API.                              **        
026200******************************************************************        
026300                                                                          
026400 2100-CHECK-FUNCTION-KEY.                                                 
026500                                                                          
026600     EVALUATE TRUE                                                        
026700         WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)                      
026800         WHEN DP020-FK-LOCAL-FUNC-02 (DP020-SRC-AID)                      
026900             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
027000                                                                          
027100         WHEN DP020-SRC-AID = DP016-ENTER                                 
027200             CONTINUE                                                     
027300                                                                          
027400         WHEN OTHER                                                       
027500             SET DP013-NO-ROLLBACK                                        
027600                 DP013-XCTL-DISPLAY-RESTART                               
027700                 DP013-CICS-ABEND                                         
027800                                 TO TRUE                                  
027900             MOVE '2100-CHECK-FUNCTION-KEY'                               
028000                                 TO DP013-PARAGRAPH                       
028100             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
028200                                 TO DP013-MESSAGE-TEXT (1)                
028300             PERFORM DP013-0000-PROCESS-ABEND                             
028400     END-EVALUATE.                                                        
028500                                                                          
028600******************************************************************        
028700** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
028800** INTER-APPLICATION COMMAREA.                                  **        
028900******************************************************************        
029000                                                                          
029100 2110-PREPARE-INTER-APPL-COMM.                                            
029200                                                                          
029300     MOVE ASC-OPER-UNT-ID-N      TO CN001-OPER-UNT-ID.                    
029400     MOVE ASC-FCLTY-ID-N         TO CN001-FCLTY-ID.                       
029500 EJECT                                                                    
029600                                                                          
029700*----------------------------------------------------------------*        
029800* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
029900* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
030000* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
030100*----------------------------------------------------------------*        
030200                                                                          
030300 2200-MOVE-SCREEN-TO-COMMAREA.                                            
030400                                                                          
030500     IF  AOPERL > ZERO                                                    
030600         MOVE AOPERI               TO ASC-OPER-UNT-ID                     
030700     ELSE                                                                 
030800         IF  AOPERF = DP015-ERASE-EOF                                     
030900             MOVE SPACE            TO ASC-OPER-UNT-ID                     
031000         END-IF                                                           
031100     END-IF.                                                              
031200                                                                          
031300     IF  AFCLTYL > ZERO                                                   
031400         MOVE AFCLTYI              TO ASC-FCLTY-ID                        
031500     ELSE                                                                 
031600         IF  AFCLTYF = DP015-ERASE-EOF                                    
031700             MOVE SPACE            TO ASC-FCLTY-ID                        
031800         END-IF                                                           
031900     END-IF.                                                              
032000                                                                          
032100*----------------------------------------------------------------*        
032200* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
032300* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
032400* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
032500* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
032600*----------------------------------------------------------------*        
032700                                                                          
032800 3000-EDIT-DATA-IN-COMMAREA.                                              
032900                                                                          
033000     PERFORM 3100-RESET-ATTRIBUTES.                                       
033100                                                                          
033200     IF  ASC-FCLTY-ID = SPACES OR ZEROES                                  
033300         MOVE +1                 TO ASC-FCLTY-ID-N                        
033400     END-IF                                                               
033500                                                                          
033600     IF  ASC-OPER-UNT-ID = SPACES OR ZEROES                               
033700         SET PS-ERROR                                                     
033800             DP020-MSG-FATAL     TO TRUE                                  
033900         MOVE SPACES             TO ASC-FCLTY-NME                         
034000                                    ADESCO                                
034100         MOVE PC-TSYMSG-00007    TO DP020-MSG-NUMBER                      
034200         MOVE DP015-UNP-ALP-BRT-OFF                                       
034300                                 TO AOPERA                                
034400         MOVE DP015-RED          TO AOPERC                                
034500         MOVE DP015-REVERSE      TO AOPERH                                
034600         MOVE -1                 TO AOPERL                                
034700         SET DP030-SET-CURSOR-APPL-1                                      
034800                                 TO TRUE                                  
034900     END-IF                                                               
035000                                                                          
035100     IF PS-NO-ERROR                                                       
035200         MOVE ASC-OPER-UNT-ID    TO DP010I-UNEDITED-FIELD                 
035300         MOVE LENGTH OF ASC-OPER-UNT-ID                                   
035400                                 TO DP010I-MAXIMUM-DIGITS                 
035500         MOVE ZERO               TO DP010I-MAXIMUM-DECIMALS               
035600         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
035700                                 TO TRUE                                  
263455         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263455              USING DP010I-NUMERIC-EDIT-AREA                              
035900         IF  DP010I-ERROR-DETECTED                                        
036000             SET PS-ERROR                                                 
036100                 DP020-MSG-FATAL TO TRUE                                  
036200             MOVE PC-TSYMSG-00008                                         
036300                                 TO DP020-MSG-NUMBER                      
036400             MOVE DP015-UNP-ALP-BRT-OFF                                   
036500                                 TO AOPERA                                
036600             MOVE DP015-RED      TO AOPERC                                
036700             MOVE DP015-REVERSE  TO AOPERH                                
036800             MOVE -1             TO AOPERL                                
036900             SET DP030-SET-CURSOR-APPL-1                                  
037000                                 TO TRUE                                  
037100         ELSE                                                             
037200             MOVE DP010I-NUMERIC-FIELD                                    
037300                                 TO ASC-OPER-UNT-ID-N                     
037400                                    DK-OPER-UNT-ID                        
037500             MOVE ASC-OPER-UNT-ID TO AOPERO                               
037600             PERFORM 3300-VALIDATE-OPER-UNT-ID                            
037700         END-IF                                                           
037800     END-IF.                                                              
037900                                                                          
038000     IF PS-NO-ERROR                                                       
038100         MOVE ASC-FCLTY-ID       TO DP010I-UNEDITED-FIELD                 
038200         MOVE LENGTH OF ASC-FCLTY-ID                                      
038300                                 TO DP010I-MAXIMUM-DIGITS                 
038400         MOVE ZERO               TO DP010I-MAXIMUM-DECIMALS               
038500         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
038600                                 TO TRUE                                  
263455         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263455              USING DP010I-NUMERIC-EDIT-AREA                              
038800         IF  DP010I-ERROR-DETECTED                                        
038900             SET PS-ERROR                                                 
039000                 DP020-MSG-FATAL TO TRUE                                  
039100             MOVE PC-TSYMSG-00008                                         
039200                                 TO DP020-MSG-NUMBER                      
039300             MOVE DP015-UNP-ALP-BRT-OFF                                   
039400                                 TO AFCLTYA                               
039500             MOVE DP015-RED      TO AFCLTYC                               
039600             MOVE DP015-REVERSE  TO AFCLTYH                               
039700             MOVE -1             TO AFCLTYL                               
039800             SET DP030-SET-CURSOR-APPL-1                                  
039900                                 TO TRUE                                  
040000         ELSE                                                             
040100             MOVE DP010I-NUMERIC-FIELD                                    
040200                                 TO ASC-FCLTY-ID-N                        
040300                                    DK-FCLTY-ID                           
040400             MOVE ASC-FCLTY-ID   TO AFCLTYO                               
040500         END-IF                                                           
040600     END-IF.                                                              
040700                                                                          
040800     IF  PS-NO-ERROR                                                      
040900         PERFORM 3200-VALIDATE-FCLTY-ID                                   
041000     END-IF.                                                              
041100                                                                          
041200     IF  PS-NO-ERROR                                                      
041300         SET DP030-SET-CURSOR-APPL-1                                      
041400                                 TO TRUE                                  
041500     END-IF.                                                              
041600                                                                          
041700*----------------------------------------------------------------*        
041800*  MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE  *        
041900*  MAP.                                                          *        
042000*----------------------------------------------------------------*        
042100                                                                          
042200 3100-RESET-ATTRIBUTES.                                                   
042300                                                                          
042400     MOVE -1                       TO AOPERL.                             
042500     MOVE DP015-UNP-NUM-NOR-OFF    TO AOPERA                              
042600                                      AFCLTYA                             
042700     MOVE DP015-GREEN              TO AOPERC                              
042800                                      AFCLTYC                             
042900     MOVE DP015-UNDERLINE          TO AOPERH                              
043000                                      AFCLTYH.                            
043100                                                                          
048400                                                                          
053700****************************************************************          
053800*    VERIFY THE OPER UNIT ENTERED ON THE MAP IS VALID.         *          
053900*                                                              *          
054000****************************************************************          
054100                                                                          
054200 3200-VALIDATE-FCLTY-ID.                                                  
054300                                                                          
054400     INITIALIZE XL071-COMMAREA.                                           
054500     MOVE DK-OPER-UNT-ID TO XL071-LOCATION-NUMBER.                        
054600                                                                          
054700     CALL XL071-LOCATION-MODULE USING                                     
054800                                DFHEIBLK                                  
054900                                XL071-COMMAREA                            
055000                                XL071-COMMAREA-REC.                       
055100                                                                          
055200     EVALUATE TRUE                                                        
055300         WHEN XL071-NO-ERRORS                                             
055310         WHEN XL071-DC-ASGMT-NOT-FND                                      
055320              MOVE XL071-LOC-NM TO ASC-FCLTY-NME                          
055330                                   ADESCO                                 
057000                                                                          
057100         WHEN XL071-LOC-NOT-FOUND                                         
057101              SET PS-ERROR                                                
057102                  DP020-MSG-FATAL TO TRUE                                 
057103              MOVE SPACES           TO ASC-FCLTY-NME                      
057104                                       ADESCO                             
057105              MOVE PC-TSYMSG-01004 TO DP020-MSG-NUMBER                    
057106              MOVE DP015-UNP-ALP-BRT-OFF                                  
057107                                      TO AFCLTYA                          
057108              MOVE DP015-RED  TO AFCLTYC                                  
057109              MOVE DP015-REVERSE TO AFCLTYH                               
057110              MOVE -1         TO AOPERL                                   
057111              SET DP030-SET-CURSOR-APPL-1                                 
057112                                      TO TRUE                             
057300                                                                          
057400         WHEN OTHER                                                       
057410              MOVE '3200-VALIDATE-FCLTY-ID'                               
057420                              TO DP013-PARAGRAPH                          
057431              STRING 'SELECT STORE NUMBER - '                             
057432                      XL071-LOCATION-NUMBER                               
057434                     ' - BAD RETURN CODE FROM XLKUT071 - '                
057435                      XL071-RETURN-CODE                                   
057436                      DELIMITED BY SIZE                                   
057440                      INTO DP013-MESSAGE-TEXT (1)                         
057441              STRING 'BAD SQLCODE - '                                     
057442                      XL071-SQLCODE                                       
057443                     ' - '                                                
057444                      XL071-DB2-TABLE                                     
057447                      DELIMITED BY SIZE                                   
057448                      INTO DP013-MESSAGE-TEXT (2)                         
057451              MOVE XL071-SQLCA     TO DP013-SQLCA                         
057460              MOVE XL071-DB2-TABLE TO DP013-DB2-TABLE-NAME (1)            
057470              SET DP013-DB2-ABEND                                         
057491                  DP013-XCTL-DISPLAY-RESTART  TO TRUE                     
057492              PERFORM DP013-0000-PROCESS-ABEND                            
058900                                                                          
059000     END-EVALUATE.                                                        
059100                                                                          
059110****************************************************************          
059120*    VERIFY THE OPER UNIT ENTERED ON THE MAP IS VALID.         *          
059130*                                                              *          
059140****************************************************************          
059150                                                                          
059160 3300-VALIDATE-OPER-UNT-ID.                                               
059170                                                                          
059180     INITIALIZE XL071-COMMAREA.                                           
059190     MOVE DK-OPER-UNT-ID TO XL071-LOCATION-NUMBER.                        
059191                                                                          
059192     CALL XL071-LOCATION-MODULE USING                                     
059193                                DFHEIBLK                                  
059194                                XL071-COMMAREA                            
059195                                XL071-COMMAREA-REC.                       
059196                                                                          
059197     EVALUATE TRUE                                                        
059198         WHEN XL071-NO-ERRORS                                             
059199         WHEN XL071-DC-ASGMT-NOT-FND                                      
059200              IF XL071-LOC-TYP-CDE = 'DC' OR                              
059201                 XL071-LOC-TYP-CDE = 'HQ'                                 
059202                 CONTINUE                                                 
059203              ELSE                                                        
059204                 SET PS-ERROR                                             
059205                     DP020-MSG-FATAL TO TRUE                              
059206                 MOVE SPACES           TO ASC-FCLTY-NME                   
059207                                          ADESCO                          
059208                 MOVE PC-TSYMSG-01004 TO DP020-MSG-NUMBER                 
059209                 MOVE DP015-UNP-ALP-BRT-OFF                               
059210                                         TO AFCLTYA                       
059211                 MOVE DP015-RED  TO AFCLTYC                               
059212                 MOVE DP015-REVERSE TO AFCLTYH                            
059213                 MOVE -1         TO AOPERL                                
059214                 SET DP030-SET-CURSOR-APPL-1                              
059215                                         TO TRUE                          
059216              END-IF                                                      
059217                                                                          
059218         WHEN XL071-LOC-NOT-FOUND                                         
059219              SET PS-ERROR                                                
059220                  DP020-MSG-FATAL TO TRUE                                 
059221              MOVE SPACES           TO ASC-FCLTY-NME                      
059222                                       ADESCO                             
059223              MOVE PC-TSYMSG-01004 TO DP020-MSG-NUMBER                    
059224              MOVE DP015-UNP-ALP-BRT-OFF                                  
059225                                      TO AFCLTYA                          
059226              MOVE DP015-RED  TO AFCLTYC                                  
059227              MOVE DP015-REVERSE TO AFCLTYH                               
059228              MOVE -1         TO AOPERL                                   
059229              SET DP030-SET-CURSOR-APPL-1                                 
059230                                      TO TRUE                             
059231                                                                          
059232         WHEN OTHER                                                       
059233              MOVE '3300-VALIDATE-OPER-ID'                                
059234                              TO DP013-PARAGRAPH                          
059236              STRING 'SELECT OPER UNT - '                                 
059237                      XL071-LOCATION-NUMBER                               
059238                     ' - BAD RETURN CODE FROM XLKUT071 - '                
059240                      XL071-RETURN-CODE                                   
059241                      DELIMITED BY SIZE                                   
059242                      INTO DP013-MESSAGE-TEXT (1)                         
059243              STRING 'SQLCODE - '                                         
059244                      XL071-SQLCODE                                       
059245                     ' - '                                                
059246                      XL071-DB2-TABLE                                     
059248                      DELIMITED BY SIZE                                   
059249                      INTO DP013-MESSAGE-TEXT (2)                         
059250              MOVE XL071-SQLCA     TO DP013-SQLCA                         
059251              MOVE XL071-DB2-TABLE TO DP013-DB2-TABLE-NAME (1)            
059252              SET DP013-DB2-ABEND                                         
059253                  DP013-XCTL-DISPLAY-RESTART  TO TRUE                     
059254              PERFORM DP013-0000-PROCESS-ABEND                            
059255                                                                          
059256     END-EVALUATE.                                                        
059260                                                                          
059300                                                                          
059400*----------------------------------------------------------------*        
059500*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
059600*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
059700*  IT HERE.                                                      *        
059800*----------------------------------------------------------------*        
059900                                                                          
060000 4000-BUILD-INITIAL-PANEL.                                                
060100                                                                          
060200     SET DP030-SET-CURSOR-APPL-1 TO TRUE                                  
060300     MOVE -1                     TO AOPERL.                               
060400     MOVE DP020-LOCATION-NBR     TO ASC-OPER-UNT-ID                       
060500     INSPECT ASC-OPER-UNT-ID REPLACING ALL SPACES BY ZERO.                
060600     MOVE ASC-OPER-UNT-ID-N      TO DK-OPER-UNT-ID                        
060700                                                                          
060800     MOVE DP020-FCLTY-ID         TO ASC-FCLTY-ID                          
060900     INSPECT ASC-FCLTY-ID REPLACING ALL SPACES BY ZERO.                   
061000     MOVE ASC-FCLTY-ID-N         TO DK-FCLTY-ID                           
061100     PERFORM 3200-VALIDATE-FCLTY-ID.                                      
061200     PERFORM 4400-MOVE-COMMAREA-TO-SCREEN.                                
061300                                                                          
061400*----------------------------------------------------------------*        
061500*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
061600*----------------------------------------------------------------*        
061700                                                                          
061800 4400-MOVE-COMMAREA-TO-SCREEN.                                            
061900                                                                          
062000     MOVE ASC-OPER-UNT-ID-N      TO AOPERO.                               
062100     MOVE ASC-FCLTY-ID-N         TO AFCLTYO.                              
062200     MOVE ASC-FCLTY-NME          TO ADESCO.                               
062300 EJECT                                                                    
062400*----------------------------------------------------------------*        
062500*    ABEND PROCESSOR MODULE                                      *        
062600*----------------------------------------------------------------*        
062700                                                                          
062800     COPY DPPD013.                                                        
