       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.   XSKUP992.                                                  
       AUTHOR.       KRITIVEEN KUSHAL.                                          
       INSTALLATION. KOHLS DEPARTMENT STORES.                                   
       DATE-WRITTEN. 08/04/2016.                                                
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * THIS IS A DATA CORRECTION PROGRAM WHICH WILL UPDATE            *        
      * XST_VND_STYL TABLE THAT HAS MISMATCH RECORDS IN INTEGRITY      *        
      * REPORT DUE TO DIFFERENCE IN THE ESL IND.                       *        
      ******************************************************************        
      ******************************************************************        
      * CHANGE HISTORY:                                                         
      *  WR/PBI        DATE        DESCRIPTION OF CHANGES                       
      *  ----------    --------    ------------------------------------         
      *  CHG0147978    08/04/16    INITIAL IMPLEMENTATION.                      
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.                        IBM-3090.                        
       OBJECT-COMPUTER.                        IBM-3090.                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT VNDSTYL-ERR-FILE ASSIGN TO INP01.                             
           SELECT VNDSTYL-RPT-FILE ASSIGN TO OUT01.                             
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  VNDSTYL-ERR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VNDSTYL-ERROR-RECORD             PIC X(400).                         
                                                                                
                                                                                
       FD  VNDSTYL-RPT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  VNDSTYL-REPORT-RECORD             PIC X(400).                        
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PROGRAM VARIABLES.                                             *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05 PV-COMMIT-COUNT               PIC 9(04)  VALUE ZEROES.            
           05 PV-SQL-SUB                    PIC S9(4)  VALUE +0 COMP.           
           05 PV-PARAGRAPH-NAME             PIC X(30)  VALUE SPACES.            
           05 PV-DB2-OPERATION-ATTEMPTED    PIC X(30)  VALUE SPACES.            
           05 PV-SQLCODE-DISPLAY            PIC +(8)9  VALUE ZEROES.            
      ******************************************************************        
      * PROGRAM SWITCHES.                                              *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-VNDSTYL-FILE-SW     PIC X(01)  VALUE 'N'.              
               88  PS-END-OF-VNDSTYL-FILE               VALUE 'Y'.              
               88  PS-NOT-END-OF-VNDSTYL-FILE           VALUE 'N'.              
                                                                                
      ******************************************************************        
      * PROGRAM ACCUMULATORS.                                          *        
      ******************************************************************        
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-VNDSTYL-ROWS-READ         PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-ERROR-RECORD-WRITTEN      PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
           05  PA-VNDSTYL-ROW-UPDATED        PIC S9(09) COMP-3                  
                                                       VALUE ZEROES.            
           05  PA-COMMIT-TAKEN              PIC S9(09) COMP-3                   
                                                       VALUE ZEROES.            
       01  ABEND-CODE                       PIC S9(04) COMP SYNC                
                                                       VALUE 0.                 
           88  ABEND-4001-CC-MISSING-ERR               VALUE +4001.             
           88  ABEND-4002-CC-INVALID-ERR               VALUE +4002.             
                                                                                
      *****************************************************************         
      * PROGRAM CONSTANTS                                                       
      *****************************************************************         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-CURRENT-PROGRAM-NAME       PIC X(08)                          
                                                   VALUE 'XSKUP992'.            
           05  PC-MAX-RETRIES                PIC S9(04) VALUE +5 COMP.          
           05  PC-COMMIT-MAX                 PIC 9(09) VALUE   999.             
                                                                                
      *****************************************************************         
      * XST_VND_STYL TABLE DCLGEN.                                              
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XST00013                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * XS DOMAIN VND_STYL MISMATCH ERROR FILE LAYOUT.                          
      *****************************************************************         
           COPY XSRD010.                                                        
      *****************************************************************         
      * STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA.                        
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * COMMUNICATION WORK AREA.                                                
      *****************************************************************         
           COPY SQLCA2.                                                         
                                                                                
      *****************************************************************         
      * VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004.                
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      *****************************************************************         
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARA.                                            *        
      ******************************************************************        
       0000-MAINLINE-PARA.                                                      
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-READ-VNDSTYL-ERROR-FILE.                                
                                                                                
           PERFORM 3000-PROCESS-RECORDS                                         
             UNTIL PS-END-OF-VNDSTYL-FILE.                                      
                                                                                
           PERFORM 4000-EOJ-ROUTINE.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZE.                                               *        
      ******************************************************************        
       1000-INITIALIZE.                                                         
           INITIALIZE DCLXST00013                                               
                                                                                
           OPEN INPUT  VNDSTYL-ERR-FILE.                                        
           OPEN OUTPUT VNDSTYL-RPT-FILE.                                        
                                                                                
      ******************************************************************        
      * 2000-MEAD-VNDSTYL-ERROR-FILE.                                           
      ******************************************************************        
       2000-READ-VNDSTYL-ERROR-FILE.                                            
                                                                                
           READ VNDSTYL-ERR-FILE                                                
           INTO XSRD010-VNDSTYL-ERR-RECORD                                      
             AT END                                                             
               SET PS-END-OF-VNDSTYL-FILE TO TRUE                               
             NOT AT END                                                         
               ADD +1                    TO  PA-VNDSTYL-ROWS-READ               
           END-READ.                                                            
                                                                                
      ******************************************************************        
      * 3000-PROCESS-RECORDS                                                    
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
                                                                                
           INITIALIZE DCLXST00013                                               
                                                                                
              IF   XSRD010-XS-STYL-ID       = XSRD010-VSM-STYL-ID               
               AND XSRD010-XS-DEPT-NBR      = XSRD010-VSM-DEPT-NBR              
               AND XSRD010-XS-MAJ-CL-NBR    = XSRD010-VSM-MAJ-CL-NBR            
               AND XSRD010-XS-SUB-CL        = XSRD010-VSM-SUB-CL                
               AND XSRD010-XS-VS-ID         = XSRD010-VSM-VS-ID                 
               AND XSRD010-XS-VS-NBR        = XSRD010-VSM-VS-NBR                
               AND XSRD010-XS-ENT-ID        = XSRD010-VSM-ENT-ID                
               AND XSRD010-XS-STKR-NBR      = XSRD010-VSM-STKR-NBR              
               AND XSRD010-XS-RTL-CLSN-DESC = XSRD010-VSM-RTL-CLSN-DESC         
               AND XSRD010-XS-LBL-NM        = XSRD010-VSM-LBL-NM                
               AND XSRD010-XS-HNG-FOLD-DESC = XSRD010-VSM-HNG-FOLD-DESC         
               AND XSRD010-VSM-VS-DESC      = XSRD010-XS-VS-DESC                
               AND XSRD010-XS-ESL-IND NOT EQUAL XSRD010-VSM-ESL-IND             
                                                                                
                  MOVE XSRD010-VSM-STYL-ID TO XST00013-STYL-ID                  
                  MOVE XSRD010-VSM-ESL-IND TO XST00013-ESL-IND                  
                                                                                
                 PERFORM 3100-UPDATE-VNDSTYL                                    
              ELSE                                                              
                 PERFORM 3200-WRITE-ERROR-RECORD                                
              END-IF.                                                           
              PERFORM 2000-READ-VNDSTYL-ERROR-FILE.                             
      ******************************************************************        
      * 3100-UPDATE-VNDSTYL.                                                    
      ******************************************************************        
       3100-UPDATE-VNDSTYL.                                                     
                                                                                
               EXEC SQL                                                         
                   UPDATE XST_VND_STYL                                          
                      SET ESL_IND      = :XST00013-ESL-IND                      
                     ,LAST_UPD_TMST = CURRENT TIMESTAMP                         
                   WHERE STYL_ID       = :XST00013-STYL-ID                      
               END-EXEC.                                                        
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                  DISPLAY 'STYL_ID UPDATED: ' XSRD010-XS-STYL-ID                
                 ADD +1  TO PA-VNDSTYL-ROW-UPDATED                              
                 ADD +1         TO PV-COMMIT-COUNT                              
                 IF PV-COMMIT-COUNT > PC-COMMIT-MAX                             
                    MOVE 0 TO PV-COMMIT-COUNT                                   
                    PERFORM 3500-UPDATE-COMMIT                                  
                 END-IF                                                         
               WHEN OTHER                                                       
                 CONTINUE                                                       
             END-EVALUATE.                                                      
                                                                                
      *****************************************************************         
      * 3200-WRITE-ERROR-RECORD.                                      *         
      **WRITE RECORDS HAVING OTHER MISMATCHES IN ERROR FILE           *         
      *****************************************************************         
       3200-WRITE-ERROR-RECORD.                                                 
                                                                                
           WRITE VNDSTYL-REPORT-RECORD FROM XSRD010-VNDSTYL-ERR-RECORD.         
                                                                                
           ADD +1                       TO PA-ERROR-RECORD-WRITTEN.             
                                                                                
       EJECT.                                                                   
      ******************************************************************        
      * 3500-UPDATE-COMMIT.                                                     
      ******************************************************************        
       3500-UPDATE-COMMIT.                                                      
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-SQL-SUB                                               
               FROM 1 BY 1                                                      
               UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
                         OR  SQLCODE = 0)                                       
                                                                                
               EXEC SQL                                                         
                   COMMIT                                                       
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE = -904                                          
                   WHEN SQLCODE = -911                                          
                   WHEN SQLCODE = -913                                          
                   WHEN SQLCODE = 0                                             
                       ADD +1 TO PA-COMMIT-TAKEN                                
                       CONTINUE                                                 
                   WHEN SQLWARN NOT EQUAL SPACES                                
                   WHEN SQLCODE NOT EQUAL ZERO                                  
                       MOVE '3500-UPDATE-COMMIT'                                
                                     TO  PV-PARAGRAPH-NAME                      
                       MOVE 'COMMIT FAILURE'                                    
                                    TO  PV-DB2-OPERATION-ATTEMPTED              
                       PERFORM 9100-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
            IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                
               MOVE '3500-UPDATE-COMMIT'                                        
                                    TO  PV-PARAGRAPH-NAME                       
               MOVE 'COMMIT RETRIES EXCEEDED'                                   
                                    TO  PV-DB2-OPERATION-ATTEMPTED              
               PERFORM 9100-SQL-ABEND                                           
            END-IF.                                                             
                                                                                
      *****************************************************************         
      * 4000-EOJ-ROUTINE.                                                       
      ******************************************************************        
       4000-EOJ-ROUTINE.                                                        
           PERFORM 3500-UPDATE-COMMIT.                                          
                                                                                
           CLOSE VNDSTYL-ERR-FILE.                                              
           PERFORM 9005-DISPLAY-TOTALS.                                         
                                                                                
      ******************************************************************        
      * 9100-SQL-ABEND.                                                         
      ******************************************************************        
       9100-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
           DISPLAY ' '.                                                         
           DISPLAY '9100-SQL-ABEND'.                                            
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = XSKUP992'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
           DISPLAY '** SQL CODE  ** = ' PV-SQLCODE-DISPLAY.                     
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * 9005-DISPLAY-TOTALS.                                                    
      ******************************************************************        
       9005-DISPLAY-TOTALS.                                                     
           DISPLAY '******** XSKUP992 TOTALS *********'.                        
           DISPLAY ' VNDSTYL RECS READ    : ' PA-VNDSTYL-ROWS-READ.             
           DISPLAY ' VNDSTYL ROWS UPDATED : ' PA-VNDSTYL-ROW-UPDATED.           
           DISPLAY ' VNDSTYL ROWS WRITTEN : ' PA-ERROR-RECORD-WRITTEN.          
           DISPLAY ' NUMBER OF COMMIT TAKN: ' PA-COMMIT-TAKEN.                  
           DISPLAY '***** END OF XSKUP992 TOTALS *****'.                        
           DISPLAY ' '.                                                         
