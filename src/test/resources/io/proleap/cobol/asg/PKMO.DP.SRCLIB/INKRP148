       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    INKRP148.                                                 
       AUTHOR.        JIM SHIFF.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04/15/00.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *     INKRP148 - AREA COUNT BY STORE                                      
      *                                                                         
      * THIS PROGRAM READS AN EXTRACT FROM THE STORE LEVEL AND PRODUCES         
      * THE AREA COUNT FOR EACH STORE REPORT.                                   
      *                                                                         
      * TOTALS ARE REPORTED FOR THE FOLLOWING LEVELS:                           
      *       STORE                                                             
      *       REPORT                                                            
      ******************************************************************        
JP0700* 07/07/00 - JOHN PRICE            WR #21015                     *        
JP0700*    - CHANGED THE WAY THAT THE DATA IS PRESENTED ON THE REPORT. *        
JP0700*      IT CURRENTLY GOES ACCROSS THE PAGE AND THEN DOWN. IT WILL *        
JP0700*      BE CHANGED TO GO DOWN THE PAGE AND THEN ACCROSS           *        
      ******************************************************************        
W21732* 02/10/03 - SUKUMAR, IGSI   WR #21732                           *        
W21732*    - NI-SKU PROJECT : COMPILED THE PROGRAM WITH CHANGED        *        
W21732*      COPYBOOK INRD135.                                         *        
W21732******************************************************************        
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W26600******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT STORE-EXTRACT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT AREA-REPORT-FILE                                              
               ASSIGN RPT01.                                                    
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  STORE-EXTRACT-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  STORE-EXTRACT-RECORD           PIC  X(30).                           
                                                                                
       FD  AREA-REPORT-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  AREA-REPORT-RECORD             PIC  X(132).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
JP0700     05  PC-1                     PIC 9(01) VALUE 1.                      
JP0700     05  PC-62                    PIC 9(02) VALUE 62.                     
JP0700     05  PC-TABLE-MAX             PIC 9(03) VALUE 186.                    
           05  PC-MAX-LINE-CNT          PIC 9(02) VALUE 65.                     
           05  PC-LINE-ITEM-MAX         PIC 9(02) VALUE 3.                      
           05  PC-LINE-ITEM-1           PIC 9(02) VALUE 1.                      
           05  PC-LINE-ITEM-2           PIC 9(02) VALUE 2.                      
           05  PC-LINE-ITEM-3           PIC 9(02) VALUE 3.                      
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-LINE-ITEM             PIC 9(02)    VALUE 1.                   
           05  PA-LINE-CNT              PIC 9(02)    VALUE 99.                  
           05  PA-TOTAL-QTY             PIC 9(09)    VALUE 0.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
W26600     05  PV-HOLD-STORE                 PIC X(04) VALUE SPACE.             
           05  PV-HOLD-AREA                  PIC S9(9) VALUE ZERO.              
           05  PV-PAGE-CNT                   PIC S9(9) VALUE ZERO.              
JP0700     05  PV-LINE-SUB                   PIC 9(03) VALUE ZERO.              
JP0700     05  PV-HOLD-LINE-SUB              PIC 9(03) VALUE ZERO.              
JP0700     05  PV-TEMP-LINE-SUB              PIC 9(03) VALUE ZERO.              
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-STORE-EXTRACT-SW    PIC X     VALUE 'N'.               
               88 PS-END-OF-STORE-EXTRACT              VALUE 'Y'.               
JP0700     05  PS-PAGE-PRINTED-SW            PIC X     VALUE 'N'.               
JP0700         88 PS-RESET-PAGE-PRINTED                VALUE 'N'.               
JP0700         88 PS-PAGE-PRINTED                      VALUE 'Y'.               
JP0700     05  PS-PRINT-STORE-TOTALS-SW      PIC X     VALUE 'N'.               
JP0700         88 PS-RESET-PRINT-STORE-TOTALS          VALUE 'N'.               
JP0700         88 PS-PRINT-STORE-TOTALS                VALUE 'Y'.               
JP0700     05  PS-STORE-TOTALS-PRINTED-SW    PIC X     VALUE 'N'.               
JP0700         88 PS-RESET-STORE-TOTALS-PRINTED        VALUE 'N'.               
JP0700         88 PS-STORE-TOTALS-PRINTED              VALUE 'Y'.               
                                                                                
       01  TD-TODAYS-DATE.                                                      
           05  TD-YEAR                      PIC  9(02)     VALUE ZERO.          
           05  TD-MONTH                     PIC  9(02)     VALUE ZERO.          
           05  TD-DAY                       PIC  9(02)     VALUE ZERO.          
                                                                                
       01  TT-TODAYS-TIME.                                                      
           05  TT-HOUR                      PIC  9(02)     VALUE ZERO.          
           05  TT-MINUTE                    PIC  9(02)     VALUE ZERO.          
           05  FILLER                       PIC  9(04)     VALUE ZERO.          
                                                                                
JP0700******************************************************************        
JP0700*  LINE TABLE (HOLDS A PAGE WORTH OF DATA)                                
JP0700******************************************************************        
JP0700 01   PT-LINE-TABLE.                                                      
JP0700      05  PT-LINE-TBL                 OCCURS 186 TIMES.                   
JP0700          10  PT-LINE-AREA            PIC 9(09).                          
JP0700          10  PT-LINE-AREA-ALPHA REDEFINES PT-LINE-AREA PIC X(09).        
JP0700          10  PT-LINE-QTY             PIC 9(09).                          
JP0700          10  PT-LINE-QTY-ALPHA REDEFINES PT-LINE-QTY   PIC X(09).        
JP0700                                                                          
      ******************************************************************        
      *  KOHL'S STANDARD FIRST HEADING LINE 132 BYTE WIDE LAYOUT                
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
       01  HL2-HEADING-LINE-2.                                                  
           05  FILLER                       PIC X(50) VALUE SPACE.              
           05  FILLER                       PIC X(32) VALUE                     
               'TOTAL PIECE COUNT BY AREA NUMBER'.                              
           05  FILLER                       PIC X(50) VALUE SPACE.              
                                                                                
       01  HL3-HEADING-LINE-3.                                                  
           05  FILLER                       PIC X(06)  VALUE 'STORE:'.          
           05  FILLER                       PIC X(02)  VALUE SPACE.             
W26600     05  HL3-STORE-NBR                PIC X(04)  VALUE SPACE.             
W26600     05  FILLER                       PIC X(120) VALUE SPACE.             
                                                                                
                                                                                
       01  HL4-HEADING-LINE-4.                                                  
           05  FILLER                       PIC X(03) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'AREA NUMBER'.                      
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  FILLER                       PIC X(08) VALUE                     
                                            'QUANTITY'.                         
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'AREA NUMBER'.                      
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  FILLER                       PIC X(08) VALUE                     
                                            'QUANTITY'.                         
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  FILLER                       PIC X(11) VALUE                     
                                            'AREA NUMBER'.                      
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  FILLER                       PIC X(08) VALUE                     
                                            'QUANTITY'.                         
           05  FILLER                       PIC X(37) VALUE SPACE.              
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                       PIC X(03) VALUE SPACE.              
           05  DL-AREA-1                    PIC ZZZZZZZZ9.                      
           05  DL-AREA-1-ALPHA REDEFINES DL-AREA-1          PIC X(09).          
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  DL-QTY-1                     PIC ZZZZZZZZ9.                      
           05  DL-QTY-1-ALPHA REDEFINES DL-QTY-1            PIC X(09).          
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  DL-AREA-2                    PIC ZZZZZZZZ9.                      
           05  DL-AREA-2-ALPHA REDEFINES DL-AREA-2          PIC X(09).          
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  DL-QTY-2                     PIC ZZZZZZZZ9.                      
           05  DL-QTY-2-ALPHA REDEFINES DL-QTY-2            PIC X(09).          
           05  FILLER                       PIC X(08) VALUE SPACE.              
           05  DL-AREA-3                    PIC ZZZZZZZZ9.                      
           05  DL-AREA-3-ALPHA REDEFINES DL-AREA-3          PIC X(09).          
           05  FILLER                       PIC X(07) VALUE SPACE.              
           05  DL-QTY-3                     PIC ZZZZZZZZ9.                      
           05  DL-QTY-3-ALPHA REDEFINES DL-QTY-3            PIC X(09).          
           05  FILLER                       PIC X(38) VALUE SPACE.              
                                                                                
       01  ST-SUB-TOTAL-LINE.                                                   
           05  FILLER                       PIC X(01) VALUE SPACE.              
           05  FILLER                       PIC X(15) VALUE                     
                                            'TOTAL QUANTITY:'.                  
           05  FILLER                       PIC X(69) VALUE SPACE.              
           05  ST-TOTAL-QTY                 PIC ZZZZZZZZ9.                      
           05  FILLER                       PIC X(38) VALUE SPACE.              
                                                                                
      *****************************************************************         
      * RECORD LAYOUT FOR STORE EXTRACT FILE                                    
      *****************************************************************         
                                                                                
           COPY INRD135.                                                        
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-INKRP148.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-EXTRACT                                         
               UNTIL PS-END-OF-STORE-EXTRACT.                                   
                                                                                
           PERFORM 8000-EOJ-PROCESS.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  STORE-EXTRACT-FILE                                       
                OUTPUT AREA-REPORT-FILE.                                        
                                                                                
JP0700     MOVE ZEROS                         TO PT-LINE-TABLE.                 
                                                                                
           PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
                                                                                
           MOVE IN135-STORE                TO PV-HOLD-STORE.                    
                                                                                
      *    BUILD STANDARD REPORT HEADING.                                       
                                                                                
           ACCEPT TD-TODAYS-DATE FROM DATE.                                     
           MOVE TD-YEAR    TO DP132O-RUN-YEAR.                                  
           MOVE TD-MONTH   TO DP132O-RUN-MONTH.                                 
           MOVE TD-DAY     TO DP132O-RUN-DAY.                                   
                                                                                
           ACCEPT TT-TODAYS-TIME FROM TIME.                                     
           MOVE TT-HOUR    TO DP132O-RUN-HOUR.                                  
           MOVE TT-MINUTE  TO DP132O-RUN-MINUTE.                                
                                                                                
           MOVE 'INKRP148' TO DP132O-PROGRAM-NAME.                              
           MOVE 1          TO DP132O-REPORT-NUMBER.                             
                                                                                
                                                                                
       2000-PROCESS-EXTRACT.                                                    
JP0700                                                                          
JP0700     PERFORM                                                              
JP0700         VARYING PV-LINE-SUB FROM 1 BY 1                                  
JP0700             UNTIL PV-LINE-SUB > PC-TABLE-MAX                             
JP0700         MOVE SPACES           TO PT-LINE-AREA-ALPHA(PV-LINE-SUB)         
JP0700                                  PT-LINE-QTY-ALPHA(PV-LINE-SUB)          
JP0700     END-PERFORM.                                                         
JP0700                                                                          
JP0700     PERFORM 2010-FILL-TABLE                                              
JP0700         VARYING PV-LINE-SUB FROM 1 BY 1                                  
JP0700             UNTIL PV-LINE-SUB > PC-TABLE-MAX OR                          
JP0700                 IN135-STORE > PV-HOLD-STORE OR                           
JP0700                     PS-END-OF-STORE-EXTRACT.                             
JP0700                                                                          
JP0700     PERFORM 7200-PRINT-PAGE-HEADINGS.                                    
JP0700     MOVE 1                             TO PV-LINE-SUB                    
JP0700                                           PV-TEMP-LINE-SUB.              
JP0700     SET PS-RESET-PAGE-PRINTED          TO TRUE.                          
JP0700     SET PS-RESET-PRINT-STORE-TOTALS    TO TRUE.                          
JP0700     SET PS-RESET-STORE-TOTALS-PRINTED  TO TRUE.                          
JP0700                                                                          
JP0700     IF  PS-END-OF-STORE-EXTRACT                                          
JP0700         PERFORM 2020-PROCESS-DETAIL-LINES                                
JP0700             UNTIL PS-PAGE-PRINTED                                        
JP0700         IF  PS-STORE-TOTALS-PRINTED                                      
JP0700             NEXT SENTENCE                                                
JP0700         ELSE                                                             
RP0700             PERFORM 7200-PRINT-PAGE-HEADINGS                             
RP0700             PERFORM 7800-PRINT-STORE-TOTAL                               
JP0700         END-IF                                                           
JP0700     ELSE                                                                 
JP0700         IF  IN135-STORE = PV-HOLD-STORE                                  
JP0700             PERFORM 2020-PROCESS-DETAIL-LINES                            
JP0700                 UNTIL PS-PAGE-PRINTED                                    
JP0700         ELSE                                                             
JP0700             PERFORM 2020-PROCESS-DETAIL-LINES                            
JP0700                 UNTIL PS-PAGE-PRINTED                                    
JP0700             IF  PS-STORE-TOTALS-PRINTED                                  
JP0700                 CONTINUE                                                 
JP0700             ELSE                                                         
RP0700                 PERFORM 7200-PRINT-PAGE-HEADINGS                         
RP0700                 PERFORM 7800-PRINT-STORE-TOTAL                           
JP0700             END-IF                                                       
JP0700             MOVE ZEROS                     TO PA-TOTAL-QTY               
JP0700             MOVE IN135-STORE               TO PV-HOLD-STORE              
JP0700         END-IF                                                           
JP0700     END-IF.                                                              
JP0700                                                                          
JP0700******************************************************************        
JP0700* 2010-FILL-TABLE THIS PARAGRAPH WILL FILL THE LINE TABLE WITH A          
JP0700*                 FULL PAGE OF DATA                                       
JP0700******************************************************************        
JP0700                                                                          
JP0700 2010-FILL-TABLE.                                                         
JP0700                                                                          
JP0700     ADD IN135-QTY                 TO PA-TOTAL-QTY.                       
JP0700     MOVE IN135-QTY                TO PT-LINE-QTY(PV-LINE-SUB).           
JP0700     MOVE IN135-AREA               TO PT-LINE-AREA(PV-LINE-SUB).          
JP0700     MOVE PV-LINE-SUB              TO PV-HOLD-LINE-SUB.                   
JP0700                                                                          
JP0700     PERFORM 4000-READ-STORE-EXTRACT-FILE.                                
JP0700                                                                          
JP0700******************************************************************        
JP0700* 2020-PROCESS-DETAIL-LINES THIS PARAGRAPH WILL FILL THE                  
JP0700*                           INDIVIDUAL DETAIL LINES FROM THE              
JP0700*                           LINE TABLE AND PRINT THEM ONE LINE            
JP0700*                           AT A TIME                                     
JP0700******************************************************************        
JP0700                                                                          
JP0700 2020-PROCESS-DETAIL-LINES.                                               
JP0700                                                                          
JP0700     IF  PS-STORE-TOTALS-PRINTED                                          
JP0700         NEXT SENTENCE                                                    
JP0700     ELSE                                                                 
JP0700         IF  PS-PRINT-STORE-TOTALS                                        
JP0700             SET PS-RESET-PRINT-STORE-TOTALS TO TRUE                      
JP0700             SET PS-STORE-TOTALS-PRINTED     TO TRUE                      
JP0700             PERFORM 7800-PRINT-STORE-TOTAL                               
JP0700         END-IF                                                           
JP0700     END-IF.                                                              
JP0700                                                                          
JP0700     IF  PT-LINE-AREA-ALPHA(PV-LINE-SUB) = SPACES                         
JP0700         IF  PS-STORE-TOTALS-PRINTED                                      
JP0700             CONTINUE                                                     
JP0700         ELSE                                                             
JP0700             SET PS-PRINT-STORE-TOTALS  TO TRUE                           
JP0700         END-IF                                                           
JP0700         MOVE SPACES                    TO DL-AREA-1-ALPHA                
JP0700                                           DL-QTY-1-ALPHA                 
JP0700     ELSE                                                                 
JP0700         MOVE PT-LINE-AREA(PV-LINE-SUB) TO DL-AREA-1                      
JP0700         MOVE PT-LINE-QTY(PV-LINE-SUB)  TO DL-QTY-1                       
JP0700     END-IF.                                                              
JP0700                                                                          
JP0700     ADD PC-62                      TO PV-LINE-SUB.                       
JP0700                                                                          
JP0700     IF  PT-LINE-AREA-ALPHA(PV-LINE-SUB) = SPACES                         
JP0700         MOVE SPACES                TO DL-AREA-2-ALPHA                    
JP0700                                       DL-QTY-2-ALPHA                     
JP0700     ELSE                                                                 
JP0700         MOVE PT-LINE-AREA(PV-LINE-SUB) TO DL-AREA-2                      
JP0700         MOVE PT-LINE-QTY(PV-LINE-SUB)  TO DL-QTY-2                       
JP0700     END-IF.                                                              
JP0700                                                                          
JP0700     ADD PC-62                      TO PV-LINE-SUB.                       
JP0700                                                                          
JP0700     IF  PT-LINE-AREA-ALPHA(PV-LINE-SUB) = SPACES                         
JP0700         MOVE SPACES                TO DL-AREA-3-ALPHA                    
JP0700                                       DL-QTY-3-ALPHA                     
JP0700     ELSE                                                                 
JP0700         MOVE PT-LINE-AREA(PV-LINE-SUB) TO DL-AREA-3                      
JP0700         MOVE PT-LINE-QTY(PV-LINE-SUB)  TO DL-QTY-3                       
JP0700     END-IF.                                                              
JP0700                                                                          
JP0700     PERFORM 7500-PRINT-DETAIL-LINE.                                      
JP0700                                                                          
JP0700     IF  PV-LINE-SUB = PC-TABLE-MAX                                       
JP0700         SET PS-PAGE-PRINTED        TO TRUE                               
JP0700     ELSE                                                                 
JP0700         MOVE PV-TEMP-LINE-SUB      TO PV-LINE-SUB                        
JP0700         ADD  PC-1                  TO PV-LINE-SUB                        
JP0700                                       PV-TEMP-LINE-SUB                   
JP0700     END-IF.                                                              
JP0700                                                                          
      ******************************************************************        
      * READ STORE FILE.                                                        
      *                                                                         
      ******************************************************************        
                                                                                
       4000-READ-STORE-EXTRACT-FILE.                                            
                                                                                
           READ STORE-EXTRACT-FILE INTO INRD135-PIECE-EXTR-REC                  
               AT END SET PS-END-OF-STORE-EXTRACT TO TRUE.                      
                                                                                
                                                                                
      ******************************************************************        
      * PRINT THE PAGE HEADINGS.                                                
      ******************************************************************        
       7200-PRINT-PAGE-HEADINGS.                                                
                                                                                
           ADD  1           TO PV-PAGE-CNT.                                     
           MOVE PV-PAGE-CNT TO DP132O-PAGE-NUMBER.                              
                                                                                
           WRITE AREA-REPORT-RECORD                                             
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE AREA-REPORT-RECORD                                             
               FROM HL2-HEADING-LINE-2                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE PV-HOLD-STORE  TO HL3-STORE-NBR.                                
           WRITE AREA-REPORT-RECORD                                             
               FROM HL3-HEADING-LINE-3                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE AREA-REPORT-RECORD                                             
               FROM HL4-HEADING-LINE-4                                          
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 4                             TO PA-LINE-CNT.                   
                                                                                
       7500-PRINT-DETAIL-LINE.                                                  
                                                                                
           WRITE AREA-REPORT-RECORD                                             
               FROM DL-DETAIL-LINE                                              
               AFTER ADVANCING 1 LINE.                                          
           ADD 1 TO PA-LINE-CNT.                                                
           MOVE SPACES                        TO DL-DETAIL-LINE.                
                                                                                
       7800-PRINT-STORE-TOTAL.                                                  
           MOVE PA-TOTAL-QTY TO ST-TOTAL-QTY.                                   
                                                                                
           WRITE AREA-REPORT-RECORD                                             
               FROM ST-SUB-TOTAL-LINE                                           
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           ADD 2 TO PA-LINE-CNT.                                                
                                                                                
       8000-EOJ-PROCESS.                                                        
                                                                                
           CLOSE STORE-EXTRACT-FILE                                             
                 AREA-REPORT-FILE.                                              
                                                                                
