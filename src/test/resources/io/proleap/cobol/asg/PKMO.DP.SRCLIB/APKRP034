000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    APKRP034.                                                 
000300 AUTHOR.        ROBIN BERNOSKI.                                           
000400 DATE-WRITTEN.  NOVEMBER 2000.                                            
000500 DATE-COMPILED.                                                           
000600*****************************************************************         
000700*                SUM OF FREIGHT COST BY TRANCODE                *         
000800*****************************************************************         
000900*    COBOL II                                                   *         
001000*****************************************************************         
001100*    THIS PROGRAM PRODUCES A REPORT WHICH SHOWS THE SUM OF      *         
001200*    THE FREIGHT COST BY TRANCODE.  ONLY SELECT TRANCODES       *         
001300*    ARE REPORTED ON.                                           *         
001400*                                                               *         
001500* INPUT:                                                        *         
001600*  1. SORTED MONTH END ADJUSTMENT JOURNAL EXTRACT (APRD046)     *         
001700*                                                               *         
001800* OUTPUT:                                                       *         
001900*  1. REPORT - SUM OF FREIGHT BY TRANCODE REPORT                *         
002000*                                                               *         
002100*****************************************************************         
002200*****************************************************************         
002300* MAINTENANCE LOG:                                              *         
002400*                                                               *         
002500* 05/16/2002                                                    *         
002600*   CHANGE MADE:  NI-SKU CHANGES.  RECOMPILE FROM PRODUCTION    *         
002600*   TO INCORPORATE CHANGES TO COPYBOOK APRD046.                 *         
002700* MADE BY:  KEVIN CATLIN                   WR/IR #: 21278       *         
002800*                                                               *         
003000*****************************************************************         
003100 ENVIRONMENT DIVISION.                                                    
003200 CONFIGURATION SECTION.                                                   
003300 SOURCE-COMPUTER.    IBM-3090.                                            
003400 OBJECT-COMPUTER.    IBM-3090.                                            
003500                                                                          
003600 INPUT-OUTPUT SECTION.                                                    
003700 FILE-CONTROL.                                                            
003800     SELECT FREIGHT-ADJSTMNT-FILE         ASSIGN TO INP01.                
003900     SELECT REPORT-FILE                   ASSIGN TO RPT01.                
004000                                                                          
004100 DATA DIVISION.                                                           
004200 FILE SECTION.                                                            
004300                                                                          
004400 FD  FREIGHT-ADJSTMNT-FILE                                                
004500     LABEL RECORDS ARE OMITTED                                            
004600     RECORDING MODE IS F                                                  
004700     RECORD CONTAINS 250 CHARACTERS                                       
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900 01  FREIGHT-ADJSTMNT-REC             PIC  X(250).                        
005000                                                                          
005100 FD  REPORT-FILE                                                          
005200     LABEL RECORDS ARE STANDARD                                           
005300     RECORDING MODE IS F                                                  
005400     BLOCK CONTAINS 0 RECORDS.                                            
005500 01  REPORT-RECORD                    PIC  X(132).                        
005600                                                                          
005700 EJECT                                                                    
005800 WORKING-STORAGE SECTION.                                                 
005900                                                                          
006000 01  PV-PROGRAM-VARIABLES.                                                
006100     05  FILLER                  PIC  X(30)  VALUE                        
006200         '** BEGINING OF APKRP034 W/S **'.                                
006300     05  PV-PROGRAM-NAME           PIC  X(08)  VALUE 'APKRP034'.          
006400     05  PV-PAGE-CNT               PIC  9(03)  VALUE ZERO.                
006500     05  PV-MAX-LINES              PIC S9(03)  VALUE +60 COMP-3.          
006600     05  PV-LINE-CNT               PIC S9(03)  VALUE +99 COMP-3.          
006700     05  PV-READ-CNT               PIC  9(07)  VALUE ZERO.                
006800     05  PV-WRITE-CNT              PIC  9(07)  VALUE ZERO.                
006900     05  PV-CURRENT-DATE.                                                 
007000         10  PV-CURRENT-YEAR       PIC  9(02)  VALUE ZERO.                
007100         10  PV-CURRENT-MONTH      PIC  9(02)  VALUE ZERO.                
007200         10  PV-CURRENT-DAY        PIC  9(02)  VALUE ZERO.                
007300     05  PV-CURRENT-TIME.                                                 
007400         10  PV-CURRENT-HOUR       PIC  9(02)  VALUE ZERO.                
007500         10  PV-CURRENT-MINUTE     PIC  9(02)  VALUE ZERO.                
007600         10  FILLER                PIC  9(02)  VALUE ZERO.                
007700     05  PV-DATE.                                                         
007800         10  PV-MM                 PIC  X(02).                            
007900         10  FILLER                PIC  X(01)  VALUE '/'.                 
008000         10  PV-DD                 PIC  X(02).                            
008100         10  FILLER                PIC  X(01)  VALUE '/'.                 
008200         10  PV-YY                 PIC  X(02).                            
008300     05  PV-HOLD-FIELDS.                                                  
008400         10  PV-HOLD-PSTG-FMN-NBR  PIC  X(02)  VALUE SPACE.               
008500         10  PV-HOLD-PSTG-FYR-NBR  PIC  X(04)  VALUE SPACE.               
008600         10  PV-HOLD-MONTHLY-TOT   PIC S9(11)V99 VALUE ZERO.              
008700         10  PV-HOLD-GRAND-TOTAL   PIC S9(11)V99 VALUE ZERO.              
008800                                                                          
008900 01  PS-PROGRAM-SWITCHES.                                                 
009000     05  PS-END-OF-FILE-SW         PIC X(01) VALUE 'N'.                   
009100         88  END-OF-FILE                     VALUE 'Y'.                   
009200         88  NOT-END-OF-FILE                 VALUE 'N'.                   
009300                                                                          
009400 EJECT                                                                    
009500*----------------------------------------------------------------*        
009600*  ADJUSTMENT JOURNAL MERCHANDISE EXTRACT LAYOUT                 *        
009700*----------------------------------------------------------------*        
009800     COPY APRD046.                                                        
009900 EJECT                                                                    
010000*----------------------------------------------------------------*        
010100*  STANDARD HEADER LAYOUT                                        *        
010200*----------------------------------------------------------------*        
010300     COPY DPWS132O.                                                       
010400                                                                          
010500 01  H1-HEADER1.                                                          
010600     05  FILLER                  PIC  X(50) VALUE SPACE.                  
010700     05  H1-TITLE                PIC  X(31) VALUE                         
010800                           'SUM OF FREIGHT COST BY TRANCODE'.             
010900     05  FILLER                  PIC  X(51) VALUE SPACE.                  
011000                                                                          
011100 EJECT                                                                    
011200 01  DH-DETAIL-HEADER.                                                    
011300     05  FILLER                  PIC  X(26)  VALUE SPACE.                 
011400     05  FILLER                  PIC  X(09)  VALUE 'TRAN CODE'.           
011500     05  FILLER                  PIC  X(22)  VALUE SPACE.                 
011600     05  FILLER                  PIC  X(12)  VALUE 'FREIGHT COST'.        
011700     05  FILLER                  PIC  X(66)  VALUE SPACE.                 
011800                                                                          
011900 01  TH-MONTHLY-TOTAL-LINE.                                               
012000     05  FILLER                  PIC  X(25)  VALUE SPACE.                 
012100     05  FILLER                  PIC  X(10)  VALUE 'TOTALS FOR'.          
012200     05  FILLER                  PIC  X(01)  VALUE SPACE.                 
012300     05  TH-MONTH                PIC  X(02)  VALUE SPACE.                 
012400     05  FILLER                  PIC  X(01)  VALUE '/'.                   
012500     05  TH-YEAR                 PIC  X(04)  VALUE SPACE.                 
012600     05  FILLER                  PIC  X(14)  VALUE SPACE.                 
012700     05  TH-MONTHLY-TOTAL-AMT    PIC  ---,---,---.99 VALUE SPACE.         
012800     05  FILLER                  PIC  X(71)  VALUE SPACE.                 
012900                                                                          
013000 01  GT-GRAND-TOTAL-LINE.                                                 
013100     05  FILLER                  PIC  X(25)  VALUE SPACE.                 
013200     05  FILLER                  PIC  X(11)  VALUE 'GRAND TOTAL'.         
013300     05  FILLER                  PIC  X(21)  VALUE SPACE.                 
013400     05  GT-GRAND-TOTAL-AMT      PIC  ---,---,---.99 VALUE SPACE.         
013500     05  FILLER                  PIC  X(81)  VALUE SPACE.                 
013600                                                                          
013700 01  ND-NO-DATA-LINE.                                                     
013800     05  FILLER                  PIC  X(42)  VALUE SPACE.                 
013900     05  FILLER                  PIC  X(80)  VALUE                        
014000         'NO ELIGIBLE TRAN CODES TO REPORT ON'.                           
014100                                                                          
014200                                                                          
014300 01  DL-DETAIL-LINE.                                                      
014400     05  FILLER                  PIC  X(30)  VALUE SPACE.                 
014500     05  DL-TRN-CDE              PIC  X(02)  VALUE SPACE.                 
014600     05  FILLER                  PIC  X(25)  VALUE SPACE.                 
014700     05  DL-FREIGHT-AMT          PIC  ---,---,---.99                      
014800                                             VALUE SPACE.                 
014900     05  FILLER                  PIC  X(61)  VALUE SPACE.                 
015000                                                                          
015100                                                                          
015200 01  RE-REPORT-END-LINE.                                                  
015300     05  FILLER                  PIC  X(49)  VALUE SPACE.                 
015400     05  FILLER                  PIC  X(28)  VALUE                        
015500         '***  END OF REPORT APKRP034-'.                                  
015600     05  RE-REPORT-NUMBER        PIC  X(01)  VALUE '1'.                   
015700     05  FILLER                  PIC  X(05)  VALUE '  ***'.               
015800     05  FILLER                  PIC  X(49)  VALUE SPACE.                 
015900 EJECT                                                                    
016000*----------------------------------------------------------------*        
016100*  ABEND DATA AREAS                                              *        
016200*----------------------------------------------------------------*        
016300 01  ABEND-CODE                  PIC S9(04)  VALUE ZEROS                  
016400                                             COMP SYNC.                   
016500     88  AC-BAD-DATA                         VALUE +4003.                 
016600     88  AC-DB2-ERROR                        VALUE +4013.                 
016700                                                                          
016800                                                                          
016900 01  ABEND-AREAS.                                                         
017000     05  AA-ABEND-LIT            PIC  X(40)  VALUE                        
017100             '*****       ABEND'.                                         
017200     05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                        
017300             '*****   PROGRAM: APKRP034'.                                 
017400     05  AA-PARAGRAPH-LIT.                                                
017500         10  FILLER              PIC  X(17)  VALUE                        
017600             '***** PARAGRAPH: '.                                         
017700         10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.                
017800     05  AA-MESSAGE-LINE.                                                 
017900         10  FILLER              PIC  X(06)  VALUE '*****'.               
018000         10  AA-MESSAGE          PIC  X(127) VALUE SPACES.                
018100     05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                        
018200             '*****    DB2 ERROR'.                                        
018300     05  AA-DB2-OPERATION-LIT.                                            
018400         10  FILLER              PIC  X(17)  VALUE                        
018500             '***** OPERATION: '.                                         
018600         10  AA-DB2-OPERATION.                                            
018700             15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.                
018800             15  AA-DB2-OP-2     PIC  X(25)  VALUE SPACES.                
018900     05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.                
019000     05  AA-DB2-TABLE-2          PIC  X(08)  VALUE SPACES.                
019100     05  AA-DB2-TABLE-3          PIC  X(08)  VALUE SPACES.                
019200                                                                          
019300                                                                          
019400     COPY DPWS004.                                                        
019500 01  FILLER                      PIC  X(25)  VALUE                        
019600     '** END OF APKRP034 W/S **'.                                         
019700 EJECT                                                                    
019800 PROCEDURE DIVISION.                                                      
019900 A100-MAIN.                                                               
020000                                                                          
020100     PERFORM B100-INITIALIZE.                                             
020200                                                                          
020300     PERFORM B200-PROCESS-TRAN-CODES                                      
020400         UNTIL END-OF-FILE.                                               
020500                                                                          
020600     PERFORM B300-END-PROGRAM.                                            
020700                                                                          
020800     GOBACK.                                                              
020900 EJECT                                                                    
021000*----------------------------------------------------------------*        
021100*    INITIALIZATION PROCESSING                                   *        
021200*----------------------------------------------------------------*        
021300 B100-INITIALIZE.                                                         
021400                                                                          
021500     OPEN INPUT  FREIGHT-ADJSTMNT-FILE                                    
021600          OUTPUT REPORT-FILE.                                             
021700                                                                          
021800     ACCEPT PV-CURRENT-DATE FROM DATE.                                    
021900     MOVE PV-CURRENT-MONTH            TO DP132O-RUN-MONTH.                
022000     MOVE PV-CURRENT-DAY              TO DP132O-RUN-DAY.                  
022100     MOVE PV-CURRENT-YEAR             TO DP132O-RUN-YEAR.                 
022200                                                                          
022300     ACCEPT PV-CURRENT-TIME FROM TIME.                                    
022400     MOVE PV-CURRENT-HOUR             TO DP132O-RUN-HOUR.                 
022500     MOVE PV-CURRENT-MINUTE           TO DP132O-RUN-MINUTE.               
022600     MOVE PV-PROGRAM-NAME             TO DP132O-PROGRAM-NAME.             
022700     MOVE '1'                         TO DP132O-REPORT-NUMBER.            
022800                                                                          
022900     PERFORM R100-READ-FREIGHT-ADJSTMT-FILE.                              
023000     MOVE AP046-PSTG-FMN-NBR TO TH-MONTH.                                 
023100     MOVE AP046-PSTG-FYR-NBR TO TH-YEAR.                                  
023200     MOVE AP046-PSTG-FMN-NBR TO PV-HOLD-PSTG-FMN-NBR.                     
023300                                                                          
023400     IF END-OF-FILE                                                       
023500         PERFORM W100-WRITE-HEADERS                                       
023600         WRITE REPORT-RECORD FROM ND-NO-DATA-LINE AFTER 1                 
023700     END-IF.                                                              
023800 EJECT                                                                    
023900*----------------------------------------------------------------*        
024000*    LOOPS THROUGH INPUT FILE AND WRITES OUT THE SUM OF FREIGHT  *        
024100*    COST REPORT BASED UPON THE PSTG MONTH.                      *        
024200*----------------------------------------------------------------*        
024300 B200-PROCESS-TRAN-CODES.                                                 
024400                                                                          
024500     IF AP046-PSTG-FMN-NBR = PV-HOLD-PSTG-FMN-NBR                         
024600        MOVE AP046-PSTG-FMN-NBR TO TH-MONTH                               
024700        MOVE AP046-PSTG-FYR-NBR TO TH-YEAR                                
024800        PERFORM C100-FILL-REPORT-FIELDS                                   
024900        PERFORM W200-WRITE-DETAIL                                         
025000     ELSE                                                                 
025100        MOVE PV-HOLD-PSTG-FMN-NBR    TO TH-MONTH                          
025200        MOVE PV-HOLD-PSTG-FYR-NBR    TO TH-YEAR                           
025300        MOVE PV-HOLD-MONTHLY-TOT     TO TH-MONTHLY-TOTAL-AMT              
025400        MOVE ZEROS                   TO PV-HOLD-MONTHLY-TOT               
025500        PERFORM W300-WRITE-MONTHLY-TOTALS                                 
025600        PERFORM C100-FILL-REPORT-FIELDS                                   
025700        PERFORM W200-WRITE-DETAIL                                         
025800     END-IF.                                                              
025900                                                                          
026000     MOVE AP046-PSTG-FMN-NBR TO PV-HOLD-PSTG-FMN-NBR.                     
026100     MOVE AP046-PSTG-FYR-NBR TO PV-HOLD-PSTG-FYR-NBR.                     
026200     PERFORM R100-READ-FREIGHT-ADJSTMT-FILE.                              
026300                                                                          
026400 EJECT                                                                    
026500*----------------------------------------------------------------*        
026600*    FINISHES REPORT, CLOSES FILES, DISPLAYS COUNTS.             *        
026700*----------------------------------------------------------------*        
026800 B300-END-PROGRAM.                                                        
026900                                                                          
027000     MOVE PV-HOLD-MONTHLY-TOT TO TH-MONTHLY-TOTAL-AMT.                    
027100     MOVE PV-HOLD-GRAND-TOTAL TO GT-GRAND-TOTAL-AMT.                      
027200     PERFORM W300-WRITE-MONTHLY-TOTALS.                                   
027300     PERFORM W400-WRITE-GRAND-TOTALS.                                     
027400                                                                          
027500     WRITE REPORT-RECORD FROM RE-REPORT-END-LINE AFTER 2.                 
027600                                                                          
027700     CLOSE FREIGHT-ADJSTMNT-FILE                                          
027800           REPORT-FILE.                                                   
027900                                                                          
028000     DISPLAY AA-PROGRAM-LIT.                                              
028100     DISPLAY 'RECORDS READ    = ' PV-READ-CNT.                            
028200     DISPLAY 'RECORDS WRITTEN = ' PV-WRITE-CNT.                           
028300                                                                          
028400 EJECT                                                                    
028500*----------------------------------------------------------------*        
028600*    PREPARES THE FIELDS FOR THE SUM OF FREIGHT COST BY          *        
028700*    TRANCODE REPORT                                             *        
028800*----------------------------------------------------------------*        
028900 C100-FILL-REPORT-FIELDS.                                                 
029000                                                                          
029100     MOVE SPACES               TO DL-DETAIL-LINE.                         
029200                                                                          
029300     MOVE AP046-AP-TRN-CDE     TO DL-TRN-CDE.                             
029400     MOVE AP046-FRGT-AMT       TO DL-FREIGHT-AMT.                         
029500                                                                          
029600     ADD  AP046-FRGT-AMT       TO PV-HOLD-MONTHLY-TOT.                    
029700     ADD  AP046-FRGT-AMT       TO PV-HOLD-GRAND-TOTAL.                    
029800                                                                          
029900 EJECT                                                                    
030000*----------------------------------------------------------------*        
030100*  READS A RECORD FROM THE SORTED MONTH END ADJUSTMENT JOURNAL   *        
030200*  EXTRACT FILE.                                                 *        
030300*----------------------------------------------------------------*        
030400 R100-READ-FREIGHT-ADJSTMT-FILE.                                          
030500                                                                          
030600     READ FREIGHT-ADJSTMNT-FILE INTO AP046-EXTRACT-FILE                   
030700         AT END SET END-OF-FILE TO TRUE.                                  
030800                                                                          
030900     IF NOT-END-OF-FILE                                                   
031000         ADD +1 TO PV-READ-CNT                                            
031100     END-IF.                                                              
031200                                                                          
031300 EJECT                                                                    
031400*----------------------------------------------------------------*        
031500*    WRITES THE HEADER LINES.                                    *        
031600*----------------------------------------------------------------*        
031700 W100-WRITE-HEADERS.                                                      
031800                                                                          
031900     ADD 1                       TO PV-PAGE-CNT.                          
032000                                                                          
032100     MOVE PV-PAGE-CNT            TO DP132O-PAGE-NUMBER.                   
032200                                                                          
032300     WRITE REPORT-RECORD                                                  
032400           FROM DP132O-STANDRD-HEADER-132-COLS AFTER PAGE.                
032500     WRITE REPORT-RECORD         FROM H1-HEADER1       AFTER 1.           
032600     WRITE REPORT-RECORD         FROM DH-DETAIL-HEADER AFTER 2.           
032700                                                                          
032800     MOVE SPACES TO REPORT-RECORD.                                        
032900     WRITE REPORT-RECORD                               AFTER 1.           
033000                                                                          
033100     MOVE 4                      TO PV-LINE-CNT.                          
033200                                                                          
033300 EJECT                                                                    
033400*----------------------------------------------------------------*        
033500*    WRITES THE DETAIL LINE.                                     *        
033600*----------------------------------------------------------------*        
033700 W200-WRITE-DETAIL.                                                       
033800                                                                          
033900     IF PV-LINE-CNT > PV-MAX-LINES                                        
034000        PERFORM W100-WRITE-HEADERS                                        
034100     END-IF.                                                              
034200                                                                          
034300     WRITE REPORT-RECORD FROM DL-DETAIL-LINE AFTER 1.                     
034400     ADD 1  TO PV-LINE-CNT.                                               
034500     ADD 1  TO PV-WRITE-CNT.                                              
034600                                                                          
034700 EJECT                                                                    
034800*----------------------------------------------------------------*        
034900*    WRITES THE MONTHLY TOTAL LINE.                              *        
035000*----------------------------------------------------------------*        
035100 W300-WRITE-MONTHLY-TOTALS.                                               
035200                                                                          
035300     IF PV-LINE-CNT > PV-MAX-LINES                                        
035400        PERFORM W100-WRITE-HEADERS                                        
035500     END-IF.                                                              
035600                                                                          
035700     WRITE REPORT-RECORD FROM TH-MONTHLY-TOTAL-LINE AFTER 2.              
035800     ADD 1  TO PV-LINE-CNT.                                               
035900     ADD 1  TO PV-WRITE-CNT.                                              
036000                                                                          
036100 EJECT                                                                    
036200*----------------------------------------------------------------*        
036300*    WRITES THE GRAND TOTAL LINE.                                *        
036400*----------------------------------------------------------------*        
036500 W400-WRITE-GRAND-TOTALS.                                                 
036600                                                                          
036700     IF PV-LINE-CNT > PV-MAX-LINES                                        
036800        PERFORM W100-WRITE-HEADERS                                        
036900     END-IF.                                                              
037000                                                                          
037100     WRITE REPORT-RECORD FROM GT-GRAND-TOTAL-LINE AFTER 1.                
037200     ADD 1  TO PV-LINE-CNT.                                               
037300     ADD 1  TO PV-WRITE-CNT.                                              
037400                                                                          
037500*----------------------------------------------------------------*        
037600*    ABEND ROUTINE                                               *        
037700*----------------------------------------------------------------*        
037800 Z999-ABEND.                                                              
037900                                                                          
038000     CLOSE FREIGHT-ADJSTMNT-FILE                                          
038100           REPORT-FILE.                                                   
038200                                                                          
038300     DISPLAY AA-ABEND-LIT.                                                
038400     DISPLAY AA-PROGRAM-LIT.                                              
038500     DISPLAY AA-PARAGRAPH-LIT.                                            
038600     DISPLAY AA-MESSAGE-LINE.                                             
038700     CALL 'ILBOABN0' USING ABEND-CODE.                                    
038800 EJECT                                                                    
