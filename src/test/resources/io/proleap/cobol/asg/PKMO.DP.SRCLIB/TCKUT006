      ***************************************************************** 00010000
       IDENTIFICATION DIVISION.                                         00020000
      ***************************************************************** 00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT006.                                00050000
       AUTHOR.                 J MYERS.                                 00060000
       INSTALLATION.           KOHLS.                                   00070000
       DATE-WRITTEN            04/2002.                                 00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ***************************************************************** 00110000
      *  PURPOSE:                                                       00120000
      *  CREATE AN UPDATED COSA UNMATCHED.  FOR ANY RECORDS THAT MATCH  00130000
      *  THE ADJ/MAF UPDATE FILE TO COSA, REMOVE THOSE RECORDS FROM THE 00140000
      *  UNMATCHED COSA FILE.                                           00150000
W10849***************************************************************** 00150100
W10849* REQUIREMENT   : MATCHING THE 4 DIGIT OF THE DEBIT CARD          00151000
W10849* PROJECT ID    : CHG0139454                                      00152000
W10849* AUTHOR        : ACCENTURE                                       00153000
W10849* DATES CHANGED : 05/18/2016                                      00154000
W10849* CHANGES MADE  : WS-MAT-CARD-NBR AND WS-COSA-CARD-NBR ARE CHANGED00156000
W10849*                 TO STORE LAST 4 DIGIT.                          00157000
W10849*                 PV-CARD-NBR-RJ IS DECLARED TO STORE THE CARD#.  00158000
W10849*                 2000-READ-MATCH-FILE & 3000-READ-COSA-FILE PARA 00159000
W10849*                 ARE CHANGED TO MOVE THE LAST 4 DIGIT TO THE KEYS00159100
052623******************************************************************00159200
052623* REQUIREMENT   : FIX UPDATE FILE LENGTH TO BE 100 BYTES          00159300
052623* PROJECT ID    : CHG0289075                                      00159400
052623* AUTHOR        : ACCENTURE                                       00159500
052623* DATES CHANGED : 05/26/2023                                      00159600
052623* CHANGES MADE  : UPDATE FILE LENGTH TO 100 BYTES                 00159700
      ***************************************************************** 00160000
       ENVIRONMENT DIVISION.                                            00170000
      ***************************************************************** 00180000
                                                                        00190000
       CONFIGURATION SECTION.                                           00200000
                                                                        00210000
       SOURCE-COMPUTER.        IBM-3090.                                00220000
                                                                        00230000
       OBJECT-COMPUTER.        IBM-3090.                                00240000
                                                                        00250000
       INPUT-OUTPUT SECTION.                                            00260000
                                                                        00270000
       FILE-CONTROL.                                                    00280000
                                                                        00290000
           SELECT MATCHED-UPDT-FILE                                     00320100
               ASSIGN TO INP01.                                         00320200
                                                                        00320300
           SELECT INPUT-UNMATCH-FILE                                    00320400
               ASSIGN TO INP02.                                         00320500
                                                                        00320600
           SELECT OUTPUT-UNMATCH-FILE                                   00320700
               ASSIGN TO OUT01.                                         00320800
                                                                        00326000
      ******************************************************************00327000
       DATA DIVISION.                                                   00328000
      ******************************************************************00329000
                                                                        00330000
       FILE SECTION.                                                    00340000
                                                                        00350000
       FD  MATCHED-UPDT-FILE                                            00430000
           RECORDING MODE IS F                                          00440000
           BLOCK CONTAINS 0 RECORDS                                     00450001
052623     RECORD CONTAINS 100 CHARACTERS.                              00451001
                                                                        00460000
052623 01  MATCHED-UPDT-REC            PIC X(100).                      00470000
                                                                        00480000
       FD  INPUT-UNMATCH-FILE                                           00490000
           RECORDING MODE IS F                                          00491000
           BLOCK CONTAINS 0 RECORDS.                                    00492000
                                                                        00493000
       01  INPUT-UNMATCH-REC           PIC X(69).                       00494000
                                                                        00495000
       FD  OUTPUT-UNMATCH-FILE                                          00496000
           RECORDING MODE IS F                                          00497000
           LABEL RECORDS ARE STANDARD                                   00498000
           BLOCK CONTAINS 0 RECORDS                                     00499000
           RECORD CONTAINS  69 CHARACTERS.                              00500000
                                                                        00510000
       01  OUTPUT-UNMATCH-REC            PIC X(69).                     00520000
                                                                        00530000
      ******************************************************************00590000
       WORKING-STORAGE SECTION.                                         00600000
      ******************************************************************00610000
       01  FILLER                            PIC X(50)  VALUE           00620000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT006 *** '.        00630000
                                                                        00640000
        01  PS-PROGRAM-SWITCHES.                                        00870000
           05  PS-END-OF-MATCH-SW            PIC X(01)  VALUE 'N'.      00891000
               88  PS-NOT-END-OF-MATCH-FILE             VALUE 'N'.      00892000
               88  PS-END-OF-MATCH-FILE                 VALUE 'Y'.      00893000
           05  PS-END-OF-COSA-SW             PIC X(01)  VALUE 'N'.      00894000
               88  PS-NOT-END-OF-COSA-FILE              VALUE 'N'.      00895000
               88  PS-END-OF-COSA-FILE                  VALUE 'Y'.      00896000
                                                                        00932000
      ******************************************************************00933000
      *   PROGRAM CONSTANTS                                            *00934000
      ******************************************************************00935000
       01  PC-CONSTANTS.                                                00936000
           05  PC-PROGRAM-NAME               PIC  X(08) VALUE           00940000
                                                       'TCKUT006'.      00950000
       01  WS-MATCHED-KEY.                                              00999400
           05  WS-MAT-STORE-NBR              PIC X(04).                 00999500
           05  WS-MAT-REG-NBR                PIC X(04).                 00999600
           05  WS-MAT-TRAN-NBR               PIC X(04).                 00999700
W10849     05  WS-MAT-CARD-NBR-LAST4         PIC X(04).                 00999900
                                                                        01000000
       01  WS-UNMATCHED-COSA-KEY.                                       01001000
           05  WS-COSA-STORE-NBR             PIC X(04).                 01001100
           05  WS-COSA-REG-NBR               PIC X(04).                 01001200
           05  WS-COSA-TRAN-NBR              PIC X(04).                 01001300
W10849     05  WS-COSA-CARD-NBR-LAST4        PIC X(04).                 01001500
                                                                        01001600
       01  PV-PROGRAM-VARIABLES.                                        01013000
           05  PV-PARAGRAPH-NAME             PIC  X(30) VALUE SPACE.    01018000
           05  PV-OPERATION-MSG-1            PIC  X(40) VALUE SPACE.    01019000
           05  PV-OPERATION-MSG-2            PIC  X(40) VALUE SPACE.    01020000
           05  PV-DB2-OPERATION              PIC  X(30) VALUE SPACE.    01030000
           05  PV-WRITE-CNT                  PIC  9(01) VALUE 0.        01040000
W10849     05  PV-CARD-NBR-RJ                PIC  X(19) JUST RIGHT.     01050000
                                                                        01316300
      ***************************************************************** 01317500
      *  COSA UNMATCHED FILE                                            01317600
      ******************************************************************01317700
           COPY TCWS002.                                                01317800
                                                                        01317900
      ***************************************************************** 01318000
      *  ADJ/MAF MATCHED UPDATE FILE                                    01319000
      ******************************************************************01320000
           COPY TCWS006.                                                01330000
                                                                        02410400
       PROCEDURE DIVISION.                                              02410500
                                                                        02410600
      ******************************************************************02410700
      * 0000-MAINLINE-PARAGRAPH                                         02410800
      ******************************************************************02410900
       0000-MAINLINE-PARAGRAPH.                                         02411000
                                                                        02412000
           PERFORM 1000-INITIALIZE-PROGRAM.                             02413000
           PERFORM 2000-READ-MATCH-FILE.                                02414000
           PERFORM 3000-READ-COSA-FILE.                                 02415000
                                                                        02416000
           PERFORM 4000-PROCESS                                         02417000
                 UNTIL PS-END-OF-MATCH-FILE  AND  PS-END-OF-COSA-FILE.  02418000
                                                                        02419000
           CLOSE MATCHED-UPDT-FILE                                      02470000
                 INPUT-UNMATCH-FILE                                     02480000
                 OUTPUT-UNMATCH-FILE.                                   02490000
                                                                        02530000
           GOBACK.                                                      02540000
                                                                        02550000
      ******************************************************************02560000
      * 1000-INITIALIZE-PROGRAM                                         02570000
      *     OPEN FILES,                                                 02580000
      ******************************************************************02590000
       1000-INITIALIZE-PROGRAM.                                         02600000
                                                                        02610000
           OPEN INPUT  MATCHED-UPDT-FILE                                02620000
                       INPUT-UNMATCH-FILE                               02630000
                OUTPUT OUTPUT-UNMATCH-FILE.                             02650000
                                                                        02680000
      ******************************************************************03223100
      * 2000-READ-INPUT-FILE                                            03223200
      *     READ THE MATCHED ADJ/MAF FILE                               03223300
      ******************************************************************03223400
       2000-READ-MATCH-FILE.                                            03223500
                                                                        03223600
           READ MATCHED-UPDT-FILE INTO TC006-DATA                       03223700
               AT END                                                   03223800
                   SET PS-END-OF-MATCH-FILE  TO TRUE                    03223900
           END-READ.                                                    03224000
                                                                        03224100
           IF  PS-END-OF-MATCH-FILE                                     03224200
               MOVE HIGH-VALUES        TO WS-MATCHED-KEY                03224300
           ELSE                                                         03224400
               MOVE TC006-STR-NBR      TO WS-MAT-STORE-NBR              03224500
               MOVE TC006-RGST-ID      TO WS-MAT-REG-NBR                03224600
               MOVE TC006-TRN-NBR      TO WS-MAT-TRAN-NBR               03224700
W10849*        MOVE TC006-CARD-NBR     TO WS-MAT-CARD-NBR               03225100
W10849         MOVE TC006-CARD-NBR     TO PV-CARD-NBR-RJ                03225200
W10849         MOVE PV-CARD-NBR-RJ(16:4) TO WS-MAT-CARD-NBR-LAST4       03225300
           END-IF.                                                      03225400
                                                                        03225500
      ******************************************************************03225600
      * 3000-READ-MAF-FILE                                              03225700
      *     READ THE COS UNMATCHED FILE                                 03225800
      ******************************************************************03225900
       3000-READ-COSA-FILE.                                             03226000
                                                                        03226100
           READ INPUT-UNMATCH-FILE  INTO TC002-DATA                     03226200
               AT END                                                   03227000
                   SET PS-END-OF-COSA-FILE TO TRUE                      03228000
      *     SET END OF FILE FOR MATCHED FILE ALSO                       03228100
                   SET PS-END-OF-MATCH-FILE  TO TRUE                    03228200
           END-READ.                                                    03229000
                                                                        03230000
           IF  PS-END-OF-COSA-FILE                                      03240000
               MOVE HIGH-VALUES        TO WS-UNMATCHED-COSA-KEY         03250000
           ELSE                                                         03251000
               MOVE 0                  TO PV-WRITE-CNT                  03251100
               MOVE TC002-STR-NBR      TO WS-COSA-STORE-NBR             03252000
               MOVE TC002-RGST-ID      TO WS-COSA-REG-NBR               03253000
               MOVE TC002-TRN-NBR      TO WS-COSA-TRAN-NBR              03254000
W10849*        MOVE TC002-CARD-NBR     TO WS-COSA-CARD-NBR              03260000
W10849         MOVE TC002-CARD-NBR     TO PV-CARD-NBR-RJ                03260100
W10849         MOVE PV-CARD-NBR-RJ(16:4) TO WS-COSA-CARD-NBR-LAST4      03260200
           END-IF.                                                      03261000
                                                                        03262000
      ***************************************************************** 03263000
      * 4000-PROCESS                                                    03264000
      *      MATCH PROCESS OF THE ADJ/MAF FILE AND COSA UNMATCHED       03265000
      *      KEYS ARE                                                   03266000
      *      WS-MAT-STORE-NBR          WS-COSA-STORE-NBR                03267000
      *      WS-MAT-REG-NBR            WS-COSA-REG-NBR                  03268000
      *      WS-MAT-TRAN-NBR           WS-COSA-TRAN-NBR                 03269000
      *      WS-MAT-CARD-NBR           WS-COSA-CARD-NBR                 03270000
      ***************************************************************** 03290000
        4000-PROCESS.                                                   03300000
                                                                        03320000
           IF WS-MATCHED-KEY   = WS-UNMATCHED-COSA-KEY                  03472000
      *  REMOVE THE UNMATCHED COSA RECORD FROM THE COSA FILE            03480000
               PERFORM 2000-READ-MATCH-FILE                             03490000
               PERFORM 3000-READ-COSA-FILE                              03500000
           ELSE                                                         03510000
               IF WS-MATCHED-KEY > WS-UNMATCHED-COSA-KEY                03511000
      *  WRITE THE COSA RECORD OUT TO THE UNMATCHED FILE                03512000
                   PERFORM 5000-WRITE-UNMATCHED-COSA                    03530000
                   PERFORM 3000-READ-COSA-FILE                          03541000
               ELSE                                                     03550000
      *  WRITE THE COSA RECORD OUT TO THE UNMATCHED FILE                03550100
      *  MAKE SURE YOU ONLY WRITE ONE UNMATCHED RECORD                  03550200
      *            PERFORM 5000-WRITE-UNMATCHED-COSA                    03551000
                   PERFORM 2000-READ-MATCH-FILE                         03560000
               END-IF                                                   03580000
           END-IF.                                                      03590000
                                                                        03600000
      ***************************************************************** 03610000
      * 5000-WRITE-UNMATCHED-COSA                                       03620000
      * WRITE OUT THE UNMATCHED COSA RECORDS BE SURE TO ONLY WRITE OUT  03630000
      * ONE COSA RECORD WHEN UNMATCHED.                                 03640000
      ***************************************************************** 03690000
       5000-WRITE-UNMATCHED-COSA.                                       03700000
                                                                        03710000
           IF PV-WRITE-CNT  = 0                                         03720000
               WRITE OUTPUT-UNMATCH-REC    FROM TC002-DATA              04100000
               MOVE 1                  TO  PV-WRITE-CNT                 04100100
           END-IF.                                                      04101000
                                                                        04110000
