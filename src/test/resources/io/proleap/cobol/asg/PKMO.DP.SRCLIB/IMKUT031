      ******************************************************************00010012
       IDENTIFICATION DIVISION.                                         00020012
      ******************************************************************00030012
                                                                        00040012
       PROGRAM-ID.             IMKUT031.                                00050029
       AUTHOR.                 D. THEEL                                 00060012
       INSTALLATION.           KOHLS.                                   00070012
       DATE-WRITTEN            9/20/2002.                               00080012
       DATE-COMPILED.                                                   00090012
                                                                        00100012
      *                                                                 00110012
      *-----------------------------------------------------------------00120012
      *    CREATE ITEM DOMAIN SKU/LOC EXTRACT RECORDS                   00130012
      *-----------------------------------------------------------------00140012
      *  THE PURPOSE OF THIS PROGRAM IS TO READ IN THE BMC UNLOAD ITEM  00150012
      *  SKU LOCATION EXTRACT FILE, WHICH INCLUDES ALL TUPCPLS RECORDS. 00160014
      *  ONLY RECORDS THAT BELONG TO ACTIVE SKUS ARE PROCESSED.  THIS   00160114
      *  IS DETERMINED BY CHECKING AGAINST THE TUPC UNLOAD FILE.  FOR   00160214
      *  ACTIVE SKU RECORDS GET THE SKU STATUS DESCRIPTION, AND IF THE  00160414
      *  MEITGP NBR IS GREATER THAN ZERO, GET THE TMITGPL GP RTL QTY AND00160514
      *  GP RTL AMT.  WRITE OUT ITEM DOMAIN SKU LOC EXTRACT RECORDS.    00160614
      *-----------------------------------------------------------------00160814
      *    P000573025 09/11/03     CHANGED INTR UPC ID DISPLAY TO       00160914
      *                            NUMERIC                              00161014
      *-----------------------------------------------------------------00161114
      *    WR27472    11/03/04     CHANGED LRECL OF INPUT FILE.         00161214
      *-----------------------------------------------------------------00161314
      *    WR28048    04/25/05  P. KEBER - ADDED LOGIC TO EXCLUDE       00161414
      *                         RECORDS FOR INACTIVE SKUS BY CHECKING   00161514
      *                         AGAINST THE TUPC UNLOAD FILE.           00161614
      *-----------------------------------------------------------------00161714
      *    WR29434    09/15/05  D. THEEL - ADDED NEW COLUMNS TO MOVE    00161816
      *                         FROM INPUT TO OUTPUT - ORIG_CLR_DTE     00161915
      *                         AND MKDN_CDE. INP01 LRECL INCREASE.     00162015
      *-----------------------------------------------------------------00162115
      *    WR35633    11/20/08  P. KEBER - ADDED LOGIC TO CREATE A SKU/ 00162217
      *                         LOCATION EXTRACT COUNTS OUTPUT FILE     00162317
      *                         CONTAINING ONE RECORD WITH THE STORE 0  00162417
      *                         AND TOTAL RECORDS COUNTS.               00162517
      *-----------------------------------------------------------------00162617
      *    WR35378    06/03/09  T. KONTAROVICH - ADDED LOGIC TO CREATE  00162821
      *                         THE DUPLICATE COPY OF A SKU/LOCATION    00162921
      *                         FILE ON THE DISK. CHANGED THE EXTRACT   00163021
      *                         RECORD'S LENGTH TO 173 BYTES.           00163121
      *-----------------------------------------------------------------00163221
      *    PB8058     07/28/10  COGNIZANT - CREATION OF OUTPUT GDG FILE 00163331
      *                         IS REMOVED TO IMPROVE THE PERFORMANCE   00163428
      *                         OF IMK069 JOB.                          00163528
      *-----------------------------------------------------------------00163728
                                                                        00164014
      ******************************************************************00310012
       ENVIRONMENT DIVISION.                                            00320012
      ******************************************************************00330012
                                                                        00340012
       CONFIGURATION SECTION.                                           00350012
                                                                        00360012
       SOURCE-COMPUTER.        IBM-3090.                                00370012
       OBJECT-COMPUTER.        IBM-3090.                                00380012
                                                                        00390012
       INPUT-OUTPUT SECTION.                                            00400012
                                                                        00410012
       FILE-CONTROL.                                                    00420012
                                                                        00430012
           SELECT SKU-LOC-UNLOAD-IN                                     00440012
               ASSIGN TO INP01.                                         00450012
                                                                        00460012
           SELECT UPC-UNLOAD-IN                                         00461012
               ASSIGN TO INP02.                                         00462012
                                                                        00463012
PB8058*    SELECT SKU-LOC-EXTRACT-OUT                                   00470031
PB8058*        ASSIGN TO OUT01.                                         00480031
                                                                        00490012
           SELECT SKU-LOC-COUNTS-OUT                                    00491017
PB8058*        ASSIGN TO OUT02.                                         00492032
PB8058         ASSIGN TO OUT01.                                         00492132
                                                                        00493017
W35378     SELECT SKU-LOC-EXTRACT-DSK                                   00494021
PB8058*        ASSIGN TO OUT03.                                         00495032
PB8058         ASSIGN TO OUT02.                                         00496032
                                                                        00500012
      ******************************************************************00510012
       DATA DIVISION.                                                   00520012
      ******************************************************************00530012
                                                                        00540012
       FILE SECTION.                                                    00550012
                                                                        00560012
       FD  SKU-LOC-UNLOAD-IN                                            00570012
           RECORDING MODE IS F                                          00580012
           BLOCK CONTAINS 0 RECORDS                                     00590012
           LABEL RECORDS ARE STANDARD.                                  00600012
       01  SKU-LOC-UNLOAD-IN-REC              PIC X(122).               00610015
                                                                        00620012
       FD  UPC-UNLOAD-IN                                                00621012
           RECORDING MODE IS F                                          00622012
           BLOCK CONTAINS 0 RECORDS                                     00623012
           LABEL RECORDS ARE STANDARD.                                  00624012
       01  UPC-UNLOAD-IN-REC                  PIC X(69).                00625012
                                                                        00626012
PB8058*FD  SKU-LOC-EXTRACT-OUT                                          00630031
|     *    RECORDING MODE IS F                                          00640031
|     *    BLOCK CONTAINS 0 RECORDS                                     00650031
|     *    LABEL RECORDS ARE STANDARD.                                  00660031
PB8058*01  SKU-LOC-EXTRACT-OUT-REC            PIC X(173).               00670031
                                                                        00680012
       FD  SKU-LOC-COUNTS-OUT                                           00681017
           RECORDING MODE IS F                                          00682017
           BLOCK CONTAINS 0 RECORDS                                     00683017
           LABEL RECORDS ARE STANDARD.                                  00684017
       01  SKU-LOC-COUNTS-OUT-REC             PIC X(80).                00685017
                                                                        00686017
W35378 FD  SKU-LOC-EXTRACT-DSK                                          00687021
W35378     RECORDING MODE IS F                                          00688021
W35378     BLOCK CONTAINS 0 RECORDS                                     00689021
W35378     LABEL RECORDS ARE STANDARD.                                  00689121
W35378 01  SKU-LOC-EXTRACT-DSK-REC            PIC X(173).               00689221
      ******************************************************************00690012
       WORKING-STORAGE SECTION.                                         00700012
      ******************************************************************00710012
       01  ABEND-CODE                         PIC S9(04)  VALUE +0      00720012
                                                          COMP SYNC.    00730012
       01  PC-PROGRAM-CONSTANTS.                                        00740012
           05  PC-PGM-NAME             PIC  X(08)     VALUE 'IMKUT031'. 00750012
           05  PC-MAX-RETRIES          PIC S9(04)     VALUE +2.         00760012
           05  PC-NEW                  PIC X(10) VALUE                  00770012
                                            'NEW       '.               00780012
           05  PC-ACTIVE               PIC X(10) VALUE                  00790012
                                            'ACTIVE    '.               00800012
           05  PC-MIXED                PIC X(10) VALUE                  00810012
                                            'MIXED     '.               00820012
           05  PC-CLEARANCE            PIC X(10) VALUE                  00830012
                                            'CLEARANCE '.               00840012
           05  PC-PROMTIONAL           PIC X(10) VALUE                  00850012
                                            'PROMTIONAL'.               00860012
           05  PC-DISCNTNUED           PIC X(10) VALUE                  00870012
                                            'DISCNTNUED'.               00880012
                                                                        00910012
       01  PS-PROGRAM-SWITCHES.                                         00920012
           05  PS-INPUT-SKU-LOC-SW            PIC X(01)    VALUE 'N'.   00930012
               88  PS-END-OF-INPUT-SKU-LOC                 VALUE 'Y'.   00940012
           05  PS-INPUT-UPC-SW                PIC X(01)    VALUE 'N'.   00941012
               88  PS-END-OF-INPUT-UPC                     VALUE 'Y'.   00942012
               88  PS-NOT-END-OF-INPUT-UPC                 VALUE 'N'.   00943012
           05  PS-SQL-ERROR-SW                PIC X(01)    VALUE 'N'.   00950013
               88  PS-SQL-ERROR                            VALUE 'Y'.   00960012
               88  PS-NO-SQL-ERROR                         VALUE 'N'.   00970012
           05  PS-UPC-ACTIVE-SW               PIC X(01)    VALUE 'N'.   00971012
               88  PS-UPC-ACTIVE                           VALUE 'Y'.   00972012
               88  PS-UPC-NOT-ACTIVE                       VALUE 'N'.   00973012
                                                                        00980012
       01  PV-PROGRAM-VARIABLES.                                        00990012
           05  PV-SKU-LOC-RECS-READ           PIC S9(10) VALUE +0  COMP.01010017
           05  PV-SKU-LOC-RECS-WRITTEN        PIC S9(10) VALUE +0  COMP.01010117
           05  PV-LOC-ZERO-RECS-WRITTEN       PIC S9(09) VALUE +0  COMP.01010217
           05  PV-UPC-RECS-READ               PIC S9(09) VALUE +0  COMP.01011012
           05  PV-PARAGRAPH-NAME              PIC  X(30)  VALUE SPACE.  01020012
           05  PV-DB2-OPERATION               PIC  X(30)  VALUE SPACE.  01030012
           05  PV-RETRY-COUNT                 PIC S9(04)  VALUE ZERO    01040012
                                                          COMP SYNC.    01050012
           05  PV-INTR-UPC-ID-TEN             PIC S9(10) VALUE +0 COMP. 01060012
           05  PV-DISPLAY-INTERNAL            PIC  9(10) VALUE ZERO.    01070012
                                                                        01071012
      *--------------------------------------------------               01080012
      *  COPYBOOK FOR BMC SKU LOC UNLOAD INPUT FILE.                    01090012
      *--------------------------------------------------               01100012
           COPY IMRD056.                                                01110012
                                                                        01120012
      *--------------------------------------------------               01121012
      *  COPYBOOK FOR BMC UPC UNLOAD INPUT FILE.                        01122012
      *--------------------------------------------------               01123012
           COPY IMRD054.                                                01124012
                                                                        01125012
      *-------------------------------------------------------          01130012
      *  COPYBOOK FOR ITEM DOMAIN SKU LOC EXTRACT OUTPUT FILE.          01140012
      *-------------------------------------------------------          01150012
           COPY IMRD057.                                                01160012
                                                                        01170012
      *-------------------------------------------------------          01171018
      *  COPYBOOK FOR ITEM DOMAIN SKU LOC EXTRACT COUNTS OUTPUT FILE.   01172018
      *-------------------------------------------------------          01173018
           COPY IMRD037.                                                01174018
                                                                        01175018
      *--------------------------------------------------               01180012
      *  MDSE ITEM GROUP PRICE AD LOC TABLE                             01190012
      *--------------------------------------------------               01200012
           EXEC SQL                                                     01210012
               INCLUDE TMITGPL                                          01220012
           END-EXEC.                                                    01230012
                                                                        01240012
      *--------------------------------------------------               01250012
      *  COMMUNICATION AREA FOR DB2                                     01260012
      *--------------------------------------------------               01270012
           EXEC SQL                                                     01280012
               INCLUDE SQLCA                                            01290012
           END-EXEC.                                                    01300012
                                                                        01310012
      *--------------------------------------------------               01320012
      *  DB2 SQL ERROR ROUTINE                                          01330012
      *--------------------------------------------------               01340012
           COPY DPWS004.                                                01350012
                                                                        01360012
       PROCEDURE DIVISION.                                              01370012
      *-----------------------------------------------------------------01380012
      *  MAIN LINE OF THE PROGRAM PERFORMS THE                          01390012
      *  INITIALIZATION ROUTINE AND CONTROLS THE PROCESSING OF THE PGM. 01400012
      *-----------------------------------------------------------------01410012
       0000-MAINLINE.                                                   01420012
                                                                        01430012
           PERFORM 0100-HOUSEKEEPING.                                   01440012
                                                                        01450012
           PERFORM 1000-PROCESS-INPUT-FILE                              01460012
               UNTIL PS-END-OF-INPUT-SKU-LOC.                           01470012
                                                                        01480012
           PERFORM 9000-EOJ.                                            01490012
           GOBACK.                                                      01500012
                                                                        01510012
      *-----------------------------------------------------------------01520012
      *  OPEN FILES AND PERFORM FIRST READ FROM INPUT FILE              01530012
      *-----------------------------------------------------------------01540012
       0100-HOUSEKEEPING.                                               01550012
                                                                        01560012
           OPEN INPUT SKU-LOC-UNLOAD-IN                                 01570012
                INPUT UPC-UNLOAD-IN                                     01571012
PB8058*         OUTPUT SKU-LOC-EXTRACT-OUT                              01580031
PB8058          OUTPUT                                                  01580131
                       SKU-LOC-COUNTS-OUT                               01581019
W35378                 SKU-LOC-EXTRACT-DSK.                             01590021
                                                                        01591017
           PERFORM 6000-READ-SKU-LOC-UNLOAD-FILE.                       01600012
                                                                        01610012
           IF PS-END-OF-INPUT-SKU-LOC                                   01620012
              DISPLAY '*** '                                            01630012
              DISPLAY '*** IMKUT031 - NO INPUT SKU LOC RECS FOUND'      01640012
              DISPLAY '*** '                                            01650012
              MOVE +4003 TO ABEND-CODE                                  01660012
              PERFORM 9999-ABEND                                        01670012
           END-IF.                                                      01680012
                                                                        01690012
           PERFORM 6100-READ-UPC-UNLOAD-FILE.                           01691012
                                                                        01692012
           IF PS-END-OF-INPUT-UPC                                       01693012
              DISPLAY '*** '                                            01694012
              DISPLAY '*** IMKUT031 - NO INPUT UPC RECS FOUND'          01695012
              DISPLAY '*** '                                            01696012
              MOVE +4004 TO ABEND-CODE                                  01697012
              PERFORM 9999-ABEND                                        01698012
           END-IF.                                                      01699012
                                                                        01699112
      *-----------------------------------------------------------------01700012
      *  PROCESS ALL INPUT RECORDS AND FORMAT ITEM SKU LOC DOMAIN       01710012
      *  EXTRACT RECORDS UNTIL END OF INPUT FILE.                       01720012
      *-----------------------------------------------------------------01730012
       1000-PROCESS-INPUT-FILE.                                         01740012
                                                                        01750012
           IF IM056-INTR-UPC-ID > IM054-UPC-ID                          01771112
               PERFORM 6100-READ-UPC-UNLOAD-FILE                        01771212
                 UNTIL IM056-INTR-UPC-ID <= IM054-UPC-ID                01771312
                    OR PS-END-OF-INPUT-UPC                              01771412
           END-IF.                                                      01771512
                                                                        01771612
           IF IM056-INTR-UPC-ID = IM054-UPC-ID                          01771712
             AND PS-NOT-END-OF-INPUT-UPC                                01771812
               SET PS-UPC-ACTIVE TO TRUE                                01771912
           ELSE                                                         01772012
               SET PS-UPC-NOT-ACTIVE TO TRUE                            01772112
           END-IF.                                                      01772312
                                                                        01772412
           IF PS-UPC-ACTIVE                                             01772512
               IF IM056-MEITGP-NBR > 0                                  01772612
                   PERFORM 1300-GET-GP-INFO                             01772712
                   MOVE MITGPL-MITGPL-QTY TO IM057-GP-RTL-QTY           01772812
                   MOVE MITGPL-GP-AMT     TO IM057-GP-AMT               01772912
               ELSE                                                     01773012
                   MOVE ZERO              TO IM057-GP-RTL-QTY           01773112
                   MOVE ZERO              TO IM057-GP-AMT               01773212
               END-IF                                                   01773312
               PERFORM 1200-MOVE-DATA-TO-OUTPUT-REC                     01773412
      ***      WRITE SKU LOC OUTPUT RECORD                              01773512
               PERFORM 7000-WRITE-SKU-LOC-OUTPUT-REC                    01773612
           END-IF.                                                      01773712
                                                                        01774012
           PERFORM 6000-READ-SKU-LOC-UNLOAD-FILE.                       01910012
                                                                        01920012
      *-----------------------------------------------------------------01930012
      * MOVE VALUES TO OUTPUT RECORD                                    01940012
      *-----------------------------------------------------------------01950012
       1200-MOVE-DATA-TO-OUTPUT-REC.                                    01960012
                                                                        01970012
           MOVE IM056-INTR-UPC-ID       TO IM057-INTR-UPC-ID.           01980012
           MOVE IM056-STR-NBR           TO IM057-STR-NBR.               01990012
           MOVE IM056-EFF-BEG-TMST      TO IM057-EFF-BEG-TMST.          02000012
           MOVE IM056-EFF-END-TMST      TO IM057-EFF-END-TMST.          02010012
           MOVE IM056-UNT-COST-AMT      TO IM057-UNT-COST-AMT.          02020012
           MOVE IM056-UNT-RTL-AMT       TO IM057-UNT-RTL-AMT.           02030012
           MOVE IM056-UNT-RTL-CHG-DTE   TO IM057-UNT-RTL-CHG-DTE.       02040012
           MOVE IM056-SKU-STAT-CDE      TO IM057-SKU-STAT-CDE.          02050012
           MOVE IM056-ORIG-CLR-DTE      TO IM057-ORIG-CLR-DTE.          02051015
           MOVE IM056-MKDN-CDE          TO IM057-MKDN-CDE.              02052015
           EVALUATE IM056-SKU-STAT-CDE                                  02060012
               WHEN '10'                                                02070012
                   MOVE PC-NEW TO IM057-SKU-STAT-DESC                   02080012
               WHEN '20'                                                02090012
                   MOVE PC-ACTIVE TO IM057-SKU-STAT-DESC                02100012
               WHEN '25'                                                02110012
                   MOVE PC-MIXED TO IM057-SKU-STAT-DESC                 02120012
               WHEN '30'                                                02130012
                   MOVE PC-CLEARANCE TO IM057-SKU-STAT-DESC             02140012
               WHEN '40'                                                02150012
                   MOVE PC-PROMTIONAL TO IM057-SKU-STAT-DESC            02160012
               WHEN '90'                                                02170012
                   MOVE PC-DISCNTNUED TO IM057-SKU-STAT-DESC            02180012
               WHEN  OTHER                                              02210012
                   MOVE SPACES TO IM057-SKU-STAT-DESC                   02220012
           END-EVALUATE.                                                02230012
           MOVE IM056-SKU-STAT-CHG-DTE  TO IM057-STAT-CHG-DTE.          02240012
           MOVE IM056-PLU-IND         TO IM057-PLU-IND.                 02250012
           MOVE IM056-MEITGP-NBR      TO IM057-MEITGP-NBR.              02260012
           MOVE SPACES                TO IM057-ITEM-SKU-LOC-FILLER.     02270012
                                                                        02280012
                                                                        02290012
      *-----------------------------------------------------------------02300012
      * GET THE GROUP QUANTITY AND PRICE BASED ON THE MEITGP NBR        02310012
      * FROM THE INPUT FILE.                                            02320012
      *-----------------------------------------------------------------02330012
       1300-GET-GP-INFO.                                                02340012
                                                                        02350012
           INITIALIZE DCLTMITGPL.                                       02360012
                                                                        02370012
           PERFORM WITH TEST AFTER                                      02380012
               VARYING PV-RETRY-COUNT FROM 1 BY 1                       02390012
                 UNTIL SQLCODE = +0                                     02400012
                    OR SQLCODE = +100                                   02410012
                    OR PV-RETRY-COUNT > PC-MAX-RETRIES                  02420012
               EXEC SQL                                                 02430012
                   SELECT MITGPL_QTY                                    02440012
                         ,GP_AMT                                        02450012
                     INTO :MITGPL-MITGPL-QTY                            02460012
                         ,:MITGPL-GP-AMT                                02470012
                     FROM TMITGPL                                       02480012
                    WHERE OPERCO_NBR    = '03'                          02490012
                      AND MEITGP_NBR    = :IM056-MEITGP-NBR             02500015
                    WITH UR                                             02510012
               END-EXEC                                                 02520012
                                                                        02530012
               EVALUATE TRUE                                            02540012
                   WHEN SQLCODE = -904                                  02550012
                   WHEN SQLCODE = -913                                  02560012
                       CONTINUE                                         02570012
                   WHEN SQLCODE = ZERO                                  02580012
                       CONTINUE                                         02590012
                   WHEN SQLCODE = +100                                  02600012
                       MOVE ZERO TO MITGPL-MITGPL-QTY                   02610012
                       MOVE ZERO TO MITGPL-GP-AMT                       02620012
                   WHEN OTHER                                           02630012
                       MOVE IM056-INTR-UPC-ID  TO PV-INTR-UPC-ID-TEN    02631012
                       MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL   02632012
                       DISPLAY '** '                                    02640012
                               PC-PGM-NAME                              02650012
                               ' - INTR UPC ID: '                       02660012
                               PV-DISPLAY-INTERNAL                      02670012
                       MOVE '1300-GET-GP-INFO' TO PV-PARAGRAPH-NAME     02680012
                       MOVE 'SELECT FROM TMITGPL'  TO PV-DB2-OPERATION  02690012
                       PERFORM 9998-SQL-ERROR                           02700012
               END-EVALUATE                                             02710012
           END-PERFORM.                                                 02720012
                                                                        02730012
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                          02740012
               MOVE IM056-INTR-UPC-ID  TO PV-INTR-UPC-ID-TEN            02741012
               MOVE PV-INTR-UPC-ID-TEN TO PV-DISPLAY-INTERNAL           02742012
               DISPLAY '** '                                            02750012
                       PC-PGM-NAME                                      02760012
                       ' - INTR UPC ID: '                               02770012
                       PV-DISPLAY-INTERNAL                              02780012
               MOVE '1300-GET-GP-INFO' TO PV-PARAGRAPH-NAME             02790012
               MOVE 'SELECT FROM TMITGPL'  TO PV-DB2-OPERATION          02800012
               PERFORM 9998-SQL-ERROR                                   02810012
           END-IF.                                                      02820012
      *-----------------------------------------------------------------02830012
                                                                        02840012
      *-----------------------------------------------------------------02850012
      * READ THE TUPCPLS UNLOAD INPUT FILE                              02860012
      *-----------------------------------------------------------------02870012
       6000-READ-SKU-LOC-UNLOAD-FILE.                                   02880012
                                                                        02890012
           READ SKU-LOC-UNLOAD-IN INTO IM056-ITEM-SKU-LOC-UNLD-REC      02920012
              AT END                                                    02930012
                 SET PS-END-OF-INPUT-SKU-LOC TO TRUE                    02940012
              NOT AT END                                                02950012
                 ADD +1   TO PV-SKU-LOC-RECS-READ.                      02960012
                                                                        02970012
      *-----------------------------------------------------------------02971012
      * READ THE TUPC UNLOAD INPUT FILE                                 02972012
      *-----------------------------------------------------------------02973012
       6100-READ-UPC-UNLOAD-FILE.                                       02974012
                                                                        02975012
           READ UPC-UNLOAD-IN INTO IM054-ITEM-UPC-UNLD-REC              02978012
              AT END                                                    02979012
                 SET PS-END-OF-INPUT-UPC TO TRUE                        02979112
              NOT AT END                                                02979212
                 ADD +1   TO PV-UPC-RECS-READ.                          02979312
                                                                        02979412
      *-----------------------------------------------------------------02980012
       7000-WRITE-SKU-LOC-OUTPUT-REC.                                   02990012
                                                                        03000012
PB8058*    WRITE SKU-LOC-EXTRACT-OUT-REC                                03010031
PB8058*                             FROM IM057-ITEM-SKU-LOC-DOMAIN-REC. 03020031
                                                                        03030012
W35378     WRITE SKU-LOC-EXTRACT-DSK-REC                                03031021
W35378                              FROM IM057-ITEM-SKU-LOC-DOMAIN-REC. 03032021
                                                                        03033019
           ADD +1 TO PV-SKU-LOC-RECS-WRITTEN.                           03040012
           IF IM057-STR-NBR EQUAL 0                                     03040117
               ADD +1 TO PV-LOC-ZERO-RECS-WRITTEN                       03041017
           END-IF.                                                      03050017
                                                                        03050117
      *-----------------------------------------------------------------03051017
       7100-WRITE-SKU-LOC-OUTPUT-REC.                                   03052017
                                                                        03053017
           WRITE SKU-LOC-COUNTS-OUT-REC                                 03054017
                                    FROM IM037-SKU-LOCATION-COUNTS-REC. 03055017
                                                                        03060012
      *-----------------------------------------------------------------03070012
      * 9000-EOJ-ROUTINE - CLOSES THE FILES ASSOCIATED WITH THE JOB.    03080012
      *-----------------------------------------------------------------03090012
       9000-EOJ.                                                        03100012
                                                                        03110012
           MOVE PV-LOC-ZERO-RECS-WRITTEN TO IM037-LOCATION-ZERO-REC-CNT.03110117
           MOVE PV-SKU-LOC-RECS-WRITTEN  TO IM037-TOTAL-RECS-CNT.       03110217
           PERFORM 7100-WRITE-SKU-LOC-OUTPUT-REC.                       03111017
                                                                        03112017
           DISPLAY ' '.                                                 03120012
           DISPLAY '============================================='.     03130012
           DISPLAY ' '.                                                 03140012
           DISPLAY ' ******  IMKUT031 PROCESSING SUMMARY  ******  '.    03150012
           PERFORM 9100-DISPLAY-PROCESSING-INFO.                        03160012
           DISPLAY '============================================='.     03170012
                                                                        03190012
           CLOSE SKU-LOC-UNLOAD-IN                                      03200012
                 UPC-UNLOAD-IN                                          03201012
PB8058*          SKU-LOC-EXTRACT-OUT                                    03210031
                 SKU-LOC-COUNTS-OUT                                     03211028
W35378           SKU-LOC-EXTRACT-DSK.                                   03212021
                                                                        03220012
      *-----------------------------------------------------------------03230012
      * DISPLAY PROCESSED RECORDS COUNTS                                03240012
      *-----------------------------------------------------------------03250012
       9100-DISPLAY-PROCESSING-INFO.                                    03260012
                                                                        03270012
           DISPLAY ' '.                                                 03280012
           DISPLAY 'SKU LOC RECORDS READ     = '                        03290012
                 PV-SKU-LOC-RECS-READ.                                  03300012
           DISPLAY 'UPC RECORDS READ         = '                        03301012
                 PV-UPC-RECS-READ.                                      03302012
           DISPLAY 'SKU LOC RECORDS WRITTEN  = '                        03310012
                 PV-SKU-LOC-RECS-WRITTEN.                               03320012
           DISPLAY 'LOC ZERO RECORDS WRITTEN = '                        03321017
                 PV-LOC-ZERO-RECS-WRITTEN.                              03322017
           DISPLAY ' '.                                                 03330012
                                                                        03331017
      *-----------------------------------------------------------------03340012
      * 9800-SQL-ABEND - DISPLAY DB2 ABEND INFO                         03350012
      *-----------------------------------------------------------------03360012
       9998-SQL-ERROR.                                                  03370012
                                                                        03380012
           SET PS-SQL-ERROR TO TRUE.                                    03390012
                                                                        03400012
           DISPLAY '** '                                                03410012
                   PC-PGM-NAME                                          03420012
                   ' ABEND IN PARAGRAPH '                               03430012
                   PV-PARAGRAPH-NAME.                                   03440012
           DISPLAY '** '                                                03450012
                   PC-PGM-NAME                                          03460012
                   ' OPERATION = ' PV-DB2-OPERATION.                    03470012
                                                                        03480012
      *    THE FOLLOWING COPYBOOK UTILIZES MODULE DSNTIAR TO FORMAT     03490012
      *    THE INFO IN SQLCA INTO UNDERSTANDABLE VERBAGE.               03500012
           COPY DPPD004.                                                03510012
                                                                        03520012
           EXEC SQL                                                     03530012
               ROLLBACK                                                 03540012
           END-EXEC.                                                    03550012
                                                                        03560012
           MOVE +4013 TO ABEND-CODE                                     03570012
           PERFORM 9999-ABEND.                                          03580012
                                                                        03590012
      *-----------------------------------------------------------------03600012
      * 9999-ABEND - FORCES PROGRAM ABEND WITH RETURN CODE              03610012
      *-----------------------------------------------------------------03620012
       9999-ABEND.                                                      03630012
                                                                        03640012
           CALL 'ILBOABN0' USING ABEND-CODE.                            03650012
                                                                        03660012
