      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             TCKUT072.                                00050000
       AUTHOR.                 DAN PAYNTER.                             00060000
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            MAY 2023.                                00080000
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *                                                                 00120000
      *   ***  COBRAND TRAILING TRANSACTION UPDATE PROGRAM   ***        00130000
      *                                                                 00140000
      *  FOR RELEASE 1 OF COBRAND VISA GIFT CARDS (REPLACEMENT TO       00150000
      *  THE ORIGINAL KOHL'S CHARGE CARD THAT SETTLES WITH FISERV),     00160000
      *  THERE WILL BE TRANSACTIONS GENERATED BY CUSTOMER SALES,        00170000
      *  PAYMENTS, PRICE ADJUSTMENTS, AND RETURNS SHORTLY AFTER AN      00180000
      *  ACCOUNT IS CONVERTED FROM FISERV KOHL'S CHARGE TO CAP ONE      00190000
      *  COBRAND VISA THAT WILL COME WITH THE SMALLER FISERV ACCOUNT    00200000
      *  NUMBER, BUT STILL REQUIRE SETTLEMENT TO FLOW TO CAPITAL ONE.   00210000
      *  ANY SUCH TRANSACTIONS WILL REQUIRE EITHER THE TEPCRD OR TEPPAY 00220000
      *  DB2 TABLES TO BE UPDATED TO MARK THE CARD/ACCOUNT AS A         00230000
      *  TRAILING TRANSACTION.  THIS INDICATOR WILL THEN BE USED BY THE 00240000
      *  THE NIGHTLY SETTLEMENT PROCESSES TO DETERMINE WHICH SETTLEMENT 00250000
      *  FILE THE TRANSACTION SHOULD BE ON.  IF THE TRLING_TRN_IND = 'Y'00260000
      *  THEN THE TRANSACTION MUST BE PICKED UP BY THE TCK BATCH PROCESS00270000
      *  AND IF THE TRLING_TRN_IND = 'N', THEN IT SHOULD BE PICKED UP BY00280000
      *  THE ARK (FISERV) BATCH SETTLEMENT PROCESS.                     00290000
      *                                                                 00300000
      *   INPUT: INP01 - CONTAINS THE DATE TO BE CHECKED FOR            00310000
      *                  TRAILING TRANSACTIONS.                         00320000
      *                                                                 00330000
      ****************************************************************  00340000
      *                                                                 00350000
      * CHANGE LOG:                                                     00360000
      * DATE:  2023-05-08                                               00370000
      * PRGMR: DAN PAYNTER                                              00380000
      * WHY:   NEW PROGRAM TO UPDATE TRAILING TRANSACTIONS.             00390000
      * CHANGE: CHG0288838                                              00391001
      ****************************************************************  00400000
      ****************************************************************  00410000
       ENVIRONMENT DIVISION.                                            00420000
      ****************************************************************  00430000
                                                                        00440000
       CONFIGURATION SECTION.                                           00450000
                                                                        00460000
       SOURCE-COMPUTER.        IBM-3090.                                00470000
                                                                        00480000
       OBJECT-COMPUTER.        IBM-3090.                                00490000
                                                                        00500000
       INPUT-OUTPUT SECTION.                                            00510000
                                                                        00520000
       FILE-CONTROL.                                                    00530000
                                                                        00540000
           SELECT DATE-CARD-FILE ASSIGN TO INP01.                       00550000
           SELECT TRAN-UPDT-FILE ASSIGN TO OUT01.                       00560000
                                                                        00570000
      ***************************************************************** 00580000
       DATA DIVISION.                                                   00590000
      ***************************************************************** 00600000
                                                                        00610000
       FILE SECTION.                                                    00620000
                                                                        00630000
       FD  DATE-CARD-FILE                                               00640000
           RECORDING MODE IS F                                          00650000
           BLOCK CONTAINS 0 RECORDS                                     00660000
           LABEL RECORDS ARE STANDARD.                                  00670000
                                                                        00680000
       01  DATE-CARD-RECORD                PIC X(80).                   00690000
                                                                        00700000
       FD  TRAN-UPDT-FILE                                               00710000
           RECORDING MODE IS F                                          00720000
           LABEL RECORDS ARE STANDARD                                   00730000
           BLOCK CONTAINS 0 RECORDS                                     00740000
           RECORD CONTAINS 250 CHARACTERS                               00750000
           DATA RECORD IS TRAN-UPDT-RECORD.                             00760000
                                                                        00770000
       01  TRAN-UPDT-RECORD                PIC X(250).                  00780000
                                                                        00790000
      ***************************************************************** 00800000
       WORKING-STORAGE SECTION.                                         00810000
      ***************************************************************** 00820000
       01  FILLER                            PIC X(50)  VALUE           00830000
           ' *** WORKING STORAGE STARTS HERE FOR TCKUT072 *** '.        00840000
                                                                        00850000
      ***************************************************************** 00860000
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                    00870000
      ***************************************************************** 00880000
       01  CC-CONTROL-CARD.                                             00890000
           05  CC-CONTROL-CARD-DISPLAY.                                 00900000
               10  CC-CONTROL-NAME     PIC X(14)    VALUE SPACE.        00910000
                   88  CC-VALID-CONTROL-NAME        VALUE               00920000
                       'P.O.S. DATE = '.                                00930000
               10  CC-POS-DATE-MMDDYY  PIC 9(6)     VALUE ZERO.         00940000
           05  FILLER                  PIC X(60)    VALUE SPACE.        00950000
                                                                        00960000
      ***************************************************************** 00970000
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)             00980000
      ***************************************************************** 00990000
           COPY DPWS500.                                                01000000
                                                                        01010000
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      01020000
                                                        COMP SYNC.      01030000
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01040000
           88  AC-DB2-ERROR                             VALUE +4013.    01050000
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    01060000
                                                                        01070000
       01  PS-PROGRAM-SWITCHES.                                         01080000
           05  PS-END-OF-DATE-FILE-SW        PIC  X(01) VALUE 'N'.      01090000
               88  PS-END-OF-DATE-FILE                  VALUE 'Y'.      01100000
           05  PS-END-OF-CRD-CSR-SW          PIC  X(01) VALUE 'N'.      01110000
               88  PS-END-OF-CRD-CSR-N                  VALUE 'N'.      01120000
               88  PS-END-OF-CRD-CSR-Y                  VALUE 'Y'.      01130000
           05  PS-END-OF-PAY-CSR-SW          PIC  X(01) VALUE 'N'.      01140000
               88  PS-END-OF-PAY-CSR-N                  VALUE 'N'.      01150000
               88  PS-END-OF-PAY-CSR-Y                  VALUE 'Y'.      01160000
                                                                        01170000
       01  PC-CONSTANTS.                                                01180000
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08) VALUE             01190000
                                                      'TCKUT072'.       01200000
           05  PC-ROW-NOT-FOUND            PIC S9(04) VALUE +100        01210000
                                                      COMP SYNC.        01220000
           05  PC-MULTIPLE-ROWS-FOUND      PIC S9(04) VALUE -811        01230000
                                                      COMP SYNC.        01240000
       01  PV-PROGRAM-VARIABLES.                                        01250000
           05  PV-POS-CCYY-MM-DD           PIC  X(10).                  01260000
           05  PV-KC-ACCT-LENGTH           PIC  S9(2)V COMP-3.          01261000
           05  PV-KC-ACCT                  PIC  X(18).                  01261100
           05  PV-KC-SUBSTR-REC10 REDEFINES PV-KC-ACCT.                 01261300
               10  PV-KC-ACCT-FIRST-10     PIC  X(10).                  01261400
               10  PV-KC-ACCT-LAST-8       PIC  X(08).                  01261500
           05  PV-KC-SUBSTR-REC16 REDEFINES PV-KC-ACCT.                 01261600
               10  PV-KC-ACCT-FIRST-16     PIC  X(16).                  01261700
               10  PV-KC-ACCT-LAST-2       PIC  X(02).                  01261800
           05  PV-KC-ACCT-SRC-CDE          PIC  X(02).                  01261900
           05  PV-ACCT-FOUND               PIC  X(01).                  01262000
                                                                        01263000
       01  PV-MISC-COUNT-FIELDS            COMP-3.                      01270000
           05  PV-CRD-CURSOR-CNT           PIC S9(07)    VALUE ZERO.    01280000
           05  PV-PAY-CURSOR-CNT           PIC S9(07)    VALUE ZERO.    01290000
           05  PV-PAY-UPDATE-CNT           PIC S9(07)    VALUE ZERO.    01291000
           05  PV-CRD-UPDATE-CNT           PIC S9(07)    VALUE ZERO.    01292000
                                                                        01300000
       01  PV-DB2-VARIABLES.                                            01310000
           05  PV-DB2-PARTN-NBR            PIC S9(04)    COMP           01320000
                                                         VALUE ZERO.    01330000
           05  PV-PREV-PARTN-NBR           PIC S9(04)    COMP           01340000
                                                         VALUE ZERO.    01350000
       01  PV-ABEND-DISPLAYS.                                           01360000
           05  PV-ABEND-PARTN-NBR          PIC  9(04)    VALUE ZERO.    01370000
           05  PV-ABEND-STR-NBR            PIC  9(04)    VALUE ZERO.    01380000
           05  PV-ABEND-RGST-ID            PIC  9(03)    VALUE ZERO.    01390000
           05  PV-ABEND-TRN-NBR            PIC  9(04)    VALUE ZERO.    01400000
                                                                        01410000
       01  PV-MISC-FIELDS.                                              01420000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01430000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01440000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01450000
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01460000
           05  PV-EDIT-MASK                PIC  Z,ZZZ,ZZ9.              01470000
                                                                        01480000
      ***************************************************************** 01490000
      *  WS FIELDS FOR TEPPAY AND TEPCRD DB2 UPDATE                     01500000
      ***************************************************************** 01510000
           COPY TCRD072.                                                01520000
                                                                        01530000
      ***************************************************************** 01540000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01550000
      ***************************************************************** 01560000
           COPY DPWS004.                                                01570000
                                                                        01580000
      ***************************************************************** 01590000
      *  DB2 COMMUNICATION AREA.                                        01600000
      ***************************************************************** 01610000
           EXEC SQL                                                     01620000
                INCLUDE SQLCA                                           01630000
           END-EXEC.                                                    01640000
                                                                        01650000
      ***************************************************************** 01660000
      *  DB2 TABLE ART_CNV_ACCT_NBR - CONVERTED PLCC ACCOUNT TABLE      01670000
      ***************************************************************** 01680000
           EXEC SQL                                                     01690000
                INCLUDE ART00003                                        01700000
           END-EXEC.                                                    01710000
                                                                        01720000
      ***************************************************************** 01721000
      *  DB2 TABLE TEPTRN - TRANSACTION TRANSACTION TABLE               01722000
      ***************************************************************** 01723000
           EXEC SQL                                                     01724000
                INCLUDE TEPTRN                                          01725000
           END-EXEC.                                                    01726000
                                                                        01727000
      ***************************************************************** 01730000
      *  DB2 TABLE TEPCRD - TRANSACTION CARD TENDER TABLE               01740000
      ***************************************************************** 01750000
           EXEC SQL                                                     01760000
                INCLUDE TEPCRD                                          01770000
           END-EXEC.                                                    01780000
                                                                        01790000
      ***************************************************************** 01800000
      *  DB2 TABLE TEPTND - TRANSACTION TENDER TABLE                    01810000
      ***************************************************************** 01820000
           EXEC SQL                                                     01830000
                INCLUDE TEPTND                                          01840000
           END-EXEC.                                                    01850000
                                                                        01860000
      ***************************************************************** 01870000
      *  DB2 TABLE TEPPAY - TRANSACTION PAYMENT TABLE                   01880000
      ***************************************************************** 01890000
           EXEC SQL                                                     01900000
                INCLUDE TEPPAY                                          01910000
           END-EXEC.                                                    01920000
                                                                        02000000
      ******************************************************************02010000
      *  DB2 TABLE TEPDKEY - CURRENT PROCESSING DAY KEYS                02020000
      ******************************************************************02030000
           EXEC SQL                                                     02040000
                INCLUDE TEPDKEY                                         02050000
           END-EXEC.                                                    02060000
                                                                        02070000
      ***************************************************************** 02080000
      *  DB2 TABLE TEPPART - PARTITION TABLE                            02090000
      ***************************************************************** 02100000
           EXEC SQL                                                     02110000
                INCLUDE TEPPART                                         02120000
           END-EXEC.                                                    02130000
                                                                        02140000
      ***************************************************************** 02150000
      *  CRD-CURSOR -- CURSOR TO RETRIEVE ALL CONVERTED PLCC CARDS      02160000
      *  THAT NEED TO BE MARKED AS 'TRAILING TRANSACTIONS'              02170000
      ***************************************************************** 02180000
           EXEC SQL                                                     02190000
             DECLARE CRD-CURSOR CURSOR FOR                              02200000
                                                                        02210000
      **> CURRENT DAY TRANSACTIONS WITH PLCC AS TENDER                  02220000
            SELECT DISTINCT                                             02230000
                   CRD.PARTN_NBR                                        02240000
                  ,CRD.STR_NBR                                          02250000
                  ,CRD.RGST_ID                                          02260000
                  ,CRD.TRN_NBR                                          02270000
                  ,CRD.STRT_TM                                          02280000
                  ,CRD.SLS_CORR_SEQ_NBR                                 02290000
                  ,CRD.TNDR_SEQ_NBR                                     02300000
                  ,TND.TNDR_ID                                          02310000
                  ,TND.TNDR_ID_SRC_CDE                                  02311000
                  ,TND.TNDR_ID_LEN_NBR                                  02320000
              FROM                                                      02330000
                   TEPTND  TND                                          02340000
                  ,TEPCRD  CRD                                          02350000
             WHERE                                                      02360000
                   TND.PARTN_NBR         = :PV-DB2-PARTN-NBR            02370000
                                                                        02380000
               AND TND.PARTN_NBR         = CRD.PARTN_NBR                02390000
               AND TND.STR_NBR           = CRD.STR_NBR                  02400000
               AND TND.RGST_ID           = CRD.RGST_ID                  02410000
               AND TND.TRN_NBR           = CRD.TRN_NBR                  02420000
               AND TND.STRT_TM           = CRD.STRT_TM                  02430000
               AND TND.SLS_CORR_SEQ_NBR  = CRD.SLS_CORR_SEQ_NBR         02440000
               AND TND.TNDR_SEQ_NBR      = CRD.TNDR_SEQ_NBR             02450000
                                                                        02460000
               AND TND.TNDR_TYP_CDE      = '04'                         02470000
               AND CRD.TRLING_TRN_IND    = 'N'                          02480000
             UNION                                                      02490000
      **> LATE AND SALES CORRECTION TRANSACTIONS WITH PLCC              02500000
      **> AS A TENDER                                                   02510000
            SELECT DISTINCT                                             02520000
                   CRD.PARTN_NBR                                        02530000
                  ,CRD.STR_NBR                                          02540000
                  ,CRD.RGST_ID                                          02550000
                  ,CRD.TRN_NBR                                          02560000
                  ,CRD.STRT_TM                                          02570000
                  ,CRD.SLS_CORR_SEQ_NBR                                 02580000
                  ,CRD.TNDR_SEQ_NBR                                     02590000
                  ,TND.TNDR_ID                                          02591000
                  ,TND.TNDR_ID_SRC_CDE                                  02600000
                  ,TND.TNDR_ID_LEN_NBR                                  02610000
              FROM                                                      02620000
                   TEPPART PRT                                          02630000
                  ,TEPTND  TND                                          02640000
                  ,TEPCRD  CRD                                          02650000
                  ,TEPDKEY DKY                                          02660000
             WHERE                                                      02670000
                   DKY.PRCG_DTE          = :PV-POS-CCYY-MM-DD           02680000
               AND PRT.SLS_DTE           = DKY.SLS_DTE                  02690000
               AND PRT.DB_STRUC_CDE      = 'O'                          02700000
               AND PRT.STR_GP_CDE        = 'A'                          02710000
                                                                        02720000
               AND TND.PARTN_NBR         = PRT.PARTN_NBR                02730000
               AND TND.STR_NBR           = DKY.STR_NBR                  02740000
               AND TND.RGST_ID           = DKY.RGST_ID                  02750000
               AND TND.TRN_NBR           = DKY.TRN_NBR                  02760000
               AND TND.STRT_TM           = DKY.STRT_TM                  02770000
               AND TND.SLS_CORR_SEQ_NBR  = DKY.SLS_CORR_SEQ_NBR         02780000
                                                                        02790000
               AND TND.PARTN_NBR         = CRD.PARTN_NBR                02800000
               AND TND.STR_NBR           = CRD.STR_NBR                  02810000
               AND TND.RGST_ID           = CRD.RGST_ID                  02820000
               AND TND.TRN_NBR           = CRD.TRN_NBR                  02830000
               AND TND.STRT_TM           = CRD.STRT_TM                  02840000
               AND TND.SLS_CORR_SEQ_NBR  = CRD.SLS_CORR_SEQ_NBR         02850000
               AND TND.TNDR_SEQ_NBR      = CRD.TNDR_SEQ_NBR             02860000
                                                                        02870000
               AND TND.TNDR_TYP_CDE      = '04'                         02880000
               AND CRD.TRLING_TRN_IND    = 'N'                          02890000
               ORDER BY 1                                               02900000
           END-EXEC.                                                    02910000
                                                                        02920000
      ***************************************************************** 02930000
      *  PAY-CURSOR -- CURSOR TO RETRIEVE ALL CONVERTED PLCC ACCOUNTS   02940000
      *  THAT NEED TO BE MARKED AS 'TRAILING TRANSACTIONS'              02950000
      ***************************************************************** 02960000
           EXEC SQL                                                     02970000
             DECLARE PAY-CURSOR CURSOR FOR                              02980000
                                                                        02990000
      **> CURRENT DAY PAYMENT TRANSACTIONS                              03000000
            SELECT DISTINCT                                             03010000
                   PAY.PARTN_NBR                                        03020000
                  ,PAY.STR_NBR                                          03030000
                  ,PAY.RGST_ID                                          03040000
                  ,PAY.TRN_NBR                                          03050000
                  ,PAY.STRT_TM                                          03060000
                  ,PAY.SLS_CORR_SEQ_NBR                                 03070000
                  ,0                                                    03080000
                  ,SUBSTR(PAY.KC_ACCT_NBR,1,10)                         03081000
                  ,PAY.KC_ACCT_SRC_CDE                                  03081100
                  ,LENGTH(PAY.KC_ACCT_NBR)                              03082000
              FROM                                                      03090000
                   TEPPAY  PAY                                          03100000
             WHERE                                                      03120000
                   PAY.PARTN_NBR         = :PV-DB2-PARTN-NBR            03130000
               AND PAY.COBRND_IND        = 'N'                          03140000
               AND PAY.TRLING_TRN_IND    = 'N'                          03150000
             UNION                                                      03170000
      **> LATE AND SALES CORRECTION PAYMENT TRANSACTIONS                03180000
            SELECT DISTINCT                                             03190000
                   PAY.PARTN_NBR                                        03200000
                  ,PAY.STR_NBR                                          03210000
                  ,PAY.RGST_ID                                          03220000
                  ,PAY.TRN_NBR                                          03230000
                  ,PAY.STRT_TM                                          03240000
                  ,PAY.SLS_CORR_SEQ_NBR                                 03250000
                  ,0                                                    03260000
                  ,SUBSTR(PAY.KC_ACCT_NBR,1,10)                         03261000
                  ,PAY.KC_ACCT_SRC_CDE                                  03261100
                  ,LENGTH(PAY.KC_ACCT_NBR)                              03262000
              FROM                                                      03270000
                   TEPPART PRT                                          03280000
                  ,TEPPAY  PAY                                          03290000
                  ,TEPDKEY DKY                                          03310000
             WHERE                                                      03320000
                   DKY.PRCG_DTE          = :PV-POS-CCYY-MM-DD           03330000
               AND PRT.SLS_DTE           = DKY.SLS_DTE                  03340000
               AND PRT.DB_STRUC_CDE      = 'O'                          03350000
               AND PRT.STR_GP_CDE        = 'A'                          03360000
                                                                        03370000
               AND PAY.PARTN_NBR         = PRT.PARTN_NBR                03380000
               AND PAY.STR_NBR           = DKY.STR_NBR                  03390000
               AND PAY.RGST_ID           = DKY.RGST_ID                  03400000
               AND PAY.TRN_NBR           = DKY.TRN_NBR                  03410000
               AND PAY.STRT_TM           = DKY.STRT_TM                  03420000
               AND PAY.SLS_CORR_SEQ_NBR  = DKY.SLS_CORR_SEQ_NBR         03430000
                                                                        03440000
               AND PAY.COBRND_IND        = 'N'                          03450000
               AND PAY.TRLING_TRN_IND    = 'N'                          03460000
               ORDER BY 1                                               03480000
           END-EXEC.                                                    03490000
                                                                        03500000
      ***************************************************************** 03510000
      * PROCEDURE DIVISION.                                           * 03520000
      ***************************************************************** 03530000
       PROCEDURE DIVISION.                                              03540000
                                                                        03550000
      ***************************************************************** 03560000
      * 0000-MAINLINE-PARAGRAPH                                         03570000
      ***************************************************************** 03580000
       0000-MAINLINE-PARAGRAPH.                                         03590000
                                                                        03600000
           PERFORM 1000-INITIALIZE-PROGRAM.                             03610000
                                                                        03620000
           PERFORM 2000-OPEN-CRD-CURSOR.                                03630000
                                                                        03640000
           PERFORM 2100-PROCESS-CRDS                                    03650000
               UNTIL PS-END-OF-CRD-CSR-Y.                               03660000
                                                                        03670000
           PERFORM 2200-CLOSE-CRD-CURSOR.                               03680000
                                                                        03690000
           PERFORM 2001-OPEN-PAY-CURSOR.                                03700000
                                                                        03710000
           PERFORM 2101-PROCESS-PYMNTS                                  03720000
               UNTIL PS-END-OF-PAY-CSR-Y.                               03730000
                                                                        03740000
           PERFORM 2201-CLOSE-PAY-CURSOR.                               03750000
                                                                        03760000
           DISPLAY '****************************************'           03770000
           DISPLAY '*************** TCKUT072 ***************'           03780000
           DISPLAY '****************************************'           03790000
           MOVE PV-PAY-CURSOR-CNT        TO PV-EDIT-MASK.               03800000
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.         03810000
           DISPLAY 'TEPPAY CURSOR ROWS FETCHED.....'                    03820000
               PV-EDIT-MASK.                                            03830000
           MOVE PV-CRD-CURSOR-CNT        TO PV-EDIT-MASK.               03840000
           INSPECT PV-EDIT-MASK REPLACING LEADING SPACE BY '.'.         03850000
           DISPLAY 'TEPCRD ROWS LOCATED............'                    03860000
               PV-EDIT-MASK.                                            03870000
           DISPLAY '****************************************'           03880000
                                                                        03890000
           CLOSE TRAN-UPDT-FILE.                                        03900000
           STOP RUN.                                                    03910000
                                                                        03920000
      ***************************************************************** 03930000
      * 1000-INITIALIZE-PROGRAM                                         03940000
      *     PROCESS CONTROL CARD                                        03950000
      ***************************************************************** 03960000
       1000-INITIALIZE-PROGRAM.                                         03970000
                                                                        03980000
           OPEN INPUT  DATE-CARD-FILE                                   03990000
                OUTPUT TRAN-UPDT-FILE.                                  04000000
                                                                        04010000
           SET PS-END-OF-CRD-CSR-N                                      04020000
               PS-END-OF-PAY-CSR-N TO TRUE.                             04030000
                                                                        04040000
           MOVE ZERO                     TO PV-CRD-CURSOR-CNT           04050000
                                            PV-PAY-CURSOR-CNT           04060000
                                            PV-DB2-PARTN-NBR.           04070000
                                                                        04080000
           PERFORM 1300-PROCESS-DATE-CARD.                              04090000
           PERFORM 1500-SELECT-TEPPART-ROW.                             04100000
           MOVE PV-DB2-PARTN-NBR  TO PV-ABEND-PARTN-NBR.                04110000
           DISPLAY 'PROCESSING PARTN = '  PV-ABEND-PARTN-NBR.           04120000
                                                                        04130000
      *1000-INITIALIZE-PROGRAM-EXIT.                                    04140000
                                                                        04150000
      ******************************************************************04160000
      * 1300-PROCESS-DATE-CARD                                          04170000
      ******************************************************************04180000
       1300-PROCESS-DATE-CARD.                                          04190000
                                                                        04200000
           READ DATE-CARD-FILE INTO CC-CONTROL-CARD                     04210000
               AT END                                                   04220000
                   SET PS-END-OF-DATE-FILE TO TRUE.                     04230000
           CLOSE DATE-CARD-FILE.                                        04240000
                                                                        04250000
           IF PS-END-OF-DATE-FILE                                       04260000
               MOVE '1300-PROCESS-DATE-CARD'  TO PV-PARAGRAPH-NAME      04270000
               MOVE 'DATE CARD MISSING      ' TO PV-OPERATION-MSG-1     04280000
               SET AC-CONTROL-CARD-ERROR TO TRUE                        04290000
               PERFORM 9999-ABEND                                       04300000
           END-IF.                                                      04310000
                                                                        04320000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              04330000
                                                                        04340000
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   04350000
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   04360000
           SET  DPG52-LK-DTE-GREG            TO TRUE.                   04370000
           MOVE CC-POS-DATE-MMDDYY           TO DPG52-LK-DATE-INPUT.    04380000
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    04390000
                                                   DPG54 DPG55 DPG56.   04400000
           EVALUATE TRUE                                                04410000
               WHEN DPG54-SEVERE-ERROR                                  04420000
               WHEN DPG54-DATE-INVALID                                  04430000
                   MOVE '1300-PROCESS-DATE-CARD'                        04440000
                                                 TO PV-PARAGRAPH-NAME   04450000
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              04460000
                                                 TO PV-OPERATION-MSG-1  04470000
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                04480000
                   MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  04490000
                   PERFORM 9999-ABEND                                   04500000
           END-EVALUATE.                                                04510000
                                                                        04520000
           MOVE DPG55-DB2-ISO-DATE-FMT       TO PV-POS-CCYY-MM-DD.      04530000
                                                                        04540000
      ***************************************************************** 04550000
      * 1500-SELECT-TEPPART-ROW                                         04560000
      *     SELECT PARTN_NBR                                            04570000
      ***************************************************************** 04580000
       1500-SELECT-TEPPART-ROW.                                         04590000
                                                                        04600000
           EXEC SQL                                                     04610000
                  SELECT  PARTN_NBR                                     04620000
                    INTO  :EPPART-PARTN-NBR                             04630000
                    FROM  TEPPART                                       04640000
                   WHERE  SLS_DTE       = :PV-POS-CCYY-MM-DD            04650000
                    AND   STR_GP_CDE          = 'A'                     04660000
                    AND   DB_STRUC_CDE        = 'O'                     04670000
           END-EXEC.                                                    04680000
                                                                        04690000
           EVALUATE TRUE                                                04700000
               WHEN SQLCODE = ZERO                                      04710000
                  MOVE EPPART-PARTN-NBR TO PV-DB2-PARTN-NBR             04720000
               WHEN OTHER                                               04730000
                  DISPLAY '**  ABEND  **'                               04740000
                  DISPLAY 'TCKUT072'                                    04750000
                  DISPLAY '1500-SELECT-TEPPART-ROW'                     04760000
                  DISPLAY 'ABENDING ON SELECT OF TEPPART'               04770000
                  DISPLAY 'SQLCODE = ' SQLCODE                          04780000
                  DISPLAY '   DATE = ' PV-POS-CCYY-MM-DD                04790000
                  MOVE '1500-SELECT-TEPPART-ROW'                        04800000
                                             TO PV-PARAGRAPH-NAME       04810000
                  MOVE 'ERROR SELECT OF TEPPART ROWS'                   04820000
                                             TO PV-DB2-OPERATION        04830000
                  PERFORM 9100-SQL-ABEND                                04840000
           END-EVALUATE.                                                04850000
                                                                        04860000
      *1500-SELECT-TEPPART-ROW.                                         04870000
                                                                        04880000
      ***************************************************************** 04890000
      * 2000-OPEN-CRD-CURSOR                                            04900000
      *     OPEN THE CRD-CURSOR.                                        04910000
      ***************************************************************** 04920000
       2000-OPEN-CRD-CURSOR.                                            04930000
                                                                        04940000
           EXEC SQL                                                     04950000
                OPEN CRD-CURSOR                                         04960000
           END-EXEC.                                                    04970000
                                                                        04980000
           EVALUATE TRUE                                                04990000
               WHEN SQLCODE = ZERO                                      05000000
                   CONTINUE                                             05010000
               WHEN SQLWARN0 NOT = SPACES                               05020000
               WHEN SQLCODE NOT = ZEROES                                05030000
                   MOVE '2000-OPEN-CRD-CURSOR'                          05040000
                                             TO PV-PARAGRAPH-NAME       05050000
                   MOVE 'ERROR ON OPEN OF CRD-CURSOR'                   05060000
                                             TO PV-DB2-OPERATION        05070000
                   PERFORM 9100-SQL-ABEND                               05080000
           END-EVALUATE.                                                05090000
                                                                        05100000
      *2000-OPEN-CRD-CURSOR-EXIT.                                       05110000
                                                                        05120000
      ***************************************************************** 05130000
      * 2001-OPEN-PAY-CURSOR                                            05140000
      *     OPEN THE PAY-CURSOR.                                        05150000
      ***************************************************************** 05160000
       2001-OPEN-PAY-CURSOR.                                            05170000
                                                                        05180000
           EXEC SQL                                                     05190000
                OPEN PAY-CURSOR                                         05200000
           END-EXEC.                                                    05210000
                                                                        05220000
           EVALUATE TRUE                                                05230000
               WHEN SQLCODE = ZERO                                      05240000
                   CONTINUE                                             05250000
               WHEN SQLWARN0 NOT = SPACES                               05260000
               WHEN SQLCODE NOT = ZEROES                                05270000
                   MOVE '2001-OPEN-PAY-CURSOR'                          05280000
                                             TO PV-PARAGRAPH-NAME       05290000
                   MOVE 'ERROR ON OPEN OF PAY-CURSOR'                   05300000
                                             TO PV-DB2-OPERATION        05310000
                   PERFORM 9100-SQL-ABEND                               05320000
           END-EVALUATE.                                                05330000
                                                                        05340000
      *2001-OPEN-PAY-CURSOR-EXIT.                                       05350000
                                                                        05360000
      ***************************************************************** 05370000
      * 2100-PROCESS-CRDS.                                              05380000
      *     PROCESS EACH ROW IN THE CRD CURSOR.                         05390000
      ***************************************************************** 05400000
       2100-PROCESS-CRDS.                                               05410000
                                                                        05420000
           PERFORM 2150-FETCH-CRD-CURSOR.                               05430000
                                                                        05440000
           IF PS-END-OF-CRD-CSR-N                                       05450000
              PERFORM 3000-SELECT-ACCT-CNV                              05451000
              IF EPTRN-PARTN-NBR = PV-PREV-PARTN-NBR                    05470000
                 CONTINUE                                               05480000
              ELSE                                                      05490000
                 MOVE EPTRN-PARTN-NBR TO PV-PREV-PARTN-NBR              05500000
              END-IF                                                    05510000
           END-IF.                                                      05520000
                                                                        05530000
      *2100-PROCESS-CRDS-EXIT.                                          05540000
                                                                        05550000
      ***************************************************************** 05560000
      * 2101-PROCESS-PYMNTS.                                            05570000
      *     PROCESS EACH ROW IN THE PAY CURSOR.                         05580000
      ***************************************************************** 05590000
       2101-PROCESS-PYMNTS.                                             05600000
                                                                        05610000
           PERFORM 2151-FETCH-PAY-CURSOR.                               05620000
                                                                        05630000
           IF PS-END-OF-PAY-CSR-N                                       05640000
              PERFORM 3000-SELECT-ACCT-CNV                              05650000
              IF EPTRN-PARTN-NBR = PV-PREV-PARTN-NBR                    05660000
                 CONTINUE                                               05670000
              ELSE                                                      05680000
                 MOVE EPTRN-PARTN-NBR TO PV-PREV-PARTN-NBR              05690000
              END-IF                                                    05700000
           END-IF.                                                      05710000
                                                                        05720000
      *2101-PROCESS-PYMNTS-EXIT.                                        05730000
                                                                        05740000
      ***************************************************************** 05750000
      * 2150-FETCH-CRD-CURSOR                                           05760000
      *     FETCH THE CRD-CURSOR                                        05770000
      ***************************************************************** 05780000
       2150-FETCH-CRD-CURSOR.                                           05790000
                                                                        05800000
           EXEC SQL                                                     05810000
                FETCH CRD-CURSOR                                        05820000
                 INTO :EPTRN-PARTN-NBR                                  05830000
                     ,:EPTRN-STR-NBR                                    05840000
                     ,:EPTRN-RGST-ID                                    05850000
                     ,:EPTRN-TRN-NBR                                    05860000
                     ,:EPTRN-STRT-TM                                    05870000
                     ,:EPTRN-SLS-CORR-SEQ-NBR                           05880000
                     ,:EPCRD-TNDR-SEQ-NBR                               05890000
                     ,:EPTND-TNDR-ID                                    05900000
                     ,:EPTND-TNDR-ID-SRC-CDE                            05901000
                     ,:EPTND-TNDR-ID-LEN-NBR                            05910000
           END-EXEC.                                                    05920000
                                                                        05930000
           EVALUATE TRUE                                                05940000
               WHEN SQLCODE = ZERO                                      05950000
                   ADD 1                      TO PV-CRD-CURSOR-CNT      05960000
                   MOVE EPTND-TNDR-ID         TO PV-KC-ACCT             05962000
                   MOVE EPTND-TNDR-ID-SRC-CDE TO PV-KC-ACCT-SRC-CDE     05963000
                   MOVE EPTND-TNDR-ID-LEN-NBR TO PV-KC-ACCT-LENGTH      05964000
                                                                        05965000
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          05970000
                   SET PS-END-OF-CRD-CSR-Y                              05980000
                                              TO TRUE                   05990000
               WHEN SQLWARN0 NOT = SPACES                               06000000
               WHEN SQLCODE NOT = ZEROES                                06010000
                   MOVE '2150-FETCH-CRD-CURSOR'                         06020000
                                              TO PV-PARAGRAPH-NAME      06030000
                   MOVE 'ERROR ON FETCH OF CRD-CURSOR '                 06040000
                                              TO PV-DB2-OPERATION       06050000
                   MOVE PV-DB2-PARTN-NBR      TO PV-ABEND-PARTN-NBR     06060000
                   PERFORM 9100-SQL-ABEND                               06070000
           END-EVALUATE.                                                06080000
                                                                        06090000
      *2150-FETCH-CRD-CURSOR-EXIT.                                      06100000
                                                                        06110000
      ***************************************************************** 06120000
      * 2151-FETCH-PAY-CURSOR                                           06130000
      *     FETCH THE PAY-CURSOR                                        06140000
      ***************************************************************** 06150000
       2151-FETCH-PAY-CURSOR.                                           06160000
                                                                        06170000
           EXEC SQL                                                     06180000
                FETCH PAY-CURSOR                                        06190000
                 INTO :EPTRN-PARTN-NBR                                  06200000
                     ,:EPTRN-STR-NBR                                    06210000
                     ,:EPTRN-RGST-ID                                    06220000
                     ,:EPTRN-TRN-NBR                                    06230000
                     ,:EPTRN-STRT-TM                                    06240000
                     ,:EPTRN-SLS-CORR-SEQ-NBR                           06250000
                     ,:EPCRD-TNDR-SEQ-NBR                               06260000
                     ,:EPPAY-KC-ACCT-NBR                                06261000
                     ,:EPPAY-KC-ACCT-SRC-CDE                            06262000
                     ,:PV-KC-ACCT-LENGTH                                06263000
           END-EXEC.                                                    06270000
                                                                        06280000
           EVALUATE TRUE                                                06290000
               WHEN SQLCODE = ZERO                                      06300000
                   ADD 1                      TO PV-PAY-CURSOR-CNT      06310000
                   MOVE EPPAY-KC-ACCT-NBR     TO PV-KC-ACCT             06311000
                   MOVE EPPAY-KC-ACCT-SRC-CDE TO PV-KC-ACCT-SRC-CDE     06312000
                   MOVE PV-KC-ACCT-LENGTH     TO PV-KC-ACCT-LENGTH      06313000
                                                                        06314000
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          06320000
                   SET PS-END-OF-PAY-CSR-Y                              06330000
                                              TO TRUE                   06340000
               WHEN SQLWARN0 NOT = SPACES                               06350000
               WHEN SQLCODE NOT = ZEROES                                06360000
                   MOVE '2151-FETCH-PAY-CURSOR'                         06370000
                                              TO PV-PARAGRAPH-NAME      06380000
                   MOVE 'ERROR ON FETCH OF PAY-CURSOR '                 06390000
                                              TO PV-DB2-OPERATION       06400000
                   MOVE PV-DB2-PARTN-NBR      TO PV-ABEND-PARTN-NBR     06410000
                   PERFORM 9100-SQL-ABEND                               06420000
           END-EVALUATE.                                                06430000
                                                                        06440000
      *2151-FETCH-PAY-CURSOR-EXIT.                                      06450000
                                                                        06460000
      ***************************************************************** 06470000
      * 2200-CLOSE-CRD-CURSOR                                           06480000
      *     CLOSE THE TRAN-CURSOR                                       06490000
      ***************************************************************** 06500000
       2200-CLOSE-CRD-CURSOR.                                           06510000
                                                                        06520000
           EXEC SQL                                                     06530000
                CLOSE CRD-CURSOR                                        06540000
           END-EXEC.                                                    06550000
                                                                        06560000
           EVALUATE TRUE                                                06570000
               WHEN SQLCODE = ZERO                                      06580000
                    CONTINUE                                            06590000
               WHEN SQLWARN0 NOT = SPACES                               06600000
               WHEN SQLCODE NOT = ZEROES                                06610000
                   MOVE '2200-CLOSE-CRD-CURSOR'                         06620000
                                             TO PV-PARAGRAPH-NAME       06630000
                   MOVE 'ERROR ON CLOSE OF CRD-CURSOR'                  06640000
                                             TO PV-DB2-OPERATION        06650000
                   PERFORM 9100-SQL-ABEND                               06660000
           END-EVALUATE.                                                06670000
           MOVE EPPART-PARTN-NBR             TO PV-DB2-PARTN-NBR.       06680000
                                                                        06690000
      *2200-CLOSE-CRD-CURSOR-EXIT.                                      06700000
                                                                        06710000
      ***************************************************************** 06720000
      * 2201-CLOSE-PAY-CURSOR                                           06730000
      *     CLOSE THE PAY-CURSOR                                        06740000
      ***************************************************************** 06750000
       2201-CLOSE-PAY-CURSOR.                                           06760000
                                                                        06770000
           EXEC SQL                                                     06780000
                CLOSE PAY-CURSOR                                        06790000
           END-EXEC.                                                    06800000
                                                                        06810000
           EVALUATE TRUE                                                06820000
               WHEN SQLCODE = ZERO                                      06830000
                    CONTINUE                                            06840000
               WHEN SQLWARN0 NOT = SPACES                               06850000
               WHEN SQLCODE NOT = ZEROES                                06860000
                   MOVE '2201-CLOSE-PAY-CURSOR'                         06870000
                                             TO PV-PARAGRAPH-NAME       06880000
                   MOVE 'ERROR ON CLOSE OF PAY-CURSOR'                  06890000
                                             TO PV-DB2-OPERATION        06900000
                   PERFORM 9100-SQL-ABEND                               06910000
           END-EVALUATE.                                                06920000
                                                                        06930000
      *2201-CLOSE-PAY-CURSOR-EXIT.                                      06940000
                                                                        06950000
      ***************************************************************** 06960000
      * 3000-SELECT-ACCT-CNV                                            06970000
      *     SELECT THE ACCOUNT CONVERSION RECORD.                       06980000
      ***************************************************************** 06990000
       3000-SELECT-ACCT-CNV.                                            07000000
                                                                        07000100
           MOVE 'N'                           TO PV-ACCT-FOUND.         07001900
           IF PV-KC-ACCT-LENGTH = 18                                    07002000
               EXEC SQL                                                 07002100
                   SELECT 'Y'                                           07002200
                     INTO :PV-ACCT-FOUND                                07002300
                     FROM ART_CNV_ACCT_NBR                              07002400
                    WHERE KC_ACCT_NBR = :PV-KC-ACCT                     07002500
               END-EXEC                                                 07002600
           ELSE                                                         07002700
               EXEC SQL                                                 07002800
                   SELECT 'Y'                                           07002900
                     INTO :PV-ACCT-FOUND                                07003000
                     FROM ART_CNV_ACCT_NBR                              07003100
                    WHERE KC_ACCT_NBR = :PV-KC-ACCT-FIRST-10            07003200
               END-EXEC                                                 07003300
           END-IF.                                                      07003400
                                                                        07003500
           EVALUATE TRUE                                                07003600
               WHEN SQLCODE = ZERO                                      07003700
                   PERFORM 8500-WRITE-UPDT-RECORD                       07003800
                                                                        07003900
               WHEN SQLCODE = PC-ROW-NOT-FOUND                          07004000
                   CONTINUE                                             07004100
                                                                        07004200
               WHEN SQLWARN0 NOT = SPACES                               07004300
               WHEN SQLCODE NOT = ZEROES                                07004400
                   MOVE '3000-SELECT-ACCT-CNV '                         07004500
                                              TO PV-PARAGRAPH-NAME      07004600
                   MOVE 'ERROR ON SELECT OF ACCT      '                 07004700
                                              TO PV-DB2-OPERATION       07004800
                   DISPLAY ' ACCT = ', PV-KC-ACCT                       07004900
                   MOVE PV-DB2-PARTN-NBR      TO PV-ABEND-PARTN-NBR     07005000
                   PERFORM 9100-SQL-ABEND                               07005100
           END-EVALUATE.                                                07005200
                                                                        07005300
      *3000-SELECT-ACCT-CNV-EXIT.                                       07006000
                                                                        07010000
      ***************************************************************** 07011000
      * 8500-WRITE-UPDT-RECORD                                          07012000
      *     WRITE THE OUTPUT RECORD WITH ITEM UPDATE DETAILS.           07013000
      ***************************************************************** 07014000
       8500-WRITE-UPDT-RECORD.                                          07015000
                                                                        07016000
           INITIALIZE TC072-TRAN-UPDT-RECORD.                           07020000
           MOVE EPTRN-PARTN-NBR        TO TC072-EPTRN-PARTN-NBR.        07030000
           MOVE EPTRN-STR-NBR          TO TC072-EPTRN-STR-NBR.          07040000
           MOVE EPTRN-RGST-ID          TO TC072-EPTRN-RGST-ID.          07050000
           MOVE EPTRN-TRN-NBR          TO TC072-EPTRN-TRN-NBR.          07060000
           MOVE EPTRN-STRT-TM          TO TC072-EPTRN-STRT-TM.          07070000
           MOVE EPTRN-SLS-CORR-SEQ-NBR TO TC072-EPTRN-SLS-CORR-SEQ-NBR. 07080000
           MOVE EPCRD-TNDR-SEQ-NBR     TO TC072-EPCRD-TNDR-SEQ-NBR.     07090000
           IF PS-END-OF-CRD-CSR-Y                                       07100000
               MOVE 'P'                TO TC072-UPDATE-TYPE             07110000
               ADD 1                   TO PV-PAY-UPDATE-CNT             07111000
           ELSE                                                         07120000
               MOVE 'C'                TO TC072-UPDATE-TYPE             07130000
               ADD 1                   TO PV-CRD-UPDATE-CNT             07131000
           END-IF.                                                      07140000
                                                                        07150000
           WRITE TRAN-UPDT-RECORD                                       07160000
               FROM TC072-TRAN-UPDT-RECORD.                             07170000
                                                                        07180000
      *8500-WRITE-UPDT-RECORD-EXIT.                                     07190000
                                                                        07200000
      ***************************************************************** 07210000
      * 9100-SQL-ABEND                                                  07220000
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.07230000
      ***************************************************************** 07240000
       9100-SQL-ABEND.                                                  07250000
           DISPLAY '** ABEND     **'.                                   07260000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        07270000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              07280000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               07290000
           DISPLAY '** PARTN NBR ** = ' PV-ABEND-PARTN-NBR.             07300000
           DISPLAY '** STORE NBR ** = ' PV-ABEND-STR-NBR.               07310000
           DISPLAY '** RGST ID   ** = ' PV-ABEND-RGST-ID.               07320000
           DISPLAY '** TRAN NBR  ** = ' PV-ABEND-TRN-NBR.               07330000
                                                                        07340000
      ***************************************************************** 07350000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     07360000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      07370000
      ***************************************************************** 07380000
           COPY DPPD004.                                                07390000
                                                                        07400000
           SET AC-DB2-ERROR TO TRUE.                                    07410000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07420000
                                                                        07430000
      ***************************************************************** 07440000
      * 9999-ABEND                                                      07450000
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  07460000
      ***************************************************************** 07470000
       9999-ABEND.                                                      07480000
           DISPLAY '** ABEND           **'.                             07490000
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  07500000
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        07510000
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       07520000
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       07530000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         07540000
