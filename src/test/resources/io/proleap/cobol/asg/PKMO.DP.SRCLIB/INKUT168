      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    INKUT168.                                                 
       AUTHOR.        J SEIDLER.                                                
       DATE-WRITTEN.  AUGUST 17, 2000.                                          
      *                                                                         
      *****************************************************************         
      *    WRITES THE TOTAL SALES FOR EACH STORE                                
      *                                                                         
      *    THIS TOTAL SALES FILE WILL BE USED IN THE INKUT172 - SALES           
      *    ADJUSTMENT CALC PROCESS.                                             
      *****************************************************************         
RB1002*    12/09/2002  RONNA BUTZ   WR24029                                     
RB1002*    NO PROGRAM CHANGES MADE, BUT COPYBOOK INRD016 CHANGED                
RB1002*    AND THIS PROGRAM NEEDS TO BE RECOMPILED.                             
RB1002*****************************************************************         
W21732*    01/18/2003  J.S.NAVIS RAJA,IGSI    WR# W21732                        
W21732*    INCREASED THE RECORD LENGTH OF POS-REC TO 50 BYTES                   
W21732*    SINCE THE COPYBOOK INRD016 WAS CHANGED.                              
W21732*****************************************************************         
W26600*    08-17-2004  M JOHNSON  W26600                                        
W26600*    STORE NUMBER EXPANSION                                               
W26600*                                                                         
W26600*****************************************************************         
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
      ***************************************************************           
       INPUT-OUTPUT SECTION.                                                    
      ***************************************************************           
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT POS-SALES-FILE                                                
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STR-TTL-SLS-FILE                                              
               ASSIGN TO OUT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD  POS-SALES-FILE                                                       
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 CHARACTERS                                          
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
W21732 01  POS-REC                         PIC X(50).                           
                                                                                
       FD  STR-TTL-SLS-FILE                                                     
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                     INKUT169
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  STR-TTL-SLS-REC                 PIC X(30).                           
                                                                                
                                                                        INKUT115
      *                                                                         
      ***************************************************************           
       WORKING-STORAGE SECTION.                                                 
      ***************************************************************           
      *                                                                         
      ** WORKING STORAGE FOR INPUT - POS FILE FOR INVENTORY                     
      *01  IN016-POS-ITEM-DETAIL.                                               
W26600     COPY INRD016.                                                        
                                                                                
      ** WORKING STORAGE FOR OUTPUT - STORE TOTAL SALES                         
      *01  IN027-TOTAL-SALES-REC.                                               
W26600     COPY INRD027.                                                        
                                                                                
      *                                                                         
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-POS-TRAN-DTE             PIC 9(07)       VALUE ZEROES         
W26600                         COMP-3.                                          
W26600     05  PV-PREV-STR-NBR             PIC 9(04)       VALUE ZEROES.        
JS0600     05  PV-STR-TRAN-QTY             PIC S9(07)      VALUE ZEROES         
W26600                         COMP-3.                                          
JS0600     05  PV-STR-RTL-SLS              PIC S9(09)V99   VALUE ZEROES         
W26600                         COMP-3.                                          
W26600     05  PV-STR-RETURN-QTY           PIC S9(07)      VALUE ZEROES         
W26600                         COMP-3.                                          
W26600     05  PV-STR-RETURN-RTL-AMT       PIC S9(09)V99   VALUE ZEROES         
W26600                         COMP-3.                                          
JS0600     05  PV-STR-TRAN-QTY-P           PIC -Z,ZZZ,ZZ9  VALUE ZEROES.        
JS0600     05  PV-STR-RTL-SLS-P            PIC -$$$,$$$,$$9.99                  
                                                           VALUE ZEROES.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-POS-SW            PIC X(01)       VALUE 'N'.           
               88  PS-END-POS-FILE                         VALUE 'Y'.           
                                                                                
       01  ABEND-CODE                      PIC S9(4)       VALUE ZEROES         
                               COMP.                                            
                                                                                
      ***************************************************************           
       PROCEDURE DIVISION.                                                      
      ***************************************************************           
      *                                                                         
           PERFORM A100-MAINLINE.                                               
           GOBACK.                                                              
      *                                                                         
      ***************************************************************           
       A100-MAINLINE.                                                           
      ***************************************************************           
      *                                                                         
           PERFORM 1000-OPEN-FILES.                                             
                                                                                
           PERFORM 7200-READ-POS-FILE.                                          
           PERFORM 2000-PROCESS-FILES                                           
               UNTIL  PS-END-POS-FILE.                                          
                                                                                
           PERFORM 8900-CLOSE-FILES.                                            
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *                                                              *          
      ****************************************************************          
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  POS-SALES-FILE                                           
                OUTPUT STR-TTL-SLS-FILE.                                        
                                                                                
      ****************************************************************          
      *                                                              *          
      ****************************************************************          
       2000-PROCESS-FILES.                                                      
                                                                                
           MOVE IN016-STR-NBR  TO PV-PREV-STR-NBR.                              
           MOVE IN016-TRAN-DTE TO PV-POS-TRAN-DTE.                              
           MOVE ZEROES         TO  PV-STR-TRAN-QTY                              
                                   PV-STR-RTL-SLS                               
                                   PV-STR-RETURN-QTY                            
                                   PV-STR-RETURN-RTL-AMT.                       
                                                                                
           PERFORM                                                              
               UNTIL PS-END-POS-FILE                                            
                   OR IN016-STR-NBR NOT = PV-PREV-STR-NBR                       
               PERFORM 5400-CALC-TOTAL-SALES                                    
               PERFORM 7200-READ-POS-FILE                                       
           END-PERFORM                                                          
                                                                                
           MOVE PV-STR-TRAN-QTY TO    PV-STR-TRAN-QTY-P.                        
           MOVE PV-STR-RTL-SLS  TO    PV-STR-RTL-SLS-P.                         
           PERFORM 7500-WRITE-STR-TTL-SLS.                                      
           DISPLAY 'STR ' PV-PREV-STR-NBR                                       
                   ' QTY ' PV-STR-TRAN-QTY-P                                    
                   ' SLS ' PV-STR-RTL-SLS-P.                                    
                                                                                
      ******************************************************************        
      * CALCULATE TOTAL SALES FOR THE STORE - AT THE REGULAR UNIT PRICE         
      ******************************************************************        
                                                                                
       5400-CALC-TOTAL-SALES.                                                   
                                                                                
           IF IN016-SALE                                                        
               COMPUTE PV-STR-TRAN-QTY =                                        
                   IN016-TRAN-QTY + PV-STR-TRAN-QTY                             
               COMPUTE PV-STR-RTL-SLS =                                         
                   (IN016-TRAN-QTY * IN016-RTL-AMT) + PV-STR-RTL-SLS            
           ELSE                                                                 
               IF IN016-RETURN                                                  
                   COMPUTE PV-STR-TRAN-QTY =                                    
                       (IN016-TRAN-QTY * -1) + PV-STR-TRAN-QTY                  
                   COMPUTE PV-STR-RTL-SLS =                                     
                       (IN016-TRAN-QTY * IN016-RTL-AMT * -1)                    
                                                + PV-STR-RTL-SLS                
                   COMPUTE PV-STR-RETURN-QTY =                                  
                       (IN016-TRAN-QTY * -1) + PV-STR-RETURN-QTY                
                   COMPUTE PV-STR-RETURN-RTL-AMT =                              
                       (IN016-TRAN-QTY * IN016-RTL-AMT * -1)                    
                                                + PV-STR-RETURN-RTL-AMT         
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
      ****************************************************************          
      * 7200-READ-POS - READS THE POS FILE.                          *          
      ****************************************************************          
JS0600 7200-READ-POS-FILE.                                                      
                                                                                
           READ POS-SALES-FILE INTO                                             
                IN016-POS-ITEM-DETAIL                                           
           AT END                                                               
               SET PS-END-POS-FILE TO TRUE                                      
           END-READ.                                                            
                                                                                
                                                                                
      ****************************************************************          
      * 7500-WRITE-STR-TTL-SLS                                                  
      ****************************************************************          
JS0600 7500-WRITE-STR-TTL-SLS.                                                  
                                                                                
           MOVE PV-PREV-STR-NBR         TO IN027-STR-NBR.                       
           MOVE PV-POS-TRAN-DTE         TO IN027-POS-TRAN-DTE.                  
           MOVE PV-STR-TRAN-QTY         TO IN027-TRAN-QTY.                      
           MOVE PV-STR-RTL-SLS          TO IN027-RTL-AMT.                       
           MOVE PV-STR-RETURN-QTY       TO IN027-RETURN-QTY.                    
           MOVE PV-STR-RETURN-RTL-AMT   TO IN027-RETURN-RTL-AMT.                
                                                                                
           WRITE STR-TTL-SLS-REC FROM IN027-TOTAL-SALES-REC.                    
                                                                                
                                                                                
      ****************************************************************          
      * 8900-CLOSE-FILES.                                                       
      ****************************************************************          
       8900-CLOSE-FILES.                                                        
                                                                                
           CLOSE POS-SALES-FILE                                                 
                 STR-TTL-SLS-FILE.                                              
