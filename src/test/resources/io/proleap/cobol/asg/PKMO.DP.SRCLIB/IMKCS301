      ******************************************************************00010099
       IDENTIFICATION DIVISION.                                         00020099
      ******************************************************************00030099
                                                                        00040099
       PROGRAM-ID.             IMKCS301.                                00050099
       AUTHOR.                 PAUL KEBER.                              00060099
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070099
       DATE-WRITTEN.           10/04/2001.                              00080099
       DATE-COMPILED.                                                   00090099
                                                                        00100099
      ******************************************************************00110099
      *  IMKCS301 - ITEM MESSAGE BRIDGE TO MQSERIES QUEUE DUMP          00120099
      ******************************************************************00130099
      *          CICS COBOL II WITH DB2 AND MQ SERIES                   00140099
      ******************************************************************00150099
      ***  CAUTION!!!!  THE I301 TRANSACTION RUNNING IN CICSP6 MUST BE  00160099
      ***              SHUT DOWN BEFORE THIS PROGRAM CAN BE MOVED INTO  00170099
      ***              PRODUCTION.  CICSP6 IS SHUT DOWN AT 2:00 AM.     00180099
      ***          DPK620 IS STARTED AFTER CICSP6 IS DOWN AND WILL COPY 00190099
      ***          THE PROGRAM FROM THE STAGE LIB TO PKMO.CICS.LOADLIB. 00200099
      ***          DPK935 DOES THE DB2 BIND BEFORE CICSP6 IS RESTARTED. 00210099
      ******************************************************************00220099
      * THIS IS A CICS PROGRAM WHICH WILL READ AND DELETE ALL ROWS ON   00230099
      * THE ITEM EVENT MESSAGE BRIDGE DB2 TABLE (IMT_EVNT_MSG_BRG) AND  00240099
      * DUMP THEM TO AN MQ SERIES QUEUE.  EACH RECORD BEING DUMPED WILL 00250099
      * ALSO BE WRITTEN TO THE ITEM BRIDGE BACKUP VSAM FILE (DDNAME     00260099
      * IMBRGBKP) FOR BACKUP PURPOSES.  THIS PROGRAM WILL THEN ISSUE    00270099
      * A CICS DELAY COMMAND AND REPEAT THIS PROCESS THROUGHOUT THE     00280099
      * PROCESSING DAY UNTIL A BUSINESS EVENT END PROCESS RECORD IS     00290099
      * READ.  THE PROGRAM WILL ALSO DELAY FOR TCKRSTCNTL-TRANS-QTY     00300099
      * MINUTES AFTER PROCESSING TCKRSTCNTL-TRANS-PRCSD-QTY RECORDS     00310099
      * IN ORDER TO ALLOW RMS TO CATCH UP WHILE PROCESSING TRANSACTIONS.00320099
      *                                                                 00330099
      * BEFORE THE PROGRAM IS STOPPED AFTER THE END PROCESS RECORD HAS  00340099
      * BEEN PROCESSED, THE IMK042 (VSM BRIDGE BACKUP VSAM FILE DUMP)   00350099
      * JOB DECK IS WRITTEN OUT, LINE BY LINE, TO THE INTERNAL READER.  00360099
      * AFTER THE JOB DECK HAS BEEN COMPLETELY WRITTEN OUT TO THE       00370099
      * INTERNAL READER, THE INTERNAL READER IS DEQUEUED AND THE PROGRAM00380099
      * IS STOPPED.                                                     00390099
      ******************************************************************00400099
      *                                                                 00410099
      * 02/26/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00420099
      *   CHANGED THE QA AND TEST QUEUE MANAGER NAME FROM KOHLNT14 TO   00430099
      *   ISMQI01T.  ADDED DELAY LOGIC FOR A TIME PERIOD A PREDETERMINED00440099
      *   NUMBER OF TIMES IF THE MQSERIES QUEUE IS FULL DURING AN MQPUT.00450099
      ******************************************************************00460099
      *                                                                 00470099
      * 03/17/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00480099
      *   REMOVED THE CHECK WHICH WOULD NOT ALLOW A COMMIT TO OCCUR IN  00490099
      *   THE MIDDLE OF AN RMS STYLE ID.                                00500099
      ******************************************************************00510099
      *                                                                 00520099
      * 05/05/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00530099
      *   CHANGED THE QA CORRELATION ID NAME FROM KSP31011 TO ISWMQ04Q. 00540099
      *   CHANGED THE TEST CORRELATION ID NAME FROM KSP31009 TO         00550099
      *   ISWMQ04T.                                                     00560099
      ******************************************************************00570099
      *                                                                 00580099
      * 06/06/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00590099
      *   ADDED A LEARNING REGION CORRELATION ID OF ISWMQ04L.           00600099
      *   ADDED A LEARNING JOB OF IMK042.                               00610099
      ******************************************************************00620099
      *                                                                 00630099
      * 07/11/2003  P. KEBER      WR25406 - PERM PRICE RED TICKET CHGS  00640099
      *   SET THE MQ PRIORITY OF EVENTS 75, 80, AND 997 TO 4 INSTEAD OF 00650099
      *   5 SO THAT THEY ARE PROCESSED AFTER ALL OTHER MESSAGES.        00660099
      ******************************************************************00670099
      *                                                                 00680099
      * 08/05/2003  P. KEBER      WR24405 - PRICING & STATUS CHGS       00690099
      *   CHANGED THE PROD CORRELATION ID NAME FROM KSP32017 TO         00700099
      *   ISWMQ04P.                                                     00710099
      ******************************************************************00720099
      *                                                                 00730099
      * 09/22/2003  P. KEBER      WR25653 - RED TICKET RUN IMPROVEMENTS 00740099
      *   ADDED A SECOND MQSERIES WRITE TO XS.ITEM.ATTRIBUTES.Q.        00750099
      ******************************************************************00760099
      *                                                                 00770099
      * 11/10/2003  P. KEBER      WR26113 - NEW MQ INTEGRATOR SERVER    00780099
      *   CHANGED THE NAME OF THE MQ INTEGRATOR SERVER FROM KSP12017 TO 00790099
      *   ISMQI01P.                                                     00800099
      ******************************************************************00810099
      *                                                                 00820099
      * 04/27/2004  P. KEBER      WR25653 - RED TICKET RUN IMPROVEMENTS 00830099
      *   SET THE MQ PRIORITY OF EVENTS 75, 80, AND 997 TO 3 FROM 4.    00840099
      ******************************************************************00850099
      *                                                                 00860099
      * 02/17/2005  P. KEBER      WR25653 - RED TICKET RUN IMPROVEMENTS 00870099
      *   RESET THE MQ PRIORITY OF ALL EVENTS TO 5 SINCE THERE ARE NO   00880099
      *   LONGER PERFORMANCE ISSUES REQUIRING A LOWER PRIORITY FOR      00890099
      *   EVENTS 75, 80, AND 997.                                       00900099
      ******************************************************************00910099
      *                                                                 00920099
      * 06/29/2005  P. KEBER      WR28048 - PRICE OPTIMIZATION          00930099
      *   DON'T PUT 075 RECORDS WITH A CRE-BY-USR-ID VALUE OF 'IMKUP041'00940099
      *   OR 080 RECORDS WITH A RED-TKT-RUN-IND VALUE OF 'Y' TO THE     00950099
      *   XS ITEM MQSERIES QUEUE.                                       00960099
      ******************************************************************00970099
      *                                                                 00980099
      * 04/06/2006  P. KEBER      WR29925 - STYLE DELETES CHGS          00990099
      *   SET THE MQ PRIORITY OF EVENTS 30, 31, AND 998 TO 3 INSTEAD OF 01000099
      *   5 SO THAT THEY ARE PROCESSED AFTER ALL OTHER MESSAGES.        01010099
      ******************************************************************01020099
      *                                                                 01030099
      * 04/07/2006  P. KEBER      WR30615 - MQSI NEW QUEUE MANAGERS     01040099
      *   CHANGED THE QUEUE MANAGER NAMES FOR TEST, QA, LEARN, AND PROD.01050099
      ******************************************************************01060099
      *                                                                 01070099
      * 07/05/2006  P. KEBER      WR29434 - SAS MARKDOWN CHANGES        01080099
      *   CHANGE TO NOT WRITE A 075 RECORD TO THE XS.ITEM.ATTRIBUTES.Q  01090099
      *   QUEUE IF IT IS CREATED BY IMKUP039 OR IMKUP041, AND TO NOT    01100099
      *   WRITE A 080 RECORD IF IM008-STR-RTL-PERM-RUN-IND IS EQUAL TO  01110099
      *   'R' OR 'Y'.                                                   01120099
      ******************************************************************01130099
      *                                                                 01140099
      * 09/27/2006  P. KEBER      WR30627 - PRIVATE BRAND ACCOUNTING    01150099
      *   TEMPORARILY SET THE MQ PRIORITY OF EVENT 65 TO 2 INSTEAD OF   01160099
      *   5 SO THAT THEY ARE PROCESSED AFTER ALL OTHER MESSAGES.        01170099
      ******************************************************************01180099
      *                                                                 01190099
      * 10/03/2006  P. KEBER      WR30627 - PRIVATE BRAND ACCOUNTING    01200099
      *   RESET THE MQ PRIORITY OF EVENT 65 BACK TO THE NORMAL 5.       01210099
      ******************************************************************01220099
      *                                                                 01230099
      * 11/09/2010  P. KEBER      PKE000000005791                       01240099
      *   CHANGE TO NOT WRITE A 997 RECORD TO THE MQSI.VSM.ITEM.INQ     01250099
      *   QUEUE SINCE IT IS ONLY WRITTEN TO THE XS QUEUE.               01260099
      ******************************************************************01270099
      *                                                                 01280099
      * 04/19/2013  D. FELLENZ    CHG0050890  MQ HUB UPGRADE            01290099
      *   CHANGED THE QMGR NAMES FROM ISMQI03P TO ISWMQ122P FOR PROD,   01300099
      *   ISMQI01Q TO ISWMQ60Q FOR QA (& STRESS), ISMQI03T TO ISWMQ80T  01310099
      *   FOR TEST.                                                     01320099
213345******************************************************************01330099
213345* 11/20/2018  JIM HUNTER   CHG0213345   MAINFRAME REFACTORING.    01340099
213345* BECAUSE THE DB2 STORED PROCEDUES WON'T BE ABLE TO USE MQ        01350099
213345* AFTER THEY ARE REFACTORED INTO JAVA APPS, THE MQ LOGIC IS       01360099
213345* BEING REPLACED WITH INSERTS TO THE IMT_EVNT_MSG_BRG TABLE.      01370099
213345* CURRENTLY THESE PROGRAMS (IMKUT026, IMKUT027 AND IMKUT053)      01380099
213345* ARE INSERTING MESSAGES TO IM.NONVSM.ITEM.CHANGES.Q.  THEY ARE   01390099
213345* BEING CHANGED TO INSERT MESSAGES TO THE IMT_EVNT_MSG_BRG TABLE  01400099
213345* WHICH IMKCS301 WILL READ (NEW BUSINESS EVENT CODES 905, 910,    01410099
213345* 915 AND 930).  THIS PROGRAM WILL THEN INSERT THEM TO            01420099
213345* IM.NONVSM.ITEM.CHANGES.Q, WHICH WILL THEN BE PROCESSED BY JOB   01430099
213345* IMK929.  IM.NONVSM.ITEM.CHANGES.Q INSERT LOGIC IS BEING COPIED  01440099
213345* FROM IMKUT026.                                                  01450099
      ******************************************************************01460099
216771* 01/29/2019  JIM HUNTER   CHG0216771                             01470099
216771* PAR 3200- HAD AN ABEND TRYING TO DELETE A ROW THAT WASN'T       01480099
216771* FOUND.  DISPLAY THE DELETE KEYS WHEN THE CALL FAILS.            01490099
216771* INITIALIZE PV-SAVE-BUS-EVNT-TYP-CDE IN PAR 2000- TO PREVENT     01500099
216771* FUTURE ABENDS.                                                  01510099
216771******************************************************************01520099
       ENVIRONMENT DIVISION.                                            01530099
      ******************************************************************01540099
                                                                        01550099
       CONFIGURATION SECTION.                                           01560099
                                                                        01570099
       SOURCE-COMPUTER.        IBM-3090.                                01580099
       OBJECT-COMPUTER.        IMB-3090.                                01590099
                                                                        01600099
      ******************************************************************01610099
       DATA DIVISION.                                                   01620099
      ******************************************************************01630099
                                                                        01640099
       WORKING-STORAGE SECTION.                                         01650099
                                                                        01660099
       01  PC-PROGRAM-CONSTANTS.                                        01670099
           05  PC-OUT-MSGLENGTH        PIC S9(9)    VALUE +450 COMP.    01680099
213345     05  PC-IM-NONVSM-OUT-MSGLENGTH PIC S9(9) VALUE +100 COMP.    01690099
           05  PC-BRIDGE-DOWN-MSG-LEN  PIC S9(8)    VALUE +108 COMP.    01700099
           05  PC-MAX-RETRIES          PIC S9(4)    VALUE 120  COMP.    01710099
           05  PC-MAXIMUM-RETRY-COUNT  PIC S9(4)    VALUE +2   COMP.    01720099
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)     VALUE 'IMKCS301'.   01730099
           05  PC-VSM-BRIDGE-DOWN-MSG  PIC X(108)   VALUE               01740099
               'OPER032 - VSM BRIDGE TO ITEM HUB TRANSACTION FAILURE - C01750099
      -        'ONTACT APPL IM GROUP RESPONSIBLE FOR CICS TRANS I301'.  01760099
           05  PC-JOB-NAME             PIC X(6)     VALUE 'CICS  '.     01770099
           05  PC-QNAME                PIC X(48)    VALUE               01780099
                                           'MQSI.VSM.ITEM.INQ'.         01790099
           05  PC-XS-QNAME             PIC X(48)    VALUE               01800099
                                           'XS.ITEM.ATTRIBUTES.Q'.      01810099
213345     05  PC-IM-QNAME             PIC X(48)    VALUE               01820099
213345                                     'IM.NONVSM.ITEM.CHANGES.Q'.  01830099
           05  PC-QMGR-NAME            PIC X(48)    VALUE SPACES.       01840099
      *                                                      QKSP       01850099
50890          88  PC-PROD-QMGR-NAME                VALUE 'ISWMQ122P'.  01860099
50890 *        88  PC-PROD-QMGR-NAME                VALUE 'ISMQI03P'.   01870099
      *                                                      QKSL       01880099
50890          88  PC-LEARN-QMGR-NAME               VALUE 'ISWMQ60Q'.   01890099
50890 *        88  PC-LEARN-QMGR-NAME               VALUE 'ISMQI01Q'.   01900099
      *                                                      QKSQ       01910099
50890          88  PC-QA-QMGR-NAME                  VALUE 'ISWMQ60Q'.   01920099
50890 *        88  PC-QA-QMGR-NAME                  VALUE 'ISMQI01Q'.   01930099
      *                                                      QKST       01940099
50890          88  PC-TEST-QMGR-NAME                VALUE 'ISWMQ80T'.   01950099
50890 *        88  PC-TEST-QMGR-NAME                VALUE 'ISMQI03T'.   01960099
                                                                        01970099
           05  PC-XS-QMGR-NAME         PIC X(48)    VALUE SPACES.       01980099
      *                                                      QKSP       01990099
               88  PC-XS-PROD-QMGR-NAME             VALUE 'QKSP'.       02000099
      *                                                      QKSL       02010099
               88  PC-XS-LEARN-QMGR-NAME            VALUE 'QKSL'.       02020099
      *                                                      QKSQ       02030099
               88  PC-XS-QA-QMGR-NAME               VALUE 'QKSQ'.       02040099
      *                                                      QKST       02050099
               88  PC-XS-TEST-QMGR-NAME             VALUE 'QKST'.       02060099
                                                                        02070099
           05  PC-CORREL-ID            PIC X(48)    VALUE SPACES.       02080099
      *                                                      QKSP       02090099
               88  PC-PROD-CORREL-ID                VALUE 'ISWMQ04P'.   02100099
      *                                                      QKSL       02110099
               88  PC-LEARN-CORREL-ID               VALUE 'ISWMQ04L'.   02120099
      *                                                      QKSQ       02130099
               88  PC-QA-CORREL-ID                  VALUE 'ISWMQ04Q'.   02140099
      *                                                      QKST       02150099
               88  PC-TEST-CORREL-ID                VALUE 'ISWMQ04T'.   02160099
                                                                        02170099
           05  PC-SYS-ID               PIC X(04).                       02180099
               88  PC-KOHLS-TEST-SYSTEM             VALUE 'CIT1'.       02190099
               88  PC-KOHLS-ACC-TEST-SYSTEM         VALUE 'CIQA',       02200099
                                                          'CIQT',       02210099
                                                          'CILA',       02220099
                                                          'CILT'.       02230099
               88  PC-KOHLS-QA-SYSTEM               VALUE 'CIQA',       02240099
                                                          'CIQT'.       02250099
               88  PC-KOHLS-LEARNING-SYSTEM         VALUE 'CILA',       02260099
                                                          'CILT'.       02270099
               88  PC-KOHLS-PROD-SYSTEM             VALUE 'CIPS',       02280099
                                                          'CIP1',       02290099
                                                          'CIP3',       02300099
                                                          'CIP4',       02310099
                                                          'CIP6'.       02320099
                                                                        02330099
       01  PS-PROGRAM-SWITCHES.                                         02340099
           05  PS-CURSOR-HAS-RECORDS-SW  PIC X(01)  VALUE 'N'.          02350099
               88  PS-CURSOR-HAS-RECORDS            VALUE 'Y'.          02360099
               88  PS-CURSOR-HAS-NO-RECORDS         VALUE 'N'.          02370099
           05  PS-END-OF-DATA-SW         PIC X(01)  VALUE 'N'.          02380099
               88  PS-END-OF-DATA                   VALUE 'Y'.          02390099
               88  PS-NOT-END-OF-DATA               VALUE 'N'.          02400099
           05  PS-END-OF-DAY-SW          PIC X(01)  VALUE 'N'.          02410099
               88  PS-END-OF-DAY                    VALUE 'Y'.          02420099
               88  PS-NOT-END-OF-DAY                VALUE 'N'.          02430099
           05  PS-FETCH-DEADLOCK-SW      PIC X(01)  VALUE 'N'.          02440099
               88  PS-FETCH-DEADLOCK                VALUE 'Y'.          02450099
               88  PS-NOT-FETCH-DEADLOCK            VALUE 'N'.          02460099
           05  PS-MQPUT-SUCCESSFUL-SW    PIC X(01)  VALUE SPACES.       02470099
               88  PS-MQPUT-SUCCESSFUL              VALUE 'Y'.          02480099
           05  PS-INTERNAL-READER-ENQ-DEQ-IND                           02490099
                                         PIC X(1)   VALUE 'D'.          02500099
               88  PS-INTERNAL-READER-ENQUEUED      VALUE 'E'.          02510099
               88  PS-INTERNAL-READER-DEQUEUED      VALUE 'D'.          02520099
                                                                        02530099
       01  PV-PROGRAM-VARIABLES.                                        02540099
           05  PV-HCONN                  PIC S9(9)  VALUE +0 COMP.      02550099
           05  PV-GQ-HANDLE              PIC S9(9)  VALUE +0 COMP.      02560099
           05  PV-PQ-HANDLE              PIC S9(9)  VALUE +0 COMP.      02570099
           05  PV-XS-PQ-HANDLE           PIC S9(9)  VALUE +0 COMP.      02580099
213345     05  PV-IM-PQ-HANDLE           PIC S9(9)  VALUE +0 COMP.      02590099
           05  PV-OPEN-OPTIONS           PIC S9(9)  VALUE +0 COMP.      02600099
           05  PV-MQ-COMPLETION-CODE     PIC S9(9)  VALUE +0 COMP.      02610099
           05  PV-MQ-REASON              PIC S9(9)  VALUE +0 COMP.      02620099
           05  PV-MQGET-COUNTER          PIC S9(9)  VALUE +0 COMP.      02630099
           05  PV-MQPUT-COUNTER          PIC S9(9)  VALUE +0 COMP.      02640099
           05  PV-DATA-LENGTH            PIC S9(9)  VALUE +0 COMP.      02650099
           05  PV-MSG-LENGTH             PIC S9(9)  VALUE +0 COMP.      02660099
           05  PV-RBA                    PIC S9(9)  VALUE +0 COMP.      02670099
           05  PV-RESTART-DELAY-SECONDS  PIC S9(9)  VALUE +0 COMP.      02680099
      *        FILLED FROM TCKRSTCNTL-RST-QTY (MAX OF 359)              02690099
           05  PV-RETRY-DELAY-SECONDS    PIC S9(9)  VALUE +0 COMP.      02700099
      *        FILLED FROM TCKRSTCNTL-CKPT-TAKEN-QTY (MAX OF 359)       02710099
           05  PV-RMS-CATCHUP-DELAY-MINUTES                             02720099
      *        FILLED FROM TCKRSTCNTL-TRANS-QTY (MAX OF 5999)           02730099
                                         PIC S9(9)  VALUE +0 COMP.      02740099
           05  PV-RMS-CATCHUP-RECS-COUNT PIC S9(9)  VALUE +0 COMP.      02750099
           05  PV-FETCH-DEADLOCK-COUNT   PIC S9(9)  VALUE +0 COMP.      02760099
           05  PV-SAVE-COMPLETION-CODE   PIC S9(9)  VALUE +0 COMP.      02770099
           05  PV-SAVE-REASON            PIC S9(9)  VALUE +0 COMP.      02780099
           05  PV-TOTAL-RECS-PROCESSED   PIC S9(9)  VALUE +0 COMP.      02790099
           05  PV-VSAM-RECORD-COUNT      PIC S9(9)  VALUE +0 COMP.      02800099
           05  PV-CICS-RESPONSE          PIC S9(4)  VALUE +0 COMP.      02810099
           05  PV-MQPUT-RETRY-COUNTER    PIC S9(4)  VALUE +0 COMP.      02820099
           05  PV-MQGET-RETRY-COUNTER    PIC S9(4)  VALUE +0 COMP.      02830099
           05  PV-RETRY-COUNT            PIC S9(4)  VALUE +0 COMP.      02840099
           05  PV-MSG-TXT-NULL-IND       PIC S9(4)  VALUE +0 COMP.      02850099
           05  PV-SAVE-RMS-STYL-ID       PIC S9(8)  VALUE +0 COMP-3.    02860099
           05  PV-SAVE-BUS-EVNT-TYP-CDE  PIC S9(3)  VALUE +0 COMP-3.    02870099
           05  PV-SAVE-CRE-TMST          PIC X(26)  VALUE SPACES.       02880099
           05  PV-DISPLAY-RESP           PIC  9(8)  VALUE ZERO.         02890099
           05  PV-PARAGRAPH              PIC X(48)  VALUE SPACES.       02900099
           05  PV-CURRENT-SYSID          PIC X(4)   VALUE SPACES.       02910099
           05  PV-CURRENT-TIMESTAMP      PIC X(26)  VALUE SPACES.       02920099
           05  PV-COMMIT-TIMESTAMP       PIC X(26)  VALUE SPACES.       02930099
           05  PV-ERROR.                                                02940099
               10  PV-ERROR-LITERAL      PIC X(14)  VALUE SPACES.       02950099
               10  PV-ERROR-VARIABLE     PIC X(34)  VALUE SPACES.       02960099
           05  PV-SAVE-ITEM-BRIDGE-MSG   PIC X(450) VALUE SPACES.       02970099
           05  PV-MSGBUFFER              PIC X(450) VALUE SPACES.       02980099
213345     05  PV-IM-NONVSM-MSGBUFFER    PIC X(100) VALUE SPACES.       02990099
           05  PV-NBR-OF-MSGS-CREATED    PIC  9(9)  VALUE ZERO.         03000099
           05  PV-SAVE-PARAGRAPH         PIC X(48)  VALUE SPACES.       03010099
                                                                        03020099
       01  PV-DT-DATE-TIME-AREA.                                        03030099
           05  PV-DT-CURRENT-ABSTIME     PIC S9(15) VALUE +0 COMP-3.    03040099
           05  PV-DT-FORMAT-DATE-MMDDYY  PIC X(8)   VALUE SPACES.       03050099
           05  PV-DT-CURRENT-TIME        PIC X(8)   VALUE SPACES.       03060099
                                                                        03070099
       01  PV-LOG-BRIDGE-STARTUP-MSG.                                   03080099
           05  FILLER                    PIC X(52)  VALUE               03090099
               'BEGINNING ITEM MESSAGE BRIDGE TO MQSERIES QUEUE DUMP'.  03100099
                                                                        03110099
       01  PV-LOG-RMS-CATCHUP-DELAY-MSG.                                03120099
           05  FILLER                    PIC X(31)  VALUE               03130099
               'BEGINNING RMS CATCHUP DELAY OF '.                       03140099
           05  PV-LOG-RCDM-CATCHUP-DLY-MIN PIC ZZ9  VALUE ZEROS.        03150099
           05  FILLER                    PIC X(26)  VALUE               03160099
               ' MINUTES AFTER PROCESSING '.                            03170099
           05  PV-LOG-RCDM-CATCHUP-RECS-CNT                             03180099
                                         PIC Z(8)9  VALUE ZEROS.        03190099
           05  FILLER                    PIC X(08)  VALUE               03200099
               ' RECORDS'.                                              03210099
                                                                        03220099
       01  PV-LOG-TOTAL-RECS-PROCESS-MSG.                               03230099
           05  FILLER                    PIC X(35)  VALUE               03240099
               'EOD TOTAL RECORDS PROCESSED COUNT: '.                   03250099
           05  PV-LOG-TRP-RECS-PROCESS-CNT                              03260099
                                         PIC Z(8)9  VALUE ZEROS.        03270099
           05  FILLER                    PIC X(25)  VALUE               03280099
               '  FETCH DEADLOCKS COUNT: '.                             03290099
           05  PV-LOG-TRP-RECS-DEADLOCL-CNT                             03300099
                                         PIC Z(8)9  VALUE ZEROS.        03310099
                                                                        03320099
       01  PV-SL-SYSTEM-LOG-MSG-AREA.                                   03330099
           05  PV-SL-SYS-LOG-QUEUE-NAME  PIC X(4)   VALUE 'CSSL'.       03340099
           05  PV-SL-SYS-LOG-MSG-LENGTH  PIC S9(4)  VALUE +128          03350099
                                                    COMP SYNC.          03360099
           05  PV-SL-SYS-LOG-Q-NAME-LENGTH                              03370099
                                         PIC S9(4)  VALUE +4            03380099
                                                    COMP SYNC.          03390099
           05  PV-SL-SYS-LOG-QUEUING-IND PIC X(1)   VALUE 'D'.          03400099
               88  PV-SL-SYSTEM-LOG-ENQUEUED        VALUE 'E'.          03410099
               88  PV-SL-SYSTEM-LOG-DEQUEUED        VALUE 'D'.          03420099
           05  PV-SL-CICS-RESPONSE-DISPLAY                              03430099
                                         PIC ZZZ9.                      03440099
           05  PV-SL-SYSTEM-LOG-MESSAGE.                                03450099
               10  PV-SL-MESSAGE-DATE    PIC X(8)   VALUE SPACES.       03460099
               10  FILLER                PIC X(1)   VALUE SPACE.        03470099
               10  PV-SL-MESSAGE-TIME    PIC X(8)   VALUE SPACES.       03480099
               10  FILLER                PIC X(1)   VALUE SPACE.        03490099
               10  PV-SL-PROGRAM-ID      PIC X(8)   VALUE SPACES.       03500099
               10  FILLER                PIC X(1)   VALUE SPACE.        03510099
               10  PV-SL-SYS-LOG-MESSAGE-DTL                            03520099
                                         PIC X(105) VALUE SPACES.       03530099
                                                                        03540099
       01  TD-TRANSIENT-DATA-AREA.                                      03550099
           05  TD-QUEUE-NAME-LENGTH      PIC S9(4)  VALUE +4  COMP.     03560099
           05  TD-JCL-LINE-LENGTH        PIC S9(4)  VALUE +80 COMP.     03570099
           05  TD-QUEUE-NAME             PIC X(4)   VALUE 'IRTD'.       03580099
           05  TD-JCL-LINE               PIC X(80)  VALUE SPACES.       03590099
                                                                        03600099
      ******************************************************************03610099
      *  JCL LINES FOR THE VSM BRIDGE TO ITEM HUB SHUTDOWN JOB          03620099
      ******************************************************************03630099
       01  JL-JCL-LINE-AREA.                                            03640099
           05  JL-JCL-LINE-1P.                                          03650099
               10  FILLER              PIC X(21)    VALUE               03660099
                   '//IMK042   JOB KOHLS,'.                             03670099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03680099
               10  FILLER              PIC X(20)    VALUE               03690099
                   'ITM BRIDGE BKUP DUMP'.                              03700099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03710099
               10  FILLER              PIC X(19)    VALUE               03720099
                   ',CLASS=P,MSGCLASS=P'.                               03730099
               10  FILLER              PIC X(18)    VALUE SPACES.       03740099
           05  JL-JCL-LINE-1L.                                          03750099
               10  FILLER              PIC X(21)    VALUE               03760099
                   '//TKMA186L JOB KOHLS,'.                             03770099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03780099
               10  FILLER              PIC X(20)    VALUE               03790099
                   'ITM BRIDGE BKUPD LRN'.                              03800099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03810099
               10  FILLER              PIC X(19)    VALUE               03820099
                   ',CLASS=A,MSGCLASS=X'.                               03830099
               10  FILLER              PIC X(18)    VALUE SPACES.       03840099
           05  JL-JCL-LINE-1Q.                                          03850099
               10  FILLER              PIC X(21)    VALUE               03860099
                   '//TKMA186Q JOB KOHLS,'.                             03870099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03880099
               10  FILLER              PIC X(20)    VALUE               03890099
                   'ITM BRIDGE BKUP D QA'.                              03900099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03910099
               10  FILLER              PIC X(19)    VALUE               03920099
                   ',CLASS=A,MSGCLASS=X'.                               03930099
               10  FILLER              PIC X(18)    VALUE SPACES.       03940099
           05  JL-JCL-LINE-1T.                                          03950099
               10  FILLER              PIC X(21)    VALUE               03960099
                   '//TKMA186A JOB KOHLS,'.                             03970099
               10  FILLER              PIC X(1)     VALUE QUOTE.        03980099
               10  FILLER              PIC X(20)    VALUE               03990099
                   'ITM BRIDGE BKUP DUMP'.                              04000099
               10  FILLER              PIC X(1)     VALUE QUOTE.        04010099
               10  FILLER              PIC X(19)    VALUE               04020099
                   ',CLASS=A,MSGCLASS=X'.                               04030099
               10  FILLER              PIC X(18)    VALUE SPACES.       04040099
           05  JL-JCL-LINE-2.                                           04050099
               10  FILLER              PIC X(13)    VALUE               04060099
                   '/*JOBPARM K=0'.                                     04070099
               10  FILLER              PIC X(67)    VALUE SPACES.       04080099
           05  JL-JCL-LINE-3P.                                          04090099
               10  FILLER              PIC X(24)    VALUE               04100099
                   '//PS010    EXEC IMK0421 '.                          04110099
               10  FILLER              PIC X(56)    VALUE SPACES.       04120099
           05  JL-JCL-LINE-3L.                                          04130099
               10  FILLER              PIC X(24)    VALUE               04140099
                   '//PS010    EXEC IMK0421L'.                          04150099
               10  FILLER              PIC X(56)    VALUE SPACES.       04160099
           05  JL-JCL-LINE-3Q.                                          04170099
               10  FILLER              PIC X(24)    VALUE               04180099
                   '//PS010    EXEC IMK0421Q'.                          04190099
               10  FILLER              PIC X(56)    VALUE SPACES.       04200099
           05  JL-JCL-LINE-3T.                                          04210099
               10  FILLER              PIC X(24)    VALUE               04220099
                   '//PS010    EXEC IMK0421T'.                          04230099
               10  FILLER              PIC X(56)    VALUE SPACES.       04240099
           05  JL-JCL-LINE-EOF.                                         04250099
               10  FILLER              PIC X(5)     VALUE '/*EOF'.      04260099
               10  FILLER              PIC X(75)    VALUE SPACES.       04270099
                                                                        04280099
      *    API CONTROL BLOCKS                                           04290099
                                                                        04300099
       01  MQM-OBJECT-DESCRIPTOR.                                       04310099
           COPY CMQODV.                                                 04320099
       01  MQM-MESSAGE-DESCRIPTOR.                                      04330099
           COPY CMQMDV.                                                 04340099
       01  MQM-GET-MESSAGE-OPTIONS.                                     04350099
           COPY CMQGMOV.                                                04360099
       01  MQM-PUT-MESSAGE-OPTIONS.                                     04370099
           COPY CMQPMOV.                                                04380099
                                                                        04390099
      *    MQV CONTAINS CONSTANTS (FOR FILLING IN THE CONTROL BLOCKS)   04400099
      *    AND RETURN CODES (FOR TESTING THE RESULT OF A CALL)          04410099
                                                                        04420099
       01  MQM-CONSTANTS.                                               04430099
           COPY CMQV.                                                   04440099
      *                                                                 04450099
       01  MQ-WORKING-STORAGE.                                          04460099
           05  WS-MQOPEN               PIC X(8)  VALUE 'CSQCOPEN'.      04470099
           05  WS-MQGET                PIC X(8)  VALUE 'CSQCGET '.      04480099
           05  WS-MQPUT                PIC X(8)  VALUE 'CSQCPUT '.      04490099
           05  WS-MQCMIT               PIC X(8)  VALUE 'CSQCCOMM'.      04500099
           05  WS-MQBACK               PIC X(8)  VALUE 'CSQCBACK'.      04510099
           05  WS-MQCLOSE              PIC X(8)  VALUE 'CSQCCLOS'.      04520099
           05  WS-MQCHECK              PIC X(8)  VALUE 'MQR2C   '.      04530099
                                                                        04540099
       01  PE-PROGRAM-ERROR-MESSAGES.                                   04550099
      *        MQ CALL ERROR MESSAGES                                   04560099
           05  PE-MQ-ERROR-MESSAGE.                                     04570099
               10  FILLER                   PIC X(19) VALUE             04580099
                                      'MQ ERROR FOR CALL: '.            04590099
               10  PE-MQ-CALL-TYPE          PIC X(7)  VALUE SPACES.     04600099
                   88  PE-MQ-OPEN                     VALUE 'MQOPEN '.  04610099
                   88  PE-MQ-PUT                      VALUE 'MQPUT  '.  04620099
                   88  PE-MQ-CLOSE                    VALUE 'MQCLOSE'.  04630099
               10  FILLER                   PIC X(02) VALUE SPACES.     04640099
               10  FILLER                   PIC X(11) VALUE             04650099
                                              'COMP CODE: '.            04660099
               10  PE-MQ-COMPLETION-CODE    PIC 9(09) VALUE ZEROS.      04670099
               10  FILLER                   PIC X(02) VALUE SPACES.     04680099
               10  FILLER                   PIC X(13) VALUE             04690099
                                            'REASON CODE: '.            04700099
               10  PE-MQ-REASON-CODE        PIC 9(09) VALUE ZEROS.      04710099
           05  PE-MQ-REASON-MESSAGE.                                    04720099
               10  FILLER                   PIC X(18) VALUE 'REASON: '. 04730099
               10  PE-MQ-REASON-TEXT        PIC X(30) VALUE SPACES.     04740099
                                                                        04750099
       01  AA-DB2-ERROR-FIELDS.                                         04760099
           05  AA-DB2-OPERATION-LIT.                                    04770099
               10  FILLER              PIC  X(17)  VALUE                04780099
                   '***** OPERATION: '.                                 04790099
               10  AA-DB2-OPERATION.                                    04800099
                   15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.        04810099
           05  AA-DB2-TABLE-1          PIC  X(18)  VALUE SPACES.        04820099
                                                                        04830099
      ***************************************************************** 04840099
      *    ABEND PROCESSING COPYBOOK.                                   04850099
      ***************************************************************** 04860099
           COPY DPWS013.                                                04870099
                                                                        04880099
      ***************************************************************** 04890099
      *  LEGACY ITEM MAINTENANCE INFO COPYBOOK                          04900099
      ******************************************************************04910099
           COPY IMRD008.                                                04920099
                                                                        04930099
213345***************************************************************** 04940099
213345*  NON-VSM ITEM MAINTENANCE INFO COPYBOOK                         04950099
213345******************************************************************04960099
213345     COPY IMRD018.                                                04970099
213345                                                                  04980099
      ******************************************************************04990099
      *  ITEM EVENT MESSAGE BRIDGE TABLE                                05000099
      ******************************************************************05010099
                                                                        05020099
           EXEC SQL                                                     05030099
               INCLUDE IMT00001                                         05040099
           END-EXEC.                                                    05050099
                                                                        05060099
      ***************************************************************** 05070099
      * DB2 CHECKPOINT RESTART CONTROL TABLE                            05080099
      ***************************************************************** 05090099
           EXEC SQL                                                     05100099
               INCLUDE TCKRSTCN                                         05110099
           END-EXEC.                                                    05120099
                                                                        05130099
      ******************************************************************05140099
      *    DB2 COMMUNICATION AREA                                       05150099
      ******************************************************************05160099
                                                                        05170099
           EXEC SQL                                                     05180099
               INCLUDE SQLCA                                            05190099
           END-EXEC.                                                    05200099
                                                                        05210099
      ******************************************************************05220099
      *  ITEM EVENT MESSAGE BRIDGE CURSOR                               05230099
      *-----------------------------------------------------------------05240099
      *  THE ORDER BY CLAUSE IS NECESSARY TO PROCESS ALL MESSAGES       05250099
      *  IN THE CORRECT ORDER.  A DELETE WHERE CURRENT OF CURSOR CANNOT 05260099
      *  BE USED SINCE THE CURSOR HAS AN ORDER BY CLAUSE.               05270099
      ******************************************************************05280099
           EXEC SQL                                                     05290099
               DECLARE ITEM_EVENT_MSG_CSR CURSOR WITH HOLD FOR          05300099
               SELECT CRE_TMST                                          05310099
                    , RMS_STYL_ID                                       05320099
                    , BUS_EVNT_TYP_CDE                                  05330099
                    , MSG_TXT                                           05340099
                 FROM IMT_EVNT_MSG_BRG                                  05350099
               ORDER BY CRE_TMST                                        05360099
                      , RMS_STYL_ID                                     05370099
                      , BUS_EVNT_TYP_CDE                                05380099
           END-EXEC.                                                    05390099
                                                                        05400099
                                                                        05410099
       LINKAGE SECTION.                                                 05420099
                                                                        05430099
       01 DFHCOMMAREA                 PIC X(01).                        05440099
                                                                        05450099
      ******************************************************************05460099
       PROCEDURE DIVISION.                                              05470099
      ******************************************************************05480099
                                                                        05490099
       0000-MAINLINE.                                                   05500099
                                                                        05510099
           MOVE PV-LOG-BRIDGE-STARTUP-MSG                               05520099
               TO PV-SL-SYS-LOG-MESSAGE-DTL.                            05530099
           PERFORM 9000-SYSTEM-LOG-MESSAGE.                             05540099
                                                                        05550099
           PERFORM 1000-MQ-SETUP.                                       05560099
           PERFORM 4200-GET-COMMIT-INFO.                                05570099
           PERFORM 4500-DB2-MQ-COMMIT.                                  05580099
                                                                        05590099
           PERFORM 2000-PROCESS-DATA                                    05600099
               UNTIL PS-END-OF-DAY.                                     05610099
                                                                        05620099
           PERFORM 8000-CLOSE-MQ-QUEUE.                                 05630099
                                                                        05640099
           MOVE PV-TOTAL-RECS-PROCESSED TO                              05650099
                PV-LOG-TRP-RECS-PROCESS-CNT.                            05660099
           MOVE PV-FETCH-DEADLOCK-COUNT TO                              05670099
                PV-LOG-TRP-RECS-DEADLOCL-CNT.                           05680099
           MOVE PV-LOG-TOTAL-RECS-PROCESS-MSG                           05690099
               TO PV-SL-SYS-LOG-MESSAGE-DTL.                            05700099
           PERFORM 9000-SYSTEM-LOG-MESSAGE.                             05710099
                                                                        05720099
           PERFORM 5000-SUBMIT-BACKUP-DUMP-JOB.                         05730099
                                                                        05740099
           PERFORM 9990-EXEC-CICS-RETURN.                               05750099
                                                                        05760099
      ******************************************************************05770099
      *    CONNECT TO THE QUEUE MANAGER                                 05780099
      ******************************************************************05790099
       1000-MQ-SETUP.                                                   05800099
                                                                        05810099
           EXEC CICS                                                    05820099
               ASSIGN SYSID (PV-CURRENT-SYSID)                          05830099
           END-EXEC.                                                    05840099
                                                                        05850099
           MOVE PV-CURRENT-SYSID TO PC-SYS-ID.                          05860099
                                                                        05870099
           IF PC-KOHLS-PROD-SYSTEM                                      05880099
               SET PC-PROD-QMGR-NAME     TO TRUE                        05890099
               SET PC-XS-PROD-QMGR-NAME  TO TRUE                        05900099
               SET PC-PROD-CORREL-ID     TO TRUE                        05910099
           ELSE                                                         05920099
                IF PC-KOHLS-LEARNING-SYSTEM                             05930099
                    SET PC-LEARN-QMGR-NAME    TO TRUE                   05940099
                    SET PC-XS-LEARN-QMGR-NAME TO TRUE                   05950099
                    SET PC-LEARN-CORREL-ID    TO TRUE                   05960099
                ELSE                                                    05970099
                    IF PC-KOHLS-QA-SYSTEM                               05980099
                        SET PC-QA-QMGR-NAME    TO TRUE                  05990099
                        SET PC-XS-QA-QMGR-NAME TO TRUE                  06000099
                        SET PC-QA-CORREL-ID    TO TRUE                  06010099
                    ELSE                                                06020099
                        SET PC-TEST-QMGR-NAME    TO TRUE                06030099
                        SET PC-XS-TEST-QMGR-NAME TO TRUE                06040099
                        SET PC-TEST-CORREL-ID    TO TRUE                06050099
                    END-IF                                              06060099
                END-IF                                                  06070099
           END-IF.                                                      06080099
                                                                        06090099
      *---------------------------------------------------------------* 06100099
      *    OPEN THE MQSI VSM ITEM INQ QUEUE FOR OUTPUT                  06110099
      *---------------------------------------------------------------* 06120099
                                                                        06130099
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                      06140099
                                  MQOO-FAIL-IF-QUIESCING +              06150099
                                  MQOO-SET-IDENTITY-CONTEXT.            06160099
           MOVE PC-QNAME                       TO MQOD-OBJECTNAME.      06170099
           MOVE PC-QMGR-NAME                   TO MQOD-OBJECTQMGRNAME.  06180099
      *                                                                 06190099
      *                                                                 06200099
           CALL WS-MQOPEN USING PV-HCONN                                06210099
                                MQM-OBJECT-DESCRIPTOR                   06220099
                                PV-OPEN-OPTIONS                         06230099
                                PV-PQ-HANDLE                            06240099
                                PV-MQ-COMPLETION-CODE                   06250099
                                PV-MQ-REASON.                           06260099
      *                                                                 06270099
      *                                                                 06280099
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    06290099
      *    AND EXIT.                                                    06300099
      *                                                                 06310099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     06320099
              MOVE '1000-MQ-SETUP' TO PV-PARAGRAPH                      06330099
              SET PE-MQ-OPEN TO TRUE                                    06340099
              PERFORM 9900-DISPLAY-MQ-ERROR                             06350099
           END-IF.                                                      06360099
                                                                        06370099
      *---------------------------------------------------------------* 06380099
      *    OPEN THE XS.ITEM.ATTRIBUTES.Q QUEUE FOR OUTPUT               06390099
      *---------------------------------------------------------------* 06400099
                                                                        06410099
           COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                      06420099
                                  MQOO-FAIL-IF-QUIESCING +              06430099
                                  MQOO-SET-IDENTITY-CONTEXT.            06440099
           MOVE PC-XS-QNAME                    TO MQOD-OBJECTNAME.      06450099
           MOVE PC-XS-QMGR-NAME                TO MQOD-OBJECTQMGRNAME.  06460099
      *                                                                 06470099
      *                                                                 06480099
           CALL WS-MQOPEN USING PV-HCONN                                06490099
                                MQM-OBJECT-DESCRIPTOR                   06500099
                                PV-OPEN-OPTIONS                         06510099
                                PV-XS-PQ-HANDLE                         06520099
                                PV-MQ-COMPLETION-CODE                   06530099
                                PV-MQ-REASON.                           06540099
      *                                                                 06550099
      *                                                                 06560099
      *    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    06570099
      *    AND EXIT.                                                    06580099
      *                                                                 06590099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     06600099
              MOVE '1000-MQ-SETUP-XS' TO PV-PARAGRAPH                   06610099
              SET PE-MQ-OPEN TO TRUE                                    06620099
              PERFORM 9900-DISPLAY-MQ-ERROR                             06630099
           END-IF.                                                      06640099
                                                                        06650099
213345*---------------------------------------------------------------* 06660099
213345*    OPEN THE IM.NONVSM.ITEM.CHANGES.Q QUEUE FOR OUTPUT           06670099
213345*---------------------------------------------------------------* 06680099
213345                                                                  06690099
213345     COMPUTE PV-OPEN-OPTIONS = MQOO-OUTPUT +                      06700099
213345                            MQOO-FAIL-IF-QUIESCING +              06710099
213345                            MQOO-SET-IDENTITY-CONTEXT.            06720099
213345     MOVE PC-IM-QNAME                    TO MQOD-OBJECTNAME.      06730099
213345     MOVE PC-XS-QMGR-NAME                TO MQOD-OBJECTQMGRNAME.  06740099
213345*                                                                 06750099
213345*                                                                 06760099
213345     CALL WS-MQOPEN USING PV-HCONN                                06770099
213345                          MQM-OBJECT-DESCRIPTOR                   06780099
213345                          PV-OPEN-OPTIONS                         06790099
213345                          PV-IM-PQ-HANDLE                         06800099
213345                          PV-MQ-COMPLETION-CODE                   06810099
213345                          PV-MQ-REASON.                           06820099
213345*                                                                 06830099
213345*                                                                 06840099
213345*    IF OPEN FAILED THEN DISPLAY ERROR MESSAGE                    06850099
213345*    AND EXIT.                                                    06860099
213345*                                                                 06870099
213345     IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     06880099
213345        MOVE '1000-MQ-SETUP-IM' TO PV-PARAGRAPH                   06890099
213345        SET PE-MQ-OPEN TO TRUE                                    06900099
213345        PERFORM 9900-DISPLAY-MQ-ERROR                             06910099
213345     END-IF.                                                      06920099
213345                                                                  06930099
      /                                                                 06940099
      ******************************************************************06950099
      *  PROCESS ALL RECORDS ON THE ITEM EVENT MESSAGE DATA BASE        06960099
      *  AND ISSUE A CICS DELAY BEFORE PROCESSING THE CURSOR AGAIN.     06970099
      ******************************************************************06980099
       2000-PROCESS-DATA.                                               06990099
                                                                        07000099
           SET PS-NOT-END-OF-DATA TO TRUE.                              07010099
           SET PS-CURSOR-HAS-NO-RECORDS TO TRUE.                        07020099
           INITIALIZE PV-SAVE-RMS-STYL-ID.                              07030099
216771     INITIALIZE PV-SAVE-BUS-EVNT-TYP-CDE.                         07040099
                                                                        07050099
           PERFORM 2010-OPEN-ITEM-EVENT-MSG-CSR.                        07060099
           SET PS-NOT-FETCH-DEADLOCK TO TRUE.                           07070099
           PERFORM 2100-FETCH-ITEM-EVENT-MSG                            07080099
               UNTIL PS-END-OF-DATA                                     07090099
                 OR  PS-END-OF-DAY                                      07100099
                 OR  PS-FETCH-DEADLOCK.                                 07110099
           IF PS-NOT-FETCH-DEADLOCK                                     07120099
               PERFORM 2020-CLOSE-ITEM-EVENT-MSG-CSR                    07130099
           ELSE                                                         07140099
      *        IF A FETCH DEADLOCK OCCURRED, A ROLLBACK HAS BEEN        07150099
      *        PERFORMED, AND NOW WE WANT TO WAIT BEFORE RESTARTING     07160099
               SET PS-CURSOR-HAS-NO-RECORDS TO TRUE                     07170099
           END-IF.                                                      07180099
                                                                        07190099
           IF PS-CURSOR-HAS-RECORDS                                     07200099
             OR PS-END-OF-DAY                                           07210099
               IF PS-CURSOR-HAS-RECORDS                                 07220099
                   PERFORM 3010-PROCESS-ITEM-EVENT-MSG                  07230099
               END-IF                                                   07240099
               IF PS-END-OF-DAY                                         07250099
                   MOVE IMT00001-MSG-TXT-TEXT                           07260099
                                          TO IMRD008-RECORD             07270099
                   MOVE IMT00001-CRE-TMST TO PV-SAVE-CRE-TMST           07280099
                                             IM008-CRE-TMST             07290099
                   MOVE IMT00001-RMS-STYL-ID                            07300099
                                          TO PV-SAVE-RMS-STYL-ID        07310099
                   MOVE IMT00001-BUS-EVNT-TYP-CDE                       07320099
                                          TO PV-SAVE-BUS-EVNT-TYP-CDE   07330099
                   PERFORM 3010-PROCESS-ITEM-EVENT-MSG                  07340099
               END-IF                                                   07350099
               PERFORM 4500-DB2-MQ-COMMIT                               07360099
           END-IF.                                                      07370099
                                                                        07380099
           MOVE ZERO TO PV-RMS-CATCHUP-RECS-COUNT.                      07390099
                                                                        07400099
           IF PS-CURSOR-HAS-NO-RECORDS                                  07410099
            AND PS-NOT-END-OF-DAY                                       07420099
      *        WAIT PV-RESTART-DELAY-SECONDS BEFORE STARTING OVER AGAIN.07430099
      *        PC-RESTART-DELAY-SECONDS CAN BE A MAXIMUM OF 359 SECONDS.07440099
               EXEC CICS                                                07450099
                   DELAY FOR SECONDS(PV-RESTART-DELAY-SECONDS)          07460099
               END-EXEC                                                 07470099
           END-IF.                                                      07480099
                                                                        07490099
      ******************************************************************07500099
      *  OPENS THE ITEM EVENT MESSAGE BRIDGE CURSOR                   * 07510099
      ******************************************************************07520099
       2010-OPEN-ITEM-EVENT-MSG-CSR.                                    07530099
                                                                        07540099
           EXEC SQL                                                     07550099
               OPEN ITEM_EVENT_MSG_CSR                                  07560099
           END-EXEC.                                                    07570099
                                                                        07580099
           EVALUATE TRUE                                                07590099
               WHEN SQLCODE = ZERO                                      07600099
                   CONTINUE                                             07610099
               WHEN OTHER                                               07620099
                   MOVE '2010-OPEN-ITEM-EVENT-MSG-CSR'                  07630099
                                             TO PV-PARAGRAPH            07640099
                   MOVE 'OPEN ITEM MSG CURSOR'                          07650099
                                             TO AA-DB2-OPERATION        07660099
                   MOVE 'IMT_EVNT_MSG_BRG'   TO AA-DB2-TABLE-1          07670099
                   PERFORM 9800-DB2-ERROR-ROUTINE                       07680099
           END-EVALUATE.                                                07690099
                                                                        07700099
      ******************************************************************07710099
      *  CLOSES THE ITEM EVENT MESSAGE BRIDGE CURSOR                    07720099
      ******************************************************************07730099
       2020-CLOSE-ITEM-EVENT-MSG-CSR.                                   07740099
                                                                        07750099
           EXEC SQL                                                     07760099
               CLOSE ITEM_EVENT_MSG_CSR                                 07770099
           END-EXEC.                                                    07780099
                                                                        07790099
           EVALUATE TRUE                                                07800099
               WHEN SQLCODE = ZEROES                                    07810099
                   CONTINUE                                             07820099
               WHEN OTHER                                               07830099
                   MOVE '2020-CLOSE-ITEM-EVENT-MSG-CSR'                 07840099
                                                 TO PV-PARAGRAPH        07850099
                   MOVE 'CLOSE ITEM MSG CURSOR ' TO AA-DB2-OPERATION    07860099
                   MOVE 'IMT_EVNT_MSG_BRG'       TO AA-DB2-TABLE-1      07870099
                   PERFORM 9800-DB2-ERROR-ROUTINE                       07880099
           END-EVALUATE.                                                07890099
       EJECT                                                            07900099
                                                                        07910099
      ******************************************************************07920099
      *  FETCHES AND PROCESSES THE ITEM EVENT MESSAGE BRIDGE RECORD     07930099
      ******************************************************************07940099
       2100-FETCH-ITEM-EVENT-MSG.                                       07950099
                                                                        07960099
           MOVE LOW-VALUES TO IMT00001-MSG-TXT.                         07970099
                                                                        07980099
           EXEC SQL                                                     07990099
               FETCH ITEM_EVENT_MSG_CSR                                 08000099
                INTO :IMT00001-CRE-TMST                                 08010099
                   , :IMT00001-RMS-STYL-ID                              08020099
                   , :IMT00001-BUS-EVNT-TYP-CDE                         08030099
                   , :IMT00001-MSG-TXT:PV-MSG-TXT-NULL-IND              08040099
           END-EXEC.                                                    08050099
                                                                        08060099
           EVALUATE TRUE                                                08070099
               WHEN SQLCODE = ZERO                                      08080099
                   IF IMT00001-BUS-EVNT-TYP-CDE = 999                   08090099
                       SET PS-END-OF-DAY         TO TRUE                08100099
                   ELSE                                                 08110099
                       SET PS-CURSOR-HAS-RECORDS TO TRUE                08120099
                       ADD +1 TO PV-TOTAL-RECS-PROCESSED                08130099
                       PERFORM 3000-CHECK-ITEM-EVENT-MSG                08140099
                   END-IF                                               08150099
               WHEN SQLCODE = +100                                      08160099
                   SET PS-END-OF-DATA TO TRUE                           08170099
               WHEN SQLCODE = -911                                      08180099
               WHEN SQLCODE = -913                                      08190099
                   SET PS-FETCH-DEADLOCK TO TRUE                        08200099
                   ADD 1 TO PV-FETCH-DEADLOCK-COUNT                     08210099
                   PERFORM 9800-DB2-ERROR-ROUTINE                       08220099
               WHEN OTHER                                               08230099
                   MOVE '2100-FETCH-ITEM-EVENT-MSG'                     08240099
                                             TO PV-PARAGRAPH            08250099
                   MOVE 'FETCH ITEM MSG CURSOR'                         08260099
                                             TO AA-DB2-OPERATION        08270099
                   MOVE 'IMT_EVNT_MSG_BRG'   TO AA-DB2-TABLE-1          08280099
                   PERFORM 9800-DB2-ERROR-ROUTINE                       08290099
           END-EVALUATE.                                                08300099
                                                                        08310099
      /                                                                 08320099
      ******************************************************************08330099
      *  CHECK IF AN ITEM EVENT MESSAGE DATA BASE RECORD SHOULD BE      08340099
      *  PROCESSED.                                                     08350099
      ******************************************************************08360099
       3000-CHECK-ITEM-EVENT-MSG.                                       08370099
                                                                        08380099
           MOVE '3000-CHECK-ITEM-EVENT-MSG' TO PV-PARAGRAPH.            08390099
                                                                        08400099
           IF  PV-SAVE-RMS-STYL-ID = ZERO                               08410099
213345     AND (PV-SAVE-BUS-EVNT-TYP-CDE NOT = 905                      08420099
213345     AND  PV-SAVE-BUS-EVNT-TYP-CDE NOT = 910                      08430099
213345     AND  PV-SAVE-BUS-EVNT-TYP-CDE NOT = 915                      08440099
213345     AND  PV-SAVE-BUS-EVNT-TYP-CDE NOT = 930)                     08450099
               CONTINUE                                                 08460099
           ELSE                                                         08470099
               PERFORM 3010-PROCESS-ITEM-EVENT-MSG                      08480099
               PERFORM 4000-DB2-MQ-COMMIT-CHECK                         08490099
           END-IF.                                                      08500099
                                                                        08510099
213345     IF IMT00001-BUS-EVNT-TYP-CDE = 905 OR 910 OR 915 OR 930      08520099
213345     DISPLAY 'IMKCS301 - BUS EVNT = ' IMT00001-BUS-EVNT-TYP-CDE   08530099
213345         MOVE IMT00001-MSG-TXT-TEXT TO IMRD018-RECORD             08540099
213345     ELSE                                                         08550099
               MOVE IMT00001-MSG-TXT-TEXT TO IMRD008-RECORD             08560099
213345     END-IF.                                                      08570099
           MOVE IMT00001-CRE-TMST         TO PV-SAVE-CRE-TMST           08580099
                                             IM008-CRE-TMST.            08590099
           MOVE IMT00001-RMS-STYL-ID      TO PV-SAVE-RMS-STYL-ID.       08600099
           MOVE IMT00001-BUS-EVNT-TYP-CDE TO PV-SAVE-BUS-EVNT-TYP-CDE.  08610099
                                                                        08620099
      ******************************************************************08630099
      *  PROCESS A PREVIOUSLY SAVED ITEM EVENT MESSAGE DATA BASE RECORD 08640099
213345*  CONTAINED IN IMRD008-RECORD OR IMRD018-RECORD.                 08650099
      ******************************************************************08660099
       3010-PROCESS-ITEM-EVENT-MSG.                                     08670099
                                                                        08680099
           MOVE '3010-PROCESS-ITEM-EVENT-MSG' TO PV-PARAGRAPH.          08690099
                                                                        08700099
           ADD +1 TO PV-RMS-CATCHUP-RECS-COUNT.                         08710099
                                                                        08720099
           PERFORM 3100-CREATE-MQ-MESSAGE.                              08730099
                                                                        08740099
           PERFORM 3200-DELETE-ITEM-EVENT-MSG.                          08750099
                                                                        08760099
213345*    DON'T WRITE THE NONVSM RECORDS TO THE BACKUP FILE            08770099
213345     IF PV-SAVE-BUS-EVNT-TYP-CDE = 905 OR 910 OR 915 OR 930       08780099
213345         CONTINUE                                                 08790099
213345     ELSE                                                         08800099
               PERFORM 3300-WRITE-BACKUP-MESSAGE                        08810099
213345     END-IF.                                                      08820099
                                                                        08830099
      ******************************************************************08840099
      *  CREATE MESSAGE FOR THE MQSI VSM ITEM INQ QUEUE.                08850099
      ******************************************************************08860099
       3100-CREATE-MQ-MESSAGE.                                          08870099
                                                                        08880099
           INITIALIZE PS-MQPUT-SUCCESSFUL-SW                            08890099
                      PV-MQPUT-RETRY-COUNTER.                           08900099
213345     IF  PV-SAVE-BUS-EVNT-TYP-CDE  = 997                          08910099
213345                                  OR 905 OR 910 OR 915 OR 930     08920099
213345         CONTINUE                                                 08930099
213345     ELSE                                                         08940099
               MOVE IMRD008-RECORD TO PV-MSGBUFFER                      08950099
               PERFORM 3110-PUT-MQ-MESSAGE                              08960099
                 UNTIL PS-MQPUT-SUCCESSFUL                              08970099
           END-IF.                                                      08980099
           INITIALIZE PS-MQPUT-SUCCESSFUL-SW                            08990099
                      PV-MQPUT-RETRY-COUNTER.                           09000099
           IF (PV-SAVE-BUS-EVNT-TYP-CDE = 5 OR 10 OR 15 OR 20 OR 25     09010099
               OR 30 OR 31 OR 40 OR 41 OR 45 OR 50 OR 55 OR 60 OR 70    09020099
               OR 85 OR 87 OR 89 OR 95 OR 100 OR 120 OR 997 OR 998)     09030099
             OR (PV-SAVE-BUS-EVNT-TYP-CDE = 75                          09040099
                 AND NOT (IM008-RTL-CHG-CRE-BY-USR-ID = 'IMKUP039'      09050099
                          OR 'IMKUP041'))                               09060099
             OR (PV-SAVE-BUS-EVNT-TYP-CDE = 80                          09070099
                 AND NOT (IM008-STR-RTL-PERM-RUN-IND = 'Y' OR 'R'))     09080099
213345         MOVE IMRD008-RECORD TO PV-MSGBUFFER                      09090099
               PERFORM 3120-PUT-XS-MQ-MESSAGE                           09100099
                 UNTIL PS-MQPUT-SUCCESSFUL                              09110099
           END-IF.                                                      09120099
213345     INITIALIZE PS-MQPUT-SUCCESSFUL-SW                            09130099
213345                PV-MQPUT-RETRY-COUNTER.                           09140099
213345     IF PV-SAVE-BUS-EVNT-TYP-CDE = 905                            09150099
213345         MOVE 005  TO  IM018-BUSINESS-EVENT-ID                    09160099
213345         MOVE IMRD018-RECORD TO PV-IM-NONVSM-MSGBUFFER            09170099
213345         PERFORM 3130-PUT-IM-NONVSM-MQ-MESSAGE                    09180099
213345            UNTIL PS-MQPUT-SUCCESSFUL                             09190099
213345     ELSE                                                         09200099
213345         IF PV-SAVE-BUS-EVNT-TYP-CDE = 910                        09210099
213345             MOVE 010  TO  IM018-BUSINESS-EVENT-ID                09220099
213345             MOVE IMRD018-RECORD TO PV-IM-NONVSM-MSGBUFFER        09230099
213345             PERFORM 3130-PUT-IM-NONVSM-MQ-MESSAGE                09240099
213345                UNTIL PS-MQPUT-SUCCESSFUL                         09250099
213345         ELSE                                                     09260099
213345             IF PV-SAVE-BUS-EVNT-TYP-CDE = 915                    09270099
213345                 MOVE 015  TO  IM018-BUSINESS-EVENT-ID            09280099
213345                 MOVE IMRD018-RECORD TO PV-IM-NONVSM-MSGBUFFER    09290099
213345                 PERFORM 3130-PUT-IM-NONVSM-MQ-MESSAGE            09300099
213345                    UNTIL PS-MQPUT-SUCCESSFUL                     09310099
213345             ELSE                                                 09320099
213345                 IF PV-SAVE-BUS-EVNT-TYP-CDE = 930                09330099
213345                    MOVE 030  TO  IM018-BUSINESS-EVENT-ID         09340099
213345                    MOVE IMRD018-RECORD TO PV-IM-NONVSM-MSGBUFFER 09350099
213345                    PERFORM 3130-PUT-IM-NONVSM-MQ-MESSAGE         09360099
213345                       UNTIL PS-MQPUT-SUCCESSFUL                  09370099
213345                 END-IF                                           09380099
213345             END-IF                                               09390099
213345         END-IF                                                   09400099
213345     END-IF.                                                      09410099
                                                                        09420099
      ******************************************************************09430099
      *  PUT MESSAGE ON THE MQSI VSM ITEM INQ QUEUE.                    09440099
      ******************************************************************09450099
       3110-PUT-MQ-MESSAGE.                                             09460099
                                                                        09470099
           MOVE PC-QMGR-NAME           TO MQOD-OBJECTQMGRNAME.          09480099
           MOVE PC-QNAME               TO MQOD-OBJECTNAME.              09490099
           MOVE MQMI-NONE              TO MQMD-MSGID.                   09500099
           MOVE PC-CORREL-ID           TO MQMD-CORRELID.                09510099
           MOVE MQFMT-STRING           TO MQMD-FORMAT.                  09520099
           MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.             09530099
      *    SET THE MQ PRIORITY OF EVENTS 30, 31, AND 998 TO 3 INSTEAD OF09540099
      *    5 SO THAT THEY ARE PROCESSED AFTER ALL OTHER MESSAGES.       09550099
           IF PV-SAVE-BUS-EVNT-TYP-CDE = 30                             09560099
             OR PV-SAVE-BUS-EVNT-TYP-CDE = 31                           09570099
             OR PV-SAVE-BUS-EVNT-TYP-CDE = 998                          09580099
               MOVE +3                 TO MQMD-PRIORITY                 09590099
           ELSE                                                         09600099
               MOVE +5                 TO MQMD-PRIORITY                 09610099
           END-IF.                                                      09620099
           COMPUTE MQPMO-OPTIONS = MQPMO-FAIL-IF-QUIESCING              09630099
                                 + MQPMO-SET-IDENTITY-CONTEXT           09640099
                                 + MQPMO-SYNCPOINT.                     09650099
      *                                                                 09660099
           CALL WS-MQPUT USING PV-HCONN                                 09670099
                               PV-PQ-HANDLE                             09680099
                               MQM-MESSAGE-DESCRIPTOR                   09690099
                               MQM-PUT-MESSAGE-OPTIONS                  09700099
                               PC-OUT-MSGLENGTH                         09710099
                               PV-MSGBUFFER                             09720099
                               PV-MQ-COMPLETION-CODE                    09730099
                               PV-MQ-REASON.                            09740099
                                                                        09750099
      *        IF PUT FAILED THEN DISPLAY ERROR MESSAGE                 09760099
      *        AND BREAK OUT OF LOOP                                    09770099
                                                                        09780099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     09790099
               IF (PV-MQ-REASON NOT = MQRC-PAGESET-FULL                 09800099
                 AND PV-MQ-REASON NOT = MQRC-Q-FULL                     09810099
                 AND PV-MQ-REASON NOT = MQRC-Q-SPACE-NOT-AVAILABLE )    09820099
                   MOVE '3110-PUT-MQ-MESSAGE'                           09830099
                                             TO PV-PARAGRAPH            09840099
                   MOVE PV-PARAGRAPH         TO PV-SAVE-PARAGRAPH       09850099
                   MOVE PV-MQ-COMPLETION-CODE                           09860099
                                             TO PV-SAVE-COMPLETION-CODE 09870099
                   MOVE PV-MQ-REASON         TO PV-SAVE-REASON          09880099
                   PERFORM 8000-CLOSE-MQ-QUEUE                          09890099
                   MOVE PV-SAVE-PARAGRAPH    TO PV-PARAGRAPH            09900099
                   MOVE PV-SAVE-COMPLETION-CODE                         09910099
                                             TO PV-MQ-COMPLETION-CODE   09920099
                   MOVE PV-SAVE-REASON       TO PV-MQ-REASON            09930099
                   SET PE-MQ-PUT             TO TRUE                    09940099
                   PERFORM 9900-DISPLAY-MQ-ERROR                        09950099
               ELSE                                                     09960099
                   ADD 1 TO PV-MQPUT-RETRY-COUNTER                      09970099
                   IF PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES           09980099
                       MOVE '3110-PUT-MQ-MESSAGE'                       09990099
                                             TO PV-PARAGRAPH            10000099
                       MOVE PV-PARAGRAPH     TO PV-SAVE-PARAGRAPH       10010099
                       MOVE PV-MQ-COMPLETION-CODE                       10020099
                                             TO PV-SAVE-COMPLETION-CODE 10030099
                       MOVE PV-MQ-REASON     TO PV-SAVE-REASON          10040099
                       PERFORM 8000-CLOSE-MQ-QUEUE                      10050099
                       MOVE PV-SAVE-PARAGRAPH                           10060099
                                             TO PV-PARAGRAPH            10070099
                       MOVE PV-SAVE-COMPLETION-CODE                     10080099
                                             TO PV-MQ-COMPLETION-CODE   10090099
                       MOVE PV-SAVE-REASON   TO PV-MQ-REASON            10100099
                       SET PE-MQ-PUT         TO TRUE                    10110099
                       PERFORM 9900-DISPLAY-MQ-ERROR                    10120099
                   ELSE                                                 10130099
      **------->       WAIT FOR RETRY DELAY SECONDS (FILLED FROM        10140099
      **------->       TCKRSTCNTL-CKPT-TAKEN-QTY) AND TRY AGAIN         10150099
                       EXEC CICS                                        10160099
                           DELAY FOR SECONDS(PV-RETRY-DELAY-SECONDS)    10170099
                       END-EXEC                                         10180099
                   END-IF                                               10190099
               END-IF                                                   10200099
           ELSE                                                         10210099
               SET PS-MQPUT-SUCCESSFUL TO TRUE                          10220099
           END-IF.                                                      10230099
                                                                        10240099
      ******************************************************************10250099
      *  PUT MESSAGE ON THE XS.ITEM.ATTRIBUTES.Q QUEUE.                 10260099
      ******************************************************************10270099
       3120-PUT-XS-MQ-MESSAGE.                                          10280099
                                                                        10290099
           MOVE PC-XS-QMGR-NAME        TO MQOD-OBJECTQMGRNAME.          10300099
           MOVE PC-XS-QNAME            TO MQOD-OBJECTNAME.              10310099
           MOVE MQMI-NONE              TO MQMD-MSGID.                   10320099
           MOVE PC-CORREL-ID           TO MQMD-CORRELID.                10330099
           MOVE MQFMT-STRING           TO MQMD-FORMAT.                  10340099
           MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.             10350099
           MOVE +5                     TO MQMD-PRIORITY.                10360099
           COMPUTE MQPMO-OPTIONS = MQPMO-FAIL-IF-QUIESCING              10370099
                                 + MQPMO-SET-IDENTITY-CONTEXT           10380099
                                 + MQPMO-SYNCPOINT.                     10390099
      *                                                                 10400099
           CALL WS-MQPUT USING PV-HCONN                                 10410099
                               PV-XS-PQ-HANDLE                          10420099
                               MQM-MESSAGE-DESCRIPTOR                   10430099
                               MQM-PUT-MESSAGE-OPTIONS                  10440099
                               PC-OUT-MSGLENGTH                         10450099
                               PV-MSGBUFFER                             10460099
                               PV-MQ-COMPLETION-CODE                    10470099
                               PV-MQ-REASON.                            10480099
                                                                        10490099
      *        IF PUT FAILED THEN DISPLAY ERROR MESSAGE                 10500099
      *        AND BREAK OUT OF LOOP                                    10510099
                                                                        10520099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     10530099
               IF (PV-MQ-REASON NOT = MQRC-PAGESET-FULL                 10540099
                 AND PV-MQ-REASON NOT = MQRC-Q-FULL                     10550099
                 AND PV-MQ-REASON NOT = MQRC-Q-SPACE-NOT-AVAILABLE )    10560099
                   MOVE '3120-PUT-XS-MQ-MESSAGE'                        10570099
                                             TO PV-PARAGRAPH            10580099
                   MOVE PV-PARAGRAPH         TO PV-SAVE-PARAGRAPH       10590099
                   MOVE PV-MQ-COMPLETION-CODE                           10600099
                                             TO PV-SAVE-COMPLETION-CODE 10610099
                   MOVE PV-MQ-REASON         TO PV-SAVE-REASON          10620099
                   PERFORM 8000-CLOSE-MQ-QUEUE                          10630099
                   MOVE PV-SAVE-PARAGRAPH    TO PV-PARAGRAPH            10640099
                   MOVE PV-SAVE-COMPLETION-CODE                         10650099
                                             TO PV-MQ-COMPLETION-CODE   10660099
                   MOVE PV-SAVE-REASON       TO PV-MQ-REASON            10670099
                   SET PE-MQ-PUT             TO TRUE                    10680099
                   PERFORM 9900-DISPLAY-MQ-ERROR                        10690099
               ELSE                                                     10700099
                   ADD 1 TO PV-MQPUT-RETRY-COUNTER                      10710099
                   IF PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES           10720099
                       MOVE '3120-PUT-XS-MQ-MESSAGE'                    10730099
                                             TO PV-PARAGRAPH            10740099
                       MOVE PV-PARAGRAPH     TO PV-SAVE-PARAGRAPH       10750099
                       MOVE PV-MQ-COMPLETION-CODE                       10760099
                                             TO PV-SAVE-COMPLETION-CODE 10770099
                       MOVE PV-MQ-REASON     TO PV-SAVE-REASON          10780099
                       PERFORM 8000-CLOSE-MQ-QUEUE                      10790099
                       MOVE PV-SAVE-PARAGRAPH                           10800099
                                             TO PV-PARAGRAPH            10810099
                       MOVE PV-SAVE-COMPLETION-CODE                     10820099
                                             TO PV-MQ-COMPLETION-CODE   10830099
                       MOVE PV-SAVE-REASON   TO PV-MQ-REASON            10840099
                       SET PE-MQ-PUT         TO TRUE                    10850099
                       PERFORM 9900-DISPLAY-MQ-ERROR                    10860099
                   ELSE                                                 10870099
      **------->       WAIT FOR RETRY DELAY SECONDS (FILLED FROM        10880099
      **------->       TCKRSTCNTL-CKPT-TAKEN-QTY) AND TRY AGAIN         10890099
                       EXEC CICS                                        10900099
                           DELAY FOR SECONDS(PV-RETRY-DELAY-SECONDS)    10910099
                       END-EXEC                                         10920099
                   END-IF                                               10930099
               END-IF                                                   10940099
           ELSE                                                         10950099
               SET PS-MQPUT-SUCCESSFUL TO TRUE                          10960099
           END-IF.                                                      10970099
                                                                        10980099
213345******************************************************************10990099
213345*  PUT MESSAGE ON THE IM.NONVSM.ITEM.CHANGES QUEUE.               11000099
213345******************************************************************11010099
213345 3130-PUT-IM-NONVSM-MQ-MESSAGE.                                   11020099
213345                                                                  11030099
213345     MOVE PC-XS-QMGR-NAME        TO MQOD-OBJECTQMGRNAME.          11040099
213345     MOVE PC-IM-QNAME            TO MQOD-OBJECTNAME.              11050099
213345     MOVE MQMI-NONE              TO MQMD-MSGID.                   11060099
213345     MOVE SPACES                 TO MQMD-CORRELID.                11070099
213345     MOVE MQFMT-STRING           TO MQMD-FORMAT.                  11080099
213345     MOVE MQPER-PERSISTENT       TO MQMD-PERSISTENCE.             11090099
213345     MOVE +5                     TO MQMD-PRIORITY.                11100099
213345     COMPUTE MQPMO-OPTIONS = MQPMO-FAIL-IF-QUIESCING              11110099
213345                           + MQPMO-SET-IDENTITY-CONTEXT           11120099
213345                           + MQPMO-SYNCPOINT.                     11130099
213345*                                                                 11140099
213345     CALL WS-MQPUT USING PV-HCONN                                 11150099
213345                         PV-IM-PQ-HANDLE                          11160099
213345                         MQM-MESSAGE-DESCRIPTOR                   11170099
213345                         MQM-PUT-MESSAGE-OPTIONS                  11180099
213345                         PC-IM-NONVSM-OUT-MSGLENGTH               11190099
213345                         PV-IM-NONVSM-MSGBUFFER                   11200099
213345                         PV-MQ-COMPLETION-CODE                    11210099
213345                         PV-MQ-REASON.                            11220099
213345                                                                  11230099
213345*        IF PUT FAILED THEN DISPLAY ERROR MESSAGE                 11240099
213345*        AND BREAK OUT OF LOOP                                    11250099
213345                                                                  11260099
213345     IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     11270099
213345         IF (PV-MQ-REASON NOT = MQRC-PAGESET-FULL                 11280099
213345           AND PV-MQ-REASON NOT = MQRC-Q-FULL                     11290099
213345           AND PV-MQ-REASON NOT = MQRC-Q-SPACE-NOT-AVAILABLE )    11300099
213345             MOVE '3130-PUT-IM-NONVSM-MQ-MESSAGE'                 11310099
213345                                       TO PV-PARAGRAPH            11320099
213345             MOVE PV-PARAGRAPH         TO PV-SAVE-PARAGRAPH       11330099
213345             MOVE PV-MQ-COMPLETION-CODE                           11340099
213345                                       TO PV-SAVE-COMPLETION-CODE 11350099
213345             MOVE PV-MQ-REASON         TO PV-SAVE-REASON          11360099
213345             PERFORM 8000-CLOSE-MQ-QUEUE                          11370099
213345             MOVE PV-SAVE-PARAGRAPH    TO PV-PARAGRAPH            11380099
213345             MOVE PV-SAVE-COMPLETION-CODE                         11390099
213345                                       TO PV-MQ-COMPLETION-CODE   11400099
213345             MOVE PV-SAVE-REASON       TO PV-MQ-REASON            11410099
213345             SET PE-MQ-PUT             TO TRUE                    11420099
213345             PERFORM 9900-DISPLAY-MQ-ERROR                        11430099
213345         ELSE                                                     11440099
213345             ADD 1 TO PV-MQPUT-RETRY-COUNTER                      11450099
213345             IF PV-MQPUT-RETRY-COUNTER > PC-MAX-RETRIES           11460099
213345                 MOVE '3130-PUT-IM-NONVSM-MQ-MESSAGE'             11470099
213345                                       TO PV-PARAGRAPH            11480099
213345                 MOVE PV-PARAGRAPH     TO PV-SAVE-PARAGRAPH       11490099
213345                 MOVE PV-MQ-COMPLETION-CODE                       11500099
213345                                       TO PV-SAVE-COMPLETION-CODE 11510099
213345                 MOVE PV-MQ-REASON     TO PV-SAVE-REASON          11520099
213345                 PERFORM 8000-CLOSE-MQ-QUEUE                      11530099
213345                 MOVE PV-SAVE-PARAGRAPH                           11540099
213345                                       TO PV-PARAGRAPH            11550099
213345                 MOVE PV-SAVE-COMPLETION-CODE                     11560099
213345                                       TO PV-MQ-COMPLETION-CODE   11570099
213345                 MOVE PV-SAVE-REASON   TO PV-MQ-REASON            11580099
213345                 SET PE-MQ-PUT         TO TRUE                    11590099
213345                 PERFORM 9900-DISPLAY-MQ-ERROR                    11600099
213345             ELSE                                                 11610099
213345**------->       WAIT FOR RETRY DELAY SECONDS (FILLED FROM        11620099
213345**------->       TCKRSTCNTL-CKPT-TAKEN-QTY) AND TRY AGAIN         11630099
213345                 EXEC CICS                                        11640099
213345                     DELAY FOR SECONDS(PV-RETRY-DELAY-SECONDS)    11650099
213345                 END-EXEC                                         11660099
213345             END-IF                                               11670099
213345         END-IF                                                   11680099
213345     ELSE                                                         11690099
213345         SET PS-MQPUT-SUCCESSFUL TO TRUE                          11700099
213345     END-IF.                                                      11710099
213345                                                                  11720099
      ******************************************************************11730099
      *  DELETE AN ITEM EVENT MESSAGE BRIDGE DATA BASE RECORD.          11740099
      ******************************************************************11750099
       3200-DELETE-ITEM-EVENT-MSG.                                      11760099
                                                                        11770099
           INITIALIZE PV-RETRY-COUNT.                                   11780099
           PERFORM WITH TEST AFTER                                      11790099
                 UNTIL SQLCODE = ZERO                                   11800099
                  OR   PV-RETRY-COUNT > PC-MAXIMUM-RETRY-COUNT          11810099
                                                                        11820099
               EXEC SQL                                                 11830099
                   DELETE FROM IMT_EVNT_MSG_BRG                         11840099
                   WHERE RMS_STYL_ID      = :PV-SAVE-RMS-STYL-ID        11850099
                     AND CRE_TMST         = :PV-SAVE-CRE-TMST           11860099
                     AND BUS_EVNT_TYP_CDE = :PV-SAVE-BUS-EVNT-TYP-CDE   11870099
               END-EXEC                                                 11880099
                                                                        11890099
               EVALUATE TRUE                                            11900099
                   WHEN SQLCODE = ZERO                                  11910099
                       CONTINUE                                         11920099
                   WHEN SQLCODE = -911                                  11930099
                   WHEN SQLCODE = -913                                  11940099
                       ADD 1 TO PV-RETRY-COUNT                          11950099
                   WHEN OTHER                                           11960099
216771                DISPLAY 'IMKCS301 DELETE FAILED '                 11970099
216771                        ' RMS STYLE ID ' PV-SAVE-RMS-STYL-ID      11980099
216771                        ' CRE TMST '     PV-SAVE-CRE-TMST         11990099
216771                        ' BUS EVENT '    PV-SAVE-BUS-EVNT-TYP-CDE 12000099
                       MOVE '3200-DELETE-ITEM-EVENT-MSG'                12010099
                                                 TO PV-PARAGRAPH        12020099
                       MOVE 'DELETE FROM IMT_EVNT_MSG_BRG'              12030099
                                                 TO AA-DB2-OPERATION    12040099
                       MOVE 'IMT_EVNT_MSG_BRG'   TO AA-DB2-TABLE-1      12050099
                       PERFORM 9800-DB2-ERROR-ROUTINE                   12060099
               END-EVALUATE                                             12070099
           END-PERFORM.                                                 12080099
                                                                        12090099
           IF SQLCODE NOT = ZERO                                        12100099
               MOVE '3200-DELETE-ITEM-EVENT-MSG'                        12110099
                                             TO PV-PARAGRAPH            12120099
               MOVE 'DELETE FROM IMT_EVNT_MSG_BRG'                      12130099
                                             TO AA-DB2-OPERATION        12140099
               MOVE 'IMT_EVNT_MSG_BRG'       TO AA-DB2-TABLE-1          12150099
               PERFORM 9800-DB2-ERROR-ROUTINE                           12160099
           END-IF.                                                      12170099
                                                                        12180099
      ******************************************************************12190099
      *  WRITE MESSAGE TO THE ITEM BRIDGE BACKUP VSAM FILE.             12200099
      ******************************************************************12210099
       3300-WRITE-BACKUP-MESSAGE.                                       12220099
                                                                        12230099
           EXEC CICS WRITE                                              12240099
                FROM  (IMRD008-RECORD)                                  12250099
                RIDFLD(PV-RBA)                                          12260099
                RBA                                                     12270099
                FILE  ('IMBRGBKP')                                      12280099
                RESP  (PV-CICS-RESPONSE)                                12290099
           END-EXEC.                                                    12300099
      *  LOGIC CONTAINS THE FOLLOWING                                   12310099
      *  WAS DP013-LOGIC-ABEND AND DP013-NO-ROLLBACK (BLANK SCREEN)     12320099
      *                                                                 12330099
      *  CHANGED HANDLING . AND . . CAUSED SCREEN TO FREEZE UP          12340099
      *  ADD DP013-LOGIC-ABEND AND DP013-XCTL-DISPLAY-RESTART (FREEZE)  12350099
      *  AND KEEP DP013-NO-ROLLBACK                                     12360099
      *                                                                 12370099
           IF  PV-CICS-RESPONSE NOT EQUAL DFHRESP(NORMAL)               12380099
               SET DP013-LINK-RETURN                                    12390099
                   DP013-OTHER-ABEND                                    12400099
                   DP013-ROLLBACK        TO TRUE                        12410099
               MOVE '5300-WRITE-VSAM-EXCEPT-REC'                        12420099
                                         TO DP013-PARAGRAPH             12430099
               MOVE 'INVALID RESPONSE FROM WRITE COMMAND:'              12440099
                                         TO DP013-MESSAGE-TEXT (1)      12450099
               MOVE PV-CICS-RESPONSE     TO PV-DISPLAY-RESP             12460099
               MOVE PV-DISPLAY-RESP      TO DP013-MESSAGE-TEXT (2)      12470099
               PERFORM 9950-PROGRAM-ABEND                               12480099
           END-IF.                                                      12490099
                                                                        12500099
      ******************************************************************12510099
      *  DETERMINE IF A DB2 COMMIT AND MQCOMMIT SHOULD BE TAKEN.        12520099
      ******************************************************************12530099
       4000-DB2-MQ-COMMIT-CHECK.                                        12540099
                                                                        12550099
           PERFORM 4100-SET-CURRENT-TIMESTAMP.                          12560099
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                12570099
      *-->     COMMIT CHANGES TO THE DB2 TABLES                         12580099
               PERFORM 4500-DB2-MQ-COMMIT                               12590099
               PERFORM 4200-GET-COMMIT-INFO                             12600099
               PERFORM 4500-DB2-MQ-COMMIT                               12610099
      *-->     CHECK IF THE TOTAL NUMBER OF RECORDS PROCESSED IS        12620099
      *-->     GREATER THAN THE RMS CATCHUP RECORDS COUNT               12630099
               IF PV-RMS-CATCHUP-RECS-COUNT > TCKRSTCNTL-TRANS-PRCSD-QTY12640099
                   MOVE PV-RMS-CATCHUP-RECS-COUNT TO                    12650099
                        PV-LOG-RCDM-CATCHUP-RECS-CNT                    12660099
                   MOVE PV-RMS-CATCHUP-DELAY-MINUTES TO                 12670099
                        PV-LOG-RCDM-CATCHUP-DLY-MIN                     12680099
                   MOVE PV-LOG-RMS-CATCHUP-DELAY-MSG                    12690099
                       TO PV-SL-SYS-LOG-MESSAGE-DTL                     12700099
                   PERFORM 9000-SYSTEM-LOG-MESSAGE                      12710099
                   MOVE ZERO TO PV-RMS-CATCHUP-RECS-COUNT               12720099
      **------->   DELAY FOR RMS CATCHUP DELAY MINUTES (FILLED FROM     12730099
      **------->   TCKRSTCNTL-TRANS-QTY)                                12740099
                   EXEC CICS                                            12750099
                       DELAY FOR MINUTES(PV-RMS-CATCHUP-DELAY-MINUTES)  12760099
                   END-EXEC                                             12770099
               END-IF                                                   12780099
           END-IF.                                                      12790099
                                                                        12800099
      ******************************************************************12810099
      *  GET THE CURRENT TIMESTAMP FROM DB2.                            12820099
      ******************************************************************12830099
       4100-SET-CURRENT-TIMESTAMP.                                      12840099
                                                                        12850099
           EXEC SQL                                                     12860099
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           12870099
           END-EXEC.                                                    12880099
      *                                                                 12890099
           IF SQLCODE = 0                                               12900099
               CONTINUE                                                 12910099
           ELSE                                                         12920099
               MOVE '4100-SET-CURRENT-TIMESTAMP'                        12930099
                                             TO PV-PARAGRAPH            12940099
               MOVE 'SET CURRENT TIMESTAMP'  TO AA-DB2-OPERATION        12950099
               MOVE 'DB2 SYSTEM'             TO AA-DB2-TABLE-1          12960099
               PERFORM 9800-DB2-ERROR-ROUTINE                           12970099
           END-IF.                                                      12980099
                                                                        12990099
      ******************************************************************13000099
      *  GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL TABLE.    13010099
      ******************************************************************13020099
       4200-GET-COMMIT-INFO.                                            13030099
                                                                        13040099
           EXEC SQL                                                     13050099
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        13060099
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       13070099
                   , TRANS_QTY                                          13080099
                   , CKPT_TAKEN_QTY                                     13090099
                   , TRANS_PRCSD_QTY                                    13100099
                   , RST_QTY                                            13110099
               INTO :PV-COMMIT-TIMESTAMP                                13120099
                   ,:TCKRSTCNTL-TRANS-QTY                               13130099
                   ,:TCKRSTCNTL-CKPT-TAKEN-QTY                          13140099
                   ,:TCKRSTCNTL-TRANS-PRCSD-QTY                         13150099
                   ,:TCKRSTCNTL-RST-QTY                                 13160099
               FROM TCKRSTCNTL                                          13170099
              WHERE JOB_NAME  = :PC-JOB-NAME                            13180099
                AND PLAN_NAME = :PC-CURRENT-PROGRAM-NAME                13190099
           END-EXEC.                                                    13200099
                                                                        13210099
           IF SQLCODE = 0                                               13220099
               MOVE TCKRSTCNTL-TRANS-QTY TO PV-RMS-CATCHUP-DELAY-MINUTES13230099
               MOVE TCKRSTCNTL-CKPT-TAKEN-QTY                           13240099
                                         TO PV-RETRY-DELAY-SECONDS      13250099
               MOVE TCKRSTCNTL-RST-QTY   TO PV-RESTART-DELAY-SECONDS    13260099
           ELSE                                                         13270099
               MOVE '4200-GET-COMMIT-INFO'   TO PV-PARAGRAPH            13280099
               MOVE 'SELECT FROM TCKRSTCNTL' TO AA-DB2-OPERATION        13290099
               MOVE 'TCKRSTCNTL'             TO AA-DB2-TABLE-1          13300099
               PERFORM 9800-DB2-ERROR-ROUTINE                           13310099
           END-IF.                                                      13320099
                                                                        13330099
      ******************************************************************13340099
      *  COMMIT THE MQ PUTS AND DB2 DELETES BY USING A CICS SYNCPOINT   13350099
      *  COMMAND. MQ COMMIT DOES NOT WORK IN CICS, THE SYNCPOINT MUST   13360099
      *  BE USED.                                                       13370099
      ******************************************************************13380099
       4500-DB2-MQ-COMMIT.                                              13390099
                                                                        13400099
           EXEC CICS                                                    13410099
               SYNCPOINT                                                13420099
           END-EXEC.                                                    13430099
                                                                        13440099
      ******************************************************************13450099
      *  WRITE ALL JCL LINES FOR THE JOB DECK OF THE VSM BRIDGE TO      13460099
      *  ITEM HUB SHUTDOWN JOB TO THE TRANSIENT DATA QUEUE FOR THE      13470099
      *  INTERNAL READER.  THIS WILL SUBMIT THE JOB WITH THE CURRENT    13480099
      *  CICS PARTITION AS THE OWNER.  ENQUEUE THE INTERNAL READER      13490099
      *  BEFORE WRITING ANY JCL LINES, TO INSURE NO INTERLEAVING OF     13500099
      *  JCL LINES FROM OTHER REQUESTS, AND DEQUEUE THE INTERNAL        13510099
      *  READER AFTER WRITING THE LAST JCL LINE.                        13520099
      ******************************************************************13530099
       5000-SUBMIT-BACKUP-DUMP-JOB.                                     13540099
                                                                        13550099
           PERFORM 7000-ENQUEUE-INTERNAL-READER.                        13560099
                                                                        13570099
           IF PC-KOHLS-PROD-SYSTEM                                      13580099
               MOVE JL-JCL-LINE-1P TO TD-JCL-LINE                       13590099
           ELSE                                                         13600099
               IF PC-KOHLS-LEARNING-SYSTEM                              13610099
                   MOVE JL-JCL-LINE-1L TO TD-JCL-LINE                   13620099
               ELSE                                                     13630099
                   IF PC-KOHLS-QA-SYSTEM                                13640099
                       MOVE JL-JCL-LINE-1Q TO TD-JCL-LINE               13650099
                   ELSE                                                 13660099
                       MOVE JL-JCL-LINE-1T TO TD-JCL-LINE               13670099
                   END-IF                                               13680099
               END-IF                                                   13690099
           END-IF.                                                      13700099
           PERFORM 5100-WRITE-TD-LINE.                                  13710099
                                                                        13720099
           MOVE JL-JCL-LINE-2 TO TD-JCL-LINE.                           13730099
           PERFORM 5100-WRITE-TD-LINE.                                  13740099
                                                                        13750099
           IF PC-KOHLS-PROD-SYSTEM                                      13760099
               MOVE JL-JCL-LINE-3P TO TD-JCL-LINE                       13770099
           ELSE                                                         13780099
               IF PC-KOHLS-LEARNING-SYSTEM                              13790099
                   MOVE JL-JCL-LINE-3L TO TD-JCL-LINE                   13800099
               ELSE                                                     13810099
                   IF PC-KOHLS-QA-SYSTEM                                13820099
                       MOVE JL-JCL-LINE-3Q TO TD-JCL-LINE               13830099
                   ELSE                                                 13840099
                       MOVE JL-JCL-LINE-3T TO TD-JCL-LINE               13850099
                   END-IF                                               13860099
               END-IF                                                   13870099
           END-IF.                                                      13880099
           PERFORM 5100-WRITE-TD-LINE.                                  13890099
                                                                        13900099
           MOVE JL-JCL-LINE-EOF TO TD-JCL-LINE.                         13910099
           PERFORM 5100-WRITE-TD-LINE.                                  13920099
                                                                        13930099
           PERFORM 7100-DEQUEUE-INTERNAL-READER.                        13940099
                                                                        13950099
      ******************************************************************13960099
      *  WRITE A JCL LINE TO THE TRANSIENT DATA QUEUE FOR THE INTERNAL *13970099
      *  READER.  ON ANY ERROR, DEQUEUE THE INTERNAL READER AND ABEND. *13980099
      ******************************************************************13990099
       5100-WRITE-TD-LINE.                                              14000099
           EXEC CICS                                                    14010099
               WRITEQ TD                                                14020099
                   QUEUE(TD-QUEUE-NAME)                                 14030099
                   FROM(TD-JCL-LINE)                                    14040099
                   LENGTH(TD-JCL-LINE-LENGTH)                           14050099
                   RESP(PV-CICS-RESPONSE)                               14060099
           END-EXEC.                                                    14070099
                                                                        14080099
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    14090099
               SET DP013-LINK-RETURN                                    14100099
                   DP013-OTHER-ABEND                                    14110099
                   DP013-NO-ROLLBACK     TO TRUE                        14120099
               MOVE '5100-WRITE-TD-LINE' TO DP013-PARAGRAPH             14130099
               MOVE 'ATTEMPTING TO WRITE TO INTERNAL READER TD QUEUE'   14140099
                                         TO DP013-MESSAGE-TEXT(1)       14150099
               PERFORM 7100-DEQUEUE-INTERNAL-READER                     14160099
               PERFORM 9950-PROGRAM-ABEND                               14170099
           END-IF.                                                      14180099
                                                                        14190099
      ******************************************************************14200099
      *  ENQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *14210099
      *  THIS WILL PREVENT INTERLEAVING OF JCL SUBMITTED CONCURRENTLY  *14220099
      *  BY MULTIPLE USERS.  IF THE QUEUE IS BUSY, THIS PROGRAM WILL   *14230099
      *  WAIT TO ENQUEUE IT, BY NOT SPECIFICALLY HANDLING THE QBUSY    *14240099
      *  CONDITION.                                                    *14250099
      ******************************************************************14260099
       7000-ENQUEUE-INTERNAL-READER.                                    14270099
           EXEC CICS                                                    14280099
               ENQ                                                      14290099
                   RESOURCE(TD-QUEUE-NAME)                              14300099
                   LENGTH(TD-QUEUE-NAME-LENGTH)                         14310099
                   RESP(PV-CICS-RESPONSE)                               14320099
           END-EXEC.                                                    14330099
                                                                        14340099
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    14350099
               SET DP013-LINK-RETURN                                    14360099
                   DP013-OTHER-ABEND                                    14370099
                   DP013-NO-ROLLBACK     TO TRUE                        14380099
               MOVE '7000-ENQUEUE-INTERNAL-READER'                      14390099
                                         TO DP013-PARAGRAPH             14400099
               STRING 'ENQUEUE '            DELIMITED BY SIZE           14410099
                      TD-QUEUE-NAME         DELIMITED BY SIZE           14420099
                      ' (INTERNAL READER)'  DELIMITED BY SIZE           14430099
                   INTO DP013-MESSAGE-TEXT (1)                          14440099
               PERFORM 9950-PROGRAM-ABEND                               14450099
           END-IF.                                                      14460099
                                                                        14470099
           SET PS-INTERNAL-READER-ENQUEUED TO TRUE.                     14480099
                                                                        14490099
      ******************************************************************14500099
      *  DEQUEUE THE TRANSIENT DATA QUEUE FOR THE INTERNAL READER.     *14510099
      *  THIS SHOULD BE DONE FOLLOWING A SUCCESSFUL REPORT REQUEST     *14520099
      *  SUBMISSION, A CANCELLATION OF THE CURRENT REPORT REQUEST, OR  *14530099
      *  ANY DETECTED ABENDS.  THIS WILL FREE UP THE INTERNAL READER   *14540099
      *  FOR THE NEXT USER.                                            *14550099
      ******************************************************************14560099
       7100-DEQUEUE-INTERNAL-READER.                                    14570099
           SET PS-INTERNAL-READER-DEQUEUED TO TRUE.                     14580099
                                                                        14590099
           EXEC CICS                                                    14600099
               DEQ                                                      14610099
                   RESOURCE(TD-QUEUE-NAME)                              14620099
                   LENGTH(TD-QUEUE-NAME-LENGTH)                         14630099
                   RESP(PV-CICS-RESPONSE)                               14640099
           END-EXEC.                                                    14650099
                                                                        14660099
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                    14670099
               SET DP013-LINK-RETURN                                    14680099
                   DP013-OTHER-ABEND                                    14690099
                   DP013-NO-ROLLBACK     TO TRUE                        14700099
               MOVE '7100-DEQUEUE-INTERNAL-READER'                      14710099
                                         TO DP013-PARAGRAPH             14720099
               STRING 'DEQUEUE '            DELIMITED BY SIZE           14730099
                      TD-QUEUE-NAME         DELIMITED BY SIZE           14740099
                      ' (INTERNAL READER)'  DELIMITED BY SIZE           14750099
                   INTO DP013-MESSAGE-TEXT (1)                          14760099
               PERFORM 9950-PROGRAM-ABEND                               14770099
           END-IF.                                                      14780099
                                                                        14790099
                                                                        14800099
      ******************************************************************14810099
      *  CLOSE THE OUTPUT MESSAGE QUEUE.                                14820099
      ******************************************************************14830099
       8000-CLOSE-MQ-QUEUE.                                             14840099
                                                                        14850099
           CALL WS-MQCLOSE USING PV-HCONN                               14860099
                                 PV-PQ-HANDLE                           14870099
                                 MQCO-NONE                              14880099
                                 PV-MQ-COMPLETION-CODE                  14890099
                                 PV-MQ-REASON.                          14900099
                                                                        14910099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     14920099
              MOVE '8000-CLOSE-MQ-QUEUE - CLOSE ERROR' TO PV-PARAGRAPH  14930099
              SET PE-MQ-CLOSE TO TRUE                                   14940099
              PERFORM 9900-DISPLAY-MQ-ERROR                             14950099
           END-IF.                                                      14960099
                                                                        14970099
      *    CLOSE THE XS.ITEM.ATTRIBUTES.Q OUTPUT MESSAGE QUEUE.         14980099
           CALL WS-MQCLOSE USING PV-HCONN                               14990099
                                 PV-XS-PQ-HANDLE                        15000099
                                 MQCO-NONE                              15010099
                                 PV-MQ-COMPLETION-CODE                  15020099
                                 PV-MQ-REASON.                          15030099
                                                                        15040099
           IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     15050099
               MOVE '8000-CLOSE-MQ-QUEUE-XS - CLOSE ERROR'              15060099
                     TO PV-PARAGRAPH                                    15070099
               SET PE-MQ-CLOSE TO TRUE                                  15080099
               PERFORM 9900-DISPLAY-MQ-ERROR                            15090099
           END-IF.                                                      15100099
                                                                        15110099
213345*    CLOSE THE IM.NONVSM.ITEM.CHANGES.Q OUTPUT MESSAGE QUEUE.     15120099
213345     CALL WS-MQCLOSE USING PV-HCONN                               15130099
213345                           PV-IM-PQ-HANDLE                        15140099
213345                           MQCO-NONE                              15150099
213345                           PV-MQ-COMPLETION-CODE                  15160099
213345                           PV-MQ-REASON.                          15170099
213345                                                                  15180099
213345     IF (PV-MQ-COMPLETION-CODE NOT = MQCC-OK)                     15190099
213345         MOVE '8000-CLOSE-MQ-QUEUE-IM - CLOSE ERROR'              15200099
213345               TO PV-PARAGRAPH                                    15210099
213345         SET PE-MQ-CLOSE TO TRUE                                  15220099
213345         PERFORM 9900-DISPLAY-MQ-ERROR                            15230099
213345     END-IF.                                                      15240099
                                                                        15250099
      ******************************************************************15260099
      *  POST PRE-FORMATTED MESSAGES TO THE CICS SYSTEM LOG.  ENQUEUE  *15270099
      *  THE LOG TO ENSURE THAT ALL MESSAGES FOR THE SAME ERROR ARE    *15280099
      *  CONSECUTIVE IN THE LOG (MESSAGES WILL BE POSTED EVEN IF THE   *15290099
      *  ENQUEUE IS NOT SUCCESSFUL).  POST THE MESSAGES AND DEQUEUE    *15300099
      *  THE CICS SYSTEM LOG.                                          *15310099
      ******************************************************************15320099
       9000-SYSTEM-LOG-MESSAGE.                                         15330099
                                                                        15340099
      *  FORMAT THE CURRENT DATE AND TIME, USING THE ABSOLUTE TIME.     15350099
           EXEC CICS                                                    15360099
               ASKTIME                                                  15370099
                   ABSTIME (PV-DT-CURRENT-ABSTIME)                      15380099
           END-EXEC.                                                    15390099
                                                                        15400099
           EXEC CICS                                                    15410099
               FORMATTIME                                               15420099
                   ABSTIME  (PV-DT-CURRENT-ABSTIME)                     15430099
                   MMDDYY   (PV-DT-FORMAT-DATE-MMDDYY)                  15440099
                   DATESEP  ('/')                                       15450099
                   TIME     (PV-DT-CURRENT-TIME)                        15460099
                   TIMESEP  (':')                                       15470099
           END-EXEC.                                                    15480099
                                                                        15490099
           MOVE PV-DT-FORMAT-DATE-MMDDYY TO  PV-SL-MESSAGE-DATE.        15500099
           MOVE PV-DT-CURRENT-TIME       TO  PV-SL-MESSAGE-TIME.        15510099
           MOVE PC-CURRENT-PROGRAM-NAME  TO  PV-SL-PROGRAM-ID.          15520099
                                                                        15530099
      *    WRITE A MESSAGE TO THE CICS SYSTEM LOG FOR THE CURRENT CICS  15540099
      *    REGION.  ALL EXCEPTIONAL CONDITIONS WILL BE IGNORED.         15550099
           EXEC CICS                                                    15560099
               WRITEQ TD                                                15570099
                   QUEUE  (PV-SL-SYS-LOG-QUEUE-NAME)                    15580099
                   FROM   (PV-SL-SYSTEM-LOG-MESSAGE)                    15590099
                   LENGTH (PV-SL-SYS-LOG-MSG-LENGTH)                    15600099
                   RESP   (PV-CICS-RESPONSE)                            15610099
           END-EXEC.                                                    15620099
                                                                        15630099
      ******************************************************************15640099
      *     DB2 ERROR ROUTINE                                           15650099
      ******************************************************************15660099
       9800-DB2-ERROR-ROUTINE.                                          15670099
                                                                        15680099
           SET DP013-LINK-RETURN                                        15690099
               DP013-DB2-ABEND                                          15700099
               DP013-ROLLBACK     TO TRUE.                              15710099
           MOVE SQLCA             TO DP013-SQLCA.                       15720099
           MOVE PV-PARAGRAPH      TO DP013-PARAGRAPH.                   15730099
           MOVE AA-DB2-OPERATION-LIT                                    15740099
                                  TO DP013-MESSAGE-TEXT (1).            15750099
           MOVE AA-DB2-TABLE-1    TO DP013-DB2-TABLE-NAME (1).          15760099
           IF PS-FETCH-DEADLOCK                                         15770099
               PERFORM DP014-0000-PROCESS-ABEND                         15780099
           ELSE                                                         15790099
               PERFORM 9950-PROGRAM-ABEND                               15800099
           END-IF.                                                      15810099
                                                                        15820099
      ******************************************************************15830099
      * DISPLAY THE VALUES OF A FAILED MQ API COMMAND, AND ABEND PGM    15840099
      ******************************************************************15850099
       9900-DISPLAY-MQ-ERROR.                                           15860099
                                                                        15870099
           CALL WS-MQCHECK USING PV-MQ-REASON PE-MQ-REASON-TEXT.        15880099
           MOVE PV-MQ-REASON      TO PE-MQ-REASON-CODE.                 15890099
                                                                        15900099
           SET DP013-LINK-RETURN                                        15910099
               DP013-OTHER-ABEND                                        15920099
               DP013-ROLLBACK     TO TRUE.                              15930099
           MOVE PV-PARAGRAPH      TO DP013-PARAGRAPH.                   15940099
           MOVE PE-MQ-ERROR-MESSAGE                                     15950099
                                  TO DP013-MESSAGE-TEXT (1).            15960099
           MOVE PE-MQ-REASON-MESSAGE                                    15970099
                                  TO DP013-MESSAGE-TEXT (2).            15980099
           PERFORM 9950-PROGRAM-ABEND.                                  15990099
                                                                        16000099
      ******************************************************************16010099
      * WRITE AN OPERATOR CONSOLE MESSAGE (IF CICS PRODUCTION) AND      16020099
      * ABEND THE PROGRAM                                               16030099
      ******************************************************************16040099
       9950-PROGRAM-ABEND.                                              16050099
                                                                        16060099
           IF PC-KOHLS-PROD-SYSTEM                                      16070099
               EXEC CICS WRITE OPERATOR                                 16080099
                         TEXT (PC-VSM-BRIDGE-DOWN-MSG)                  16090099
                         TEXTLENGTH (PC-BRIDGE-DOWN-MSG-LEN)            16100099
                         CRITICAL                                       16110099
               END-EXEC                                                 16120099
           END-IF.                                                      16130099
           PERFORM DP014-0000-PROCESS-ABEND.                            16140099
           PERFORM 9990-EXEC-CICS-RETURN.                               16150099
                                                                        16160099
       9990-EXEC-CICS-RETURN.                                           16170099
           EXEC CICS                                                    16180099
               RETURN                                                   16190099
           END-EXEC.                                                    16200099
                                                                        16210099
      ******************************************************************16220099
      *    NON-CICS ARCHITECTURE ABEND ROUTINE CALL                     16230099
      ******************************************************************16240099
                                                                        16250099
           COPY DPPD014.                                                16260099
