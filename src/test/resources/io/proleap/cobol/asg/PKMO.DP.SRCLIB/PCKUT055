       IDENTIFICATION DIVISION.                                                 
       TITLE 'EXTRACT INVENTORY STORES FOR STORE EXPECTED DELETIONS'            
                                                                                
       PROGRAM-ID.             PCKUT055.                                        
       AUTHOR.                 DENNIS STEFFEN.                                  
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            DOG DAYS OF AUGUST 2005.                         
       DATE-COMPILED.                                                           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT SELECT-STORE-EXT-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INV-STORE-FILE                                                
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT INV-STORE-EXT-TODAY                                           
               ASSIGN TO OUT02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  SELECT-STORE-EXT-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  SELECT-STORE-REC                 PIC X(080).                         
                                                                                
       FD  INV-STORE-FILE                                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  INV-STORE-REC                    PIC X(080).                         
                                                                                
       FD  INV-STORE-EXT-TODAY                                                  
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  INV-STORE-EXT-TODAY-REC          PIC X(080).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER.                                                              
           05  FILLER                      PIC  X(36)    VALUE                  
               ' ***** WORKING STORAGE PGM=PCKUT055 '.                          
                                                                                
       01  PS-SWITCHES.                                                         
                                                                                
           05  PS-END-CSR-SW                PIC X(01) VALUE 'N'.                
               88  PS-END-CSR-YES                     VALUE 'Y'.                
               88  PS-END-CSR-NO                      VALUE 'N'.                
           05  PS-ALL-STORES-SW             PIC X(01) VALUE 'N'.                
               88  PS-ALL-STORES-YES                  VALUE 'Y'.                
               88  PS-ALL-STORES-NO                   VALUE 'N'.                
                                                                                
       01  DK-DB2-KEYS.                                                         
           05  DK-STORE-LOW                 PIC S9(04) COMP VALUE ZERO.         
           05  DK-STORE-HIGH                PIC S9(04) COMP VALUE ZERO.         
           05  DK-DTE-OFFSET                PIC S9(04) COMP VALUE ZERO.         
           05  DK-LOW-DTE                   PIC X(10) VALUE SPACES.             
           05  DK-HIGH-DTE                  PIC X(10) VALUE SPACES.             
       01  PC-CONSTANTS.                                                        
           05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
                                                         'PCKUT055'.            
           05  PC-ILBOABN0                 PIC  X(08)    VALUE                  
                                                         'ILBOABN0'.            
                                                                                
       01  PV-MISC-STUFF.                                                       
           05  PV-DATE                      PIC X(10) VALUE SPACES.             
           05  PV-BOOK-DTE-ADJ              PIC X(10) VALUE SPACES.             
           05  PV-ABEND-PARAGRAPH           PIC X(30) VALUE SPACES.             
           05  PV-DB2-OPERATION-ATTEMPTED   PIC X(60) VALUE SPACE.              
           05  PV-SQL-CODE                  PIC 9(04) VALUE ZERO.               
           05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
                                                         COMP SYNC.             
           05  PV-WRITE-1-COUNTER           PIC S9(09) COMP-3                   
                                                    VALUE ZERO.                 
           05  PV-WRITE-2-COUNTER           PIC S9(09) COMP-3                   
                                                    VALUE ZERO.                 
           05  PV-ABEND-OPERATION           PIC X(20) VALUE SPACES.             
           05  PV-ABEND-STRUCTURE-1         PIC X(20) VALUE SPACES.             
                                                                                
       01  ABEND-CODE                       PIC S9(04)     VALUE +0             
                                                           COMP SYNC.           
           88  ABEND-4013-DB2-ERROR                        VALUE +4013.         
                                                                                
      *    INTRANSIT UNLOAD FILE                                                
           COPY PCRD046.                                                        
                                                                                
      *    STORES PURGE DATA EXTRACT FILE.                                      
           COPY PCRD067.                                                        
                                                                                
      *    PARM TABLE                                                           
           EXEC SQL                                                             
               INCLUDE TKRPRPM                                                  
           END-EXEC.                                                            
                                                                                
      *    XLT_MDSE_INV_PARM                                                    
           EXEC SQL                                                             
               INCLUDE XLT00025                                                 
           END-EXEC.                                                            
                                                                                
      *    XLT_LOC                                                              
           EXEC SQL                                                             
               INCLUDE XLT00010                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               DECLARE INV-CSR CURSOR FOR                                       
               SELECT                                                           
                    P.LOC_NBR                                                   
                  , P.INV_DTE                                                   
                  , P.FIN_BK_DTE                                                
                  , PARM.FUNC_QTY                                               
                  , P.PLND_INV_DTE                                              
                  , P.FIN_BK_DTE + PARM.FUNC_QTY DAYS                           
               FROM                                                             
                   XLT_MDSE_INV_PARM P                                          
                ,  TKRPRPM           PARM                                       
                ,  XLT_LOC           L                                          
               WHERE                                                            
                    PARM.LOC_ID       = 90                                      
                AND P.LOC_NBR              = L.LOC_NBR                          
                AND PARM.INTFC_SYS_CDE     = 'KHL'                              
                AND PARM.SYS_PRC_FUNC_CDE  = 'PURG01'                           
                AND PARM.FUNC_PRC_STAT_CDE = 'AC'                               
                AND P.LOC_NBR BETWEEN  :DK-STORE-LOW  AND                       
                                       :DK-STORE-HIGH                           
                                                                                
                AND P.FIN_BK_DTE       <> '9999-09-09'                          
                AND (P.FIN_BK_DTE + PARM.FUNC_QTY DAYS) BETWEEN                 
                                    :DK-LOW-DTE                                 
                AND                 :DK-HIGH-DTE                                
                AND                                                             
                (                                                               
                   (P.SCHD_PHY_CNT_CDE       = 'PI'                             
                 AND P.SCN_INV_IND           = 'Y')                             
                                                                                
                 OR                                                             
                                                                                
                    (L.E_BUS_IND           = 'Y'                                
                     AND L.LOC_TYP_CDE     = 'DS')                              
                                                                                
                )                                                               
                                                                                
                WITH UR                                                         
           END-EXEC.                                                            
      /                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
           COPY DPWS004.                                                        
      /                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           IF PV-WRITE-2-COUNTER  > ZEROES                                      
              PERFORM 2000-PROCESS                                              
           END-IF                                                               
           PERFORM 9000-QUIT.                                                   
           GOBACK.                                                              
                                                                                
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION         '  TO PV-ABEND-PARAGRAPH.          
                                                                                
           DISPLAY SPACES                                                       
           DISPLAY SPACES                                                       
           DISPLAY 'START PCKUT055'                                             
           DISPLAY SPACES                                                       
                                                                                
               OPEN INPUT                                                       
                   SELECT-STORE-EXT-FILE                                        
               OPEN OUTPUT                                                      
                    INV-STORE-FILE                                              
                    INV-STORE-EXT-TODAY                                         
                                                                                
               PERFORM 8000-READ-STORE-CONTROL UNTIL PS-ALL-STORES-YES          
                    OR PC067-PROCESSED-REC-SW  EQUAL 'N'                        
                 IF PC067-PROCESSED-REC-SW  = 'N'                               
                    MOVE PC067-STORE-START-NUM TO DK-STORE-LOW                  
                    MOVE PC067-STORE-END-NUM   TO DK-STORE-HIGH                 
                    MOVE PC067-OFFSET-DATE     TO DK-DTE-OFFSET                 
                    MOVE PC067-PURGE-DATE      TO PV-DATE                       
                                                  DK-HIGH-DTE                   
                    PERFORM 7100-CALC-LOW-DTE                                   
                    DISPLAY 'DATE RANGE IS: ' DK-LOW-DTE ' THRU '               
                             DK-HIGH-DTE                                        
                    DISPLAY SPACES                                              
                    DISPLAY 'STORE CONTROL = ' DK-STORE-LOW ' THRU '            
                             DK-STORE-HIGH                                      
                    DISPLAY SPACES                                              
                    DISPLAY SPACES                                              
                    PERFORM 8200-WRITE-STORE-PURGE-TODAY                        
            DISPLAY 'LOC      INV         BOOK      PLND-INV   ADJ BOOK'        
              '       DAYS'                                                     
                                                                                
                 END-IF.                                                        
                                                                                
      /                                                                         
       2000-PROCESS.                                                            
           PERFORM 7000-OPEN-CSR                                                
           PERFORM 7010-FETCH-CSR                                               
           PERFORM UNTIL PS-END-CSR-YES                                         
              MOVE '2000-PROCESS'              TO PV-ABEND-PARAGRAPH            
                                                                                
              MOVE XLT00025-LOC-NBR            TO PC046-LOC-NBR                 
              MOVE XLT00025-INV-DTE            TO PC046-INV-DTE                 
              MOVE XLT00025-PLND-INV-DTE       TO PC046-PLND-INV-DTE            
              MOVE KRPRPM-FUNC-QTY             TO PC046-FIN-DTE-ADJ-QTY         
              MOVE XLT00025-FIN-BK-DTE         TO PC046-FIN-BK-DTE              
              MOVE PV-BOOK-DTE-ADJ             TO PC046-FIN-BK-DTE-ADJ          
              MOVE DK-LOW-DTE                  TO PC046-BEGIN-DTE               
              MOVE PV-DATE                     TO PC046-END-DTE                 
                                                                                
              PERFORM 8100-WRITE-STORE-REC                                      
              PERFORM 7010-FETCH-CSR                                            
           END-PERFORM                                                          
           PERFORM 7020-CLOSE-CSR.                                              
      /                                                                         
       7000-OPEN-CSR.                                                           
                                                                                
           MOVE '7000-OPEN-CSR'             TO PV-ABEND-PARAGRAPH.              
                                                                                
           EXEC SQL                                                             
               OPEN INV-CSR                                                     
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                       CONTINUE                                                 
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
               WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
                   DISPLAY '**************************************'             
                   DISPLAY 'WARNING ISSUED '                                    
                   DISPLAY 'OPEN CURSOR                           '             
                   DISPLAY '**************************************'             
                   PERFORM 9800-SQL-WARNING                                     
                                                                                
               WHEN SQLCODE < ZERO                                              
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED ON                       '             
                   DISPLAY 'OPEN CURSOR                           '             
                   DISPLAY '**************************************'             
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       7010-FETCH-CSR.                                                          
                                                                                
           MOVE '7010-FETCH-CSR'          TO PV-ABEND-PARAGRAPH.                
                                                                                
           EXEC SQL                                                             
               FETCH INV-CSR                                                    
               INTO                                                             
                   :XLT00025-LOC-NBR                                            
               ,   :XLT00025-INV-DTE                                            
               ,   :XLT00025-FIN-BK-DTE                                         
               ,   :KRPRPM-FUNC-QTY                                             
               ,   :XLT00025-PLND-INV-DTE                                       
               ,   :PV-BOOK-DTE-ADJ                                             
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY XLT00025-LOC-NBR '  '                                
                           XLT00025-INV-DTE '  '                                
                           XLT00025-FIN-BK-DTE '  '                             
                           XLT00025-PLND-INV-DTE '  '                           
                           PV-BOOK-DTE-ADJ       '  '                           
                           KRPRPM-FUNC-QTY                                      
                                                                                
               WHEN SQLCODE = +100                                              
                   SET PS-END-CSR-YES       TO TRUE                             
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
               WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
                   DISPLAY '**************************************'             
                   DISPLAY 'WARNING ISSUED '                                    
                   DISPLAY 'FETCH CURSOR                          '             
                   DISPLAY 'XLT_MDSE_INV_PARM                     '             
                   DISPLAY '**************************************'             
                   PERFORM 9800-SQL-WARNING                                     
                                                                                
               WHEN SQLCODE < ZERO                                              
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED ON                       '             
                   DISPLAY 'FETCH CURSOR                          '             
                   DISPLAY 'XLT_MDSE_INV_PARM                     '             
                   DISPLAY '**************************************'             
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       7020-CLOSE-CSR.                                                          
                                                                                
           MOVE '7020-CLOSE-CSR'          TO PV-ABEND-PARAGRAPH.                
                                                                                
           EXEC SQL                                                             
               CLOSE INV-CSR                                                    
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                       CONTINUE                                                 
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
               WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
                   DISPLAY '**************************************'             
                   DISPLAY 'WARNING ISSUED '                                    
                   DISPLAY 'CLOSE CURSOR                          '             
                   DISPLAY '**************************************'             
                   PERFORM 9800-SQL-WARNING                                     
                                                                                
               WHEN SQLCODE < ZERO                                              
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED ON                       '             
                   DISPLAY 'CLOSE CURSOR                          '             
                   DISPLAY '**************************************'             
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       7100-CALC-LOW-DTE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   DATE(DAYS(:PV-DATE) - :DK-DTE-OFFSET)                        
               INTO                                                             
                  :DK-LOW-DTE                                                   
               FROM                                                             
                   SYSIBM.SYSDUMMY1                                             
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                       CONTINUE                                                 
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
               WHEN (SQLCODE > ZERO OR SQLWARN0 = 'W')                          
                   DISPLAY '**************************************'             
                   DISPLAY 'WARNING ISSUED '                                    
                   DISPLAY 'CALCULATE LOW DATE                    '             
                   DISPLAY '**************************************'             
                   PERFORM 9800-SQL-WARNING                                     
                                                                                
               WHEN SQLCODE < ZERO                                              
                   DISPLAY '**************************************'             
                   DISPLAY 'ERROR ISSUED ON                       '             
                   DISPLAY 'CALCULATE LOW DATE                    '             
                   DISPLAY '**************************************'             
                   PERFORM 9900-SQL-ERROR                                       
                                                                                
           END-EVALUATE.                                                        
      /                                                                         
       8000-READ-STORE-CONTROL.                                                 
                                                                                
           MOVE '8000-READ-STORE-CONTROL'   TO PV-ABEND-PARAGRAPH.              
                                                                                
           READ SELECT-STORE-EXT-FILE                                           
               INTO                                                             
                   PC067-STORE-DATA-PURGE-EXTRACT                               
               AT END                                                           
                   SET PS-ALL-STORES-YES    TO TRUE                             
               NOT AT END                                                       
                   SET PS-ALL-STORES-NO       TO TRUE                           
           END-READ.                                                            
                                                                                
                                                                                
       8100-WRITE-STORE-REC.                                                    
                                                                                
           MOVE '8100-WRITE-STORE-REC'  TO PV-ABEND-PARAGRAPH.                  
           WRITE                                                                
               INV-STORE-REC                                                    
               FROM PC046-INV-PARM-REC                                          
           END-WRITE.                                                           
           ADD 1                            TO PV-WRITE-1-COUNTER.              
                                                                                
       8200-WRITE-STORE-PURGE-TODAY.                                            
                                                                                
           MOVE '8200-WRITE-STORE-PURGE-TODAY' TO PV-ABEND-PARAGRAPH.           
           WRITE                                                                
               INV-STORE-EXT-TODAY-REC                                          
               FROM PC067-STORE-DATA-PURGE-EXTRACT                              
           END-WRITE.                                                           
           ADD 1                            TO PV-WRITE-2-COUNTER.              
                                                                                
       9000-QUIT.                                                               
                                                                                
               CLOSE                                                            
                   SELECT-STORE-EXT-FILE                                        
                   INV-STORE-FILE                                               
                   INV-STORE-EXT-TODAY                                          
                                                                                
                                                                                
           DISPLAY SPACES                                                       
           DISPLAY 'TOTAL STORES ELIGIBLE FOR PURGING = : '                     
                                               PV-WRITE-1-COUNTER               
           IF PV-WRITE-1-COUNTER = ZERO                                         
               MOVE 1                       TO RETURN-CODE                      
           END-IF                                                               
                                                                                
           IF PV-WRITE-2-COUNTER = ZERO                                         
               MOVE 1                       TO RETURN-CODE                      
           END-IF                                                               
                                                                                
           DISPLAY SPACES.                                                      
                                                                                
      /                                                                         
      ******************************************************************        
      *  THIS MODULE PERFORMS THE SQL WARNING.                         *        
      ******************************************************************        
                                                                                
       9800-SQL-WARNING.                                                        
                                                                                
           MOVE SQLCODE               TO SQLCODE2.                              
           MOVE SQLERRD(3)            TO SQLERRD3.                              
           DISPLAY '** ABEND **       ** = SQLWARNING'.                         
           DISPLAY '** PROGRAM        ** = PCKUT055'.                           
           DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                          
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                          
                                                                                
           COPY DPPD004.                                                        
           SET ABEND-4013-DB2-ERROR   TO TRUE                                   
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
      /                                                                         
      ******************************************************************        
      * THIS MODULE PERFORMS THE SQL ERROR.                            *        
      ******************************************************************        
                                                                                
       9900-SQL-ERROR.                                                          
                                                                                
           MOVE SQLCODE               TO SQLCODE2.                              
           MOVE SQLERRD(3)            TO SQLERRD3.                              
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = PCKUT055'.                           
           DISPLAY '** PARAGRAPH      ** = ' PV-ABEND-PARAGRAPH.                
           DISPLAY '** OPERATION      ** = ' PV-ABEND-OPERATION                 
           DISPLAY '** TABLE          ** = ' PV-ABEND-STRUCTURE-1               
           DISPLAY SPACES                                                       
                                                                                
           COPY DPPD004.                                                        
           SET ABEND-4013-DB2-ERROR   TO TRUE                                   
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
