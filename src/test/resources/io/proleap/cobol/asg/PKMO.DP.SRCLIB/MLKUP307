       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUP307.                                                 
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  01-11-2006.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUP307 - MLT_DEPT_BRND_VND TABLE RECALC UPDATE                        
      ******************************************************************        
      *                                                                         
      * THIS PROGRAM WILL TAKE IN A COMMON RECALC EXTRACT AND UPDATE            
      *    SHNK_RTL_AMT     - RETAIL SHRINK                                     
      *    END_INV_RTL_AMT  - ENDING INVENTORY RETAIL                           
      *    END_INV_COST_AMT - ENDING INVENTORY COST                             
W28582*    GRO_MRGN_AMT     - GROSS MARGIN                                      
R28582*    INV_COST_ADJ1_AMT - INVENTORY COST ADJUSTMENT 1                      
R28582*    INV_COST_ADJ2_AMT - INVENTORY COST ADJUSTMENT 2                      
R28582*    END_INV_COST1_AMT - ENDING INVENTORY COST 1                          
R28582*    END_INV_COST2_AMT - ENDING INVENTORY COST 2                          
B28582*    CHANGE WEEK END DATE TO MONTH END DATE                               
      *                                                                         
      * THERE ARE TWO INPUT FILES TO THIS PROGRAM:                              
      *    INP01 - CONTROL CARD FILE                                            
      *            USE TO DETERMINE WHICH CHECKPOINT RESTART RECORD/ROW         
      *            TO USE FOR RESTARTING THIS PROGRAM.                          
      *    INP02 - COMMON RECALC EXTRACT FILE                                   
      *            THIS FILE CONTAIN THE RECALC RECORD(S) TO BE UPDATED.        
      *                                                                         
      * THIS PROGRAM USES CHECKPOINT RESTART LOGIC TO DETERMINE IF AND          
      * WHEN A CHECKPOINT IS TO BE TAKEN. A MINIMUM OF 10 SECONDS WILL          
      * EXPIRE BEFORE A CHECKPOINT / COMMIT WILL BE PERFORMED.                  
      *                                                                         
      * CONDITIONS:                                                             
      * NORMAL START - START FROM THE BEGINNING OF THE INPUT FILE               
      * RESTART - SELECT CHECKPOINT RESTART KEY FROM TCKRSTINF, USE             
W28582*   FSCLMNENDDT/DEPT/PARTN-NBR/LWST-ID AS A STARTING POINT FOR            
      *   RECALC TO CONTINUE; > THE LAST COMMITED KEY.                          
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * W28582 01-12-2006 C. SCHWENN   NEW PROGRAM FOR VENDOR BRAND             
      *                                PROJECT.                                 
W28582* W28582 10-09-2006 J. SELWITSCHKA                                        
W28582*                                CHANGE TO ALLOW FOR PARTITION            
W28582*                                TABLE INSERT/UPDATE. ALSO,               
W28582*                                INSERT/UPDATE GRO_MRGN_AMT.              
R28582* W28582 11-21-2006 ROD JANSSEN  ADD ICA1, ICA2, EIC1, AND EIC2 TO        
R28582*                                SMOOTH F_CALC FROM THE WEEKLY            
R28582*                                DETAIL LEDGER DATA SMOOTH PROCESS        
J28582* W28582 12-06-2006 J. SELWITSCHKA                                        
J28582*                                CHECK EIC2 INSTEAD OF EIC WHEN           
J28582*                                DROPPING RECORDS.                        
X28582* W28582 04-30-2007 R. JANSSEN  INCLUDE GROSS MARGIN IN CARRY             
X28582*                               FORWARD TEST.                             
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CC-PGM-MODE-CNTL-FILE                                         
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT RECALC-EXTR-FILE                                              
               ASSIGN TO INP02.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CC-PGM-MODE-CNTL-FILE                                                
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CC-PGM-MODE-CNTL-REC            PIC X(80).                           
                                                                                
       FD  RECALC-EXTR-FILE                                                     
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
W28582 01  RECALC-EXTR-REC                 PIC X(400).                          
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************        
      *  COMMON RECALC EXTRACT                                                  
      ******************************************************************        
      *01  ML070-COMMON-RECALC-EXTRACT.                                         
           COPY MLRD070.                                                        
                                                                                
       01  CC-CONTROL-CARD-REC.                                                 
           05  CC-JOB-NAME                 PIC  X(08)      VALUE SPACES.        
           05  FILLER                      PIC  X(01)      VALUE SPACES.        
           05  CC-RECALC-MODE              PIC  X(01)      VALUE SPACE.         
               88  CC-ADJUST-RECALC-MODE                   VALUE 'A'.           
               88  CC-REPLACE-RECALC-MODE                  VALUE SPACE.         
           05  FILLER                      PIC  X(70)      VALUE SPACES.        
                                                                                
       01  ABEND-CODE                      PIC S9(04)      VALUE ZEROES         
                               COMP SYNC.                                       
           88  MISSING-CONTROL-ABEND                       VALUE +4002.         
           88  INVALID-CONTROL-ABEND                       VALUE +4003.         
           88  SQL-ABEND                                   VALUE +4013.         
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-COMMIT-COUNT             PIC  9(09)      VALUE ZEROES.        
           05  PA-INSERT-COUNT             PIC  9(09)      VALUE ZEROES.        
           05  PA-UPDATE-COUNT             PIC  9(09)      VALUE ZEROES.        
           05  PA-EXTRACT-RECS-READ        PIC  9(09)      VALUE ZEROES.        
           05  PA-ZERO-REC-DROPPED         PIC  9(09)      VALUE ZEROES.        
           05  PA-DUPLICATE-REC            PIC  9(09)      VALUE ZEROES.        
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-FIRST-TIME-THRU-SW       PIC X           VALUE 'Y'.           
               88  PS-NOT-FIRST-TIME-THRU                  VALUE 'N'.           
               88  PS-FIRST-TIME-THRU                      VALUE 'Y'.           
           05  PS-EOF-EXTR-FILE-SW         PIC X(01)       VALUE 'N'.           
               88  PS-EOF-EXTR-FILE                        VALUE 'Y'.           
           05  PS-FIRST-COMMIT-SW          PIC X           VALUE 'N'.           
               88  PS-FIRST-COMMIT                         VALUE 'Y'.           
           05  PS-RESTART-ROW-SW           PIC X           VALUE 'N'.           
               88  PS-RESTART-KEY-ESTABLISHED              VALUE 'Y'.           
           05  PS-COMMIT-TAKEN-SW          PIC X           VALUE 'N'.           
               88  PS-COMMIT-TAKEN                         VALUE 'Y'.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC  X(08)      VALUE                
               'MLKUP307'.                                                      
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'ATTEMPT TO CLOSE DB2 CURSOR  '.                                
           05  PE-MSG-06                       PIC X(31) VALUE                  
                'ERROR UPDATE MLT_DEPT_BRND_VND'.                               
           05  PE-MSG-07                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTCNTL '.                                
           05  PE-MSG-08                       PIC X(30) VALUE                  
                'UPDATE WORK_STRG - TCKRSTINF '.                                
           05  PE-MSG-09                       PIC X(30) VALUE                  
                'PGM ABEND DURING A COMMIT    '.                                
           05  PE-MSG-10                       PIC X(30) VALUE                  
                'UPDATE OF TCKRSTCNTL FAILED  '.                                
           05  PE-MSG-11                       PIC X(30) VALUE                  
                'ERROR IN SELECT - TCKRSTINF  '.                                
           05  PE-MSG-12                       PIC X(30) VALUE                  
                'UNABLE TO OBTAIN TIMESTAMP   '.                                
           05  PE-MSG-13                       PIC X(30) VALUE                  
                'SHRINK NOT FND FOR DEPT/DATE '.                                
           05  PE-MSG-14                       PIC X(34) VALUE                  
                'ERROR ''INSERT'' MLT_DEPT_BRND_VND'.                           
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-CURRENT-TIMESTAMP        PIC  X(26)      VALUE SPACES.        
           05  PV-COMMIT-TIMESTAMP         PIC  X(26)      VALUE SPACES.        
           05  PV-PARAGRAPH-NAME           PIC  X(30)      VALUE SPACES.        
           05  PV-ABEND-MESSAGE            PIC  X(75)      VALUE SPACES.        
           05  PV-OPERATION-ATTEMPTED      PIC  X(30)      VALUE SPACES.        
                                                                                
      ******************************************************************        
      *  ERROR MESSAGE AREA FOR DB2                                             
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
W28545* M/L F-CALC TABLE  (MLT_DEPT_BRND_VND)            *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE MLT00010                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
      ****************************************************                      
      *CHECKPOINT RESTART CONTROL AND INFORMATION TABLES *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * CHECKPOINT RESTART VARIABLES                                   *        
      ******************************************************************        
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                 PIC S9(04)      VALUE +89            
                               COMP.                                            
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-PREV-DTL-KEY          PIC  X(19)     VALUE SPACES.        
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                            
B28582             15  CR-FISCAL-MN-END-DTE PIC  X(10).                         
                   15  CR-DEPT-NBR          PIC S9(4)V COMP-3.                  
W28582             15  CR-PARTN-NBR         PIC S9(4)  COMP.                    
W28582             15  CR-ML-LWST-ID        PIC S9(9)  COMP.                    
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-DEPTBRNDVND-UPDATE-COUNT PIC 9(09) VALUE ZEROES.          
               10  FILLER                      PIC  X(01) VALUE SPACES.         
               10  CR-DEPTBRNDVND-INSERT-COUNT PIC 9(09) VALUE ZEROES.          
               10  FILLER                      PIC  X(01) VALUE SPACES.         
               10  CR-DEPTBRNDVND-COMMIT-COUNT PIC 9(09) VALUE ZEROES.          
               10  FILLER                      PIC  X(01) VALUE SPACES.         
               10  CR-DEPTBRNDVND-INPUT-COUNT  PIC 9(09) VALUE ZEROES.          
               10  FILLER                      PIC  X(01) VALUE SPACES.         
               10  CR-ZERO-REC-DROPPED      PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
               10  CR-DUPLICATE-REC         PIC  9(09) VALUE ZEROES.            
               10  FILLER                   PIC  X(01) VALUE SPACES.            
                                                                                
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAIN-MLKUP307.                                                      
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-INPUT                                           
                UNTIL PS-EOF-EXTR-FILE.                                         
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      /*****************************************************************        
      * 1000-INITIALIZATION.                                                    
      * INITIALIZE VARIOUS VARIABLES AND CHECK TO SEE WHAT RESTART              
      * CONDITION TO OPERATE UNDER.                                             
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT CC-PGM-MODE-CNTL-FILE                                     
                      RECALC-EXTR-FILE.                                         
                                                                                
           PERFORM 7100-READ-PROCESS-JOB-NAME.                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' PROCESSING JOB NAME: ' CC-JOB-NAME.                        
           IF CC-ADJUST-RECALC-MODE                                             
               DISPLAY '   MODE: ADJUST'                                        
           ELSE                                                                 
               DISPLAY '   MODE: REPLACE/NORMAL'                                
           END-IF.                                                              
                                                                                
           IF CC-JOB-NAME = SPACES                                              
               SET INVALID-CONTROL-ABEND  TO TRUE                               
               PERFORM 9999-ABEND                                               
           END-IF.                                                              
                                                                                
      *** TEST FOR A RESTART OF THIS PROGRAM....                                
           PERFORM 7650-INIT-CHECKPOINT-SELECT.                                 
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM 7700-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                   TO CR-CHECKPOINT-RESTART-VAR                                 
               DISPLAY ' '                                                      
               DISPLAY '--------------------------------------------'           
W28582         DISPLAY 'RESTART-LAST DATE/DEPT/PARTN/LWSTID COMMIT: ',          
B28582                  ' FISCALMN:  ' CR-FISCAL-MN-END-DTE                     
                        ' DEPT:      ' CR-DEPT-NBR                              
W28582                  ' PARTN-NBR: ' CR-PARTN-NBR                             
W28582                  ' LWST-ID    ' CR-ML-LWST-ID                            
               DISPLAY '--------------------------------------------'           
               DISPLAY ' '                                                      
               MOVE CR-DEPTBRNDVND-INPUT-COUNT TO PA-EXTRACT-RECS-READ          
               MOVE CR-DEPTBRNDVND-UPDATE-COUNT TO PA-UPDATE-COUNT              
               MOVE CR-DEPTBRNDVND-INSERT-COUNT TO PA-INSERT-COUNT              
               MOVE CR-DEPTBRNDVND-COMMIT-COUNT TO PA-COMMIT-COUNT              
               MOVE CR-ZERO-REC-DROPPED       TO PA-ZERO-REC-DROPPED            
               MOVE CR-DUPLICATE-REC          TO PA-DUPLICATE-REC               
           ELSE                                                                 
               SET PS-FIRST-COMMIT TO TRUE                                      
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                             
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                          
                                 TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'        
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
           END-IF.                                                              
                                                                                
      *** READ THE RECALC EXTRACT FILE...                                       
           SET PS-FIRST-TIME-THRU TO TRUE.                                      
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
               PERFORM 7000-READ-EXTR-FILE                                      
B28582             UNTIL (ML070-FSCL-END-DTE = CR-FISCAL-MN-END-DTE AND         
                          ML070-DEPT-NBR     = CR-DEPT-NBR          AND         
W28582                    ML070-PARTN-NBR    = CR-PARTN-NBR         AND         
W28582                    ML070-ML-LWST-ID   = CR-ML-LWST-ID)                   
                    OR (PS-EOF-EXTR-FILE)                                       
           END-IF.                                                              
                                                                                
      * GET THE NEXT RECORD                                                     
      * FOR RESTARTING, THIS WILL CONTINUE AFTER THE LAST COMMITED REC          
      * THIS IS IMPORTANT WHEN RUNNING UNDER ADJUSTMENT MODE, SEE CNTL          
           IF NOT PS-EOF-EXTR-FILE                                              
               PERFORM 7000-READ-EXTR-FILE                                      
           END-IF.                                                              
                                                                                
      *** END OF FILE PROCESSING...                                             
           IF PS-EOF-EXTR-FILE                                                  
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                               
                   DISPLAY 'NO RECORDS TO PROCESSED AFTER RESTART'              
               ELSE                                                             
                   IF TCKRSTCNTL-CKPT-STAT-CODE = '0'                           
                       DISPLAY 'NO ROWS SELECTED FOR RECALCULATION'             
                       DISPLAY 'CHECK IF INPUT FILE IS EMPTY      '             
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
                                                                                
      /*****************************************************************        
      * 2000-PROCESS-INPUT.                                                     
      * MAIN PROCESSING PARAGRAPH-PERFORMED UNTIL END OF INPUT FILE             
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
                                                                                
           ADD 1 TO PA-EXTRACT-RECS-READ.                                       
                                                                                
                                                                                
           IF ML070-CURR-FISCAL-EXIST                                           
               PERFORM 4000-PROCESS-RECALC                                      
           ELSE                                                                 
               IF ML070-CURR-END-INV-RTL-AMT   = ZEROES                         
J28582             AND ML070-END-INV-COST-AMT2 = ZEROES                         
X28582             AND ML070-ADJ-GRO-MRGN-AMT  = ZEROES                         
                   ADD +1           TO PA-ZERO-REC-DROPPED                      
               ELSE                                                             
                   PERFORM 6100-INSERT-CARRY-FWD-ROW                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 7800-COMMIT-PROCESSING.                                      
                                                                                
           PERFORM 7000-READ-EXTR-FILE.                                         
                                                                                
      ******************************************************************        
      * 4000-PROCESS-RECALC.                                                    
      ******************************************************************        
       4000-PROCESS-RECALC.                                                     
                                                                                
           IF CC-ADJUST-RECALC-MODE                                             
               PERFORM 6200-GET-CURRENT-ROW                                     
               ADD MLT00010-SHNK-RTL-AMT                                        
                   TO ML070-CURR-SHNK-RTL-AMT                                   
               ADD MLT00010-END-INV-RTL-AMT                                     
                   TO ML070-CURR-END-INV-RTL-AMT                                
               ADD MLT00010-END-INV-COST-AMT                                    
                   TO ML070-CURR-END-INV-COST-AMT                               
W28582         ADD MLT00010-GRO-MRGN-AMT                                        
R28582             TO ML070-ADJ-GRO-MRGN-AMT                                    
R28582***          TO ML070-CURR-GRO-MRGN-AMT                                   
R28582         ADD MLT00010-INV-COST-ADJ1-AMT                                   
R28582             TO ML070-INV-COST-ADJ1                                       
R28582         ADD MLT00010-INV-COST-ADJ2-AMT                                   
R28582             TO ML070-INV-COST-ADJ2                                       
R28582         ADD MLT00010-END-INV-COST1-AMT                                   
R28582             TO ML070-END-INV-COST-AMT1                                   
R28582         ADD MLT00010-END-INV-COST2-AMT                                   
R28582             TO ML070-END-INV-COST-AMT2                                   
           END-IF.                                                              
                                                                                
           PERFORM 6300-UPDATE-PROCESSING.                                      
                                                                                
      ******************************************************************        
      * 6100-INSERT-CARRY-FWD-ROW                                               
      * PERFORMED IF NO ROW IS FOUND ON DB2 (FOR THIS PROCESSING WEEK)          
      * BUT INVENTORY DOLLARS (COST AND/OR RETAIL) FOUND FOR LAST WEEK          
      ******************************************************************        
       6100-INSERT-CARRY-FWD-ROW.                                               
                                                                                
           INITIALIZE DCLMLT00010.                                              
B28582     MOVE ML070-FSCL-END-DTE    TO MLT00010-FSCL-MN-END-DTE.              
           MOVE ML070-DEPT-NBR        TO MLT00010-DEPT-NBR.                     
W28582     MOVE ML070-PARTN-NBR       TO MLT00010-PARTN-NBR.                    
W28582     MOVE ML070-ML-LWST-ID      TO MLT00010-ML-LO-MDSE-LVL-ID.            
                                                                                
           MOVE ML070-CURR-SHNK-RTL-AMT     TO MLT00010-SHNK-RTL-AMT.           
           MOVE ML070-CURR-END-INV-RTL-AMT                                      
               TO MLT00010-END-INV-RTL-AMT.                                     
           MOVE ML070-CURR-END-INV-COST-AMT                                     
               TO MLT00010-END-INV-COST-AMT.                                    
R28582***  MOVE ML070-CURR-GRO-MRGN-AMT                                         
R28582     MOVE ML070-ADJ-GRO-MRGN-AMT                                          
W28582         TO MLT00010-GRO-MRGN-AMT.                                        
R28582     MOVE ML070-INV-COST-ADJ1                                             
R28582         TO MLT00010-INV-COST-ADJ1-AMT.                                   
R28582     MOVE ML070-INV-COST-ADJ2                                             
R28582         TO MLT00010-INV-COST-ADJ2-AMT.                                   
R28582     MOVE ML070-END-INV-COST-AMT1                                         
R28582         TO MLT00010-END-INV-COST1-AMT.                                   
R28582     MOVE ML070-END-INV-COST-AMT2                                         
R28582         TO MLT00010-END-INV-COST2-AMT.                                   
                                                                                
           EXEC SQL                                                             
               INSERT INTO MLT_DEPT_BRND_VND                                    
W28582              (  PARTN_NBR                                                
                     , DEPT_NBR                                                 
W28582               , ML_LO_MDSE_LVL_ID                                        
B28582               , FSCL_MN_END_DTE                                          
                     , SHNK_RTL_AMT                                             
                     , END_INV_RTL_AMT                                          
                     , END_INV_COST_AMT                                         
W28582               , GRO_MRGN_AMT                                             
R28582               , INV_COST_ADJ1_AMT                                        
R28582               , INV_COST_ADJ2_AMT                                        
R28582               , END_INV_COST1_AMT                                        
R28582               , END_INV_COST2_AMT )                                      
W28582         VALUES (:MLT00010-PARTN-NBR                                      
                      ,:MLT00010-DEPT-NBR                                       
W28582                ,:MLT00010-ML-LO-MDSE-LVL-ID                              
B28582                ,:MLT00010-FSCL-MN-END-DTE                                
                      ,:MLT00010-SHNK-RTL-AMT                                   
                      ,:MLT00010-END-INV-RTL-AMT                                
                      ,:MLT00010-END-INV-COST-AMT                               
W28582                ,:MLT00010-GRO-MRGN-AMT                                   
R28582                ,:MLT00010-INV-COST-ADJ1-AMT                              
R28582                ,:MLT00010-INV-COST-ADJ2-AMT                              
R28582                ,:MLT00010-END-INV-COST1-AMT                              
R28582                ,:MLT00010-END-INV-COST2-AMT )                            
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   ADD 1                      TO PA-INSERT-COUNT                
               WHEN SQLCODE = -803                                              
                   ADD 1 TO PA-DUPLICATE-REC                                    
                   PERFORM 6300-UPDATE-PROCESSING                               
               WHEN OTHER                                                       
                   DISPLAY ' ' PA-EXTRACT-RECS-READ                             
B82582                  ' FSCLMNDTE:   ' MLT00010-FSCL-MN-END-DTE               
                        ' DEPT:        ' MLT00010-DEPT-NBR,                     
W28582                  ' PARTN-NBR:   ' MLT00010-PARTN-NBR,                    
W28582                  ' MDSE-LVL-ID: ' MLT00010-ML-LO-MDSE-LVL-ID             
                   MOVE '6100-INSERT-CARRY-FWD-ROW' TO PV-PARAGRAPH-NAME        
                   MOVE PE-MSG-14             TO PV-OPERATION-ATTEMPTED         
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 6200-GET-CURRENT-ROW.                                                   
      * GET THE CURRENT VALUE FOR A GIVEN ROWS                                  
      ******************************************************************        
       6200-GET-CURRENT-ROW.                                                    
                                                                                
B28582     MOVE ML070-FSCL-END-DTE   TO MLT00010-FSCL-MN-END-DTE.               
           MOVE ML070-DEPT-NBR       TO MLT00010-DEPT-NBR.                      
W28582     MOVE ML070-PARTN-NBR      TO MLT00010-PARTN-NBR.                     
W28582     MOVE ML070-ML-LWST-ID     TO MLT00010-ML-LO-MDSE-LVL-ID.             
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                       SHNK_RTL_AMT                                             
                      ,END_INV_RTL_AMT                                          
                      ,END_INV_COST_AMT                                         
W28582                ,GRO_MRGN_AMT                                             
R28582                ,INV_COST_ADJ1_AMT                                        
R28582                ,INV_COST_ADJ2_AMT                                        
R28582                ,END_INV_COST1_AMT                                        
R28582                ,END_INV_COST2_AMT                                        
                 INTO                                                           
                       :MLT00010-SHNK-RTL-AMT                                   
                      ,:MLT00010-END-INV-RTL-AMT                                
                      ,:MLT00010-END-INV-COST-AMT                               
W28582                ,:MLT00010-GRO-MRGN-AMT                                   
R28582                ,:MLT00010-INV-COST-ADJ1-AMT                              
R28582                ,:MLT00010-INV-COST-ADJ2-AMT                              
R28582                ,:MLT00010-END-INV-COST1-AMT                              
R28582                ,:MLT00010-END-INV-COST2-AMT                              
                 FROM                                                           
                       MLT_DEPT_BRND_VND                                        
B28582           WHERE FSCL_MN_END_DTE = :MLT00010-FSCL-MN-END-DTE              
                   AND DEPT_NBR        = :MLT00010-DEPT-NBR                     
W28582             AND PARTN_NBR       = :MLT00010-PARTN-NBR                    
W28582             AND ML_LO_MDSE_LVL_ID = :MLT00010-ML-LO-MDSE-LVL-ID          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY '*************************************'              
                   DISPLAY 'ROW IN PROCESS : '                                  
                   DISPLAY ' ' PA-EXTRACT-RECS-READ                             
B28582                  ' FSCLMNDT:    ' MLT00010-FSCL-MN-END-DTE,              
                        ' DEPT:        ' MLT00010-DEPT-NBR,                     
W28582                  ' PARTN-NBR:   ' MLT00010-PARTN-NBR,                    
W28582                  ' MDSE-LVL-ID: ' MLT00010-ML-LO-MDSE-LVL-ID             
                   DISPLAY '*************************************'              
B28582             DISPLAY 'MONTH-END-DATE  : ' ML070-FSCL-END-DTE              
                   MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED              
                   MOVE '6200-GET-CURRENT-ROW'  TO PV-PARAGRAPH-NAME            
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 6300-UPDATE-PROCESSING.                                                 
      * UPDATE COLUMNS WITH VALUES JUST CALCULATED FOR THIS DEPARTMENT          
      ******************************************************************        
       6300-UPDATE-PROCESSING.                                                  
                                                                                
B28582     MOVE ML070-FSCL-END-DTE    TO MLT00010-FSCL-MN-END-DTE.              
           MOVE ML070-DEPT-NBR        TO MLT00010-DEPT-NBR.                     
W28582     MOVE ML070-PARTN-NBR       TO MLT00010-PARTN-NBR.                    
W28582     MOVE ML070-ML-LWST-ID      TO MLT00010-ML-LO-MDSE-LVL-ID.            
                                                                                
           EXEC SQL                                                             
               UPDATE MLT_DEPT_BRND_VND                                         
                  SET                                                           
                       SHNK_RTL_AMT     = :ML070-CURR-SHNK-RTL-AMT              
                      ,END_INV_RTL_AMT  = :ML070-CURR-END-INV-RTL-AMT           
                      ,END_INV_COST_AMT = :ML070-CURR-END-INV-COST-AMT          
R28582                ,GRO_MRGN_AMT     = :ML070-ADJ-GRO-MRGN-AMT               
R28582                ,INV_COST_ADJ1_AMT = :ML070-INV-COST-ADJ1                 
R28582                ,INV_COST_ADJ2_AMT = :ML070-INV-COST-ADJ2                 
R28582                ,END_INV_COST1_AMT = :ML070-END-INV-COST-AMT1             
R28582                ,END_INV_COST2_AMT = :ML070-END-INV-COST-AMT2             
B28582           WHERE FSCL_MN_END_DTE = :MLT00010-FSCL-MN-END-DTE              
                   AND DEPT_NBR        = :MLT00010-DEPT-NBR                     
W28582             AND PARTN_NBR       = :MLT00010-PARTN-NBR                    
W28582             AND ML_LO_MDSE_LVL_ID = :MLT00010-ML-LO-MDSE-LVL-ID          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                   ADD 1                TO PA-UPDATE-COUNT                      
RONNA          WHEN 100                                                         
RONNA              PERFORM 6100-INSERT-CARRY-FWD-ROW                            
               WHEN OTHER                                                       
                   DISPLAY '*************************************'              
                   DISPLAY 'ROW IN PROCESS : '                                  
                   DISPLAY ' ' PA-EXTRACT-RECS-READ                             
B28582                  ' FSCLMNDTE:   ' MLT00010-FSCL-MN-END-DTE               
                        ' DEPT:        ' MLT00010-DEPT-NBR                      
W28582                  ' PARTN-NBR:   ' MLT00010-PARTN-NBR                     
W28582                  ' MDSE-LVL-ID: ' MLT00010-ML-LO-MDSE-LVL-ID             
                   DISPLAY '*************************************'              
B28582             DISPLAY 'MONTH-END-DATE  : ' ML070-FSCL-END-DTE              
                   MOVE PE-MSG-06        TO PV-OPERATION-ATTEMPTED              
                   MOVE '6300-UPDATE-PROCESSING'  TO PV-PARAGRAPH-NAME          
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * 7000-READ-EXTR-FILE.                                                    
      ******************************************************************        
       7000-READ-EXTR-FILE.                                                     
                                                                                
           READ RECALC-EXTR-FILE INTO ML070-COMMON-RECALC-EXTRACT               
               AT END                                                           
                  SET PS-EOF-EXTR-FILE TO TRUE.                                 
                                                                                
                                                                                
      ******************************************************************        
      * READS THE PROCESSING JOB NAME CONTROL CARD FILE.                        
      ******************************************************************        
       7100-READ-PROCESS-JOB-NAME.                                              
           READ CC-PGM-MODE-CNTL-FILE INTO CC-CONTROL-CARD-REC                  
               AT END                                                           
               SET MISSING-CONTROL-ABEND  TO TRUE                               
               PERFORM 9999-ABEND.                                              
                                                                                
      ******************************************************************        
      * 7600-GET-COMMIT-TIMESTAMP.                                     *        
      *   GET THE TIMESTAMP FROM THE CHECKPOINT TABLE FOR NEXT COMMIT  *        
      ******************************************************************        
       7600-GET-COMMIT-TIMESTAMP.                                               
                                                                                
           EXEC SQL                                                             
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
               INTO :PV-COMMIT-TIMESTAMP                                        
               FROM TCKRSTCNTL                                                  
              WHERE JOB_NAME  = :CC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   DISPLAY ' JOB: ' CC-JOB-NAME                                 
                           ' PROGRAM: ' PC-PROGRAM-NAME                         
                   MOVE '7600-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME        
                   MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED         
                   PERFORM 9950-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * 7650-INIT-CHECKPOINT-SELECT                                    *        
      * CHECK STATUS CODE TO SEE IF THIS IS A RESTART CONDITION        *        
      ******************************************************************        
       7650-INIT-CHECKPOINT-SELECT.                                             
      *                                                                         
           EXEC SQL                                                             
             SELECT  CKPT_STAT_CODE                                             
                    ,CKPT_FRQNCY_QTY                                            
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                                 
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :CC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' JOB: ' CC-JOB-NAME                                     
                       ' PROGRAM: ' PC-PROGRAM-NAME                             
               MOVE '7650-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME          
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7700-SELECT-RESTART-INFO                                       *        
      * OBTAIN INFORMATION REGARDING WHERE TO RESTART TABLE PROCESSING *        
      ******************************************************************        
       7700-SELECT-RESTART-INFO.                                                
      *                                                                         
           EXEC SQL                                                             
             SELECT  WORK_STRG_DESC                                             
               INTO  :TCKRSTINF-WORK-STRG-DESC                                  
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :CC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
      *                                                                         
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' JOB: ' CC-JOB-NAME                                     
                       ' PROGRAM: ' PC-PROGRAM-NAME                             
               MOVE '*7700-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME           
               MOVE PE-MSG-11             TO PV-OPERATION-ATTEMPTED             
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7750-SET-CURRENT-TIMESTAMP.                                    *        
      * CURRENT TIMESTAMP SET TO BE COMPARED TO COMMIT TIMESTAMP       *        
      ******************************************************************        
       7750-SET-CURRENT-TIMESTAMP.                                              
                                                                                
           EXEC SQL                                                             
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-12             TO PV-OPERATION-ATTEMPTED             
               MOVE '*7750-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME         
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7800-COMMIT-PROCESSING.                                        *        
      * COMPARE COMMIT TIMESTAMP TO CURRENT TO DETERMINE IF COMMIT REQD*        
      ******************************************************************        
       7800-COMMIT-PROCESSING.                                                  
           MOVE '*7800-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.             
                                                                                
           PERFORM 7750-SET-CURRENT-TIMESTAMP.                                  
                                                                                
           IF (PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP)  OR                  
              (PS-EOF-EXTR-FILE)                                                
      * PREPARE COMMIT RECORD FOR UPDATE                                        
               ADD 1                      TO PA-COMMIT-COUNT                    
B28582         MOVE ML070-FSCL-END-DTE    TO CR-FISCAL-MN-END-DTE               
               MOVE ML070-DEPT-NBR        TO CR-DEPT-NBR                        
W28582         MOVE ML070-PARTN-NBR       TO CR-PARTN-NBR                       
W28582         MOVE ML070-ML-LWST-ID      TO CR-ML-LWST-ID                      
               MOVE PA-EXTRACT-RECS-READ  TO CR-DEPTBRNDVND-INPUT-COUNT         
               MOVE PA-UPDATE-COUNT       TO CR-DEPTBRNDVND-UPDATE-COUNT        
               MOVE PA-INSERT-COUNT       TO CR-DEPTBRNDVND-INSERT-COUNT        
               MOVE PA-COMMIT-COUNT       TO CR-DEPTBRNDVND-COMMIT-COUNT        
               MOVE PA-ZERO-REC-DROPPED   TO CR-ZERO-REC-DROPPED                
               MOVE PA-DUPLICATE-REC      TO CR-DUPLICATE-REC                   
      * MOVE COMMIT INFO TO WORKING STORAGE ROW                                 
               MOVE CR-CHECKPOINT-RESTART-AREA TO                               
                                                TCKRSTINF-WORK-STRG-DESC        
               MOVE 'Y' TO PS-COMMIT-TAKEN-SW                                   
               IF PS-FIRST-COMMIT                                               
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                        
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                        
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                               
               END-IF                                                           
                                                                                
               PERFORM 7850-COMMIT-CHANGES                                      
                                                                                
               PERFORM 7600-GET-COMMIT-TIMESTAMP                                
                                                                                
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * 7850-COMMIT-CHANGES.                                                    
      * UPDATE CHECKPOINT INFORMATION TABLE. TAKE A COMMIT.                     
      ******************************************************************        
       7850-COMMIT-CHANGES.                                                     
                                                                                
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME       = :CC-JOB-NAME                               
                AND PLAN_NAME      = :PC-PROGRAM-NAME                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' JOB: ' CC-JOB-NAME                                     
                       ' PROGRAM: ' PC-PROGRAM-NAME                             
               MOVE PE-MSG-08                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PE-MSG-09                  TO PV-OPERATION-ATTEMPTED        
               MOVE  '* 7850-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME             
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * UPDATE CHECKPOINT STATUS                                                
      ******************************************************************        
       7900-UPDATE-CHECKPOINT-STATUS.                                           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :CC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY ' JOB: ' CC-JOB-NAME                                     
                       ' PROGRAM: ' PC-PROGRAM-NAME                             
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME        
               MOVE PE-MSG-10                  TO PV-OPERATION-ATTEMPTED        
               PERFORM 9950-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      /*****************************************************************        
      * 9000-EOJ-ROUTINE.                                                       
      * RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE              
      ******************************************************************        
       9000-EOJ-ROUTINE.                                                        
                                                                                
           PERFORM 7800-COMMIT-PROCESSING                                       
                                                                                
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                               
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                               
           CLOSE CC-PGM-MODE-CNTL-FILE                                          
                 RECALC-EXTR-FILE.                                              
      *                                                                         
           DISPLAY '                                             '.             
           DISPLAY '******* END OF JOB STATISTICS ********'.                    
           DISPLAY ' '.                                                         
           DISPLAY 'INPUT EXTRACT RECORDS READ   ' PA-EXTRACT-RECS-READ.        
           DISPLAY '  DEPTBRNDVND ROWS UPDATED   ' PA-UPDATE-COUNT.             
           DISPLAY '  DEPTBRNDVND ROWS INSERTED  ' PA-INSERT-COUNT.             
           DISPLAY '  ZERO CARRY FWD DROPPED     ' PA-ZERO-REC-DROPPED.         
           DISPLAY '  DUPLICATE REC              ' PA-DUPLICATE-REC.            
           DISPLAY ' '.                                                         
           DISPLAY '  COMMITS TAKEN              ' PA-COMMIT-COUNT.             
           DISPLAY '                                             '.             
           DISPLAY 'EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ EOJ '.          
                                                                                
      ******************************************************************        
      * 9950-SQL-ABEND.                                                         
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9950-SQL-ABEND.                                                          
                                                                                
           SET SQL-ABEND              TO TRUE.                                  
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * 9999-ABEND.                                                             
      *  STANDARD ABEND ROUTINE                                                 
      ******************************************************************        
       9999-ABEND.                                                              
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
                                                                                
