000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT043.                                                 
000500 AUTHOR.        DEANNA BRANTNER.                                          
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL, 2003.                                              
000800 DATE-COMPILED.                                                           
000900****************************************************************          
001000*    THIS PROGRAM WILL COMPARE RMS PO / LEGACY PO              *          
001100* PO DOMAIN TEMPLATE.........:  XXT_PO_DISC                    *          
001200* PO DOMAIN TEMPLATE COPYBOOK:  XLRD017                        *          
001300*                                                              *          
001310*        INPUT COMES TO THIS PROGRAM FROM XLKUT060 AND MUST    *          
001320*    BE SORTED BY THE PO AND DISC TYPE.                                   
001380*                                                              *          
001390*       THIS PROGRAM MATCHES THE LEGACY AND RMS FILES BY PO    *          
001391*    NUMBER, AND DISC-TYPE-CDE.                                *          
001392*    WRITES ERROR RECORDS OUT FOR ANY UNMATCHED RECORDS. IT    *          
001393*    ALSO CHECKS TO SEE IF THERE ARE ANY DATA DIFFERENCES FOR  *          
001394*    THOSE RECORDS WHOSE PO NUMBERS MATCH. IF THERE ARE ANY,   *          
001395*    AN ERROR RECORD IS WRITTEN INDICATING THE FIELDS THAT     *          
001396*    ARE DIFFERENT.                                            *          
001397*        THERE ARE SOME MOVES OF FIELDS COMMENTED OUT. THIS    *          
001398*    HAS BEEN DONE BECAUSE SOME OF THESE FIELDS AREN'T AVAIL-  *          
001399*    ABLE ON ONE OF THE SYSTEMS.  YOU MAY FIND THESE BY DOING  *          
001400*    A F '**??'.                                               *          
001410*****************************************************************         
001500 ENVIRONMENT DIVISION.                                                    
001600 INPUT-OUTPUT SECTION.                                                    
001700                                                                          
001800 FILE-CONTROL.                                                            
001900                                                                          
002000     SELECT RMS-PO-FILE                                                   
002010         ASSIGN TO INP01.                                                 
002100                                                                          
002110     SELECT LEGACY-FILE                                                   
002120         ASSIGN TO INP02.                                                 
002130                                                                          
002140     SELECT ERROR-FILE                                                    
002150         ASSIGN TO OUT01.                                                 
002160                                                                          
002200*****************************************************************         
002300 DATA DIVISION.                                                           
002400*****************************************************************         
002500 FILE SECTION.                                                            
002600                                                                          
002700 FD  RMS-PO-FILE                                                          
002800     RECORDING MODE IS F                                                  
002810     BLOCK CONTAINS 0 RECORDS                                             
002900     LABEL RECORDS ARE STANDARD.                                          
003000                                                                          
003300 01  RMS-PO-RECORD                PIC X(40).                              
003700                                                                          
003710 FD  LEGACY-FILE                                                          
003720     RECORDING MODE IS F                                                  
003730     BLOCK CONTAINS 0 RECORDS                                             
003740     LABEL RECORDS ARE STANDARD.                                          
003750                                                                          
003760 01  LEGACY-RECORD                PIC X(40).                              
003770                                                                          
003800                                                                          
003810 FD  ERROR-FILE                                                           
003820     RECORDING MODE IS F                                                  
003830     BLOCK CONTAINS 0 RECORDS                                             
003840     LABEL RECORDS ARE STANDARD.                                          
003850                                                                          
003860 01  ERROR-RECORD                 PIC X(830).                             
003870                                                                          
003880                                                                          
003900*****************************************************************         
004000 WORKING-STORAGE SECTION.                                                 
004100                                                                          
004200 01  PS-PROGRAM-SWITCHES.                                                 
004300     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
004400         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
004500         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
004510     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
004520         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
004530         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
004540     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
004550         88  PS-EMAIL-YES                        VALUE 'Y'.               
004560         88  PS-EMAIL-NO                         VALUE 'N'.               
004600                                                                          
004700 01  PC-PROGRAM-CONSTANTS.                                                
004800     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT043'.        
004900     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
005000                                                                          
005001 01  PC-SORT-KEY.                                                         
005002     05  PC-DL-ID                     PIC  9(08) VALUE ZEROES.            
005003     05  PC-DL-DTL-ID                 PIC  9(08) VALUE ZEROES.            
005004                                                                          
005001 01  PS-RMS-SORT-KEY.                                                     
005002     05  PS-RMS-PO-NBR                PIC  9(08) VALUE ZEROES.            
005003     05  PS-RMS-DISC-TYPE             PIC  X(06) VALUE SPACES.            
005004                                                                          
005001 01  PS-LEGACY-SORT-KEY.                                                  
005002     05  PS-LEGACY-PO-NBR             PIC  9(08) VALUE ZEROES.            
005003     05  PS-LEGACY-DISC-TYPE          PIC  X(06) VALUE SPACES.            
005004                                                                          
005100 01  PV-PROGRAM-VARIABLES.                                                
005900     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
005910     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
006000     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
006100     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
006200     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
006300     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
006400     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
006500     05  PV-ALL-NINES                 PIC  9(09) VALUE 99999999.          
007000                                                                          
007100 01  PV-ABEND-FIELDS.                                                     
007200     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
007300                                                 COMP SYNC.               
007400     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT043'.        
007500     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
007600     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
007700     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
008200                                                                          
008300 01  PV-RMS-INPUT.                                                        
009206     COPY XLRD017.                                                        
009300                                                                          
009400 01  PV-LEGACY-INPUT.                                                     
009500     COPY XLRD017.                                                        
009600                                                                          
009601 01  PV-ERROR-OUTPUT.                                                     
009602     COPY XLRD023.                                                        
009603                                                                          
010800*****************************************************************         
010900 PROCEDURE DIVISION.                                                      
011000*****************************************************************         
011010     DISPLAY ' '.                                                         
011100     DISPLAY '**** BEGINNING XLKUT043 ****'.                              
011200     DISPLAY ' '.                                                         
011300                                                                          
011400     OPEN INPUT  RMS-PO-FILE                                              
011401                 LEGACY-FILE                                              
011410          OUTPUT ERROR-FILE.                                              
011420     SET PS-EMAIL-NO       TO TRUE.                                       
011500                                                                          
011600     PERFORM 1000-READ-RMS-INPUT.                                         
011610     PERFORM 1100-READ-LEGACY-INPUT.                                      
011700                                                                          
012100     PERFORM 2000-PROCESS-INPUT-FILE                                      
012200       UNTIL PS-EOF-INPUT1-YES AND                                        
012210             PS-EOF-INPUT2-YES.                                           
012300                                                                          
012310     CLOSE RMS-PO-FILE                                                    
012320           LEGACY-FILE                                                    
012330           ERROR-FILE.                                                    
012500                                                                          
012600     DISPLAY ' '.                                                         
012601     DISPLAY '  INPUT RMS PO REC = ' PV-RECORDS-READ-1.                   
012610     DISPLAY '  INPUT LEGACY REC = ' PV-RECORDS-READ-2.                   
012611     DISPLAY ' '.                                                         
012612     DISPLAY '          XLRD017'.                                         
012613     DISPLAY 'FILES ARE MATCHED BY PO NUMBER - 8 POSITIONS'.              
012614     DISPLAY '                 DISC-TYPE-CDE - 6 POSITIONS'.              
012620     DISPLAY ' '.                                                         
012621     DISPLAY PC-FIVE-BLANKS                                               
012622             'RECORDS ON BOTH FILES ---------> '                          
012623              PV-RECORDS-ON-BOTH.                                         
012630     DISPLAY ' '.                                                         
012710     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
012720              PV-RECORDS-WRITTEN.                                         
012760     DISPLAY PC-FIVE-BLANKS                                               
012770             'RECORDS ON RMS ONLY -----------> '                          
012781              PV-RECORDS-ON-RMS-ONLY                                      
012782             ' - "A" RECORD CODE'.                                        
012790     DISPLAY PC-FIVE-BLANKS                                               
012791             'RECORDS ON LEGACY ONLY --------> '                          
012793              PV-RECORDS-ON-LEGACY-ONLY                                   
012794             ' - "B" RECORD CODE'.                                        
012795     DISPLAY PC-FIVE-BLANKS                                               
012796             'RECORDS WITH DATA MISMATCH ----> '                          
012798              PV-RECORDS-DATA-MISMATCH                                    
012799             ' - "C" RECORD CODE'.                                        
012800     DISPLAY ' '.                                                         
012900     DISPLAY '****  END OF  XLKUT043  ****'.                              
012910     DISPLAY ' '.                                                         
013000                                                                          
013100*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
013200     IF PS-EMAIL-YES                                                      
013300         MOVE 4095 TO RETURN-CODE                                         
013301     END-IF.                                                              
013302     GOBACK.                                                              
013303                                                                          
013304                                                                          
013400*****************************************************************         
013500*  READ A RMS PO RECORD                                                   
013600*****************************************************************         
013700 1000-READ-RMS-INPUT.                                                     
013800                                                                          
013900     MOVE '1000-READ-RMS-INPUT    ' TO PV-ABEND-PARAGRAPH.                
014000                                                                          
014010     INITIALIZE PV-RMS-INPUT.                                             
014020                                                                          
014100     READ RMS-PO-FILE                                                     
014200          INTO PV-RMS-INPUT                                               
014300            AT END                                                        
014400               SET PS-EOF-INPUT1-YES TO TRUE                              
014900     END-READ.                                                            
015000                                                                          
015900     MOVE XL017-PO-NBR OF PV-RMS-INPUT        TO PS-RMS-PO-NBR.           
016000     MOVE XL017-DISC-TYPE-CDE OF PV-RMS-INPUT TO PS-RMS-DISC-TYPE.        
015010     IF PS-EOF-INPUT1-YES                                                 
015020        MOVE PV-ALL-NINES TO XL017-PO-NBR OF PV-RMS-INPUT                 
015021                             XL017-DL-ID  OF PV-RMS-INPUT                 
015022                             XL017-DL-DTL-ID OF PV-RMS-INPUT              
015030     ELSE                                                                 
015040        ADD +1 TO PV-RECORDS-READ-1                                       
015050     END-IF.                                                              
015100                                                                          
015101*****************************************************************         
015102*  READ A LEGACY RECORD                                                   
015103*****************************************************************         
015104 1100-READ-LEGACY-INPUT.                                                  
015105                                                                          
015106     MOVE '1100-READ-LEGACY-INPUT' TO PV-ABEND-PARAGRAPH.                 
015107                                                                          
015108     INITIALIZE PV-LEGACY-INPUT.                                          
015109                                                                          
015110     READ LEGACY-FILE                                                     
015111          INTO PV-LEGACY-INPUT                                            
015112            AT END                                                        
015113               SET PS-EOF-INPUT2-YES TO TRUE                              
015114     END-READ.                                                            
015115                                                                          
015900     MOVE XL017-PO-NBR OF PV-LEGACY-INPUT     TO PS-LEGACY-PO-NBR.        
016000     MOVE XL017-DISC-TYPE-CDE OF PV-LEGACY-INPUT TO                       
                                                    PS-LEGACY-DISC-TYPE.        
015116     IF PS-EOF-INPUT2-YES                                                 
015117        MOVE PV-ALL-NINES TO XL017-PO-NBR OF PV-LEGACY-INPUT              
015118                             XL017-DL-ID  OF PV-LEGACY-INPUT              
015119                             XL017-DL-DTL-ID OF PV-LEGACY-INPUT           
015120     ELSE                                                                 
015121        ADD +1 TO PV-RECORDS-READ-2                                       
015122     END-IF.                                                              
015130                                                                          
015200*****************************************************************         
015300*  REFORMAT THE RMS PO EXTRACT FILE                                       
015400*****************************************************************         
015500 2000-PROCESS-INPUT-FILE.                                                 
015600                                                                          
015700     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
015800                                                                          
015900     IF XL017-PO-NBR   OF PV-RMS-INPUT    <                               
016000        XL017-PO-NBR   OF PV-LEGACY-INPUT                                 
016001*******************************                                           
016002***  ON RMS NOT ON LEGACY                                                 
016003*******************************                                           
016004        ADD 1 TO PV-RECORDS-ON-RMS-ONLY                                   
016113        SET XL023-ON-RMS-ONLY TO TRUE                                     
016114        PERFORM 3400-WRITE-RMS-ERROR                                      
016115        PERFORM 1000-READ-RMS-INPUT                                       
016116     ELSE                                                                 
016117        IF XL017-PO-NBR  OF PV-RMS-INPUT    >                             
016118           XL017-PO-NBR  OF PV-LEGACY-INPUT                               
016119*******************************                                           
016120***   ON LEGACY NOT ON RMS                                                
016121*******************************                                           
016122           ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                             
016123           SET XL023-ON-LEGACY-ONLY TO TRUE                               
016124           PERFORM 4000-WRITE-LEGACY-ERROR                                
016125           PERFORM 1100-READ-LEGACY-INPUT                                 
016126        ELSE                                                              
016127*******************************                                           
016128***  ON RMS AND ON LEGACY                                                 
016129*******************************                                           
016130           PERFORM 2400-CHECK-KEY-VALUES                                  
016131        END-IF                                                            
016132     END-IF.                                                              
016133                                                                          
016134****************************************************************          
016135*    CHECK ALL THE KEY VALUES FOR MISMATCHES.                  *          
016136*                                                              *          
016137*                                                              *          
016138****************************************************************          
016139 2400-CHECK-KEY-VALUES.                                                   
016140                                                                          
016141     MOVE '2400-CHECK-KEY-VALUES ' TO PV-ABEND-PARAGRAPH.                 
016142                                                                          
016143     EVALUATE TRUE                                                        
016144         WHEN PS-RMS-SORT-KEY  <  PS-LEGACY-SORT-KEY                      
016146              ADD 1 TO PV-RECORDS-ON-RMS-ONLY                             
016147              SET XL023-ON-RMS-ONLY TO TRUE                               
016148              PERFORM 3400-WRITE-RMS-ERROR                                
016149              PERFORM 1000-READ-RMS-INPUT                                 
016150                                                                          
016144         WHEN PS-RMS-SORT-KEY  >  PS-LEGACY-SORT-KEY                      
016153              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
016154              SET XL023-ON-LEGACY-ONLY TO TRUE                            
016155              PERFORM 4000-WRITE-LEGACY-ERROR                             
016156              PERFORM 1100-READ-LEGACY-INPUT                              
016157                                                                          
016172         WHEN  PS-RMS-SORT-KEY = PS-LEGACY-SORT-KEY                       
016173              ADD 1 TO PV-RECORDS-ON-BOTH                                 
016180              SET XL023-ON-BOTH-RMS-LEGACY TO TRUE                        
016181              PERFORM 3400-WRITE-RMS-ERROR                                
016182              PERFORM 4000-WRITE-LEGACY-ERROR                             
016183              PERFORM 1000-READ-RMS-INPUT                                 
016184              PERFORM 1100-READ-LEGACY-INPUT                              
016186                                                                          
016187     END-EVALUATE.                                                        
016188                                                                          
016189                                                                          
021600*****************************************************************         
021700*  WRITE A RMS RECORD TO THE ERROR FILE                                   
021800*****************************************************************         
021900 3400-WRITE-RMS-ERROR.                                                    
022000                                                                          
022100     SET XL023-XXT-PO-DISC  TO TRUE.                                      
022200     MOVE XL017-PO-NBR    OF PV-RMS-INPUT TO XL023-PO-SORT-KEY-PO.        
022300     MOVE XL017-DL-ID     OF PV-RMS-INPUT  TO PC-DL-ID.                   
022400     MOVE XL017-DL-DTL-ID OF PV-RMS-INPUT  TO PC-DL-DTL-ID.               
022500     MOVE PC-SORT-KEY     TO XL023-PO-SORT-KEY-DATA.                      
022600     INITIALIZE XL023-PO-DISC.                                            
022700                                                                          
022800     MOVE XL017-PO-NBR OF PV-RMS-INPUT    TO XL023-DI-PO-NBR-RMS.         
022900     MOVE XL017-DL-ID    OF PV-RMS-INPUT    TO XL023-DI-DL-ID-RMS.        
023000     MOVE XL017-DL-DTL-ID OF PV-RMS-INPUT                                 
023100                                        TO XL023-DI-DL-DTL-ID-RMS.        
023200     MOVE XL017-DISC-TYPE-CDE OF PV-RMS-INPUT                             
023300                                    TO XL023-DI-DISC-TYPE-CDE-RMS.        
023400     MOVE XL017-EXTD-DAYS-QTY OF PV-RMS-INPUT                             
023500                                    TO XL023-DI-EXTD-DAYS-QTY-RMS.        
           IF XL017-DISC-PCT OF PV-RMS-INPUT(1:3) NUMERIC AND                   
              XL017-DISC-PCT OF PV-RMS-INPUT(5:2) NUMERIC                       
               MOVE XL017-DISC-PCT OF PV-RMS-INPUT TO                           
                                                   XL023-DI-DISC-PCT-RMS        
           ELSE                                                                 
               MOVE ZEROES TO                      XL023-DI-DISC-PCT-RMS        
           END-IF.                                                              
023700**?? MOVE XL017-TOT-DISC-AMT OF PV-RMS-INPUT                              
023800**??                                 TO XL023-DI-TOT-DISC-AMT-RMS.        
023900                                                                          
024000     IF XL023-ON-BOTH-RMS-LEGACY                                          
024100         CONTINUE                                                         
024200     ELSE                                                                 
024300         WRITE ERROR-RECORD FROM  PV-ERROR-OUTPUT                         
024400         ADD +1              TO   PV-RECORDS-WRITTEN                      
024500     END-IF.                                                              
024600                                                                          
024700*****************************************************************         
024800*  WRITE A LEGACY RECORD TO THE ERROR FILE                                
024900*****************************************************************         
025000 4000-WRITE-LEGACY-ERROR.                                                 
025100                                                                          
025200     IF XL023-ON-BOTH-RMS-LEGACY                                          
025300         CONTINUE                                                         
025400     ELSE                                                                 
025500         SET XL023-XXT-PO-DISC  TO TRUE                                   
025600         MOVE XL017-PO-NBR OF PV-LEGACY-INPUT                             
025700                                        TO XL023-PO-SORT-KEY-PO           
025800         MOVE XL017-DL-ID  OF PV-LEGACY-INPUT TO PC-DL-ID                 
025900         MOVE XL017-DL-DTL-ID OF PV-LEGACY-INPUT TO PC-DL-DTL-ID          
026000         MOVE PC-SORT-KEY     TO XL023-PO-SORT-KEY-DATA                   
026100         INITIALIZE XL023-PO-DISC                                         
026200     END-IF.                                                              
026300                                                                          
026400     MOVE XL017-PO-NBR OF PV-LEGACY-INPUT TO XL023-DI-PO-NBR-LGCY.        
026500     MOVE XL017-DL-ID   OF PV-LEGACY-INPUT TO XL023-DI-DL-ID-LGCY.        
026600     MOVE XL017-DL-DTL-ID OF PV-LEGACY-INPUT                              
026700                                       TO XL023-DI-DL-DTL-ID-LGCY.        
026800     MOVE XL017-DISC-TYPE-CDE OF PV-LEGACY-INPUT                          
026900                                   TO XL023-DI-DISC-TYPE-CDE-LGCY.        
027000     MOVE XL017-EXTD-DAYS-QTY OF PV-LEGACY-INPUT                          
027100                                   TO XL023-DI-EXTD-DAYS-QTY-LGCY.        
027200     MOVE XL017-DISC-PCT OF PV-LEGACY-INPUT                               
027300                                        TO XL023-DI-DISC-PCT-LGCY.        
027400**?? MOVE XL017-TOT-DISC-AMT OF PV-LEGACY-INPUT                           
027500**??                                TO XL023-DI-TOT-DISC-AMT-LGCY.        
027600                                                                          
027700     IF XL023-ON-BOTH-RMS-LEGACY                                          
027800         PERFORM 4300-COMPARE                                             
032800     ELSE                                                                 
032900         WRITE ERROR-RECORD FROM  PV-ERROR-OUTPUT                         
033000         ADD +1              TO   PV-RECORDS-WRITTEN                      
027900     END-IF.                                                              
028000                                                                          
028500                                                                          
028600*****************************************************************         
028700*  COMPARE FIELDS TO FIND DISCREPANCIES.                                  
028800*****************************************************************         
028900 4300-COMPARE.                                                            
029000                                                                          
029100     IF XL023-DI-PO-NBR-RMS  NOT = XL023-DI-PO-NBR-LGCY                   
029200         MOVE '*'  TO  XL023-DI-PO-NBR-DIFF                               
029300     END-IF.                                                              
029310**?? IF XL023-DI-DL-ID-RMS  NOT = XL023-DI-DL-ID-LGCY                     
029320**??     MOVE '*'  TO  XL023-DI-DL-ID-DIFF                                
029330**?? END-IF.                                                              
029331**?? IF XL023-DI-DL-DTL-ID-RMS  NOT = XL023-DI-DL-DTL-ID-LGCY             
029332**??     MOVE '*'  TO  XL023-DI-DL-DTL-ID-DIFF                            
029333**?? END-IF.                                                              
029340                                                                          
029400     IF XL023-DI-DISC-TYPE-CDE-RMS NOT =                                  
029500                                     XL023-DI-DISC-TYPE-CDE-LGCY          
029600         MOVE '*'  TO XL023-DI-DISC-TYPE-CDE-DIFF                         
029700     END-IF.                                                              
029800     IF XL023-DI-EXTD-DAYS-QTY-RMS NOT =                                  
029900                                     XL023-DI-EXTD-DAYS-QTY-LGCY          
030000         MOVE '*'  TO XL023-DI-EXTD-DAYS-QTY-DIFF                         
030100     END-IF.                                                              
030200     IF XL023-DI-DISC-PCT-RMS  NOT = XL023-DI-DISC-PCT-LGCY               
030300         MOVE '*'  TO XL023-DI-DISC-PCT-DIFF                              
030400     END-IF.                                                              
030500**?? IF XL023-DI-TOT-DISC-AMT-RMS  NOT =                                  
030510**??                         XL023-DI-TOT-DISC-AMT-LGCY                   
030600**??     MOVE '*'  TO XL023-DI-DISC-PCT-DIFF                              
030700**?? END-IF.                                                              
029200         IF  XL023-DI-PO-NBR-DIFF        = '*' OR                         
029600             XL023-DI-DISC-TYPE-CDE-DIFF = '*' OR                         
030000             XL023-DI-EXTD-DAYS-QTY-DIFF = '*' OR                         
030300             XL023-DI-DISC-PCT-DIFF      = '*'                            
016179                ADD 1 TO PV-RECORDS-DATA-MISMATCH                         
028100                WRITE ERROR-RECORD FROM PV-ERROR-OUTPUT                   
028301                ADD +1 TO PV-RECORDS-WRITTEN                              
028303*               SET SWITCH FOR E-MAIL NOTIFICATION OF LEGACY ERROR        
028400*               SET PS-EMAIL-YES       TO TRUE                            
030400     END-IF.                                                              
