000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.             VLKUT032.                                        
000400 AUTHOR.                 VIRGENE LENNEMAN.                                
000500 INSTALLATION.           KOHLS DEPARTMENT STORES.                         
000600 DATE-WRITTEN.           FEBRUARY 2005.                                   
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*                                                                         
001000*  PROGRAM NARRATIVE:                                                     
001100*  THIS PROGRAM WILL CREATE A FILE CONTAINING DEPARTMENT                  
001200*  INFORMATION FOR COMPLIANCE NETWORK                                     
001300*                                                                         
001400*                                                                         
001500 ENVIRONMENT DIVISION.                                                    
001600                                                                          
001700 CONFIGURATION SECTION.                                                   
001800                                                                          
001900 SOURCE-COMPUTER.                        IBM-3090.                        
002000 OBJECT-COMPUTER.                        IBM-3090.                        
002100                                                                          
002200 INPUT-OUTPUT SECTION.                                                    
002300                                                                          
002400 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT  VL-DEPT-FILE                                                 
002700         ASSIGN TO OUT01.                                                 
002800                                                                          
002900     SELECT  VL-MARKER-FILE                                               
003000         ASSIGN TO OUT02.                                                 
003100                                                                          
003200******************************************************************        
003300 DATA DIVISION.                                                           
003400******************************************************************        
003500                                                                          
003600 FILE SECTION.                                                            
003700*                                                                         
003800 FD  VL-DEPT-FILE                                                         
003900     RECORDING MODE IS F                                                  
004000     LABEL RECORDS ARE STANDARD                                           
004100     BLOCK CONTAINS 0 RECORDS.                                            
004200 01  VL-DEPT-RECORD                   PIC  X(067).                        
004300*                                                                         
004400*                                                                         
004500 FD  VL-MARKER-FILE                                                       
004600     RECORDING MODE IS F                                                  
004700     LABEL RECORDS ARE STANDARD                                           
004800     BLOCK CONTAINS 0 RECORDS.                                            
004900 01  VL-MARKER-RECORD                 PIC  X(078).                        
005000*                                                                         
005100*                                                                         
005200 WORKING-STORAGE SECTION.                                                 
000250***************************************************************** 00025015
000260*    INPUT CONTROL CARD                                           00026015
000270***************************************************************** 00027015
000054 01  CC-CONTROL-CARD.                                                     
000055     05  CC-SCHEDULE-DATE.                                                
000056         10  CC-CCYY                 PIC X(04).                           
000057         10  CC-MM                   PIC X(02).                           
000058         10  CC-DD                   PIC X(02).                           
000059                                                                          
005300*****************************************************************         
005400*    PROGRAM CONSTANTS                                                    
005500*****************************************************************         
005600 01  PC-CONSTANTS.                                                        
005700     05  PC-CURRENT-PGM-VLKUT032      PIC  X(08) VALUE 'VLKUT032'.        
005800     05  PC-MAX-RETRIES               PIC S9(04) VALUE +3                 
005900                                                 COMP SYNC.               
006000                                                                          
006100*****************************************************************         
006200*    PROGRAM SWITCHES                                                     
006300*****************************************************************         
006400 01  PS-SWITCHES.                                                         
006500     05  PS-END-CURSOR-SWITCH         PIC  X(01) VALUE 'N'.               
006600         88  PS-END-CSR                          VALUE 'Y'.               
006700         88  PS-NOT-END-CSR                      VALUE 'N'.               
006800                                                                          
006900*****************************************************************         
007000*    PROGRAM VARIABLES                                                    
007100*****************************************************************         
007200 01  PV-VARIABLES.                                                        
007300     05  PV-RETRY-COUNT               PIC S9(04) VALUE +0                 
007400                                                 COMP SYNC.               
007500     05  PV-ABEND-CODE                PIC S9(04) VALUE ZERO               
007600                                                 COMP SYNC.               
007700     05  PV-PARAGRAPH                 PIC  X(30) VALUE SPACES.            
007800     05  PV-DB2-FUNCTION              PIC  X(30) VALUE SPACES.            
007900     05  PV-ABEND-STRUCTURE-1         PIC  X(20) VALUE SPACES.            
008000     05  PV-ABEND-STRUCTURE-2         PIC  X(20) VALUE SPACES.            
008100     05  PV-ABEND-STRUCTURE-3         PIC  X(20) VALUE SPACES.            
008200     05  PV-ERROR-MSG                 PIC  X(60) VALUE SPACES.            
008300     05  PV-RECS-WRITTEN              PIC  9(09) VALUE ZERO.              
009000     05  PV-RUN-DATE-CN               PIC  X(08) VALUE SPACES.            
010100     05  PV-RUN-TIME-CN               PIC  X(06) VALUE SPACES.            
010600***** FILE NAME CONSISTS OF RUN-DATE-CN_VENDOR.DAT                        
010700*****     EXAMPLE: 20050317_VENDOR.DAT                                    
010800     05  PV-FILE-NAME.                                                    
010900         10  PV-CREATE-DATE           PIC  X(08) VALUE SPACES.            
011000         10  PV-UNDERSCORE            PIC  X(01) VALUE '_'.               
011100         10  PV-DEPT-DAT              PIC  X(08) VALUE SPACES.            
011200                                                                          
011300***  MARKER FILE LAYOUT                                                   
011400     COPY VLRD029.                                                        
011500                                                                          
011600***  DEPARTMENT FILE LAYOUT                                               
011700     COPY VLRD032.                                                        
011800                                                                          
000418***  DATE ROUTINE COPYBOOK                                        00041815
000419     COPY DPWS500.                                                00041915
000420                                                                  00042015
011900***  DB2 ABEND COPYBOOK                                                   
012000     COPY DPWS004.                                                        
012100                                                                          
012200     EXEC SQL                                                             
012300         INCLUDE SQLCA                                                    
012400     END-EXEC.                                                            
012500                                                                          
012600     COPY SQLCA2.                                                         
012700                                                                          
012800***  XLT_DEPT                                                             
012900     EXEC SQL                                                             
013000         INCLUDE XLT00002                                                 
013100     END-EXEC.                                                            
013200                                                                          
013300***  XLT_MAJ_CL                                                           
013400     EXEC SQL                                                             
013500         INCLUDE XLT00022                                                 
013600     END-EXEC.                                                            
013700                                                                          
013800***  XLT_SUB_CL                                                           
013900     EXEC SQL                                                             
014000         INCLUDE XLT00023                                                 
014100     END-EXEC.                                                            
014200                                                                          
014300                                                                          
014400***  EXTRACT CURSOR                                                       
014500     EXEC SQL                                                             
014600         DECLARE EXTRACT-CSR CURSOR FOR                                   
014700         SELECT A.DEPT_NBR                                                
014800              , A.DEPT_NM                                                 
014900              , B.MAJ_CL_NBR                                              
015000              , C.SUB_CL_NBR                                              
015100           FROM                                                           
015200                XLT_DEPT A                                                
015300              , XLT_MAJ_CL B                                              
015400              , XLT_SUB_CL C                                              
015500           WHERE A.DEPT_NBR = B.DEPT_NBR                                  
015600             AND B.DEPT_NBR = C.DEPT_NBR                                  
015700             AND B.MAJ_CL_NBR = C.MAJ_CL_NBR                              
015800        ORDER BY A.DEPT_NBR, B.MAJ_CL_NBR, C.SUB_CL_NBR                   
015800        WITH UR                                                           
015900     END-EXEC.                                                            
016000                                                                          
016100 LINKAGE SECTION.                                                         
016200                                                                          
016300 PROCEDURE DIVISION.                                                      
016400*****************************************************************         
016500*  MAINLINE LOGIC FOR PROGRAM                                             
016600*****************************************************************         
016700 0000-MAINLINE.                                                           
016800     PERFORM 1000-INITIALIZE.                                             
016900                                                                          
017000     PERFORM 2000-PROCESS                                                 
017100         UNTIL PS-END-CSR.                                                
017200                                                                          
017300     PERFORM 9000-EOJ.                                                    
017400                                                                          
017500     STOP RUN.                                                            
017600                                                                          
017700*****************************************************************         
017800*  INITIALIZATION PARAGRAPH                                               
017900*****************************************************************         
018000 1000-INITIALIZE.                                                         
018100     OPEN OUTPUT VL-DEPT-FILE                                             
018200                 VL-MARKER-FILE.                                          
018300                                                                          
000126     ACCEPT CC-CONTROL-CARD FROM SYSIN.                                   
000127                                                                          
000128     DISPLAY 'CONTROL CARD ====>  ' CC-CONTROL-CARD.                      
000129                                                                          
000130     PERFORM 1100-VALIDATE-CONTROL-CARD.                                  
000131                                                                          
000132******* TIME IS CONTAINED CURRENT-DATE IN POSITION 9 FOR 8                
000133     MOVE FUNCTION CURRENT-DATE(9:8) TO PV-RUN-TIME-CN.                   
000135                                                                          
000136     DISPLAY '******  RUN TIME  => '  PV-RUN-TIME-CN.                     
000137                                                                          
020300     PERFORM 7000-OPEN-EXTRACT-CURSOR.                                    
020400     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
020500                                                                          
000142******************************************************************        
000143**  VALIDATE THE DATE FROM THE SCHEDULE                                   
000144******************************************************************        
000145 1100-VALIDATE-CONTROL-CARD.                                              
000146     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
000147     SET DPG51-FISCAL-454-CALENDAR                                        
000148         DPG51-DO-NOT-INCR-DECR-DATE                                      
000149         DPG52-LK-DTE-ISO           TO TRUE.                              
000150                                                                          
000151     MOVE CC-SCHEDULE-DATE     TO DPG52-LK-DATE-INPUT.                    
000152                                                                          
000153     CALL DP500-KOHLS-CALENDAR-ROUTINE USING                              
000154             DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
000155                                                                          
000156     IF DPG54-NO-ERROR                                                    
000157         MOVE CC-SCHEDULE-DATE     TO PV-RUN-DATE-CN                      
000158     ELSE                                                                 
000159         DISPLAY '** ABEND **'                                            
000160         DISPLAY '** PROGRAM = VLKUT032 -- CALLED MODULE '                
000161                  'DPKUT500 **'                                           
000162         DISPLAY '** PARAGRAPH = 1100-VALIDATE-CONTROL-CARD **'           
000163         DISPLAY '** INVALID SCHEDULE DATE: ' CC-SCHEDULE-DATE            
000164         DISPLAY '** ERROR MESSAGE FROM CALLED MODULE:   '                
000165         DISPLAY DPG54-ERROR-MESSAGE                                      
000166         MOVE +4003 TO PV-ABEND-CODE                                      
000167         CALL 'ILBOABN0' USING PV-ABEND-CODE                              
000168     END-IF.                                                              
000169                                                                          
000169                                                                          
020600*****************************************************************         
020700* PROCESS DATA FROM CURSOR AND WRITE OUTPUT RECORD                        
020800*****************************************************************         
020900 2000-PROCESS.                                                            
021000     INITIALIZE VL032-DEPARTMENT-RECORD.                                  
021110     MOVE 'A'                     TO VL032-ACD-INDICATOR.                 
021200     MOVE XLT00002-DEPT-NBR       TO VL032-DEPT-NUMBER.                   
021300     MOVE XLT00002-DEPT-NM        TO VL032-DEPT-DESC.                     
021400     MOVE XLT00022-MAJ-CL-NBR     TO VL032-SUB-DEPT-NUMBER.               
021500     MOVE XLT00023-SUB-CL-NBR     TO VL032-CLASS.                         
021600     MOVE 'X'                     TO VL032-ANCHOR-CONSTANT.               
021700                                                                          
021800     PERFORM  8000-WRITE-OUTPUT.                                          
021900                                                                          
022000     PERFORM 7200-FETCH-EXTRACT-CURSOR.                                   
022100                                                                          
022200                                                                          
022300******************************************************************        
022400* OPEN CURSOR                                                             
022500******************************************************************        
022600 7000-OPEN-EXTRACT-CURSOR.                                                
022700     PERFORM                                                              
022800         WITH TEST AFTER                                                  
022900         VARYING PV-RETRY-COUNT                                           
023000         FROM 1 BY 1                                                      
023100         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
023200         OR  (SQLCODE = +0))                                              
023300                                                                          
023400         EXEC SQL                                                         
023500              OPEN EXTRACT-CSR                                            
023600         END-EXEC                                                         
023700                                                                          
023800         EVALUATE TRUE                                                    
023900             WHEN SQLCODE = +0                                            
024000                 CONTINUE                                                 
024100             WHEN SQLWARN0 NOT EQUAL SPACE                                
024200             WHEN SQLCODE NOT EQUAL ZERO                                  
024300                 DISPLAY '**********  ABEND ************'                 
024400                 DISPLAY '******* PROGRAM = VLKUT032 ***'                 
024500                 DISPLAY '** PARAGRAPH = 7000-OPEN-EXTRACT-CURSOR'        
024600                 MOVE '7000-OPEN-EXTRACT-CURSOR' TO PV-PARAGRAPH          
024700                 MOVE 'OPEN CURSOR    ' TO PV-DB2-FUNCTION                
024800                 MOVE 'XLT_DEPT'        TO PV-ABEND-STRUCTURE-1           
024900                 MOVE 'XLT_MAJ_CL'      TO PV-ABEND-STRUCTURE-2           
025000                 MOVE 'XLT_SUB_CL'      TO PV-ABEND-STRUCTURE-3           
025100                 PERFORM 9200-SQL-ERROR                                   
025200         END-EVALUATE                                                     
025300     END-PERFORM.                                                         
025400                                                                          
025500     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
025600         MOVE '7000-OPEN-EXTRACT-CURSOR'  TO PV-PARAGRAPH                 
025700         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
025800         MOVE 'XLT_DEPT'               TO PV-ABEND-STRUCTURE-1            
025900         MOVE 'XLT_MAJ_CL'             TO PV-ABEND-STRUCTURE-2            
026000         MOVE 'XLT_SUB_CL'             TO PV-ABEND-STRUCTURE-3            
026100         PERFORM 9200-SQL-ERROR                                           
026200     END-IF.                                                              
026300                                                                          
026400******************************************************************        
026500* CLOSE CURSOR                                                            
026600******************************************************************        
026700 7100-CLOSE-EXTRACT-CURSOR.                                               
026800     PERFORM                                                              
026900         WITH TEST AFTER                                                  
027000         VARYING PV-RETRY-COUNT                                           
027100         FROM 1 BY 1                                                      
027200         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
027300         OR  (SQLCODE = +0))                                              
027400                                                                          
027500         EXEC SQL                                                         
027600             CLOSE EXTRACT-CSR                                            
027700         END-EXEC                                                         
027800                                                                          
027900         EVALUATE TRUE                                                    
028000             WHEN SQLCODE = +0                                            
028100                 CONTINUE                                                 
028200             WHEN SQLWARN0 NOT EQUAL SPACE                                
028300             WHEN SQLCODE NOT EQUAL ZERO                                  
028400                 DISPLAY '*********  ABEND ************'                  
028500                 DISPLAY '******* PROGRAM = VLKUT032 **'                  
028600                 DISPLAY '* PARAGRAPH = 7100-CLOSE-EXTRACT-CURSOR'        
028700                 PERFORM 9200-SQL-ERROR                                   
028800         END-EVALUATE                                                     
028900     END-PERFORM.                                                         
029000                                                                          
029100     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
029200         MOVE '7100-CLOSE-EXTRACT-CURSOR' TO PV-PARAGRAPH                 
029300         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
029400         MOVE 'XLT_DEPT'               TO PV-ABEND-STRUCTURE-1            
029500         MOVE 'XLT_MAJ_CL'             TO PV-ABEND-STRUCTURE-2            
029600         MOVE 'XLT_SUB_CL'             TO PV-ABEND-STRUCTURE-3            
029700         PERFORM 9200-SQL-ERROR                                           
029800     END-IF.                                                              
029900                                                                          
030000******************************************************************        
030100*  FETCH CURSOR                                                           
030200******************************************************************        
030300 7200-FETCH-EXTRACT-CURSOR.                                               
030400     PERFORM                                                              
030500         WITH TEST AFTER                                                  
030600         VARYING PV-RETRY-COUNT                                           
030700         FROM 1 BY 1                                                      
030800         UNTIL ((PV-RETRY-COUNT > PC-MAX-RETRIES)                         
030900         OR  (SQLCODE = +0) OR (PS-END-CSR))                              
031000                                                                          
031100         EXEC SQL                                                         
031200             FETCH EXTRACT-CSR                                            
031300             INTO :XLT00002-DEPT-NBR                                      
031400                , :XLT00002-DEPT-NM                                       
031500                , :XLT00022-MAJ-CL-NBR                                    
031600                , :XLT00023-SUB-CL-NBR                                    
031700         END-EXEC                                                         
031800                                                                          
031900         EVALUATE TRUE                                                    
032000             WHEN SQLCODE = ZERO                                          
032100                 CONTINUE                                                 
032200             WHEN SQLCODE = +100                                          
032300                 SET PS-END-CSR TO TRUE                                   
032400             WHEN SQLWARN0 NOT EQUAL SPACE                                
032500             WHEN SQLCODE NOT EQUAL ZERO                                  
032600                 DISPLAY '*********  ABEND ************'                  
032700                 DISPLAY '****** PROGRAM = VLKUT032 ***'                  
032800                 DISPLAY '* PARAGRAPH = 7200-FETCH-EXTRACT-CURSOR'        
032900                 PERFORM 9200-SQL-ERROR                                   
033000         END-EVALUATE                                                     
033100     END-PERFORM.                                                         
033200                                                                          
033300     IF PV-RETRY-COUNT > PC-MAX-RETRIES                                   
033400         MOVE '7200-FETCH-EXTRACT-CURSOR' TO PV-PARAGRAPH                 
033500         MOVE 'MAX RETRIES EXCEEDED  ' TO PV-DB2-FUNCTION                 
033600         MOVE 'XLT_DEPT'               TO PV-ABEND-STRUCTURE-1            
033700         MOVE 'XLT_MAJ_CL'             TO PV-ABEND-STRUCTURE-2            
033800         MOVE 'XLT_SUB_CL'             TO PV-ABEND-STRUCTURE-3            
033900         PERFORM 9200-SQL-ERROR                                           
034000     END-IF.                                                              
034100                                                                          
034200*****************************************************************         
034300*  WRITE OUTPUT FILE                                                      
034400*****************************************************************         
034500 8000-WRITE-OUTPUT.                                                       
034600     WRITE VL-DEPT-RECORD FROM VL032-DEPARTMENT-RECORD.                   
034700                                                                          
034800     ADD 1                TO PV-RECS-WRITTEN.                             
034900                                                                          
035000*****************************************************************         
035100*  CREATE MARKER FILE                                                     
035200*****************************************************************         
035300 8500-CREATE-MARKER.                                                      
035400     MOVE PV-RUN-DATE-CN  TO PV-CREATE-DATE.                              
035500     MOVE 'DEPT.DAT'      TO PV-DEPT-DAT.                                 
035600     MOVE PV-FILE-NAME    TO VL029-FILE-NAME.                             
035700     MOVE PV-RECS-WRITTEN TO VL029-RECORD-COUNT.                          
035800     MOVE PV-RUN-DATE-CN  TO VL029-BUILD-DATE.                            
035900     MOVE PV-RUN-TIME-CN  TO VL029-BUILD-TIME.                            
036000     MOVE 'X'             TO VL029-ANCHOR-CONSTANT.                       
036100                                                                          
036200     WRITE VL-MARKER-RECORD FROM VL029-MARKER-RECORD.                     
036300                                                                          
036400*****************************************************************         
036500*  END OF JOB PROCESSING                                                  
036600*****************************************************************         
036700 9000-EOJ.                                                                
036800     DISPLAY 'RECORDS WRITTEN ===>  ' PV-RECS-WRITTEN.                    
036900                                                                          
037000     PERFORM 8500-CREATE-MARKER.                                          
037100                                                                          
037200     PERFORM 7100-CLOSE-EXTRACT-CURSOR.                                   
037300                                                                          
037400     CLOSE VL-DEPT-FILE                                                   
037500           VL-MARKER-FILE.                                                
037600                                                                          
037700*****************************************************************         
037800*  PARAGRAPH EXECUTED WHEN SERIOUS DB2 ERROR OCCURRED                     
037900*****************************************************************         
038000 9200-SQL-ERROR.                                                          
038100     CLOSE VL-DEPT-FILE                                                   
038200                                                                          
038300     MOVE SQLCODE                     TO SQLCODE2.                        
038400     MOVE SQLERRD(3)                  TO SQLERRD3.                        
038500                                                                          
038600     DISPLAY '** ABEND **         ** = SQLERROR'.                         
038700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PGM-VLKUT032.                
038800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH.                           
038900     DISPLAY '** OPERATION ** = ' PV-DB2-FUNCTION.                        
039000     DISPLAY '**           ** = ' PV-ERROR-MSG                            
039100                                                                          
039200     DISPLAY '** SQLCODE          ** = ' SQLCODE2.                        
039300     DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                        
039400     DISPLAY '** SQLERRM          ** = ' SQLERRM.                         
039500     DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                        
039600                                                                          
039700     COPY DPPD004.                                                        
039800                                                                          
039900     MOVE +4013                       TO PV-ABEND-CODE.                   
040000     CALL 'ILBOABN0' USING PV-ABEND-CODE.                                 
040100                                                                          
040200*************  END OF VLKUT032  ********************************          
