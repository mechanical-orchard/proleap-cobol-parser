00001 *                                                                 11/29/94
00002 *                                                                 INKUT112
00003 *                                                                    LV004
00004  IDENTIFICATION DIVISION.                                         INKUT112
00005                                                                   INKUT112
00006  PROGRAM-ID.              INKUT112.                               INKUT112
00007  AUTHOR.                  TROY BIRK.                              INKUT112
00008  INSTALLATION.            KOHLS DEPARTMENT STORES.                INKUT112
00009  DATE-WRITTEN.            10/93.                                  INKUT112
00010  DATE-COMPILED.                                                   INKUT112
00011                                                                   INKUT112
00012 ******************************************************************INKUT112
00013 *   REMOVE BAD STORES FROM INPUT FILE                            *   CL**3
00014 ******************************************************************INKUT112
00015 *                          CHANGE LOG                            *   CL**3
00016 ******************************************************************   CL**3
00017 *  03/94    M. BLAZICH                                           *   CL**3
00018 *  COPY BOOKS AND FILE SIZES CHANGED FOR EXTD_AMT EXPANSION,     *   CL**3
00019 *  AND ADD'L SCAN-LINE-NBR FIELD                                 *   CL**3
00017 *  02/99    M. KLAMIK                                            *   CL**3
00018 *  EXPAND COPYBOOKS INWS008 AND INWS010 TO ACCOMMODATE NEW           CL**3
      *  SCAN DATE AND TIMESTAMP.                                               
W21732******************************************************************   CL**3
W21732*  11/11/2002         IGSI                                                
W21732*  CHANGED THE FILE LENGTH OF OUT-COUNT-RECORD FILE TO 100 BYTES.         
W21732*  INWS008 COPYBOOK IS REMOVED AS IT IS NO LONGER USED.                   
C21732*  02/24/2003         SCOTT JACKSON                                       
C21732*  INWS008 IS BEING RETAINED AND BE USED FOR A DIFFERENT PURPOSE.         
C21732*  INWS008 USED TO STRIP OFF SKUS NOT FOUND (XREF) IN INKUT101.           
C21732*  THESE RECS WILL BE MERGED WITH INKUT102/INKUT110 INVALID RECS.         
W26600******************************************************************   CL**3
W26600*                                                                    CL**3
W26600*  W26600  06/  /2004  PHYSICAL INVENTORY STORE EXPANSION            CL**3
W26600*  CHANGED IWS008 RECORD LAYOUT TO INCLUDE TWO FIELDS THAT WERE      CL**3
W26600*  JUST IN THE PROGRAM IN008-INVALID-RCD, AND IN008-INVALID-ITEM     CL**3
W26600*  M JOHNSON                                                         CL**3
W26600*                                                                    CL**3
00020 ******************************************************************   CL**3
729935*  PM00729935 1/22/2005                                              CL**3
729935*  CHANGE  INVALID RANGE FILE DID NOT HAVE END OF FILE HANDLING      CL**3
729935*          CAUSING A STATUS 46.                                      CL**3
729935*  R JANSSEN                                                         CL**3
729935*                                                                    CL**3
00020 ******************************************************************   CL**3
00021 *                                                                 INKUT112
00022  ENVIRONMENT DIVISION.                                            INKUT112
00023 *                                                                 INKUT112
00024 *                                                                 INKUT112
00025  CONFIGURATION SECTION.                                           INKUT112
00026                                                                   INKUT112
00027  SOURCE-COMPUTER. IBM-3090.                                       INKUT112
00028  OBJECT-COMPUTER. IBM-3090.                                       INKUT112
00029 *                                                                 INKUT112
00030 *                                                                 INKUT112
00031  INPUT-OUTPUT SECTION.                                            INKUT112
00032                                                                   INKUT112
00033  FILE-CONTROL.                                                    INKUT112
00034      SELECT IN-COUNT-FILE                                         INKUT112
00035          ASSIGN TO INP01.                                         INKUT112
00036      SELECT INVALID-RANGE-FILE                                    INKUT112
00037          ASSIGN TO INP02.                                         INKUT112
00038      SELECT OUT-COUNT-FILE                                        INKUT112
00039          ASSIGN TO OUT01.                                         INKUT112
C21732     SELECT INVALID-SKU-FILE                                      INKUT112
00041          ASSIGN TO OUT02.                                         INKUT112
00042                                                                   INKUT112
00043  DATA DIVISION.                                                   INKUT112
00044  FILE SECTION.                                                    INKUT112
00045                                                                   INKUT112
00046  FD  IN-COUNT-FILE                                                INKUT112
00047      LABEL RECORDS ARE STANDARD                                   INKUT112
00048      RECORDING MODE IS F                                          INKUT112
00049      BLOCK CONTAINS 0 RECORDS.                                    INKUT112
W21732 01  IN-COUNT-RECORD         PIC X(100).                          INKUT112
00051                                                                   INKUT112
00052  FD  OUT-COUNT-FILE                                               INKUT112
00053      LABEL RECORDS ARE STANDARD                                   INKUT112
00054      RECORDING MODE IS F                                          INKUT112
00055      BLOCK CONTAINS 0 RECORDS.                                    INKUT112
W21732 01  OUT-COUNT-RECORD        PIC X(100).                          INKUT112
00057                                                                   INKUT112
C21732 FD  INVALID-SKU-FILE                                             INKUT112
00059      LABEL RECORDS ARE STANDARD                                   INKUT112
00060      RECORDING MODE IS F                                          INKUT112
00061      BLOCK CONTAINS 0 RECORDS.                                    INKUT112
C21732 01  INVALID-SKU-RECORD      PIC X(88).                           INKUT112
00063                                                                   INKUT112
00064  FD  INVALID-RANGE-FILE                                           INKUT112
00065      LABEL RECORDS ARE STANDARD                                   INKUT112
00066      RECORDING MODE IS F                                          INKUT112
00067      BLOCK CONTAINS 0 RECORDS.                                    INKUT112
00068  01  INVALID-RANGE-RECORD    PIC X(18).                           INKUT112
00069                                                                   INKUT112
00070      EJECT                                                        INKUT112
00071  WORKING-STORAGE SECTION.                                         INKUT112
00072 *                                                                 INKUT112
00073  01  ABEND-CODE               PIC S9(04)  VALUE ZERO              INKUT112
00074                                           COMP SYNC.              INKUT112
00075 *                                                                 INKUT112
00076  01  PV-PROGRAM-VARIABLES.                                        INKUT112
00077      05  PV-INVALID-RANGE.                                        INKUT112
00078          10  PV-RANGE-LOW     PIC 9(08)  VALUE ZERO.              INKUT112
00079          10  FILLER           PIC X(01)  VALUE SPACES.            INKUT112
00080          10  PV-RANGE-HIGH    PIC 9(08)  VALUE ZERO.              INKUT112
00081          10  PV-RETRY-SW      PIC X(01)  VALUE SPACES.            INKUT112
00082              88  PV-RETRY-STORE          VALUE 'Y'.               INKUT112
C21732     05  PV-LINE-NUMBER       PIC 9(06)  VALUE ZERO.                 CL**4
C21732     05  PV-TEMP-LINE         PIC 9(06)  VALUE ZERO.              INKUT104
C21732     05  PV-HEADER-RECORD.                                        INKUT112
C21732         10  FILLER           PIC X(13)  VALUE 'HEADER RECORD'.   INKUT112
C21732         10  FILLER           PIC X(37)  VALUE SPACES.            INKUT112
C21732 01  SA-SAVE-AREAS.                                               INKUT104
W26600     05  SA-STORE-NUMBER         PIC X(04)    VALUE ZERO.         INKUT104
C21732     05  SA-AREA-NUMBER          PIC 9(06)    VALUE ZERO.         INKUT104
C21732     05  SA-SCAN-LINE-NUMBER     PIC 9(06)    VALUE ZERO.            CL**5
00086 *                                                                 INKUT112
00087  01  PS-PROGRAM-SWITCHES.                                         INKUT112
729935     05  PS-INVALID-DONE-SW   PIC X       VALUE 'N'.              INKUT112
729935         88  PS-NO-INVALID-RECORD         VALUE 'Y'.              INKUT112
00088      05  PS-MORE-RECS-SW      PIC X       VALUE 'Y'.              INKUT112
00089          88  PS-NO-MORE-RECS              VALUE 'N'.              INKUT112
C21732     05  PS-INVALID-RECORD-SW    PIC X    VALUE 'Y'.              INKUT102
C21732     05  PS-INVALID-ITEM-SW      PIC X    VALUE 'Y'.              INKUT102
00090 *                                                                 INKUT112
00091 ***************************************************************** INKUT112
00092 * RECORD LAYOUT FOR OUTPUT FILE, RECORD NUMBER IS APPENDED TO     INKUT112
00093 * THE INPUT FILE.                                                 INKUT112
00094 ***************************************************************** INKUT112
00095      COPY INWS010.                                                   CL**4
00096 *                                                                 INKUT112
00099 ***************************************************************** INKUT112
00100 * RECORD LAYOUT FOR OUTPUT FILE, THIS WILL BE USED WHEN A STORE   INKUT112
00101 * CONTAINS INVALIDS BECAUSE SKU ATTRIBUTES COULD NOT BE FOUND.    INKUT112
00102 ***************************************************************** INKUT112
00103      COPY INWS008.                                                   CL**4
W26600*01  PV-SPECIAL-IN008-LAYOUT        REDEFINES                     INKUT102
W26600*    IN008-INVENTORY-COUNT-RECORD.                                INKUT102
W26600*    05  PV-OUTPUT-RECORD.                                        INKUT102
W26600*        10  FILLER                 PIC X(85).                    INKUT102
W26600*        10  PV-OUTPUT-INVALID-REC  PIC X(01).                    INKUT102
W26600*        10  PV-OUTPUT-INVALID-ITEM PIC X(01).                    INKUT102
W26600*                                                                 INKUT112
00105                                                                   INKUT112
00106      EJECT                                                        INKUT112
00107  LINKAGE SECTION.                                                 INKUT112
00108 *                                                                 INKUT112
00109  PROCEDURE DIVISION.                                              INKUT112
00110                                                                   INKUT112
00111 ****************************************************************  INKUT112
00112 *                                                                 INKUT112
00113 ****************************************************************  INKUT112
00114  A100-MAIN-MODULE.                                                INKUT112
00115                                                                   INKUT112
00116      PERFORM B100-OPEN-FILES.                                     INKUT112
00117      PERFORM Z100-READ-PHYSCNT-INPUT.                             INKUT112
00118      PERFORM Z200-READ-RANGE-FILE.                                INKUT112
00119      PERFORM B200-PROCESS-RECORDS                                 INKUT112
00120                   UNTIL PS-NO-MORE-RECS.                          INKUT112
00121      PERFORM B300-CLOSE-FILES.                                    INKUT112
00122      STOP RUN.                                                    INKUT112
00123 *                                                                 INKUT112
00124 ******************************************************************INKUT112
00125 *    OPEN ALL FILES.                                              INKUT112
00126 ******************************************************************INKUT112
00127  B100-OPEN-FILES.                                                 INKUT112
00128                                                                   INKUT112
00129      OPEN INPUT  IN-COUNT-FILE                                    INKUT112
00130                  INVALID-RANGE-FILE                               INKUT112
C21732          OUTPUT INVALID-SKU-FILE                                 INKUT112
00132                  OUT-COUNT-FILE.                                  INKUT112
00133 *                                                                 INKUT112
00134 ****************************************************************  INKUT112
C21732* IF THE PRE-EDIT PROGRAM (INKUT101) FAILED TO RETRIEVE SKU       INKUT112
C21732* ATTRIBUTES FOR THIS RECORD, STRIP OFF AND WRITE AS INVALID.     INKUT112
C21732* REC WILL BE MERGED WITH OTHER INVALIDS LATER IN THE EDIT PROCESSINKUT112
00136 ****************************************************************  INKUT112
00137  B200-PROCESS-RECORDS.                                            INKUT112
00138                                                                   INKUT112
C21732**   PV-LINE-NUMBER IS MOVED TO IN010-LINE-NUMBER. THIS IS PASSED    CL**4
C21732**   ALONG TO THE VALID RECORDS (COPY INWS010). IF RECORD IS FOUND   CL**4
C21732**   TO CONTAIN NO ATTRIBUTES FROM PRE-EDIT PROGRAM , (NOT FOUND)    CL**4
C21732**   THE LINE NUMBER IS ALSO PASSED ALONG FOR INVALID RECORDS.       CL**4
C21732**                                                                   CL**4
C21732**   PV-LINE-NUMBER IS INCREMENTED BY 1 FOR EACH RECORD W/IN         CL**4
C21732**   A STORE/AREA.  IT IS RESET UPON A CHANGE IN STORE OR AREA.      CL**4
C21732                                                                     CL**4
C21732     IF IN010-STORE-NUMBER NOT = SA-STORE-NUMBER OR                  CL**2
C21732        IN010-AREA-NUMBER  NOT = SA-AREA-NUMBER                      CL**2
C21732           MOVE  0                 TO PV-LINE-NUMBER                 CL**4
C21732           MOVE IN010-STORE-NUMBER TO SA-STORE-NUMBER                CL**2
C21732           MOVE IN010-AREA-NUMBER  TO SA-AREA-NUMBER                 CL**2
C21732     END-IF                                                          CL**2
C21732                                                                     CL**2
C21732     ADD 1                  TO PV-LINE-NUMBER.                       CL**4
C21732                                                                     CL**2
C21732     MOVE PV-LINE-NUMBER TO IN010-LINE-NUMBER.                       CL**4
C21732                                                                     CL**4
C21732     IF IN010-DEPT-CLASS = ZEROES                                         
C21732*        INDICATES SKU ATTRIBUTES WERE NOT FOUND ON TSKXREF               
C21732         PERFORM B400-CREATE-INVALID-SKU-REC                              
C21732     ELSE                                                                 
00139          IF IN010-RECORD-NUMBER >= PV-RANGE-LOW  AND              INKUT112
00140             IN010-RECORD-NUMBER <= PV-RANGE-HIGH                  INKUT112
W21732            CONTINUE                                                      
00141**            IF PV-RETRY-STORE                                    INKUT112
00142 *                MOVE IN010-INVENTORY-COUNT-RECORD TO             INKUT112
00143 *                     IN008-INVENTORY-COUNT-RECORD                INKUT112
00144 *                PERFORM Z400-WRITE-INVALID-RECORD                INKUT112
00145**            END-IF                                               INKUT112
00146          ELSE                                                     INKUT112
00147              PERFORM Z300-WRITE-NEW-PHYSCNT-RECORD                INKUT112
00148          END-IF                                                   INKUT112
C21732     END-IF.                                                      INKUT112
00149                                                                   INKUT112
00150      IF IN010-RECORD-NUMBER = PV-RANGE-HIGH                       INKUT112
729935       IF (NOT PS-NO-INVALID-RECORD)                                      
00151          PERFORM Z200-READ-RANGE-FILE                             INKUT112
729935       END-IF                                                             
00152      END-IF.                                                      INKUT112
00153                                                                   INKUT112
00154      PERFORM Z100-READ-PHYSCNT-INPUT.                             INKUT112
00155 *                                                                 INKUT112
00156 ******************************************************************INKUT112
00157 *   CLOSE ALL FILES.                                              INKUT112
00158 ******************************************************************INKUT112
00159  B300-CLOSE-FILES.                                                INKUT112
00160                                                                   INKUT112
00161      CLOSE   IN-COUNT-FILE                                        INKUT112
00162              INVALID-RANGE-FILE                                   INKUT112
C21732             INVALID-SKU-FILE                                     INKUT112
00164              OUT-COUNT-FILE.                                      INKUT112
00165 *                                                                 INKUT112
C21732****************************************************************  INKUT112
C21732* WRITE SKU-NOT-FOUND RECORDS TO AN INVALIDS FILE                 INKUT112
C32732****************************************************************  INKUT112
C21732 B400-CREATE-INVALID-SKU-REC.                                             
C21732                                                                          
W26600*    MOVE IN010-INVENTORY-COUNT-RECORD TO                         INKUT102
W26600*         IN008-INVENTORY-COUNT-RECORD                            INKUT102
W26600     INITIALIZE                  IN008-INVENTORY-COUNT-RECORD.            
W26600     MOVE IN010-STORE-NUMBER     TO IN008-STORE-NUMBER.                   
W26600     MOVE IN010-AREA-NUMBER      TO IN008-AREA-NUMBER.                    
W26600     MOVE IN010-LINE-NUMBER      TO IN008-LINE-NUMBER.                    
W26600     MOVE IN010-SCAN-LINE-NUMBER TO IN008-SCAN-LINE-NUMBER                
W26600     MOVE IN010-SKU              TO IN008-SKU.                            
W26600     MOVE IN010-QUANTITY         TO IN008-QUANTITY.                       
W26600     MOVE IN010-PRICE            TO IN008-PRICE.                          
W26600     MOVE IN010-UPC-CODE         TO IN008-UPC-CODE.                       
W26600     MOVE IN010-INVALID-INDICATOR                                         
W26600                                 TO IN008-INVALID-INDICATOR.              
W26600     MOVE IN010-SCAN-DTE         TO IN008-SCAN-DTE.                       
W26600     MOVE IN010-SCAN-TIMESTAMP   TO IN008-SCAN-TIMESTAMP.                 
C21732                                                                  INKUT102
C21732*    PREVENTS DUP RECORDS ON TINVDTL INSERTS                      INKUT102
C21732*    MOVE IN008-SCAN-LINE-NUMBER   TO IN008-LINE-NUMBER           INKUT102
C21732                                                                  INKUT102
W26600     MOVE PS-INVALID-RECORD-SW   TO IN008-INVALID-RCD.            INKUT102
W26600     MOVE PS-INVALID-ITEM-SW     TO IN008-INVALID-ITEM.           INKUT102
C21732                                                                  INKUT112
C21732     PERFORM Z400-WRITE-INVALID-RECORD.                           INKUT102
00166 *                                                                 INKUT112
00167 ****************************************************************  INKUT112
00168 * Z100-READ-PHYCNT-INPUT  READS THE INPUT FILE                    INKUT112
00169 ****************************************************************  INKUT112
00170  Z100-READ-PHYSCNT-INPUT.                                         INKUT112
00171                                                                   INKUT112
00172      READ IN-COUNT-FILE                                           INKUT112
00173            AT END                                                 INKUT112
00174              SET PS-NO-MORE-RECS TO TRUE.                         INKUT112
00175                                                                   INKUT112
00176      MOVE IN-COUNT-RECORD TO IN010-INVENTORY-COUNT-RECORD.        INKUT112
00177 *                                                                 INKUT112
00178 ****************************************************************  INKUT112
00179 *                                                                 INKUT112
00180 ****************************************************************  INKUT112
00181  Z200-READ-RANGE-FILE.                                            INKUT112
00182                                                                   INKUT112
00183      READ INVALID-RANGE-FILE                                      INKUT112
00184          INTO PV-INVALID-RANGE                                    INKUT112
00185          AT END                                                   INKUT112
729935             MOVE 'Y' TO PS-INVALID-DONE-SW                               
00186              INITIALIZE PV-INVALID-RANGE                          INKUT112
00187              MOVE SPACE TO PV-RETRY-SW.                           INKUT112
C21732                                                                  INKUT112
C21732*    IF PV-RETRY-STORE                                            INKUT112
C21732*        WRITE RETRY-RECORD                                       INKUT112
C21732*            FROM PV-HEADER-RECORD                                INKUT112
C21732*    END-IF.                                                      INKUT112
00193 *                                                                 INKUT112
00194 ****************************************************************  INKUT112
00195 *                                                                 INKUT112
00196 ****************************************************************  INKUT112
00197  Z300-WRITE-NEW-PHYSCNT-RECORD.                                   INKUT112
00198                                                                   INKUT112
00199      WRITE OUT-COUNT-RECORD                                       INKUT112
00200          FROM IN010-INVENTORY-COUNT-RECORD.                       INKUT112
00201 ****************************************************************  INKUT112
00202 *                                                                 INKUT112
00203 ****************************************************************  INKUT112
00204  Z400-WRITE-INVALID-RECORD.                                       INKUT112
00205                                                                   INKUT112
00206      WRITE INVALID-SKU-RECORD                                     INKUT112
00207          FROM IN008-INVENTORY-COUNT-RECORD.                       INKUT112
