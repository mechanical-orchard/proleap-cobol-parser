000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    DPKCS050.                                                 
000300 AUTHOR.        CHERI MASTEL - COMPUWARE.                                 
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  05-01-96.                                                 
000600 DATE-COMPILED.                                                           
000700*----------------------------------------------------------------*        
000800*    Z050 - PHYSICAL UNIT CRITERIA SPECIFICATION                 *        
000900*                                                                *        
001000*    THIS PROGRAM ALLOWS THE USER TO ENTER DATA TO BE USED BY    *        
001100*    SUBSEQUENT PROGRAMS TO CREATE A LIST OR TO BRING UP A       *        
001200*    DETAIL SCREEN.                                              *        
001300*    * THE STORE NUMBER, IF ENTERED, IS EDITTED TO BE A VALID    *        
001400*      NUMERIC.                                                  *        
001500*    * THE IP ADDRESS, IF ENTERED, IS EDITTED FOR EACH OCTET TO  *        
001600*      BE A VALID NUMERIC, WITH VALUES 000-255 INCLUSIVE.        *        
001700*    * ALL OTHER EDITS ARE DONE IN THE DESTINATION PROGRAMS      *        
001800*      WHEN THE 'GO-TO' TYPE FUNCTION IS USED.                   *        
      *----------------------------------------------------------------*        
      *    12/22/03  TKMA464   MADE CHANGES FOR EXPANDING LOCATION     *        
      *                        FROM 3 TO 5 DIGITS.                     *        
001900*----------------------------------------------------------------*        
262023*   CHANGED 09/28/2021    BY JIM HUNTER      WR: CHG0262023      *        
262023*   CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE  *        
262023*   MAKING THE CALL DYNAMIC VS STATIC                            *        
262023*----------------------------------------------------------------*        
002000                                                                          
002100 ENVIRONMENT DIVISION.                                                    
002200                                                                          
002300 DATA DIVISION.                                                           
002400                                                                          
002500 WORKING-STORAGE SECTION.                                                 
002600*                                                                         
002700 01  PC-PROGRAM-CONSTANTS.                                                
002800     05  PC-CURRENT-MAP-NAME       PIC  X(08)  VALUE  'DP050A  '.         
002900     05  PC-CURRENT-MAPSET-NAME    PIC  X(08)  VALUE  'DPKM050 '.         
003000     05  PC-CURRENT-PROGRAM-NAME   PIC  X(08)  VALUE  'DPKCS050'.         
003100     05  PC-TSYMSG-NUMBERS.                                               
003200         10  PC-TSYMSG-00005       PIC  9(05)  VALUE  00005.              
003300         10  PC-TSYMSG-00008       PIC  9(05)  VALUE  00008.              
003400         10  PC-TSYMSG-00017       PIC  9(05)  VALUE  00017.              
003500         10  PC-TSYMSG-00224       PIC  9(05)  VALUE  00224.              
003600         10  PC-TSYMSG-01202       PIC  9(05)  VALUE  01202.              
003700         10  PC-TSYMSG-01285       PIC  9(05)  VALUE  01285.              
003800     05  PC-VALID-OCTET-MIN        PIC  X(03)  VALUE  '000'.              
003900     05  PC-VALID-OCTET-MAX        PIC  X(03)  VALUE  '255'.              
004000     05  PC-DOT                    PIC  X(01)  VALUE  '.'.                
004100     05  PC-OPERATING-COMPANY      PIC  X(02)  VALUE  '03'.               
004200     05  PC-IP-ADDR-NOT-ENTERED.                                          
004300         10  PC-ZEROS-IP-ADDR      PIC  X(15)  VALUE                      
004400                                               '000.000.000.000'.         
004500         10  PC-ZERO-IP-ADDR       PIC  X(15)  VALUE                      
004600                                               '  0.  0.  0.  0'.         
004700         10  PC-SPACES-IP-ADDR     PIC  X(15)  VALUE SPACES.              
004800         10  PC-SPACE-IP-ADDR      PIC  X(15)  VALUE                      
004900                                               '   .   .   .   '.         
005000*                                                                         
005100 01  PS-PROGRAM-SWITCHES.                                                 
005200     05  PS-ERROR-SW               PIC  X(01)  VALUE  'N'.                
005300         88  PS-ERROR                          VALUE  'Y'.                
005400         88  PS-NO-ERROR                       VALUE  'N'.                
005500*                                                                         
005600 01  PM-PROGRAM-MESSAGES.                                                 
005700     05  PM-ABEND-INFO-A.                                                 
005800         10  FILLER                PIC X(15)   VALUE                      
005900                                   'DP020-SRC-AID: '.                     
006000         10  PM-A-SRC-AID          PIC X(02)   VALUE  SPACE.              
006100         10  FILLER                PIC X(19)   VALUE                      
006200                                   '. DP020-FK-ACTION: '.                 
006300         10  PM-A-FK-ACTION        PIC X(01)   VALUE  SPACE.              
006400         10  FILLER                PIC X(22)   VALUE                      
006500                                   '. DP020-FK-DISPN-IND: '.              
006600         10  PM-A-FK-DISPN         PIC X(01)   VALUE  SPACE.              
006700         10  FILLER                PIC X(01)   VALUE '.'.                 
006800         10  FILLER                PIC X(19)   VALUE SPACE.               
006900                                                                          
007000*----------------------------------------------------------------*        
007100*    MAP LAYOUT                                                  *        
007200*----------------------------------------------------------------*        
007300     COPY DPKM050.                                                        
007400                                                                          
007500*----------------------------------------------------------------*        
007600*    ATTRIBUTE SETTINGS COPYBOOK.                                *        
007700*----------------------------------------------------------------*        
007800     COPY DPWS015.                                                        
007900                                                                          
008000*----------------------------------------------------------------*        
008100*    FUNCTION KEYS COPYBOOK                                      *        
008200*----------------------------------------------------------------*        
008300     COPY DPWS016.                                                        
008400                                                                          
008500*----------------------------------------------------------------*        
008600** THE CALLING PARAMETER LIST (DPWS010) FOR THE NUMERIC EDIT    **        
008700** SUB-ROUTINE (DPKUT100).                                      **        
008800*----------------------------------------------------------------*        
008900     COPY DPWS010I.                                                       
009000                                                                          
      *----------------------------------------------------------------*        
      *    PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.           *        
      *----------------------------------------------------------------*        
                                                                                
           COPY DPWS030.                                                        
           COPY DPWS930.                                                        
009600                                                                          
009800*****************************************************************         
009900*    ARCHITECTURE COMMAREAS: 4072 MAX LENGTH TOTAL                        
010000*    * GLOBAL COMMAREA:      1265 BYTES, COPY BOOK DPWS020                
010100*    * INTER-APPLICATION:     200 BYTES, TEMPORARY!                       
010200*    * APPLICATION SPECIFIC: 2865 BYTES MAX                               
010300*****************************************************************         
010400                                                                          
010500*****************************************************************         
010600*    STANDARD COMMAREA.                                                   
010700*****************************************************************         
010800     COPY DPWS020.                                                        
010900     05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                        
011000                                                                          
011100*****************************************************************         
011200*    INTER-APPLICATION COMMAREA.                                          
011300*****************************************************************         
011400         10  INTER-APPL-COMM-AREA PIC X(200).                             
011500     COPY DPWS250.                                                        
011600                                                                          
011700*****************************************************************         
011800*    SPECIFIC COMMMAREA FOR DPKCS050.                                     
011900*****************************************************************         
012000         10  ASC-SPECIFIC-COMMAREA.                                       
012100             15  ASC-LOCATION.                                            
012200                 20  ASC-STORE.                                           
012300                     25  ASC-STORE-N          PIC  9(05).                 
012400             15  ASC-VTAM.                                                
012500                 20  ASC-LINE                 PIC  X(02).                 
012600                 20  ASC-PHYSICAL-UNIT        PIC  X(02).                 
012700             15  ASC-IP-ADDR.                                             
012800                 20  ASC-IP-ADDR-OCTET1.                                  
012900                     25  ASC-IP-ADDR-OCTET1-N PIC  9(03).                 
013000                 20  ASC-IP-ADDR-DOT1         PIC  X(01).                 
013100                 20  ASC-IP-ADDR-OCTET2.                                  
013200                     25  ASC-IP-ADDR-OCTET2-N PIC  9(03).                 
013300                 20  ASC-IP-ADDR-DOT2         PIC  X(01).                 
013400                 20  ASC-IP-ADDR-OCTET3.                                  
013500                     25  ASC-IP-ADDR-OCTET3-N PIC  9(03).                 
013600                 20  ASC-IP-ADDR-DOT3         PIC  X(01).                 
013700                 20  ASC-IP-ADDR-OCTET4.                                  
013800                     25  ASC-IP-ADDR-OCTET4-N PIC  9(03).                 
014000 EJECT                                                                    
014100                                                                          
014200*----------------------------------------------------------------*        
014300*    ABEND PROCESSING COPYBOOK.                                  *        
014400*----------------------------------------------------------------*        
014500     COPY DPWS013.                                                        
014600                                                                          
014700 LINKAGE SECTION.                                                         
014800                                                                          
014900 01  DFHCOMMAREA.                                                         
015000     05  FILLER                         OCCURS  1 TO 4072 TIMES           
015100                                        DEPENDING ON EIBCALEN.            
015200         10  FILLER                     PIC X.                            
015300                                                                          
015400                                                                          
015500 PROCEDURE DIVISION.                                                      
015600*****************************************************************         
015700** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. **         
015800** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  **         
015900** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            **         
016000**                                                             **         
016100** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  **         
016200** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       **         
016300*****************************************************************         
016400 0000-MAIN-MODULE.                                                        
016500     INITIALIZE DP030-CICS-API-FIELDS.                                    
016600     MOVE +1                       TO DP030-NUMBER-OF-MAPS.               
016700     MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.                  
016800     MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).                 
016900     SET DP030-RECEIVE-APPL-MAP    TO TRUE.                               
017000     MOVE LENGTH OF DP050AI        TO DP030-MAP-LENGTH (1).               
017100     MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).             
017200     PERFORM 0001-CALL-CICS-ARCH-API.                                     
017300*                                                                         
017400     PERFORM 1000-CONTROL-PROCESSING                                      
017500*                                                                         
017600     MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).             
017700     PERFORM 0001-CALL-CICS-ARCH-API.                                     
017800                                                                          
017900 0001-CALL-CICS-ARCH-API.                                                 
018000     CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
018100                                    DFHCOMMAREA                           
018200                                    DP030-CICS-API-FIELDS                 
018300                                    DP020-STANDARD-COMMAREA               
018400                                    DP050AI.                              
018500*                                                                         
018600     IF  DP030-RC-CALL-SUCCESSFUL                                         
018700         CONTINUE                                                         
018800     ELSE                                                                 
018900         SET DP013-NO-ROLLBACK                                            
019000             DP013-XCTL-DISPLAY-RESTART                                   
019100             DP013-CICS-ABEND      TO TRUE                                
019200         MOVE 'BEFORE 0000-MAIN-MODULE'                                   
019300                                   TO DP013-PARAGRAPH                     
019400         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
019500-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
019600         MOVE DP030-RETURN-CODE                                           
019700                                   TO DP013-MESSAGE-TEXT (3)              
019800         PERFORM DP013-0000-PROCESS-ABEND                                 
019900     END-IF.                                                              
020000 EJECT                                                                    
020100*----------------------------------------------------------------*        
020200*    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *        
020300*    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *        
020400*----------------------------------------------------------------*        
020500                                                                          
020600 1000-CONTROL-PROCESSING.                                                 
020700                                                                          
020800     EVALUATE TRUE                                                        
020900         WHEN DP020-NEXT-ACT-INITIAL                                      
021000*            *****************************************************        
021100*            * NEXT ACT: INITIAL                                 *        
021200*            *****************************************************        
021300             INITIALIZE ASC-SPECIFIC-COMMAREA                             
021400             MOVE SPACES TO ASC-STORE                                     
                                  ASC-IP-ADDR-OCTET1                            
                                  ASC-IP-ADDR-OCTET2                            
                                  ASC-IP-ADDR-OCTET3                            
                                  ASC-IP-ADDR-OCTET4                            
021500             PERFORM 4000-BUILD-INITIAL-PANEL                             
021600                                                                          
021700         WHEN DP020-NEXT-ACT-READ-MAP                                     
021800*            *****************************************************        
021900*            * NEXT ACT: READ MAP                                *        
022000*            *****************************************************        
022100             PERFORM 2000-FKEY-PROCESSING                                 
022200                                                                          
022300         WHEN DP020-NEXT-ACT-RETURN                                       
022400*            *****************************************************        
022500*            * NEXT ACT: <RETURN>                                *        
022600*            *****************************************************        
022700*            PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
022800             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
022900             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
023000                                                                          
023100         WHEN OTHER                                                       
023200*            *****************************************************        
023300*            * NEXT ACT: OTHER (UNDEFINED)                       *        
023400*            *****************************************************        
023500             SET DP013-LOGIC-ABEND                                        
023600                 DP013-NO-ROLLBACK TO TRUE                                
023700             MOVE '1000-CONTROL-PROCESSING'                               
023800                                   TO  DP013-PARAGRAPH                    
023900             MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'           
024000                                   TO  DP013-MESSAGE-TEXT(1)              
024100             MOVE DP020-NEXT-APPL-ACTIVITY                                
024200                                   TO DP013-MESSAGE-TEXT(2)               
024300             PERFORM DP013-0000-PROCESS-ABEND                             
024400     END-EVALUATE.                                                        
024500                                                                          
024600*----------------------------------------------------------------*        
024700* FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*        
024800* SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*        
024900* EDITS.                                                         *        
025000*----------------------------------------------------------------*        
025100                                                                          
025200 2000-FKEY-PROCESSING.                                                    
025300                                                                          
025400     EVALUATE TRUE                                                        
025500                                                                          
025600         WHEN DP020-SRC-AID = DP016-CLEAR                                 
025700*            *****************************************************        
025800*            * PFKEY ACTION: CLEAR                         <ESC> *        
025900*            *   <CLEAR> ("ESC" KEY)                             *      12
026000*            *                                                   *      12
026100*            * PRESENT SCREEN AS IT WAS AT THE TIME OF THE LAST  *      12
026200*            * <ENTER>, INCLUDING ANY ERROR MESSAGES.            *      12
026300*            *****************************************************        
026400             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
026500             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
026600                                                                          
026700         WHEN DP020-FK-REFRESH(DP020-SRC-AID)                             
026800*            *****************************************************        
026900*            * PFKEY ACTION: <REFRESH>                           *        
027000*            * PRESENT SCREEN AS FOLLOWS:                        *        
027100*            * - BRING BACK WHAT WAS LAST SAVED.                 *        
027200*            * - SHOW SCREEN AS ORIGINALLY PRESENTED (BLANK OR   *        
027300*            *   WITH DATA FROM DATABASE).                       *        
027400*            *****************************************************        
027500             INITIALIZE ASC-SPECIFIC-COMMAREA                             
027600             MOVE SPACES TO ASC-STORE                                     
                                  ASC-IP-ADDR-OCTET1                            
                                  ASC-IP-ADDR-OCTET2                            
                                  ASC-IP-ADDR-OCTET3                            
                                  ASC-IP-ADDR-OCTET4                            
027700             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
027800             PERFORM 4000-BUILD-INITIAL-PANEL                             
027900             PERFORM 3200-RESET-ATTRIBUTES                                
028000                                                                          
028100         WHEN OTHER                                                       
028200*            *****************************************************        
028300*            * PFKEY ACTION: OTHER (APPLICATION SPECIFIC)        *        
028400*            *****************************************************        
028500             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
028600             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
028700             IF  PS-NO-ERROR                                              
028800                 PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     
028900                 PERFORM 4000-BUILD-INITIAL-PANEL                         
029000                 SET DP030-CONTINUE-GO TO TRUE                            
029100                 PERFORM 2100-CHECK-FUNCTION-KEY                          
029200             ELSE                                                         
029300                 SET DP030-OVERRIDE-GO TO TRUE                            
029400             END-IF                                                       
029500     END-EVALUATE.                                                        
029600                                                                          
029700******************************************************************        
029800** ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED     **        
029900** FIRST.  NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED **        
030000** FROM THE CICS ARCHITECTURE API.                              **        
030100******************************************************************        
030200 2100-CHECK-FUNCTION-KEY.                                                 
030300     EVALUATE TRUE                                                        
030400                                                                          
030500         WHEN DP020-FK-DISPN-GO (DP020-SRC-AID)                           
030600*            *****************************************************        
030700*            * APPLICATION-SPECIFIC PFKEY: LOCAL GO-TO           *        
030800*            * LOCAL-FUNC-01 = ADD                               *        
030900*            * LOCAL-FUNC-02 = INQUIRY                           *        
031000*            * LOCAL-FUNC-03 = UPDATE                            *        
031100*            * LOCAL-FUNC-04 = DELETE                            *        
031200*            * LOCAL-FUNC-05 = LST PHYU                          *        
031300*            *****************************************************        
031400             PERFORM 2110-PREPARE-INTER-APPL-COMM                         
031500                                                                          
031600         WHEN DP020-SRC-AID = DP016-ENTER                                 
031700*            *****************************************************        
031800*            * APPLICATION-SPECIFIC PFKEY: <ENTER>               *        
031900*            * EDIT THE DATA AND PRESENT IT ON THE SCREEN        *        
032000*            *****************************************************        
032100             PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                         
032200             PERFORM 3000-EDIT-DATA-IN-COMMAREA                           
032300             PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                         
032400                                                                          
032500         WHEN OTHER                                                       
032600*            *****************************************************        
032700*            * APPLICATION-SPECIFIC PFKEY: OTHER (UNDEFINED)     *        
032800*            *****************************************************        
032900             SET DP013-NO-ROLLBACK                                        
033000                 DP013-XCTL-DISPLAY-RESTART                               
033100                 DP013-CICS-ABEND  TO TRUE                                
033200             MOVE '2100-CHECK-FUNCTION-KEY'                               
033300                                   TO DP013-PARAGRAPH                     
033400             MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'              
033500                                   TO DP013-MESSAGE-TEXT (1)              
033600             MOVE 'OR CODE MISSING FROM THIS PROGRAM TO PROCESS:'         
033700                                   TO DP013-MESSAGE-TEXT (2)              
033800             MOVE DP020-SRC-AID    TO PM-A-SRC-AID                        
033900             MOVE DP020-FK-ACTION (DP020-SRC-AID)                         
034000                                   TO PM-A-FK-ACTION                      
034100             MOVE DP020-FK-DISPN-IND (DP020-SRC-AID)                      
034200                                   TO PM-A-FK-DISPN                       
034300             MOVE PM-ABEND-INFO-A  TO DP013-MESSAGE-TEXT (3)              
034400             MOVE 'REVIEW PFKEYS SETUP FOR THIS MNEMONIC.'                
034500                                   TO DP013-MESSAGE-TEXT (4)              
034600             PERFORM DP013-0000-PROCESS-ABEND                             
034700                                                                          
034800     END-EVALUATE.                                                        
034900                                                                          
035000******************************************************************        
035100** MOVE DATA NECESSARY TO THE DESTINATION PROGRAM(S) INTO THE   **        
035200** INTER-APPLICATION COMMAREA.                                  **        
035300******************************************************************        
035400 2110-PREPARE-INTER-APPL-COMM.                                            
035500     INITIALIZE DP250-INTER-APPL-COMMAREA.                                
035600     MOVE ASC-STORE-N              TO DP250-LOCATION-NBR.                 
035700     MOVE ASC-LINE                 TO DP250-LINE.                         
035800     MOVE ASC-PHYSICAL-UNIT        TO DP250-PHYSICAL-UNIT.                
035900     MOVE ASC-IP-ADDR-OCTET1       TO DP250-IP-ADDR-OCTET1.               
036000     MOVE PC-DOT                   TO DP250-IP-ADDR-DOT1.                 
036100     MOVE ASC-IP-ADDR-OCTET2       TO DP250-IP-ADDR-OCTET2.               
036200     MOVE PC-DOT                   TO DP250-IP-ADDR-DOT2.                 
036300     MOVE ASC-IP-ADDR-OCTET3       TO DP250-IP-ADDR-OCTET3.               
036400     MOVE PC-DOT                   TO DP250-IP-ADDR-DOT3.                 
036500     MOVE ASC-IP-ADDR-OCTET4       TO DP250-IP-ADDR-OCTET4.               
036600 EJECT                                                                    
036700*----------------------------------------------------------------*        
036800* MOVE DATA ENTERED ON THE SCREEN INTO THEIR RESPECTIVE FIELDS IN*        
036900* THE APPLICATION-SPECIFIC COMMAREA.  ALL EDITS ARE DONE IN THE  *        
037000* APPLICATION-SPECIFIC COMMAREA, NOT ON THE SCREEN.              *        
037100*----------------------------------------------------------------*        
037200                                                                          
037300 2200-MOVE-SCREEN-TO-COMMAREA.                                            
037400     IF  ASTORL > ZERO                                                    
037500         MOVE ASTORI               TO ASC-STORE                           
037600     ELSE                                                                 
037700         IF  ASTORF = DP015-ERASE-EOF                                     
037800             MOVE SPACE            TO ASC-STORE                           
037900         END-IF                                                           
038000     END-IF.                                                              
038100                                                                          
038200     IF  ALINEL > ZERO                                                    
038300         MOVE ALINEI               TO ASC-LINE                            
038400     ELSE                                                                 
038500         IF  ALINEF = DP015-ERASE-EOF                                     
038600             MOVE SPACE            TO ASC-LINE                            
038700         END-IF                                                           
038800     END-IF.                                                              
038900                                                                          
039000     IF  APHYUL > ZERO                                                    
039100         MOVE APHYUI               TO ASC-PHYSICAL-UNIT                   
039200     ELSE                                                                 
039300         IF  APHYUF = DP015-ERASE-EOF                                     
039400             MOVE SPACE            TO ASC-PHYSICAL-UNIT                   
039500         END-IF                                                           
039600     END-IF.                                                              
039700                                                                          
039800     IF  AIP1L > ZERO                                                     
039900         MOVE AIP1I                TO ASC-IP-ADDR-OCTET1                  
040000     ELSE                                                                 
040100         IF  AIP1F = DP015-ERASE-EOF                                      
040200             MOVE SPACE            TO ASC-IP-ADDR-OCTET1                  
040300         END-IF                                                           
040400     END-IF.                                                              
040500                                                                          
040600     IF  AIP2L > ZERO                                                     
040700         MOVE AIP2I                TO ASC-IP-ADDR-OCTET2                  
040800     ELSE                                                                 
040900         IF  AIP2F = DP015-ERASE-EOF                                      
041000             MOVE SPACE            TO ASC-IP-ADDR-OCTET2                  
041100         END-IF                                                           
041200     END-IF.                                                              
041300                                                                          
041400     IF  AIP3L > ZERO                                                     
041500         MOVE AIP3I                TO ASC-IP-ADDR-OCTET3                  
041600     ELSE                                                                 
041700         IF  AIP3F = DP015-ERASE-EOF                                      
041800             MOVE SPACE            TO ASC-IP-ADDR-OCTET3                  
041900         END-IF                                                           
042000     END-IF.                                                              
042100                                                                          
042200     IF  AIP4L > ZERO                                                     
042300         MOVE AIP4I                TO ASC-IP-ADDR-OCTET4                  
042400     ELSE                                                                 
042500         IF  AIP4F = DP015-ERASE-EOF                                      
042600             MOVE SPACE            TO ASC-IP-ADDR-OCTET4                  
042700         END-IF                                                           
042800     END-IF.                                                              
042900                                                                          
043000*----------------------------------------------------------------*        
043100* ENSURE THE DATA IS CORRECT.  NOTE THAT IN A CRITERIA SPECIFI-  *        
043200* CATION SCREEN ONLY MINIMAL EDITS ARE DONE.  EDITS RELATED TO   *        
043300* A SPECIFIC SCREEN THAT THE USER IS GOING TO (LOCAL-GO) ARE DONE*        
043400* IN THE DESTINATION PROGRAM, NOT HERE.                          *        
043500*----------------------------------------------------------------*        
043600                                                                          
043700 3000-EDIT-DATA-IN-COMMAREA.                                              
043800     PERFORM 3200-RESET-ATTRIBUTES.                                       
043900     PERFORM 3220-INDIVIDUAL-FIELD-EDITS.                                 
044000*                                                                         
044100     IF  PS-NO-ERROR                                                      
044200         PERFORM 3230-CROSSCHECK-FIELD-EDITS                              
044300     END-IF.                                                              
044400*                                                                         
044500     IF  PS-NO-ERROR                                                      
044600         MOVE -1                   TO ASTORL                              
044700         SET DP030-SET-CURSOR-APPL-1                                      
044800                                   TO TRUE                                
044900     END-IF.                                                              
045000                                                                          
045100******************************************************************        
045200** MOVE THE DEFAULT ATTRIBUTES TO ALL OF THE OPEN FIELDS ON THE **        
045300** MAP.                                                         **        
045400******************************************************************        
045500 3200-RESET-ATTRIBUTES.                                                   
045600     MOVE DP015-UNP-ALP-NOR-OFF    TO ASTORA                              
045700                                      ALINEA                              
045800                                      APHYUA                              
045900                                      AIP1A                               
046000                                      AIP2A                               
046100                                      AIP3A                               
046200                                      AIP4A.                              
046300     MOVE DP015-GREEN              TO ASTORC                              
046400                                      ALINEC                              
046500                                      APHYUC                              
046600                                      AIP1C                               
046700                                      AIP2C                               
046800                                      AIP3C                               
046900                                      AIP4C.                              
047000     MOVE DP015-UNDERLINE          TO ASTORH                              
047100                                      ALINEH                              
047200                                      APHYUH                              
047300                                      AIP1H                               
047400                                      AIP2H                               
047500                                      AIP3H                               
047600                                      AIP4H.                              
047700                                                                          
047800*----------------------------------------------------------------*        
047900*    BASIC FIELD EDITS                                           *        
048000*    *****************                                           *        
048100*                                                                *        
048200*    FIELDS ARE EDITED IN REVERSE ORDER OF HOW THEY APPEAR ON    *        
048300*    THE SCREEN.  IN THIS WAY, FOR EACH ERROR ENCOUNTERED, THE   *        
048400*    ERROR MESSAGE AND CURSOR POSITIONING WILL OVERLAY ANY       *        
048500*    PREVIOUS ERROR MESSAGE AND CURSOR POSITIONING, UNTIL THE    *        
048600*    ERROR MESSAGE AND CURSOR POSITIONING ARE SET FOR THE FIRST  *        
048700*    ERROR ENCOUNTERED ON THE SCREEN.                            *        
048800*                                                                *        
048900*    - IP ADDRESS:                                               *        
049000*      04) IF ENTERED,                                           *        
049100*      - IP ADDRESS OCTETS MUST BE NUMERIC AND EACH OCTET'S      *        
049200*        VALUE MUST BE 000-255 INCLUSIVE.                        *        
049300*                                                                *        
049400*    - UNIT:                                                     *        
049500*      03) NO EDITS AT THIS TIME.                                *        
049600*                                                                *        
049700*    - LINE:                                                     *        
049800*      02) NO EDITS AT THIS TIME.                                *        
049900*                                                                *        
050000*    - STORE:                                                    *        
050100*      01) IF ENTERED, MUST BE VALID DIGITS, 000-999.            *        
050200*                                                                *        
050300*----------------------------------------------------------------*        
050400 3220-INDIVIDUAL-FIELD-EDITS.                                             
050500                                                                          
050600*    *************************************************************        
050700*    * IP ADDRESS RANGE: END OF RANGE, OCTET4                    *        
050800*    *************************************************************        
050900     IF  ASC-IP-ADDR-OCTET4 = SPACE                                       
051000         CONTINUE                                                         
051100     ELSE                                                                 
051200         MOVE ASC-IP-ADDR-OCTET4 TO DP010I-UNEDITED-FIELD                 
051300         MOVE LENGTH OF ASC-IP-ADDR-OCTET4                                
051400                                  TO DP010I-MAXIMUM-DIGITS                
051500         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
051600         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
051700                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
051900         IF  DP010I-ERROR-DETECTED                                        
052000             SET PS-ERROR         TO TRUE                                 
052100             SET DP020-MSG-FATAL  TO TRUE                                 
052200*            ---- MUST BE NUMERIC                                         
052300             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
052400             MOVE DP015-UNP-ALP-BRT-OFF                                   
052500                                  TO AIP4A                                
052600             MOVE DP015-RED       TO AIP4C                                
052700             MOVE DP015-REVERSE   TO AIP4H                                
052800             MOVE -1              TO AIP4L                                
052900             SET DP030-SET-CURSOR-APPL-1                                  
053000                                  TO TRUE                                 
053100         ELSE                                                             
053200             MOVE DP010I-NUMERIC-FIELD                                    
053300                                  TO ASC-IP-ADDR-OCTET4-N                 
053400             IF ASC-IP-ADDR-OCTET4 < PC-VALID-OCTET-MIN                   
053500             OR ASC-IP-ADDR-OCTET4 > PC-VALID-OCTET-MAX                   
053600*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
053700                 SET PS-ERROR         TO TRUE                             
053800                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
053900                 MOVE DP015-UNP-ALP-BRT-OFF                               
054000                                      TO AIP4A                            
054100                 MOVE DP015-RED       TO AIP4C                            
054200                 MOVE DP015-REVERSE   TO AIP4H                            
054300                 MOVE -1              TO AIP4L                            
054400                 SET DP030-SET-CURSOR-APPL-1                              
054500                                      TO TRUE                             
054600             ELSE                                                         
054700                 CONTINUE                                                 
054800         END-IF                                                           
054900     END-IF.                                                              
055000                                                                          
055100                                                                          
055200*    *************************************************************        
055300*    * IP ADDRESS RANGE: END OF RANGE, OCTET3                    *        
055400*    *************************************************************        
055500     IF  ASC-IP-ADDR-OCTET3 = SPACE                                       
055600         CONTINUE                                                         
055700     ELSE                                                                 
055800         MOVE ASC-IP-ADDR-OCTET3 TO DP010I-UNEDITED-FIELD                 
055900         MOVE LENGTH OF ASC-IP-ADDR-OCTET3                                
056000                                  TO DP010I-MAXIMUM-DIGITS                
056100         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
056200         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
056300                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
056500         IF  DP010I-ERROR-DETECTED                                        
056600             SET PS-ERROR         TO TRUE                                 
056700             SET DP020-MSG-FATAL  TO TRUE                                 
056800*            ---- MUST BE NUMERIC                                         
056900             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
057000             MOVE DP015-UNP-ALP-BRT-OFF                                   
057100                                  TO AIP3A                                
057200             MOVE DP015-RED       TO AIP3C                                
057300             MOVE DP015-REVERSE   TO AIP3H                                
057400             MOVE -1              TO AIP3L                                
057500             SET DP030-SET-CURSOR-APPL-1                                  
057600                                  TO TRUE                                 
057700         ELSE                                                             
057800             MOVE DP010I-NUMERIC-FIELD                                    
057900                                  TO ASC-IP-ADDR-OCTET3-N                 
058000             IF ASC-IP-ADDR-OCTET3 < PC-VALID-OCTET-MIN                   
058100             OR ASC-IP-ADDR-OCTET3 > PC-VALID-OCTET-MAX                   
058200                 SET PS-ERROR         TO TRUE                             
058300                 SET DP020-MSG-FATAL  TO TRUE                             
058400*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
058500                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
058600                 MOVE DP015-UNP-ALP-BRT-OFF                               
058700                                      TO AIP3A                            
058800                 MOVE DP015-RED       TO AIP3C                            
058900                 MOVE DP015-REVERSE   TO AIP3H                            
059000                 MOVE -1              TO AIP3L                            
059100                 SET DP030-SET-CURSOR-APPL-1                              
059200                                      TO TRUE                             
059300             ELSE                                                         
059400                 CONTINUE                                                 
059500         END-IF                                                           
059600     END-IF.                                                              
059700                                                                          
059800                                                                          
059900*    *************************************************************        
060000*    * IP ADDRESS RANGE: END OF RANGE, OCTET2                    *        
060100*    *************************************************************        
060200     IF  ASC-IP-ADDR-OCTET2 = SPACE                                       
060300         CONTINUE                                                         
060400     ELSE                                                                 
060500         MOVE ASC-IP-ADDR-OCTET2 TO DP010I-UNEDITED-FIELD                 
060600         MOVE LENGTH OF ASC-IP-ADDR-OCTET2                                
060700                                  TO DP010I-MAXIMUM-DIGITS                
060800         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
060900         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
061000                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
061200         IF  DP010I-ERROR-DETECTED                                        
061300             SET PS-ERROR         TO TRUE                                 
061400             SET DP020-MSG-FATAL  TO TRUE                                 
061500*            ---- MUST BE NUMERIC                                         
061600             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
061700             MOVE DP015-UNP-ALP-BRT-OFF                                   
061800                                  TO AIP2A                                
061900             MOVE DP015-RED       TO AIP2C                                
062000             MOVE DP015-REVERSE   TO AIP2H                                
062100             MOVE -1              TO AIP2L                                
062200             SET DP030-SET-CURSOR-APPL-1                                  
062300                                  TO TRUE                                 
062400         ELSE                                                             
062500             MOVE DP010I-NUMERIC-FIELD                                    
062600                                  TO ASC-IP-ADDR-OCTET2-N                 
062700             IF ASC-IP-ADDR-OCTET2 < PC-VALID-OCTET-MIN                   
062800             OR ASC-IP-ADDR-OCTET2 > PC-VALID-OCTET-MAX                   
062900                 SET PS-ERROR         TO TRUE                             
063000                 SET DP020-MSG-FATAL  TO TRUE                             
063100*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
063200                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
063300                 MOVE DP015-UNP-ALP-BRT-OFF                               
063400                                      TO AIP2A                            
063500                 MOVE DP015-RED       TO AIP2C                            
063600                 MOVE DP015-REVERSE   TO AIP2H                            
063700                 MOVE -1              TO AIP2L                            
063800                 SET DP030-SET-CURSOR-APPL-1                              
063900                                      TO TRUE                             
064000             ELSE                                                         
064100                 CONTINUE                                                 
064200         END-IF                                                           
064300     END-IF.                                                              
064400                                                                          
064500                                                                          
064700*    *************************************************************        
064800*    * IP ADDRESS RANGE: END OF RANGE, OCTET1                    *        
064900*    *************************************************************        
065000     IF  ASC-IP-ADDR-OCTET1 = SPACE                                       
065100         CONTINUE                                                         
065200     ELSE                                                                 
065300         MOVE ASC-IP-ADDR-OCTET1 TO DP010I-UNEDITED-FIELD                 
065400         MOVE LENGTH OF ASC-IP-ADDR-OCTET1                                
065500                                  TO DP010I-MAXIMUM-DIGITS                
065600         MOVE ZERO                TO DP010I-MAXIMUM-DECIMALS              
065700         SET DP010I-NEGATIVE-NOT-ALLOWED                                  
065800                                  TO TRUE                                 
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
066000         IF  DP010I-ERROR-DETECTED                                        
066100             SET PS-ERROR         TO TRUE                                 
066200             SET DP020-MSG-FATAL  TO TRUE                                 
066300*            ---- MUST BE NUMERIC                                         
066400             MOVE PC-TSYMSG-00008 TO DP020-MSG-NUMBER                     
066500             MOVE DP015-UNP-ALP-BRT-OFF                                   
066600                                  TO AIP1A                                
066700             MOVE DP015-RED       TO AIP1C                                
066800             MOVE DP015-REVERSE   TO AIP1H                                
066900             MOVE -1              TO AIP1L                                
067000             SET DP030-SET-CURSOR-APPL-1                                  
067100                                  TO TRUE                                 
067200         ELSE                                                             
067300             MOVE DP010I-NUMERIC-FIELD                                    
067400                                  TO ASC-IP-ADDR-OCTET1-N                 
067500             IF ASC-IP-ADDR-OCTET1 < PC-VALID-OCTET-MIN                   
067600             OR ASC-IP-ADDR-OCTET1 > PC-VALID-OCTET-MAX                   
067700                 SET PS-ERROR         TO TRUE                             
067800                 SET DP020-MSG-FATAL  TO TRUE                             
067900*                ---- MUST BE VALID VALUE, 000 - 255 INCLUSIVE            
068000                 MOVE PC-TSYMSG-01285 TO DP020-MSG-NUMBER                 
068100                 MOVE DP015-UNP-ALP-BRT-OFF                               
068200                                      TO AIP1A                            
068300                 MOVE DP015-RED       TO AIP1C                            
068400                 MOVE DP015-REVERSE   TO AIP1H                            
068500                 MOVE -1              TO AIP1L                            
068600                 SET DP030-SET-CURSOR-APPL-1                              
068700                                      TO TRUE                             
068800             ELSE                                                         
068900                 CONTINUE                                                 
069000         END-IF                                                           
069100     END-IF.                                                              
069200                                                                          
069300                                                                          
069400*----------------------------------------------------------------*        
069500* THIS IS A CROSS-CHECK EDIT, BUT TO THE USER OF THIS SCREEN,    *        
069600* THIS IS PROBABLY PERCEIVED AS ONE FIELD...                     *        
069700* IF ONE OCTET OF AN IP ADDRESS IS ENTERED, ALL OCTETS MUST      *        
069800*    BE ENTERED (IE: THE FULL IP ADDRESS MUST BE ENTERED).       *        
069900*----------------------------------------------------------------*        
070000     IF  ASC-IP-ADDR-OCTET1 = SPACE                                       
070100     AND ASC-IP-ADDR-OCTET2 = SPACE                                       
070200     AND ASC-IP-ADDR-OCTET3 = SPACE                                       
070300     AND ASC-IP-ADDR-OCTET4 = SPACE                                       
070400         CONTINUE                                                         
070500     ELSE                                                                 
070600         IF  ASC-IP-ADDR-OCTET1 = SPACE                                   
070700         OR  ASC-IP-ADDR-OCTET2 = SPACE                                   
070800         OR  ASC-IP-ADDR-OCTET3 = SPACE                                   
070900         OR  ASC-IP-ADDR-OCTET4 = SPACE                                   
071000             SET PS-ERROR         TO TRUE                                 
071100             SET DP020-MSG-FATAL  TO TRUE                                 
071200*            ---- INVALID VALUE                                           
071300             MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                     
071400             MOVE DP015-UNP-ALP-BRT-OFF                                   
071500                                  TO AIP1A                                
071600                                     AIP2A                                
071700                                     AIP3A                                
071800                                     AIP4A                                
071900             MOVE DP015-RED       TO AIP1C                                
072000                                     AIP2C                                
072100                                     AIP3C                                
072200                                     AIP4C                                
072300             MOVE DP015-REVERSE   TO AIP1H                                
072400                                     AIP2H                                
072500                                     AIP3H                                
072600                                     AIP4H                                
072700             SET DP030-SET-CURSOR-APPL-1                                  
072800                                  TO TRUE                                 
072900             IF  ASC-IP-ADDR-OCTET4 = SPACE                               
073000                 MOVE -1          TO AIP4L                                
073100             END-IF                                                       
073200             IF  ASC-IP-ADDR-OCTET3 = SPACE                               
073300                 MOVE -1          TO AIP3L                                
073400             END-IF                                                       
073500             IF  ASC-IP-ADDR-OCTET2 = SPACE                               
073600                 MOVE -1          TO AIP2L                                
073700             END-IF                                                       
073800             IF  ASC-IP-ADDR-OCTET1 = SPACE                               
073900                 MOVE -1          TO AIP1L                                
074000             END-IF                                                       
074100         ELSE                                                             
074200*            *****************************************************        
074300*            * ALL OCTETS POPULATED, SO ADD THE DOTS IN-BETWEEN  *        
074400*            *****************************************************        
074500             MOVE PC-DOT          TO  ASC-IP-ADDR-DOT1                    
074600                                      ASC-IP-ADDR-DOT2                    
074700                                      ASC-IP-ADDR-DOT3                    
074800         END-IF                                                           
074900     END-IF.                                                              
075000                                                                          
075100     IF ASC-IP-ADDR        = PC-ZEROS-IP-ADDR                             
075200     OR ASC-IP-ADDR        = PC-ZERO-IP-ADDR                              
075300     OR ASC-IP-ADDR        = PC-SPACE-IP-ADDR                             
075400         MOVE PC-SPACES-IP-ADDR TO ASC-IP-ADDR.                           
075500                                                                          
075600                                                                          
075700*    *************************************************************        
075800*    * EDIT STORE                                                *        
075900*    *************************************************************        
076000     IF  ASC-STORE > SPACE                                                
076100         MOVE ASC-STORE            TO DP010I-UNEDITED-FIELD               
076200         MOVE LENGTH OF ASC-STORE  TO DP010I-MAXIMUM-DIGITS               
076300         MOVE ZERO                 TO DP010I-MAXIMUM-DECIMALS             
076400         SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE                          
262023         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
262023              USING DP010I-NUMERIC-EDIT-AREA                              
076600         IF  DP010I-ERROR-DETECTED                                        
076700             SET PS-ERROR                                                 
076800                 DP020-MSG-FATAL   TO TRUE                                
076900             MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER                    
077000             MOVE DP015-UNP-ALP-BRT-OFF                                   
077100                                   TO ASTORA                              
077200             MOVE DP015-RED        TO ASTORC                              
077300             MOVE DP015-REVERSE    TO ASTORH                              
077400             MOVE -1               TO ASTORL                              
077500             SET DP030-SET-CURSOR-APPL-1 TO TRUE                          
077600         ELSE                                                             
077700             MOVE DP010I-NUMERIC-FIELD                                    
077800                                   TO ASC-STORE-N                         
077900             MOVE ASC-STORE-N      TO ASTORO                              
078000         END-IF                                                           
078100     END-IF.                                                              
078200                                                                          
078300*----------------------------------------------------------------*        
078400*    CROSS-CHECK EDITS                                           *        
078500*    *****************                                           *        
078600*                                                                *        
078700*    FIELDS ARE EDITED IN REVERSE ORDER OF HOW THEY APPEAR ON    *        
078800*    THE SCREEN.  IN THIS WAY, FOR EACH ERROR ENCOUNTERED, THE   *        
078900*    ERROR MESSAGE AND CURSOR POSITIONING WILL OVERLAY ANY       *        
079000*    PREVIOUS ERROR MESSAGE AND CURSOR POSITIONING, UNTIL THE    *        
079100*    ERROR MESSAGE AND CURSOR POSITIONING ARE SET FOR THE FIRST  *        
079200*    ERROR ENCOUNTERED ON THE SCREEN.                            *        
079300*                                                                *        
079400*    - IP ADDRESS:                                               *        
079500*      * IF ENTERED, VTAM FIELDS (LINE,UNIT) MUST NOT BE ENTERED.*        
079600*      * IF ENTERED, STORE FIELD MUST NOT BE ENTERED.            *        
079700*                                                                *        
079800*    - VTAM FIELDS:                                              *        
079900*      * IF ENTERED, IP ADDRESS MUST NOT BE ENTERED.             *        
080000*      * IF ENTERED, STORE FIELD MUST NOT BE ENTERED.            *        
080100*----------------------------------------------------------------*        
080200 3230-CROSSCHECK-FIELD-EDITS.                                             
080300     IF  ASC-IP-ADDR GREATER THAN SPACE                                   
080400     AND ASC-IP-ADDR NOT EQUAL    PC-SPACE-IP-ADDR                        
080500         IF ASC-PHYSICAL-UNIT GREATER THAN SPACE                          
080600             SET PS-ERROR              TO TRUE                            
080700             SET DP020-MSG-FATAL       TO TRUE                            
080800*            --- CANNOT ENTER BOTH FIELDS                                 
080900             MOVE PC-TSYMSG-01202      TO DP020-MSG-NUMBER                
081000             MOVE DP015-UNP-ALP-BRT-OFF                                   
081100                                       TO AIP1A                           
081200                                          AIP2A                           
081300                                          AIP3A                           
081400                                          AIP4A                           
081500                                          APHYUA                          
081600             MOVE DP015-RED            TO AIP1C                           
081700                                          AIP2C                           
081800                                          AIP3C                           
081900                                          AIP4C                           
082000                                          APHYUC                          
082100             MOVE DP015-REVERSE        TO AIP1H                           
082200                                          AIP2H                           
082300                                          AIP3H                           
082400                                          AIP4H                           
082500                                          APHYUH                          
082600             MOVE -1                   TO APHYUL                          
082700             SET DP030-SET-CURSOR-APPL-1                                  
082800                                       TO TRUE                            
082900         END-IF                                                           
083000         IF ASC-LINE          GREATER THAN SPACE                          
083100             SET PS-ERROR              TO TRUE                            
083200             SET DP020-MSG-FATAL       TO TRUE                            
083300*            --- CANNOT ENTER BOTH FIELDS                                 
083400             MOVE PC-TSYMSG-01202      TO DP020-MSG-NUMBER                
083500             MOVE DP015-UNP-ALP-BRT-OFF                                   
083600                                       TO AIP1A                           
083700                                          AIP2A                           
083800                                          AIP3A                           
083900                                          AIP4A                           
084000                                          ALINEA                          
084100             MOVE DP015-RED            TO AIP1C                           
084200                                          AIP2C                           
084300                                          AIP3C                           
084400                                          AIP4C                           
084500                                          ALINEC                          
084600             MOVE DP015-REVERSE        TO AIP1H                           
084700                                          AIP2H                           
084800                                          AIP3H                           
084900                                          AIP4H                           
085000                                          ALINEH                          
085100             MOVE -1                   TO ALINEL                          
085200             SET DP030-SET-CURSOR-APPL-1                                  
085300                                       TO TRUE                            
085400         IF ASC-STORE         GREATER THAN SPACE                          
085500         AND ASC-STORE-N      NOT EQUAL    ZERO                           
085600             SET PS-ERROR              TO TRUE                            
085700             SET DP020-MSG-FATAL       TO TRUE                            
085800*            --- CANNOT ENTER BOTH FIELDS                                 
085900             MOVE PC-TSYMSG-01202      TO DP020-MSG-NUMBER                
086000             MOVE DP015-UNP-ALP-BRT-OFF                                   
086100                                       TO AIP1A                           
086200                                          AIP2A                           
086300                                          AIP3A                           
086400                                          AIP4A                           
086500                                          ASTORA                          
086600             MOVE DP015-RED            TO AIP1C                           
086700                                          AIP2C                           
086800                                          AIP3C                           
086900                                          AIP4C                           
087000                                          ASTORC                          
087100             MOVE DP015-REVERSE        TO AIP1H                           
087200                                          AIP2H                           
087300                                          AIP3H                           
087400                                          AIP4H                           
087500                                          ASTORH                          
087600             MOVE -1                   TO ASTORL                          
087700             SET DP030-SET-CURSOR-APPL-1                                  
087800                                       TO TRUE                            
087900         END-IF                                                           
088000     END-IF.                                                              
088100*                                                                         
088200     IF ASC-STORE   GREATER THAN SPACE                                    
088300     AND ASC-STORE-N NOT EQUAL   ZERO                                     
088400         IF ASC-PHYSICAL-UNIT GREATER THAN SPACE                          
088500             SET PS-ERROR              TO TRUE                            
088600             SET DP020-MSG-FATAL       TO TRUE                            
088700*            --- CANNOT ENTER BOTH FIELDS                                 
088800             MOVE PC-TSYMSG-01202      TO DP020-MSG-NUMBER                
088900             MOVE DP015-UNP-ALP-BRT-OFF                                   
089000                                       TO ASTORA                          
089100                                          APHYUA                          
089200             MOVE DP015-RED            TO ASTORC                          
089300                                          APHYUC                          
089400             MOVE DP015-REVERSE        TO ASTORH                          
089500                                          APHYUH                          
089600             MOVE -1                   TO ASTORL                          
089700             SET DP030-SET-CURSOR-APPL-1                                  
089800                                       TO TRUE                            
089900         END-IF                                                           
090000         IF ASC-LINE          GREATER THAN SPACE                          
090100             SET PS-ERROR              TO TRUE                            
090200             SET DP020-MSG-FATAL       TO TRUE                            
090300*            --- CANNOT ENTER BOTH FIELDS                                 
090400             MOVE PC-TSYMSG-01202      TO DP020-MSG-NUMBER                
090500             MOVE DP015-UNP-ALP-BRT-OFF                                   
090600                                       TO ASTORA                          
090700                                          ALINEA                          
090800             MOVE DP015-RED            TO ASTORC                          
090900                                          ALINEC                          
091000             MOVE DP015-REVERSE        TO ASTORH                          
091100                                          ALINEH                          
091200             MOVE -1                   TO ASTORL                          
091300             SET DP030-SET-CURSOR-APPL-1                                  
091400                                       TO TRUE                            
091500         END-IF                                                           
091600     END-IF.                                                              
091700*                                                                         
091800*----------------------------------------------------------------*        
091900*  POSITION THE CURSOR AT THE APPROPRIATE FIELD.                 *        
092000*  IF THERE IS ANY DATA TO MOVE TO THE SCREEN ON INITIAL, MOVE   *        
092100*  IT HERE.                                                      *        
092200*----------------------------------------------------------------*        
092300 4000-BUILD-INITIAL-PANEL.                                                
092400     SET DP030-SET-CURSOR-APPL-1   TO TRUE                                
092500     MOVE -1                       TO ASTORL.                             
092600                                                                          
092700*    MOVE DP250-LOCATION-NBR       TO ASC-STORE-N.                        
092800*    MOVE DP250-LINE               TO ASC-LINE.                           
092900*    MOVE DP250-PHYSICAL-UNIT      TO ASC-PHYSICAL-UNIT.                  
093000*    MOVE DP250-IP-ADDR-OCTET1     TO ASC-IP-ADDR-OCTET1.                 
093100*    MOVE PC-DOT                   TO ASC-IP-ADDR-DOT1.                   
093200*    MOVE DP250-IP-ADDR-OCTET2     TO ASC-IP-ADDR-OCTET2.                 
093300*    MOVE PC-DOT                   TO ASC-IP-ADDR-DOT2.                   
093400*    MOVE DP250-IP-ADDR-OCTET3     TO ASC-IP-ADDR-OCTET3.                 
093500*    MOVE PC-DOT                   TO ASC-IP-ADDR-DOT3.                   
093600*    MOVE DP250-IP-ADDR-OCTET4     TO ASC-IP-ADDR-OCTET4.                 
093700                                                                          
093800*                                                                         
093900*----------------------------------------------------------------*        
094000*    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *        
094100*----------------------------------------------------------------*        
094200 4400-MOVE-COMMAREA-TO-SCREEN.                                            
094300     MOVE ASC-STORE-N              TO ASTORO.                             
094400     MOVE ASC-LINE                 TO ALINEO.                             
094500     MOVE ASC-PHYSICAL-UNIT        TO APHYUO.                             
094600     MOVE ASC-IP-ADDR-OCTET1       TO AIP1O.                              
094700     MOVE ASC-IP-ADDR-OCTET2       TO AIP2O.                              
094800     MOVE ASC-IP-ADDR-OCTET3       TO AIP3O.                              
094900     MOVE ASC-IP-ADDR-OCTET4       TO AIP4O.                              
095000                                                                          
095100*----------------------------------------------------------------*        
095200*    ABEND PROCESSOR MODULE                                      *        
095300*----------------------------------------------------------------*        
095400     COPY DPPD013.                                                        
