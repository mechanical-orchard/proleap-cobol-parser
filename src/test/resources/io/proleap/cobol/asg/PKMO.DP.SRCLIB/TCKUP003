      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
                                                                                
       PROGRAM-ID.             TCKUP003.                                        
       AUTHOR.                 CHRISTOPHER CLARK.                               
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN            FEBRUARY 2002.                                   
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      *                                                               *         
      *  THIS PROGRAM UPDATES THE TEPCRD TABLE AND MARKS ALL THE DEBIT*         
      *  TRANSACTIONS TO A 'P' FROM A 'U'.                            *         
      *                                                               *         
      *   INPUT: INP01 - CONTAINS DEBIT CARD TRANSACTIONS FOR THE PAST*         
      *                  TWO DAYS SORTED BY STORE NUMBER, DATE, TIME, *         
      *                  REGISTER, AND TRANSACTION NUMBER.            *         
      *                                                               *         
      *****************************************************************         
      *                                                               *         
      *  THE PROGRAM READS A CONTROL CARD FILE TO GET THE POS DATE.   *         
      *                                                               *         
      *  UPDATES EACH TRANSACTION IN THE DATABASE TO PROCESSED.       *         
      *                                                               *         
      *****************************************************************         
C62728*  10/04/2013 COGNIZANT           WR#CHG0062728                 *         
C62728*                                                               *         
C62728*  REMOVED LOGIC THAT REFERENCING THE FOLLOWING COSA HISTORY    *         
C62728*  TABLES FOR 9607 - FSH 2013-CFMC PROJECT.                     *         
C62728*                                                               *         
C62728*  TEPCRDH                                                      *         
C62728*****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
                                                                                
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT DBT-CRD-INPUT-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  DBT-CRD-INPUT-FILE                                                   
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  DBT-CRD-INPUT-RECORD            PIC X(69).                           
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
      ******************************************************************        
      *   PROGRAM SWITCHES                                             *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-FILE-SW       PIC X(01)  VALUE 'N'.              
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.              
                                                                                
      ******************************************************************        
      *   PROGRAM CONSTANTS                                            *        
      ******************************************************************        
       01  PC-CONSTANTS.                                                        
           05  PC-LIT-P                    PIC X         VALUE 'P'.             
           05  PC-PERIOD                   PIC X         VALUE '.'.             
           05  PC-DASH                     PIC X         VALUE '-'.             
           05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE                  
                                                         'TCKUP003'.            
           05  PC-ABEND-MSSG                PIC X(12)      VALUE                
               '** ABEND ** '.                                                  
           05  PC-DB-ERROR-PREFIX           PIC X(09)      VALUE                
               'DB2 ERROR'.                                                     
           05  PC-DB-ATTEMPTING-MSSG        PIC X(14)      VALUE                
               'ATTEMPTING TO '.                                                
                                                                                
      ******************************************************************        
      *   PROGRAM VARIABLES.                                           *        
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.           
           05  PV-DB2-ERROR-MSSG            PIC X(40)      VALUE SPACE.         
           05  PV-PARTN-NBR                PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-PARTN-NBRH               PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-STR-NBR                  PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-RGST-ID                  PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-TRN-NBR                  PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-SLS-CORR-SEQ-NBR         PIC  S9(4)    COMP                   
                                                         VALUE ZERO.            
           05  PV-COMMIT-COUNTER           PIC  9(4)     VALUE ZERO.            
                                                                                
       01  PV-START-TIME.                                                       
           05  PV-STRT-TIME                PIC  X(06)    VALUE SPACES.          
           05  FILLER REDEFINES PV-STRT-TIME.                                   
               10  PV-STRT-TIME-HH         PIC X(02).                           
               10  PV-STRT-TIME-MM         PIC X(02).                           
               10  PV-STRT-TIME-SS         PIC X(02).                           
       01  PV-START-TIME.                                                       
           05  PV-STRT-TIME-RFRM           PIC  X(08)    VALUE SPACES.          
           05  FILLER REDEFINES PV-STRT-TIME-RFRM.                              
               10  PV-STRT-TIME-RFRM-HH    PIC X(02).                           
               10  PV-STRT-TIME-RFRM-F1    PIC X(01).                           
               10  PV-STRT-TIME-RFRM-MM    PIC X(02).                           
               10  PV-STRT-TIME-RFRM-F2    PIC X(01).                           
               10  PV-STRT-TIME-RFRM-SS    PIC X(02).                           
       01  PV-SLS-DTE.                                                          
           05  PV-SLS-CCYY                 PIC  X(04)    VALUE SPACES.          
           05  PV-SLS-DASH1                PIC  X(01)    VALUE '-'.             
           05  PV-SLS-MM                   PIC  X(02)    VALUE SPACES.          
           05  PV-SLS-DASH2                PIC  X(01)    VALUE '-'.             
           05  PV-SLS-DD                   PIC  X(02)    VALUE SPACES.          
       01  PV-SLS-DTE-RE REDEFINES PV-SLS-DTE PIC X(10).                        
                                                                                
       01  ABEND-CODE  COMP                PIC S9(04)    VALUE ZEROS.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
      ******************************************************************        
      *  INPUT FILE LAYOUT                                             *        
      ******************************************************************        
           COPY TCWS002.                                                        
      ******************************************************************        
      *    STANDARD DB2 ERROR HANDLING AREA                            *        
      ******************************************************************        
           COPY DPWS004.                                                        
      ******************************************************************        
      *    STANDARD DB2 DCLGEN FOR THE PARTITION TABLE (TEPPART)       *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TEPPART                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    STANDARD DB2 DCLGEN FOR THE CREDIT TENDER TABLE (TEPCRD)    *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TEPCRD                                                   
           END-EXEC.                                                            
      ******************************************************************        
      *    DB2 DCLGEN FOR THE DATA ENTRY KEY TABLE                     *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TEPDKEY                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    STANDARD DB2 COMMUNICATIONS AREA                            *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      * PROCEDURE DIVISION.                                           *         
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * 0000-MAINLINE-PARAGRAPH                                        *        
      *      PRODUCE DAILY DEBIT CARD AUDIT REPORT                     *        
      ******************************************************************        
       0000-MAINLINE-PARAGRAPH.                                                 
                                                                                
           PERFORM 1000-INITIALIZE-PROGRAM.                                     
                                                                                
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                                
                                                                                
           PERFORM 2000-PROCESS-RECORDS                                         
               UNTIL PS-END-OF-INPUT-FILE.                                      
                                                                                
           GOBACK.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * 1000-INITIALIZE-PROGRAM                                                 
      *  -  OPEN FILES                                                          
      ******************************************************************        
                                                                                
       1000-INITIALIZE-PROGRAM.                                                 
                                                                                
           OPEN INPUT  DBT-CRD-INPUT-FILE.                                      
                                                                                
           MOVE 0           TO PV-COMMIT-COUNTER.                               
                                                                                
      ******************************************************************        
      * 2000-PROCESS-RECORDS                                           *        
      ******************************************************************        
       2000-PROCESS-RECORDS.                                                    
                                                                                
           IF PV-COMMIT-COUNTER > 500                                           
              PERFORM 8000-COMMIT                                               
              MOVE 0        TO PV-COMMIT-COUNTER                                
           END-IF.                                                              
           PERFORM 4000-UPDATE-TEPCRD-PROCESS-CDE.                              
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                                
                                                                                
      ******************************************************************        
      *2100-READ-DBT-CRD-INPUT-FILE                                    *        
      ******************************************************************        
       2100-READ-DBT-CRD-INPUT-FILE.                                            
           READ DBT-CRD-INPUT-FILE                                              
               INTO TC002-DATA                                                  
               AT END                                                           
                   SET PS-END-OF-INPUT-FILE TO TRUE                             
           END-READ.                                                            
                                                                                
      ******************************************************************        
      *4000-UPDATE-TEPCRD-PROCESS-CDE.                                          
      ******************************************************************        
       4000-UPDATE-TEPCRD-PROCESS-CDE.                                          
                                                                                
           MOVE TC002-PARTN-NBR          TO  PV-PARTN-NBR.                      
           MOVE TC002-STR-NBR            TO  PV-STR-NBR.                        
           MOVE TC002-RGST-ID            TO  PV-RGST-ID.                        
           MOVE TC002-TRN-NBR            TO  PV-TRN-NBR.                        
           MOVE TC002-SLS-CORR-SEQ-NBR   TO  PV-SLS-CORR-SEQ-NBR.               
           MOVE TC002-STRT-TIME          TO  PV-STRT-TIME.                      
           MOVE PV-STRT-TIME-HH          TO  PV-STRT-TIME-RFRM-HH.              
           MOVE PV-STRT-TIME-MM          TO  PV-STRT-TIME-RFRM-MM.              
           MOVE PV-STRT-TIME-SS          TO  PV-STRT-TIME-RFRM-SS.              
           MOVE PC-PERIOD                TO  PV-STRT-TIME-RFRM-F1.              
           MOVE PC-PERIOD                TO  PV-STRT-TIME-RFRM-F2.              
                                                                                
           EXEC SQL                                                             
               UPDATE TEPCRD                                                    
                   SET STTLMNT_PRCD_CDE   = 'P'                                 
                   WHERE PARTN_NBR        = :PV-PARTN-NBR                       
                     AND STR_NBR          = :PV-STR-NBR                         
                     AND RGST_ID          = :PV-RGST-ID                         
                     AND TRN_NBR          = :PV-TRN-NBR                         
                     AND STRT_TM          = :PV-STRT-TIME-RFRM                  
                     AND SLS_CORR_SEQ_NBR = :PV-SLS-CORR-SEQ-NBR                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '4000-UPDATE-TEPCRD-PROCESS-CDE'                        
                                              TO PV-PARAGRAPH-NAME              
                   MOVE 'TO UPDATE TEPCRD '                                     
                                              TO PV-DB2-ERROR-MSSG              
                   PERFORM 9400-DB2-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
           ADD   +1                     TO PV-COMMIT-COUNTER.                   
                                                                                
      *****************************************************************         
      * COMMIT DB2 CHANGES                                            *         
      *****************************************************************         
       8000-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT EQUAL SPACE                                    
               WHEN SQLCODE NOT EQUAL ZERO                                      
                   MOVE '8000-COMMIT'    TO PV-PARAGRAPH-NAME                   
                   MOVE 'TO COMMIT '     TO PV-DB2-ERROR-MSSG                   
                   PERFORM 9400-DB2-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    DB2 ERROR                                                   *        
      *      PERFORMED ONLY VIA (3500-FIND-MERCHANT-NUMBER)            *        
      *    FUNCTION: HANDLE DB2 ERRORS                                 *        
      ******************************************************************        
       9400-DB2-ERROR.                                                          
                                                                                
           DISPLAY PC-ABEND-MSSG  PC-CURRENT-PROGRAM-NAME.                      
           DISPLAY PC-DB-ERROR-PREFIX                                           
           DISPLAY PC-DB-ATTEMPTING-MSSG PV-DB2-ERROR-MSSG.                     
           DISPLAY 'PARTITION: ' PV-PARTN-NBR.                                  
           DISPLAY 'STORE:     ' PV-STR-NBR.                                    
           DISPLAY 'REGISTER:  ' PV-RGST-ID.                                    
           DISPLAY 'TRANS:     ' PV-TRN-NBR.                                    
           DISPLAY 'SLSCORRSEQ:' PV-SLS-CORR-SEQ-NBR.                           
           COPY DPPD004.                                                        
           SET ABEND-4013-DB2-ERROR TO TRUE.                                    
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
           EJECT                                                                
