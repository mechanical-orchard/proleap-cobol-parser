       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    MLKUP302                                          00030000
       AUTHOR.        BOB MCASEY.                                       00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  5-20-1998.                                        00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *           MLKUP302 - MAJOR CLASS MERCHANDISE LEDGER            *00100000
      *                      TABLE UPDATE (TMNMCL)                     *00110000
W26603******************************************************************00120000
W26603* CHG ID   DATE        DESCRIPTION                               *00130000
W26603* ------   ----------  ----------------------------------------- *00140000
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00150000
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00160000
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00170000
W26603*                      A DB2 SELECT.                             *00180000
W26603*                                                                *00190000
W26603*                      - RONNA MCDONALD                          *00200000
      ******************************************************************00210000
                                                                        00220000
                                                                        00230000
       ENVIRONMENT DIVISION.                                            00240000
                                                                        00250000
       CONFIGURATION SECTION.                                           00260000
       SOURCE-COMPUTER.        IBM-3090.                                00270000
       OBJECT-COMPUTER.        IBM-3090.                                00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
           SELECT CONDENSED-FILE                                        00310000
               ASSIGN TO INP01.                                         00320000
                                                                        00330000
                                                                        00340000
       DATA DIVISION.                                                   00350000
       FILE SECTION.                                                    00360000
                                                                        00370000
       FD  CONDENSED-FILE                                               00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           RECORDING MODE IS F                                          00400000
           BLOCK CONTAINS 0 RECORDS.                                    00410000
       01  CONDENSED-RECORD            PIC X(100).                      00420000
      *                                                                 00430000
                                                                        00440000
       WORKING-STORAGE SECTION.                                         00450000
                                                                        00460000
       01  FILLER                      PIC X(28)   VALUE                00470000
                'BEGINNING OF WORKING STORAGE'.                         00480000
                                                                        00490000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00500000
                                                                        00510000
       01  PC-PROGRAM-CONSTANTS.                                        00520000
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK225'.    00530000
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP302'.  00540000
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00550000
                                                                        00560000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00570000
           05  PE-MSG-01                       PIC X(50) VALUE          00580000
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00590000
           05  PE-MSG-02                       PIC X(50) VALUE          00600000
                'ATTEMPT TO UPDATE ROW ON TABLE TMNMCL FAILED    '.     00610000
           05  PE-MSG-03                       PIC X(50) VALUE          00620000
                'ATTEMPT TO INSERT ROW ON TABLE TMNMCL FAILED    '.     00630000
           05  PE-MSG-04                       PIC X(50) VALUE          00640000
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00650000
           05  PE-MSG-05                       PIC X(50) VALUE          00660000
                'UPDATE TO TCKRSTINF FAILED                      '.     00670000
           05  PE-MSG-06                       PIC X(50) VALUE          00680000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00690000
           05  PE-MSG-07                       PIC X(50) VALUE          00700000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00710000
      *                                                                 00720000
       01  PS-PROGRAM-SWITCHES.                                         00730000
           05  PS-EOF-CONDENSED-FILE-SW     PIC X(01)     VALUE 'N'.    00740000
               88  PS-EOF-CONDENSED-FILE                  VALUE 'Y'.    00750000
           05  PS-MNMCL-CURSOR-EOF-SW       PIC X(01)     VALUE 'N'.    00760000
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00770000
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00780000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00790000
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00800000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00810000
                                                                        00820000
       01  PI-PROGRAM-INDICATORS.                                       00830000
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00840000
               88  PI-UPDATE-MNMCL-ROW                    VALUE 'U'.    00850000
               88  PI-INSERT-MNMCL-ROW                    VALUE 'I'.    00860000
      *                                                                 00870000
       01  PROGRAM-VARIABLES.                                           00880000
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     00890000
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     00900000
      *                                                                 00910000
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 00920000
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00930000
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 00940000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00950000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 00960000
      *                                                                 00970000
       01  PA-PROGRAM-ACCUMULATORS.                                     00980000
           05  PA-RECORD-COUNTS.                                        00990000
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01000000
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01010000
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01020000
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01030000
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01040000
                                                                        01050000
       01  PD-PROGRAM-DISPLAYS.                                         01060000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01070000
       01  SAVE-AREA.                                                   01080000
           05  SV-PRIMARY-KEY.                                          01090000
               10  SV-FSCL-MN-NBR        PIC X(02)  VALUE SPACES.       01100000
               10  SV-FSCL-MN-END-DTE    PIC X(10)  VALUE SPACES.       01110000
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01120000
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01130000
           05  SV-UPDATE-FIELDS.                                        01140000
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01150000
W26603         10  SV-RCPT-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01160000
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01170000
W26603         10  SV-PADJ-NET-COST      PIC S9(11)V99  COMP-3 VALUE +0.01180000
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01190000
W26603         10  SV-MKDN-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01200000
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01210000
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01220000
W26603         10  SV-XFER-OUT-RTL       PIC S9(11)V99  COMP-3 VALUE +0.01230000
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01240000
      *                                                                 01250000
      ******************************************************************01260000
      * MLRD105 - M/L MONTHLY MCL DATABASE UPDATE TRANSACTION LAYOUT    01270000
      ******************************************************************01280000
           COPY MLRD105.                                                01290000
      *                                                                 01300000
      ******************************************************************01310000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01320000
      ******************************************************************01330000
           COPY DPWS004.                                                01340000
      *                                                                 01350000
      ******************************************************************01360000
      *  USED FOR DB2 ERRORS                                            01370000
      ******************************************************************01380000
           EXEC SQL                                                     01390000
               INCLUDE SQLCA                                            01400000
           END-EXEC.                                                    01410000
      *                                                                 01420000
      ******************************************************************01430000
      *  M/L MONTH-TO-DATE MAJOR CLASS TABLE                            01440000
      ******************************************************************01450000
           EXEC SQL                                                     01460000
               INCLUDE TMNMCL                                           01470000
           END-EXEC.                                                    01480000
      *                                                                 01490000
      ***************************************************************** 01500000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01510000
      ***************************************************************** 01520000
           EXEC SQL                                                     01530000
               INCLUDE TCKRSTCN                                         01540000
           END-EXEC.                                                    01550000
      *                                                                 01560000
           EXEC SQL                                                     01570000
               INCLUDE TCKRSTIN                                         01580000
           END-EXEC.                                                    01590000
      *                                                                 01600000
      ******************************************************************01610000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01620000
      ******************************************************************01630000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01640000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +26  COMP. 01650000
           05  CR-CHECKPOINT-RESTART-VAR.                               01660000
               10  CR-PREV-DTL-KEY          PIC  X(17) VALUE SPACES.    01670000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01680000
                   15  CR-FISCAL-NBR        PIC  X(02).                 01690000
                   15  CR-FISCAL-MN-END-DTE PIC  X(10).                 01700000
                   15  CR-DEPT-NBR          PIC  X(03).                 01710000
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 01720000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01730000
      *                                                                 01740000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01750000
           05  CK-SAVE-PREV-KEY         PIC  X(17) VALUE SPACES.        01760000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01770000
               10  CK-FISCAL-NBR        PIC  X(02).                     01780000
               10  CK-FISCAL-MN-END-DTE PIC  X(10).                     01790000
               10  CK-DEPT-NBR          PIC  X(03).                     01800000
               10  CK-MAJ-CL-NBR        PIC  X(02).                     01810000
                                                                        01820000
                                                                        01830000
      *-------------------*                                             01840000
       PROCEDURE DIVISION.                                              01850000
      *-------------------*                                             01860000
                                                                        01870000
       0000-MLKUP302.                                                   01880000
                                                                        01890000
           PERFORM 1000-INITIALIZATION.                                 01900000
      *                                                                 01910000
           PERFORM 2000-PROCESS-INPUT                                   01920000
                UNTIL PS-EOF-CONDENSED-FILE.                            01930000
      *                                                                 01940000
           PERFORM 8000-EOJ-ROUTINE.                                    01950000
                                                                        01960000
           STOP RUN.                                                    01970000
                                                                        01980000
      ******************************************************************01990000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02000000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02010000
      ******************************************************************02020000
      *                                                                 02030000
       1000-INITIALIZATION.                                             02040000
      *                                                                 02050000
           OPEN INPUT CONDENSED-FILE.                                   02060000
                                                                        02070000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02080000
                                                                        02090000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02100000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02110000
               PERFORM 7500-SELECT-RESTART-INFO                         02120000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02130000
                                            CR-CHECKPOINT-RESTART-VAR   02140000
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02150000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02160000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 02170000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          02180000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02190000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02200000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02210000
           ELSE                                                         02220000
               SET PS-FIRST-COMMIT TO TRUE                              02230000
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02240000
           END-IF.                                                      02250000
      *                                                                 02260000
           PERFORM 4000-READ-CONDENSED-FILE UNTIL                       02270000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02280000
            OR PS-EOF-CONDENSED-FILE.                                   02290000
      *                                                                 02300000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02310000
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02320000
                        PA-INPUT-COUNT                                  02330000
               DISPLAY 'FISCAL MONTH NBR      ' ML105-FSCL-MN-NBR       02340000
               DISPLAY 'FISCAL MONTH END DATE ' ML105-FSCL-MN-END-DTE   02350000
               DISPLAY 'DEPARTMENT NBR        ' ML105-DEPT-NBR          02360000
               DISPLAY 'MAJOR CLASS NBR       ' ML105-MAJ-CL-NBR        02370000
           END-IF.                                                      02380000
                                                                        02390000
           IF PS-EOF-CONDENSED-FILE                                     02400000
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02410000
                   CONTINUE                                             02420000
               ELSE                                                     02430000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02440000
           ELSE                                                         02450000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02460000
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02470000
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02480000
           END-IF.                                                      02490000
                                                                        02500000
      ******************************************************************02510000
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02520000
      ******************************************************************02530000
       2000-PROCESS-INPUT.                                              02540000
      *                                                                 02550000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02560000
      *                                                                 02570000
           PERFORM 7000-SELECT-DB-ROW.                                  02580000
      *                                                                 02590000
           IF PS-PROGRAM-DEADLOCKED                                     02600000
               CONTINUE                                                 02610000
           ELSE                                                         02620000
               EVALUATE TRUE                                            02630000
                   WHEN PI-UPDATE-MNMCL-ROW                             02640000
                       PERFORM 7100-UPDATE-MNMCL-ROW                    02650000
                   WHEN PI-INSERT-MNMCL-ROW                             02660000
                       PERFORM 7200-INSERT-MNMCL-ROW                    02670000
                   WHEN OTHER                                           02680000
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   02690000
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         02700000
                       PERFORM 9999-SQL-ABEND                           02710000
               END-EVALUATE                                             02720000
           END-IF.                                                      02730000
      *                                                                 02740000
           IF PS-PROGRAM-DEADLOCKED                                     02750000
               CONTINUE                                                 02760000
           ELSE                                                         02770000
               PERFORM 7700-COMMIT-PROCESSING                           02780000
               PERFORM 4000-READ-CONDENSED-FILE                         02790000
           END-IF.                                                      02800000
      *                                                                 02810000
      *                                                                 02820000
      ******************************************************************02830000
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         02840000
      ******************************************************************02850000
       2100-CALCULATE-UPDATES.                                          02860000
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     02870000
      *                                                                 02880000
           COMPUTE SV-SLS-RTL-AMT       =                               02890000
                   MNMCL-SLS-RTL-AMT       + ML105-SLS-RTL-AMT.         02900000
           COMPUTE SV-MKDN-RTL-AMT      =                               02910000
                   MNMCL-MKDN-RTL-AMT      + ML105-MKDN-RTL-AMT.        02920000
           COMPUTE SV-MKUP-RTL-AMT      =                               02930000
                   MNMCL-MKUP-RTL-AMT      + ML105-MKUP-RTL-AMT.        02940000
           COMPUTE SV-XFER-IN-RTL-AMT   =                               02950000
                   MNMCL-XFER-IN-RTL-AMT   + ML105-XFER-IN-RTL-AMT.     02960000
           COMPUTE SV-XFER-OUT-RTL      =                               02970000
                   MNMCL-XFER-OUT-RTL-AMT  + ML105-XFER-OUT-RTL-AMT.    02980000
           COMPUTE SV-RCPT-RTL-AMT      =                               02990000
                   MNMCL-RCPT-RTL-AMT + ML105-RCPT-RTL-AMT.             03000000
           COMPUTE SV-RCPT-NET-COST     =                               03010000
                   MNMCL-RCPT-NET-COST-AMT + ML105-RCPT-NET-COST-AMT.   03020000
           COMPUTE SV-PADJ-RTL-AMT      =                               03030000
                   MNMCL-PADJ-RTL-AMT      + ML105-PADJ-RTL-AMT.        03040000
           COMPUTE SV-PADJ-NET-COST     =                               03050000
                   MNMCL-PADJ-NET-COST-AMT + ML105-PADJ-NET-COST-AMT    03060000
           COMPUTE SV-INV-ADJ-RTL-AMT   =                               03070000
                    MNMCL-INV-ADJ-RTL-AMT  + ML105-INV-ADJ-RTL-AMT.     03080000
      *                                                                 03090000
      ******************************************************************03100000
      * READS THE CONDENSED FILE INTO THE TMNMCL LAYOUT                 03110000
      ******************************************************************03120000
       4000-READ-CONDENSED-FILE.                                        03130000
           READ CONDENSED-FILE INTO ML105-UPDATE-REC                    03140000
              AT END                                                    03150000
                MOVE 'Y' TO PS-EOF-CONDENSED-FILE-SW.                   03160000
     *                                                                  03170000
           IF PS-EOF-CONDENSED-FILE                                     03180000
               MOVE ML105-FSCL-MN-NBR         TO CR-FISCAL-NBR          03190000
               MOVE ML105-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   03200000
               MOVE ML105-DEPT-NBR            TO CR-DEPT-NBR            03210000
               MOVE ML105-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03220000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    03230000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03240000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03250000
                                             TCKRSTINF-WORK-STRG-DESC   03260000
               EXEC SQL                                                 03270000
                 UPDATE TCKRSTINF                                       03280000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03290000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03300000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03310000
               END-EXEC                                                 03320000
               IF SQLCODE = 0                                           03330000
                   CONTINUE                                             03340000
               ELSE                                                     03350000
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    03360000
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  03370000
                   PERFORM 9999-SQL-ABEND                               03380000
               END-IF                                                   03390000
            ELSE                                                        03400000
                ADD 1                        TO PA-INPUT-COUNT          03410000
            END-IF.                                                     03420000
      *                                                                 03430000
      ******************************************************************03440000
      * SELECT A ROW FROM THE MONTHLY MCL  TABLE THAT MATCHES INPUT KEY 03450000
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      03460000
      ******************************************************************03470000
       7000-SELECT-DB-ROW.                                              03480000
      *                                                                 03490000
           EXEC SQL                                                     03500000
               SELECT SLS_RTL_AMT                                       03510000
                     ,MKDN_RTL_AMT                                      03520000
                     ,MKUP_RTL_AMT                                      03530000
                     ,XFER_IN_RTL_AMT                                   03540000
                     ,XFER_OUT_RTL_AMT                                  03550000
                     ,RCPT_RTL_AMT                                      03560000
                     ,RCPT_NET_COST_AMT                                 03570000
                     ,PADJ_RTL_AMT                                      03580000
                     ,PADJ_NET_COST_AMT                                 03590000
                     ,INV_ADJ_RTL_AMT                                   03600000
                 INTO :MNMCL-SLS-RTL-AMT                                03610000
                     ,:MNMCL-MKDN-RTL-AMT                               03620000
                     ,:MNMCL-MKUP-RTL-AMT                               03630000
                     ,:MNMCL-XFER-IN-RTL-AMT                            03640000
                     ,:MNMCL-XFER-OUT-RTL-AMT                           03650000
                     ,:MNMCL-RCPT-RTL-AMT                               03660000
                     ,:MNMCL-RCPT-NET-COST-AMT                          03670000
                     ,:MNMCL-PADJ-RTL-AMT                               03680000
                     ,:MNMCL-PADJ-NET-COST-AMT                          03690000
                     ,:MNMCL-INV-ADJ-RTL-AMT                            03700000
               FROM TMNMCL                                              03710000
              WHERE   FSCL_MN_NBR         = :ML105-FSCL-MN-NBR          03720000
                AND   FSCL_MN_END_DTE     = :ML105-FSCL-MN-END-DTE      03730000
                AND   DEPT_NBR            = :ML105-DEPT-NBR             03740000
                AND   MAJ_CL_NBR          = :ML105-MAJ-CL-NBR           03750000
W26603        WITH UR                                                   03760000
           END-EXEC.                                                    03770000
      *                                                                 03780000
           EVALUATE SQLCODE                                             03790000
               WHEN 0                                                   03800000
                   SET PI-UPDATE-MNMCL-ROW        TO TRUE               03810000
               WHEN +100                                                03820000
                   SET PI-INSERT-MNMCL-ROW        TO TRUE               03830000
               WHEN -911                                                03840000
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            03850000
                   ADD +1             TO PV-DEADLOCK-COUNT              03860000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              03870000
                       MOVE '7000-SELECT-DB-ROW' TO                     03880000
                                                    PV-PARAGRAPH-NAME   03890000
                       PERFORM 9000-DEADLOCK-ERROR                      03900000
                   ELSE                                                 03910000
                       PERFORM 7999-RESTART-PROCESS                     03920000
                   END-IF                                               03930000
               WHEN OTHER                                               03940000
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     03950000
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             03960000
                   PERFORM 9999-SQL-ABEND                               03970000
           END-EVALUATE.                                                03980000
      *                                                                 03990000
      *                                                                 04000000
      ***************************************************************** 04010000
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  04020000
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 04030000
      * FROM INPUT REC MLRD105                                          04040000
      ***************************************************************** 04050000
       7100-UPDATE-MNMCL-ROW.                                           04060000
      *                                                                 04070000
           PERFORM 2100-CALCULATE-UPDATES.                              04080000
      *                                                                 04090000
           EXEC SQL                                                     04100000
               UPDATE TMNMCL                                            04110000
                   SET RCPT_RTL_AMT        = :SV-RCPT-RTL-AMT           04120000
                     , RCPT_NET_COST_AMT   = :SV-RCPT-NET-COST          04130000
                     , PADJ_RTL_AMT        = :SV-PADJ-RTL-AMT           04140000
                     , PADJ_NET_COST_AMT   = :SV-PADJ-NET-COST          04150000
                     , SLS_RTL_AMT         = :SV-SLS-RTL-AMT            04160000
                     , MKDN_RTL_AMT        = :SV-MKDN-RTL-AMT           04170000
                     , MKUP_RTL_AMT        = :SV-MKUP-RTL-AMT           04180000
                     , XFER_IN_RTL_AMT     = :SV-XFER-IN-RTL-AMT        04190000
                     , XFER_OUT_RTL_AMT    = :SV-XFER-OUT-RTL           04200000
                     , INV_ADJ_RTL_AMT     = :SV-INV-ADJ-RTL-AMT        04210000
              WHERE   FSCL_MN_NBR          = :ML105-FSCL-MN-NBR         04220000
                AND   FSCL_MN_END_DTE      = :ML105-FSCL-MN-END-DTE     04230000
                AND   DEPT_NBR             = :ML105-DEPT-NBR            04240000
                AND   MAJ_CL_NBR           = :ML105-MAJ-CL-NBR          04250000
           END-EXEC                                                     04260000
      *                                                                 04270000
           EVALUATE SQLCODE                                             04280000
               WHEN 0                                                   04290000
                   CONTINUE                                             04300000
               WHEN -911                                                04310000
                   ADD +1             TO PV-DEADLOCK-COUNT              04320000
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            04330000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              04340000
                       MOVE '7100-UPDATE-MNMCL-ROW' TO                  04350000
                                                    PV-PARAGRAPH-NAME   04360000
                       PERFORM 9000-DEADLOCK-ERROR                      04370000
                   ELSE                                                 04380000
                       PERFORM 7999-RESTART-PROCESS                     04390000
                   END-IF                                               04400000
               WHEN OTHER                                               04410000
                   MOVE '7100-UPDATE-MNMCL-ROW' TO PV-PARAGRAPH-NAME    04420000
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED04430000
                   PERFORM 9999-SQL-ABEND                               04440000
           END-EVALUATE.                                                04450000
      *                                                                 04460000
           ADD 1 TO PA-UPDATE-COUNT.                                    04470000
      *                                                                 04480000
      ***************************************************************** 04490000
      * THERE WAS NO MATCHING ROW ON THE MONTHLY MCL  TABLE. INITIAL  * 04500000
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 04510000
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN RECALC04520000
      ***************************************************************** 04530000
       7200-INSERT-MNMCL-ROW.                                           04540000
      *                                                                 04550000
      *                                                                 04560000
           EXEC SQL                                                     04570000
               INSERT INTO TMNMCL                                       04580000
                   (   FSCL_MN_NBR                                      04590000
                     , FSCL_MN_END_DTE                                  04600000
                     , DEPT_NBR                                         04610000
                     , MAJ_CL_NBR                                       04620000
                     , RCPT_RTL_AMT                                     04630000
                     , RCPT_NET_COST_AMT                                04640000
                     , PADJ_RTL_AMT                                     04650000
                     , PADJ_NET_COST_AMT                                04660000
                     , SLS_RTL_AMT                                      04670000
                     , MKDN_RTL_AMT                                     04680000
                     , MKUP_RTL_AMT                                     04690000
                     , XFER_IN_RTL_AMT                                  04700000
                     , XFER_OUT_RTL_AMT                                 04710000
                     , INV_ADJ_RTL_AMT                                  04720000
                     , SHNK_RTL_AMT                                     04730000
                     , END_INV_RTL_AMT                                  04740000
                     , CUM_MKUP_PCT                                     04750000
                     , END_INV_COST_AMT                                 04760000
                     , SLS_COST_AMT                                     04770000
                     , GRO_MRGN_AMT      )                              04780000
               VALUES                                                   04790000
                   (   :ML105-FSCL-MN-NBR                               04800000
                     , :ML105-FSCL-MN-END-DTE                           04810000
                     , :ML105-DEPT-NBR                                  04820000
                     , :ML105-MAJ-CL-NBR                                04830000
                     , :ML105-RCPT-RTL-AMT                              04840000
                     , :ML105-RCPT-NET-COST-AMT                         04850000
                     , :ML105-PADJ-RTL-AMT                              04860000
                     , :ML105-PADJ-NET-COST-AMT                         04870000
                     , :ML105-SLS-RTL-AMT                               04880000
                     , :ML105-MKDN-RTL-AMT                              04890000
                     , :ML105-MKUP-RTL-AMT                              04900000
                     , :ML105-XFER-IN-RTL-AMT                           04910000
                     , :ML105-XFER-OUT-RTL-AMT                          04920000
                     , :ML105-INV-ADJ-RTL-AMT                           04930000
                     , 0                                                04940000
                     , 0                                                04950000
                     , 0                                                04960000
                     , 0                                                04970000
                     , 0                                                04980000
                     , 0                       )                        04990000
           END-EXEC                                                     05000000
      *                                                                 05010000
           EVALUATE SQLCODE                                             05020000
               WHEN 0                                                   05030000
                   CONTINUE                                             05040000
               WHEN -911                                                05050000
                   ADD +1             TO PV-DEADLOCK-COUNT              05060000
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            05070000
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05080000
                       MOVE '7200-INSERT-MNMCL-ROW' TO                  05090000
                                                    PV-PARAGRAPH-NAME   05100000
                       PERFORM 9000-DEADLOCK-ERROR                      05110000
                   ELSE                                                 05120000
                       PERFORM 7999-RESTART-PROCESS                     05130000
                   END-IF                                               05140000
               WHEN OTHER                                               05150000
                   MOVE '7200-INSERT-MNMCL-ROW' TO PV-PARAGRAPH-NAME    05160000
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED05170000
                   PERFORM 9999-SQL-ABEND                               05180000
           END-EVALUATE.                                                05190000
                                                                        05200000
           ADD 1 TO PA-INSERT-COUNT.                                    05210000
                                                                        05220000
      ******************************************************************05230000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05240000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05250000
      *            CONTROL TABLE                                       *05260000
      ******************************************************************05270000
       7300-GET-COMMIT-TIMESTAMP.                                       05280000
                                                                        05290000
           EXEC SQL                                                     05300000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05310000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05320000
               INTO :PV-COMMIT-TIMESTAMP                                05330000
               FROM TCKRSTCNTL                                          05340000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05350000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05360000
           END-EXEC.                                                    05370000
                                                                        05380000
           EVALUATE TRUE                                                05390000
               WHEN SQLCODE = 0                                         05400000
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  05410000
                   CONTINUE                                             05420000
               WHEN SQLCODE NOT EQUAL ZERO                              05430000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05440000
                   PERFORM 9999-SQL-ABEND                               05450000
           END-EVALUATE.                                                05460000
      *                                                                 05470000
      *                                                                 05480000
      ******************************************************************05490000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05500000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05510000
      *            IF WE ARE IN A RESTART CONDITION.                   *05520000
      ******************************************************************05530000
       7400-INIT-CHECKPOINT-SELECT.                                     05540000
                                                                        05550000
           EXEC SQL                                                     05560000
             SELECT  CKPT_STAT_CODE                                     05570000
                    ,CKPT_FRQNCY_QTY                                    05580000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05590000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05600000
               FROM  TCKRSTCNTL                                         05610000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05620000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05630000
           END-EXEC.                                                    05640000
                                                                        05650000
           IF SQLCODE = 0                                               05660000
               CONTINUE                                                 05670000
           ELSE                                                         05680000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05690000
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     05700000
               PERFORM 9999-SQL-ABEND                                   05710000
           END-IF.                                                      05720000
      *                                                                 05730000
      *                                                                 05740000
      ******************************************************************05750000
      * 7500-SELECT-RESTART-INFO                                       *05760000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05770000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05780000
      ******************************************************************05790000
       7500-SELECT-RESTART-INFO.                                        05800000
                                                                        05810000
           EXEC SQL                                                     05820000
             SELECT  WORK_STRG_DESC                                     05830000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05840000
               FROM  TCKRSTINF                                          05850000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05860000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05870000
           END-EXEC.                                                    05880000
                                                                        05890000
           IF SQLCODE = 0                                               05900000
               CONTINUE                                                 05910000
           ELSE                                                         05920000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   05930000
               PERFORM 9999-SQL-ABEND                                   05940000
           END-IF.                                                      05950000
                                                                        05960000
      ******************************************************************05970000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *05980000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *05990000
      ******************************************************************06000000
       7600-SET-CURRENT-TIMESTAMP.                                      06010000
                                                                        06020000
           EXEC SQL                                                     06030000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06040000
           END-EXEC.                                                    06050000
                                                                        06060000
           IF SQLCODE = 0                                               06070000
               CONTINUE                                                 06080000
           ELSE                                                         06090000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06100000
               PERFORM 9999-SQL-ABEND                                   06110000
           END-IF.                                                      06120000
      *                                                                 06130000
      *                                                                 06140000
      ******************************************************************06150000
      * 7700-COMMIT-PROCESSING.                                        *06160000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06170000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06180000
      ******************************************************************06190000
       7700-COMMIT-PROCESSING.                                          06200000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06210000
                                                                        06220000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06230000
                                                                        06240000
      * PREPARE COMMIT RECORD FOR UPDATE                                06250000
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                06260000
               MOVE ML105-FSCL-MN-NBR         TO CR-FISCAL-NBR          06270000
               MOVE ML105-FSCL-MN-END-DTE     TO CR-FISCAL-MN-END-DTE   06280000
               MOVE ML105-DEPT-NBR            TO CR-DEPT-NBR            06290000
               MOVE ML105-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          06300000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06310000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  06320000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06330000
                                             TCKRSTINF-WORK-STRG-DESC   06340000
               IF PS-FIRST-COMMIT                                       06350000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06360000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06370000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06380000
               END-IF                                                   06390000
               PERFORM 7800-COMMIT-CHANGES                              06400000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06410000
           END-IF.                                                      06420000
      *                                                                 06430000
      *                                                                 06440000
      ******************************************************************06450000
      * 7800-COMMIT-CHANGES.                                            06460000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06470000
      *            TAKE A COMMIT.                                       06480000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06490000
      ******************************************************************06500000
       7800-COMMIT-CHANGES.                                             06510000
                                                                        06520000
           EXEC SQL                                                     06530000
             UPDATE TCKRSTINF                                           06540000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06550000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06560000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06570000
           END-EXEC.                                                    06580000
                                                                        06590000
           IF SQLCODE = 0                                               06600000
               CONTINUE                                                 06610000
           ELSE                                                         06620000
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED06630000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06640000
               PERFORM 9999-SQL-ABEND                                   06650000
           END-IF.                                                      06660000
                                                                        06670000
           EXEC SQL                                                     06680000
               COMMIT                                                   06690000
           END-EXEC.                                                    06700000
                                                                        06710000
           IF SQLCODE = 0                                               06720000
               CONTINUE                                                 06730000
           ELSE                                                         06740000
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED06750000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06760000
               PERFORM 9999-SQL-ABEND                                   06770000
           END-IF.                                                      06780000
      *                                                                 06790000
      ******************************************************************06800000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   06810000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   06820000
      ******************************************************************06830000
       7900-UPDATE-CHECKPOINT-STATUS.                                   06840000
                                                                        06850000
           EXEC SQL                                                     06860000
             UPDATE  TCKRSTCNTL                                         06870000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        06880000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           06890000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06900000
           END-EXEC.                                                    06910000
                                                                        06920000
           IF SQLCODE = 0                                               06930000
               CONTINUE                                                 06940000
           ELSE                                                         06950000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME06960000
               PERFORM 9999-SQL-ABEND                                   06970000
           END-IF.                                                      06980000
                                                                        06990000
           EJECT                                                        07000000
      *                                                                 07010000
      *                                                                 07020000
      ******************************************************************07030000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TMNMCL ROW FOR UPDATE 07040000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07050000
      ******************************************************************07060000
       7999-RESTART-PROCESS.                                            07070000
      *                                                                 07080000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07090000
      *                                                                 07100000
           CLOSE CONDENSED-FILE.                                        07110000
      *                                                                 07120000
           PERFORM 7500-SELECT-RESTART-INFO.                            07130000
      *                                                                 07140000
           DISPLAY '**** RESTART **** '.                                07150000
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 07160000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07170000
                                                                        07180000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07190000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07200000
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       07210000
           IF PS-FIRST-COMMIT                                           07220000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               07230000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07240000
                      ' BEGINNING'                                      07250000
           ELSE                                                         07260000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07270000
                        TCKRSTINF-WORK-STRG-DESC (1:30)                 07280000
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 07290000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07300000
                    CR-CHECKPOINT-RESTART-AREA                          07310000
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07320000
               DISPLAY 'MONTH NBR       ' CR-FISCAL-NBR                 07330000
               DISPLAY 'MONTH END DATE  ' CR-FISCAL-MN-END-DTE          07340000
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   07350000
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 07360000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07370000
           END-IF.                                                      07380000
                                                                        07390000
           OPEN INPUT CONDENSED-FILE.                                   07400000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07410000
      *                                                                 07420000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07430000
           PERFORM 4000-READ-CONDENSED-FILE                             07440000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT.              07450000
           DISPLAY 'INPUT RECORD RESTARTED ON RECORD '                  07460000
                    PA-INPUT-COUNT                                      07470000
           DISPLAY 'FISCAL MONTH NBR      ' ML105-FSCL-MN-NBR.          07480000
           DISPLAY 'FISCAL MONTH END DATE ' ML105-FSCL-MN-END-DTE.      07490000
           DISPLAY 'DEPARTMENT NBR        ' ML105-DEPT-NBR.             07500000
           DISPLAY 'MAJOR CLASS NBR       ' ML105-MAJ-CL-NBR.           07510000
      ******************************************************************07520000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07530000
      ******************************************************************07540000
       8000-EOJ-ROUTINE.                                                07550000
      *                                                                 07560000
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       07570000
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       07580000
                                                                        07590000
      *                                                                 07600000
           DISPLAY SPACES.                                              07610000
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         07620000
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        07630000
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        07640000
           DISPLAY SPACES.                                              07650000
      *                                                                 07660000
           CLOSE CONDENSED-FILE.                                        07670000
      *                                                                 07680000
      *                                                                 07690000
      ******************************************************************07700000
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       07710000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   07720000
      ******************************************************************07730000
       9000-DEADLOCK-ERROR.                                             07740000
      *                                                                 07750000
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     07760000
               PERFORM 9999-SQL-ABEND.                                  07770000
      *                                                                 07780000
      ******************************************************************07790000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               07800000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             07810000
      ******************************************************************07820000
       9999-SQL-ABEND.                                                  07830000
                                                                        07840000
           MOVE +4013                 TO ABEND-CODE.                    07850000
           DISPLAY '**  ABEND     **'.                                  07860000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              07870000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            07880000
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       07890000
                                                                        07900000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         07910000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        07920000
                                                                        07930000
           COPY DPPD004.                                                07940000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           07950000
