      ******************************************************************00010000
       IDENTIFICATION DIVISION.                                         00020000
      ******************************************************************00030000
      *                                                                 00040000
       PROGRAM-ID.    IMKUT215.                                         00050000
       AUTHOR.        BOB MCASEY.                                       00060000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00070000
       DATE-WRITTEN.  JANUARY, 2003.                                    00080000
       DATE-COMPILED.                                                   00090000
      *                                                                 00100000
      ******************************************************************00110000
      *   THIS PROGRAM CREATES ...                                     *00120005
      ******************************************************************00130000
      *  CHANGED:                                                      *00140000
      *                                                                *00150000
      *    WR/PITS#     DATE        DESCRIPTION                        *00160000
      *    --------   ----------   ---------------------------------   *00170000
      *                                                                *00180000
      *               01-21-2003    INITIAL DEVELOPMENT.               *00190000
      *                                                                 00200000
      ******************************************************************00210000
       ENVIRONMENT DIVISION.                                            00220000
      ******************************************************************00230000
      *                                                                 00240000
       CONFIGURATION SECTION.                                           00250000
       SOURCE-COMPUTER.    IBM-3090.                                    00260000
       OBJECT-COMPUTER.    IBM-3090.                                    00270000
      *                                                                 00280000
       INPUT-OUTPUT SECTION.                                            00290000
       FILE-CONTROL.                                                    00300000
      *                                                                 00310000
           SELECT MDSEAR-EXTR-IN-FILE                                   00320000
               ASSIGN TO INP01.                                         00330000
      *                                                                 00340000
           SELECT DEFAULT-MAINT-OUT-FILE                                00350000
               ASSIGN TO OUT01.                                         00360000
      *                                                                 00370000
      ******************************************************************00380000
       DATA DIVISION.                                                   00390000
      ******************************************************************00400000
      *                                                                 00410000
       FILE SECTION.                                                    00420000
      *                                                                 00430000
       FD  MDSEAR-EXTR-IN-FILE                                          00440000
           RECORDING MODE IS F                                          00450002
           BLOCK CONTAINS 0 RECORDS.                                    00460000
       01  MDSEAR-EXTR-IN-REC      PIC X(64).                           00470004
      *                                                                 00480000
       FD  DEFAULT-MAINT-OUT-FILE                                       00490000
           RECORDING MODE IS F                                          00500000
           BLOCK CONTAINS 0 RECORDS.                                    00510000
       01  DEFAULT-MAINT-OUT-REC   PIC X(109).                          00520006
      *                                                                 00530000
      *                                                                 00540000
       WORKING-STORAGE SECTION.                                         00550000
      ******************************************************************00560000
      *  INPUT/OUTPUT FILE RECORD LAYOUT                                00570000
      ******************************************************************00580000
           COPY IMRD040.                                                00590000
      *                                                                 00600000
       01  ABEND-CODE                      PIC S9(04)    VALUE ZERO     00610000
                                                         COMP SYNC.     00620000
      *                                                                 00630000
       01  PA-PROGRAM-ACCUMULATORS.                                     00640000
           05  PA-WRITE-COUNT              PIC 9(07)    VALUE ZERO.     00650004
           05  PA-READ-COUNT               PIC 9(07)    VALUE ZERO.     00660000
           05  PA-DEF-MAINT-COUNT          PIC 9(07)    VALUE ZERO.     00670005
      *                                                                 00680000
       01  PC-PROGRAM-CONSTANTS.                                        00690000
           05  PC-PROGRAM-NAME             PIC X(08)     VALUE          00700000
               'IMKUT215'.                                              00710000
      *                                                                 00720000
       01  PS-PROGRAM-SWITCHES.                                         00730000
           05  PS-END-OF-INPUT-FILE-SW     PIC X(01)  VALUE 'N'.        00740000
               88  PS-END-OF-INPUT-FILE               VALUE 'Y'.        00750000
           05  PS-END-OF-DEF-MAINT-CSR-SW  PIC X(01)  VALUE 'N'.        00760004
               88  PS-END-OF-DEF-MAINT-CSR            VALUE 'Y'.        00770004
      *                                                                 00780000
       01  PV-PROGRAM-VARIABLES.                                        00790005
           05  PV-PARA-NAME                PIC X(35).                   00800005
                                                                        00810000
      *                                                                 00820000
           COPY SQLCA2.                                                 00830000
      *                                                                 00840000
      ******************************************************************00850000
      *  TDCVSVL -                                                      00860000
      ******************************************************************00870000
      *                                                                 00880000
           EXEC SQL                                                     00890000
                INCLUDE TDCVSVL                                         00900000
           END-EXEC.                                                    00910000
      *                                                                 00920000
      ******************************************************************00930000
      *  TVSCHVL -                                                      00940000
      ******************************************************************00950000
      *                                                                 00960000
           EXEC SQL                                                     00970000
                INCLUDE TVSCHVL                                         00980000
           END-EXEC.                                                    00990000
      *                                                                 01000000
      ******************************************************************01010000
      *  TVSCHAR -                                                      01020000
      ******************************************************************01030000
      *                                                                 01040000
           EXEC SQL                                                     01050000
                INCLUDE TVSCHAR                                         01060000
           END-EXEC.                                                    01070000
      *                                                                 01080000
           EXEC SQL                                                     01090000
                INCLUDE SQLCA                                           01100000
           END-EXEC.                                                    01110000
                                                                        01120000
      ******************************************************************01130004
      * TABLE CURSOR - DEFAULT MAINTENANCE CURSOR                       01140004
      ******************************************************************01150004
           EXEC SQL                                                     01160004
                DECLARE DEFAULT_MAINT_CSR CURSOR FOR                    01170004
                    SELECT CHR.CHARTC_CDE                               01221009
                         , CHVL.CHARTC_SEQ_NBR                          01222009
                         , CHVL.CHARTC_VAL_DESC                         01223009
                         , CHR.CHARTC_DESC                              01224009
                         , VSVL.PRIM_IND                                01225009
                      FROM TDCVSVL VSVL                                 01230004
                         , TVSCHVL CHVL                                 01240004
                         , TVSCHAR CHR                                  01250004
                     WHERE VSVL.MDSEAR_ID      = :IM040-MDSEAR-DKEY     01260007
                       AND CHVL.STAT_CDE       = 'A'                    01270004
                       AND CHVL.CHARTC_CDE     = VSVL.CHARTC_CDE        01280004
                       AND CHVL.CHARTC_SEQ_NBR = VSVL.CHARTC_SEQ_NBR    01290004
                       AND CHVL.CHARTC_CDE     = CHR.CHARTC_CDE         01300004
             WITH UR                                                    01310004
           END-EXEC.                                                    01320004
                                                                        01330004
      ******************************************************************01340000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01350000
      ******************************************************************01360000
           COPY DPWS004.                                                01370000
                                                                        01380000
      *                                                                 01390000
      ******************************************************************01400000
       PROCEDURE DIVISION.                                              01410000
      ******************************************************************01420000
      *                                                                 01430000
       0000-IMKUT215.                                                   01440000
      ******************************************************************01450000
      * MAIN PROCESSING ROUTINE                                        *01460000
      ******************************************************************01470000
                                                                        01480000
           PERFORM 1000-HOUSEKEEPING.                                   01490000
      *                                                                 01500000
           PERFORM 2000-PROCESS-MDSEAR-EXTR                             01510000
             UNTIL PS-END-OF-INPUT-FILE.                                01520000
      *                                                                 01530000
           PERFORM 1500-END-OF-JOB.                                     01540000
           GOBACK.                                                      01550000
      *                                                                 01560000
                                                                        01570000
       1000-HOUSEKEEPING.                                               01580000
                                                                        01590000
           MOVE '*1000-HOUSEKEEPING.              *' TO PV-PARA-NAME.   01600005
                                                                        01610000
           OPEN INPUT  MDSEAR-EXTR-IN-FILE                              01620000
                OUTPUT DEFAULT-MAINT-OUT-FILE.                          01630000
                                                                        01640000
           PERFORM 6000-READ-MDSEAR-EXT.                                01650000
           IF PS-END-OF-INPUT-FILE                                      01660000
               DISPLAY '********** IMKUT215 ***********'                01670000
               DISPLAY 'MDSEAR EXTRACT FILE IS EMPTY   '                01680005
               DISPLAY '*******************************'                01690000
           END-IF.                                                      01700000
                                                                        01710000
       1500-END-OF-JOB.                                                 01720000
                                                                        01730000
      ******************************************************************01740000
      *  CLOSE FILES AND DISPLAY COUNTS                                *01750000
      ******************************************************************01760000
                                                                        01770000
           CLOSE MDSEAR-EXTR-IN-FILE                                    01780000
                 DEFAULT-MAINT-OUT-FILE.                                01790000
                                                                        01800000
           DISPLAY '***************************'                        01810000
           DISPLAY 'END OF JOB STATS'                                   01820000
           DISPLAY '# OF RECS READ           : = ' PA-READ-COUNT.       01830005
           DISPLAY '# OF DEFAULT CSR FETCHES : = ' PA-DEF-MAINT-COUNT.  01840005
           DISPLAY '# OF RECS WRITTEN        : = ' PA-WRITE-COUNT.      01841005
           DISPLAY '***************************'.                       01850000
                                                                        01860000
      ******************************************************************01870004
      * MAIN PROCESS OF MDSEAR EXTRACT                                 *01880004
      ******************************************************************01890004
       2000-PROCESS-MDSEAR-EXTR.                                        01900000
           MOVE '*2000-PROCESS-MDSEAR-EXTR        *' TO PV-PARA-NAME.   01910005
                                                                        01920000
           PERFORM 7000-OPEN-DEFAULT-MAINT-CSR.                         01930004
                                                                        01940004
           MOVE 'N' TO PS-END-OF-DEF-MAINT-CSR-SW.                      01950004
                                                                        01960004
           PERFORM 3000-PROCESS-CURSOR                                  01970004
             UNTIL PS-END-OF-DEF-MAINT-CSR.                             01980004
                                                                        01990004
           PERFORM 6000-READ-MDSEAR-EXT.                                02000004
                                                                        02010004
      ******************************************************************02020004
      * PROCESS DEFAULT MAINTENANCE CURSOR                             *02030004
      ******************************************************************02040004
       3000-PROCESS-CURSOR.                                             02050004
           PERFORM 7200-FETCH-DEFAULT-MAINT-CSR.                        02060004
                                                                        02070004
           IF PS-END-OF-DEF-MAINT-CSR                                   02080004
               PERFORM 7400-CLOSE-DEFAULT-MAINT-CSR                     02090004
           ELSE                                                         02100004
               PERFORM 8000-WRITE-DEFAULT-MAINT-REC                     02110004
           END-IF.                                                      02120004
      *                                                                 02130000
      ******************************************************************02140005
      * READ SKU DELETES FILE.                                         *02150005
      ******************************************************************02160005
       6000-READ-MDSEAR-EXT.                                            02170000
           MOVE '*6000-READ-MDSEAR-EXT.          *' TO PV-PARA-NAME.    02180005
                                                                        02190005
           READ MDSEAR-EXTR-IN-FILE INTO IM040-DEFAULT-MAINT-EXTRACT    02200000
               AT END                                                   02210000
                   SET PS-END-OF-INPUT-FILE TO TRUE.                    02220004
                                                                        02230000
           IF PS-END-OF-INPUT-FILE                                      02240000
               CONTINUE                                                 02250005
           ELSE                                                         02260000
               ADD 1 TO  PA-READ-COUNT                                  02270000
           END-IF.                                                      02290000
      *                                                                 02310000
      *                                                                 02320000
      ******************************************************************02330004
      * 7000 OPEN DEFAULT_MAINT_CSR CURSOR                             *02340004
      ******************************************************************02350004
       7000-OPEN-DEFAULT-MAINT-CSR.                                     02360004
                                                                        02370004
           EXEC SQL                                                     02380004
               OPEN DEFAULT_MAINT_CSR                                   02390004
           END-EXEC.                                                    02400004
                                                                        02410004
           EVALUATE TRUE                                                02420004
               WHEN SQLCODE = 0                                         02430004
                 CONTINUE                                               02440004
               WHEN SQLCODE > 0                                         02450004
               WHEN SQLCODE < 0                                         02460004
                 PERFORM 9900-EVALUATE-SQLCODE                          02470004
           END-EVALUATE.                                                02480004
                                                                        02490004
      ******************************************************************02500004
      * 7200 FETCH DEFAULT MAINTENANCE CURSOR                           02510004
      ******************************************************************02520004
       7200-FETCH-DEFAULT-MAINT-CSR.                                    02530004
                                                                        02540004
           MOVE '*7200-FETCH-DEFAULT-MAINT-CSR. *' TO PV-PARA-NAME.     02550005
                                                                        02560004
           EXEC SQL                                                     02570004
               FETCH DEFAULT_MAINT_CSR                                  02580004
                INTO :IM040-VSCHAR-CHARTC-CDE                           02590010
                   , :IM040-VSCHVL-CHARTC-SEQ-NBR                       02600008
                   , :IM040-VSCHVL-CHARTC-VAL-DESC                      02610008
                   , :IM040-VSCHAR-CHARTC-DESC                          02620008
                   , :IM040-DCVSVL-PRIM-IND                             02630008
           END-EXEC.                                                    02640004
                                                                        02650004
           EVALUATE TRUE                                                02660004
               WHEN SQLCODE = 0                                         02670004
                 ADD 1 TO PA-DEF-MAINT-COUNT                            02680005
               WHEN SQLCODE = +100                                      02690004
                 SET PS-END-OF-DEF-MAINT-CSR TO TRUE                    02700004
               WHEN OTHER                                               02710004
                 PERFORM 9900-EVALUATE-SQLCODE                          02720004
           END-EVALUATE.                                                02730004
                                                                        02740004
      ******************************************************************02750004
      * 7400 CLOSE DEFAULT MAINTENANCE CURSOR                          *02760005
      ******************************************************************02770004
       7400-CLOSE-DEFAULT-MAINT-CSR.                                    02780004
                                                                        02790004
           EXEC SQL                                                     02800004
               CLOSE DEFAULT_MAINT_CSR                                  02810004
           END-EXEC.                                                    02820004
                                                                        02830004
      *                                                                 02840000
      ******************************************************************02850005
      * WRITE DEFAULT MAINTENANCE RECORDS                              *02860005
      ******************************************************************02870005
       8000-WRITE-DEFAULT-MAINT-REC.                                    02880004
           MOVE '*8000-DEFAULT-MAINT-REC.       *' TO PV-PARA-NAME.     02890005
                                                                        02900004
           WRITE DEFAULT-MAINT-OUT-REC  FROM                            02910004
                 IM040-DEFAULT-MAINT-EXTRACT.                           02920004
           ADD +1 TO PA-WRITE-COUNT.                                    02930004
                                                                        02940004
      ******************************************************************02950004
      * 9900 EVALUATE SQL CODE.                                        *02960004
      ******************************************************************02970004
       9900-EVALUATE-SQLCODE.                                           02980004
                                                                        02990004
           EVALUATE TRUE                                                03000004
              WHEN SQLCODE = +0                                         03010004
              WHEN SQLCODE = +100                                       03020004
                 CONTINUE                                               03030004
              WHEN OTHER                                                03040004
                 PERFORM 9980-SQL-ERROR                                 03050004
           END-EVALUATE.                                                03060004
                                                                        03070004
       9980-SQL-ERROR.                                                  03080000
      ******************************************************************03090000
      * ABEND ROUTINE FOR BAD SQL CALLS                                *03100000
      ******************************************************************03110000
           DISPLAY '** ABEND **          = SQL ERROR'.                  03120000
           DISPLAY '** PROGRAM **        = ' PC-PROGRAM-NAME.           03130000
           DISPLAY '** PARAGRAPH **      = ' PV-PARA-NAME.              03140005
      *                                                                 03150000
      ******************************************************************03160000
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-    03170000
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE         03180000
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE     03190000
      * FORMAT.                                                         03200000
      ******************************************************************03210000
           COPY DPPD004.                                                03220000
                                                                        03230000
             EXEC SQL                                                   03240000
                COMMIT                                                  03250000
             END-EXEC.                                                  03260000
      *                                                                 03270000
           MOVE +4013                      TO ABEND-CODE.               03280000
           CALL 'ILBOABN0' USING ABEND-CODE.                            03290000
                                                                        03300000
