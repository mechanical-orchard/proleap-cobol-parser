       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKUT211.                                       
       AUTHOR.                  LEE YANG.                                       
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            07/12/2001.                                     
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  THIS IS A RE-WRITE OF THE ORIGINAL INTRANSIT EXTRACTION PGM   *        
      *           NEW VERSION CREATED ON 5/10/2005                     *        
      ******************************************************************        
      *                                                                *        
      *  INVENTORY LEDGER STORE-EXPECTED REVERSAL ADJUSTMENT CREATION  *        
      *                                                                *        
      *  THIS PROGRAM WILL PROCESS AND CALCULATE THE NECESSARY DATA    *        
      *  TO BUILD THE REVERSAL ADJUSTMENT TRANSACTIONS FOR INVENTORY   *        
      *  LEDGER AND THE CUTOFF DATABASE USING THE STORE-EXPECTED UNITS *        
      *  THIS PROGRAM WILL ONLY CONTAIN DATA FOR MANIFESTED STORES.    *        
      *                                                                *        
      *  THE INPUT TO THIS PROGRAM WILL BE THE INVENTORY DATE          *        
      *  IN THE FORMAT OF CCYY-MM-DD.  FOR EXAMPLE: '2005-06-20'       *        
      *                                                                *        
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE       PGMR        DESCRIPTION OF CHANGES                  
      * -------  ---------- ----------- --------------------------------        
      * WR28543  05-10-2005 S JACKSON   INSTALL RE-WRITTEN VERSION              
800497*P00800497 09-13-2005 R. JANSSEN  ISSUE INSERTING MISSING SKUS    00119027
W28545* WR28545  03-17-2006 J. BLAZICH  SELECT STORES THAT ARE NOT YET          
W28545*                                 OPEN FOR ADJUSTMENTS                    
P28545* 28545    04-11-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE            
P28545*                                 SETUP                                   
W33304* W33304   04-04-2008 CHUCK EBERT HOLD INVENTORY LEDGER OPEN              
W33304*                                 ACROSS PERIODS                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INV-DATE-CNTL-FILE                                            
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORE-EXPECTED-EXTRACT                                        
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT STORE-EXPECTED-TINSCLS-FMT                                    
               ASSIGN TO OUT01.                                                 
                                                                                
           SELECT STORE-EXPECTED-CUTOFF-FMT                                     
               ASSIGN TO OUT02.                                                 
                                                                                
           SELECT ERROR-FILE                                                    
               ASSIGN TO OUT03.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INV-DATE-CNTL-FILE                                                   
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  INV-DATE-CNTL-REC               PIC X(80).                           
                                                                                
       FD  STORE-EXPECTED-EXTRACT                                               
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-RECORD           PIC X(100).                          
                                                                                
       FD  STORE-EXPECTED-TINSCLS-FMT                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-TINSCLS-REC       PIC X(100).                         
                                                                                
       FD  STORE-EXPECTED-CUTOFF-FMT                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  STORE-EXPECTED-CUTOFF-REC       PIC X(56).                           
                                                                                
       FD  ERROR-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ERROR-REC                       PIC X(100).                          
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-999                  PIC 9(03)    VALUE 999.                  
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INPUT-LOC            PIC  9(5)    VALUE ZERO.                 
           05  PV-HOLD-LOC-NBR         PIC S9(4)    VALUE ZERO COMP.            
           05  PV-PREV-LOC-NBR         PIC S9(4)    VALUE ZERO COMP.            
           05  PV-HOLD-KEY-DATE        PIC X(10)    VALUE SPACES.               
           05  PV-PLND-INV-DTE         PIC X(10)    VALUE SPACES.               
           05  PV-STR-NBR              PIC S9(4)    VALUE ZERO COMP.            
           05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.               
           05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.               
P28545     05  PV-HOLD-AREA.                                                    
P28545         10  PV-HOLD-DEPT-NBR       PIC 9(04) VALUE ZERO.                 
P28545         10  PV-HOLD-MCL-NBR        PIC 9(04) VALUE ZERO.                 
P28545         10  PV-HOLD-SCL-NBR        PIC 9(04) VALUE ZERO.                 
P28545         10  PV-HOLD-SKU-NBR        PIC 9(08) VALUE ZERO.                 
P28545         10  PV-HOLD-UPC-NBR        PIC 9(13) VALUE ZERO.                 
      *                                                                         
       01  SA-SAVE-AREAS.                                                       
           05  SA-SAVE-VARIABLE        PIC X(04)    VALUE SPACES.               
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW       PIC X        VALUE 'N'.                  
               88  PS-END-OF-FILE                   VALUE 'Y'.                  
               88  PS-NOT-END-OF-FILE               VALUE 'N'.                  
           05  PS-VALID-LOC-SW         PIC X        VALUE 'N'.                  
               88  PS-VALID-LOCATION                VALUE 'Y'.                  
               88  PS-INVALID-LOCATION              VALUE 'N'.                  
W33304     05  PS-END-OF-CSR-SW        PIC X        VALUE SPACES.               
W33304         88  PS-END-OF-CSR                    VALUE 'Y'.                  
W33304                                                                          
      *                                                                         
W33304 01  TABLE-AREA.                                                          
W33304******************************************************************        
W33304*     VALID CONTROL TABLE                                                 
W33304******************************************************************        
W33304     10  TABLE-MAX.                                                       
W33304         15  PARM-TABLE-MAX        PIC 9(5)       VALUE 32767.            
W33304                                                                          
W33304     10  TABLE-PARMS.                                                     
W33304         15  TINVPAR-TBL-SIZE      PIC 9(05)      VALUE ZEROES.           
W33304                                                                          
W33304     10  TINVPAR-TABLE.                                                   
W33304         15  TINVPAR-ENTRY    OCCURS 32767 TIMES                          
W33304                              ASCENDING KEY IS PARM-LOC-NBR               
W33304                              INDEXED BY PARM-INDEX.                      
W33304             20  PARM-LOC-NBR      PIC 9(5).                              
W33304             20  PARM-FSCL-MN-DTE  PIC X(10).                             
W33304             20  PARM-FSCL-MN-NBR  PIC X(02).                             
W33304                                                                          
      ***  I/O FORMAT                                                           
           COPY INRD137.                                                        
      *****************************************************************         
      *  INVENTORY STORE-EXPECTED EXTRACT FILE LAYOUT - WORK AREA               
      ***TO BE USED AGAIN AS THE OUTPUT RECORD LAYOUT AS WELL AS INPUT          
      *****************************************************************         
           COPY INRD137 REPLACING IN137 BY INXXX.                               
      *                                                                         
      ***  CUTOFF DATABASE UPDATE FORMAT                                        
           COPY INRD050.                                                        
      *                                                                         
      ***  COMMON PRICING MODULE                                                
           COPY INWS400.                                                        
      *                                                                         
      *****  SQL ABEND ROUTINE WORKING STORAGE                                  
           COPY DPWS004.                                                        
           EJECT                                                                
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
P28545     EXEC SQL                                                             
P28545         INCLUDE TINCNTL                                                  
P28545     END-EXEC.                                                            
P28545                                                                          
P28545*****************************************************************         
P28545* TSKXREF DCLGEN - THIS TABLE IS CALLED IN THE PRICING MODULE             
P28545*****************************************************************         
P28545*    EXEC SQL                                                             
P28545*       INCLUDE TSKXREF                                                   
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545*****************************************************************         
P28545* TUPC DCLGEN - THIS TABLE IS CALLED IN THE PRICING MODULE                
P28545*****************************************************************         
P28545*    EXEC SQL                                                             
P28545*       INCLUDE TUPC                                                      
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545*****************************************************************         
P28545* TUPCPLS DCLGEN - THIS TABLE IS CALLED IN THE PRICING MODULE             
P28545*****************************************************************         
P28545*    EXEC SQL                                                             
P28545*       INCLUDE TUPCPLS                                                   
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545*****************************************************************         
P28545* TMITGPL DCLGEN - THIS TABLE IS CALLED IN THE PRICING MODULE             
P28545*****************************************************************         
P28545*    EXEC SQL                                                             
P28545*       INCLUDE TMITGPL                                                   
P28545*    END-EXEC.                                                            
P28545*                                                                         
P28545*****************************************************************         
P28545* TMEITGP DCLGEN - THIS TABLE IS CALLED IN THE PRICING MODULE             
P28545*****************************************************************         
P28545*    EXEC SQL                                                             
P28545*       INCLUDE TMEITGP                                                   
P28545*    END-EXEC.                                                            
P28545*                                                                         
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         INCLUDE XMT00002                                                 
W28545     END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
                                                                                
       01  ABEND-CODE               PIC S9(04)  VALUE ZERO                      
                                                COMP SYNC.                      
W33304     EXEC SQL                                                             
W33304          DECLARE TINVPAR_CSR CURSOR FOR                                  
W33304           SELECT A.LOC_NBR                                               
W33304                 ,B.OPN_FSCL_MN_DTE                                       
W33304                 ,B.OPN_FSCL_MN_NBR                                       
W33304             FROM TINVPAR A                                               
W33304                 ,TINCNTL B                                               
W33304           WHERE A.ACTL_FIN_BK_DTE  = '9999-09-09'                        
W33304             AND A.LOC_INV_STAT_CDE = 'IN'                                
W33304             AND A.INV_ID           = B.INV_ID                            
W33304             AND B.ACTV_IND         = 'Y'                                 
W33304           ORDER BY LOC_NBR                                               
W33304     END-EXEC.                                                            
           EJECT                                                                
       LINKAGE SECTION.                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       1000-MAIN-MODULE.                                                        
                                                                                
           PERFORM 1100-OPEN-FILES.                                             
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
W33304     PERFORM 6100-LOAD-TINVPAR-TBL.                                       
                                                                                
           PERFORM 2000-PROCESS-FILE UNTIL                                      
               PS-END-OF-FILE.                                                  
                                                                                
           PERFORM 1900-CLOSE-FILES.                                            
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1100-OPEN-FILES.                                                         
      *                                                                         
           OPEN INPUT  INV-DATE-CNTL-FILE                                       
                       STORE-EXPECTED-EXTRACT                                   
                OUTPUT STORE-EXPECTED-TINSCLS-FMT                               
                       STORE-EXPECTED-CUTOFF-FMT                                
                       ERROR-FILE.                                              
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       1900-CLOSE-FILES.                                                        
      *                                                                         
           CLOSE INV-DATE-CNTL-FILE                                             
                 STORE-EXPECTED-EXTRACT                                         
                 STORE-EXPECTED-TINSCLS-FMT                                     
                 STORE-EXPECTED-CUTOFF-FMT                                      
                 ERROR-FILE.                                                    
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       2000-PROCESS-FILE.                                                       
                                                                                
P28545     INITIALIZE IN400-PV-INPUT-AREA.                                      
P28545                                                                          
           INITIALIZE INXXX-STR-EXPTED-RECORD.                                  
           MOVE IN137-STR-EXPTED-RECORD TO                                      
                INXXX-STR-EXPTED-RECORD.                                        
                                                                                
           MOVE IN137-STORE-NBR-9  TO PV-INPUT-LOC                              
           MOVE PV-INPUT-LOC       TO PV-HOLD-LOC-NBR                           
                                      IN400-PV-LOC-IN                           
           MOVE IN137-ADJ-DATE     TO PV-HOLD-KEY-DATE                          
P28545                                IN400-PV-EFF-DTE-IN.                      
                                                                                
           IF PV-HOLD-LOC-NBR = PV-PREV-LOC-NBR                                 
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 6200-SELECT-XMTLOC                                       
           END-IF.                                                              
                                                                                
           IF PS-VALID-LOCATION                                                 
               IF IN137-INTR-UPC-ID > 0                                         
P28545             MOVE IN137-INTR-UPC-ID TO IN400-PV-UPC-ID-IN                 
                   SET IN400-PV-INTR-UPC-ID-LOOKUP TO TRUE                      
               ELSE                                                             
                   IF IN137-SKU-9 > 0                                           
P28545                 MOVE IN137-SKU-9   TO IN400-PV-SKU-IN                    
                       SET IN400-PV-SKU-LOOKUP TO TRUE                          
                   END-IF                                                       
               END-IF                                                           
                                                                                
               PERFORM IN400-LOOKUP-RETAIL                                      
               IF IN400-PS-RTL-FOUND                                            
      *            MOVE LOOKUP VALUES TO THE CORRESPONDING OUTPUT FIELD         
      *                                                                         
W33304D            MOVE MLCNTL-OPN-FSCL-MN-DTE TO INXXX-FSCL-MN-END-DTE         
W33304D            MOVE MLCNTL-OPN-FSCL-MN-NBR TO INXXX-FSCL-MN-NBR             
W33304D                                                                         
                   PERFORM 3000-FORMAT-TINSCLS-RECORD                           
                   PERFORM 5000-WRITE-TINSCLS-RECORD                            
      *                                                                         
                   PERFORM 3100-FORMAT-CUTOFF-RECORD                            
                   PERFORM 5100-WRITE-CUTOFF-RECORD                             
      *                                                                         
               ELSE                                                             
P28545             DISPLAY '** INVALID 1: ' IN137-INTR-UPC-ID,                  
P28545                     ' SKU        : ' IN137-SKU-9,                        
P28545                     ' STORE      : ' IN137-STORE-NBR-9,                  
P28545                     ' DATE       : ' IN137-ADJ-DATE                      
P28545             DISPLAY IN400-PV-PARAGRAPH                                   
                   PERFORM 5200-WRITE-ERROR-RECORD                              
               END-IF                                                           
           ELSE                                                                 
P28545         DISPLAY '** INVALID 2: ' IN137-INTR-UPC-ID,                      
P28545                 ' SKU        : ' IN137-SKU-9,                            
P28545                 ' STORE      : ' IN137-STORE-NBR-9,                      
P28545                 ' DATE       : ' IN137-ADJ-DATE                          
P28545         DISPLAY IN400-PV-PARAGRAPH                                       
               PERFORM 5200-WRITE-ERROR-RECORD                                  
           END-IF.                                                              
      *                                                                         
           PERFORM 4000-READ-INPUT-FILE.                                        
      *                                                                         
      ******************************************************************        
      * 3000-FORMAT-TINSCLS-RECORD                                              
      ******************************************************************        
       3000-FORMAT-TINSCLS-RECORD.                                              
      *                                                                         
P28545     MOVE IN400-PRC-SKU-OUT        TO PV-HOLD-SKU-NBR                     
P28545     MOVE PV-HOLD-SKU-NBR          TO INXXX-SKU-9                         
P28545                                                                          
P28545     MOVE IN400-ATT-DEPT-OUT       TO PV-HOLD-DEPT-NBR                    
P28545     MOVE PV-HOLD-DEPT-NBR         TO INXXX-DEPT-9                        
P28545                                                                          
P28545     MOVE IN400-ATT-SCL-OUT        TO PV-HOLD-SCL-NBR                     
P28545     MOVE PV-HOLD-SCL-NBR          TO INXXX-CLASS-9                       
P28545                                                                          
P28545     MOVE IN400-PRC-UNIT-RTL-OUT   TO INXXX-RTL-AMT                       
      *                                                                         
P28545     COMPUTE IN400-PRC-EXTD-RTL-OUT ROUNDED =                             
P28545            (IN400-PRC-UNIT-RTL-OUT * IN137-BAL-QTY)                      
P28545     MOVE IN400-PRC-EXTD-RTL-OUT   TO INXXX-EXTD-RTL-AMT.                 
      *                                                                         
      ******************************************************************        
      * 3100-FORMAT-CUTOFF-RECORD                                               
      ******************************************************************        
       3100-FORMAT-CUTOFF-RECORD.                                               
      *                                                                         
           MOVE IN137-STORE-NBR-9       TO IN050-LOC-NBR.                       
P28545                                                                          
P28545     MOVE IN400-PRC-SKU-OUT        TO PV-HOLD-SKU-NBR                     
P28545     MOVE PV-HOLD-SKU-NBR          TO IN050-SKU-NBR.                      
P28545                                                                          
           MOVE ZEROES                  TO IN050-CTOFF-OH-QTY                   
                                           IN050-IN-TRNST-CTOFF-QTY             
                                           IN050-POST-FSH-RCPT-QTY              
                                           IN050-OLD-CTOFF-OH-QTY               
                                           IN050-OLD-IN-TRNST-CTOFF-QTY         
L27808                                     IN050-OLD-STR-EXPTED-OH-QTY.         
           MOVE IN137-ADJ-DATE          TO IN050-PROCESS-DATE.                  
      *                                                                         
      *    SETS IN050-ITEM-CTOFF-TAKEN-IND AS IN050-ITEM-CTOFF-EXISTS           
           MOVE 1                       TO IN050-ITEM-CTOFF-TAKEN-IND.          
      *                                                                         
           MOVE IN137-BAL-QTY           TO IN050-STR-EXPTED-OH-QTY.             
      *                                                                         
      *                                                                         
      ******************************************************************        
      * 4000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       4000-READ-INPUT-FILE.                                                    
                                                                                
           READ STORE-EXPECTED-EXTRACT                                          
               INTO IN137-STR-EXPTED-RECORD                                     
                   AT END                                                       
                       MOVE 'Y' TO PS-END-OF-FILE-SW.                           
      *                                                                         
      ******************************************************************        
      * 5000-WRITE-TINSCLS-RECORD                                               
      ******************************************************************        
       5000-WRITE-TINSCLS-RECORD.                                               
      *                                                                         
           WRITE STORE-EXPECTED-TINSCLS-REC FROM                                
                 INXXX-STR-EXPTED-RECORD.                                       
      *                                                                         
      ******************************************************************        
      * 5100-WRITE-CUTOFF-RECORD                                                
      ******************************************************************        
       5100-WRITE-CUTOFF-RECORD.                                                
      *                                                                         
           WRITE STORE-EXPECTED-CUTOFF-REC FROM                                 
                 IN050-CTOFF-CON-EXTR-RECORD.                                   
      *                                                                         
      ******************************************************************        
      * 5200-WRITE-ERROR-RECORD                                                 
      ******************************************************************        
       5200-WRITE-ERROR-RECORD.                                                 
      *                                                                         
           WRITE ERROR-REC FROM                                                 
                 INXXX-STR-EXPTED-RECORD.                                       
      *                                                                         
W33304******************************************************************        
W33304*  LOAD WORKING-STORAGE LOCATION TABLE WITH VALID LOCATIONS.              
W33304******************************************************************        
W33304 6100-LOAD-TINVPAR-TBL.                                                   
W33304                                                                          
W33304     MOVE '6100-LOAD-TINVPAR-TBL' TO PV-PARAGRAPH-NAME.                   
W33304     EXEC SQL                                                             
W33304         OPEN TINVPAR_CSR                                                 
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE SQLCODE                                                     
W33304        WHEN +0                                                           
W33304          CONTINUE                                                        
W33304        WHEN OTHER                                                        
W33304          MOVE 'OPEN TINVPAR CURSOR' TO                                   
W33304              PV-DB2-OPER-ATTEMPTED                                       
W33304          PERFORM 9900-SQL-ABEND                                          
W33304     END-EVALUATE.                                                        
W33304                                                                          
W33304     MOVE ZERO TO TINVPAR-TBL-SIZE.                                       
W33304     MOVE 'N' TO PS-END-OF-CSR-SW.                                        
W33304     PERFORM 6150-FETCH-PARM-CSR.                                         
W33304                                                                          
W33304     PERFORM VARYING PARM-INDEX FROM 1 BY 1                               
W33304                       UNTIL PARM-INDEX > PARM-TABLE-MAX                  
W33304                          OR PS-END-OF-CSR                                
W33304       MOVE INVPAR-LOC-NBR         TO  PARM-LOC-NBR(PARM-INDEX)           
W33304       MOVE INCNTL-OPN-FSCL-MN-NBR TO                                     
W33304                                   PARM-FSCL-MN-NBR(PARM-INDEX)           
W33304       MOVE INCNTL-OPN-FSCL-MN-DTE TO                                     
W33304                                   PARM-FSCL-MN-DTE(PARM-INDEX)           
W33304       ADD 1 TO TINVPAR-TBL-SIZE                                          
W33304       PERFORM 6150-FETCH-PARM-CSR                                        
W33304     END-PERFORM.                                                         
W33304                                                                          
W33304     IF PARM-INDEX > PARM-TABLE-MAX                                       
W33304       DISPLAY '*********** ABEND ************************'               
W33304       DISPLAY ' TABLE OVERFLOW ON PARMS-TABLE            '               
W33304       DISPLAY ' INCREASE PARM-TABLE-MAX LIMIT            '               
W33304       DISPLAY ' CURRENT LIMIT = ' PARM-TABLE-MAX                         
W33304       DISPLAY '******************************************'               
W33304       DISPLAY '  '                                                       
W33304       MOVE +4004 TO ABEND-CODE                                           
W33304       CALL 'ILBOABN0' USING ABEND-CODE                                   
W33304     END-IF.                                                              
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         CLOSE TINVPAR_CSR                                                
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE SQLCODE                                                     
W33304        WHEN +0                                                           
W33304          CONTINUE                                                        
W33304        WHEN OTHER                                                        
W33304          MOVE 'CLOSE TINVPAR CURSOR' TO                                  
W33304              PV-DB2-OPER-ATTEMPTED                                       
W33304          PERFORM 9900-SQL-ABEND                                          
W33304     END-EVALUATE.                                                        
W33304                                                                          
W33304 6150-FETCH-PARM-CSR.                                                     
W33304                                                                          
W33304     MOVE '6150-FETCH-PARM-CSR' TO PV-PARAGRAPH-NAME.                     
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         FETCH TINVPAR_CSR                                                
W33304         INTO :INVPAR-LOC-NBR                                             
W33304             ,:INCNTL-OPN-FSCL-MN-DTE                                     
W33304             ,:INCNTL-OPN-FSCL-MN-NBR                                     
W33304     END-EXEC.                                                            
W33304                                                                          
W33304     EVALUATE SQLCODE                                                     
W33304       WHEN +0                                                            
W33304             CONTINUE                                                     
W33304       WHEN +100                                                          
W33304             SET PS-END-OF-CSR TO TRUE                                    
W33304       WHEN OTHER                                                         
W33304             MOVE 'FETCH TINVPAR CURSOR' TO                               
W33304                  PV-DB2-OPER-ATTEMPTED                                   
W33304             PERFORM 9900-SQL-ABEND                                       
W33304     END-EVALUATE.                                                        
                                                                                
      *                                                                         
      ******************************************************************        
      * 6200-SELECT-XMTLOC                                                      
      ******************************************************************        
       6200-SELECT-XMTLOC.                                                      
                                                                                
           MOVE PV-HOLD-LOC-NBR TO PV-PREV-LOC-NBR.                             
                                                                                
W28545     EXEC SQL                                                             
W28545          SELECT A.LOC_NBR                                                
W28545            INTO :XMT00001-LOC-NBR                                        
W28545            FROM XMT_LOC             A                                    
W28545                ,XMT_DC_LOC_ASGMT    C                                    
W28545           WHERE A.LOC_NBR = :PV-HOLD-LOC-NBR                             
W28545             AND A.LOC_NBR = C.LOC_NBR                                    
W28545             AND C.EFF_BEG_DTE <= :PV-HOLD-KEY-DATE                       
W28545             AND A.CLO_DTE >= :PV-HOLD-KEY-DATE                           
W28545             AND A.FINCL_CLO_DTE = '9999-09-09'                           
W28545             AND C.EFF_BEG_DTE =                                          
W28545                 (SELECT MIN(B.EFF_BEG_DTE)                               
W28545                     FROM XMT_DC_LOC_ASGMT    B                           
W28545                    WHERE B.LOC_NBR = :PV-HOLD-LOC-NBR)                   
W28545           WITH UR                                                        
W28545     END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
W33304D            SET PS-VALID-LOCATION TO TRUE                                
W33304             PERFORM 6300-VALIDATE-INV-LOC                                
               WHEN SQLCODE = +100                                              
                   SET PS-INVALID-LOCATION TO TRUE                              
               WHEN OTHER                                                       
W33304             MOVE '6200-SELECT-XMTLOC'   TO PV-PARAGRAPH-NAME             
W33304             MOVE 'SELECT ROW FROM XMT_LOC'                               
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9900-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      *                                                                         
W33304******************************************************************        
W33304*  FIND THE LOCATION NUMBER IN THE INTERNAL PARMS TABLE AND GET           
W33304*  THE INVENTORY CONTROL FISAL MONTH DATA.                                
W33304******************************************************************        
W33304 6300-VALIDATE-INV-LOC.                                                   
W33304                                                                          
W33304     MOVE '6300-VALIDATE-INV-LOC' TO PV-PARAGRAPH-NAME.                   
W33304                                                                          
W33304     SET PARM-INDEX TO 1.                                                 
W33304     SEARCH TINVPAR-ENTRY                                                 
W33304       AT END                                                             
W33304           SET PS-INVALID-LOCATION TO TRUE                                
W33304       WHEN PARM-LOC-NBR(PARM-INDEX) = PV-INPUT-LOC                       
W33304           SET PS-VALID-LOCATION TO TRUE                                  
W33304           MOVE PARM-FSCL-MN-DTE(PARM-INDEX) TO                           
W33304                           INXXX-FSCL-MN-END-DTE                          
W33304           MOVE PARM-FSCL-MN-NBR(PARM-INDEX) TO                           
W33304                           INXXX-FSCL-MN-NBR                              
W33304       WHEN PARM-INDEX > TINVPAR-TBL-SIZE                                 
W33304           SET PS-INVALID-LOCATION TO TRUE                                
W33304     END-SEARCH.                                                          
W33304                                                                          
      ******************************************************************        
      *  STANDARD DB2 ERROR ABEND ROUTINE                                       
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.                     
      ******************************************************************        
       9900-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKUT211'.                             
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *IN400-LOOKUP-BY-SKU.                                                     
      *IN400-LOOKUP-BY-UPC-NBR.                                                 
      *IN400-LOOKUP-BY-UPC-ID.                                                  
      *IN400-LOOKUP-BY-ITM-NBR.                                                 
           COPY INPD400.                                                        
                                                                                
26006 *ML501-LOOKUP-VENDOR.                                                     
26006 *ML501-LOOKUP-DEFAULT.                                                    
26006 *ML501-LOOKUP-BY-SKU.                                                     
26006 *ML501-LOOKUP-BY-UPC-NBR.                                                 
26006 *ML501-LOOKUP-BY-UPC-ID.                                                  
26006 *ML501-LOOKUP-BY-ITM-NBR.                                                 
26006 *ML501-LOOKUP-BY-INHOUSE.                                                 
26006 *    COPY MLPD501.                                                        
                                                                                
                                                                                
