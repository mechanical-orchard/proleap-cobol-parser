       ID DIVISION.                                                             
       PROGRAM-ID.      VAKRP011.                                               
       AUTHOR.          INFOSYS.                                                
       DATE-WRITTEN     OCTOBER 28 2008.                                        
       DATE-COMPILED.                                                           
      ******************************************************************        
      * THIS PROGRAM USES JULIAN DATE TO ASSIGN BATCH NUMBER (RCPTS)   *        
      *    JULIAN DATE SELECTED FOR SAT WK-END-DTE.                    *        
      *       ADD 1   DAY  FOR RECEIPTS  (SAT PLUS 1 DAY)              *        
      *       ADD 499 DAYS FOR PURCHASE ADJ                            *        
      *                                                                *        
      *  ** IF AP RUNS INTO NEXT DAY, MAY NEED TO ADD 500 DAYS FOR RPT *        
      *                                                                *        
      ******************************************************************        
      * THIS PROGRAM REPORTS ON THE PURCHASES, TRANSFERS, MARKDOWNS,AND*        
      * MARKUPS WHICH WILL BE INPUT INTO THE MERCHANDISE LEDGER UPDATE.*        
      * THE REPORT WILL LIST TOTALS BY TYPE AND BATCH NUMBER FOR ALL   *        
      * TRANSACTIONS WHICH SURVIVED THE EDIT AND REFORMAT.             *        
      *                                                                *        
      * THIS PROGRAM IS COPIED FROM MLKRP004.                          *        
      *                                                                *        
      * WR#33304   NIJESH/INFOSYS  10/28/2008                          *        
      *            NEW PROGRAM WRIITEN AS PART OF HOLDING INVENTORY    *        
      *            LEDGER OPEN (HILO).                                 *        
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
           SELECT  BATCH-FILE               ASSIGN INP01.                       
           SELECT  LEDGER-CONTROL-FILE      ASSIGN INP02.                       
           SELECT  DATE-CONTROL-FILE        ASSIGN INP03.                       
           SELECT  REPORT-FILE              ASSIGN RPT01.                       
           SELECT  REPORT-FILE-2            ASSIGN RPT02.                       
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
       FILE SECTION.                                                            
       FD  BATCH-FILE                                                           
           RECORDING MODE F                                                     
           LABEL RECORDS STANDARD                                               
           BLOCK CONTAINS  0 RECORDS.                                           
       01  BATCH-RECORD                     PIC X(150).                         
      *                                                                         
       FD  LEDGER-CONTROL-FILE                                                  
           RECORDING MODE F                                                     
           LABEL RECORDS STANDARD                                               
           BLOCK CONTAINS  0 RECORDS.                                           
       01  LEDGER-CONTROL-RECORD            PIC X(80).                          
      *                                                                         
       FD  DATE-CONTROL-FILE                                                    
           RECORDING MODE F                                                     
           LABEL RECORDS STANDARD                                               
           BLOCK CONTAINS  0 RECORDS.                                           
       01  DATE-CONTROL-RECORD              PIC X(80).                          
      *                                                                         
       FD  REPORT-FILE                                                          
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  REPORT-RECORD.                                                       
           05  REPORT-LINE                  PIC X(132).                         
      *                                                                         
       FD  REPORT-FILE-2                                                        
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED                                                
           BLOCK CONTAINS  0 RECORDS.                                           
       01  REPORT-RECORD-2.                                                     
           05  REPORT-LINE-2                PIC X(132).                         
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
      ******************************************************************        
      *  STANDARD HEADING FORMAT AREA.                                 *        
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      ******************************************************************        
      *  INPUT FILE STRUCTURE AREA                                     *        
      ******************************************************************        
           COPY INRD053.                                                        
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004       *        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  COPYBOOK REQUIRED TO GET DP085-GET-CURR-JOB-INFO-PARAMETER    *        
      ******************************************************************        
           COPY DPWS085.                                                        
                                                                                
      ******************************************************************        
      *    USED FOR SQL COMMUNICATION AREA.                            *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TDTATTR                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *    DCLGEN FOR TINCNTL                                          *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
       01  LC-LEDGER-CONTROL-RECORD.                                            
           05  LC-LEDGER-IND                PIC X(01).                          
               88  LC-MERCH-LEDGER                        VALUE 'M'.            
               88  LC-INV-LEDGER                          VALUE 'I'.            
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  LC-OVERRIDE-IND              PIC X(01)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  LC-OVERRIDE-BATCH-NO         PIC X(03)     VALUE SPACES.         
           05  FILLER                       PIC X(73).                          
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                        COMP-3.               
           05  PA-RETAIL                    PIC S9(11)V99 VALUE ZERO.           
           05  PA-COST                      PIC S9(11)V99 VALUE ZERO.           
           05  PA-CODE-RETAIL               PIC S9(11)V99 VALUE ZERO.           
           05  PA-CODE-COST                 PIC S9(11)V99 VALUE ZERO.           
           05  PA-TOTAL-RETAIL              PIC S9(11)V99 VALUE ZERO.           
           05  PA-TOTAL-COST                PIC S9(11)V99 VALUE ZERO.           
           05  PA-GRAND-RETAIL              PIC S9(11)V99 VALUE ZERO.           
           05  PA-GRAND-COST                PIC S9(11)V99 VALUE ZERO.           
           05  PA-RECORD-COUNT              PIC S9(9)     VALUE ZERO.           
      *                                                                         
       01  PA2-PROGRAM-ACCUMULATORS                       COMP-3.               
      * PURCHASE ADJ TRANSACTIONS/ CODE 01=RECEIPT /CURR OR PRIOR WEEK          
           05  PA2-PRCH-ADJ-RECPT-CURR      PIC S9(10)V99 VALUE ZEROS.          
      * RECEIPT TRANSACTIONS/CODE 01=RECEIPT/CURR                               
           05  PA2-RECEIPTS-CURR            PIC S9(10)V99 VALUE ZEROS.          
      * TOTAL ACCUMULATOR (PURCH ADJ/RECPTS / TOTAL RECPTS/ADJ CURRENT)         
           05  PA2-TOTAL-CURR-RECEIPTS      PIC S9(11)V99 VALUE ZEROS.          
      *                                                                         
       01  PC-PRINT-CONTROL                               COMP-3.               
            05  PC-LINE-COUNT               PIC S9(09)    VALUE +56.            
            05  PC-PAGE-COUNT               PIC S9(05)    VALUE ZERO.           
            05  PC-PAGE-COUNT2              PIC S9(05)    VALUE ZERO.           
            05  PC-LINE-MAX                 PIC S9(09)    VALUE +55.            
            05  PC-ADVANCE                  PIC S9(03)    VALUE ZERO.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
            05  PS-CUTOFF-WEEK-SW           PIC X(01)     VALUE 'N'.            
                88  PS-CUTOFF-WEEK-DATE                   VALUE 'Y'.            
                88  PS-END-OF-WEEK-DATE                   VALUE 'N'.            
            05  PS-BATCH-FOUND-SW           PIC X(01)     VALUE 'N'.            
                88  PS-BATCH-FOUND-YES                    VALUE 'Y'.            
                88  PS-BATCH-FOUND-NO                     VALUE 'N'.            
            05  PS-FIRST-BATCH-SW           PIC X(01)     VALUE 'Y'.            
                88  PS-FIRST-BATCH-YES                    VALUE 'Y'.            
                88  PS-FIRST-BATCH-NO                     VALUE 'N'.            
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
            05  PC-MERCH-LEDGER-LIT         PIC X(20)     VALUE                 
                                                '(MERCHANDISE LEDGER)'.         
            05  PC-INV-LEDGER-LIT           PIC X(20)     VALUE                 
                                                ' (INVENTORY LEDGER) '.         
            05  PC-TITLE-BATCH              PIC X(33)     VALUE                 
                                   'WEEKLY BATCH TOTALS BEFORE UPDATE'.         
            05  PC-TITLE-RECEIPTS           PIC X(33)     VALUE                 
                                   'MULTIPLE FLUSH REPORT            '.         
            05  PC-RECEIPTS-HEADING         PIC X(13)     VALUE                 
                                                       'RECEIPTS     '.         
            05  PC-RCPTS-AND-ADJ            PIC X(31)     VALUE                 
                                     'VERIFIED RECEIPTS & RECEIPT ADJ'.         
            05  PC-PRCH-ADJ-HEADING         PIC X(13)     VALUE                 
                                                        'PURCHASE ADJ'.         
            05  PC-PRCH-ADJ                 PIC X(20)     VALUE                 
                                                'PURCHASE ADJUSTMENTS'.         
            05  PC-CURR-WEEK-HDR            PIC X(26)     VALUE                 
                                          'VERIFIED FOR THE WEEK OF  '.         
            05  PC-MAX-RETRIES              PIC S9(04)    COMP SYNC             
                                                          VALUE +3.             
                                                                                
       01  PC-TRAN-TYPES.                                                       
            05  BATCH-RECEIPTS              PIC 9(03)     VALUE ZEROES.         
            05  BATCH-PURCHASE-ADJ          PIC 9(03)     VALUE ZEROES.         
            05  PC-AP-CODES                 PIC X(02).                          
                88 RECEIPTS-AND-ADJ                       VALUE '01'.           
      * SYSIN CONTROL-CARD                                                      
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                       PIC X(03)     VALUE SPACES.         
           05  SYSIN-DATE                   PIC X(10)     VALUE ZERO.           
      *                                                                         
       01  CC-JULIAN-DATES.                                                     
           05  CC-JULIAN-DATE-SIGN          PIC S9(07)    VALUE ZEROES.         
           05  CC-JULIAN-DATE-9             PIC 9(07)     VALUE ZEROES.         
           05  CC-JULIAN-DATE.                                                  
               10  CC-JULIAN-YEAR           PIC 9(04)     VALUE ZEROES.         
               10  CC-JULIAN-DAY            PIC 9(03)     VALUE ZEROES.         
           05  CC-JULIAN-PLUS-500           PIC 9(03)     VALUE ZEROES.         
           05  CC-JULIAN-PLUS-499           PIC 9(03)     VALUE ZEROES.         
           05  CC-JULIAN-PLUS1              PIC 9(03)     VALUE ZEROES.         
      *                                                                         
       01  SA-SAVE-AREAS.                                                       
           05  SA-BATCH                     PIC X(04)     VALUE SPACES.         
           05  SA-BATCH-2                   REDEFINES SA-BATCH.                 
               10  FILLER                   PIC X(01).                          
               10  SA-SHORT-BATCH           PIC X(03).                          
           05  SA-BATCH-CONVERT             PIC X(04)     VALUE SPACES.         
           05  SA-BATCH-NUMERIC             REDEFINES SA-BATCH-CONVERT.         
               10  FILLER                   PIC X(01).                          
               10  SA-BATCH-NUM             PIC 9(03).                          
           05  SA-CODE                      PIC 9(02)     VALUE ZERO.           
           05  SA-PURCH-BATCH.                                                  
               10  SA-P-BATCH               PIC 9(03)     VALUE ZERO.           
               10  FILLER                   PIC X(01)     VALUE SPACES.         
           05  SA-DATE.                                                         
               10  SA-YEAR                  PIC X(02)     VALUE SPACES.         
               10  SA-MONTH                 PIC X(02)     VALUE SPACES.         
               10  SA-DAY                   PIC X(02)     VALUE SPACES.         
           05  SA-TIME.                                                         
               10  SA-HOUR                  PIC X(02)     VALUE SPACES.         
               10  SA-MINUTE                PIC X(02)     VALUE SPACES.         
               10  FILLER                   PIC X(02).                          
           05  SA-DATE-DB2-N                PIC X(10).                          
           05  SA-DATE-DB2                  REDEFINES SA-DATE-DB2-N.            
               10  SA-CC-DB2                PIC X(02).                          
               10  SA-YY-DB2                PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  SA-MM-DB2                PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  SA-DD-DB2                PIC X(02).                          
           05  HOLD-JULIAN                  PIC 9(05).                          
           05  HOLD-JULIAN-RED              REDEFINES HOLD-JULIAN.              
               10  FILLER                   PIC 9(02).                          
               10  JULIAN-DAY-HOLD          PIC 9(03).                          
           05  HOLD-JULIAN-PLUS.                                                
               10  JULIAN-DAY-PLUS          PIC 9(03).                          
      *                                                                         
           05  SA-BATCH-NO-JRNL.                                                
               10  SA-BATCH-NO-JRNL-X       PIC X(03)     VALUE SPACE.          
               10  FILLER                   PIC X(01)     VALUE SPACE.          
           05  SA-BATCH-NO-UNJRNL.                                              
               10  SA-BATCH-NO-UNJRNL-X     PIC X(03)     VALUE SPACE.          
               10  FILLER                   PIC X(01)     VALUE SPACE.          
           05  SA-FINCL-INV-STAT-CDE       PIC X(02)     VALUE SPACE.           
           05  SA-INV-ID                    PIC S9(9)     VALUE ZERO.           
      *                                                                         
       01  PT-PURCHASE-TYPE                 PIC X(02)     VALUE SPACE.          
           88  PT-REVERSAL                                VALUE                 
                                                         '02' '03'              
                                                         '04' '05'              
                                                         '08' '11'              
                                                         '14'.                  
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-JULIAN-DATE-HOLD          PIC S9(9)     VALUE ZERO.           
           05  PV-JULIAN-DATE-CNVRT         PIC 9(09)     VALUE ZERO.           
           05  PV-PREV-FISCAL-WEEK-BEGIN    PIC X(10)     VALUE SPACE.          
           05  PV-TRANSFER-BATCH.                                               
               10  FILLER                   PIC 9(01)     VALUE ZERO.           
               10  PV-TRANSFER-BATCH-POS2-4                                     
                                            PIC X(03)     VALUE SPACES.         
           05  PV-PARAGRAPH-NAME            PIC X(30)     VALUE SPACE.          
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30)     VALUE SPACE.          
           05  PV-BATCH-NUM                 PIC 9(03)     VALUE ZERO.           
           05  PV-ABEND-CODE                PIC S9(04)    COMP                  
                                                          VALUE ZERO.           
           05  PV-RETRY-COUNT               PIC S9(04)    COMP SYNC             
                                                          VALUE ZERO.           
           05  PV-JOB-INFO.                                                     
               10  FILLER                   PIC X(01)     VALUE '('.            
               10  PV-JOB-NAME              PIC X(08)     VALUE SPACES.         
               10  FILLER                   PIC X(01)     VALUE ')'.            
           05  PV-CYCLE-RETAIL              PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-CYCLE-COST                PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-BATCH-RETAIL              PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-BATCH-COST                PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-RECEIPT-AMOUNT            PIC S9(10)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-PURCHASE-AMOUNT           PIC S9(10)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-PUR-RCP-TOTAL             PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
           05  PV-BATCH-COUNT               PIC S9(09)    COMP SYNC             
                                                          VALUE ZERO.           
           05  PV-MAX-BATCH                 PIC S9(09)    COMP SYNC             
                                                          VALUE ZERO.           
                                                                                
       01  HR-HOLD-RECORD.                                                      
           05  HR-CODE                      PIC X(02)     VALUE SPACES.         
               88  HR-PURCHASE                            VALUE '10'.           
               88  HR-TRANSFER                            VALUE '30'            
                                                                '31'.           
               88  HR-MARKDOWN                            VALUE '40'.           
               88  HR-MARKON                              VALUE '50'.           
               88  HR-IN-ADJUST                           VALUE '80'.           
           05  HR-BATCH                     PIC 9(04)     VALUE ZERO.           
           05  FILLER                       PIC X(142)    VALUE SPACES.         
           05  HR-INV-STAT-CDE              PIC X(02)     VALUE SPACES.         
                                                                                
       01  ALL-CODE-TABLE.                                                      
           05  ALL-CODES                    OCCURS 99 TIMES.                    
               10  CODE-BATCH               OCCURS 9999 TIMES.                  
                   15  CODE-RETAIL          PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
                   15  CODE-COST            PIC S9(11)V9(02)                    
                                                          VALUE ZERO.           
                                                                                
       01  ALL-BATCH-TABLE.                                                     
           05  ALL-BATCH                    OCCURS 9999 TIMES                   
                                            INDEXED BY BATCH-INDEX.             
               10  BATCH-VALUE              PIC X(04) VALUE SPACES.             
               10  BATCH-NUMBER             PIC 9(04) VALUE ZERO.               
                                                                                
       01  SW-SWITCHES.                                                         
           05  SW-EOF-BATCH                 PIC X(01)     VALUE 'N'.            
               88  SW-EOF-BATCH-FILE                      VALUE 'Y'.            
           05  SW-FIRST-PASS-THRU           PIC X(01)     VALUE 'Y'.            
               88  SW-FIRST-PASS                          VALUE 'Y'.            
           05  SW-INPUT-FILE-PASS-THRU      PIC X(01)     VALUE 'N'.            
               88  SW-NO-INPUT-REC                        VALUE 'Y'.            
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE +0              
                                                          COMP SYNC.            
       01  BLANK-LINE                       PIC X(132)    VALUE SPACES.         
                                                                                
       01  H2-HEADING1.                                                         
           05  FILLER                       PIC X(05)     VALUE 'CYCLE'.        
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  CYCLE-NO                     PIC 9(05)     VALUE ZEROES.         
           05  FILLER                       PIC X(02)     VALUE SPACES.         
           05  INV-STAT-CDE                 PIC X(02)     VALUE SPACES.         
           05  FILLER                       PIC X(46)     VALUE SPACES.         
           05  CYCLE-NAME                   PIC X(20)     VALUE SPACES.         
           05  FILLER                       PIC X(51)     VALUE SPACES.         
                                                                                
       01  H2-HEADING2.                                                         
           05  FILLER                       PIC X(39)     VALUE SPACES.         
           05  FILLER                       PIC X(33)     VALUE                 
                                    'WEEKLY BATCH TOTALS BEFORE UPDATE'.        
           05  FILLER                       PIC X(02)     VALUE SPACES.         
           05  H2-LEDGER-LIT                PIC X(20)     VALUE SPACES.         
           05  FILLER                       PIC X(38)     VALUE SPACES.         
                                                                                
       01  H3-HEADING3.                                                         
           05  FILLER                       PIC X(37)     VALUE SPACES.         
           05  FILLER                       PIC X(60)     VALUE                 
                       'CODE  BATCH NO  RETAIL AMOUNT     COST AMOUNT'.         
           05  FILLER                       PIC X(35)     VALUE SPACES.         
                                                                                
       01  H4-HEADING1.                                                         
           05  FILLER                       PIC X(39)     VALUE SPACES.         
           05  H4-HEADING-TITLE             PIC X(33)     VALUE SPACES.         
           05  FILLER                       PIC X(02)     VALUE SPACES.         
           05  H4-LEDGER-LIT                PIC X(20)     VALUE SPACES.         
           05  FILLER                       PIC X(38)     VALUE SPACES.         
                                                                                
       01  D1-DETAIL-LINE.                                                      
           05  FILLER                       PIC X(38)     VALUE SPACE.          
           05  D1-CODE                      PIC X(02)     VALUE SPACES.         
           05  FILLER                       PIC X(5)      VALUE SPACES.         
           05  D1-BATCH                     PIC X(04)     VALUE SPACES.         
           05  D1-RETAIL                    PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
           05  D1-COST                      PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
                                                                                
       01  T1-TOTAL-LINE.                                                       
           05  FILLER                       PIC X(35)     VALUE SPACE.          
           05  T1-HOLD                      PIC X(13)     VALUE SPACES.         
           05  T1-RETAIL                    PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
           05  T1-COST                      PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
                                                                                
       01  RL-RECORD-LINE.                                                      
           05  FILLER                       PIC X(01)     VALUE SPACE.          
           05  RL-RECORDS                   PIC ZZ,ZZZ,ZZZ                      
                                                          VALUE ZERO.           
           05  FILLER                       PIC X(20)     VALUE                 
                                                   ' RECORDS TOTALED'.          
                                                                                
      ********** FOLLOWING ENTRIES ARE USED FOR REPORT-2***************         
       01  D2-JOURNAL-HEADER.                                                   
           05  FILLER                       PIC X(20)     VALUE SPACES.         
           05  D2-ENTRY-TYPE                PIC X(13)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  D2-TRANS-TYPE                PIC X(04)     VALUE SPACES.         
           05  FILLER                       PIC X(84)     VALUE SPACES.         
                                                                                
      *D2-JOURNAL-LINE IS THE VERIFIED WEEK (PRIOR) MM/DD/YY HEADING            
       01  D2-JOURNAL-LINE.                                                     
           05  FILLER                       PIC X(38)     VALUE SPACES.         
           05  D2-VERIFIED-WEEK             PIC X(26)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  D2-WEEK-DATES.                                                   
               10 D2-WEEK-MM                PIC X(02).                          
               10 FILLER                    PIC X(01)     VALUE '/'.            
               10 D2-DAY-DD                 PIC X(02).                          
               10 FILLER                    PIC X(01)     VALUE '/'.            
               10 D2-YEAR-YY                PIC X(02).                          
           05  FILLER                       PIC X(59)     VALUE SPACES.         
                                                                                
       01  D3-JOURNAL-LINE.                                                     
           05  FILLER                       PIC X(42)     VALUE SPACES.         
           05  D3-VERIFIED-TYPE             PIC X(31).                          
           05  FILLER                       PIC X(02)     VALUE SPACES.         
           05  D3-VERIFIED-AMTS             PIC Z,ZZZ,ZZZ,ZZ9.99-.              
           05  FILLER                       PIC X(24)     VALUE SPACES.         
                                                                                
       01  D4-TOTAL-LINE.                                                       
           05  FILLER                       PIC X(39)     VALUE SPACES.         
           05  D4-BATCH-TITLE               PIC X(05)     VALUE 'BATCH'.        
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  D4-BATCH-TYPE                PIC X(04)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  D4-RETAIL-VALUE              PIC X(19)     VALUE                 
                                                 ' RETAIL VALUE      '.         
           05  D4-VERIFIED-AMTS             PIC Z,ZZZ,ZZZ,ZZ9.99-.              
           05  FILLER                       PIC X(30)     VALUE SPACES.         
      *                                                                         
       01  T2-TOTAL-LINE.                                                       
           05  FILLER                       PIC X(22)     VALUE SPACES.         
           05  FILLER                       PIC X(37)     VALUE                 
                                'TOTAL RECEIPT & ADJ VERIFIED WEEK OF '.        
           05  T2-CURR-DATE                 PIC X(08)     VALUE SPACES.         
           05  FILLER                       PIC X(08)     VALUE SPACES.         
           05  T2-VERIFIED-AMTS             PIC Z,ZZZ,ZZZ,ZZ9.99-.              
           05  FILLER                       PIC X(20)     VALUE SPACES.         
                                                                                
       01  RCPT-JOURNAL-HEADER.                                                 
           05  FILLER                       PIC X(20)     VALUE SPACES.         
           05  R2-ENTRY-TYPE                PIC X(13)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  FILLER                       PIC X(05)     VALUE 'TOTAL'.        
           05  FILLER                       PIC X(83)     VALUE SPACES.         
                                                                                
       01  RCPT-TOTAL-LINE.                                                     
           05  FILLER                       PIC X(08)     VALUE SPACES.         
           05  FILLER                       PIC X(51)     VALUE                 
                  ' CYCLES GRAND TOTAL RECEIPT & ADJ VERIFIED WEEK OF '.        
           05  R2-CURR-DATE                 PIC X(08)     VALUE SPACES.         
           05  FILLER                       PIC X(08)     VALUE SPACES.         
           05  R2-VERIFIED-AMTS             PIC Z,ZZZ,ZZZ,ZZ9.99-.              
           05  FILLER                       PIC X(20)     VALUE SPACES.         
                                                                                
      * LINE FOR PRINTING THE GRAND CODE TOTAL FOR ALL CODE.                    
       01  CODE-CYCLE-LINE.                                                     
           05  FILLER                       PIC X(05)     VALUE 'CYCLE'.        
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  FILLER                       PIC 9(05)     VALUE 99999.          
           05  FILLER                       PIC X(50)     VALUE SPACES.         
           05  FILLER                       PIC X(20)     VALUE                 
                                                'ALL INVENTORY CYCLES'.         
           05  FILLER                       PIC X(51)     VALUE SPACE.          
                                                                                
       01  CODE-HEADING.                                                        
           05  FILLER                       PIC X(37)     VALUE SPACES.         
           05  FILLER                       PIC X(60)     VALUE                 
                       'CODE  TOTAL     RETAIL AMOUNT     COST AMOUNT'.         
           05  FILLER                       PIC X(35)     VALUE SPACES.         
                                                                                
       01  CODE-DETAIL-LINE.                                                    
           05  FILLER                       PIC X(38)     VALUE SPACE.          
           05  C1-CODE                      PIC X(02)     VALUE SPACES.         
           05  FILLER                       PIC X(01)     VALUE SPACES.         
           05  FILLER                       PIC X(05)     VALUE 'TOTAL'.        
           05  FILLER                       PIC X(02)     VALUE SPACES.         
           05  C1-RETAIL                    PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
           05  C1-COST                      PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
                                                                                
       01  CYCLE-GRAND-LINE.                                                    
           05  FILLER                       PIC X(19)     VALUE SPACES.         
           05  FILLER                       PIC X(18)     VALUE                 
                                                  'CYCLES GRAND TOTAL'.         
           05  FILLER                       PIC X(11)     VALUE SPACES.         
           05  G1-RETAIL                    PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
           05  G1-COST                      PIC ZZ,ZZZ,ZZZ,ZZZ.99-              
                                                          VALUE ZERO.           
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
        EJECT                                                                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
      *A000-MAINLINE  HIGHEST LEVEL CONTROL MODULE                    *         
      *****************************************************************         
       A000-MAINLINE.                                                           
                                                                                
            PERFORM A100-INITIALIZATION-ROUTINE.                                
            PERFORM B200-PROCESS-BATCHES                                        
               UNTIL SW-EOF-BATCH-FILE.                                         
                                                                                
            IF  SW-NO-INPUT-REC                                                 
                PERFORM B250-NO-INPUT-RECORD                                    
            ELSE                                                                
                PERFORM B275-PROCESS-REPORT-OUTPUT                              
            END-IF.                                                             
                                                                                
            CLOSE BATCH-FILE                                                    
                  REPORT-FILE                                                   
                  REPORT-FILE-2.                                                
            STOP RUN.                                                           
                                                                                
      ******************************************************************        
      * OPEN BATCH/REPORT FILES. OBTAIN SATURDAY SYSIN DATE FOR PROCESS*        
      * NEW MODULE   -  WAS PREVIOUSLY EXECUTED DIRECLTY IN A000-MAIN***        
      ******************************************************************        
       A100-INITIALIZATION-ROUTINE.                                             
                                                                                
            OPEN INPUT  BATCH-FILE                                              
                        LEDGER-CONTROL-FILE                                     
                        DATE-CONTROL-FILE                                       
                 OUTPUT REPORT-FILE                                             
                        REPORT-FILE-2.                                          
                                                                                
      *  GET CURRENT JOB INFORMATION FOR USE IN THE REPORT                      
            CALL DP085-GET-CURR-JOB-INFO-PGM                                    
                                            USING                               
                                         DP085-GET-CURR-JOB-INFO-PARMS.         
            MOVE DP085-JOB-NAME             TO PV-JOB-NAME.                     
            MOVE PV-JOB-INFO                TO                                  
                                 DP132O-STANDRD-HEADER-132-COLS(20:10).         
                                                                                
            PERFORM B150-PROCESS-CONTROL-CARD.                                  
            INITIALIZE PA2-PROGRAM-ACCUMULATORS                                 
                       DCLTINCNTL                                               
                       DCLTDTATTR                                               
                       ALL-CODE-TABLE.                                          
      *****************************************************************         
      *  DETERMINE THE PURCHASE ADJ/RECEIPTS BATCH NUMBER FROM        *         
      *  THE CURRENT DATE OR FROM THE OVERRIDE CONTROL CARD NUMBER    *         
      *****************************************************************         
            IF LC-OVERRIDE-IND EQUAL 'O'                                        
                MOVE LC-OVERRIDE-BATCH-NO   TO JULIAN-DAY-HOLD                  
            ELSE                                                                
                MOVE SYSIN-DATE             TO DTATTR-KY-DTE                    
                PERFORM E100-SELECT-TDTATTR                                     
                MOVE DTATTR-JLN-DTE         TO CC-JULIAN-DATE-SIGN              
                MOVE CC-JULIAN-DATE-SIGN    TO CC-JULIAN-DATE-9                 
                MOVE CC-JULIAN-DATE-9       TO CC-JULIAN-DATE                   
                MOVE DTATTR-FSCL-WK-BEG-DTE TO SA-DATE-DB2-N                    
      *****************************************************************         
      * JULIAN DATE IS USED ON THE BATCH TOTALS FOR PURCHASE TRANS.   *         
      * THE JULIAN DATE FROM THE JOB CARD WILL BE ATTACHED TO RECEIPTS*         
      * THE JULIAN DATE (PLUS 500 DAYS) IS USED FOR PURCH-ADJUSTMENTS *         
      * OCCASIONALLY AP RUNS AFTER MIDNIGHT WHICH WOULD HAVE A JULIAN *         
      * DATE OF 500 - 1DAY (REASON FOR PLUS-499 VAR)                  *         
      *****************************************************************         
                COMPUTE CC-JULIAN-PLUS1    = CC-JULIAN-DAY + 1                  
                COMPUTE CC-JULIAN-PLUS-500 = CC-JULIAN-PLUS1 + 500              
                COMPUTE CC-JULIAN-PLUS-499 = CC-JULIAN-PLUS1 + 499              
      ** DATES                                                                  
                DISPLAY 'CC-JULIAN-DAY: ',      CC-JULIAN-DAY                   
                DISPLAY 'CC-JULIAN-PLUS1: ',    CC-JULIAN-PLUS1                 
                DISPLAY 'CC-JULIAN-PLUS-500: ', CC-JULIAN-PLUS-500              
                DISPLAY 'CC-JULIAN-PLUS-499: ', CC-JULIAN-PLUS-499              
                MOVE CC-JULIAN-PLUS1        TO SA-BATCH-NO-UNJRNL-X             
                MOVE CC-JULIAN-PLUS-499     TO SA-BATCH-NO-JRNL-X               
            END-IF.                                                             
                                                                                
            PERFORM B100-FORMAT-DATE-TIME.                                      
                                                                                
            MOVE 'VAKRP011'                 TO DP132O-PROGRAM-NAME.             
                                                                                
            PERFORM Z100-READ-BATCH-FILE.                                       
                                                                                
      *****************************************************************         
      * B100-FORMAT-DATE-TIME  THE CURRENT DATE AND TIME ARE MOVED    *         
      * TO THE REPORT HEADING LINES.                                  *         
      *****************************************************************         
        B100-FORMAT-DATE-TIME.                                                  
                                                                                
            ACCEPT SA-DATE                  FROM DATE.                          
            ACCEPT SA-TIME                  FROM TIME.                          
            MOVE SA-MONTH                   TO DP132O-RUN-MONTH.                
            MOVE SA-DAY                     TO DP132O-RUN-DAY.                  
            MOVE SA-YEAR                    TO DP132O-RUN-YEAR.                 
            MOVE SA-HOUR                    TO DP132O-RUN-HOUR.                 
            MOVE SA-MINUTE                  TO DP132O-RUN-MINUTE.               
      *                                                                         
      *****************************************************************         
      * VALIDATE THE JOBTRAC DATE AND OBTAIN BEGINNING OF WEEK(SUNDAY)*         
      *****************************************************************         
                                                                                
            IF  DTATTR-DAY-NM = 'SATURDAY '                                     
                NEXT SENTENCE                                                   
            ELSE                                                                
                EVALUATE TRUE                                                   
                   WHEN LC-INV-LEDGER                                           
                        SET PS-CUTOFF-WEEK-DATE                                 
                                            TO TRUE                             
                   WHEN LC-MERCH-LEDGER                                         
                        DISPLAY '** ABEND **'                                   
                        DISPLAY '** PGM = VAKRP011'                             
                        DISPLAY '** INPUT DATE NOT A SATURDAY DATE.'            
                        MOVE +4003          TO ABEND-CODE                       
                        CALL 'ILBOABN0'     USING ABEND-CODE                    
                END-EVALUATE                                                    
            END-IF.                                                             
                                                                                
            DISPLAY 'DTATTR-DAY-NM : ', DTATTR-DAY-NM.                          
                                                                                
      *****************************************************************         
      * ACCEPT THE INPUT CONTROL CARD.                                *         
      *****************************************************************         
       B150-PROCESS-CONTROL-CARD.                                               
                                                                                
           READ LEDGER-CONTROL-FILE         INTO                                
                                               LC-LEDGER-CONTROL-RECORD         
               AT END DISPLAY '*** ABEND *** IN VAKRP011'                       
                      DISPLAY 'LEDGER CONTROL CARD EMPTY'                       
                      MOVE +4003            TO ABEND-CODE                       
                      CALL 'ILBOABN0'       USING ABEND-CODE.                   
                                                                                
           DISPLAY 'VAKRP011 LEDGER CONTROL CARD:'                              
                                              LC-LEDGER-CONTROL-RECORD.         
                                                                                
           EVALUATE TRUE                                                        
               WHEN LC-MERCH-LEDGER                                             
                   MOVE PC-TITLE-BATCH      TO H4-HEADING-TITLE                 
                   MOVE PC-MERCH-LEDGER-LIT TO H2-LEDGER-LIT                    
                                               H4-LEDGER-LIT                    
               WHEN LC-INV-LEDGER                                               
                   MOVE PC-TITLE-BATCH      TO H4-HEADING-TITLE                 
                   MOVE PC-INV-LEDGER-LIT   TO H2-LEDGER-LIT                    
                                               H4-LEDGER-LIT                    
               WHEN OTHER                                                       
                   DISPLAY '*** ABEND *** IN VAKRP011'                          
                   DISPLAY 'INVALID LEDGER INDICATOR ON CONTROL CARD.'          
                   MOVE +4003               TO ABEND-CODE                       
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
           END-EVALUATE.                                                        
                                                                                
           IF LC-OVERRIDE-IND EQUAL 'O'                                         
               IF LC-OVERRIDE-BATCH-NO NUMERIC                                  
                   CONTINUE                                                     
               ELSE                                                             
                   DISPLAY '*** ABEND *** IN VAKRP011'                          
                   DISPLAY 'INVALID BATCH NUMBER ON CONTROL CARD.'              
                   DISPLAY LC-LEDGER-CONTROL-RECORD                             
                   MOVE +4003               TO ABEND-CODE                       
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
               END-IF                                                           
           END-IF.                                                              
                                                                                
           READ DATE-CONTROL-FILE           INTO CC-CONTROL-CARD                
               AT END DISPLAY '*** ABEND *** IN VAKRP011'                       
                      DISPLAY 'DATE CONTROL CARD EMPTY'                         
                      MOVE +4003            TO ABEND-CODE                       
                      CALL 'ILBOABN0'       USING ABEND-CODE.                   
                                                                                
           CLOSE LEDGER-CONTROL-FILE                                            
                 DATE-CONTROL-FILE.                                             
                                                                                
      *****************************************************************         
      * B200-PROCESS-BATCHES    CONTROLS THE PROCESSING OF TRANSACTION*         
      * TYPES.  CHECKS FOR CHANGES IN BATCH OR TRANSACTION TYPES, AND *         
      * CALCULATES THE COST AND RETAIL AMOUNTS FOR EACH BATCH.        *         
      *****************************************************************         
        B200-PROCESS-BATCHES.                                                   
                                                                                
            IF  SW-FIRST-PASS                                                   
                MOVE 'N'                    TO SW-FIRST-PASS-THRU               
                MOVE HR-CODE                TO SA-CODE                          
                MOVE HR-BATCH               TO SA-BATCH                         
                MOVE D115-INV-ID            TO SA-INV-ID                        
                MOVE D115-FINCL-INV-STAT-CDE                                    
                                            TO SA-FINCL-INV-STAT-CDE            
                                               INV-STAT-CDE                     
                PERFORM E200-GET-INV-NAME                                       
                PERFORM D100-WRITE-HEADINGS                                     
            END-IF.                                                             
                                                                                
            IF  D115-INV-ID NOT = SA-INV-ID                                     
                PERFORM C100-WRITE-BATCH-LINE                                   
                PERFORM C200-WRITE-CODE-TOTAL                                   
                PERFORM B300-WRITE-FINAL-TOTAL                                  
                PERFORM B500-TALLY-RECEIPTS                                     
                PERFORM C700-WRITE-RECEIPT-REPORT                               
                PERFORM E200-GET-INV-NAME                                       
                PERFORM D100-WRITE-HEADINGS                                     
                MOVE HR-CODE                TO SA-CODE                          
                MOVE HR-BATCH               TO SA-BATCH                         
            END-IF.                                                             
                                                                                
            IF  HR-CODE NOT = SA-CODE                                           
                PERFORM C100-WRITE-BATCH-LINE                                   
                PERFORM C200-WRITE-CODE-TOTAL                                   
                IF HR-INV-STAT-CDE NOT = SA-FINCL-INV-STAT-CDE                  
                   MOVE HR-INV-STAT-CDE TO INV-STAT-CDE                         
                                           SA-FINCL-INV-STAT-CDE                
                   PERFORM D100-WRITE-HEADINGS                                  
                END-IF                                                          
            ELSE                                                                
                IF HR-BATCH NOT = SA-BATCH                                      
                    PERFORM C100-WRITE-BATCH-LINE                               
                END-IF                                                          
            END-IF.                                                             
                                                                                
            IF  HR-PURCHASE                                                     
                MOVE D115-TRANS-CD          TO PT-PURCHASE-TYPE                 
                IF  PT-REVERSAL                                                 
                    PERFORM C300-PROCESS-REVERSAL-AMT                           
                ELSE                                                            
                    COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT             
                    COMPUTE PA-COST = PA-COST + D115-NET-COST                   
                END-IF                                                          
      ********* PROCESS FOR VERIFIED RECEIPTS/ADJ PURCHASE REPORT *****         
                PERFORM B210-PROCESS-RECEIPTS                                   
            END-IF.                                                             
                                                                                
            IF  HR-TRANSFER                                                     
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    (D115-RETAIL-PER * D115-UNITS-TRANSFER)                     
                MOVE ZERO                   TO PA-COST                          
            END-IF.                                                             
                                                                                
            IF HR-MARKDOWN                                                      
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    ((D115-MKDWN-OPRICE - D115-MKDWN-NPRICE) *                  
                    D115-MKDWN-QTY)                                             
                MOVE ZERO                   TO PA-COST                          
            END-IF.                                                             
                                                                                
            IF HR-MARKON                                                        
                COMPUTE PA-RETAIL = PA-RETAIL +                                 
                    ((D115-MARKON-NPRICE - D115-MARKON-OPRICE) *                
                    D115-MARKON-QTY)                                            
                MOVE ZERO                   TO PA-COST                          
            END-IF.                                                             
                                                                                
            IF HR-IN-ADJUST                                                     
                COMPUTE PA-RETAIL = PA-RETAIL + D115-AT-RETAIL                  
                MOVE ZERO                   TO PA-COST                          
                MOVE JULIAN-DAY-PLUS        TO HR-BATCH                         
            END-IF.                                                             
                                                                                
            ADD 1                           TO PA-RECORD-COUNT.                 
            MOVE HR-CODE                    TO SA-CODE.                         
            MOVE HR-BATCH                   TO SA-BATCH.                        
            MOVE D115-INV-ID                TO SA-INV-ID.                       
            MOVE D115-FINCL-INV-STAT-CDE    TO SA-FINCL-INV-STAT-CDE.           
            PERFORM Z100-READ-BATCH-FILE.                                       
                                                                                
      *****************************************************************         
      * B210-PROCESS  CONVERT SYSIN DATE TO CALENDAR FORMATS          *         
      * RECEIPTS AP CODE = '01'  ANY OTHER AP CODES LISTED AS 'OTHER' *         
      *****************************************************************         
       B210-PROCESS-RECEIPTS.                                                   
                                                                                
            MOVE D115-AP-TRANS-CDE          TO PC-AP-CODES.                     
            MOVE DTATTR-FSCL-WK-BEG-DTE     TO SA-DATE-DB2-N.                   
      *****************************************************************         
      * FOLLOWING LOGIC DETERMINES WHICH PARAGRAPH/TOTALS TO CALCULATE*         
      *****************************************************************         
      *                                                                         
      ***** IF 'RECEIPTS' ENTRIES                                               
            IF  D115-BATCH-NO EQUAL SA-BATCH-NO-UNJRNL                          
                ADD D115-RETAIL-AMT         TO PA2-RECEIPTS-CURR                
            ELSE                                                                
      ***** IF 'PURCHASE ADJ' ENTRIES                                           
                IF  D115-BATCH-NO EQUAL SA-BATCH-NO-JRNL                        
                    ADD D115-RETAIL-AMT     TO PA2-PRCH-ADJ-RECPT-CURR          
                END-IF                                                          
            END-IF.                                                             
                                                                                
      *****************************************************************         
      *B250-NO-INPUT-RECORD. TO WRITE REPORT INCASE OF NO INPUT RECORD*         
      *****************************************************************         
       B250-NO-INPUT-RECORD.                                                    
                                                                                
            PERFORM D100-WRITE-HEADINGS.                                        
                                                                                
            INITIALIZE D1-DETAIL-LINE                                           
            MOVE D1-DETAIL-LINE             TO REPORT-LINE.                     
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING PC-ADVANCE.                                     
                                                                                
            MOVE 'CODE TOTAL'               TO T1-HOLD.                         
            MOVE PA-TOTAL-RETAIL            TO T1-RETAIL.                       
            MOVE PA-TOTAL-COST              TO T1-COST.                         
            MOVE T1-TOTAL-LINE              TO REPORT-LINE.                     
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
                                                                                
            MOVE 'CYCLE TOTALS'             TO T1-HOLD.                         
            MOVE PA-GRAND-RETAIL            TO T1-RETAIL.                       
            MOVE PA-GRAND-COST              TO T1-COST.                         
            MOVE T1-TOTAL-LINE              TO REPORT-RECORD.                   
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
                                                                                
            MOVE PA-RECORD-COUNT            TO RL-RECORDS.                      
            MOVE RL-RECORD-LINE             TO REPORT-RECORD.                   
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
                                                                                
            PERFORM C700-WRITE-RECEIPT-REPORT.                                  
                                                                                
      *****************************************************************         
      *B275-PROCESS-REPORT-OUTPUT. TO WRITE REPORT WHEN WE HAVE INPUT *         
      *                         RECORDS IN THE FILE                   *         
      *****************************************************************         
       B275-PROCESS-REPORT-OUTPUT.                                              
                                                                                
            PERFORM C100-WRITE-BATCH-LINE.                                      
            PERFORM C200-WRITE-CODE-TOTAL.                                      
            PERFORM B300-WRITE-FINAL-TOTAL.                                     
            PERFORM B400-PRINT-ALL-CODE.                                        
            PERFORM B500-TALLY-RECEIPTS.                                        
            PERFORM C700-WRITE-RECEIPT-REPORT.                                  
            PERFORM C900-WRITE-TOTAL-RECEIPT.                                   
                                                                                
      *****************************************************************         
      * B300-WRITE-FINAL-TOTAL  FORMATS FINAL TOTAL LINE              *         
      *****************************************************************         
        B300-WRITE-FINAL-TOTAL.                                                 
                                                                                
            MOVE 'CYCLE TOTALS'             TO T1-HOLD.                         
            MOVE PA-GRAND-RETAIL            TO T1-RETAIL.                       
            MOVE PA-GRAND-COST              TO T1-COST.                         
            MOVE T1-TOTAL-LINE              TO REPORT-RECORD.                   
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
            MOVE PA-RECORD-COUNT            TO RL-RECORDS.                      
            MOVE RL-RECORD-LINE             TO REPORT-RECORD.                   
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING 2.                                              
            MOVE 2                          TO PC-ADVANCE.                      
            ADD PC-ADVANCE                  TO PC-LINE-COUNT.                   
            MOVE ZERO                       TO PA-GRAND-COST                    
                                               PA-GRAND-RETAIL                  
                                               PA-RECORD-COUNT.                 
                                                                                
      *****************************************************************         
      * B400-PRINT-ALL-CODE THIS PARA PRINTS THE TOTAL AMOUNT FOR     *         
      * ALL CODES                                                     *         
      *****************************************************************         
       B400-PRINT-ALL-CODE.                                                     
                                                                                
           ADD 1                            TO PC-PAGE-COUNT.                   
           MOVE PC-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
           MOVE '1'                         TO DP132O-REPORT-NUMBER.            
           MOVE DP132O-STANDRD-HEADER-132-COLS                                  
                                            TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING PAGE.                                            
                                                                                
           MOVE CODE-CYCLE-LINE             TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE H3-HEADING3                 TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 10                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 30                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 31                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 40                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 50                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE ZERO                        TO PV-BATCH-COUNT.                  
           MOVE 80                          TO SA-CODE.                         
           PERFORM B410-PRINT-CODE-BATCH                                        
               VARYING PV-BATCH-COUNT FROM 1 BY 1                               
                 UNTIL (PV-BATCH-COUNT > PV-MAX-BATCH).                         
           PERFORM B420-PRINT-TOTAL-CODE.                                       
                                                                                
           MOVE PV-BATCH-RETAIL             TO G1-RETAIL.                       
           MOVE PV-BATCH-COST               TO G1-COST.                         
           MOVE CYCLE-GRAND-LINE            TO REPORT-RECORD.                   
           WRITE REPORT-RECORD              AFTER ADVANCING 1.                  
                                                                                
      *****************************************************************         
      * B410-PRINT-CODE-BATCH THIS PARA PRINTS THE TOTAL AMOUNT FOR   *         
      * EVERY INDIVIDUAL BATCH AND CODE COMBINATION                   *         
      *****************************************************************         
       B410-PRINT-CODE-BATCH.                                                   
                                                                                
           IF ((CODE-RETAIL(SA-CODE, PV-BATCH-COUNT) NOT = ZERO)                
              OR (CODE-COST(SA-CODE, PV-BATCH-COUNT) NOT = ZERO))               
              MOVE SA-CODE                  TO D1-CODE                          
              MOVE BATCH-VALUE(PV-BATCH-COUNT)                                  
                                            TO D1-BATCH                         
              MOVE CODE-RETAIL(SA-CODE, PV-BATCH-COUNT)                         
                                            TO D1-RETAIL                        
              MOVE CODE-COST(SA-CODE, PV-BATCH-COUNT)                           
                                            TO D1-COST                          
              ADD CODE-RETAIL(SA-CODE, PV-BATCH-COUNT)                          
                                            TO PV-CYCLE-RETAIL                  
              ADD CODE-COST(SA-CODE, PV-BATCH-COUNT)                            
                                            TO PV-CYCLE-COST                    
              MOVE D1-DETAIL-LINE           TO REPORT-RECORD                    
              WRITE REPORT-RECORD           AFTER ADVANCING 1                   
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B420-PRINT-TOTAL-CODE THIS PARA PRINTS THE TOTAL AMOUNT FOR   *         
      * EVERY INDIVIDUAL CODE                                         *         
      *****************************************************************         
       B420-PRINT-TOTAL-CODE.                                                   
                                                                                
           IF ((PV-CYCLE-RETAIL NOT = ZERO)                                     
              OR (PV-CYCLE-COST NOT = ZERO))                                    
              MOVE SA-CODE                  TO C1-CODE                          
              MOVE PV-CYCLE-RETAIL          TO C1-RETAIL                        
              MOVE PV-CYCLE-COST            TO C1-COST                          
              ADD PV-CYCLE-RETAIL           TO PV-BATCH-RETAIL                  
              ADD PV-CYCLE-COST             TO PV-BATCH-COST                    
              MOVE CODE-DETAIL-LINE         TO REPORT-RECORD                    
              WRITE REPORT-RECORD           AFTER ADVANCING 2                   
              WRITE REPORT-RECORD FROM BLANK-LINE AFTER ADVANCING 1             
              MOVE ZERO                     TO PV-CYCLE-RETAIL                  
              MOVE ZERO                     TO PV-CYCLE-COST                    
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B500-TALLY-RECEIPTS   GETS TALLY RECEIPTS                     *         
      *****************************************************************         
        B500-TALLY-RECEIPTS.                                                    
                                                                                
            ADD PA2-PRCH-ADJ-RECPT-CURR     TO PA2-RECEIPTS-CURR                
                GIVING PA2-TOTAL-CURR-RECEIPTS.                                 
                                                                                
      *****************************************************************         
      * C100-WRITE-BATCH-LINE   FORMATS TOTAL FOR A BATCH NUMBER      *         
      *****************************************************************         
        C100-WRITE-BATCH-LINE.                                                  
                                                                                
            IF  PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                       
                PERFORM D100-WRITE-HEADINGS                                     
            END-IF.                                                             
                                                                                
            MOVE SA-CODE                    TO D1-CODE.                         
            MOVE PA-RETAIL                  TO D1-RETAIL                        
                                               PA-CODE-RETAIL.                  
            ADD PA-RETAIL                   TO PA-TOTAL-RETAIL.                 
            MOVE ZERO                       TO PA-RETAIL.                       
                                                                                
            IF  SA-CODE = '10'                                                  
                MOVE PA-COST                TO D1-COST                          
                                               PA-CODE-COST                     
                ADD  PA-COST                TO PA-TOTAL-COST                    
                MOVE ZERO                   TO PA-COST                          
                MOVE SA-BATCH               TO SA-PURCH-BATCH                   
                MOVE SA-P-BATCH             TO D1-BATCH                         
            ELSE                                                                
                MOVE SA-BATCH               TO D1-BATCH                         
                MOVE ZERO                   TO D1-COST                          
                                               PA-CODE-COST                     
            END-IF.                                                             
                                                                                
            SET BATCH-INDEX                 TO 1.                               
                                                                                
            SEARCH ALL-BATCH                                                    
                AT END                                                          
                   SET  PS-BATCH-FOUND-NO   TO TRUE                             
                WHEN D1-BATCH = BATCH-VALUE(BATCH-INDEX)                        
                   SET  PS-BATCH-FOUND-YES  TO TRUE                             
                   MOVE BATCH-NUMBER(BATCH-INDEX)                               
                                            TO PV-BATCH-NUM                     
            END-SEARCH.                                                         
                                                                                
            IF  ((PS-BATCH-FOUND-NO)                                            
                OR (PS-FIRST-BATCH-YES))                                        
                SET PS-FIRST-BATCH-NO       TO TRUE                             
                ADD 1                       TO PV-MAX-BATCH                     
                MOVE D1-BATCH               TO BATCH-VALUE(PV-MAX-BATCH)        
                MOVE PV-MAX-BATCH           TO PV-BATCH-NUM                     
                MOVE PV-MAX-BATCH           TO                                  
                                              BATCH-NUMBER(PV-MAX-BATCH)        
            END-IF.                                                             
                                                                                
            ADD PA-CODE-RETAIL              TO                                  
                                CODE-RETAIL (SA-CODE, PV-BATCH-NUM).            
            ADD PA-CODE-COST                TO                                  
                                  CODE-COST (SA-CODE, PV-BATCH-NUM).            
            MOVE HR-BATCH                   TO SA-BATCH.                        
            MOVE D1-DETAIL-LINE             TO REPORT-LINE.                     
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING PC-ADVANCE.                                     
            MOVE 1                          TO PC-ADVANCE.                      
            ADD PC-ADVANCE                  TO PC-LINE-COUNT.                   
                                                                                
      *****************************************************************         
      * C200-WRITE-CODE-TOTAL  FORMATS TOTAL FOR A TRAN TYPE          *         
      *****************************************************************         
       C200-WRITE-CODE-TOTAL.                                                   
                                                                                
            IF  PC-LINE-COUNT IS GREATER THAN PC-LINE-MAX                       
                PERFORM D100-WRITE-HEADINGS                                     
            END-IF.                                                             
                                                                                
            MOVE 'CODE TOTAL'               TO T1-HOLD.                         
            MOVE PA-TOTAL-RETAIL            TO T1-RETAIL.                       
            MOVE PA-TOTAL-COST              TO T1-COST.                         
            MOVE T1-TOTAL-LINE              TO REPORT-LINE.                     
            ADD PA-TOTAL-RETAIL             TO PA-GRAND-RETAIL.                 
            ADD PA-TOTAL-COST               TO PA-GRAND-COST.                   
            MOVE ZERO                       TO PA-TOTAL-RETAIL                  
                                               PA-TOTAL-COST.                   
            MOVE HR-CODE                    TO SA-CODE.                         
            MOVE 2                          TO PC-ADVANCE.                      
            WRITE REPORT-RECORD                                                 
                AFTER ADVANCING PC-ADVANCE.                                     
            ADD PC-ADVANCE                  TO PC-LINE-COUNT.                   
                                                                                
      *****************************************************************         
      * C300-PROCESS-REVERSAL-AMT   PURCHASE RECORDS WITH             *         
      * TRANSACTION CODES OF 02, 03, 04, 05, 08, 11 AND 14 MUST HAVE  *         
      * THERE SIGNS REVERSED PRIOR TO BEING ADDED                     *         
      *****************************************************************         
       C300-PROCESS-REVERSAL-AMT.                                               
                                                                                
            COMPUTE D115-NET-COST = D115-NET-COST * -1.                         
            COMPUTE D115-RETAIL-AMT = D115-RETAIL-AMT * -1.                     
            COMPUTE PA-RETAIL = PA-RETAIL + D115-RETAIL-AMT.                    
            COMPUTE PA-COST = PA-COST + D115-NET-COST.                          
                                                                                
      *****************************************************************         
      * C700-WRITE-RECEIPT-REPORT  TO WRITE THE RECEIPT REPORT        *         
      *****************************************************************         
       C700-WRITE-RECEIPT-REPORT.                                               
                                                                                
           PERFORM D200-WRITE-HEADINGS.                                         
           MOVE SPACES                      TO REPORT-RECORD-2.                 
                                                                                
      ***  IF THE RUN IS EXECUTED DURING CUTOFF WEEK, MOVE THE PREVIOUS         
      ***  FISCAL WEEK BEGIN DATE (LAST WEEK OF MONTH) TO THE RPT DATES         
           IF  PS-CUTOFF-WEEK-DATE                                              
               MOVE PV-PREV-FISCAL-WEEK-BEGIN                                   
                                            TO SA-DATE-DB2-N                    
           END-IF.                                                              
                                                                                
           MOVE SA-MM-DB2                   TO D2-WEEK-MM.                      
           MOVE SA-DD-DB2                   TO D2-DAY-DD.                       
           MOVE SA-YY-DB2                   TO D2-YEAR-YY.                      
      *                                                                         
           PERFORM C750-PRINT-REPORT-2.                                         
                                                                                
      * PRINT TOTAL OF ALL CURRENT RECEIPT TRANSACTIONS                         
           MOVE D2-WEEK-DATES               TO T2-CURR-DATE.                    
           MOVE PA2-TOTAL-CURR-RECEIPTS     TO T2-VERIFIED-AMTS.                
           WRITE REPORT-RECORD-2            FROM T2-TOTAL-LINE                  
               AFTER ADVANCING 3 LINES.                                         
           MOVE ZERO                        TO PA2-TOTAL-CURR-RECEIPTS          
                                               PA2-PRCH-ADJ-RECPT-CURR          
                                               PA2-RECEIPTS-CURR.               
                                                                                
      *****************************************************************         
      * START PROCESSING OF SECOND REPORT                             *         
      * PRINT RECEIPTS AND PURCHASE ADJUSTMENTS                       *         
      *****************************************************************         
       C750-PRINT-REPORT-2.                                                     
                                                                                
           PERFORM C760-PROCESS-RECEIPTS-TRANS.                                 
           PERFORM C780-PROCESS-PRCH-ADJ-TRANS.                                 
      *                                                                         
       C760-PROCESS-RECEIPTS-TRANS.                                             
           MOVE PC-RECEIPTS-HEADING         TO D2-ENTRY-TYPE.                   
           MOVE CC-JULIAN-PLUS1             TO D2-TRANS-TYPE.                   
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-HEADER              
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           PERFORM C770-PRINT-RECEIPTS.                                         
      *                                                                         
       C770-PRINT-RECEIPTS.                                                     
           MOVE PC-CURR-WEEK-HDR            TO D2-VERIFIED-WEEK.                
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-LINE                
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           ADD PA2-RECEIPTS-CURR            TO PV-RECEIPT-AMOUNT.               
           MOVE PA2-RECEIPTS-CURR           TO D3-VERIFIED-AMTS.                
           MOVE PC-RCPTS-AND-ADJ            TO D3-VERIFIED-TYPE.                
           WRITE REPORT-RECORD-2            FROM D3-JOURNAL-LINE                
               AFTER ADVANCING 2 LINE.                                          
                                                                                
      *                                                                         
       C780-PROCESS-PRCH-ADJ-TRANS.                                             
           MOVE PC-PRCH-ADJ-HEADING         TO D2-ENTRY-TYPE.                   
           MOVE CC-JULIAN-PLUS-499          TO D2-TRANS-TYPE.                   
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-HEADER              
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           PERFORM C790-PRINT-PURCHASE-ADJ.                                     
      *                                                                         
       C790-PRINT-PURCHASE-ADJ.                                                 
           MOVE PC-CURR-WEEK-HDR            TO D2-VERIFIED-WEEK.                
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-LINE                
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           ADD PA2-PRCH-ADJ-RECPT-CURR      TO PV-PURCHASE-AMOUNT.              
           MOVE PA2-PRCH-ADJ-RECPT-CURR     TO D3-VERIFIED-AMTS.                
           MOVE PC-PRCH-ADJ                 TO D3-VERIFIED-TYPE.                
           WRITE REPORT-RECORD-2            FROM D3-JOURNAL-LINE                
               AFTER ADVANCING 2 LINE.                                          
                                                                                
      *****************************************************************         
      * C900-WRITE-TOTAL-RECEIPT  TO WRITE THE GRAND TOTAL IN RECEIPT *         
      *                                                 REPORT        *         
      *****************************************************************         
       C900-WRITE-TOTAL-RECEIPT.                                                
                                                                                
           ADD 1                            TO PC-PAGE-COUNT2.                  
           MOVE PC-PAGE-COUNT2              TO DP132O-PAGE-NUMBER.              
           MOVE '2'                         TO DP132O-REPORT-NUMBER.            
           MOVE DP132O-STANDRD-HEADER-132-COLS                                  
                                            TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           MOVE CODE-CYCLE-LINE             TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE PC-TITLE-RECEIPTS           TO H4-HEADING-TITLE.                
           MOVE H4-HEADING1                 TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE PC-RECEIPTS-HEADING         TO R2-ENTRY-TYPE.                   
           WRITE REPORT-RECORD-2            FROM RCPT-JOURNAL-HEADER            
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           MOVE PC-CURR-WEEK-HDR            TO D2-VERIFIED-WEEK.                
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-LINE                
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           MOVE PV-RECEIPT-AMOUNT           TO D3-VERIFIED-AMTS.                
           MOVE PC-RCPTS-AND-ADJ            TO D3-VERIFIED-TYPE.                
           WRITE REPORT-RECORD-2            FROM D3-JOURNAL-LINE                
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           MOVE PC-PRCH-ADJ-HEADING         TO R2-ENTRY-TYPE.                   
           WRITE REPORT-RECORD-2            FROM RCPT-JOURNAL-HEADER            
               AFTER ADVANCING 3 LINES.                                         
                                                                                
           MOVE PC-CURR-WEEK-HDR            TO D2-VERIFIED-WEEK.                
           WRITE REPORT-RECORD-2            FROM D2-JOURNAL-LINE                
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           MOVE PV-PURCHASE-AMOUNT          TO D3-VERIFIED-AMTS.                
           MOVE PC-PRCH-ADJ                 TO D3-VERIFIED-TYPE.                
           WRITE REPORT-RECORD-2            FROM D3-JOURNAL-LINE                
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           ADD PV-RECEIPT-AMOUNT            TO PV-PURCHASE-AMOUNT               
                GIVING PV-PUR-RCP-TOTAL.                                        
                                                                                
           MOVE D2-WEEK-DATES               TO R2-CURR-DATE.                    
           MOVE PV-PUR-RCP-TOTAL            TO R2-VERIFIED-AMTS.                
           WRITE REPORT-RECORD-2            FROM RCPT-TOTAL-LINE                
               AFTER ADVANCING 3 LINES.                                         
                                                                                
      *****************************************************************         
      * D100-WRITE-HEADINGS   FORMATS HEADINGS FOR A NEW PAGE         *         
      *****************************************************************         
       D100-WRITE-HEADINGS.                                                     
                                                                                
           ADD 1                            TO PC-PAGE-COUNT.                   
           MOVE PC-PAGE-COUNT               TO DP132O-PAGE-NUMBER.              
           MOVE '1'                         TO DP132O-REPORT-NUMBER.            
           MOVE DP132O-STANDRD-HEADER-132-COLS                                  
                                            TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING PAGE.                                            
                                                                                
           MOVE H2-HEADING1                 TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE H2-HEADING2                 TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE H3-HEADING3                 TO REPORT-RECORD.                   
           WRITE REPORT-RECORD                                                  
               AFTER ADVANCING 2.                                               
                                                                                
           MOVE ZERO                        TO PC-LINE-COUNT.                   
           MOVE 7                           TO PC-LINE-COUNT.                   
           MOVE 2                           TO PC-ADVANCE.                      
                                                                                
      *****************************************************************         
      * D200-WRITE-HEADINGS   FORMATS HEADINGS RECPT/ADJ PAGE         *         
      *****************************************************************         
       D200-WRITE-HEADINGS.                                                     
                                                                                
           ADD 1                            TO PC-PAGE-COUNT2.                  
           MOVE PC-PAGE-COUNT2              TO DP132O-PAGE-NUMBER.              
           MOVE '2'                         TO DP132O-REPORT-NUMBER.            
           MOVE DP132O-STANDRD-HEADER-132-COLS                                  
                                            TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING PAGE.                                            
                                                                                
           MOVE H2-HEADING1                 TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING 1.                                               
                                                                                
           MOVE PC-TITLE-RECEIPTS           TO H4-HEADING-TITLE.                
           MOVE H4-HEADING1                 TO REPORT-RECORD-2.                 
           WRITE REPORT-RECORD-2                                                
               AFTER ADVANCING 1.                                               
                                                                                
      *****************************************************************         
      *  DATE TABLE CURSOR                                            *         
      *****************************************************************         
       E100-SELECT-TDTATTR.                                                     
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT       FROM 1 BY 1                         
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                                                                                
            EXEC SQL                                                            
                 SELECT JLN_DTE                                                 
                       ,DAY_NM                                                  
                       ,FSCL_WK_BEG_DTE                                         
                       ,FSCL_WK_END_DTE                                         
                       ,FSCL_MN_END_DTE                                         
                       ,(PFW_END_DTE - 6 DAYS)                                  
                   INTO :DTATTR-JLN-DTE                                         
                       ,:DTATTR-DAY-NM                                          
                       ,:DTATTR-FSCL-WK-BEG-DTE                                 
                       ,:DTATTR-FSCL-WK-END-DTE                                 
                       ,:DTATTR-FSCL-MN-END-DTE                                 
                       ,:PV-PREV-FISCAL-WEEK-BEGIN                              
                   FROM TDTATTR                                                 
                  WHERE KY_DTE = :DTATTR-KY-DTE                                 
                   WITH UR                                                      
            END-EXEC                                                            
                                                                                
            EVALUATE TRUE                                                       
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'DTATTR-KEY-DATE:       ' DTATTR-KY-DTE              
                   DISPLAY 'DTATTR-DAY-NAME:       ' DTATTR-DAY-NM              
                   DISPLAY 'DTATTR-JULIAN-YYYYDDD: ' DTATTR-JLN-DTE             
                   DISPLAY 'DTATTR-FSCL-WK DATES : '                            
                            DTATTR-FSCL-WK-BEG-DTE, ' THRU ',                   
                            DTATTR-FSCL-WK-END-DTE                              
                   DISPLAY 'DTATTR-FSCL-MN-END-DT: '                            
                                               DTATTR-FSCL-MN-END-DTE           
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    'E100-SELECT-TDTATTR'                                
                                            TO PV-PARAGRAPH-NAME                
                   MOVE    'SELECT JULIAN DATES'                                
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9998-SQL-ABEND                                       
            END-EVALUATE                                                        
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TDTATTR **'                  
               MOVE 'E100-SELECT-TDTATTR'   TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT ROW FROM TDTATTR'                                   
                                            TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9998-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      *  TO GET THE INVENTORY NAME                                    *         
      *****************************************************************         
       E200-GET-INV-NAME.                                                       
                                                                                
           INITIALIZE INCNTL-INV-NM-TEXT.                                       
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT       FROM 1 BY 1                         
                 UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                                                                                
            EXEC SQL                                                            
                 SELECT INV_NM                                                  
                   INTO :INCNTL-INV-NM                                          
                   FROM TINCNTL                                                 
                  WHERE INV_ID = :D115-INV-ID                                   
                   WITH UR                                                      
            END-EXEC                                                            
                                                                                
            EVALUATE TRUE                                                       
               WHEN SQLCODE = ZERO                                              
                   MOVE INCNTL-INV-NM-TEXT  TO CYCLE-NAME                       
                   MOVE D115-INV-ID         TO CYCLE-NO                         
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -911                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'INVENTORY ID: ' D115-INV-ID                         
                   MOVE    'E200-GET-INV-NAME'                                  
                                            TO PV-PARAGRAPH-NAME                
                   MOVE    'SELECT ROW FROM TINCNTL'                            
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9998-SQL-ABEND                                       
            END-EVALUATE                                                        
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TINCNTL **'                  
               MOVE 'E200-GET-INV-NAME'                                         
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT ROW FROM TINCNTL'                                   
                                            TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9998-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * READ THE INPUT FILE. FOR TRANSFERS (CODES 30 AND 31),THE BATCH*         
      * NUMBEER IS NOT IN SAME POSITION AS ON THE OTHER TRANSACTIONS  *         
      * SO MOVE IT INTO THE SAME POSITION BEFORE FURTHER PROCESSING.  *         
      *****************************************************************         
       Z100-READ-BATCH-FILE.                                                    
                                                                                
           READ BATCH-FILE                  INTO D115-ON-ORDER-REC              
               AT END MOVE 'Y'              TO SW-EOF-BATCH.                    
                                                                                
           MOVE D115-ON-ORDER-REC           TO HR-HOLD-RECORD                   
                                               D115-PURCH-JOURNAL-REC           
                                               D115-TRANSFER-TO-REC             
                                               D115-TRANSFER-FROM-REC           
                                               D115-MKDWN-REC                   
                                               D115-MARKON-REC                  
                                               D115-ADJ-REC.                    
                                                                                
           IF  SW-FIRST-PASS                                                    
               IF  SW-EOF-BATCH-FILE                                            
                   MOVE 'Y'                 TO SW-INPUT-FILE-PASS-THRU          
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  NOT SW-EOF-BATCH-FILE                                            
               IF  HR-TRANSFER                                                  
                   MOVE D115-TRANS-BATCH    TO PV-TRANSFER-BATCH-POS2-4         
                   MOVE PV-TRANSFER-BATCH   TO HR-BATCH                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * THIS PARA IS USED TO PERFORM THE PROGRAM ABEND                *         
      *****************************************************************         
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                       TO PV-ABEND-CODE.                   
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = VAKRP011'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      *****************************************************************         
      * THIS PARA CALLS THE ABEND CODE                                *         
      *****************************************************************         
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'                  USING PV-ABEND-CODE.                
