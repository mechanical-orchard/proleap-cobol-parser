000100*****************************************************************         
000200 IDENTIFICATION DIVISION.                                                 
000300*****************************************************************         
000400 PROGRAM-ID.    XLKUT053.                                                 
000500 AUTHOR.        PAUL OMAN.                                                
000600 INSTALLATION.  KOHLS.                                                    
000700 DATE-WRITTEN   APRIL 2003.                                               
000800 DATE-COMPILED.                                                           
000900                                                                          
001000*****************************************************************         
001100*    PROGRAM COMPARES RMS AND DOMAIN INFORMATION BY THE PACK    *         
001200*    SKU NUMBER. ANY RECORDS ON RMS, BUT NOT ON DOMAIN OR ON    *         
001300*    DOMAIN BUT NOT ON RMS ARE WRITTEN TO AN ERROR FILE. ANY    *         
001400*    RECORDS THAT MATCH ON THE KEY FIELD, BUT HAS DATA THAT     *         
001401*    DOESN'T MATCH WILL BE WRITTEN TO AN ERROR FILE.            *         
001500*                                                               *         
001900*****************************************************************         
002000 ENVIRONMENT DIVISION.                                                    
002100 INPUT-OUTPUT SECTION.                                                    
002200                                                                          
002300 FILE-CONTROL.                                                            
002400                                                                          
002500     SELECT RMS-SKU-FILE                                                  
002600         ASSIGN TO INP01.                                                 
002700                                                                          
002800     SELECT LOGISTIC-SKU-FILE                                             
002900         ASSIGN TO INP02.                                                 
003000                                                                          
003100     SELECT ERROR-FILE                                                    
003200         ASSIGN TO OUT01.                                                 
003300                                                                          
003400*****************************************************************         
003500 DATA DIVISION.                                                           
003600*****************************************************************         
003700 FILE SECTION.                                                            
003800                                                                          
003900 FD  RMS-SKU-FILE                                                         
004000     RECORDING MODE IS F                                                  
004100     BLOCK CONTAINS 0 RECORDS                                             
004200     LABEL RECORDS ARE STANDARD.                                          
004300                                                                          
004400 01  RMS-SKU-RECORD               PIC X(144).                             
004500                                                                          
004600 FD  LOGISTIC-SKU-FILE                                                    
004700     RECORDING MODE IS F                                                  
004800     BLOCK CONTAINS 0 RECORDS                                             
004900     LABEL RECORDS ARE STANDARD.                                          
005000                                                                          
005100 01  LOGISTIC-SKU-RECORD          PIC X(144).                             
005200                                                                          
005300                                                                          
005400 FD  ERROR-FILE                                                           
005500     RECORDING MODE IS F                                                  
005600     BLOCK CONTAINS 0 RECORDS                                             
005700     LABEL RECORDS ARE STANDARD.                                          
005800                                                                          
005900 01  ERROR-RECORD                 PIC X(144).                             
006000                                                                          
006100                                                                          
006200*****************************************************************         
006300 WORKING-STORAGE SECTION.                                                 
006400                                                                          
006500 01  PS-PROGRAM-SWITCHES.                                                 
006600     05  PS-EOF-INPUT1-SW             PIC X(01)  VALUE 'N'.               
006700         88  PS-EOF-INPUT1-YES                   VALUE 'Y'.               
006800         88  PS-EOF-INPUT1-NO                    VALUE 'N'.               
006900     05  PS-EOF-INPUT2-SW             PIC X(01)  VALUE 'N'.               
007000         88  PS-EOF-INPUT2-YES                   VALUE 'Y'.               
007100         88  PS-EOF-INPUT2-NO                    VALUE 'N'.               
007200     05  PS-EMAIL-SW                  PIC X(01)  VALUE 'N'.               
007300         88  PS-EMAIL-YES                        VALUE 'Y'.               
007400         88  PS-EMAIL-NO                         VALUE 'N'.               
007500                                                                          
007600 01  PC-PROGRAM-CONSTANTS.                                                
007700     05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'XLKUT053'.        
007800     05  PC-FIVE-BLANKS               PIC  X(05) VALUE SPACES.            
007900                                                                          
008000 01  PV-PROGRAM-VARIABLES.                                                
008100     05  PV-RECORDS-READ-1            PIC  9(09) VALUE  0.                
008200     05  PV-RECORDS-READ-2            PIC  9(09) VALUE  0.                
008300     05  PV-RECORDS-WRITTEN           PIC  9(09) VALUE  0.                
008400     05  PV-RECORDS-ON-BOTH           PIC  9(09) VALUE  0.                
008500     05  PV-RECORDS-ON-RMS-ONLY       PIC  9(09) VALUE  0.                
008600     05  PV-RECORDS-ON-LEGACY-ONLY    PIC  9(09) VALUE  0.                
008700     05  PV-RECORDS-DATA-MISMATCH     PIC  9(09) VALUE  0.                
008800     05  PV-ALL-NINES                 PIC  9(09) VALUE 99999999.          
008810*PRB0051963 START                                                         
008900     05  PV-PACK-PK-DESC-LOGISTIC     PIC  X(40).                         
009000     05  PV-PACK-PK-DESC-RMS          PIC  X(40).                         
009100*PRB0051963 END                                                           
009200                                                                          
009300 01  PV-ABEND-FIELDS.                                                     
009400     05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
009500                                                 COMP SYNC.               
009600     05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'XLKUT053'.        
009700     05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
009800     05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
009900     05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
010000     05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
010100     05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
010200     05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
010300                                                                          
010400*--------------------------------------------------------------*          
010500*    IMRD027 - INPUT AND OUTPUT RECORD LAYOUT FOR PACK DATA.   *          
010600*                                                              *          
010700*--------------------------------------------------------------*          
010800                                                                          
010900 01  PV-RMS-INPUT.                                                        
010901     COPY XLRD027.                                                        
011700                                                                          
011710 01  PV-LOGISTIC-INPUT.                                                   
011711     COPY XLRD027.                                                        
012500                                                                          
012600*****************************************************************         
012700 PROCEDURE DIVISION.                                                      
012800*****************************************************************         
012900     DISPLAY ' '.                                                         
013000     DISPLAY '**** BEGINNING XLKUT053 ****'.                              
013100     DISPLAY ' '.                                                         
013200                                                                          
013300     OPEN INPUT  RMS-SKU-FILE                                             
013400                 LOGISTIC-SKU-FILE                                        
013500          OUTPUT ERROR-FILE.                                              
013600     SET PS-EMAIL-NO       TO TRUE.                                       
013700                                                                          
013800     PERFORM 1000-READ-RMS-INPUT.                                         
013900     PERFORM 1100-READ-LOGISTIC-INPUT.                                    
014000                                                                          
014100     PERFORM 2000-PROCESS-INPUT-FILE                                      
014200       UNTIL PS-EOF-INPUT1-YES AND                                        
014300             PS-EOF-INPUT2-YES.                                           
014400                                                                          
014500     CLOSE RMS-SKU-FILE                                                   
014600           LOGISTIC-SKU-FILE                                              
014700           ERROR-FILE.                                                    
014800                                                                          
014900     DISPLAY ' '.                                                         
015000     DISPLAY '  INPUT RMS SKU REC    = '                                  
015100              PV-RECORDS-READ-1.                                          
015200     DISPLAY '  INPUT DOMAIN SKU REC = '                                  
015300              PV-RECORDS-READ-2.                                          
015400     DISPLAY ' '.                                                         
015500     DISPLAY 'FILES ARE MATCHED BY PACK SKU NUMBER'.                      
015600     DISPLAY ' '.                                                         
015700     DISPLAY PC-FIVE-BLANKS                                               
015800             'RECORDS ON BOTH FILES ---------> '                          
015900              PV-RECORDS-ON-BOTH.                                         
016000     DISPLAY ' '.                                                         
016100     DISPLAY '  OUTPUT RECORDS WRITTEN ------> '                          
016200              PV-RECORDS-WRITTEN.                                         
016300     DISPLAY PC-FIVE-BLANKS                                               
016400             'RECORDS ON RMS ONLY -----------> '                          
016500              PV-RECORDS-ON-RMS-ONLY.                                     
016600     DISPLAY PC-FIVE-BLANKS                                               
016700             'RECORDS ON LEGACY ONLY --------> '                          
016800              PV-RECORDS-ON-LEGACY-ONLY.                                  
016900     DISPLAY PC-FIVE-BLANKS                                               
017000             'RECORDS WITH DATA MISMATCH ----> '                          
017100              PV-RECORDS-DATA-MISMATCH.                                   
017200                                                                          
017300     DISPLAY ' '.                                                         
017400     DISPLAY '****  END OF  XLKUT053  ****'.                              
017500     DISPLAY ' '.                                                         
017600                                                                          
017700*    SET RETURN CODE IF E-MAIL SWITCH IS TRUE.                            
017800     IF PS-EMAIL-YES                                                      
017900         MOVE 4095 TO RETURN-CODE                                         
018000     END-IF.                                                              
018100     GOBACK.                                                              
018200                                                                          
018300*****************************************************************         
018400*  READ A RMS SKU RECORD                                                  
018500*****************************************************************         
018600 1000-READ-RMS-INPUT.                                                     
018700                                                                          
018800     MOVE '1000-READ-INPUT-FILE    ' TO PV-ABEND-PARAGRAPH.               
018900                                                                          
019000     INITIALIZE PV-RMS-INPUT.                                             
019100                                                                          
019200     READ RMS-SKU-FILE                                                    
019300          INTO PV-RMS-INPUT                                               
019400            AT END                                                        
019500               SET PS-EOF-INPUT1-YES TO TRUE                              
019600     END-READ.                                                            
019700                                                                          
019800     IF PS-EOF-INPUT1-YES                                                 
019900        MOVE PV-ALL-NINES TO XL027-PACK-PK-SKU-NBR                        
020000                          OF PV-RMS-INPUT                                 
020100     ELSE                                                                 
020200        ADD +1 TO PV-RECORDS-READ-1                                       
020300     END-IF.                                                              
020400                                                                          
020500*****************************************************************         
020600*  READ A LOGISTIC SKU RECORD                                             
020700*****************************************************************         
020800 1100-READ-LOGISTIC-INPUT.                                                
020900                                                                          
021000     MOVE '1100-READ-LOGISTIC-INPUT' TO PV-ABEND-PARAGRAPH.               
021100                                                                          
021200     INITIALIZE PV-LOGISTIC-INPUT.                                        
021300                                                                          
021400     READ LOGISTIC-SKU-FILE                                               
021500          INTO PV-LOGISTIC-INPUT                                          
021600            AT END                                                        
021700               SET PS-EOF-INPUT2-YES TO TRUE                              
021800     END-READ.                                                            
021900                                                                          
022000     IF PS-EOF-INPUT2-YES                                                 
022100        MOVE PV-ALL-NINES TO XL027-PACK-PK-SKU-NBR                        
022200                          OF PV-LOGISTIC-INPUT                            
022300     ELSE                                                                 
022400        ADD +1 TO PV-RECORDS-READ-2                                       
022500     END-IF.                                                              
022600                                                                          
022700*****************************************************************         
022800*                                                               *         
022900*****************************************************************         
023000 2000-PROCESS-INPUT-FILE.                                                 
023100                                                                          
023200     MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
023300                                                                          
023400     IF XL027-PACK-PK-SKU-NBR OF PV-RMS-INPUT <                           
023500        XL027-PACK-PK-SKU-NBR OF PV-LOGISTIC-INPUT                        
023600*************************************                                     
023700*       ON RMS NOT ON DOMAIN                                              
023800*************************************                                     
023900        ADD 1 TO PV-RECORDS-ON-RMS-ONLY                                   
025800        MOVE 'NOT ON DOMAIN PACK HEADER'                                  
025801                              TO XL027-PACK-ERROR-MESSAGE                 
025802                              OF PV-RMS-INPUT                             
025803        PERFORM 2100-WRITE-RMS-ERROR                                      
025804        PERFORM 1000-READ-RMS-INPUT                                       
025805     ELSE                                                                 
025806        IF XL027-PACK-PK-SKU-NBR OF PV-RMS-INPUT =                        
025807           XL027-PACK-PK-SKU-NBR OF PV-LOGISTIC-INPUT                     
025808*************************************                                     
025809*       ON RMS AND ON DOMAIN                                              
025810*************************************                                     
025811           ADD 1 TO PV-RECORDS-ON-BOTH                                    
025812***        IF PV-PACK-RMS-DATA =                                          
025813***           PV-PACK-LOGISTIC-DATA                                       
025814           IF XL027-PACK-DATA OF PV-RMS-INPUT =                           
025815              XL027-PACK-DATA OF PV-LOGISTIC-INPUT                        
025816              PERFORM 1000-READ-RMS-INPUT                                 
025817              PERFORM 1100-READ-LOGISTIC-INPUT                            
025818           ELSE                                                           
025819*************************************                                     
025820*       BUT SOMETHING DOESNT MATCH                                        
025830*************************************                                     
025840*PRB0051963 START                                                         
025841              PERFORM 2200-PK-DESC-CONVERT                                
025842              IF XL027-PACK-DATA OF PV-RMS-INPUT NOT EQUAL                
025843                 XL027-PACK-DATA OF PV-LOGISTIC-INPUT                     
025844*PRB0051963 END                                                           
025845              ADD 1 TO PV-RECORDS-DATA-MISMATCH                           
025846              MOVE 'DATA MISMATCH - RMS'                                  
025847                                   TO XL027-PACK-ERROR-MESSAGE            
025848                                   OF PV-RMS-INPUT                        
025849              PERFORM 2100-WRITE-RMS-ERROR                                
025850              MOVE 'DATA MISMATCH - DOMAIN'                               
025851                                   TO XL027-PACK-ERROR-MESSAGE            
025852                                   OF PV-LOGISTIC-INPUT                   
025900              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
026000              PERFORM 1000-READ-RMS-INPUT                                 
026100              PERFORM 1100-READ-LOGISTIC-INPUT                            
026200*PRB0051963 START                                                         
026300              END-IF                                                      
026301*PRB0051963 END                                                           
026302           END-IF                                                         
026303        ELSE                                                              
026400           IF XL027-PACK-PK-SKU-NBR OF PV-RMS-INPUT >                     
026500              XL027-PACK-PK-SKU-NBR OF PV-LOGISTIC-INPUT                  
026600*************************************                                     
026700*       ON DOMAIN NOT ON RMS                                              
026800*************************************                                     
026900              ADD 1 TO PV-RECORDS-ON-LEGACY-ONLY                          
027000              MOVE 'NOT ON RMS PACK HEADER'                               
027001                                    TO XL027-PACK-ERROR-MESSAGE           
027002                                    OF PV-LOGISTIC-INPUT                  
027003              PERFORM 2150-WRITE-LOGISTIC-ERROR                           
027100              PERFORM 1100-READ-LOGISTIC-INPUT                            
027200           END-IF                                                         
027300        END-IF                                                            
027400     END-IF.                                                              
027500                                                                          
027600*****************************************************************         
027700*  WRITE A RMS RECORD TO THE ERROR FILE                                   
027800*****************************************************************         
027900 2100-WRITE-RMS-ERROR.                                                    
028000                                                                          
028100     WRITE ERROR-RECORD FROM                                              
028200         PV-RMS-INPUT                                                     
028300     END-WRITE.                                                           
028400     ADD +1                 TO PV-RECORDS-WRITTEN.                        
028500                                                                          
028600*    SET SWITCH FOR E-MAIL NOTIFICATION OF RMS ERROR.                     
028700     SET PS-EMAIL-YES       TO TRUE.                                      
028800                                                                          
028900*****************************************************************         
029000*  WRITE A LOGISTIC RECORD TO THE ERROR FILE                              
029100*****************************************************************         
029200 2150-WRITE-LOGISTIC-ERROR.                                               
029300                                                                          
029400     WRITE ERROR-RECORD FROM                                              
029500         PV-LOGISTIC-INPUT                                                
029600     END-WRITE.                                                           
029700     ADD +1                 TO PV-RECORDS-WRITTEN.                        
029800                                                                          
029900*    SET SWITCH FOR E-MAIL NOTIFICATION OF LOGISTIC ERROR.                
030000     SET PS-EMAIL-YES       TO TRUE.                                      
030100                                                                          
030110*PRB0051963 START                                                         
030200 2200-PK-DESC-CONVERT.                                                    
030300                                                                          
030400     MOVE XL027-PACK-PK-DESC OF PV-LOGISTIC-INPUT                         
030500       TO PV-PACK-PK-DESC-LOGISTIC                                        
030600     MOVE FUNCTION UPPER-CASE(PV-PACK-PK-DESC-LOGISTIC)                   
030700                TO XL027-PACK-PK-DESC OF PV-LOGISTIC-INPUT                
030800     MOVE XL027-PACK-PK-DESC OF PV-LOGISTIC-INPUT                         
030900       TO PV-PACK-PK-DESC-RMS                                             
031000     MOVE FUNCTION UPPER-CASE(PV-PACK-PK-DESC-RMS)                        
031100                TO XL027-PACK-PK-DESC OF PV-LOGISTIC-INPUT.               
031200*PRB0051963 END                                                           
