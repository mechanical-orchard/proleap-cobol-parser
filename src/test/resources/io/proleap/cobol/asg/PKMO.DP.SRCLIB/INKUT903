       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    INKUT903.                                                 
       AUTHOR.        RYAN STAUFFACHER.                                         
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-28-08.                                                 
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                         
      * INKUT903 -                                                              
      *                                                                         
      * THIS PROGRAM WILL COMPARE EXTRACTS OF TINSLCS AND                       
      * INT_SUB_CL_INV_LDGR AND CREATE AN EXTRACT FILE OF THE                   
      * DIFFERENCES.                                                            
      *****************************************************************         
      **CHANGES      CHG ID:W34517    S. JACKSON                                
      ** PASS IN THE DB2 TABLE FSCL-MN-END-DTE TO DATA INTEGRITY                
      ** REPORT PROGRAM (INKRP210)                                              
W33304*****************************************************************         
W33304**CHANGES      CHG ID:W33304    K. PETERSON                               
W33304** CHANGES FOR HILO PROJECT - HOLDING INVENTORY LEDGER OPEN               
W33304**   CHANGES REQUIRED DUE TO REMOVAL OF COLUMNS KNW_THFT_RTL_AMT          
W33304**   AND CTOFF_KNW_THFT_AMT FROM TABLE TINSCLS.                           
      **                                                                        
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       CONFIGURATION SECTION.                                                   
      *                                                                         
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT IN-TINSCLS-EXTR-FILE                                          
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT VA-SUB-CL-INV-LDGR-EXTR-FILE                                  
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT DATA-INTEGRITY-DIFF-FILE                                      
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  IN-TINSCLS-EXTR-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  IN-TINSCLS-EXTR-REC  PIC X(131).                                     
                                                                                
       FD  VA-SUB-CL-INV-LDGR-EXTR-FILE                                         
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
W33304 01  VA-SUB-CL-INV-LDGR-EXTR-REC  PIC X(131).                             
                                                                                
       FD  DATA-INTEGRITY-DIFF-FILE                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS.                                            
                                                                                
       01  DATA-INTEGRITY-DIFF-REC     PIC X(80).                               
                                                                                
      *                                                                         
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  PC-CONSTANTS.                                                        
           05  PC-DPKUT500                  PIC X(08) VALUE 'DPKUT500'.         
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-MORE-IN-EXTR-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-IN-EXTR-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-IN-EXTR-RECS         VALUE 'N'.                   
           05  PS-MORE-VA-EXTR-RECS-SW PIC X       VALUE 'Y'.                   
               88  PS-MORE-VA-EXTR-RECS            VALUE 'Y'.                   
               88  PS-NO-MORE-VA-EXTR-RECS         VALUE 'N'.                   
           05  PS-BYPASS-0-DOLLARS-SW  PIC X       VALUE 'Y'.                   
               88  PS-BYPASS-0-DOLLARS             VALUE 'Y'.                   
               88  PS-NO-BYPASS-DOLLARS            VALUE 'N'.                   
                                                                                
       01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
           05  PA-IN-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-VA-RECS-READ         PIC S9(13)  VALUE ZERO.                  
           05  PA-KEY-MATCHES          PIC S9(13)  VALUE ZERO.                  
           05  PA-REPORT-RECS-WRITTEN  PIC S9(13)  VALUE ZERO.                  
                                                                                
       01  CD-COUNTER-DISPLAY.                                                  
           05  CD-COUNT-HOLD1          PIC Z,ZZZ,ZZZ,ZZZ,ZZ9.                   
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-IN-FILE-KEY.                                                  
               10  PV-IN-FSCL-MN-NBR     PIC   X(2).                            
               10  PV-IN-FSCL-MN-END-DTE PIC   X(10).                           
               10  PV-IN-DEPT-NBR        PIC   X(3).                            
               10  PV-IN-SUB-CL-NBR      PIC   X(2).                            
               10  PV-IN-STR-NBR         PIC  S9(4) COMP.                       
           05  PV-VA-FILE-KEY.                                                  
               10  PV-VA-FSCL-MN-NBR     PIC   X(2).                            
               10  PV-VA-FSCL-MN-END-DTE PIC   X(10).                           
               10  PV-VA-DEPT-NBR        PIC   X(3).                            
               10  PV-VA-SUB-CL-NBR      PIC   X(2).                            
               10  PV-VA-STR-NBR         PIC  S9(4) COMP.                       
           05  PV-WORK-ADJ-DTE.                                                 
               10  PV-WORK-ADJ-MM      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-DD      PIC  X(2) VALUE '00'.                    
               10  PV-WORK-ADJ-YY      PIC  X(2) VALUE '00'.                    
           05  PV-WORK-CLASS.                                                   
               10  PV-WORK-CLASS1      PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
           05  PV-WRITE-MAJOR-CLASS.                                            
               10  PV-WRITE-MAJ-CLASS1 PIC  X(1) VALUE '0'.                     
               10  FILLER              PIC  X(1) VALUE '0'.                     
                                                                                
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE               PIC S9(04)  VALUE ZERO                   
                                                   COMP SYNC.                   
               88  AC-BAD-DATA                     VALUE +4003.                 
               88  AC-DB2-ERROR                    VALUE +4013.                 
           05  AA-ABEND-LIT            PIC X(40)   VALUE                        
                 '*****       ABEND'.                                           
           05  AA-PROGRAM-LIT          PIC X(40)   VALUE                        
                   '*****   PROGRAM: INKUT903'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC X(17)   VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC X(35)   VALUE SPACES.                
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER              PIC X(06)   VALUE '*****'.               
               10  AA-MESSAGE          PIC X(127)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC X(40)   VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC X(17)   VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC X(25)   VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC X(25)   VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-3          PIC X(08)   VALUE SPACES.                
           05  AA-DB2-TABLE-4          PIC X(08)   VALUE SPACES.                
                                                                                
       EJECT                                                                    
                                                                                
      *** DATA INTEGRITY DIFFERENCE REPORT FILE                                 
           COPY INRD900.                                                        
      ******************************************************************        
      *    DB2 DCLGEN FOR INVENTORY SUBCLASS STORE TABLE TINSCLS                
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE TINSCLS                                                 
           END-EXEC.                                                            
      ******************************************************************        
      *    DB2 DCLGEN FOR INT_SUB_CL_INV_LDGR TABLE                             
      ******************************************************************        
           EXEC SQL                                                             
                INCLUDE INT00007                                                
           END-EXEC.                                                            
                                                                                
                                                                                
                                                                                
                                                                                
                                                                                
      *                                                                         
      ******************************************************************        
      *    CALENDAR ROUTINE                                                     
      ******************************************************************        
           COPY DPWS500.                                                        
       EJECT                                                                    
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAINLINE-INKUT903.                                                  
      ******************************************************************        
      *                                                                         
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-MAIN-PROCESS.                                           
                                                                                
           PERFORM 8000-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
      *                                                                         
           OPEN INPUT  IN-TINSCLS-EXTR-FILE                                     
                       VA-SUB-CL-INV-LDGR-EXTR-FILE.                            
           OPEN OUTPUT DATA-INTEGRITY-DIFF-FILE.                                
                                                                                
           INITIALIZE PA-PROGRAM-ACCUMULATORS.                                  
           INITIALIZE PV-PROGRAM-VARIABLES.                                     
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
      *                                                                         
           PERFORM 4100-READ-IN-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-IN-EXTR-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY IN EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4002              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-VA-EXTRACT-FILE.                                   
                                                                                
           IF PS-NO-MORE-VA-EXTR-RECS                                           
               MOVE '2000-MAIN-PROCESS'                                         
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'EMPTY VA EXTRACT FILE'                                     
                                       TO AA-MESSAGE                            
               MOVE +4003              TO ABEND-CODE                            
               PERFORM 9900-COMMON-ABEND-ROUTINE                                
           END-IF.                                                              
                                                                                
           PERFORM                                                              
             UNTIL PS-NO-MORE-IN-EXTR-RECS                                      
               AND PS-NO-MORE-VA-EXTR-RECS                                      
               PERFORM 2100-COMPARE-FILES                                       
           END-PERFORM.                                                         
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       2100-COMPARE-FILES.                                                      
      ******************************************************************        
      *                                                                         
           PERFORM 3000-BUILD-FILE-KEYS.                                        
                                                                                
           IF PV-IN-FILE-KEY = PV-VA-FILE-KEY                                   
               ADD 1                   TO PA-KEY-MATCHES                        
               PERFORM 5100-COMPARE-BOTH-FILE-AMTS                              
               PERFORM 4100-READ-IN-EXTRACT-FILE                                
               PERFORM 4200-READ-VA-EXTRACT-FILE                                
           ELSE                                                                 
               IF PV-IN-FILE-KEY < PV-VA-FILE-KEY                               
                   PERFORM 5200-PROCESS-ONLY-IN-FILE                            
                   PERFORM 4100-READ-IN-EXTRACT-FILE                            
               ELSE                                                             
                   PERFORM 5300-PROCESS-ONLY-VA-FILE                            
                   PERFORM 4200-READ-VA-EXTRACT-FILE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       3000-BUILD-FILE-KEYS.                                                    
      ******************************************************************        
      *                                                                         
           INITIALIZE PV-IN-FILE-KEY.                                           
           INITIALIZE PV-VA-FILE-KEY.                                           
                                                                                
           IF DCLTINSCLS = HIGH-VALUES                                          
               MOVE HIGH-VALUES        TO PV-IN-FILE-KEY                        
           ELSE                                                                 
               MOVE INSCLS-FSCL-MN-NBR     TO PV-IN-FSCL-MN-NBR                 
               MOVE INSCLS-FSCL-MN-END-DTE TO PV-IN-FSCL-MN-END-DTE             
               MOVE INSCLS-DEPT-NBR        TO PV-IN-DEPT-NBR                    
               MOVE INSCLS-SUB-CL-NBR      TO PV-IN-SUB-CL-NBR                  
               MOVE INSCLS-STR-NBR         TO PV-IN-STR-NBR                     
           END-IF.                                                              
           IF DCLINT00007 = HIGH-VALUES                                         
               MOVE HIGH-VALUES        TO PV-VA-FILE-KEY                        
           ELSE                                                                 
               MOVE INT00007-FSCL-MN-NBR     TO PV-VA-FSCL-MN-NBR               
               MOVE INT00007-FSCL-MN-END-DTE TO PV-VA-FSCL-MN-END-DTE           
               MOVE INT00007-DEPT-NBR        TO PV-VA-DEPT-NBR                  
               MOVE INT00007-SUB-CL-NBR      TO PV-VA-SUB-CL-NBR                
               MOVE INT00007-STR-NBR         TO PV-VA-STR-NBR                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4100-READ-IN-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-IN-EXTR-RECS                                              
               READ IN-TINSCLS-EXTR-FILE                                        
                 INTO DCLTINSCLS                                                
                   AT END                                                       
                       SET PS-NO-MORE-IN-EXTR-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO DCLTINSCLS                            
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-IN-EXTR-RECS                                              
               ADD 1                   TO PA-IN-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       4200-READ-VA-EXTRACT-FILE.                                               
      ******************************************************************        
      *                                                                         
           IF PS-MORE-VA-EXTR-RECS                                              
               READ VA-SUB-CL-INV-LDGR-EXTR-FILE                                
                 INTO DCLINT00007                                               
                   AT END                                                       
                       SET PS-NO-MORE-VA-EXTR-RECS                              
                                       TO TRUE                                  
                       MOVE HIGH-VALUES                                         
                                       TO DCLINT00007                           
               END-READ                                                         
           END-IF.                                                              
                                                                                
           IF PS-MORE-VA-EXTR-RECS                                              
               ADD 1                   TO PA-VA-RECS-READ                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5100-COMPARE-BOTH-FILE-AMTS.                                             
      ******************************************************************        
      *                                                                         
           MOVE INT00007-FSCL-MN-NBR   TO IN900-FSCL-MN-NBR.                    
W34517     MOVE INT00007-FSCL-MN-END-DTE TO IN900-FSCL-MN-END-DTE.              
           MOVE INT00007-DEPT-NBR      TO IN900-DEPT-NBR.                       
           MOVE INT00007-SUB-CL-NBR    TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE INT00007-SUB-CL-NBR    TO IN900-SUB-CL-NBR.                     
           MOVE INT00007-STR-NBR       TO IN900-STR-NBR.                        
           MOVE SPACES                 TO IN900-DKEY-NBR.                       
                                                                                
           IF INSCLS-SLS-RTL-AMT        NOT = INT00007-SLS-RTL-AMT              
               MOVE INSCLS-SLS-RTL-AMT   TO IN900-IN-AMT                        
               MOVE INT00007-SLS-RTL-AMT TO IN900-VA-AMT                        
               MOVE 6                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-MKDN-RTL-AMT       NOT = INT00007-MKDN-RTL-AMT             
               MOVE INSCLS-MKDN-RTL-AMT   TO IN900-IN-AMT                       
               MOVE INT00007-MKDN-RTL-AMT TO IN900-VA-AMT                       
               MOVE 7                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-MKUP-RTL-AMT       NOT = INT00007-MKUP-RTL-AMT             
               MOVE INSCLS-MKUP-RTL-AMT   TO IN900-IN-AMT                       
               MOVE INT00007-MKUP-RTL-AMT TO IN900-VA-AMT                       
               MOVE 8                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-XFER-IN-RTL-AMT    NOT = INT00007-XFER-IN-RTL-AMT          
               MOVE INSCLS-XFER-IN-RTL-AMT   TO IN900-IN-AMT                    
               MOVE INT00007-XFER-IN-RTL-AMT TO IN900-VA-AMT                    
               MOVE 9                  TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-XFER-OUT-RTL-AMT   NOT = INT00007-XFER-OUT-RTL-AMT         
               MOVE INSCLS-XFER-OUT-RTL-AMT   TO IN900-IN-AMT                   
               MOVE INT00007-XFER-OUT-RTL-AMT TO IN900-VA-AMT                   
               MOVE 10                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-RCPT-RTL-AMT       NOT = INT00007-RCPT-RTL-AMT             
               MOVE INSCLS-RCPT-RTL-AMT   TO IN900-IN-AMT                       
               MOVE INT00007-RCPT-RTL-AMT TO IN900-VA-AMT                       
               MOVE 11                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-PADJ-RTL-AMT       NOT = INT00007-PADJ-RTL-AMT             
               MOVE INSCLS-PADJ-RTL-AMT   TO IN900-IN-AMT                       
               MOVE INT00007-PADJ-RTL-AMT TO IN900-VA-AMT                       
               MOVE 12                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-SHNK-RTL-AMT       NOT = INT00007-SHNK-RTL-AMT             
               MOVE INSCLS-SHNK-RTL-AMT   TO IN900-IN-AMT                       
               MOVE INT00007-SHNK-RTL-AMT TO IN900-VA-AMT                       
               MOVE 13                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-END-INV-RTL-AMT    NOT = INT00007-END-INV-RTL-AMT          
               MOVE INSCLS-END-INV-RTL-AMT   TO IN900-IN-AMT                    
               MOVE INT00007-END-INV-RTL-AMT TO IN900-VA-AMT                    
               MOVE 14                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-CTOFF-SLS-AMT      NOT = INT00007-CTOFF-SLS-AMT            
               MOVE INSCLS-CTOFF-SLS-AMT   TO IN900-IN-AMT                      
               MOVE INT00007-CTOFF-SLS-AMT TO IN900-VA-AMT                      
               MOVE 15                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-CTOFF-SHNK-AMT     NOT = INT00007-CTOFF-SHNK-AMT           
               MOVE INSCLS-CTOFF-SHNK-AMT   TO IN900-IN-AMT                     
               MOVE INT00007-CTOFF-SHNK-AMT TO IN900-VA-AMT                     
               MOVE 16                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-CTOFF-END-INV-AMT  NOT = INT00007-CTOFF-END-INV-AMT        
               MOVE INSCLS-CTOFF-END-INV-AMT   TO IN900-IN-AMT                  
               MOVE INT00007-CTOFF-END-INV-AMT TO IN900-VA-AMT                  
               MOVE 17                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
OCFSFS     IF INSCLS-STR-PARL-SHIP-RTL-AMT                                      
OCFSFS                              NOT = INT00007-STR-PARL-SHIP-RTL-AMT        
OCFSFS         MOVE INSCLS-STR-PARL-SHIP-RTL-AMT    TO IN900-IN-AMT             
OCFSFS         MOVE INT00007-STR-PARL-SHIP-RTL-AMT  TO IN900-VA-AMT             
               MOVE 18                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
           IF INSCLS-STR-EXPTED-RTL-AMT NOT =                                   
              INT00007-STR-EXPTED-RTL-AMT                                       
               MOVE INSCLS-STR-EXPTED-RTL-AMT   TO IN900-IN-AMT                 
               MOVE INT00007-STR-EXPTED-RTL-AMT TO IN900-VA-AMT                 
               MOVE 19                 TO IN900-ERR-MSG-NBR                     
               WRITE DATA-INTEGRITY-DIFF-REC                                    
                   FROM IN900-DATA-INTEGRITY-DIFF-REC                           
               ADD 1                   TO PA-REPORT-RECS-WRITTEN                
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5200-PROCESS-ONLY-IN-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5210-BYPASS-IN-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-PROCESS-NO-VA-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5210-BYPASS-IN-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (INSCLS-SLS-RTL-AMT        = 0) AND                               
              (INSCLS-MKDN-RTL-AMT       = 0) AND                               
              (INSCLS-MKUP-RTL-AMT       = 0) AND                               
              (INSCLS-XFER-IN-RTL-AMT    = 0) AND                               
              (INSCLS-XFER-OUT-RTL-AMT   = 0) AND                               
              (INSCLS-RCPT-RTL-AMT       = 0) AND                               
              (INSCLS-PADJ-RTL-AMT       = 0) AND                               
              (INSCLS-SHNK-RTL-AMT       = 0) AND                               
              (INSCLS-END-INV-RTL-AMT    = 0) AND                               
              (INSCLS-CTOFF-SLS-AMT      = 0) AND                               
              (INSCLS-CTOFF-SHNK-AMT     = 0) AND                               
              (INSCLS-CTOFF-END-INV-AMT  = 0) AND                               
OCFSFS        (INSCLS-STR-PARL-SHIP-RTL-AMT = 0) AND                            
W33304        (INSCLS-STR-EXPTED-RTL-AMT = 0)                                   
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5220-PROCESS-NO-VA-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           MOVE INSCLS-FSCL-MN-NBR     TO IN900-FSCL-MN-NBR.                    
W34517     MOVE INSCLS-FSCL-MN-END-DTE TO IN900-FSCL-MN-END-DTE.                
           MOVE INSCLS-DEPT-NBR        TO IN900-DEPT-NBR.                       
           MOVE INSCLS-SUB-CL-NBR      TO IN900-SUB-CL-NBR                      
           MOVE INSCLS-SUB-CL-NBR      TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE INSCLS-STR-NBR         TO IN900-STR-NBR                         
           MOVE SPACES                 TO IN900-DKEY-NBR                        
                                                                                
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 1                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5300-PROCESS-ONLY-VA-FILE.                                               
      ******************************************************************        
      *                                                                         
           PERFORM 5310-BYPASS-VA-ADJ-0-DLRS-CHK.                               
                                                                                
           IF PS-BYPASS-0-DOLLARS                                               
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5320-PROCESS-NO-IN-RECD-ERR                              
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5310-BYPASS-VA-ADJ-0-DLRS-CHK.                                           
      ******************************************************************        
      *                                                                         
           IF (INT00007-SLS-RTL-AMT        = 0) AND                             
              (INT00007-MKDN-RTL-AMT       = 0) AND                             
              (INT00007-MKUP-RTL-AMT       = 0) AND                             
              (INT00007-XFER-IN-RTL-AMT    = 0) AND                             
              (INT00007-XFER-OUT-RTL-AMT   = 0) AND                             
              (INT00007-RCPT-RTL-AMT       = 0) AND                             
              (INT00007-PADJ-RTL-AMT       = 0) AND                             
              (INT00007-SHNK-RTL-AMT       = 0) AND                             
              (INT00007-END-INV-RTL-AMT    = 0) AND                             
              (INT00007-CTOFF-SLS-AMT      = 0) AND                             
              (INT00007-CTOFF-SHNK-AMT     = 0) AND                             
              (INT00007-CTOFF-END-INV-AMT  = 0) AND                             
OCFSFS        (INT00007-STR-PARL-SHIP-RTL-AMT = 0) AND                          
W33304        (INT00007-STR-EXPTED-RTL-AMT = 0)                                 
               SET PS-BYPASS-0-DOLLARS TO TRUE                                  
           ELSE                                                                 
               SET PS-NO-BYPASS-DOLLARS TO TRUE                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       5320-PROCESS-NO-IN-RECD-ERR.                                             
      ******************************************************************        
      *                                                                         
           MOVE INT00007-FSCL-MN-NBR   TO IN900-FSCL-MN-NBR.                    
W34517     MOVE INT00007-FSCL-MN-END-DTE TO IN900-FSCL-MN-END-DTE.              
           MOVE INT00007-DEPT-NBR      TO IN900-DEPT-NBR.                       
           MOVE INT00007-SUB-CL-NBR    TO PV-WORK-CLASS.                        
           MOVE PV-WORK-CLASS1         TO PV-WRITE-MAJ-CLASS1                   
           MOVE PV-WRITE-MAJOR-CLASS   TO IN900-MAJ-CL-NBR.                     
           MOVE INT00007-SUB-CL-NBR    TO IN900-SUB-CL-NBR.                     
           MOVE INT00007-STR-NBR       TO IN900-STR-NBR.                        
           MOVE 0                      TO IN900-IN-AMT                          
                                          IN900-VA-AMT.                         
           MOVE 2                      TO IN900-ERR-MSG-NBR.                    
                                                                                
           WRITE DATA-INTEGRITY-DIFF-REC                                        
               FROM IN900-DATA-INTEGRITY-DIFF-REC.                              
                                                                                
           ADD 1                       TO PA-REPORT-RECS-WRITTEN.               
                                                                                
      ******************************************************************        
       8000-EOJ.                                                                
      ******************************************************************        
      *                                                                         
           DISPLAY ' '.                                                         
           DISPLAY '  PROGRAM INKUT903 PROCESSING SUMMARY'.                     
           DISPLAY '  -----------------------------------'.                     
           DISPLAY ' '.                                                         
      *                                                                         
           MOVE PA-IN-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'IN RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-VA-RECS-READ        TO CD-COUNT-HOLD1.                       
           DISPLAY 'VA RECORDS READ =        ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-KEY-MATCHES         TO CD-COUNT-HOLD1.                       
           DISPLAY 'KEY MATCHES =            ' CD-COUNT-HOLD1.                  
      *                                                                         
           MOVE PA-REPORT-RECS-WRITTEN TO CD-COUNT-HOLD1.                       
           DISPLAY 'REPORT RECORDS WRITTEN = ' CD-COUNT-HOLD1.                  
      *                                                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE IN-TINSCLS-EXTR-FILE.                                          
           CLOSE VA-SUB-CL-INV-LDGR-EXTR-FILE.                                  
           CLOSE DATA-INTEGRITY-DIFF-FILE.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
      *                                                                         
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
