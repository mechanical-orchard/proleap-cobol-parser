       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              ETKUP049.                                       
       DATE-WRITTEN.            04/14/2000.                                     
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:                                                                 
      *    THIS PROGRAM WILL CREATE AN AUTOMATED FOLLOWUP FILE FOR              
      * MISSING FUNCTIONAL ACKNOWLEDGEMENTS ON PURCHASE ORDERS.                 
      * INFORMATION FOR THE EXTRACT FILE COMES FROM THE TRADING PARTNER         
      * TABLE TTRDPRT, THE ENT ID TABLE TENTNME, THE TRANSACTION STATUS         
      * TABLE TFGTRST, THE TRADING PARTNER FUNCTIONAL GROUP TBL TTRDFNC,        
      * AND THE PURCHASE ORDER SHIPMENT STATUS TABLE TPLNSHP.                   
      *                                                                         
      *    AUTOMATED FOLLOWUP MODE - EMAIL..                                    
      *    (CHANGES TO THIS MODULES MAY NEED TO BE MADE TO......                
      *                     ETKUP048, ETKUP050)                                 
      ******************************************************************        
0305JF* 03-05-2001 JON FIEDLER 0305JF                                           
0305JF* ADDED VENDOR NAME TO SYSOUT ERROR MESSAGE.                              
0305JF******************************************************************        
      * 10/11/2021 GERMAN BANDA                    WR/IR #: CHG0262418*         
      *            - CHANGE CALL OF DPKUT100 TO                       *         
      *              CALL DP010I-NUMERIC-EDIT-ROUTINE                 *         
      *              MAKING THE CALL DYNAMIC VS STATIC                *         
      *                                                               *         
      *---------------------------------------------------------------*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-S390.                                               
       OBJECT-COMPUTER. IBM-S390.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PO-EMAIL-FILE                                                 
               ASSIGN TO OUT01                                                  
               FILE STATUS IS WS-FILE-STATUS.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PO-EMAIL-FILE                                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS PO-EMAIL-RECORD.                                      
                                                                                
       01  PO-EMAIL-RECORD          PIC X(80).                                  
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  EMAIL-LINE-1.                                                        
           05  EL1-VERBIAGE          PIC X(80)  VALUE SPACES.                   
                                                                                
       01  WS-MESSAGE-TABLE.                                                    
           05  FILLER                PIC X(75)  VALUE                           
SP0510     'KOHLs has transmitted the following EDI xxxs to your company        
SP0510-    '.             '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           '   PO Numbers:   Date Transmitted:    GS Control #:   Ship D        
      -    'ate           '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           ' xxxxxxxxxxxxxxx     mm/dd/yyyy         yyyyyyyyy     mm/dd/        
      -    'yyyy          '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'We have not received the 997 (Functional Acknowledgement). K        
      -    'OHLs requires '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'that the Acknowledgement must be sent within 48 hours of tra        
      -    'nsmission.    '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'Failure to respond may result in chargebacks. Thank you for         
      -    'your          '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'assistance in this matter.                                          
      -    '              '.                                                    
           05  FILLER                PIC X(75)  VALUE SPACES.                   
           05  FILLER                PIC X(75)  VALUE                           
           'KOHLs Department Stores                                             
      -    '              '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'Merchandise Information Office - MIO                                
      -    '              '.                                                    
           05  FILLER                PIC X(75)  VALUE                           
           'Email: EDIMIO@kohls.com                                             
      -    '              '.                                                    
       01  WS-MESSAGE-ELEMENTS REDEFINES WS-MESSAGE-TABLE.                      
           05  WS-MSG-ELEMENTS       OCCURS 11 TIMES.                           
               10  WS-MSG-DETAIL     PIC X(75).                                 
                                                                                
       01  WS-MONTH-TABLE.                                                      
           05  FILLER                PIC X(05) VALUE '01JAN'.                   
           05  FILLER                PIC X(05) VALUE '02FEB'.                   
           05  FILLER                PIC X(05) VALUE '03MAR'.                   
           05  FILLER                PIC X(05) VALUE '04APR'.                   
           05  FILLER                PIC X(05) VALUE '05MAY'.                   
           05  FILLER                PIC X(05) VALUE '06JUN'.                   
           05  FILLER                PIC X(05) VALUE '07JUL'.                   
           05  FILLER                PIC X(05) VALUE '08AUG'.                   
           05  FILLER                PIC X(05) VALUE '09SEP'.                   
           05  FILLER                PIC X(05) VALUE '10OCT'.                   
           05  FILLER                PIC X(05) VALUE '11NOV'.                   
           05  FILLER                PIC X(05) VALUE '12DEC'.                   
       01  WS-MONTH-ELEMENTS REDEFINES WS-MONTH-TABLE.                          
           05  WS-MTH-ELEMENTS       OCCURS 12 TIMES.                           
               10  WS-MTH            PIC X(02).                                 
               10  WS-MTH-VARIABLE   PIC X(03).                                 
                                                                                
       01  WS-MSG-INVOICE.                                                      
           05  WS-MSG-VERB1  PIC X(01).                                         
           05  WS-MSG-PO     PIC X(15).                                         
           05  WS-MSG-VERB2  PIC X(05).                                         
           05  WS-MSG-DATE.                                                     
               10  WS-DTE-MM PIC X(02).                                         
               10  WS-DTE-P1 PIC X(01).                                         
               10  WS-DTE-DD PIC X(02).                                         
               10  WS-DTE-P2 PIC X(01).                                         
               10  WS-DTE-YY PIC X(04).                                         
           05  WS-MSG-VERB3  PIC X(09).                                         
           05  WS-MSG-ISA    PIC X(09).                                         
           05  WS-MSG-VERB4  PIC X(05).                                         
           05  WS-MSG-SHPDTE.                                                   
               10  WS-SHPDTE-MM PIC X(02).                                      
               10  WS-SHPDTE-P1 PIC X(01).                                      
               10  WS-SHPDTE-DD PIC X(02).                                      
               10  WS-SHPDTE-P2 PIC X(01).                                      
               10  WS-SHPDTE-YY PIC X(04).                                      
           05  WS-MSG-VERB5  PIC X(11).                                         
SP0510                                                                          
SP0510 01  WS-MSG-HEADER.                                                       
SP0510     05  WS-MSGHDR-VERB1  PIC X(40).                                      
SP0510     05  WS-MSGHDR-DOC    PIC X(03).                                      
SP0510     05  WS-MSGHDR-VERB2  PIC X(32).                                      
                                                                                
       01  WS-TIMESTAMP              PIC X(26)  VALUE SPACES.                   
                                                                                
       01  WS-DB2-TIMESTAMP.                                                    
           05  WS-DB2-DATE.                                                     
               10  WS-DB2-CEN        PIC X(02).                                 
               10  WS-DB2-YEAR       PIC X(02).                                 
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-MTH        PIC X(02).                                 
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-DAY        PIC X(02).                                 
           05  WS-DB2-TIME.                                                     
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-HR         PIC X(02).                                 
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-MIN        PIC X(02).                                 
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-SEC        PIC X(02).                                 
               10  FILLER            PIC X(01).                                 
               10  WS-DB2-MICRO      PIC X(06).                                 
                                                                                
       01  WS-TIMESTAMP-REFORMAT.                                               
           05  WS-DAY                PIC X(02) VALUE ZEROS.                     
           05  FILLER                PIC X(01) VALUE SPACE.                     
           05  WS-MONTH              PIC X(03) VALUE SPACES.                    
           05  FILLER                PIC X(01) VALUE SPACE.                     
           05  WS-YEAR               PIC X(02) VALUE ZEROS.                     
           05  FILLER                PIC X(01) VALUE SPACE.                     
           05  WS-HOUR               PIC X(02) VALUE SPACES.                    
           05  WS-VERB-P1            PIC X(01) VALUE ':'.                       
           05  WS-MINUTE             PIC X(02) VALUE SpACES.                    
           05  WS-VERB-P2            PIC X(01) VALUE ':'.                       
           05  WS-SECOND             PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(01) VALUE SPACE.                     
           05  WS-ZONE               PIC X(03) VALUE 'CDT'.                     
                                                                                
       01  WS-EDIT-ENTID             PIC 9(10)  VALUE ZEROS.                    
       01  WS-ENTID-REFORMAT REDEFINES                                          
           WS-EDIT-ENTID             PIC X(10).                                 
                                                                                
       01  PV-PARAGRAPH-NAME         PIC X(30)  VALUE SPACES.                   
       01  PV-DB2-OPER-ATTEMPTED     PIC X(30)  VALUE SPACES.                   
                                                                                
       01  WS-FILE-STATUS            PIC X(02)  VALUE SPACE.                    
           88  SUCCESSFUL-COMPLETION            VALUE '00'.                     
           88  FILE-NOT-FOUND                   VALUE '23'.                     
                                                                                
       01  WS-REFORMAT-DATE.                                                    
           05  WS-REFDTE-CCYY          PIC X(04) VALUE SPACES.                  
           05  WS-REFDTE-P1            PIC X(01) VALUE SPACES.                  
           05  WS-REFDTE-MM            PIC X(02) VALUE SPACES.                  
           05  WS-REFDTE-P2            PIC X(01) VALUE SPACES.                  
           05  WS-REFDTE-DD            PIC X(02) VALUE SPACES.                  
                                                                                
       01  NUMERIC-FIELD-AREA.                                                  
           05  WS-GS-SEQ-NBR-COUNT   PIC S9(09) COMP VALUE ZEROS.               
           05  WS-MSG-SEQ-NBR-COUNT  PIC S9(09) COMP VALUE ZEROS.               
           05  WS-SUB                PIC S9(09) COMP VALUE ZEROS.               
           05  MTH-NDX               PIC S9(09) COMP VALUE ZEROS.               
           05  WS-EDIT-PO-NUMBER     PIC S9(09) COMP VALUE ZEROS.               
                                                                                
       01  SAVE-AREA.                                                           
           05  WS-ENTID-SAVE        PIC S9(09) COMP VALUE ZEROS.                
           05  WS-GS-CNTL-NBR-SAVE  PIC S9(9)V COMP-3 VALUE ZEROS.              
           05  WS-FUNC-CDE-SEQ-SAVE PIC S9(09) COMP VALUE ZEROS.                
           05  WS-TRN-SET-SEQ-SAVE  PIC S9(09) COMP VALUE ZEROS.                
SP0510     05  WS-TRN-SET-CDE-SAVE  PIC X(3)   VALUE SPACES.                    
           05  WS-TRN-DTL-TXT-SAVE  PIC X(30)  VALUE SPACES.                    
           05  WS-TRANS-DATE-SAVE   PIC X(10)  VALUE SPACES.                    
           05  WS-TP-DKEY-SAVE      PIC S9(09) COMP VALUE ZEROS.                
           05  WS-TP-DKEY-TMST-SAVE PIC X(26)  VALUE SPACES.                    
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW        PIC X(01)  VALUE 'N'.                      
               88  END-OF-FILE                  VALUE 'Y'.                      
           05  WS-FIRST-SW           PIC X(01)  VALUE 'N'.                      
               88  FIRST-THRU                   VALUE 'N'.                      
               88  CONTINUED                    VALUE 'Y'.                      
           05  WS-PROCESSED-SW       PIC X(01)  VALUE 'N'.                      
               88  PROCESSED-TEXT               VALUE 'Y'.                      
           05  WS-ERROR-FOUND-SW     PIC X(01)  VALUE 'N'.                      
               88  ERRORS-FOUND                 VALUE 'Y'.                      
                                                                                
       01  ABEND-CODE                PIC S9(04)  VALUE +0 COMP SYNC.            
           88 ABEND-4013                         VALUE +4013.                   
           88 ABEND-4095                         VALUE +4095.                   
                                                                                
      ******************************************************************        
      * SQLCA MESSAGE MODULE DSNTIAR ARGUMENTS......                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * NUMERIC EDIT LAYOUT ARGUMENTS.....                                      
      ******************************************************************        
           COPY DPWS010I.                                                       
                                                                                
      ******************************************************************        
      * TRADING PARTNER TABLE ARGUMENTS.....                                    
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER ENTERPRISE NAME ARGUMENTS....                           
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TENTNME                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * FUNCTIONAL GROUP TRANSACTION TABLE ARGUMENTS...                         
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TFGTRST                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER FUNCTION GROUP CONTROL TABLE ARGUMENTS......            
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDFNC                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * PURCHASE ORDER SHIPMENT INFORMATION ARGUMENTS......                     
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TPLNSHP                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER DUPLICATE MASTER LISTING TABLE ARGUMENTS.               
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDMLT                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TRADING PARTNER TRANSACTION SET TABLE ARGUMENTS.                        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDTRN                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * DB2 ERROR HANDLING ARGUMENTS....                                        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR THE TRADING PARTNER FUNCTIONAL CONTROL GROUP.          
      ******************************************************************        
           EXEC SQL                                                             
               DECLARE TRDFNC_CURSOR CURSOR FOR                                 
                  SELECT DISTINCT                                               
                       A.TP_DKEY,                                               
                       A.FUNC_CDE,                                              
                       TRAN_SET_CDE,                                            
                       PROD_TEST_IND,                                           
                       B.INACTV_DTE,                                            
                       C.ENT_ID,                                                
                       C.VEND_INCHG_ID,                                         
                       C.VEND_INCHG_ID_DESC                                     
                  FROM TTRDFNC A,                                               
                       TTRDTRN B,                                               
                       TTRDPRT C                                                
                  WHERE C.ENT_ID = :TRDPRT-ENT-ID                               
                    AND C.TP_DKEY = A.TP_DKEY                                   
                    AND C.TP_DKEY = B.TP_DKEY                                   
                    AND A.FUNC_CDE = 'TX'                                       
                    AND TRAN_SET_CDE = '864'                                    
                    AND PROD_TEST_IND = 'P'                                     
                    AND B.INACTV_DTE = '9999-09-09'                             
                    AND C.INACTV_DTE = '9999-09-09'                             
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * TABLE CURSOR FOR DOCUMENT 864 MESSAGE TEXT INFORMATION......            
      ******************************************************************        
           EXEC SQL                                                             
             DECLARE TRDPRT_CURSOR CURSOR FOR                                   
               SELECT DISTINCT                                                  
                  A.ENT_NAME_DESC,                                              
                  B.ENT_ID,                                                     
                  C.FUNC_CDE_CNTL_NBR,                                          
                  C.TRN_SET_CDE,                                                
                  C.VEND_ACK_IND,                                               
                  C.TP_DKEY,                                                    
                  C.TP_DKEY_TMST,                                               
                  C.TRN_DTL_TXT,                                                
                  C.FUNC_CDE_SEQ_NBR,                                           
                  C.TRN_SET_SEQ_NBR,                                            
                  C.PO_ACKNL_STAT_CDE                                           
               FROM TENTNME A,                                                  
                    TTRDPRT B,                                                  
                    TFGTRST C                                                   
              WHERE B.INACTV_DTE = '9999-09-09'                                 
                 AND A.ENT_ID = B.ENT_ID                                        
                 AND A.TYPE_CDE = '01'                                          
                 AND B.TP_DKEY = C.TP_DKEY                                      
                 AND C.VEND_ACK_IND = 'N'                                       
SP0510           AND (C.TRN_SET_CDE = '850' OR                                  
SP0510                C.TRN_SET_CDE = '860')                                    
                 AND C.TP_DKEY_TMST BETWEEN                                     
                     TIMESTAMP(CHAR(CURRENT DATE - 5 DAYS) ||                   
                      '-00.00.00.000000')  AND                                  
                     TIMESTAMP(CHAR(CURRENT DATE - 4 DAYS) ||                   
                      '-23.59.59.999999')                                       
                 AND C.PO_ACKNL_STAT_CDE = ' '                                  
SP0510      ORDER BY 2, 4, 3, 8                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * SQL ERROR HANDLING ARGUMENTS.......                                     
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE TRADING PARTNER CURSOR        
      *      LOOKING FOR DISTINCT PURCHASE ORDERS WHERE A FUNCTIONAL            
      *      ACKNOWLEDGEMENT HAS NOT BEEN RECEIVED IN THE LAST 4 DAYS.          
      *      THE CURSOR INCLUDES THE FOLLOWING TABLES.                          
      *               TTRDPRT - TTRADING PARTNER TABLE.                         
      *               TENTNME - ENT ID NAME TABLE.                              
      *               TFGTRST - TRANSACTION STATUS TABLE.                       
      *      INFORMATION IS SORTED BY ENT ID, GS CONTROL NUMBER, AND PO         
      *      NUMBER.                                                            
      *                                                                         
      *         GENERATE THE FUNCTIONAL ACKNOWLEDGEMENT EXTRACT RECORDS         
      *      FOR TRADING PARTNERS WHO ARE ABLE TO RECEIVE 864S. VERIFY          
      *      THE TRADING PARTNER IS ABLE TO RECEIVE TEXT MESSAGES BY            
      *      READING THE FUNCTIONAL GROUP TABLE CURSOR. CHECK FOR A TX          
      *      FOR THE TRADING PARTNER. THE CURSOR INCLUDES THE FOLLOWING         
      *      TABLES.                                                            
      *               TTRDFNC - TRADING PARTNER FINCTIONAL GROUP TABLE.         
      *               TFGTRST - TRANSACTION STATUS TABLE.                       
      *                                                                         
      *         IF THE TRADING PARTNER IS UNABLE TO RECEIVE AN 864, WE          
      *      WILL THEN SEND MIO AN EMAIL FORMATTED SIMILAR TO THE 864.          
      *      MIO WILL THEN FORWARD THE EMAIL TO THE TRADING PARTNER.            
      *                                                                         
      *         CHECK FOR A PARENT/CHILD RELATIONSHIP BY COMPARING THE          
      *      INTERCHANGE ID ON THE TRADING PARTNER TABLE WITH THE ID            
      *      ON THE TRADING PARTNER DUPLICATE MASTER TABLE. THE CHECK           
      *      AIDS IN ELIMINATING MULTIPLE 864 DOCUMENTS GOING TO A              
      *      TRADING PARTNER WHICH SHARES THEIR ID WITH MULTIPLE VENDORS        
      *               TTRDMLT - TRADING PARTNER DUPLICATE MASTER LIST.          
      *                                                                         
      *         HARD ERRORS OCCUR ON THE OPENS AND CLOSES OF THE TABLE          
      *      CURSORS. ABEND THE MODULE. (ERROR STATUS 4013)                     
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 0100-INITIALIZATION                                          
              THRU 0100-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 1000-PROCESS-FILE                                            
              THRU 1000-PROCESS-FILE-EXIT                                       
                 UNTIL END-OF-FILE.                                             
                                                                                
           PERFORM 9000-END-OF-JOB-TASKS                                        
              THRU 9000-END-OF-JOB-TASKS-EXIT.                                  
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 0100-INITIALIZATION....                                                 
      ******************************************************************        
       0100-INITIALIZATION.                                                     
                                                                                
           OPEN OUTPUT PO-EMAIL-FILE.                                           
                                                                                
           EXEC SQL                                                             
               OPEN TRDPRT_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '0100-INITIALZATION' TO PV-PARAGRAPH-NAME                    
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              PERFORM 9900-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
           MOVE ZEROS            TO WS-SUB.                                     
           MOVE ZEROS            TO WS-GS-SEQ-NBR-COUNT.                        
           MOVE ZEROS            TO WS-MSG-SEQ-NBR-COUNT.                       
           MOVE 'N'              TO END-OF-FILE-SW.                             
                                                                                
       0100-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 1000-PROCESS FILE...                                                    
      *   THE ROUTINE OPENS THE CURSOR FOR TABLES TTRDPRT, TFGTRST, AND         
      *   TENTNME. THE CRITERIA FOR CONTINUED PROCESSING ICLUDE THE             
      *   FOLLOWING.                                                            
      *                                                                         
      *   1. ACTIVE TRADING PARTNER                                             
      *   2. TYPE CODE IS EQUAL TO '01'                                         
      *   3. NO FUNCTIONAL ACKNOWLEDGEMENT HAS BEEN SENT FOR THE PO             
      *   4. THE PO IS TWO DAYS OLD AND WE HAVE NOT RECEIVED THE FA YET.        
      ******************************************************************        
       1000-PROCESS-FILE.                                                       
                                                                                
           EXEC SQL                                                             
               FETCH TRDPRT_CURSOR                                              
               INTO :ENTNME-ENT-NAME-DESC,                                      
                    :TRDPRT-ENT-ID,                                             
                    :FGTRST-FUNC-CDE-CNTL-NBR,                                  
                    :FGTRST-TRN-SET-CDE,                                        
                    :FGTRST-VEND-ACK-IND,                                       
                    :FGTRST-TP-DKEY,                                            
                    :FGTRST-TP-DKEY-TMST,                                       
                    :FGTRST-TRN-DTL-TXT,                                        
                    :FGTRST-FUNC-CDE-SEQ-NBR,                                   
                    :FGTRST-TRN-SET-SEQ-NBR                                     
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               PERFORM 2000-GENERATE-864                                        
                  THRU 2000-GENERATE-864-EXIT                                   
             WHEN SQLCODE IS EQUAL TO +100                                      
               MOVE 'Y' TO END-OF-FILE-SW                                       
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '1000-PROCESS-FILE' TO PV-PARAGRAPH-NAME                    
               MOVE 'FETCH TRDPRT' TO PV-DB2-OPER-ATTEMPTED                     
               MOVE 'Y' TO END-OF-FILE-SW                                       
               PERFORM 9900-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
       1000-PROCESS-FILE-EXIT.                                                  
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-GENERATE THE 864.....                                              
      *   THE ROUTINE OPENS THE CURSOR FOR TABLE TTRDFNC AND CALL THE           
      *   CURSOR PROCESS MODULE 3000-PROCESS-CURSOR. THE ROUTINE CHECKS         
      *   TO SEE IF THE VENDOR IS ABLE TO ACCEPT AUTOMATED TEXT                 
      *   MESSAGES. (DOCUMENT 864)                                              
      ******************************************************************        
       2000-GENERATE-864.                                                       
                                                                                
           EXEC SQL                                                             
               OPEN TRDFNC_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '2000-GENERATE-864' TO PV-PARAGRAPH-NAME                     
              MOVE 'OPEN CURSOR' TO PV-DB2-OPER-ATTEMPTED                       
              MOVE 'Y' TO END-OF-FILE-SW                                        
              PERFORM 9900-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3000-PROCESS-CURSOR                                          
              THRU 3000-PROCESS-CURSOR-EXIT.                                    
                                                                                
           EXEC SQL                                                             
               CLOSE TRDFNC_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '2000-GENERATE-864' TO PV-PARAGRAPH-NAME                     
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              MOVE 'Y' TO END-OF-FILE-SW                                        
              PERFORM 9900-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
       2000-GENERATE-864-EXIT.                                                  
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3000-PROCESS THE CURSOR FOR TABLE TTRDFNC FOR THE ENT-ID.....           
      *   THE ROUTINE CHECKS THE TTRDFNC TABLE TO SEE IF THE VENDOR             
      *   SETUP TO ACCEPT DOCUMENT 864 - TEXT MESSAGES. THE FUNCTION            
      *   CODE MUST BE A "TX" AND A SUCCESS FETCH HAS TO BE PERFORMED           
      *   BEFORE THE EXTRACT IS PERFORMED.                                      
      *                                                                         
      *   IF A ROW IS NOT FOUND, GENERATE AN EMAIL FORMATTED AS THE             
      *   AUTOMATED FOLLOWUP TEXT MESSAGE.                                      
      *                                                                         
      *   IF A PROBLEM OCCURS, ERROR THE SELECT AND END THE PROGRAM.            
      ******************************************************************        
       3000-PROCESS-CURSOR.                                                     
                                                                                
           EXEC SQL                                                             
               FETCH TRDFNC_CURSOR                                              
               INTO :TRDFNC-TP-DKEY,                                            
                    :TRDFNC-FUNC-CDE,                                           
                    :TRDTRN-TRAN-SET-CDE,                                       
                    :TRDTRN-PROD-TEST-IND,                                      
                    :TRDTRN-INACTV-DTE,                                         
                    :TRDPRT-ENT-ID,                                             
                    :TRDPRT-VEND-INCHG-ID,                                      
                    :TRDPRT-VEND-INCHG-ID-DESC                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLCODE IS EQUAL TO +100                                      
               PERFORM 3200-GEN-FUNC-ACK-EMAIL                                  
                  THRU 3200-GEN-FUNC-ACK-EMAIL-EXIT                             
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE 'Y' TO END-OF-FILE-SW                                       
               MOVE '3000-PROCESS-CURSOR' TO PV-PARAGRAPH-NAME                  
               MOVE 'FETCH CURSOR' TO PV-DB2-OPER-ATTEMPTED                     
               PERFORM 9900-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
       3000-PROCESS-CURSOR-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3200-GENERATE A FUNCTIONAL ACKNOWLEDGEMENT EMAIL..                      
      ******************************************************************        
       3200-GEN-FUNC-ACK-EMAIL.                                                 
      ***   TEST FOR A PARENT/CHILD RELATIONSHIP. WE ONLY WANT TO SEND          
      *** ONE EMAIL PER ENT ID. NO DUPLICATES OF THE SAME TO THE SAME           
      *** VENDOR. THE FOLLOWING SCENARIOS HOLD TO FOR 864 GENERATION.           
      ***                                                                       
      ***   SCENARIOS.                                                          
      ***   1.  IF THE INTERCHANGE ID IS NOT ON TTRDMLT, SEND THE 864 AS        
      ***       IS TO THE TRADING PARTNER.                                      
      ***   2.  IF THE INTERCHANGE ID IS ON TTRDMLT AND THE ENTIDS MATCH        
      ***       BETWEEN TTRDPRT AND TTRDMLT, SEND THE 864 AS IS TO THE          
      ***       TRADING PARTNER.                                                
      ***   3.  IF THE INTERCHANGE ID IS ON TTRDMLT AND THE ENTIDS DO           
      ***       NOT MATCH BETWEEN TTRDPRT AND TTRDMLT, DO NOT SEND THE          
      ***       864 TO THE TRADING PARTNER.                                     
      ***                                                                       
                                                                                
           EXEC SQL                                                             
             SELECT PRNT_ENT_ID                                                 
               INTO :TRDMLT-PRNT-ENT-ID                                         
             FROM TTRDMLT                                                       
             WHERE VND_INCHG_ID = :TRDPRT-VEND-INCHG-ID                         
               AND VND_INCHG_ID_DESC = :TRDPRT-VEND-INCHG-ID-DESC               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLCODE IS EQUAL TO +100                                      
               GO TO 3200-GEN-FUNC-ACK-CONTINUE                                 
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               DISPLAY SPACES                                                   
               DISPLAY 'UNABLE TO LOCATE TTRDMLT TABLE ENTRY - ETKUP049'        
               DISPLAY 'VEND_INCHG_ID_DESC = ' TRDPRT-VEND-INCHG-ID-DESC        
               DISPLAY 'VEND_INCHG_ID = ' TRDPRT-VEND-INCHG-ID                  
               DISPLAY 'ENT_ID = ' TRDPRT-ENT-ID                                
               DISPLAY 'SQLCODE = ' SQLCODE                                     
               MOVE '3200-GEN-FUNC-ACK' TO PV-PARAGRAPH-NAME                    
               MOVE 'SLECT TRDMLT' TO PV-DB2-OPER-ATTEMPTED                     
               MOVE 'Y' TO END-OF-FILE-SW                                       
               PERFORM 9900-SQL-ERROR                                           
               GO TO 3200-GEN-FUNC-ACK-EMAIL-EXIT                               
           END-EVALUATE.                                                        
                                                                                
           IF TRDMLT-PRNT-ENT-ID IS NOT EQUAL TO TRDPRT-ENT-ID                  
               PERFORM 5300-UPD-TFGTRST-STATUS                                  
                  THRU 5300-UPD-TFGTRST-STATUS-EXIT                             
               GO TO 3200-GEN-FUNC-ACK-EMAIL-EXIT.                              
                                                                                
       3200-GEN-FUNC-ACK-CONTINUE.                                              
      ***   PRIME THE CONTROL BREAKS FOR TEXT MESSAGE PROCESSING AND            
      *** BEGIN TO GENERATE THE FIRST DOCUMENT....                              
                                                                                
           IF FIRST-THRU                                                        
             MOVE TRDPRT-ENT-ID            TO WS-ENTID-SAVE                     
             MOVE FGTRST-FUNC-CDE-CNTL-NBR TO WS-GS-CNTL-NBR-SAVE               
             MOVE FGTRST-FUNC-CDE-SEQ-NBR  TO WS-FUNC-CDE-SEQ-SAVE              
             MOVE FGTRST-TRN-SET-SEQ-NBR   TO WS-TRN-SET-SEQ-SAVE               
SP0510       MOVE FGTRST-TRN-SET-CDE       TO WS-TRN-SET-CDE-SAVE               
             MOVE FGTRST-TRN-DTL-TXT       TO WS-TRN-DTL-TXT-SAVE               
             MOVE FGTRST-TP-DKEY-TMST      TO WS-TRANS-DATE-SAVE                
             MOVE FGTRST-TP-DKEY           TO WS-TP-DKEY-SAVE                   
             MOVE FGTRST-TP-DKEY-TMST      TO WS-TP-DKEY-TMST-SAVE              
             PERFORM 4500-EMAIL-HEADER                                          
                THRU 4500-EMAIL-HEADER-EXIT                                     
             MOVE ZEROS         TO WS-SUB                                       
             PERFORM 4600-EMAIL-VERB                                            
                THRU 4600-EMAIL-VERB-EXIT                                       
                  VARYING WS-SUB FROM 1 BY 1                                    
                  UNTIL WS-SUB IS GREATER THAN 2                                
             PERFORM 4700-EMAIL-PO                                              
                THRU 4700-EMAIL-PO-EXIT                                         
             MOVE 'Y'                      TO WS-FIRST-SW.                      
                                                                                
      ***   CONTROL BREAK PROCESSING...                                         
      *** 1.  TRDPRT-ENT-ID (TRADING PARTNER ENTITY ID)                         
      *** 2.  FGTRST-FUNC-CDE-CNTL-NBR (FUNCTIONAL GROUP)                       
      *** 3.  FGTRST-TRN-DTL-TXT (PURCHASE ORDER NUMBER)                        
      *** 4.  FGTRST-TP-DKEY (TP DKEY)                                          
      *** 5.  FGTRST-TP-DKEY-TMST (TP DKEY TIMESTAMP)                           
                                                                                
SP0510     IF (TRDPRT-ENT-ID EQUAL WS-ENTID-SAVE AND                            
SP0510         FGTRST-TRN-SET-CDE = WS-TRN-SET-CDE-SAVE)                        
             IF FGTRST-FUNC-CDE-CNTL-NBR EQUAL WS-GS-CNTL-NBR-SAVE              
               IF FGTRST-TRN-DTL-TXT EQUAL WS-TRN-DTL-TXT-SAVE                  
                 IF FGTRST-TP-DKEY EQUAL WS-TP-DKEY-SAVE                        
                   IF FGTRST-TP-DKEY-TMST EQUAL WS-TP-DKEY-TMST-SAVE            
                     NEXT SENTENCE                                              
                   ELSE                                                         
                     MOVE TRDPRT-ENT-ID TO WS-ENTID-SAVE                        
                     MOVE FGTRST-FUNC-CDE-CNTL-NBR                              
                                  TO WS-GS-CNTL-NBR-SAVE                        
                     MOVE FGTRST-FUNC-CDE-SEQ-NBR                               
                                  TO WS-FUNC-CDE-SEQ-SAVE                       
                     MOVE FGTRST-TRN-SET-SEQ-NBR                                
                                  TO WS-TRN-SET-SEQ-SAVE                        
SP0510               MOVE FGTRST-TRN-SET-CDE TO WS-TRN-SET-CDE-SAVE             
                     MOVE FGTRST-TRN-DTL-TXT                                    
                                  TO WS-TRN-DTL-TXT-SAVE                        
                     MOVE FGTRST-TP-DKEY-TMST TO WS-TRANS-DATE-SAVE             
                     MOVE FGTRST-TP-DKEY TO WS-TP-DKEY-SAVE                     
                     MOVE FGTRST-TP-DKEY-TMST TO WS-TP-DKEY-TMST-SAVE           
                     PERFORM 4700-EMAIL-PO                                      
                        THRU 4700-EMAIL-PO-EXIT                                 
                 ELSE                                                           
                   MOVE TRDPRT-ENT-ID TO WS-ENTID-SAVE                          
                   MOVE FGTRST-FUNC-CDE-CNTL-NBR                                
                                  TO WS-GS-CNTL-NBR-SAVE                        
                   MOVE FGTRST-FUNC-CDE-SEQ-NBR                                 
                                  TO WS-FUNC-CDE-SEQ-SAVE                       
                   MOVE FGTRST-TRN-SET-SEQ-NBR                                  
                                  TO WS-TRN-SET-SEQ-SAVE                        
SP0510             MOVE FGTRST-TRN-SET-CDE TO WS-TRN-SET-CDE-SAVE               
                   MOVE FGTRST-TRN-DTL-TXT                                      
                                  TO WS-TRN-DTL-TXT-SAVE                        
                   MOVE FGTRST-TP-DKEY-TMST TO WS-TRANS-DATE-SAVE               
                   MOVE FGTRST-TP-DKEY TO WS-TP-DKEY-SAVE                       
                   MOVE FGTRST-TP-DKEY-TMST TO WS-TP-DKEY-TMST-SAVE             
                   PERFORM 4700-EMAIL-PO                                        
                      THRU 4700-EMAIL-PO-EXIT                                   
               ELSE                                                             
                 MOVE TRDPRT-ENT-ID TO WS-ENTID-SAVE                            
                 MOVE FGTRST-FUNC-CDE-CNTL-NBR                                  
                                  TO WS-GS-CNTL-NBR-SAVE                        
                 MOVE FGTRST-FUNC-CDE-SEQ-NBR                                   
                                  TO WS-FUNC-CDE-SEQ-SAVE                       
                 MOVE FGTRST-TRN-SET-SEQ-NBR                                    
                                  TO WS-TRN-SET-SEQ-SAVE                        
SP0510           MOVE FGTRST-TRN-SET-CDE TO WS-TRN-SET-CDE-SAVE                 
                 MOVE FGTRST-TRN-DTL-TXT                                        
                                  TO WS-TRN-DTL-TXT-SAVE                        
                 MOVE FGTRST-TP-DKEY-TMST TO WS-TRANS-DATE-SAVE                 
                 MOVE FGTRST-TP-DKEY TO WS-TP-DKEY-SAVE                         
                 MOVE FGTRST-TP-DKEY-TMST TO WS-TP-DKEY-TMST-SAVE               
                 PERFORM 4700-EMAIL-PO                                          
                    THRU 4700-EMAIL-PO-EXIT                                     
             ELSE                                                               
               MOVE TRDPRT-ENT-ID TO WS-ENTID-SAVE                              
               MOVE FGTRST-FUNC-CDE-CNTL-NBR                                    
                                  TO WS-GS-CNTL-NBR-SAVE                        
               MOVE FGTRST-FUNC-CDE-SEQ-NBR                                     
                                  TO WS-FUNC-CDE-SEQ-SAVE                       
               MOVE FGTRST-TRN-SET-SEQ-NBR                                      
                                  TO WS-TRN-SET-SEQ-SAVE                        
SP0510         MOVE FGTRST-TRN-SET-CDE TO WS-TRN-SET-CDE-SAVE                   
               MOVE FGTRST-TRN-DTL-TXT                                          
                                  TO WS-TRN-DTL-TXT-SAVE                        
               MOVE FGTRST-TP-DKEY-TMST TO WS-TRANS-DATE-SAVE                   
               MOVE FGTRST-TP-DKEY TO WS-TP-DKEY-SAVE                           
               MOVE FGTRST-TP-DKEY-TMST TO WS-TP-DKEY-TMST-SAVE                 
               PERFORM 4700-EMAIL-PO                                            
                  THRU 4700-EMAIL-PO-EXIT                                       
           ELSE                                                                 
             MOVE ZEROS         TO WS-SUB                                       
             PERFORM 4600-EMAIL-VERB                                            
                THRU 4600-EMAIL-VERB-EXIT                                       
                  VARYING WS-SUB FROM 4 BY 1                                    
                  UNTIL WS-SUB IS GREATER THAN 11                               
             MOVE SPACES        TO EMAIL-LINE-1                                 
             WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1                            
             MOVE TRDPRT-ENT-ID            TO WS-ENTID-SAVE                     
             MOVE FGTRST-FUNC-CDE-CNTL-NBR TO WS-GS-CNTL-NBR-SAVE               
             MOVE FGTRST-FUNC-CDE-SEQ-NBR  TO WS-FUNC-CDE-SEQ-SAVE              
             MOVE FGTRST-TRN-SET-SEQ-NBR   TO WS-TRN-SET-SEQ-SAVE               
SP0510       MOVE FGTRST-TRN-SET-CDE       TO WS-TRN-SET-CDE-SAVE               
             MOVE FGTRST-TRN-DTL-TXT       TO WS-TRN-DTL-TXT-SAVE               
             MOVE FGTRST-TP-DKEY-TMST      TO WS-TRANS-DATE-SAVE                
             MOVE FGTRST-TP-DKEY           TO WS-TP-DKEY-SAVE                   
             MOVE FGTRST-TP-DKEY-TMST      TO WS-TP-DKEY-TMST-SAVE              
             PERFORM 4500-EMAIL-HEADER                                          
                THRU 4500-EMAIL-HEADER-EXIT                                     
             MOVE ZEROS         TO WS-SUB                                       
             PERFORM 4600-EMAIL-VERB                                            
                THRU 4600-EMAIL-VERB-EXIT                                       
                  VARYING WS-SUB FROM 1 BY 1                                    
                  UNTIL WS-SUB IS GREATER THAN 2                                
             PERFORM 4700-EMAIL-PO                                              
                THRU 4700-EMAIL-PO-EXIT                                         
           END-IF.                                                              
                                                                                
           MOVE 'Y'                 TO WS-PROCESSED-SW.                         
                                                                                
       3200-GEN-FUNC-ACK-EMAIL-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4500-RECORD TYPE 01 (THE MESSAGE HEADER SEGMENT)....                    
      ******************************************************************        
       4500-EMAIL-HEADER.                                                       
      *** RFC822 - EMAIL HEADER.....                                            
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'helo' DELIMITED BY SIZE                                      
                  ' ' DELIMITED BY SIZE                                         
                  'KOHLS' DELIMITED BY SIZE                                     
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** RFC822 - MAIL FROM...                                                 
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'mail from:' DELIMITED BY SIZE                                
                  '<' DELIMITED BY SIZE                                         
                  'IS-EDI@KOHLS.COM' DELIMITED BY SIZE                          
                  '>' DELIMITED BY SIZE                                         
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** RFC822 - RCPT FROM...                                                 
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'rcpt to:' DELIMITED BY SIZE                                  
                  '<' DELIMITED BY SIZE                                         
                  'EDIMIO@KOHLS.COM' DELIMITED BY SIZE                          
                  '>' DELIMITED BY SIZE                                         
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** RFC822 - RCPT FROM (CC: - carbon copy) same as rcpt to:....         
      *                                                                         
      *    MOVE SPACES           TO EMAIL-LINE-1.                               
      *    STRING 'rcpt to:' DELIMITED BY SIZE                                  
      *           '<' DELIMITED BY SIZE                                         
      *           'ROD.JANSSEN@KOHLS.COM' DELIMITED BY SIZE                     
      *           '>' DELIMITED BY SIZE                                         
      *                          INTO EL1-VERBIAGE.                             
      *    WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
      *                                                                         
      *** RFC822 - DATA...                                                      
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           MOVE 'data'           TO EL1-VERBIAGE.                               
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** EMAIL - DATE LINE...                                                  
                                                                                
           PERFORM 5100-CALC-DTE-ROUTINE                                        
              THRU 5100-CALC-DTE-ROUTINE-EXIT.                                  
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'Date:     ' DELIMITED BY SIZE                                
                  WS-TIMESTAMP-REFORMAT DELIMITED BY SIZE                       
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** EMAIL - FROM LINE..                                                   
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'From:     ' DELIMITED BY SIZE                                
                  'IS-EDI@KOHLS.COM' DELIMITED BY SIZE                          
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** EMAIL - TO LINE..                                                     
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'To:       ' DELIMITED BY SIZE                                
                  'EDIMIO@KOHLS.COM' DELIMITED BY SIZE                          
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** EMAIL - CC LINE(S)...                                                 
      *                                                                         
      *    MOVE SPACES           TO EMAIL-LINE-1.                               
      *    STRING 'Cc:       ' DELIMITED BY SIZE                                
      *           'ROD.JANSSEN@KOHLS.COM' DELIMITED BY SIZE                     
      *                          INTO EL1-VERBIAGE.                             
      *    WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
      *                                                                         
      *** EMAIL - SUBJECT LINE...                                               
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           STRING 'Subject:  ' DELIMITED BY SIZE                                
                  'Missing 997' DELIMITED BY SIZE                               
                  ' - ' DELIMITED BY SIZE                                       
                  ENTNME-ENT-NAME-DESC DELIMITED BY SIZE                        
                                 INTO EL1-VERBIAGE.                             
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
      *** EMAIL BREAK....                                                       
                                                                                
           MOVE SPACES           TO EMAIL-LINE-1.                               
           WRITE PO-EMAIL-RECORD FROM EMAIL-LINE-1.                             
                                                                                
       4500-EMAIL-HEADER-EXIT.                                                  
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4600-EMAIL VERBIAGE (THE DETAIL VERBIAGE SEGMENT).....                  
      ******************************************************************        
       4600-EMAIL-VERB.                                                         
SP0510                                                                          
SP0510     IF WS-SUB = 1                                                        
SP0510        MOVE WS-MSG-DETAIL(WS-SUB) TO WS-MSG-HEADER                       
SP0510        MOVE WS-TRN-SET-CDE-SAVE TO WS-MSGHDR-DOC                         
SP0510        WRITE PO-EMAIL-RECORD FROM WS-MSG-HEADER                          
SP0510     ELSE                                                                 
              WRITE PO-EMAIL-RECORD FROM WS-MSG-DETAIL (WS-SUB).                
                                                                                
                                                                                
       4600-EMAIL-VERB-EXIT.                                                    
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4700-EMAIL PURCHASE ORDER INFORMATION..                                 
      ******************************************************************        
       4700-EMAIL-PO.                                                           
      **** GENERATE THE EMAIL PURCHASE ORDER LINE.....                          
                                                                                
           MOVE SPACES               TO EMAIL-LINE-1.                           
                                                                                
           MOVE WS-MSG-DETAIL (3)    TO WS-MSG-INVOICE.                         
           MOVE WS-TRN-DTL-TXT-SAVE (1:15) TO WS-MSG-PO.                        
                                                                                
           MOVE WS-GS-CNTL-NBR-SAVE  TO WS-MSG-ISA.                             
                                                                                
           MOVE WS-TRANS-DATE-SAVE   TO WS-REFORMAT-DATE.                       
           MOVE WS-REFDTE-MM         TO WS-DTE-MM.                              
           MOVE '/'                  TO WS-DTE-P1.                              
           MOVE WS-REFDTE-DD         TO WS-DTE-DD.                              
           MOVE '/'                  TO WS-DTE-P2.                              
           MOVE WS-REFDTE-CCYY       TO WS-DTE-YY.                              
                                                                                
           PERFORM 5000-GET-PO-SHIP-DATE                                        
              THRU 5000-GET-PO-SHIP-DATE-EXIT.                                  
                                                                                
           MOVE WS-MSG-INVOICE       TO EL1-VERBIAGE.                           
                                                                                
           WRITE PO-EMAIL-RECORD     FROM EMAIL-LINE-1.                         
                                                                                
      **** UPDATE TFGTRST WITH THE ACKNOWLEDGEMENT STATUS....                   
                                                                                
           PERFORM 5200-UPD-TFGTRST-STATUS                                      
              THRU 5200-UPD-TFGTRST-STATUS-EXIT.                                
                                                                                
       4700-EMAIL-PO-EXIT.                                                      
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 5000-GET THE PURCHASE ORDER SHIPMENT DATE....                           
      ******************************************************************        
       5000-GET-PO-SHIP-DATE.                                                   
                                                                                
           MOVE WS-TRN-DTL-TXT-SAVE(1:15) TO DP010I-UNEDITED-FIELD.             
           MOVE 17                        TO DP010I-MAXIMUM-DIGITS.             
           MOVE ZEROS                     TO DP010I-MAXIMUM-DECIMALS.           
                                                                                
           SET DP010I-NEGATIVE-NOT-ALLOWED TO TRUE.                             
                                                                                
           CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
                         USING DP010I-NUMERIC-EDIT-AREA                         
                                                                                
           IF DP010I-ERROR-DETECTED                                             
               MOVE ZEROS           TO WS-EDIT-PO-NUMBER                        
           ELSE                                                                 
               MOVE DP010I-NUM-FIELD-17-DIG-0-DEC TO WS-EDIT-PO-NUMBER.         
                                                                                
           EXEC SQL                                                             
             SELECT DONT_SHIP_BEF_DTE                                           
             INTO :PLNSHP-DONT-SHIP-BEF-DTE                                     
             FROM TPLNSHP                                                       
             WHERE PO_NBR = :WS-EDIT-PO-NUMBER                                  
               AND VER_STATUS_CDE = 'A'                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               MOVE PLNSHP-DONT-SHIP-BEF-DTE TO WS-REFORMAT-DATE                
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
               MOVE '9999-09-09'    TO WS-REFORMAT-DATE                         
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-REFDTE-MM         TO WS-SHPDTE-MM.                           
           MOVE '/'                  TO WS-SHPDTE-P1.                           
           MOVE WS-REFDTE-DD         TO WS-SHPDTE-DD.                           
           MOVE '/'                  TO WS-SHPDTE-P2.                           
           MOVE WS-REFDTE-CCYY       TO WS-SHPDTE-YY.                           
                                                                                
       5000-GET-PO-SHIP-DATE-EXIT.                                              
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 5100-CALCULATE THE DATE FOR THE EMAIL.....                              
      ******************************************************************        
       5100-CALC-DTE-ROUTINE.                                                   
                                                                                
           EXEC SQL                                                             
              SET :WS-TIMESTAMP = CURRENT TIMESTAMP                             
           END-EXEC.                                                            
                                                                                
           MOVE WS-TIMESTAMP         TO WS-DB2-TIMESTAMP.                       
                                                                                
           MOVE WS-DB2-YEAR          TO WS-YEAR.                                
           MOVE WS-DB2-DAY           TO WS-DAY.                                 
           MOVE WS-DB2-MTH           TO WS-MONTH.                               
                                                                                
           PERFORM 6000-GET-MONTH                                               
              THRU 6000-GET-MONTH-EXIT                                          
                  VARYING MTH-NDX FROM 1 BY 1                                   
                  UNTIL MTH-NDX IS GREATER THAN 12.                             
                                                                                
           MOVE WS-DB2-HR            TO WS-HOUR.                                
           MOVE ':'                  TO WS-VERB-P1, WS-VERB-P2.                 
           MOVE WS-DB2-MIN           TO WS-MINUTE.                              
           MOVE WS-DB2-SEC           TO WS-SECOND.                              
           MOVE 'CDT'                TO WS-ZONE.                                
                                                                                
       5100-CALC-DTE-ROUTINE-EXIT.                                              
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 5200-UPDATE TFGTRST WITH THE ACKNOWLEDGEMENT STATUS CODE.               
      ***   IF A CHANGE IS MADE TO THIS ROUTINE, VERIFY THAT THE CHANGE         
      *** IS NEEDED/NOT NEEDED TO THE 5300- ROUTINE.                            
      ******************************************************************        
       5200-UPD-TFGTRST-STATUS.                                                 
                                                                                
           EXEC SQL                                                        CL*17
               UPDATE TFGTRST                                              CL*20
                 SET PO_ACKNL_STAT_CDE = 'E'                                    
               WHERE TP_DKEY          = :WS-TP-DKEY-SAVE                   CL*17
                 AND TP_DKEY_TMST     = :WS-TP-DKEY-TMST-SAVE              CL*17
                 AND FUNC_CDE_SEQ_NBR = :WS-FUNC-CDE-SEQ-SAVE              CL*17
                 AND TRN_SET_SEQ_NBR  = :WS-TRN-SET-SEQ-SAVE               CL*19
SP0510           AND TRN_SET_CDE      = :WS-TRN-SET-CDE-SAVE                    
           END-EXEC.                                                       CL*17
                                                                           CL*17
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE IS EQUAL TO ZERO                                    
                   CONTINUE                                                     
               WHEN SQLWARN0 IS NOT EQUAL TO SPACES                             
               WHEN SQLCODE IS NOT EQUAL TO ZERO                                
                   DISPLAY SPACES                                               
                   DISPLAY '** ETKUP049 **'                                     
                   DISPLAY '** 5200-UPDATE-TFGTRST-W-STATUS **'                 
                   DISPLAY 'SQLCODE = ' SQLCODE                                 
0305JF             PERFORM 7000-GET-VENDOR-NAME                                 
0305JF             DISPLAY 'VENDOR = ' ENTNME-ENT-NAME-DESC                     
                   DISPLAY 'TP_DKEY = ' WS-TP-DKEY-SAVE                         
                   DISPLAY 'TP_DKEY_TMST = ' WS-TP-DKEY-TMST-SAVE               
                   DISPLAY 'FUNC_CDE_SEQ_NBR = ' WS-FUNC-CDE-SEQ-SAVE           
                   DISPLAY 'TRN_SET_SEQ_NBR = ' WS-TRN-SET-SEQ-SAVE             
SP0510             DISPLAY 'TRN_SET_CDE      = ' WS-TRN-SET-CDE-SAVE            
                   DISPLAY 'FUNC_CDE_CNTL_NBR = ' WS-GS-CNTL-NBR-SAVE           
                   MOVE 'Y' TO WS-ERROR-FOUND-SW                                
           END-EVALUATE.                                                        
                                                                           CL*17
       5200-UPD-TFGTRST-STATUS-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 5300-UPDATE TFGTRST WITH THE ACKNOWLEDGEMENT STATUS CODE.               
      ***   IF A CHANGE IS MADE TO THIS ROUTINE, VERIFY THAT THE CHANGE         
      *** IS NEEDED/NOT NEEDED TO THE 5200- ROUTINE.                            
      ******************************************************************        
       5300-UPD-TFGTRST-STATUS.                                                 
                                                                                
           EXEC SQL                                                        CL*17
               UPDATE TFGTRST                                              CL*20
                 SET PO_ACKNL_STAT_CDE = 'E'                                    
               WHERE TP_DKEY          = :FGTRST-TP-DKEY                    CL*17
                 AND TP_DKEY_TMST     = :FGTRST-TP-DKEY-TMST               CL*17
                 AND FUNC_CDE_SEQ_NBR = :FGTRST-FUNC-CDE-SEQ-NBR           CL*17
                 AND TRN_SET_SEQ_NBR  = :FGTRST-TRN-SET-SEQ-NBR            CL*19
SP0510           AND TRN_SET_CDE      = :FGTRST-TRN-SET-CDE                     
           END-EXEC.                                                       CL*17
                                                                           CL*17
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE IS EQUAL TO ZERO                                    
                   CONTINUE                                                     
               WHEN SQLWARN0 IS NOT EQUAL TO SPACES                             
               WHEN SQLCODE IS NOT EQUAL TO ZERO                                
                   DISPLAY SPACES                                               
                   DISPLAY '** ETKUP048 **'                                     
                   DISPLAY '** 5300-UPDATE-TFGTRST-W-STATUS **'                 
                   DISPLAY 'SQLCODE = ' SQLCODE                                 
0305JF             PERFORM 7100-GET-VENDOR-NAME                                 
0305JF             DISPLAY 'VENDOR = ' ENTNME-ENT-NAME-DESC                     
                   DISPLAY 'TP_DKEY = ' FGTRST-TP-DKEY                          
                   DISPLAY 'TP_DKEY_TMST = ' FGTRST-TP-DKEY-TMST                
                   DISPLAY 'FUNC_CDE_SEQ_NBR = ' FGTRST-FUNC-CDE-SEQ-NBR        
                   DISPLAY 'TRN_SET_SEQ_NBR = ' FGTRST-TRN-SET-SEQ-NBR          
SP0510             DISPLAY 'TRN_SET_CDE      = ' FGTRST-TRN-SET-CDE             
                   MOVE 'Y' TO WS-ERROR-FOUND-SW                                
           END-EVALUATE.                                                        
                                                                           CL*17
       5300-UPD-TFGTRST-STATUS-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 6000-GET THE MONTH NAME...                                              
      ******************************************************************        
       6000-GET-MONTH.                                                          
                                                                                
           IF WS-MONTH IS EQUAL TO WS-MTH (MTH-NDX)                             
               MOVE WS-MTH-VARIABLE (MTH-NDX) TO WS-MONTH                       
               MOVE 99                        TO MTH-NDX.                       
                                                                                
       6000-GET-MONTH-EXIT.                                                     
           EXIT.                                                                
                                                                                
0305JF 7000-GET-VENDOR-NAME.                                                    
0305JF                                                                          
0305JF     EXEC SQL                                                             
0305JF        SELECT ENT_NAME_DESC                                              
0305JF          INTO :ENTNME-ENT-NAME-DESC                                      
0305JF          FROM TENTNME E                                                  
0305JF              ,TTRDPRT T                                                  
0305JF         WHERE TP_DKEY = :WS-TP-DKEY-SAVE                                 
0305JF           AND T.ENT_ID = E.ENT_ID                                        
0305JF           AND TYPE_CDE = '01'                                            
0305JF     END-EXEC.                                                            
0305JF                                                                          
0305JF     IF SQLCODE NOT = +000                                                
0305JF        MOVE 'NOT FOUND' TO ENTNME-ENT-NAME-DESC                          
0305JF     END-IF.                                                              
0305JF                                                                          
0305JF 7100-GET-VENDOR-NAME.                                                    
0305JF                                                                          
0305JF     EXEC SQL                                                             
0305JF        SELECT ENT_NAME_DESC                                              
0305JF          INTO :ENTNME-ENT-NAME-DESC                                      
0305JF          FROM TENTNME E                                                  
0305JF              ,TTRDPRT T                                                  
0305JF         WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
0305JF           AND T.ENT_ID = E.ENT_ID                                        
0305JF           AND TYPE_CDE = '01'                                            
0305JF     END-EXEC.                                                            
0305JF                                                                          
0305JF     IF SQLCODE NOT = +000                                                
0305JF        MOVE 'NOT FOUND' TO ENTNME-ENT-NAME-DESC                          
0305JF     END-IF.                                                              
0305JF                                                                          
      ******************************************************************        
      * 9000-END OF JOB TASKS......                                             
      ******************************************************************        
       9000-END-OF-JOB-TASKS.                                                   
      *** GENERATE A FINAL CONTROL BREAK, IF NECESSARY. (EMAIL)                 
                                                                                
           IF PROCESSED-TEXT                                                    
               MOVE ZEROS         TO WS-SUB                                     
               PERFORM 4600-EMAIL-VERB                                          
                  THRU 4600-EMAIL-VERB-EXIT                                     
                    VARYING WS-SUB FROM 4 BY 1                                  
                    UNTIL WS-SUB IS GREATER THAN 11.                            
                                                                                
      *** CLOSE THE FILES...                                                    
                                                                                
           IF ERRORS-FOUND AND                                                  
              ABEND-CODE IS NOT EQUAL TO +4013                                  
               SET ABEND-4095 TO TRUE.                                          
                                                                                
           CLOSE PO-EMAIL-FILE.                                                 
                                                                                
           EXEC SQL                                                             
               CLOSE TRDPRT_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE IS EQUAL TO ZERO                                      
               CONTINUE                                                         
             WHEN SQLWARN IS NOT EQUAL TO SPACES                                
             WHEN SQLCODE IS NOT EQUAL TO ZERO                                  
              MOVE '9000-END-OF-JOB-TASKS' TO PV-PARAGRAPH-NAME                 
              MOVE 'CLOSE CURSOR' TO PV-DB2-OPER-ATTEMPTED                      
              PERFORM 9900-SQL-ERROR                                            
           END-EVALUATE.                                                        
                                                                                
       9000-END-OF-JOB-TASKS-EXIT.                                              
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9900-SQL ERROR HANDLING...                                              
      ******************************************************************        
       9900-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
