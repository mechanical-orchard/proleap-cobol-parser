      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.                DPKCS019.                                     
       AUTHOR.                    DWYNE WALLACE.                                
       INSTALLATION.              KOHLS DEPARTMENT STORES.                      
       DATE-WRITTEN.              SEPTEMBER 1990.                               
       DATE-COMPILED.                                                           
       SKIP2                                                                    
      ******************************************************************        
      *  CICS PROGRAM - DPKCS019                                       *        
      *  TRANSACTION ID. - Z019                                        *        
      *                                                                *        
      *                UPDATE MNEMONIC PROGRAM                         *        
      *                                                                *        
      *  THIS TRANSACTION PROCESSES ALL UPDATE REQUESTS FOR THE TMNEMO-*        
      *  NIC DB2 DATABASE. THIS DATABASE IS USED TO CONTROL NAVIGATION *        
      *  WITHIN CICS.                                                  *        
      *                                                                *        
      *  PF KEY PROCESSING:                                            *        
      *                                                                *        
      *         ENTER - EDIT SCREEN                                    *        
      *         PA2   - TOGGLE FUNCTION KEY LINES                      *        
      *         PF5   - REFRESH LIST FROM DATABASE                     *        
      *         PF15  - UPDATE DATABASE                                *        
      *         CLEAR - RESET SCREEN TO LAST DISPLAY                   *        
      *                                                                *        
      *  NAVIGATION PF KEY PROCESSING:                                 *        
      *                                                                *        
      *         *SEE MNEMONIC FILE FOR NAVIGATION PF KEY DEFINITIONS.  *        
      *                                                                *        
      *  CHANGED 4/92 BY M. FURLICK PER W/R 1937.                      *        
      *         ADDED ENTERABLE PROGRAM ID,                            *        
      *               ENTERABLE ACCESSIBILITY INDICATOR;               *        
      *         MINOR SCREEN CHANGE TO ACCOMODATE THESE FIELDS.        *        
      *                                                                *        
      *    03/14/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.       *        
      *                       1) MODIFIED MAP (DPKM019) AND PGM TO     *        
      *                          SET THE UPD-VSAM-IND, DISPLAY IT ON   *        
      *                          THE SCREEN, AND TO ADD IT TO THE      *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *                       2) MODIFIED MAP (DPKM019) AND PGM TO     *        
      *                          DISPLAY PROGRAM-NAME, FROM THE        *        
      *                          DB2 TMNEMONIC TABLE.                  *        
      *    09/09/99  TKMA464  ADD ACCURATE LOCATION REQUIRED INDICATOR.*        
      *                                                                *        
      ******************************************************************        
       SKIP2                                                                    
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       EJECT                                                                    
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       SKIP2                                                                    
       WORKING-STORAGE SECTION.                                                 
       SKIP1                                                                    
      *********************                                                     
      * PROGRAM CONSTANTS *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-CONSTANT-AREA.                                               
           05  PC-MAXIMUM-COMMAREA-LENGTH                                       
                                       PIC S9(4)      VALUE +4072               
                                                      COMP SYNC.                
           05  PC-CURRENT-MAP-NAME     PIC X(8)       VALUE 'DP019A  '.         
           05  PC-CURRENT-MAPSET-NAME  PIC X(8)       VALUE 'DPKM019 '.         
           05  PC-CURRENT-PROGRAM-NAME PIC X(8)       VALUE 'DPKCS019'.         
           05  PC-NAVIGATOR-TRAN-ID    PIC X(4)       VALUE 'Z008'.             
           05  PC-MENU-TRAN-ID         PIC X(4)       VALUE 'MENU'.             
           05  PC-MENU-TRAN            PIC X(4)       VALUE 'Z007'.             
           05  PC-MENU-COMMAREA-LEN    PIC X(4)       VALUE '1845'.             
           05  PC-LEAVE-SYSTEM-LENGTH  PIC S9(4)      VALUE +80                 
                                                      COMP SYNC.                
           05  PC-MRO-PGM              PIC X(8)       VALUE 'DFHCRP  '.         
           05  PC-MAX-COMM-LENGTH      PIC 9(4)       VALUE 2865.               
           05  PC-MIN-COMM-LENGTH      PIC 9(4)       VALUE 0003.               
           05  PC-TRANSACTION-ENABLED  PIC S9(8)      COMP                      
                                                      VALUE 23.                 
           05  PC-TRANSACTION-DISABLED PIC S9(8)      COMP                      
                                                      VALUE 24.                 
           05  PC-TRANSACTION-DISABLING                                         
                                       PIC S9(8)      COMP                      
                                                      VALUE 25.                 
           05  PC-Y                    PIC X          VALUE 'Y'.                
                                                                                
           05  PC-TSYMSG-00005         PIC 9(5)       VALUE 00005.              
           05  PC-TSYMSG-00008         PIC 9(5)       VALUE 00008.              
           05  PC-TSYMSG-00045         PIC 9(5)       VALUE 00045.              
           05  PC-TSYMSG-00059         PIC 9(5)       VALUE 00059.              
           05  PC-TSYMSG-00070         PIC 9(5)       VALUE 00070.              
           05  PC-TSYMSG-00148         PIC 9(5)       VALUE 00148.              
           05  PC-TSYMSG-00255         PIC 9(5)       VALUE 00255.              
           05  PC-TSYMSG-00256         PIC 9(5)       VALUE 00256.              
           05  PC-TSYMSG-00257         PIC 9(5)       VALUE 00257.              
           05  PC-TSYMSG-00258         PIC 9(5)       VALUE 00258.              
       SKIP2                                                                    
      *********************                                                     
      * PROGRAM VARIABLES *                                                     
      *********************                                                     
       SKIP1                                                                    
       01  PROGRAM-VARIABLE-AREA.                                               
           05  PV-CICS-RESPONSE        PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-RESP                 PIC S9(8)      VALUE ZERO                
                                                      COMP SYNC.                
           05  PV-TERMINAL             PIC X(4)       VALUE SPACES.             
           05  PV-PROGRAM              PIC X(8)       VALUE SPACES.             
           05  PV-LEAVE-SYSTEM-MSG     PIC X(80)      VALUE SPACES.             
           05  PV-HOLD-MESSAGE         PIC X(78)      VALUE SPACES.             
           05  PV-HOLD-SEVERITY-IND    PIC X(1)       VALUE SPACES.             
           05  PV-HOLD-AFRMOBJI        PIC X(3).                                
           05  PV-HOLD-AFRMOBJI-NUM    REDEFINES                                
               PV-HOLD-AFRMOBJI        PIC 9(3).                                
           05  PV-MNEMONIC             PIC X(8)       VALUE SPACES.             
           05  PV-COMM-LENGTH-ALPHA    PIC X(4).                                
           05  PV-COMM-LENGTH-NUMERIC  REDEFINES                                
               PV-COMM-LENGTH-ALPHA    PIC 9(4).                                
       EJECT                                                                    
      ********************                                                      
      * PROGRAM SWITCHES *                                                      
      ********************                                                      
       SKIP1                                                                    
       01  PROGRAM-SWITCHES.                                                    
           05  PS-MAP-SEND-TYPE        PIC X          VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET           VALUE '1'.                
               88  PS-SEND-MAP-N-DATA-FRSET-CPOSN     VALUE '2'.                
               88  PS-SEND-DATA-ONLY-FRSET            VALUE '3'.                
               88  PS-SEND-DATA-ONLY                  VALUE '4'.                
               88  PS-SEND-DATA-ONLY-ERASEAUP         VALUE '5'.                
               88  PS-SEND-MAP-ONLY-FRSET             VALUE '6'.                
           05  PS-LOCAL-FUNCTION-KEY-IND                                        
                                       PIC X          VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-NO               VALUE 'N'.                
               88  PS-LOCAL-FUNCTION-YES              VALUE 'Y'.                
           05  PS-ROW-FOUND-IND        PIC X          VALUE 'N'.                
               88  PS-ROW-FOUND-NO                    VALUE 'N'.                
               88  PS-ROW-FOUND-YES                   VALUE 'Y'.                
           05  PS-EDIT-ERROR-SW        PIC X          VALUE 'N'.                
               88  PS-EDIT-ERROR-NO                   VALUE 'N'.                
               88  PS-EDIT-ERROR-YES                  VALUE 'Y'.                
           05  PS-NEW-MNEMONIC-ENTERED-IND                                      
                                       PIC X          VALUE 'N'.                
               88  PS-NEW-MNEMONIC-ENTERED-NO         VALUE 'N'.                
               88  PS-NEW-MNEMONIC-ENTERED-YES        VALUE 'Y'.                
           05  PS-CHECK-PGM-SW         PIC X          VALUE 'N'.                
               88  PS-DONT-CHECK-PROGRAM              VALUE 'N'.                
               88  PS-CHECK-PROGRAM                   VALUE 'Y'.                
       EJECT                                                                    
      ******************                                                        
      * ERROR MESSAGES *                                                        
      ******************                                                        
       SKIP1                                                                    
       01  ERROR-MESSAGES.                                                      
           05  NOT-NAVG-TRANID-MSG     PIC X(78)      VALUE                     
               'THE TRANSACTION ENTERED MUST NOT EQUAL Z008 OR MENU             
      -        '                      '.                                        
           05  NOT-CICS-TRANID-MSG     PIC X(78)      VALUE                     
               'THE TRANSACTION MUST FIRST BE DEFINED TO CICS BEFORE IT         
      -        'CAN BE ADDED TO NAVIGA'.                                        
           05  TIMESTAMP-ERROR-MSG     PIC X(78)      VALUE                     
               'DATABASE TIMESTAMP INCONSISTENCY FOUND                          
      -        '                      '.                                        
           05  INVALID-NEXT-APPL-MSG   PIC X(78)      VALUE                     
               'NEXT APPLICATION CODE INVALID                                   
      -        '                      '.                                        
           05  PM-PROGRAM-MESSAGE-22          PIC X(78)    VALUE                
           'LOGIC ERROR IN TRYING TO READ MD LIST QUEUE - RESP = '.             
           05  FILLER REDEFINES PM-PROGRAM-MESSAGE-22.                          
               10  FILLER                     PIC X(61).                        
               10  PM-RESP-22                 PIC 9999.                         
               10  FILLER                     PIC X(12).                        
       EJECT                                                                    
      **************************                                                
      * DB2 COMMUNICATION AREA *                                                
      **************************                                                
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
       SKIP2                                                                    
      ***********************************                                       
      * DB2 SQL RETURN CODE DEFINITIONS *                                       
      *    USING '88' LEVEL             *                                       
      ***********************************                                       
       SKIP1                                                                    
       01  SQL-RETURN-CODE             PIC S9(9)      COMP-4.                   
           88  SQL-SUCCESSFUL                         VALUE +0.                 
           88  SQL-ROW-FOUND                          VALUE +0.                 
           88  SQL-ROW-NOT-FOUND                      VALUE +100.               
           88  SQL-WARNING                            VALUE +100                
                                                            +558                
                                                            +560.               
           88  SQL-DUPLICATE-ROW                      VALUE -803.               
           88  SQL-MORE-THAN-1-ROW                    VALUE -811.               
           88  SQL-NOT-AVAILABLE                      VALUE -911.               
           88  SQL-DB2-DOWN                           VALUE -923.               
           88  SQL-NULL-VALUE                         VALUE -305.               
           88  SQL-DEPENDENT-ROW                      VALUE -532.               
       EJECT                                                                    
      **************************************                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DFHAID.                                                         
       EJECT                                                                    
      *---------------------------------------------------------------*         
      *    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005   *         
      *---------------------------------------------------------------*         
           COPY DPWS006.                                                        
       EJECT                                                                    
      **************************************************************            
      * WORKING-STORAGE LAYOUT USED TO LOAD THE FUNCTION KEY LINES *            
      **************************************************************            
           COPY DPWS009.                                                        
       EJECT                                                                    
      ***************************                                               
      * COMMON ABEND DATA AREAS *                                               
      ***************************                                               
       SKIP1                                                                    
           COPY DPWS013.                                                        
       EJECT                                                                    
      *****************************                                             
      * ARCHITECTURE MESSAGE AREA *                                             
      *****************************                                             
       SKIP1                                                                    
           COPY DPRD507I.                                                       
       EJECT                                                                    
      *********************************************************                 
      * KOHL'S DEFINED:                                       *                 
      * STANDARD ATTRIBUTE AND PRINTER CONTROL CHARACTER LIST *                 
      *********************************************************                 
       SKIP1                                                                    
           COPY DPWS015.                                                        
       EJECT                                                                    
      **************************************                                    
      * KOHL'S:                            *                                    
      * STANDARD ATTENTION IDENTIFIER LIST *                                    
      **************************************                                    
       SKIP1                                                                    
           COPY DPWS016.                                                        
       EJECT                                                                    
      ***************************************************************           
      * WORKING-STORAGE LAYOUT USED TO GET THE SYSTEM DATE AND TIME *           
      ***************************************************************           
           COPY DPWS017.                                                        
       EJECT                                                                    
      *******************************                                           
      * STANDARD COMMUNICATION AREA *                                           
      *******************************                                           
       SKIP1                                                                    
           COPY DPWS020.                                                        
           05  FILLER                  REDEFINES                                
               DP020-VARIABLE-COMMAREA.                                         
      ***************************************                                   
      * CICSARC INTER APPLICATION DATA AREA *                                   
      ***************************************                                   
               10  INTER-APPL-COMM-AREA                                         
                                       PIC X(200).                              
           COPY DPWS050.                                                        
           COPY DPWS051.                                                        
           COPY DPWS052.                                                        
           COPY DPWS055.                                                        
      **********************************                                        
      * APPLICATION SPECIFIC COMM AREA *                                        
      **********************************                                        
               10  APPL-SPECIFIC-COMM-AREA                                      
                                       PIC X(2865).                             
               10  DP019-APPL-SPECIFIC-COMM-AREA                                
                                       REDEFINES                                
                   APPL-SPECIFIC-COMM-AREA.                                     
                   15  FILLER          PIC X(02).                               
                   15  DP019-CURRENT-SCREEN-IMAGE.                              
                       20  DP019-SCREEN-TRAN-ID                                 
                                       PIC X(4).                                
                       20  DP019-SCREEN-MNEMONIC                                
                                       PIC X(8).                                
                       20  DP019-SCREEN-MNEMONIC-TITLE                          
                                       PIC X(30).                               
                       20  DP019-SCREEN-AVAILABIL-IND                           
                                       PIC X.                                   
                       20  DP019-SCREEN-COMMAREA-FORMAT                         
                                       PIC X.                                   
                       20  DP019-SCREEN-SAVE-COMM-IND                           
                                       PIC X.                                   
                       20  DP019-SCREEN-SAVE-COMM-LENGTH                        
                                       PIC X(4).                                
                       20  DP019-SCREEN-PROGRAM                                 
                                       PIC X(8).                                
                       20  DP019-SCREEN-GLOBAL-ACCESS                           
                                       PIC X(1).                                
                       20  DP019-SCREEN-TYPE-CODE                               
                                       PIC X(1).                                
                       20  DP019-SCREEN-UPD-VSAM-IND                            
                                       PIC X(1).                                
                       20  DP019-SCREEN-DB2-PKG-CDE                             
                                       PIC X(1).                                
                       20  DP019-SCREEN-CMNT-TXT                                
                                       PIC X(40).                               
                       20  DP019-ACCURATE-LOCATION-REQ                          
                                       PIC X(1).                                
                   15  DP019-TRAN-DEFINED-CICS                                  
                                       PIC X.                                   
                   15  DP019-SELECT-MAINT-RESULT                                
                                       PIC X.                                   
                       88  DP019-RESULT-IS-CREATE      VALUE '1'.               
                       88  DP019-RESULT-IS-UPDATE      VALUE '2'.               
                       88  DP019-RESULT-IS-DELETE      VALUE '3'.               
                       88  DP019-RESULT-IS-READ        VALUE '4'.               
                       88  DP019-RESULT-IS-ERROR       VALUE '5'.               
                   15  DP019-INTER-APPL-COMMAREA.                               
                       20  DP019-INTER-APPL-FORMAT-CODE                         
                                       PIC X(04).                               
                       20  DP019-INTER-APPL-ACKNLG-CODE                         
                                       PIC X(04).                               
                       20  DP019-MITM-AREA.                                     
                           25  DP019-MITM-MENU-NAME                             
                                       PIC X(08).                               
                           25  DP019-MITM-ITEM-MNEMONIC                         
                                       PIC X(08).                               
                           25  DP019-MITM-SEQUENCE-NUMBER                       
                                       PIC S9(4)      COMP.                     
                           25  DP019-MITM-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP019-MITM-TITLE                                 
                                       PIC X(30).                               
                           25  DP019-MITM-ITEM-TITLE                            
                                       PIC X(30).                               
                           25  DP019-MITM-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP019-MITM-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP019-MITM-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP019-MITM-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP019-MITM-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP019-MITM-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(109).                              
                       20  DP019-FKEY-AREA                                      
                                       REDEFINES                                
                           DP019-MITM-AREA.                                     
                           25  DP019-FKEY-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP019-FKEY-FUNCTION-KEY                          
                                       PIC S9(04)     COMP.                     
                           25  DP019-FKEY-FUNCT-KEY-MNEMONIC                    
                                       PIC X(08).                               
                           25  DP019-FKEY-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP019-FKEY-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP019-FKEY-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP019-FKEY-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP019-FKEY-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP019-FKEY-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(173).                              
                       20  DP019-CMIA-AREA                                      
                                       REDEFINES                                
                           DP019-FKEY-AREA.                                     
                           25  DP019-CMIA-MNEMONIC                              
                                       PIC X(08).                               
                           25  DP019-CMIA-TRAN-ID                               
                                       PIC X(04).                               
                           25  DP019-CMIA-TITLE                                 
                                       PIC X(30).                               
                           25  DP019-CMIA-MAINTENANCE-RESULT                    
                                       PIC X.                                   
                               88  DP019-CMIA-RESULT-IS-CREATE                  
                                       VALUE '1'.                               
                               88  DP019-CMIA-RESULT-IS-UPDATE                  
                                       VALUE '2'.                               
                               88  DP019-CMIA-RESULT-IS-DELETE                  
                                       VALUE '3'.                               
                               88  DP019-CMIA-RESULT-IS-READ                    
                                       VALUE '4'.                               
                               88  DP019-CMIA-RESULT-IS-ERROR                   
                                       VALUE '5'.                               
                           25  FILLER  PIC X(149).                              
                   15  DP019-TIMESTAMP     PIC X(26).                           
                   15  DP019-LAST-MNEMONIC PIC X(08).                           
                   15  DP019-MN-SUB        PIC S9(4) COMP.                      
                   15  DP019-MN-ITEM       PIC S9(4) COMP.                      
                   15  DP019-TSQB-SW       PIC X(01).                           
                       88  DP019-USE-TSQB            VALUE 'Y'.                 
                       88  DP019-NO-TSQB             VALUE 'N'.                 
                   15  DP019-SAVE-TSQB-SW  PIC X(01).                           
                       88  DP019-SAVE-TSQB           VALUE 'Y'.                 
                       88  DP019-DONT-SAVE-TSQB      VALUE 'N'.                 
                   15  DP019-DFHCRP-SW     PIC X(01).                           
                       88  DP019-ISSUED-MESSAGE      VALUE 'Y'.                 
                       88  DP019-DIDNT-ISSUE-MESSAGE VALUE 'N'.                 
                   15  DP010-ERR-00256-OVERRIDE-SW    PIC X.                    
                       88  DP010-ERR-00256-OVERRIDE-Y VALUE 'Y'.                
                       88  DP010-ERR-00256-OVERRIDE-N VALUE 'N'.                
                   15  DP019-SAVE-INTER-APPL-COMMAREA                           
                                           PIC X(200).                          
                   15  DP019-SAVE-TSQB-RECORD PIC X(1500).                      
       EJECT                                                                    
      **************************************************************            
      * TSQ 'B' RECORD LAYOUT.                                     *            
      **************************************************************            
           COPY DPWS002.                                                        
       EJECT                                                                    
      **************************************************************            
      * PARAMETER LIST USED TO CALL THE 'TRANSLATE AID' SUBROUTINE *            
      **************************************************************            
           COPY DPWS045.                                                        
        EJECT                                                                   
      ********************************                                          
      * DSECT FOR THE DPKM010 MAPSET *                                          
      ********************************                                          
       SKIP1                                                                    
           COPY DPKM019.                                                        
       SKIP2                                                                    
      ************************************************                          
      * REDEFINES OF DP019AI MAPSET OF DETAIL FIELDS *                          
      ************************************************                          
       01  MD-MAP-DATA-AREA            REDEFINES                                
           DP019AO.                                                             
           05  FILLER                  PIC X(600).                              
       EJECT                                                                    
      ******************************                                            
      * DCLGEN FOR TABLE TMNEMONIC *                                            
      ******************************                                            
       SKIP1                                                                    
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
       EJECT                                                                    
       LINKAGE SECTION.                                                         
       SKIP2                                                                    
       01  DFHCOMMAREA.                                                         
           05  FILLER                         OCCURS  1 TO 4072 TIMES           
                                              DEPENDING ON EIBCALEN.            
               10  FILLER                     PIC X.                            
       EJECT                                                                    
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
      * A000-UPDATE-MNEMONIC                                           *        
      * (Z019-00 UPDATE MNEMONIC)                                      *        
      * MAINLINE SECTION.                                              *        
      ******************************************************************        
                                                                                
       A000-UPDATE-MNEMONIC.                                                    
                                                                                
           PERFORM DP015-0000-CHECK-COMMAREA.                                   
                                                                                
      *****************************************************************         
      * THE VARIABLE 'RETURN-WITH-TRAN-ID' WILL BE SET TO 'Y' IN      *         
      * U13-CHECK-COMMAREA IF A BAD COMMAREA OR NO COMMAREA WAS       *         
      * ENCOUNTERED. IF THAT HAPPENS U13-CHECK-COMMAREA STARTS THE    *         
      * NAVIGATION TRANSACTION AND SETS THIS INDICATOR TO 'Y'. CHECK- *         
      * ING FOR A 'Y' HERE WILL PREVENT THIS TRANSACTION FROM CONTINU-*         
      * ING TO EXECUTE IF THE NAVIGATOR HAS BEEN REQUESTED TO START.  *         
      *****************************************************************         
                                                                                
           IF  DP020-SRC-RETURN-W-TRAN-ID                                       
               PERFORM A050-CONTROL-PROCESSING                                  
           END-IF.                                                              
                                                                                
           PERFORM DP003-0000-RETURN-TO-CICS.                                   
                                                                                
           GOBACK.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A050-CONTROL-PROCESSING                                        *        
      * (Z019-01 CONTROL PROCESSING)                                   *        
      ******************************************************************        
                                                                                
       A050-CONTROL-PROCESSING.                                                 
                                                                                
           EVALUATE TRUE                                                        
                                                                                
               WHEN DP020-NEXT-ACT-INITIAL                                      
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   INITIALIZE DP019-APPL-SPECIFIC-COMM-AREA                     
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A240-GET-FIRST-MNEMONIC                              
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN DP020-NEXT-ACT-RETURN                                       
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-ERROR                                        
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A200-PROCESS-INTER-APPL-COMM                         
                   MOVE PV-HOLD-MESSAGE                                         
                                       TO DP020-MSG-MESSAGE                     
                   MOVE PV-HOLD-SEVERITY-IND                                    
                                       TO DP020-MSG-SEVERITY-IND                
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN DP020-NEXT-ACT-READ-MAP                                     
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM A470-SET-COMMAND-ATTRIBUTES                          
                   PERFORM A100-PROCESS-MAP                                     
                                                                                
               WHEN OTHER                                                       
                   SET  DP013-OTHER-ABEND                                       
                                       TO TRUE                                  
                   MOVE 'A050-CONTROL-PROCESSING       '                        
                                       TO DP013-PARAGRAPH                       
                   MOVE INVALID-NEXT-APPL-MSG                                   
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE DP020-NEXT-APPL-ACTIVITY                                
                                       TO DP013-MESSAGE-TEXT (2)                
                   PERFORM DP013-0000-PROCESS-ABEND                             
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A100-PROCESS-MAP                                               *        
      * (Z019-03 PROCESS-MAP)                                          *        
      ******************************************************************        
                                                                                
       A100-PROCESS-MAP.                                                        
                                                                                
           IF  EIBAID  =  DFHPA2                                                
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
               MOVE DP016-PA2            TO DP020-SRC-AID                       
               SET PS-SEND-DATA-ONLY     TO TRUE                                
               MOVE -1                   TO ANAVKEYL                            
               PERFORM DP009-0000-LOAD-FK-LINES                                 
               PERFORM DP018-0000-GET-CURR-TIME                                 
               PERFORM DP008-0000-WRITE-PANEL                                   
           ELSE                                                                 
               IF  EIBAID  =  DFHCLEAR                                          
                   MOVE DP016-CLEAR      TO DP020-SRC-AID                       
                   MOVE DP015-PRO-NOR-OFF                                       
                                         TO AMNEMA                              
                   MOVE DP015-BLUE       TO AMNEMC                              
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                         TO TRUE                                
                   MOVE -1               TO ANAVKEYL                            
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
               ELSE                                                             
                   PERFORM DP007-0000-GET-PANEL                                 
                   PERFORM A650-MOVE-SCREEN-TO-COMM                             
                   IF  PS-NEW-MNEMONIC-ENTERED-NO                               
                       PERFORM A200-PROCESS-INTER-APPL-COMM                     
                       PERFORM A300-CHECK-LOCAL-PF-KEYS                         
                       IF  PS-LOCAL-FUNCTION-NO                                 
                           SET DP019-DONT-SAVE-TSQB                             
                                         TO  TRUE                               
                           PERFORM DP015-1000-START-NAVIGATOR                   
                       ELSE                                                     
                           IF  ((DP020-SRC-AID  =  DP016-ENTER)    AND          
                                (DP020-NAV-MNEMONIC  NOT = SPACES  AND          
                                                           LOW-VALUES))         
                               SET DP019-SAVE-TSQB                              
                                         TO  TRUE                               
                               PERFORM DP015-1000-START-NAVIGATOR               
                           ELSE                                                 
                               MOVE -1   TO ANAVKEYL                            
                               PERFORM A350-LOCAL-PF-KEY-CONTROL                
                           END-IF                                               
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A200-PROCESS-INTER-APPL-COMM                                   *        
      * (Z019-05 PROCESS INTER-APPL COMMAREA)                          *        
      * THE PURPOSE OF THIS MODULE IS TO MANIPULATE THE INTER-APPL-    *        
      * COMMAREA UNDER THE FOUR POSSIBLE CYCLES OF THIS TRANSACTION:   *        
      *                                                                *        
      *        1. DP020-NEXT-ACT-INITIAL                               *        
      *        2. DP020-NEXT-ACT-RETURN                                *        
      *        3. DP020-NEXT-ACT-READ-MAP                              *        
      *        4. DP020-NEXT-ACT-ERROR-RETURN                          *        
      *                                                                *        
      * THREE DATA AREAS ARE NEEDED TO CORRECTLY USE THE INTER-APPL-   *        
      * COMMAREA:                                                      *        
      *                                                                *        
      *        1. THE INTER-APPL-COMMAREA PORTION OF THE STANDARD-COMM-*        
      *           AREA                                                 *        
      *        2. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO STORE THE CONTENTS OF THE INTER-APPL-COMMAREA SENT*        
      *           TO THIS PROGRAM. (NOTE: ONLY SAVE THOSE FIELDS       *        
      *           RELAVENT TO THIS PROGRAM).                           *        
      *        3. AN AREA OF THE APPLICATION COMMAREA THAT WILL BE USED*        
      *           TO HOLD THE DATA RELAVENT TO THE CURRENT OBJECT BEING*        
      *           PROCESSED BY THIS PROGRAM.                           *        
      ******************************************************************        
                                                                                
       A200-PROCESS-INTER-APPL-COMM.                                            
                                                                                
           EVALUATE TRUE                                                        
                                                                                
           WHEN DP020-NEXT-ACT-INITIAL                                          
                MOVE DP020-INTER-APPL-DATA-AREA                                 
                     TO  DP019-SAVE-INTER-APPL-COMMAREA                         
                                                                                
           WHEN DP020-NEXT-ACT-RETURN                                           
                IF  DP019-SAVE-TSQB                                             
                    MOVE DP019-SAVE-INTER-APPL-COMMAREA                         
                                     TO  DP020-INTER-APPL-DATA-AREA             
                END-IF                                                          
                IF  DP055-UPDATED                                               
                    IF  DP019-USE-TSQB                                          
                        MOVE DP019-SAVE-TSQB-RECORD                             
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
                        SET DP002-UPD (DP019-MN-SUB)                            
                                     TO  TRUE                                   
                        MOVE DP002-ARCH-MAINT-TSQB-REC                          
                                     TO  DP019-SAVE-TSQB-RECORD                 
                    END-IF                                                      
                END-IF                                                          
                                                                                
           WHEN DP020-NEXT-ACT-READ-MAP                                         
                IF  DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                         
                    PERFORM A800-SETUP-INTER-APPL                               
                ELSE                                                            
                    IF  DP020-FK-RETURN (DP020-SRC-AID)                         
                        IF  DP019-USE-TSQB                                      
                            PERFORM B300-REWRITE-TSQB                           
                        END-IF                                                  
                        PERFORM A850-RESET-INITIAL-INTER-APPL                   
                    END-IF                                                      
                END-IF                                                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * INITIALIZE ITEM AND SUB FIELDS, GET FIRST MNEMONIC FOR         *        
      * PROCESSING.                                                    *        
      ******************************************************************        
                                                                                
       A240-GET-FIRST-MNEMONIC.                                                 
                                                                                
           IF  DP020-INT-APPL-FORMAT-IND EQUAL '1'                              
               SET DP019-NO-TSQB     TO  TRUE                                   
               MOVE DP055-MNEMONIC   TO  DP019-SCREEN-MNEMONIC                  
           ELSE                                                                 
               IF  DP020-INT-APPL-FORMAT-IND EQUAL '2'                          
                   SET DP019-USE-TSQB                                           
                                     TO  TRUE                                   
                   MOVE 1            TO  DP019-MN-ITEM                          
                                         DP019-MN-SUB                           
                   PERFORM B100-READ-LIST-QUEUE                                 
                   MOVE DP002-MNEMONIC (1)                                      
                                     TO  DP019-SCREEN-MNEMONIC                  
                   SET DP002-INQ (1) TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP019-SAVE-TSQB-RECORD                 
               ELSE                                                             
                   SET DP019-NO-TSQB TO  TRUE                                   
                   MOVE DP020-NAV-KEY-DATA                                      
                                     TO  DP019-SCREEN-MNEMONIC                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A250-SEND-INITIAL-SCREEN                                       *        
      * (Z019-06 SEND INITIAL SCREEN)                                  *        
      * RETRIEVE THE DATA FROM THE DATABASE, FORMAT DATA ON SCREEN, AND*        
      * DISPLAY THE SCREEN TO THE USER.                                *        
      ******************************************************************        
                                                                                
       A250-SEND-INITIAL-SCREEN.                                                
                                                                                
           PERFORM A500-RETRIEVE-DATA-FOR-SCREEN.                               
                                                                                
           IF  NOT DP020-NEXT-ACT-APPL-ERROR                                    
               PERFORM A550-DISPLAY-PANEL                                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A300-CHECK-LOCAL-PF-KEYS                                       *        
      * (Z019-09 CHECK FOR LOCAL PF KEYS)                              *        
      *                                                                *        
      * LOCAL FUNCTION KEYS:                                           *        
      *      PF5   (DP020-SRC-AID = 05)  - REFRESH SCREEN              *        
      *      PF15  (DP020-SRC-AID = 14)  - UPDATE TMNEMONIC TABLE      *        
      *      PA2   (DP020-SRC-AID = 26)  - TOGGLE THE FUNCTION KEY     *        
      *                                    LINES ON SCREEN             *        
      *      CLEAR (DP020-SRC-AID = 27)  - REFRESH SCREEN              *        
      *      ENTER (DP020-SRC-AID = 28)  - EDIT SCREEN                 *        
      ******************************************************************        
                                                                                
       A300-CHECK-LOCAL-PF-KEYS.                                                
                                                                                
           SET PS-LOCAL-FUNCTION-NO    TO TRUE.                                 
                                                                                
           IF (DP020-FK-LOCAL-KEY (DP020-SRC-AID)      OR                       
               DP020-FK-CONFIRM (DP020-SRC-AID)        OR                       
               DP020-FK-REFRESH (DP020-SRC-AID)        OR                       
               DP020-FK-PREV-PAGE (DP020-SRC-AID)      OR                       
               DP020-FK-NEXT-PAGE (DP020-SRC-AID)      OR                       
               DP020-FK-FIRST-PAGE (DP020-SRC-AID)     OR                       
               DP020-FK-NEXT-SELECTION (DP020-SRC-AID) OR                       
               DP020-FK-DESELECT (DP020-SRC-AID))                               
               SET PS-LOCAL-FUNCTION-YES                                        
                                       TO TRUE                                  
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A350-LOCAL-PF-KEY-CONTROL                                      *        
      * (Z019-10 LOCAL PF KEY CONTROL)                                 *        
      ******************************************************************        
                                                                                
       A350-LOCAL-PF-KEY-CONTROL.                                               
                                                                                
           EVALUATE DP020-SRC-AID                                               
                                                                                
               WHEN  DP016-PF5                                                  
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   SET DP019-DIDNT-ISSUE-MESSAGE                                
                                       TO TRUE                                  
                   PERFORM A250-SEND-INITIAL-SCREEN                             
                                                                                
               WHEN  DP016-PF20                                                 
                   IF  DP019-USE-TSQB                                           
                       SET PS-SEND-MAP-N-DATA-FRSET                             
                                       TO TRUE                                  
                       PERFORM B200-GET-NEXT-MNEMONIC                           
                       IF (DP020-MSG-NUMBER NUMERIC                             
                       AND DP020-MSG-NUMBER = PC-TSYMSG-00070)                  
                           SET PS-SEND-DATA-ONLY-FRSET TO TRUE                  
                           PERFORM A550-DISPLAY-PANEL                           
                       ELSE                                                     
                           SET DP019-DIDNT-ISSUE-MESSAGE                        
                                       TO TRUE                                  
                           PERFORM A250-SEND-INITIAL-SCREEN                     
                       END-IF                                                   
                   ELSE                                                         
                       MOVE PC-TSYMSG-00070                                     
                                     TO  DP020-MSG-NUMBER                       
                       SET DP020-MSG-FATAL                                      
                                     TO  TRUE                                   
                       PERFORM A550-DISPLAY-PANEL                               
                   END-IF                                                       
                                                                                
               WHEN  DP016-PF15                                                 
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                       TO TRUE                                  
                   PERFORM A450-EDIT-SCREEN                                     
                   IF  PS-EDIT-ERROR-NO                                         
                       PERFORM A400-PROCESS-UPDATE                              
                   END-IF                                                       
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN  DP016-PA2                                                  
                   MOVE -1             TO ANAVKEYL                              
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   PERFORM DP005-0000-FORMAT-MSG-LINES                          
                   PERFORM DP009-0000-LOAD-FK-LINES                             
                   PERFORM DP018-0000-GET-CURR-TIME                             
                   PERFORM DP008-0000-WRITE-PANEL                               
                                                                                
               WHEN  DP016-CLEAR                                                
                   SET PS-SEND-DATA-ONLY-FRSET                                  
                                       TO TRUE                                  
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO AMNEMA                                
                   MOVE DP015-BLUE     TO AMNEMC                                
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
               WHEN OTHER                                                       
                   PERFORM A450-EDIT-SCREEN                                     
                   PERFORM A550-DISPLAY-PANEL                                   
                                                                                
           END-EVALUATE.                                                        
       EJECT                                                                    
      ******************************************************************        
      * A400-PROCESS-UPDATE                                            *        
      * (Z019-12 PROCESS UPDATE TO DATABASE)                           *        
      * ALL DB2 DATABASE MAINTENANCE USES 'TIMESTAMP' VALIDATION SYNTAX*        
      * TO ENSURE THAT THE SET OF DATA BEING UPDATED IS IN THE SAME    *        
      * CONDITION AT THE TIME OF UPDATE THAT IT WAS IN WHEN THE SET OF *        
      * DATA WAS RETRIEVED FROM THE DATABASE FOR PROCESSING. THIS IS A *        
      * RECOGNITION OF THE POTENTIAL FOR CONCURRENT UPDATING ON ALL    *        
      * RELATIONS WITHIN THE DB2 ENVIRONMENT.                          *        
      ******************************************************************        
                                                                                
       A400-PROCESS-UPDATE.                                                     
                                                                                
           MOVE DP019-SCREEN-MNEMONIC  TO TMNEMONIC-MNEMONIC-CODE.              
           MOVE DP019-SCREEN-TRAN-ID   TO TMNEMONIC-TRANS-ID-CODE.              
           MOVE DP019-SCREEN-AVAILABIL-IND                                      
                                       TO TMNEMONIC-AVAIL-INDCT.                
           IF  DP019-SCREEN-COMMAREA-FORMAT EQUAL 'N'                           
               MOVE DP019-SCREEN-COMMAREA-FORMAT                                
                                       TO TMNEMONIC-CMAREA-FRMT-CODE            
           ELSE                                                                 
               MOVE 'K'                TO TMNEMONIC-CMAREA-FRMT-CODE            
           END-IF.                                                              
           MOVE DP019-SCREEN-SAVE-COMM-IND                                      
                                       TO TMNEMONIC-SAVE-CMAREA-INDCT.          
           MOVE DP019-SCREEN-SAVE-COMM-LENGTH                                   
                                       TO PV-COMM-LENGTH-ALPHA                  
           MOVE DP019-SCREEN-GLOBAL-ACCESS                                      
                                       TO TMNEMONIC-GLBL-ACCS-IND.              
           MOVE DP019-SCREEN-PROGRAM   TO TMNEMONIC-PGM-NAME.                   
           MOVE DP019-SCREEN-TYPE-CODE TO TMNEMONIC-SCRN-TYP-CDE.               
      *    MOVE DP019-SCREEN-UPD-VSAM-IND                                       
      *                                TO TMNEMONIC-UPD-VSAM-IND.               
           MOVE PC-Y                   TO TMNEMONIC-UPD-VSAM-IND.               
           MOVE DP019-SCREEN-DB2-PKG-CDE                                        
                                       TO TMNEMONIC-DB2-PKG-CDE.                
           MOVE DP019-SCREEN-CMNT-TXT  TO TMNEMONIC-CMNT-TXT.                   
           MOVE DP019-ACCURATE-LOCATION-REQ                                     
                                       TO TMNEMONIC-ACRT-LOC-REQR-IND.          
           MOVE PV-COMM-LENGTH-NUMERIC TO TMNEMONIC-CMAREA-LNGTH-MSRMT.         
           MOVE DP019-SCREEN-MNEMONIC-TITLE                                     
                                       TO TMNEMONIC-PANEL-TITLE-NAME.           
           MOVE DP019-TIMESTAMP        TO TMNEMONIC-LAST-UPDATE-TMESTP.         
                                                                                
           PERFORM X910-UPDATE-TMNEMONIC-ROW.                                   
                                                                                
           IF  SQL-ROW-NOT-FOUND                                                
               MOVE DP019-SCREEN-MNEMONIC                                       
                                       TO TMNEMONIC-MNEMONIC-CODE               
               PERFORM X900-READ-TMNEMONIC-ROW                                  
               IF  SQL-ROW-FOUND                                                
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO AMNEMA                                
                   MOVE DP015-BLUE     TO AMNEMC                                
      *                                                                         
                   MOVE TMNEMONIC-UPD-VSAM-IND                                  
                                       TO DP019-SCREEN-UPD-VSAM-IND             
                                          AUVINDO                               
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO AUVINDA                               
                   MOVE DP015-BLUE     TO AUVINDC                               
      *                                                                         
                   MOVE TMNEMONIC-DB2-PKG-CDE                                   
                                       TO DP019-SCREEN-DB2-PKG-CDE              
                                          APKGO                                 
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO APKGA                                 
                   MOVE DP015-UNDERLINE                                         
                                       TO APKGH                                 
                   MOVE DP015-GREEN    TO APKGC                                 
      *                                                                         
                   MOVE TMNEMONIC-CMNT-TXT                                      
                                       TO DP019-SCREEN-CMNT-TXT                 
                                          ACMNTO                                
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ACMNTA                                
                   MOVE DP015-UNDERLINE                                         
                                       TO ACMNTH                                
                   MOVE DP015-GREEN    TO ACMNTC                                
      *                                                                         
                   MOVE TMNEMONIC-ACRT-LOC-REQR-IND                             
                                       TO DP019-ACCURATE-LOCATION-REQ           
                                          ALOCO                                 
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ALOCA                                 
                   MOVE DP015-UNDERLINE                                         
                                       TO ALOCH                                 
                   MOVE DP015-GREEN    TO ALOCC                                 
      *                                                                         
                   MOVE TMNEMONIC-TRANS-ID-CODE                                 
                                       TO DP019-SCREEN-TRAN-ID                  
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ATRANIDA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ATRANIDH                              
                   MOVE DP015-GREEN    TO ATRANIDC                              
      *                                                                         
                   MOVE TMNEMONIC-AVAIL-INDCT                                   
                                       TO DP019-SCREEN-AVAILABIL-IND            
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO AAVAILA                               
                   MOVE DP015-UNDERLINE                                         
                                       TO AAVAILH                               
                   MOVE DP015-GREEN    TO AAVAILC                               
      *                                                                         
                   IF  TMNEMONIC-CMAREA-FRMT-CODE EQUAL 'N'                     
                       MOVE TMNEMONIC-CMAREA-FRMT-CODE                          
                                       TO DP019-SCREEN-COMMAREA-FORMAT          
                   ELSE                                                         
                       MOVE 'Y'        TO DP019-SCREEN-COMMAREA-FORMAT          
                   END-IF                                                       
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO AFORMATA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO AFORMATH                              
                   MOVE DP015-GREEN    TO AFORMATC                              
                   MOVE TMNEMONIC-SAVE-CMAREA-INDCT                             
                                       TO DP019-SCREEN-SAVE-COMM-IND            
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ASAVCOMA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ASAVCOMH                              
                   MOVE DP015-GREEN    TO ASAVCOMC                              
                   MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                            
                                       TO PV-COMM-LENGTH-NUMERIC                
                   MOVE PV-COMM-LENGTH-ALPHA                                    
                                       TO DP019-SCREEN-SAVE-COMM-LENGTH         
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ACOMLENA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ACOMLENH                              
                   MOVE DP015-GREEN    TO ACOMLENC                              
                   MOVE TMNEMONIC-PANEL-TITLE-NAME                              
                                       TO DP019-SCREEN-MNEMONIC-TITLE           
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ATRNTLEA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ATRNTLEH                              
                   MOVE DP015-GREEN    TO ATRNTLEC                              
                   MOVE TMNEMONIC-LAST-UPDATE-TMESTP                            
                                       TO DP019-TIMESTAMP                       
                   MOVE 5              TO DP019-SELECT-MAINT-RESULT             
                   MOVE TIMESTAMP-ERROR-MSG                                     
                                       TO DP020-MSG-MESSAGE                     
                   SET DP020-MSG-FATAL TO TRUE                                  
               ELSE                                                             
                   MOVE DP015-PRO-BRT-OFF                                       
                                       TO AMNEMA                                
                   MOVE DP015-RED      TO AMNEMC                                
                   MOVE DP015-REVERSE  TO AMNEMH                                
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO ATRANIDA                              
                   MOVE DP015-BLUE     TO ATRANIDC                              
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO AFORMATA                              
                   MOVE DP015-BLUE     TO AFORMATC                              
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO ASAVCOMA                              
                   MOVE DP015-BLUE     TO ASAVCOMC                              
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO AAVAILA                               
                   MOVE DP015-BLUE     TO AAVAILC                               
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO ACOMLENA                              
                   MOVE DP015-BLUE     TO ACOMLENC                              
                   MOVE DP015-PRO-NOR-OFF                                       
                                       TO ATRNTLEA                              
                   MOVE DP015-BLUE     TO ATRNTLEC                              
                   MOVE 5              TO DP019-SELECT-MAINT-RESULT             
                   MOVE PC-TSYMSG-00148                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-FATAL TO TRUE                                  
               END-IF                                                           
           ELSE                                                                 
               MOVE PC-Y               TO  DP019-SCREEN-UPD-VSAM-IND            
                                           AUVINDO                              
               MOVE PC-TSYMSG-00059    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-INFORMATIONAL                                      
                                       TO TRUE                                  
               IF  DP019-USE-TSQB                                               
                   MOVE DP019-SAVE-TSQB-RECORD                                  
                                       TO DP002-ARCH-MAINT-TSQB-REC             
                   SET DP002-UPD (DP019-MN-SUB)                                 
                                       TO TRUE                                  
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                       TO DP019-SAVE-TSQB-RECORD                
               ELSE                                                             
                   SET DP055-UPDATED   TO  TRUE                                 
                   MOVE DP020-INTER-APPL-DATA-AREA                              
                                    TO  DP019-SAVE-INTER-APPL-COMMAREA          
               END-IF                                                           
                                                                                
               MOVE 2                  TO DP019-SELECT-MAINT-RESULT             
               PERFORM X905-READ-TMNEMONIC-ROW                                  
               IF  SQL-ROW-FOUND                                                
                   MOVE TMNEMONIC-LAST-UPDATE-TMESTP                            
                                       TO DP019-TIMESTAMP                       
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      *                                                                *        
      * A450-EDIT-SCREEN                                               *        
      * (Z019-11 EDIT SCREEN)                                          *        
      * EDITS WILL OCCUR FROM THE BOTTOM OF THE SCREEN TO THE TOP      *        
      *                                                                *        
      * NOTE THAT THE DB2 TABLE WILL BE UPDATED IF THERE ARE NO EDIT   *        
      * ERRORS (AS INDICATED BY VALUE OF PS-EDIT-ERROR-NO).            *        
      * THEREFORE AN UPDATE OCCURS REGARDLESS OF WHETHER ANY FIELDS ARE*        
      * CHANGED (TEST FOR FIELD CHANGES IN A650-MOVE-SCREEN-TO-COMM).  *        
      ******************************************************************        
                                                                                
       A450-EDIT-SCREEN.                                                        
                                                                                
           SET PS-EDIT-ERROR-NO        TO TRUE.                                 
                                                                                
      * VALIDATE COMMENT-TEXT FIELD.                                            
      * THIS FIELD IS FREE-FORMAT AND NO ENTRY IS REQUIRED.                     
      * THEREFORE NO EDITS ARE PERFORMED.                                       
      * HOWEVER, IF THE FIELD ENTRY HAS CHANGED (SEE PARAGRAPH                  
      * A650-MOVE-SCREEN-TO-COMM), THE DB2 TABLE WILL BE UPDATED.               
                                                                                
           MOVE DP015-UNP-ALP-NOR-OFF  TO ACMNTA.                               
           MOVE DP015-UNDERLINE        TO ACMNTH.                               
           MOVE DP015-GREEN            TO ACMNTC.                               
                                                                                
                                                                                
      * VALIDATE ACCURATE LOCATION REQUIRED INDICATOR                           
                                                                                
           IF  DP019-ACCURATE-LOCATION-REQ = ('Y'  OR  'N')                     
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ALOCA                                 
               MOVE DP015-UNDERLINE    TO ALOCH                                 
               MOVE DP015-GREEN        TO ALOCC                                 
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ALOCL                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ALOCA                                 
               MOVE DP015-RED          TO ALOCC                                 
               MOVE DP015-REVERSE      TO ALOCH                                 
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN DB2 PACKAGE CODE,                                       
      * FROM THE APPLICATION SPECIFIC COMMAREA.                                 
      * FIELD IS REQUIRED AND THE VALID VALUES ARE 'P' AND 'A'.                 
                                                                                
           IF  DP019-SCREEN-DB2-PKG-CDE = ('P'  OR  'A')                        
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-UNDERLINE    TO APKGH                                 
               MOVE DP015-GREEN        TO APKGC                                 
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO APKGL                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-RED          TO APKGC                                 
               MOVE DP015-REVERSE      TO APKGH                                 
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN SAVE GLOBAL ACCESS INDICATOR                            
      * FROM THE APPLICATION SPECIFIC COMMAREA                                  
                                                                                
           IF  DP019-SCREEN-TYPE-CODE = ('M'  OR  'L'  OR  'D')                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASCRTYPA                              
               MOVE DP015-UNDERLINE    TO ASCRTYPH                              
               MOVE DP015-GREEN        TO ASCRTYPC                              
                                                                                
               IF (DP019-SCREEN-TYPE-CODE = 'M'                                 
               AND DP019-SCREEN-TRAN-ID NOT EQUAL PC-MENU-TRAN)                 
               OR (DP019-SCREEN-TYPE-CODE NOT = 'M'                             
               AND DP019-SCREEN-TRAN-ID  EQUAL PC-MENU-TRAN)                    
                   SET PS-EDIT-ERROR-YES                                        
                                       TO  TRUE                                 
                   MOVE PC-TSYMSG-00255                                         
                                       TO  DP020-MSG-NUMBER                     
                   SET DP020-MSG-FATAL TO  TRUE                                 
                   MOVE -1             TO  ASCRTYPL                             
                   MOVE DP015-RED      TO  ASCRTYPC                             
                   MOVE DP015-REVERSE  TO  ASCRTYPH                             
               ELSE                                                             
                   IF  DP019-SCREEN-TYPE-CODE = 'M'                             
                       MOVE PC-Y       TO  DP019-SCREEN-GLOBAL-ACCESS           
                                           DP019-SCREEN-SAVE-COMM-IND           
                                           DP019-SCREEN-COMMAREA-FORMAT         
                       MOVE PC-MENU-COMMAREA-LEN                                
                                       TO  DP019-SCREEN-SAVE-COMM-LENGTH        
                                           ACOMLENO                             
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ASCRTYPL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASCRTYPA                              
               MOVE DP015-RED          TO ASCRTYPC                              
               MOVE DP015-REVERSE      TO ASCRTYPH                              
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN SAVE GLOBAL ACCESS INDICATOR                            
      * FROM THE APPLICATION SPECIFIC COMMAREA                                  
                                                                                
           IF  DP019-SCREEN-GLOBAL-ACCESS = ('Y'  OR  'N')                      
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AGLBACCA                              
               MOVE DP015-UNDERLINE    TO AGLBACCH                              
               MOVE DP015-GREEN        TO AGLBACCC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO  DP020-MSG-NUMBER                     
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AGLBACCL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AGLBACCA                              
               MOVE DP015-RED          TO AGLBACCC                              
               MOVE DP015-REVERSE      TO AGLBACCH                              
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN COMMAREA LENGTH            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           EXEC CICS                                                            
               BIF                                                              
                 DEEDIT FIELD (DP019-SCREEN-SAVE-COMM-LENGTH)                   
                       LENGTH (4)                                               
           END-EXEC.                                                            
                                                                                
           IF  DP019-SCREEN-SAVE-COMM-LENGTH  NUMERIC                           
             IF  DP019-SCREEN-SAVE-COMM-LENGTH  >  PC-MAX-COMM-LENGTH           
                 SET PS-EDIT-ERROR-YES TO TRUE                                  
                 MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                      
                 SET DP020-MSG-FATAL   TO TRUE                                  
                 MOVE -1               TO ACOMLENL                              
                 MOVE DP015-UNP-ALP-NOR-OFF                                     
                                       TO ACOMLENA                              
                 MOVE DP015-RED        TO ACOMLENC                              
                 MOVE DP015-REVERSE    TO ACOMLENH                              
             ELSE                                                               
                 MOVE DP015-UNP-ALP-NOR-OFF                                     
                                       TO ACOMLENA                              
                 MOVE DP015-UNDERLINE  TO ACOMLENH                              
                 MOVE DP015-GREEN      TO ACOMLENC                              
             END-IF                                                             
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00008    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ACOMLENL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ACOMLENA                              
               MOVE DP015-RED          TO ACOMLENC                              
               MOVE DP015-REVERSE      TO ACOMLENH                              
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN SAVE COMMAREA INDICATOR    *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF  DP019-SCREEN-SAVE-COMM-IND  =  ('Y'  OR  'N')                    
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASAVCOMA                              
               MOVE DP015-UNDERLINE    TO ASAVCOMH                              
               MOVE DP015-GREEN        TO ASAVCOMC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ASAVCOML                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASAVCOMA                              
               MOVE DP015-RED          TO ASAVCOMC                              
               MOVE DP015-REVERSE      TO ASAVCOMH                              
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN COMMAREA FORMAT            *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF  DP019-SCREEN-COMMAREA-FORMAT  =  ('Y'  OR  'N')                  
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AFORMATA                              
               MOVE DP015-UNDERLINE    TO AFORMATH                              
               MOVE DP015-GREEN        TO AFORMATC                              
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AFORMATL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AFORMATA                              
               MOVE DP015-RED          TO AFORMATC                              
               MOVE DP015-REVERSE      TO AFORMATH                              
           END-IF.                                                              
                                                                                
           IF (DP019-SCREEN-COMMAREA-FORMAT  =  'Y'                             
               AND                                                              
               DP019-SCREEN-SAVE-COMM-LENGTH  <  PC-MIN-COMM-LENGTH)            
                 SET PS-EDIT-ERROR-YES TO TRUE                                  
                 MOVE PC-TSYMSG-00005  TO DP020-MSG-NUMBER                      
                 SET DP020-MSG-FATAL   TO TRUE                                  
                 MOVE -1               TO ACOMLENL                              
                 MOVE DP015-UNP-ALP-NOR-OFF                                     
                                       TO ACOMLENA                              
                 MOVE DP015-RED        TO ACOMLENC                              
                 MOVE DP015-REVERSE    TO ACOMLENH                              
             END-IF.                                                            
                                                                                
      * VALIDATE SCREEN AVAILABILITY INDICATOR     *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF  DP019-SCREEN-AVAILABIL-IND  =  ('Y'  OR  'N')                    
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AAVAILA                               
               MOVE DP015-UNDERLINE    TO AAVAILH                               
               MOVE DP015-GREEN        TO AAVAILC                               
           ELSE                                                                 
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AAVAILL                               
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AAVAILA                               
               MOVE DP015-RED          TO AAVAILC                               
               MOVE DP015-REVERSE      TO AAVAILH                               
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN MNEMONIC TITLE             *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF (DP019-SCREEN-MNEMONIC-TITLE  =  SPACES    OR                     
                                               LOW-VALUES)                      
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ATRNTLEL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRNTLEA                              
               MOVE DP015-RED          TO ATRNTLEC                              
               MOVE DP015-REVERSE      TO ATRNTLEH                              
           ELSE                                                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRNTLEA                              
               MOVE DP015-UNDERLINE    TO ATRNTLEH                              
               MOVE DP015-GREEN        TO ATRNTLEC                              
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN TRAN-ID                    *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF (DP019-SCREEN-TRAN-ID  =  SPACES    OR                            
                                        LOW-VALUES)                             
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO ATRANIDL                              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRANIDA                              
               MOVE DP015-RED          TO ATRANIDC                              
               MOVE DP015-REVERSE      TO ATRANIDH                              
           ELSE                                                                 
                                                                                
      *    MAKE SURE THAT THE TRANSACTION ENTERED ON THE     *                  
      *    SCREEN IS NOT THE NAVIGATOR TRANSACTION. ANY      *                  
      *    ATTEMPT TO NAVIGATE TO THE NAVIGATOR THRU THE     *                  
      *    NAVIGATOR WILL CAUSE THE ARCHITECURE TO LOOP      *                  
                                                                                
               IF ( DP019-SCREEN-TRAN-ID  =  PC-NAVIGATOR-TRAN-ID OR            
                                             PC-MENU-TRAN-ID )                  
                                                                                
                   SET PS-EDIT-ERROR-YES   TO TRUE                              
                   MOVE PC-TSYMSG-00257                                         
                                       TO DP020-MSG-NUMBER                      
                   SET DP020-MSG-FATAL TO TRUE                                  
                   MOVE -1             TO ATRANIDL                              
                   MOVE DP015-UNP-ALP-BRT-OFF                                   
                                       TO ATRANIDA                              
                   MOVE DP015-RED      TO ATRANIDC                              
                   MOVE DP015-REVERSE  TO ATRANIDH                              
               ELSE                                                             
                                                                                
      * CICS INQUIRY TO MAKE SURE TRAN-ID IS DEFINED TO CICS *                  
      * RETURN CODE:                                         *                  
      *            ZERO -  TRANSACTION FOUND                 *                  
      *             23  -  TRANSACTION ENABLED               *                  
      *             24  -  TRANSACTION DISABLED              *                  
      *             25  -  TRANSACTION DISABLING             *                  
                                                                                
               EXEC CICS                                                        
                   INQUIRE                                                      
                       TRANSACTION (DP019-SCREEN-TRAN-ID)                       
                           RESP    (PV-CICS-RESPONSE)                           
                       PROGRAM     (PV-PROGRAM)                                 
               END-EXEC                                                         
               IF  PV-CICS-RESPONSE  =  ZEROS                     OR            
                                        PC-TRANSACTION-ENABLED    OR            
                                        PC-TRANSACTION-DISABLED   OR            
                                        PC-TRANSACTION-DISABLING                
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ATRANIDA                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ATRANIDH                              
                   MOVE DP015-GREEN    TO ATRANIDC                              
                   IF  PV-PROGRAM EQUAL PC-MRO-PGM                              
                       SET PS-CHECK-PROGRAM TO TRUE                             
                       IF  DP019-ISSUED-MESSAGE                                 
                           CONTINUE                                             
                       ELSE                                                     
                           MOVE PC-TSYMSG-00258                                 
                                       TO  DP020-MSG-NUMBER                     
                           SET DP020-MSG-INFORMATIONAL                          
                                       TO  TRUE                                 
                           SET DP019-ISSUED-MESSAGE                             
                                       TO  TRUE                                 
                       END-IF                                                   
                   ELSE                                                         
                       MOVE PV-PROGRAM TO DP019-SCREEN-PROGRAM                  
                                          APROGO                                
                       SET PS-DONT-CHECK-PROGRAM                                
                                       TO  TRUE                                 
                   END-IF                                                       
               ELSE                                                             
                   IF  DP010-ERR-00256-OVERRIDE-Y                               
      *                ******************************************               
      *                * THIS MESSAGE OCCURRED ON THE LAST PASS;                
      *                * ON THIS PASS, ALLOW "ADD" TO OCCUR.                    
      *                *******************************************              
                       SET DP010-ERR-00256-OVERRIDE-N                           
                                         TO TRUE                                
                       SET PS-EDIT-ERROR-NO                                     
                                         TO TRUE                                
                   ELSE                                                         
                       SET PS-EDIT-ERROR-YES                                    
                                         TO TRUE                                
                       MOVE PC-TSYMSG-00256                                     
                                         TO DP020-MSG-NUMBER                    
      *                SET DP020-MSG-FATAL                                      
                       SET DP020-MSG-INFORMATIONAL                              
                                         TO TRUE                                
                       SET DP010-ERR-00256-OVERRIDE-Y                           
                                         TO TRUE                                
                       MOVE -1           TO ATRANIDL                            
                       MOVE DP015-UNP-ALP-NOR-OFF                               
                                         TO ATRANIDA                            
                       MOVE DP015-RED                                           
                                         TO ATRANIDC                            
                       MOVE DP015-REVERSE                                       
                                         TO ATRANIDH                            
                       SET PS-DONT-CHECK-PROGRAM                                
                                         TO TRUE                                
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN PROGRAM NAME               *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF  PS-CHECK-PROGRAM                                                 
               MOVE DP015-UNP-ALP-NOR-OFF  TO APROGA                            
               MOVE DP015-UNDERLINE        TO APROGH                            
               MOVE DP015-GREEN            TO APROGC                            
               MOVE -1                     TO APROGL                            
                                                                                
               IF  DP019-SCREEN-PROGRAM NOT GREATER THAN SPACES                 
                   MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                  
                   SET DP020-MSG-FATAL     TO TRUE                              
                   MOVE -1                 TO APROGL                            
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO APROGA                                
                   MOVE DP015-RED          TO APROGC                            
                   MOVE DP015-REVERSE      TO APROGH                            
               END-IF                                                           
           ELSE                                                                 
               MOVE DP015-ASK-NOR-OFF  TO APROGA                                
               MOVE DP015-HL-OFF       TO APROGH                                
               MOVE DP015-BLUE         TO APROGC                                
           END-IF.                                                              
                                                                                
      * VALIDATE SCREEN MNEMONIC                   *                            
      * FROM THE APPLICATION SPECIFIC COMMAREA     *                            
                                                                                
           IF (DP019-SCREEN-MNEMONIC  =  SPACES    OR                           
                                         LOW-VALUES)                            
               SET PS-EDIT-ERROR-YES   TO TRUE                                  
               MOVE PC-TSYMSG-00005    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
               MOVE -1                 TO AMNEML                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AMNEMA                                
               MOVE DP015-RED          TO AMNEMC                                
               MOVE DP015-REVERSE      TO AMNEMH                                
               MOVE DP015-PRO-NOR-OFF                                           
                                       TO ATRANIDA                              
               MOVE DP015-BLUE         TO ATRANIDC                              
               MOVE DP015-PRO-NOR-OFF  TO ATRNTLEA                              
               MOVE DP015-BLUE         TO ATRNTLEC                              
               MOVE DP015-PRO-NOR-OFF  TO AFORMATA                              
               MOVE DP015-BLUE         TO AFORMATC                              
               MOVE DP015-PRO-NOR-OFF  TO ACOMLENA                              
               MOVE DP015-BLUE         TO ACOMLENC                              
               MOVE DP015-PRO-NOR-OFF  TO ASAVCOMA                              
               MOVE DP015-BLUE         TO ASAVCOMC                              
               MOVE DP015-PRO-NOR-OFF  TO AAVAILA                               
               MOVE DP015-BLUE         TO AAVAILC                               
           ELSE                                                                 
               MOVE DP015-PRO-NOR-OFF  TO AMNEMA                                
               MOVE DP015-BLUE         TO AMNEMC                                
           END-IF.                                                              
                                                                                
           IF  PS-EDIT-ERROR-NO                                                 
               MOVE -1                 TO ATRANIDL                              
           END-IF.                                                              
       EJECT                                                                    
      *****************************************************************         
      *     EVERY TIME IN SET THE COMMAND FIELD TO EITHER RED         *         
      *     OR GREEN. THIS IS BECAUSE IT IS UNEDITTED IN THE          *         
      *     PROGRAM AND IT NEEDS RESETTING.                           *         
      *****************************************************************         
       A470-SET-COMMAND-ATTRIBUTES.                                             
                                                                                
           IF DP020-NEXT-ACT-ERROR                                              
               MOVE DP020-MSG-MESSAGE                                           
                                       TO PV-HOLD-MESSAGE                       
               MOVE DP020-MSG-SEVERITY-IND                                      
                                       TO PV-HOLD-SEVERITY-IND                  
               IF DP020-NAV-MNEMONIC NOT EQUAL TO SPACES                        
                   MOVE DP015-UNP-ALP-BRT-MDT                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-RED                                               
                                       TO ANAVCMDC                              
                   MOVE DP015-REVERSE                                           
                                       TO ANAVCMDH                              
               ELSE                                                             
                   MOVE SPACES         TO ANAVCMDO                              
                                          DP020-NAV-MNEMONIC                    
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           ELSE                                                                 
               IF  (EIBAID  NOT EQUAL DFHPA2    AND                             
                                      DFHCLEAR)                                 
                   MOVE DP015-UNP-ALP-NOR-OFF                                   
                                       TO ANAVCMDA                              
                   MOVE DP015-GREEN                                             
                                       TO ANAVCMDC                              
                   MOVE DP015-UNDERLINE                                         
                                       TO ANAVCMDH                              
               END-IF                                                           
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A500-RETRIEVE-DATA-FOR-SCREEN                                  *        
      * (Z019-07 RETRIEVE DATA FOR SCREEN                              *        
      ******************************************************************        
                                                                                
       A500-RETRIEVE-DATA-FOR-SCREEN.                                           
                                                                                
           MOVE DP019-SCREEN-MNEMONIC  TO TMNEMONIC-MNEMONIC-CODE.              
           PERFORM X900-READ-TMNEMONIC-ROW.                                     
                                                                                
           IF  SQL-ROW-FOUND                                                    
      *                                                                         
               MOVE TMNEMONIC-UPD-VSAM-IND                                      
                                       TO DP019-SCREEN-UPD-VSAM-IND             
                                          AUVINDO                               
               MOVE DP015-PRO-NOR-OFF                                           
                                       TO AUVINDA                               
               MOVE DP015-BLUE         TO AUVINDC                               
      *                                                                         
               MOVE TMNEMONIC-DB2-PKG-CDE                                       
                                       TO DP019-SCREEN-DB2-PKG-CDE              
                                          APKGO                                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-UNDERLINE    TO APKGH                                 
               MOVE DP015-GREEN        TO APKGC                                 
      *                                                                         
               MOVE TMNEMONIC-CMNT-TXT TO DP019-SCREEN-CMNT-TXT                 
                                          ACMNTO                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ACMNTA                                
               MOVE DP015-UNDERLINE    TO ACMNTH                                
               MOVE DP015-GREEN        TO ACMNTC                                
      *                                                                         
               MOVE TMNEMONIC-ACRT-LOC-REQR-IND                                 
                                       TO DP019-ACCURATE-LOCATION-REQ           
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ALOCA                                 
               MOVE DP015-UNDERLINE    TO ALOCH                                 
               MOVE DP015-GREEN        TO ALOCC                                 
                                                                                
               MOVE TMNEMONIC-TRANS-ID-CODE                                     
                                       TO DP019-SCREEN-TRAN-ID                  
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRANIDA                              
               MOVE DP015-UNDERLINE    TO ATRANIDH                              
               MOVE DP015-GREEN        TO ATRANIDC                              
                                                                                
               MOVE TMNEMONIC-GLBL-ACCS-IND                                     
                                       TO DP019-SCREEN-GLOBAL-ACCESS            
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AGLBACCA                              
               MOVE DP015-UNDERLINE    TO AGLBACCH                              
               MOVE DP015-GREEN        TO AGLBACCC                              
                                                                                
               MOVE TMNEMONIC-PGM-NAME                                          
                                       TO DP019-SCREEN-PROGRAM                  
                                                                                
               MOVE TMNEMONIC-SCRN-TYP-CDE                                      
                                       TO DP019-SCREEN-TYPE-CODE                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASCRTYPA                              
               MOVE DP015-UNDERLINE    TO ASCRTYPH                              
               MOVE DP015-GREEN        TO ASCRTYPC                              
                                                                                
               MOVE TMNEMONIC-UPD-VSAM-IND                                      
                                       TO DP019-SCREEN-UPD-VSAM-IND             
               MOVE DP015-PRO-NOR-OFF  TO AUVINDA                               
               MOVE DP015-BLUE         TO AUVINDC                               
                                                                                
               MOVE TMNEMONIC-DB2-PKG-CDE                                       
                                       TO DP019-SCREEN-DB2-PKG-CDE              
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO APKGA                                 
               MOVE DP015-UNDERLINE    TO APKGH                                 
               MOVE DP015-GREEN        TO APKGC                                 
                                                                                
               MOVE TMNEMONIC-CMNT-TXT                                          
                                       TO DP019-SCREEN-CMNT-TXT                 
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ACMNTA                                
               MOVE DP015-UNDERLINE    TO ACMNTH                                
               MOVE DP015-GREEN        TO ACMNTC                                
                                                                                
               MOVE TMNEMONIC-MNEMONIC-CODE                                     
                                       TO DP019-SCREEN-MNEMONIC                 
               MOVE DP015-PRO-NOR-OFF  TO AMNEMA                                
               MOVE DP015-BLUE         TO AMNEMC                                
                                                                                
               MOVE TMNEMONIC-PANEL-TITLE-NAME                                  
                                       TO DP019-SCREEN-MNEMONIC-TITLE           
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ATRNTLEA                              
               MOVE DP015-UNDERLINE    TO ATRNTLEH                              
               MOVE DP015-GREEN        TO ATRNTLEC                              
                                                                                
               MOVE TMNEMONIC-AVAIL-INDCT                                       
                                       TO DP019-SCREEN-AVAILABIL-IND            
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AAVAILA                               
               MOVE DP015-UNDERLINE    TO AAVAILH                               
               MOVE DP015-GREEN        TO AAVAILC                               
                                                                                
               IF  TMNEMONIC-CMAREA-FRMT-CODE EQUAL 'N'                         
                   MOVE TMNEMONIC-CMAREA-FRMT-CODE                              
                                       TO DP019-SCREEN-COMMAREA-FORMAT          
               ELSE                                                             
                   MOVE 'Y'            TO DP019-SCREEN-COMMAREA-FORMAT          
               END-IF                                                           
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO AFORMATA                              
               MOVE DP015-UNDERLINE    TO AFORMATH                              
               MOVE DP015-GREEN        TO AFORMATC                              
                                                                                
               MOVE TMNEMONIC-SAVE-CMAREA-INDCT                                 
                                       TO DP019-SCREEN-SAVE-COMM-IND            
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ASAVCOMA                              
               MOVE DP015-UNDERLINE    TO ASAVCOMH                              
               MOVE DP015-GREEN        TO ASAVCOMC                              
                                                                                
               MOVE TMNEMONIC-CMAREA-LNGTH-MSRMT                                
                                       TO PV-COMM-LENGTH-NUMERIC                
               MOVE PV-COMM-LENGTH-ALPHA                                        
                                       TO DP019-SCREEN-SAVE-COMM-LENGTH         
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                       TO ACOMLENA                              
               MOVE DP015-UNDERLINE    TO ACOMLENH                              
               MOVE DP015-GREEN        TO ACOMLENC                              
                                                                                
               MOVE TMNEMONIC-LAST-UPDATE-TMESTP                                
                                       TO DP019-TIMESTAMP                       
               SET PS-ROW-FOUND-YES    TO TRUE                                  
               MOVE -1                 TO ATRANIDL                              
           ELSE                                                                 
               SET PS-ROW-FOUND-NO     TO TRUE                                  
               SET DP020-NEXT-ACT-APPL-ERROR                                    
                                       TO TRUE                                  
               MOVE PC-TSYMSG-00148    TO DP020-MSG-NUMBER                      
               SET DP020-MSG-FATAL     TO TRUE                                  
                                                                                
               IF  DP019-USE-TSQB                                               
                   PERFORM B300-REWRITE-TSQB                                    
               END-IF                                                           
                                                                                
               PERFORM A850-RESET-INITIAL-INTER-APPL                            
               PERFORM DP015-1000-START-NAVIGATOR                               
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A550-DISPLAY-PANEL                                             *        
      * (Z019-02 DISPLAY PANEL)                                        *        
      ******************************************************************        
                                                                                
       A550-DISPLAY-PANEL.                                                      
                                                                                
           IF  PS-SEND-DATA-ONLY-FRSET                                          
               PERFORM DP018-0000-GET-CURR-TIME                                 
           ELSE                                                                 
               PERFORM DP017-0000-GET-CURR-DATE-TIME                            
               PERFORM DP002-0000-FORMAT-TITLE-LINES                            
           END-IF.                                                              
                                                                                
           IF  PS-SEND-MAP-N-DATA-FRSET    OR                                   
               PS-NEW-MNEMONIC-ENTERED-YES                                      
               PERFORM A600-LOAD-DETAIL-DATA                                    
           END-IF.                                                              
                                                                                
           PERFORM DP005-0000-FORMAT-MSG-LINES.                                 
                                                                                
           IF PS-SEND-MAP-N-DATA-FRSET    OR                                    
              DP020-SRC-AID  =  DP016-PA2                                       
      ***************************************                                   
      * PA2 KEY  -  CHANGE PFKEYS DISPLAYED *                                   
      ***************************************                                   
              PERFORM DP009-0000-LOAD-FK-LINES                                  
           END-IF.                                                              
                                                                                
           PERFORM DP008-0000-WRITE-PANEL.                                      
       EJECT                                                                    
      ******************************************************************        
      * A600-LOAD-DETAIL-DATA                                          *        
      * (Z019-08 LOAD DETAIL DATA)                                     *        
      ******************************************************************        
                                                                                
       A600-LOAD-DETAIL-DATA.                                                   
                                                                                
           MOVE DP019-SCREEN-MNEMONIC  TO AMNEMO.                               
           MOVE DP019-SCREEN-UPD-VSAM-IND                                       
                                       TO AUVINDO.                              
           MOVE DP019-ACCURATE-LOCATION-REQ                                     
                                       TO ALOCO.                                
           MOVE DP019-SCREEN-DB2-PKG-CDE                                        
                                       TO APKGO.                                
           MOVE DP019-SCREEN-CMNT-TXT  TO ACMNTO.                               
           MOVE DP019-SCREEN-TRAN-ID   TO ATRANIDO.                             
           MOVE DP019-SCREEN-MNEMONIC-TITLE                                     
                                       TO ATRNTLEO.                             
           MOVE DP019-SCREEN-AVAILABIL-IND                                      
                                       TO AAVAILO.                              
           MOVE DP019-SCREEN-COMMAREA-FORMAT                                    
                                       TO AFORMATO.                             
           MOVE DP019-SCREEN-SAVE-COMM-IND                                      
                                       TO ASAVCOMO.                             
           MOVE DP019-SCREEN-SAVE-COMM-LENGTH                                   
                                       TO ACOMLENO.                             
           MOVE DP019-SCREEN-GLOBAL-ACCESS                                      
                                       TO AGLBACCO.                             
           MOVE DP019-SCREEN-PROGRAM   TO APROGO.                               
           MOVE DP019-SCREEN-TYPE-CODE TO ASCRTYPO.                             
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A650-MOVE-SCREEN-TO-COMM                                       *        
      * (Z019-13 MOVE SCREEN TO COMMAREA)                              *        
      * IN ORDER TO MINIMIZE DATA TRANSMISSIONS, ALL SCREEN VARIABLES  *        
      * WILL BE STORED IN THE COMMAREA. ALL EDITING AND DATABASE I/O   *        
      * WILL BE FROM THESE FIELDS IN THE APPLICATION SPECIFIC COMMAREA.*        
      * ONLY ATTRIBUTE CHANGES WILL BE SENT TO THE TERMINAL AS LONG AS *        
      * THE CURRENT MAP IS STILL BEING DISPLAYED. THIS MODULE OVERLAYS *        
      * ANY COMMAREA SCREEN VARIABLE THAT HAS CHANGED (EITHER A LENGTH *        
      * GREATER THAN ZERO OR EOF HAS BEEN PRESSED) WITH THE INPUT FIELD*        
      * FROM THE SCREEN.                                               *        
      ******************************************************************        
                                                                                
       A650-MOVE-SCREEN-TO-COMM.                                                
                                                                                
           IF  DP020-SRC-AID  NOT = DP016-CLEAR                                 
               IF ((ANAVKEYL  >  ZEROS)   AND                                   
                   (ANAVCMDL  =  ZEROS)   AND                                   
                   (DP020-SRC-AID  =  DP016-ENTER))                             
                   SET PS-NEW-MNEMONIC-ENTERED-YES                              
                                           TO TRUE                              
                   INITIALIZE DP019-CURRENT-SCREEN-IMAGE                        
                   MOVE ANAVKEYI           TO PV-MNEMONIC                       
                                              DP019-SCREEN-MNEMONIC             
                   MOVE LOW-VALUES         TO ANAVKEYO                          
                   MOVE SPACES             TO DP019-SELECT-MAINT-RESULT         
                   SET PS-SEND-MAP-N-DATA-FRSET                                 
                                           TO TRUE                              
                   PERFORM A250-SEND-INITIAL-SCREEN                             
               ELSE                                                             
                   SET PS-NEW-MNEMONIC-ENTERED-NO                               
                                           TO TRUE                              
                   IF  AGLBACCL  >  ZERO                                        
                       MOVE AGLBACCI       TO                                   
                                           DP019-SCREEN-GLOBAL-ACCESS           
                   ELSE                                                         
                       IF  AGLBACCF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-GLOBAL-ACCESS           
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ACOMLENL  >  ZERO                                        
                       MOVE ACOMLENI       TO                                   
                                           DP019-SCREEN-SAVE-COMM-LENGTH        
                   ELSE                                                         
                       IF  ACOMLENF  =  DP015-ERASE-EOF                         
                           MOVE ZEROS      TO                                   
                                           DP019-SCREEN-SAVE-COMM-LENGTH        
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ASAVCOML  >  ZERO                                        
                       MOVE ASAVCOMI       TO                                   
                                           DP019-SCREEN-SAVE-COMM-IND           
                   ELSE                                                         
                       IF  ASAVCOMF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-SAVE-COMM-IND           
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  AFORMATL  >  ZERO                                        
                       MOVE AFORMATI       TO                                   
                                           DP019-SCREEN-COMMAREA-FORMAT         
                   ELSE                                                         
                       IF  AFORMATF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-COMMAREA-FORMAT         
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  AAVAILL  >  ZERO                                         
                       MOVE AAVAILI        TO                                   
                                           DP019-SCREEN-AVAILABIL-IND           
                   ELSE                                                         
                       IF  AAVAILF  =  DP015-ERASE-EOF                          
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-AVAILABIL-IND           
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ATRNTLEL  >  ZERO                                        
                       MOVE ATRNTLEI       TO                                   
                                           DP019-SCREEN-MNEMONIC-TITLE          
                   ELSE                                                         
                       IF  ATRNTLEF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-MNEMONIC-TITLE          
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ATRANIDL  >  ZERO                                        
                       MOVE ATRANIDI       TO                                   
                                           DP019-SCREEN-TRAN-ID                 
                       SET DP019-DIDNT-ISSUE-MESSAGE                            
                                           TO  TRUE                             
                   ELSE                                                         
                       IF  ATRANIDF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-TRAN-ID                 
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  APROGL  >  ZERO                                          
                       MOVE APROGI         TO                                   
                                           DP019-SCREEN-PROGRAM                 
                   ELSE                                                         
                       IF  APROGF    =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-PROGRAM                 
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ASCRTYPL > ZERO                                          
                       MOVE ASCRTYPI       TO                                   
                                           DP019-SCREEN-TYPE-CODE               
                   ELSE                                                         
                       IF  ASCRTYPF  =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-TYPE-CODE               
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  APKGL    > ZERO                                          
                       MOVE APKGI          TO                                   
                                           DP019-SCREEN-DB2-PKG-CDE             
                   ELSE                                                         
                       IF  APKGF     =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-DB2-PKG-CDE             
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ACMNTL   > ZERO                                          
                       MOVE ACMNTI         TO                                   
                                           DP019-SCREEN-CMNT-TXT                
                   ELSE                                                         
                       IF  ACMNTF    =  DP015-ERASE-EOF                         
                           MOVE SPACES     TO                                   
                                           DP019-SCREEN-CMNT-TXT                
                       END-IF                                                   
                   END-IF                                                       
                                                                                
                   IF  ALOCL  >  ZERO                                           
                       MOVE ALOCI       TO                                      
                                           DP019-ACCURATE-LOCATION-REQ          
                   ELSE                                                         
                       IF  ALOCF  =  DP015-ERASE-EOF                            
                           MOVE SPACES     TO                                   
                                           DP019-ACCURATE-LOCATION-REQ          
                       END-IF                                                   
                   END-IF                                                       
                                                                                
               END-IF                                                           
                                                                                
           END-IF.                                                              
       EJECT                                                                    
      ******************************************************************        
      * A800-SETUP-INTER-APPL                                          *        
      * (Z019-16 SETUP INTER-APPL)                                     *        
      *                                                                *        
      * WHENEVER THIS TRANSACTION IS ABOUT TO NAVIGATE TO ANOTHER      *        
      * TRANSACTION, THIS MODULE WILL FORMAT THE INTER APPLICATION     *        
      * COMMAREA WITH THE DATA SAVED FOR THE CURRENT OBJECT BEING PRO- *        
      * CESSED BY THIS TRANSACTION.                                    *        
      ******************************************************************        
                                                                                
       A800-SETUP-INTER-APPL.                                                   
                                                                                
           MOVE DP020-INTER-APPL-DATA-AREA                                      
                   TO  DP019-SAVE-INTER-APPL-COMMAREA                           
           MOVE DP019-SCREEN-MNEMONIC  TO DP055-MNEMONIC.                       
                                                                                
       EJECT                                                                    
      ******************************************************************        
      * A850-RESET-INITIAL-INTER-APPL                                  *        
      * (Z019-17 RESET INITIAL INTER-APPL)                             *        
      *                                                                *        
      * WHEN THIS MODULE IS ABOUT TO RETURN TO THE MODULE THAT INVOKED *        
      * IT, IT WILL RESET THE COMMAREA TO THE CONTENTS THAT WERE SENT  *        
      * TO IT ALONG WITH ANY UPDATES TO THE OBJECT SENT TO IT, ALONG   *        
      * WITH AN ACKNOWLEDGEMENT CODE.                                  *        
      ******************************************************************        
                                                                                
       A850-RESET-INITIAL-INTER-APPL.                                           
                                                                                
           MOVE DP019-SAVE-INTER-APPL-COMMAREA                                  
                                       TO DP020-INTER-APPL-DATA-AREA.           
                                                                                
           IF  DP019-USE-TSQB                                                   
                                                                                
               IF  DP020-NEXT-ACT-APPL-ERROR                                    
                   MOVE DP019-SAVE-TSQB-RECORD                                  
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
                   SET DP002-ERR (DP019-MN-ITEM)                                
                                     TO  TRUE                                   
                   MOVE DP002-ARCH-MAINT-TSQB-REC                               
                                     TO  DP019-SAVE-TSQB-RECORD                 
                   PERFORM B300-REWRITE-TSQB                                    
               END-IF                                                           
                                                                                
               COMPUTE DP055-NUMBER-PROCESSED-ITEMS =                           
                    ((DP019-MN-ITEM - 1) * DP002-MAX-ITEMS) +                   
                      DP019-MN-SUB                                              
           ELSE                                                                 
               MOVE ZERO  TO DP055-NUMBER-PROCESSED-ITEMS                       
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      **********************************                                        
      * READ A TSQB RECORD.            *                                        
      * PERFORMED BY A240              *                                        
      **********************************                                        
                                                                                
       B100-READ-LIST-QUEUE.                                                    
                                                                                
           EXEC CICS                                                            
               READQ TS                                                         
               QUEUE(DP055-TSQB-QUEUE-NAME)                                     
               INTO(DP002-ARCH-MAINT-TSQB-REC)                                  
               LENGTH(DP055-TSQB-QUEUE-LENGTH)                                  
               ITEM(DP019-MN-ITEM)                                              
               RESP(PV-RESP)                                                    
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP EQUAL DFHRESP(NORMAL)                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE 'B100-READ-LIST-QUEUE'                                      
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    READ THE TEMP STORAGE QUEUE, GET THE NEXT MNEMONIC.         *        
      *    IF AT END, ISSUE MESSAGE, OTHERWISE CONTINUE PROCESSING.    *        
      *                                                                *        
      *    PERFORMED BY A350                                           *        
      *----------------------------------------------------------------*        
                                                                                
       B200-GET-NEXT-MNEMONIC.                                                  
                                                                                
           ADD 1                     TO  DP019-MN-SUB.                          
           IF  DP019-MN-SUB GREATER THAN DP002-MAX-ITEMS                        
               PERFORM B300-REWRITE-TSQB                                        
               ADD 1                 TO  DP019-MN-ITEM                          
               MOVE 1                TO  DP019-MN-SUB                           
               PERFORM B100-READ-LIST-QUEUE                                     
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP019-SAVE-TSQB-RECORD                 
           ELSE                                                                 
               MOVE DP019-SAVE-TSQB-RECORD                                      
                                     TO  DP002-ARCH-MAINT-TSQB-REC              
           END-IF.                                                              
                                                                                
           IF  DP002-MNEMONIC (DP019-MN-SUB) GREATER THAN SPACES                
               MOVE DP002-MNEMONIC (DP019-MN-SUB)                               
                                     TO  DP019-SCREEN-MNEMONIC                  
               SET DP002-INQ (DP019-MN-SUB)                                     
                                     TO  TRUE                                   
               MOVE DP002-ARCH-MAINT-TSQB-REC                                   
                                     TO  DP019-SAVE-TSQB-RECORD                 
           ELSE                                                                 
               SUBTRACT 1          FROM  DP019-MN-SUB                           
               MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER                       
               SET DP020-MSG-INFORMATIONAL                                      
                                     TO  TRUE                                   
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *----------------------------------------------------------------*        
      *    UPDATE THE LIST QUEUE TO REFLECT INQUIRE STATUS.            *        
      *                                                                *        
      *    PERFORMED BY A200, B200.                                    *        
      *----------------------------------------------------------------*        
                                                                                
       B300-REWRITE-TSQB.                                                       
                                                                                
           EXEC CICS                                                            
                WRITEQ TS                                                       
                QUEUE(DP055-TSQB-QUEUE-NAME)                                    
                FROM(DP019-SAVE-TSQB-RECORD)                                    
                LENGTH(DP055-TSQB-QUEUE-LENGTH)                                 
                ITEM(DP019-MN-ITEM)                                             
                RESP(PV-RESP)                                                   
                REWRITE                                                         
           END-EXEC.                                                            
                                                                                
           IF  PV-RESP NOT EQUAL DFHRESP(NORMAL)                                
               MOVE PV-RESP          TO  PM-RESP-22                             
               SET DP013-LOGIC-ABEND TO  TRUE                                   
               SET DP013-NO-ROLLBACK TO  TRUE                                   
               MOVE 'B300-REWRITE-TSQB'                                         
                                     TO  DP013-PARAGRAPH                        
               MOVE PM-PROGRAM-MESSAGE-22                                       
                                     TO  DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      **********************************                                        
      * FETCH ROW FROM TABLE TMNEMONIC *                                        
      **********************************                                        
                                                                                
       X900-READ-TMNEMONIC-ROW.                                                 
                                                                                
             EXEC SQL                                                           
                 SELECT       MNEMONIC_CODE,                                    
                              TRANS_ID_CODE,                                    
                              AVAIL_INDCT,                                      
                              CMAREA_FRMT_CODE,                                 
                              SAVE_CMAREA_INDCT,                                
                              CMAREA_LNGTH_MSRMT,                               
                              PANEL_TITLE_NAME,                                 
                              LAST_UPDATE_TMESTP,                               
                              GLBL_ACCS_IND,                                    
                              PGM_NAME,                                         
                              SCRN_TYP_CDE,                                     
                              UPD_VSAM_IND,                                     
                              DB2_PKG_CDE,                                      
                              CMNT_TXT,                                         
                              ACRT_LOC_REQR_IND                                 
                       INTO   :TMNEMONIC-MNEMONIC-CODE,                         
                              :TMNEMONIC-TRANS-ID-CODE,                         
                              :TMNEMONIC-AVAIL-INDCT,                           
                              :TMNEMONIC-CMAREA-FRMT-CODE,                      
                              :TMNEMONIC-SAVE-CMAREA-INDCT,                     
                              :TMNEMONIC-CMAREA-LNGTH-MSRMT,                    
                              :TMNEMONIC-PANEL-TITLE-NAME,                      
                              :TMNEMONIC-LAST-UPDATE-TMESTP,                    
                              :TMNEMONIC-GLBL-ACCS-IND,                         
                              :TMNEMONIC-PGM-NAME,                              
                              :TMNEMONIC-SCRN-TYP-CDE,                          
                              :TMNEMONIC-UPD-VSAM-IND,                          
                              :TMNEMONIC-DB2-PKG-CDE,                           
                              :TMNEMONIC-CMNT-TXT,                              
                              :TMNEMONIC-ACRT-LOC-REQR-IND                      
                       FROM   TMNEMONIC                                         
                       WHERE  MNEMONIC_CODE  =  :TMNEMONIC-MNEMONIC-CODE        
             END-EXEC.                                                          
                                                                                
      * CHECK SQLCODE OF SELECT *                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  SQL-ROW-NOT-FOUND                                              
                 CONTINUE                                                       
             ELSE                                                               
                 IF  NOT SQL-SUCCESSFUL                                         
                     SET  DP013-DB2-ABEND  TO TRUE                              
                     MOVE 'X900-READ-TMNEMONIC-ROW       '                      
                                       TO DP013-PARAGRAPH                       
                     MOVE SQLCA        TO DP013-SQLCA                           
                     MOVE 'TMNEMONIC         '                                  
                                       TO DP013-DB2-TABLE-NAME (1)              
                     PERFORM DP013-0000-PROCESS-ABEND                           
                 END-IF                                                         
             END-IF.                                                            
       EJECT                                                                    
      *********************************************                             
      * FETCH ROW FROM TABLE TMNEMONIC            *                             
      * TO ACQUIRE THE UPDATED RECORD'S TIMESTAMP *                             
      *********************************************                             
                                                                                
       X905-READ-TMNEMONIC-ROW.                                                 
                                                                                
             EXEC SQL                                                           
                 SELECT       MNEMONIC_CODE,                                    
                              LAST_UPDATE_TMESTP                                
                       INTO   :TMNEMONIC-MNEMONIC-CODE,                         
                              :TMNEMONIC-LAST-UPDATE-TMESTP                     
                       FROM   TMNEMONIC                                         
                       WHERE  MNEMONIC_CODE  =  :TMNEMONIC-MNEMONIC-CODE        
             END-EXEC.                                                          
                                                                                
      * CHECK SQLCODE OF SELECT *                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  SQL-ROW-NOT-FOUND                                              
                 CONTINUE                                                       
             ELSE                                                               
                 IF  NOT SQL-SUCCESSFUL                                         
                     SET  DP013-DB2-ABEND  TO TRUE                              
                     MOVE 'X905-READ-TMNEMONIC-ROW       '                      
                                       TO DP013-PARAGRAPH                       
                     MOVE SQLCA        TO DP013-SQLCA                           
                     MOVE 'TMNEMONIC         '                                  
                                       TO DP013-DB2-TABLE-NAME (1)              
                     PERFORM DP013-0000-PROCESS-ABEND                           
                 END-IF                                                         
             END-IF.                                                            
       EJECT                                                                    
      ***********************************                                       
      * UPDATE ROW FROM TABLE TMNEMONIC *                                       
      ***********************************                                       
                                                                                
       X910-UPDATE-TMNEMONIC-ROW.                                               
                                                                                
             EXEC SQL                                                           
                 UPDATE                                                         
                        TMNEMONIC                                               
                   SET                                                          
                        TRANS_ID_CODE      =                                    
                                          :TMNEMONIC-TRANS-ID-CODE,             
                        AVAIL_INDCT        =                                    
                                          :TMNEMONIC-AVAIL-INDCT,               
                        CMAREA_FRMT_CODE   =                                    
                                          :TMNEMONIC-CMAREA-FRMT-CODE,          
                        SAVE_CMAREA_INDCT  =                                    
                                          :TMNEMONIC-SAVE-CMAREA-INDCT,         
                        CMAREA_LNGTH_MSRMT =                                    
                                          :TMNEMONIC-CMAREA-LNGTH-MSRMT,        
                        PANEL_TITLE_NAME   =                                    
                                          :TMNEMONIC-PANEL-TITLE-NAME,          
                        GLBL_ACCS_IND      =                                    
                                          :TMNEMONIC-GLBL-ACCS-IND,             
                        PGM_NAME           =                                    
                                          :TMNEMONIC-PGM-NAME,                  
                        SCRN_TYP_CDE       =                                    
                                          :TMNEMONIC-SCRN-TYP-CDE,              
                        UPD_VSAM_IND       =                                    
                                          :TMNEMONIC-UPD-VSAM-IND,              
                        DB2_PKG_CDE        =                                    
                                          :TMNEMONIC-DB2-PKG-CDE,               
                        CMNT_TXT           =                                    
                                          :TMNEMONIC-CMNT-TXT,                  
                        ACRT_LOC_REQR_IND  =                                    
                                          :TMNEMONIC-ACRT-LOC-REQR-IND,         
                        LAST_UPDATE_TMESTP =                                    
                                           CURRENT TIMESTAMP                    
                  WHERE                                                         
                         (MNEMONIC_CODE     =                                   
                                  :TMNEMONIC-MNEMONIC-CODE                      
                               AND                                              
                               LAST_UPDATE_TMESTP =                             
                                  :TMNEMONIC-LAST-UPDATE-TMESTP)                
             END-EXEC.                                                          
                                                                                
      * CHECK SQLCODE OF UPDATE *                                               
                                                                                
             MOVE SQLCODE              TO SQL-RETURN-CODE.                      
             IF  SQL-ROW-NOT-FOUND                                              
                 CONTINUE                                                       
             ELSE                                                               
                 IF  NOT SQL-SUCCESSFUL                                         
                     SET  DP013-DB2-ABEND  TO TRUE                              
                     MOVE 'X910-UPDATE-TMNEMONIC-ROW     '                      
                                       TO DP013-PARAGRAPH                       
                     MOVE SQLCA        TO DP013-SQLCA                           
                     MOVE 'TMNEMONIC         '                                  
                                       TO DP013-DB2-TABLE-NAME (1)              
                     PERFORM DP013-0000-PROCESS-ABEND                           
                 END-IF                                                         
             END-IF.                                                            
       EJECT                                                                    
      **************************************                                    
      * COPY DP002-0000-FORMAT-TITLE-LINES *                                    
      **************************************                                    
           COPY DPPD022.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP003-0000-RETURN-TO-CICS *                                        
      **********************************                                        
           COPY DPPD003.                                                        
       EJECT                                                                    
      ************************************                                      
      * COPY DP005-0000-FORMAT-MSG-LINES *                                      
      ************************************                                      
           COPY DPPD005.                                                        
       EJECT                                                                    
      *****************************                                             
      * COPY DP007-0000-GET-PANEL *                                             
      *****************************                                             
           COPY DPPD007.                                                        
       EJECT                                                                    
      *******************************                                           
      * COPY DP008-0000-WRITE-PANEL *                                           
      *******************************                                           
           COPY DPPD008.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP009-0000-LOAD-FK-LINES *                                         
      *********************************                                         
           COPY DPPD009.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP013-0000-PROCESS-ABEND *                                         
      *********************************                                         
           COPY DPPD013.                                                        
       EJECT                                                                    
      **********************************                                        
      * COPY DP015-0000-CHECK-COMMAREA *                                        
      **********************************                                        
           COPY DPPD015.                                                        
       EJECT                                                                    
      **************************************                                    
      * COPY DP017-0000-GET-CURR-DATE-TIME *                                    
      **************************************                                    
           COPY DPPD017.                                                        
       EJECT                                                                    
      *********************************                                         
      * COPY DP018-0000-GET-CURR-TIME *                                         
      *********************************                                         
           COPY DPPD018.                                                        
