000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400*                                                                         
000500 PROGRAM-ID.    FSKUP071.                                                 
000600 AUTHOR.        LINDA FRIESS.                                             
000700 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000800 DATE-WRITTEN.  03-07-2003.                                               
000900 DATE-COMPILED.                                                           
001000*                                                                         
001100******************************************************************        
001200*       FSKUP071 - SALES UPDATE TO THE SUMMARY TABLES            *        
001300******************************************************************        
001400*   NOTE: THIS PROGRAM REPLACES FSKUP031 (WHICH WAS DERIVED FROM *        
001500*   FSKUP030).                                                   *        
001600******************************************************************        
001700*                                                                *        
001800*   THIS PROGRAM UPDATES THE FOLLOWING DB2 TABLES:               *        
001900*       DB2 TABLE NAME    DB2 ELEMENTS UPDATED                   *        
002000*       --------------    ------------------------------------   *        
002100*       SKU_STORE_WKLY    SALES_UNIT_QTY; SALES_AMT;             *        
002200*                         XXX_SLS_UNIT_QTY WHERE XXX = DAY OF THE*        
002300*                                                      WEEK      *        
002400*                         IF THERE IS NO RECORD FOR THE SPECIFIC *        
002500*                         STORE NUMBER/SALES-TYPE/INDCT, IT IS   *        
002600*                         CREATED.  STORE 000 IS ALWAYS UPDATED, *        
002700*                         IF THERE IS NO RECORD FOR STORE 000/   *        
002800*                         SALES-TYPE/INDCT, IT IS ALSO CREATED.  *        
002900*       SKU_STORE_MTHLY   SALES_UNIT_QTY; SALES_AMT              *        
003000*                         IF THERE IS NO RECORD FOR THE SPECIFIC *        
003100*                         STORE NUMBER/SALES-TYPE/INDCT, IT IS   *        
003200*                         CREATED.  STORE 000 IS ALWAYS UPDATED, *        
003300*                         IF THERE IS NO RECORD FOR STORE 000/   *        
003400*                         SALES-TYPE/INDCT, IT IS ALSO CREATED.  *        
003500*                                                                *        
003600*                                                                *        
003700*   INPUT:     CONTROL-FILE                                      *        
003800*              SALES AUDIT TABLE                                 *        
003900*                                                                *        
004000*   OUTPUT:    NONE                                              *        
004100*                                                                *        
004200*   I/O:       SKU-STORE-WKLY DB2 TABLE                          *        
004300*              SKU-STORE-MTHLY DB2 TABLE                         *        
004400*                                                                *        
004500*   GENERAL LOGIC DESCRIPTION:                                   *        
004600*                                                                *        
004700*       THIS PROGRAM UPDATES 2 SUMMARY DB2 TABLES USING DATA     *        
004800*   FROM THE FSTT_OH_SLS_AUD TABLE.  THE TWO SUMMARY DB2 TABLES  *        
004900*   ARE:                                                         *        
005000*                                                                *        
005100* TSKSTWKS - KEYS ARE ITEM #, STORE #, WEEK #, AND SALES-TYPE.   *        
005200*            THIS IS WEEKLY SALES INFO BY TYPE FOR EACH SKU      *        
005300*            IN EACH STORE.                                      *        
005400* TSKSTMTH - KEYS ARE ITEM #, STORE #, MONTH #, AND SALES-TYPE.  *        
005500*            THIS IS MONTHLY SALES INFO BY TYPE FOR EACH SKU     *        
005600*            IN EACH STORE.                                      *        
005700*                                                                *        
005800*   THIS PROGRAM FETCHES ROWS FROM THE ONHAND SALES AUDIT TABLE. *        
005900*   ANY CHANGE IN ITEM#, WEEK#, OR MONTH# TRIGGERS THE FOLLOWING:*        
006000*                                                                *        
006100*     1.  WEEK # CHANGE - TSKSTWKS IS UPDATED FOR THE STORE 000  *        
006200*                         RECORD                                 *        
006300*     2.  MONTH # CHANGE - TSKSTMTH IS UPDATED FOR THE STORE 000 *        
006400*                         RECORD.                                *        
006500*     3.  ITEM # CHANGE - TSKSTWKS AND TSKSTMTH ARE UPDATED      *        
006600*                         FOR THE STORE 000 RECORD.              *        
006700*                                                                *        
006800*    STORE 000 DATA IS HELD IN AN INTERNAL TABLE BY SALES TYPE.  *        
006900*    ALL STORE 000 RECORDS ARE WRITTEN TO THE DB2 TABLES ON A    *        
007000*    CHANGE IN ITEM NUMBER.                                      *        
007100*                                                                *        
007200*    AFTER THE ABOVE PROCESSES (IF APPLICABLE) ARE DONE, THE     *        
007300*    PROGRAM UPDATES TSKSTWKS AND TSKSTMTH BASED ON THE          *        
007400*    INPUT INFO FOR THE SPECIFIC STORE AND ACCUMULATES QTY AND   *        
007500*    AMT.                                                        *        
007600*                                                                *        
007700*    AT EOF, THE COUNT OF DB2 I/O (UPDATES AND INSERTS) ON       *        
007800*    EACH OF THE TABLES WILL BE DISPLAYED.  ALSO, A TOTAL        *        
007900*    UNIT AND TOTAL DOLLAR AMT WILL BE DISPLAYED.                *        
008000*                                                                *        
008100******************************************************************        
008200*  CHANGE MADE: CHANGED PROGRAM FOR STORE NUMBER EXPANSION TO    *        
008300*  HANDLE NEW NUMERIC STORE NUMBER COLUMN ON TSKSTWKS            *        
008400*      MADE BY:  ANNE KINTOPF.                                   *        
008500*         DATE:  03/29/2005               WR# 26354              *        
008600******************************************************************        
008200*  CHANGE MADE: UNCOMMENTED THE MOVE OF PA-COMMIT-CNT TO         *        
008300*  PV-DIS-COMMITS IN PARA 9000-EOF-ROUTINE.  THIS PROGRAM WAS    *        
008300*  SHOWING IN THE DB2 LOGS WITH HIGH NUMBERS OF UNCOMMITTED      *        
008300*  UPDATES AND THE DISPLAYS WERE SHOWING THAT NO COMMITS WERE    *        
008300*  BEING TAKEN.  THE DISPLAYS WERE USED TO SET THE TIME          *        
008300*  FREQUENCY IN SECONDS ON THE TCKRSTCNTL TABLE.                 *        
008300*  FIND 'UNCMNT' TO SEE LINE CHANGED.                                     
008400*      MADE BY:  JIM HUNTER.                                     *        
008500*         DATE:  06/21/2022               CHG0271378             *        
008600******************************************************************        
008700 ENVIRONMENT DIVISION.                                                    
008800******************************************************************        
008900 CONFIGURATION SECTION.                                                   
009000******************************************************************        
009100 SOURCE-COMPUTER.    IBM-3090.                                            
009200 OBJECT-COMPUTER.    IBM-3090.                                            
009300******************************************************************        
009400 INPUT-OUTPUT SECTION.                                                    
009500******************************************************************        
009600 FILE-CONTROL.                                                            
009700******************************************************************        
009800*                                                                         
009900     SELECT CONTROL-FILE    ASSIGN TO INP01.                              
010000*                                                                         
010100******************************************************************        
010101 DATA DIVISION.                                                           
010102*****************************************************************         
010103*                                                                         
010104 FILE SECTION.                                                            
010105*                                                                         
010106 FD  CONTROL-FILE                                                         
010107     RECORDING MODE IS F                                                  
010108     DATA RECORD IS CONTROL-RECORD.                                       
010109                                                                          
010110 01  CONTROL-RECORD                    PIC X(80).                         
010120*                                                                         
010130******************************************************************        
010140 WORKING-STORAGE SECTION.                                                 
010150*                                                                         
012700 01  CONTROL-DATA-IN.                                                     
012800     05  CTL-PROCESSING-DATE           PIC X(10).                         
013500     05  FILLER                        PIC X(70).                         
013600*                                                                         
013700 01  ABEND-CODE                    PIC S9(04)    VALUE ZEROES             
013800                                                 COMP                     
013900                                                 SYNC.                    
014000*                                                                         
014100******************************************************************        
014200*  THIS DETAIL LINE IS PRINTED OUT FOR AN ABEND.                 *        
014300******************************************************************        
014400 01  DL-DETAIL-LINES.                                                     
014500     05  DL-ERROR-LINE.                                                   
014600         10  FILLER                PIC X(08)     VALUE SPACES.            
014700         10  FILLER                PIC X(12)     VALUE                    
014800             'ERROR MSG = '.                                              
014900         10  DL-ERR-MSG            PIC X(30)     VALUE SPACES.            
015000         10  FILLER                PIC X(05)     VALUE SPACES.            
015100         10  FILLER                PIC X(13)     VALUE                    
015200             'RECORD-KEY = ' .                                            
015300         10  DL-ERR-KEY.                                                  
015400             15  DL-ERR-ITEM       PIC 9(15)     VALUE ZERO.              
015500             15  FILLER            PIC X(01)     VALUE SPACES.            
015600             15  DL-ERR-STORE      PIC 9(04)     VALUE ZERO.              
015700             15  FILLER            PIC X(01)     VALUE SPACES.            
015800             15  DL-ERR-TYPE       PIC X(02)     VALUE SPACES.            
015900             15  FILLER            PIC X(01)     VALUE SPACES.            
016000             15  DL-ERR-BOW-DATE   PIC X(10)     VALUE SPACES.            
016100         10  FILLER                PIC X(05)     VALUE SPACES.            
016200         10  FILLER                PIC X(11)     VALUE                    
016300             'SQLCODE = '.                                                
016400         10  DL-ERR-SQLCODE        PIC S9(04)    VALUE ZEROES.            
016500         10  FILLER                PIC X(05)     VALUE SPACES.            
016600*                                                                         
016700 01  PC-PROGRAM-CONSTANTS.                                                
016800     05  PC-JOB-NAME               PIC X(08)     VALUE 'FSK032  '.        
016900     05  PC-PROGRAM-NAME           PIC X(08)     VALUE 'FSKUP071'.        
017000     05  PC-000-STORENO            PIC 9(05)     VALUE ZERO.              
017100     05  PC-DUP-DB2-REC            PIC S9(09)    VALUE -803               
017200                                                 COMP-4 SYNC.             
017300     05  PC-DB2-CONTENTION         PIC S9(09)    VALUE -904               
017400                                                 COMP-4 SYNC.             
017500     05  PC-DB2-DEADLOCK           PIC S9(09)    VALUE -911               
017600                                                 COMP-4 SYNC.             
017700     05  PC-DB2-CONTENTION-LIMIT   PIC S9(02)    VALUE +3.                
017800     05  PC-ERROR-CODES.                                                  
017900         10  PC-SQL-ERROR          PIC S9(04)    VALUE +4013.             
018000*                                                                         
018100****  ERROR LINES - INSERT/UPDATE ERROR DISPLAYS                          
018200     05  PE-ERROR-DB2-PARMS-L1     PIC X(60)    VALUE SPACES.             
018300     05  PE-ERROR-DB2-PARMS-L2     PIC X(60)    VALUE SPACES.             
018400     05  PE-ERROR-MSG-L1.                                                 
018500         10 FILLER                 PIC X(08)     VALUE                    
018600             'ITEM-NO='.                                                  
018700         10 PE-ITEM-NUMBER         PIC 9(15).                             
018800         10 FILLER                 PIC X(01)     VALUE SPACES.            
018900         10 FILLER                 PIC X(08)     VALUE                    
019000             'STORENO='.                                                  
019100         10 PE-STORENO             PIC 9(05).                             
019200         10 FILLER                 PIC X(01)     VALUE SPACES.            
019300         10 FILLER                 PIC X(09)     VALUE                    
019400             'BOW/YRMO='.                                                 
019500         10 PE-BOW-DTE             PIC X(10).                             
019600         10 FILLER                 PIC X(05)     VALUE SPACES.            
019700     05  PE-ERROR-MSG-L2.                                                 
019800         10 FILLER                 PIC X(10)     VALUE                    
019900             'SALE-TYPE='.                                                
020000         10 PE-SALE-TYPE           PIC X(02).                             
020100         10 FILLER                 PIC X(01)     VALUE SPACES.            
020200         10 FILLER                 PIC X(10)     VALUE                    
020300             'SALE-QTY ='.                                                
020400         10 PE-SALE-QTY            PIC 9(07).                             
020500         10 FILLER                 PIC X(01)     VALUE SPACES.            
020600         10 FILLER                 PIC X(10)     VALUE                    
020700             'SALE-AMT ='.                                                
020800         10 PE-SALE-AMT            PIC 9(07)V99.                          
020900         10 FILLER                 PIC X(10)     VALUE SPACES.            
021000*                                                                         
021100 01  PA-DB2-ERROR-ACCUMULATORS                   COMP-3.                  
021200     05  PA-DB2-CONTENTION-CTR     PIC S9(3)     VALUE 0.                 
021300*                                                                         
021400*                                                                         
028500 01  PROGRAM-SWITCHES.                                                    
028600     05  PS-EOF-CONTROL-FILE         PIC  X(01)  VALUE 'N'.               
028700         88  EOF-CONTROL-FILE                    VALUE 'Y'.               
028800     05  PS-EOF-SALES-CURSOR         PIC  X(01)  VALUE 'N'.               
028900         88  EOF-SALES-CURSOR                    VALUE 'Y'.               
029000     05  PS-FIRST-COMMIT-SW          PIC  X(01)  VALUE 'N'.               
029100         88  PS-FIRST-COMMIT                     VALUE 'Y'.               
029200*                                                                         
029300 01  PA-PROGRAM-ACCUMULATORS                 COMP-3.                      
029400     05  PA-ST-INPUT           PIC S9(7)     VALUE 0.                     
029500     05  PA-INPUT-AMT          PIC S9(9)V99  VALUE 0.                     
029600     05  PA-INPUT-QTY          PIC S9(7)     VALUE 0.                     
029700     05  PA-COMMIT-CNT         PIC  9(7)     VALUE 0.                     
029800*                                                                         
029900******************************************************************        
030000*    ACCUMULATION OF QTY AND AMOUNTS FOR THE SAME ITEM-NUMBER    *        
030100*    (AND FSCL-BOW-DTE AND SALES-TYPE, FOR TSKSTWKS AND TSKSTMTH.*        
030200*    BY ACCUMULATING QTY AND AMOUNT, THE NUMBER OF UPDATES TO    *        
030300*    STORE 000 RECORD IS REDUCED TO 1                            *        
030400******************************************************************        
030500 01  PA-STORE-000-TSKSTWKS-TABLE.                                         
030600     05  PA-STORE-000-TSKSTWKS-COUNTS                                     
030700                                   OCCURS 10 TIMES.                       
030800         10  PA-STORE-000-WK-AMT                                          
030900                                   PIC S9(9)V99  VALUE 0.                 
031000         10  PA-STORE-000-WK-QTY                                          
031100                                   PIC S9(7)     VALUE 0.                 
031200         10  PA-STORE-000-SALES-QTY                                       
031300                                   PIC S9(7)     VALUE 0.                 
031400         10  PA-STORE-000-WKLY-TYPE                                       
031500                                   PIC X(2)      VALUE SPACES.            
031600*                                                                         
031700*01  PA-STORE-000-TSKSTMTH-TABLE.                                         
031800*    05  PA-STORE-000-TSKSTMTH-COUNTS                                     
031900*                                  OCCURS 10 TIMES.                       
032000*        10  PA-STORE-000-MTH-AMT                                         
032100*                                  PIC S9(9)V99  VALUE 0.                 
032200*        10  PA-STORE-000-MTH-QTY                                         
032300*                                  PIC S9(7)     VALUE 0.                 
032400*        10  PA-STORE-000-MTHLY-TYPE                                      
032500*                                  PIC X(2)      VALUE SPACES.            
032600******************************************************************        
032700*    PA-DB2-ACCESS-COUNTS                                        *        
032800*    NUMBER OF ACCESSES, I.E. READS, UPDATES AND INSERTS TO      *        
032900*    EACH DB2 TABLE.                                             *        
033000******************************************************************        
033100 01  PA-DB2-ACCESS-COUNTS.                                                
033200     05  PA-DB2-TSKSTWK-UPD    PIC S9(7)     VALUE 0.                     
033300     05  PA-DB2-TSKSTWK-INS    PIC S9(7)     VALUE 0.                     
033400*    05  PA-DB2-TSKSTMTH-UPD   PIC S9(7)     VALUE 0.                     
033500*    05  PA-DB2-TSKSTMTH-INS   PIC S9(7)     VALUE 0.                     
033600*                                                                         
033700 01  PI-PROGRAM-INDICATORS.                                               
033800     05  PI-EOF-PROCESSED-IND  PIC X(01)     VALUE 'N'.                   
033900         88  PI-EOF-PROCESSED                VALUE 'Y'.                   
034000     05  PI-DB2-CALL-COMPLETE-IND                                         
034100                               PIC X(01)     VALUE 'N'.                   
034200         88  PI-DB2-CALL-COMPLETE            VALUE 'Y'.                   
034300*                                                                         
034400 01  PV-PROGRAM-VARIABLES.                                                
034500*    05  PV-STORENO-COMP       PIC S9(4) COMP VALUE 0.                    
034500     05  PV-STORENO-NUM        PIC 9(05).                                 
034600*    05  FILLER REDEFINES PV-STORENO-NUM.                                 
034700*        10  FILLER            PIC X(01).                                 
034800*        10  PV-STORENO        PIC X(03).                                 
034900                                                                          
035000     05  PV-STORE-000-SUB      PIC 99        VALUE 0.                     
035100     05  PV-STORE-000-SUB-X  REDEFINES  PV-STORE-000-SUB                  
035200                               PIC XX.                                    
035300     05  PV-PARAGRAPH-NAME     PIC X(30)     VALUE SPACES.                
035400     05  PV-DB2-SALES-TYPE         PIC X(02)     VALUE SPACES.            
035500     05  PV-ITEM-NBR               PIC S9(15)V COMP-3                     
035600                                                 VALUE ZERO.              
035700     05  PV-COMMIT-TIMESTAMP       PIC X(26)     VALUE SPACES.            
035800     05  PV-CURRENT-TIMESTAMP      PIC X(26)     VALUE SPACES.            
035900     05  PV-TOTAL-SLD-QTY          PIC S9(5)V COMP-3                      
036000                                                 VALUE ZERO.              
036100     05  PV-TOTAL-CHRGD-AMT        PIC S9(7)V99 COMP-3                    
036200                                                 VALUE ZERO.              
036300     05  PV-NULL-IND-SLD           PIC S9(04)    COMP.                    
036400     05  PV-NULL-IND-CHRGD         PIC S9(04)    COMP.                    
036500     05  PV-SALES-QTY              PIC S9(5)V COMP-3.                     
036600                                                                          
036700     05  PV-DAY-OF-WEEK        PIC 9(01).                                 
084400         88 PV-SUNDAY              VALUE 1.                               
084600         88 PV-MONDAY              VALUE 2.                               
084800         88 PV-TUESDAY             VALUE 3.                               
085000         88 PV-WEDNESDAY           VALUE 4.                               
085200         88 PV-THURSDAY            VALUE 5.                               
085400         88 PV-FRIDAY              VALUE 6.                               
085600         88 PV-SATURDAY            VALUE 7.                               
085700****************************************************************          
085800*   FOLLOWING FIELDS ARE USED TO UPDATE WEEKLY AND MONTHLY     *          
085900*   SALES USING ONE UPDATE CALL RATHER THAN A READ AND THEN    *          
086000*   AN UPDATE.                                                 *          
086100****************************************************************          
086200     05  PV-UPDATE-BUCKETS.                                               
086300         10  PV-WKSL-SALES-UNIT-QTY    PIC S9(7)V COMP-3                  
086400                                                 VALUE ZEROES.            
086500         10  PV-WKSL-SALES-AMT         PIC S9(7)V99 COMP-3                
086600                                                 VALUE ZEROES.            
086700         10  PV-WKSL-SUN-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
086800                                                 VALUE ZEROES.            
086900         10  PV-WKSL-MON-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
087000                                                 VALUE ZEROES.            
087100         10  PV-WKSL-TUE-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
087200                                                 VALUE ZEROES.            
087300         10  PV-WKSL-WED-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
087400                                                 VALUE ZEROES.            
087500         10  PV-WKSL-THU-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
087600                                                 VALUE ZEROES.            
087700         10  PV-WKSL-FRI-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
087800                                                 VALUE ZEROES.            
087900         10  PV-WKSL-SAT-SLS-UNIT-QTY  PIC S9(5)V COMP-3                  
088000                                                 VALUE ZEROES.            
088100         10  PV-MTHSL-SALES-UNIT-QTY   PIC S9(7)V COMP-3                  
088200                                                 VALUE ZEROES.            
088300         10  PV-MTHSL-SALES-AMT        PIC S9(7)V99 COMP-3                
088400                                                 VALUE ZEROES.            
088500*****************************************************************         
088600*     FORMATTED FIELDS FOR END OF JOB DISPLAY COUNTS.           *         
088700*****************************************************************         
088800         10  PV-DIS-SALES-AMT      PIC ZZZ,ZZZ,ZZ9.99-                    
088900                                                 VALUE ZEROES.            
089000         10  PV-DIS-SALES-QTY      PIC Z,ZZZ,ZZ9-                         
089100                                                 VALUE ZEROES.            
089200         10  PV-DIS-RECORDS-READ   PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089300         10  PV-DIS-TSKSTWK-UPD    PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089400         10  PV-DIS-TSKSTWK-INS    PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089500*        10  PV-DIS-TSKSTMTH-UPD   PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089600*        10  PV-DIS-TSKSTMTH-INS   PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089700         10  PV-DIS-COMMITS        PIC Z,ZZZ,ZZ9 VALUE ZEROES.            
089800                                                                          
089900*****************************************************************         
090000*  HOLD AREA FOR SKU/SALES INFORMATION                          *         
090100*****************************************************************         
090200     05  SK-SAVE-KEYS.                                                    
090300         10  SK-ITEM-NUMBER        PIC S9(15)V   COMP-3                   
090400                                                 VALUE ZEROES.            
090500         10  SK-SALES-TYPE         PIC X(02)     VALUE SPACES.            
090600         10  SK-WKLY-FSCL-BOW-DTE                                         
090700                                   PIC X(10)     VALUE SPACES.            
090800         10  SK-MTHLY-ACCTG-PERIOD.                                       
090900             15  SK-MTHLY-PERD-YR  PIC X(04)     VALUE SPACES.            
091000             15  SK-MTHLY-PERD-MN  PIC X(02)     VALUE SPACES.            
091100         10  SK-MTHLY-ACCTG-PERIOD-X                                      
091200                   REDEFINES SK-MTHLY-ACCTG-PERIOD                        
091300                                   PIC X(06).                             
091400*                                                                         
091500     05  WK-WORK-KEYS.                                                    
091600         10  WK-ITEM-NUMBER        PIC S9(15)V   COMP-3                   
091700                                                 VALUE ZEROES.            
091800         10  WK-SALES-TYPE         PIC X(02)     VALUE SPACES.            
091900         10  WK-WKLY-FSCL-BOW-DTE                                         
092000                                   PIC X(10)     VALUE SPACES.            
092100         10  WK-MTHLY-ACCTG-PERIOD.                                       
092200             15  WK-MTHLY-PERD-YR  PIC X(04)     VALUE SPACES.            
092300             15  WK-MTHLY-PERD-MN  PIC X(02)     VALUE SPACES.            
092400*                                                                         
092500*                                                                         
092600 01  CR-CHECKPOINT-RESTART-AREA.                                          
092700     05  CR-AREA-LEN                 PIC S9(04) VALUE +15  COMP.          
092800                                                                          
092900     05  CR-CHECKPOINT-RESTART-VAR.                                       
093000         10  CR-ITEM-NBR             PIC S9(15)V  VALUE ZERO.             
093100*                                                                         
093200*                                                                         
093300****************************************************************          
093400* CALENDAR ROUTINE WORK AREA                                              
093500****************************************************************          
093600     COPY DPWS500.                                                        
093700*                                                                         
093800******************************************************************        
093900*  DCLGEN MEMBER FOR THE FSTT_OH_SLS_AUD TABLE                   *        
094000******************************************************************        
094100     EXEC SQL                                                             
094200         INCLUDE FST00005                                                 
094300     END-EXEC.                                                            
094400*                                                                         
094500******************************************************************        
094600*  DCLGEN MEMBER FOR THE TSKSTWKS TABLE                          *        
094700******************************************************************        
094800     EXEC SQL                                                             
094900         INCLUDE TSKSTWKS                                                 
095000     END-EXEC.                                                            
095100*                                                                         
095200******************************************************************        
095300*  DCLGEN MEMBER FOR THE TSKSTMTH TABLE                          *        
095400******************************************************************        
095500*    EXEC SQL                                                             
095600*        INCLUDE TSKSTMTH                                                 
095700*    END-EXEC.                                                            
095800*                                                                         
095900******************************************************************        
096000*  DCLGEN FOR THE CHECKPOINT RESTART CONTROL TABLE                        
096100******************************************************************        
096200     EXEC SQL                                                             
096300         INCLUDE TCKRSTCN                                                 
096400     END-EXEC.                                                            
096500*                                                                         
096600******************************************************************        
096700*  COBOL DECLARATIONS FOR THE CHECKPOINT RESTART INFO TABLE               
096800******************************************************************        
096900     EXEC SQL                                                             
097000         INCLUDE TCKRSTIN                                                 
097100     END-EXEC.                                                            
097200*                                                                         
097300******************************************************************        
097400*  COMMUNICATIONS AREA FOR DB2                                   *        
097500******************************************************************        
097600     EXEC SQL                                                             
097700         INCLUDE SQLCA                                                    
097800     END-EXEC.                                                            
097900*                                                                         
098000******************************************************************        
098100*  SQL ERROR AREA                                                *        
098200******************************************************************        
098300     COPY SQLCA2.                                                         
098400*                                                                         
098500******************************************************************        
098600*  SALES TRANSACTION CURSOR                                      *        
098700******************************************************************        
098800*                                                                         
098900     EXEC SQL                                                             
099000       DECLARE SALES_CSR CURSOR WITH HOLD FOR                             
099100       SELECT ITM_NBR                                                     
099200             ,LOC_NBR                                                     
099300             ,FS_SLS_TYP_CDE                                              
099400             ,ACCTG_PERD_YR                                               
099500             ,ACCTG_PERD_MN                                               
099600             ,ACCTG_BOW_DTE                                               
099700             ,SUM(SLD_QTY)                                                
099800             ,SUM(NET_CHRGD_AMT)                                          
099900         FROM FSTT_OH_SLS_AUD                                             
100000        WHERE PRCG_DTE = :CTL-PROCESSING-DATE                             
100100          AND TRN_STAT_CDE = 'C'                                          
100200          AND ITM_NBR > :PV-ITEM-NBR                                      
100300        GROUP BY ITM_NBR                                                  
100400                ,LOC_NBR                                                  
100500                ,FS_SLS_TYP_CDE                                           
100600                ,ACCTG_PERD_YR                                            
100700                ,ACCTG_PERD_MN                                            
100800                ,ACCTG_BOW_DTE                                            
100900        FOR FETCH ONLY                                                    
101000     END-EXEC.                                                            
101100*                                                                         
101200*                                                                         
101300******************************************************************        
101400 PROCEDURE DIVISION.                                                      
101500******************************************************************        
101600*  THIS IS THE MAIN MODULE.                                      *        
101700*  PERFORMS:   1.  HOUSEKEEPING                                  *        
101800*              2.  MAIN ROUTINE UNTIL EOF                        *        
101900*              3.  EOF PROCESSING                                *        
102000******************************************************************        
102100*                                                                         
102200     PERFORM 1000-HOUSEKEEPING.                                           
102300*                                                                         
102400     PERFORM 2000-MAIN-ROUTINE                                            
102500         UNTIL PI-EOF-PROCESSED.                                          
102600*                                                                         
102700     PERFORM 9000-EOF-ROUTINE.                                            
102800     STOP RUN.                                                            
102900                                                                          
103000*                                                                         
103100******************************************************************        
103200*  DOES THE FOLLOWING:                                           *        
103300*     1.  READ DATE FROM CONTROL FILE & GET DAY OF WEEK          *        
103400*     2.  CHECK IF IN RESTART STATUS                             *        
103500*     3.  OPEN CURSOR & FETCH FIRST ROW                          *        
103600*     4.  SET UP SAVE-KEYS BASED ON FIRST ROW READ               *        
103700******************************************************************        
103800 1000-HOUSEKEEPING.                                                       
103900*                                                                         
104000     DISPLAY '*************************************************'.         
104100     DISPLAY '*** START OF FSKUP071 - SUMMARY TABLE UPDATE  ***'.         
104200     DISPLAY '*************************************************'.         
104300     DISPLAY ' '.                                                         
104400*                                                                         
104500     OPEN INPUT  CONTROL-FILE.                                            
104600     PERFORM 7500-READ-CONTROL-FILE.                                      
104700     CLOSE CONTROL-FILE.                                                  
104800*                                                                         
104900     PERFORM 1100-GET-DAY-OF-WEEK.                                        
105000*                                                                         
105100     PERFORM 8100-INIT-CHECKPOINT-SELECT.                                 
105200                                                                          
105300**   IF PROGRAM IS IN A RESTART STATUS, SELECT SAVED DATA FROM            
105400**   THE RESTART INFO TABLE.                                              
105500*                                                                         
105600     IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                                   
105700         PERFORM 8200-SELECT-RESTART-INFO                                 
105800         MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                            
105900                            CR-CHECKPOINT-RESTART-VAR                     
106000         MOVE CR-ITEM-NBR TO PV-ITEM-NBR                                  
106100     ELSE                                                                 
106200         SET PS-FIRST-COMMIT TO TRUE                                      
106300     END-IF.                                                              
106400*                                                                         
106500     PERFORM 7600-OPEN-SALES-CURSOR.                                      
106600*                                                                         
106700     PERFORM 7000-FETCH-SALES-CSR.                                        
106800*                                                                         
106900     IF EOF-SALES-CURSOR                                                  
107000        SET PI-EOF-PROCESSED TO TRUE                                      
107100     ELSE                                                                 
107200        MOVE FST00005-ITM-NBR        TO SK-ITEM-NUMBER                    
107300        MOVE FST00005-FS-SLS-TYP-CDE TO SK-SALES-TYPE                     
107400        MOVE FST00005-ACCTG-BOW-DTE  TO SK-WKLY-FSCL-BOW-DTE              
107500        MOVE FST00005-ACCTG-PERD-YR  TO SK-MTHLY-PERD-YR                  
107600        MOVE FST00005-ACCTG-PERD-MN  TO SK-MTHLY-PERD-MN                  
107700     END-IF.                                                              
107800                                                                          
107900*                                                                         
108000******************************************************************        
108100*    CALL THE DATE ROUTINE TO DETERMINE THE DAY OF THE WEEK.              
108200******************************************************************        
108300 1100-GET-DAY-OF-WEEK.                                                    
108400*                                                                         
108500     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
108600     SET  DPG51-FISCAL-JAN-DEC-F2 TO TRUE.                                
108700     SET  DPG52-LK-DTE-ISO-FMT    TO TRUE.                                
108800     MOVE CTL-PROCESSING-DATE     TO DPG52-LK-DATE-INPUT.                 
108900*                                                                         
109000     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
109100         USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                       
109200*                                                                         
109300     EVALUATE TRUE                                                        
109400      WHEN DPG54-SEVERE-ERROR                                             
109500          DISPLAY '**********************************'                    
109600          DISPLAY '** ABEND **'                                           
109700          DISPLAY '** PGM = FSKUP071'                                     
109800          DISPLAY '** PARA = 1100-GET-DAY-OF-WEEK'                        
109900          DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE.'              
110000          DISPLAY '** ERROR MESSAGE = '                                   
110100                 DPG54-ERROR-MESSAGE                                      
110200          DISPLAY '** DPG51 = ' DPG51                                     
110300          DISPLAY '** DPG52 = ' DPG52                                     
110400          DISPLAY '** DPG53 = ' DPG53                                     
110500          DISPLAY '** DPG54 = ' DPG54                                     
110600          DISPLAY '**********************************'                    
110700          MOVE +4003 TO ABEND-CODE                                        
110800          CALL 'ILBOABN0' USING ABEND-CODE                                
110900      WHEN DPG54-WARNING-ERROR                                            
111000          DISPLAY                                                         
111100             '** WARNING RETURNED BY CALENDAR ROUTINE.'                   
111200          DISPLAY                                                         
111300             '** CALLED BY 1100-GET-DAY-OF-WEEK'                          
111400          DISPLAY                                                         
111500             '** WARNING MESSAGE = ' DPG54-ERROR-MESSAGE                  
111600      WHEN OTHER                                                          
111700          CONTINUE                                                        
111800     END-EVALUATE.                                                        
111900*                                                                         
112000     MOVE DPG55-NUMERIC-WEEK-DAY TO PV-DAY-OF-WEEK.                       
112100                                                                          
112200*                                                                         
112300******************************************************************        
112400*  THIS PARAGRAPH DOES THE FOLLOWING:                            *        
112500*     1.  CHECK FOR ANY BREAK IN KEYS.  PERFORM TO UPDATE        *        
112600*         STORE 000 RECORD OF CORRESPONDING DB2 TABLES           *        
112700*         DEPENDING ON WHICH KEYS CHANGED.  STORE-000 IS THE     *        
112800*         SUMMARY RECORD.                                        *        
112900*     2.  PERFORM TO UPDATE DB2 TABLES BASED ON RECORD JUST READ *        
113000*     3.  PERFORM TO ACCUMULATE QTY AND AMT.  THESE              *        
113100*         ACCUMULATED VALUES WILL BE USED TO UPDATE THE STORE 000*        
113200*         RECORD WHEN KEY CHANGES (SEE #1 ABOVE).                *        
113300*     4.  FETCHES THE NEXT ROW FROM THE CURSOR                   *        
113400******************************************************************        
113500 2000-MAIN-ROUTINE.                                                       
113600*                                                                         
113700*    UPDATE STORE 000 RECORD ON TSKSTWKS WHEN ITEM-NUMBER OR              
113800*    FSCL-BOW-DTE (BEGINNING OF WEEK DATE) CHANGE                         
113900*                                                                         
114000     IF EOF-SALES-CURSOR OR                                               
114100        (WK-ITEM-NUMBER NOT = SK-ITEM-NUMBER) OR                          
114200        (WK-WKLY-FSCL-BOW-DTE NOT = SK-WKLY-FSCL-BOW-DTE)                 
114300         MOVE PC-000-STORENO  TO PV-STORENO-NUM                           
114400*                                                                         
114500         PERFORM 2300-UPDATE-SKU-STORE-WKLY                               
114600             VARYING PV-STORE-000-SUB                                     
114700             FROM 1 BY 1                                                  
114800             UNTIL PV-STORE-000-SUB > 10                                  
114900*                                                                         
115000         MOVE SK-ITEM-NUMBER TO CR-ITEM-NBR                               
115100         PERFORM 8500-COMMIT-PROCESSING                                   
115200*                                                                         
115300         INITIALIZE PA-STORE-000-TSKSTWKS-TABLE                           
115400     END-IF.                                                              
115500*                                                                         
115600*    UPDATE STORE 000 RECORD ON TSKSTMTH WHEN ITEM-NUMBER OR              
115700*    FSCL-YR-MN-NBR (YEAR AND MONTH NUMBER) CHANGE                        
115800*                                                                         
115900*    IF EOF-SALES-CURSOR OR                                               
116000*       (WK-ITEM-NUMBER NOT = SK-ITEM-NUMBER) OR                          
116100*       (WK-MTHLY-ACCTG-PERIOD NOT = SK-MTHLY-ACCTG-PERIOD)               
116200*        MOVE PC-000-STORENO  TO PV-STORENO                               
116300*                                                                         
116400*        PERFORM 2400-UPDATE-SKU-STORE-MTHLY                              
116500*            VARYING PV-STORE-000-SUB                                     
116600*            FROM 1 BY 1                                                  
116700*            UNTIL PV-STORE-000-SUB > 10                                  
116800*                                                                         
116900*        MOVE SK-ITEM-NUMBER TO CR-ITEM-NBR                               
117000*        PERFORM 8500-COMMIT-PROCESSING                                   
117100*                                                                         
117200*        INITIALIZE PA-STORE-000-TSKSTMTH-TABLE                           
117300*    END-IF.                                                              
117400*                                                                         
117500*    SAVE KEYS FOR NEW ITEM                                               
117600*                                                                         
117700     MOVE WK-ITEM-NUMBER        TO SK-ITEM-NUMBER.                        
117800     MOVE WK-SALES-TYPE         TO SK-SALES-TYPE.                         
117900     MOVE WK-WKLY-FSCL-BOW-DTE  TO SK-WKLY-FSCL-BOW-DTE.                  
118000     MOVE WK-MTHLY-ACCTG-PERIOD TO SK-MTHLY-ACCTG-PERIOD.                 
118100*                                                                         
118200*                                                                         
118300*    UPDATE SPECIFIC STORE ROWS FOR TSKSTWKS AND TSKSTMTH                 
118400*                                                                         
118500     IF EOF-SALES-CURSOR                                                  
118600         SET PI-EOF-PROCESSED TO TRUE                                     
118700     ELSE                                                                 
118800         MOVE FST00005-LOC-NBR TO PV-STORENO-NUM                          
118900         PERFORM 2300-UPDATE-SKU-STORE-WKLY                               
119000*        PERFORM 2400-UPDATE-SKU-STORE-MTHLY                              
119100                                                                          
119200         PERFORM 7000-FETCH-SALES-CSR                                     
119300     END-IF.                                                              
119400                                                                          
119500*                                                                         
119600******************************************************************        
119700*  TRIES TO ADD A ROW TO THE TSKSTWKS TABLE. IF THERE            *        
119800*  IS A DUPLICATE, THE ROW WILL BE READ AND UPDATED.             *        
119900*  ALSO ACCUMULATE QTY AND AMT FOR STORE-000 UPDATE.             *        
120000******************************************************************        
120100 2300-UPDATE-SKU-STORE-WKLY.                                              
120200*                                                                         
120300     IF ((PV-STORENO-NUM = PC-000-STORENO AND                             
120400         PA-STORE-000-WKLY-TYPE (PV-STORE-000-SUB) NOT = SPACES)          
120500                               OR                                         
120600         (PV-STORENO-NUM NOT = PC-000-STORENO))                           
120700         MOVE 'N'  TO PI-DB2-CALL-COMPLETE-IND                            
120800         MOVE ZERO TO PA-DB2-CONTENTION-CTR                               
120900                                                                          
121000         PERFORM 5120-SETUP-WEEKLY                                        
121100         IF PV-STORENO-NUM = PC-000-STORENO                               
121200             MOVE PA-STORE-000-WKLY-TYPE (PV-STORE-000-SUB)               
121300                                TO PV-DB2-SALES-TYPE                      
121400         ELSE                                                             
121500             MOVE SK-SALES-TYPE TO PV-DB2-SALES-TYPE                      
121600         END-IF                                                           
121700                                                                          
121800         PERFORM 5100-INSERT-SKU-STORE-WKLY                               
121900             UNTIL PI-DB2-CALL-COMPLETE                                   
122000                                                                          
122100         IF SQLCODE = PC-DUP-DB2-REC                                      
122200             MOVE 'N'  TO PI-DB2-CALL-COMPLETE-IND                        
122300             MOVE ZERO TO PA-DB2-CONTENTION-CTR                           
122400                                                                          
122500             PERFORM 5210-SETUP-DAILY-SALES                               
122600             PERFORM 5200-UPDATE-SKU-STORE-WKLY                           
122700                 UNTIL PI-DB2-CALL-COMPLETE                               
122800         END-IF                                                           
122900     END-IF.                                                              
123000*                                                                         
123100*    ACCUMULATE QTY AND AMT FOR STORE-000 UPDATE OF TSKSTWKS              
123200*                                                                         
123300     IF PV-STORENO-NUM NOT = PC-000-STORENO                               
123400         MOVE WK-SALES-TYPE      TO PV-STORE-000-SUB-X                    
123500         MOVE WK-SALES-TYPE      TO PA-STORE-000-WKLY-TYPE                
123600                                        (PV-STORE-000-SUB)                
123700         ADD PV-TOTAL-CHRGD-AMT  TO PA-STORE-000-WK-AMT                   
123800                                        (PV-STORE-000-SUB)                
123900         ADD PV-TOTAL-SLD-QTY    TO PA-STORE-000-WK-QTY                   
124000                                        (PV-STORE-000-SUB)                
124100         ADD PV-TOTAL-SLD-QTY    TO PA-STORE-000-SALES-QTY                
124200                                        (PV-STORE-000-SUB)                
124300     END-IF.                                                              
124400                                                                          
124500*                                                                         
124600******************************************************************        
124700*  TRIES TO ADD A RECORD TO THE TSKSTMTH TABLE. IF THERE         *        
124800*  IS A DUPLICATE, THE RECORD WILL BE READ AND UPDATED.          *        
124900*  ALSO ACCUMULATE QTY AND AMT FOR STORE-000 UPDATE.             *        
125000******************************************************************        
125100*2400-UPDATE-SKU-STORE-MTHLY.                                             
125200*                                                                         
125300*    IF ((PV-STORENO = PC-000-STORENO AND                                 
125400*        PA-STORE-000-MTHLY-TYPE (PV-STORE-000-SUB)                       
125500*                                           NOT = SPACES)                 
125600*                              OR                                         
125700*        (PV-STORENO NOT = PC-000-STORENO))                               
125800*                                                                         
125900*        MOVE 'N'  TO PI-DB2-CALL-COMPLETE-IND                            
126000*        MOVE ZERO TO PA-DB2-CONTENTION-CTR                               
126100*        IF PV-STORENO = PC-000-STORENO                                   
126200*            MOVE PA-STORE-000-MTHLY-TYPE (PV-STORE-000-SUB) TO           
126300*                              PV-DB2-SALES-TYPE                          
126400*        ELSE                                                             
126500*            MOVE SK-SALES-TYPE TO PV-DB2-SALES-TYPE                      
126600*        END-IF                                                           
126700*                                                                         
126800*        PERFORM 6100-INSERT-SKU-STORE-MTHLY                              
126900*            UNTIL PI-DB2-CALL-COMPLETE                                   
127000*                                                                         
127100*        IF SQLCODE = PC-DUP-DB2-REC                                      
127200*            MOVE 'N'  TO PI-DB2-CALL-COMPLETE-IND                        
127300*            MOVE ZERO TO PA-DB2-CONTENTION-CTR                           
127400*                                                                         
127500*            PERFORM 6200-UPDATE-SKU-STORE-MTHLY                          
127600*                UNTIL PI-DB2-CALL-COMPLETE                               
127700*        END-IF                                                           
127800*    END-IF.                                                              
127900*                                                                         
128000*   ACCUMULATE QTY AND AMT FOR STORE 000 UPDATE OF TSKSTMTH               
128100*                                                                         
128200*    IF PV-STORENO NOT = PC-000-STORENO                                   
128300*        MOVE WK-SALES-TYPE      TO PV-STORE-000-SUB-X                    
128400*        MOVE WK-SALES-TYPE      TO PA-STORE-000-MTHLY-TYPE               
128500*                                       (PV-STORE-000-SUB)                
128600*        ADD PV-TOTAL-CHRGD-AMT  TO PA-STORE-000-MTH-AMT                  
128700*                                       (PV-STORE-000-SUB)                
128800*        ADD PV-TOTAL-SLD-QTY    TO PA-STORE-000-MTH-QTY                  
128900*                                       (PV-STORE-000-SUB)                
129000*    END-IF.                                                              
129100*                                                                         
129200*                                                                         
129300******************************************************************        
129400*  SQL INSERT FOR TSKSTWKS                                       *        
129500******************************************************************        
129600 5100-INSERT-SKU-STORE-WKLY.                                              
129700*                                                                         
139300     MOVE PV-STORENO-NUM   TO SKSTWKS-STR-NBR.                            
129700*                                                                         
129800     EXEC SQL                                                             
129900         INSERT INTO TSKSTWKS                                             
130000                     (STR_NBR,                                            
130100                      FSCL_BOW_DTE,                                       
130200                      SALES_TYPE_CODE,                                    
130300                      SALES_UNIT_QTY,                                     
130400                      SALES_AMT,                                          
130500                      SUN_SLS_UNIT_QTY,                                   
130600                      MON_SLS_UNIT_QTY,                                   
130700                      TUE_SLS_UNIT_QTY,                                   
130800                      WED_SLS_UNIT_QTY,                                   
130900                      THU_SLS_UNIT_QTY,                                   
131000                      FRI_SLS_UNIT_QTY,                                   
131100                      SAT_SLS_UNIT_QTY,                                   
131200                      ITEM_NMBR)                                          
131300             VALUES  (:SKSTWKS-STR-NBR,                                   
131400                      :SK-WKLY-FSCL-BOW-DTE,                              
131500                      :PV-DB2-SALES-TYPE,                                 
131600                      :SKSTWKS-SALES-UNIT-QTY,                            
131700                      :SKSTWKS-SALES-AMT,                                 
131800                      :SKSTWKS-SUN-SLS-UNIT-QTY,                          
131900                      :SKSTWKS-MON-SLS-UNIT-QTY,                          
132000                      :SKSTWKS-TUE-SLS-UNIT-QTY,                          
132100                      :SKSTWKS-WED-SLS-UNIT-QTY,                          
132200                      :SKSTWKS-THU-SLS-UNIT-QTY,                          
132300                      :SKSTWKS-FRI-SLS-UNIT-QTY,                          
132400                      :SKSTWKS-SAT-SLS-UNIT-QTY,                          
132500                      :SK-ITEM-NUMBER)                                    
132600     END-EXEC.                                                            
132700*                                                                         
132800     IF SQLCODE = ZEROES                                                  
132900         ADD 1 TO PA-DB2-TSKSTWK-INS                                      
133000         MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                             
133700         MOVE SKSTWKS-STR-NBR TO PV-STORENO-NUM                           
133100     ELSE                                                                 
133200         IF SQLCODE NOT = PC-DUP-DB2-REC                                  
133300             MOVE '5100-INSERT-SKU-STORE-WKLY      '                      
133400                                 TO PV-PARAGRAPH-NAME                     
133500             MOVE 'INSERT ERROR ON TSKSTWKS      '                        
133600                                 TO DL-ERR-MSG                            
133700             MOVE SK-ITEM-NUMBER TO PE-ITEM-NUMBER                        
133700             MOVE SKSTWKS-STR-NBR TO PV-STORENO-NUM                       
133800             MOVE  PV-STORENO-NUM TO PE-STORENO                           
133900             MOVE SK-WKLY-FSCL-BOW-DTE TO PE-BOW-DTE                      
134000             MOVE PV-DB2-SALES-TYPE    TO PE-SALE-TYPE                    
134100             MOVE SKSTWKS-SALES-UNIT-QTY TO PE-SALE-QTY                   
134200             MOVE SKSTWKS-SALES-AMT      TO PE-SALE-AMT                   
134300             MOVE PE-ERROR-MSG-L1 TO PE-ERROR-DB2-PARMS-L1                
134400             MOVE PE-ERROR-MSG-L2 TO PE-ERROR-DB2-PARMS-L2                
134500             PERFORM 9500-ERROR-ROUTINE                                   
134600         ELSE                                                             
134700             MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                         
134800         END-IF                                                           
134900     END-IF.                                                              
135000                                                                          
135100*                                                                         
135200******************************************************************        
135300*  SET UP HOST VARIABLES FOR SQL INSERT OF TSKSTWKS              *        
135400******************************************************************        
135500 5120-SETUP-WEEKLY.                                                       
135600*                                                                         
135700     IF  PV-STORENO-NUM =  PC-000-STORENO                                 
135800        MOVE PA-STORE-000-WK-QTY (PV-STORE-000-SUB)                       
135900                                     TO SKSTWKS-SALES-UNIT-QTY            
136000        MOVE PA-STORE-000-WK-AMT (PV-STORE-000-SUB)                       
136100                                     TO SKSTWKS-SALES-AMT                 
136200        MOVE PA-STORE-000-SALES-QTY (PV-STORE-000-SUB)                    
136300                                     TO PV-SALES-QTY                      
136400     ELSE                                                                 
136500        MOVE PV-TOTAL-SLD-QTY        TO SKSTWKS-SALES-UNIT-QTY            
136600                                        PV-SALES-QTY                      
136700        MOVE PV-TOTAL-CHRGD-AMT      TO SKSTWKS-SALES-AMT                 
136800     END-IF.                                                              
136900*                                                                         
137000*  MOVE SALES QUANTITY TO APPROPRIATE DAILY BUCKET                        
137100*                                                                         
137200     EVALUATE TRUE                                                        
137300         WHEN PV-SUNDAY                                                   
137400             MOVE PV-SALES-QTY TO SKSTWKS-SUN-SLS-UNIT-QTY                
137500         WHEN PV-MONDAY                                                   
137600             MOVE PV-SALES-QTY TO SKSTWKS-MON-SLS-UNIT-QTY                
137700         WHEN PV-TUESDAY                                                  
137800             MOVE PV-SALES-QTY TO SKSTWKS-TUE-SLS-UNIT-QTY                
137900         WHEN PV-WEDNESDAY                                                
138000             MOVE PV-SALES-QTY TO SKSTWKS-WED-SLS-UNIT-QTY                
138100         WHEN PV-THURSDAY                                                 
138200             MOVE PV-SALES-QTY TO SKSTWKS-THU-SLS-UNIT-QTY                
138300         WHEN PV-FRIDAY                                                   
138400             MOVE PV-SALES-QTY TO SKSTWKS-FRI-SLS-UNIT-QTY                
138500         WHEN PV-SATURDAY                                                 
138600             MOVE PV-SALES-QTY TO SKSTWKS-SAT-SLS-UNIT-QTY                
138700     END-EVALUATE.                                                        
138800                                                                          
138900*                                                                         
139000******************************************************************        
139100*  SQL UPDATE FOR TSKSTWKS                                       *        
139200******************************************************************        
139300 5200-UPDATE-SKU-STORE-WKLY.                                              
139300                                                                          
139300     MOVE PV-STORENO-NUM   TO SKSTWKS-STR-NBR.                            
\39300                                                                          
139400     EXEC SQL                                                             
139500         UPDATE TSKSTWKS                                                  
139600             SET SALES_UNIT_QTY   = SALES_UNIT_QTY +                      
139700                                     :PV-WKSL-SALES-UNIT-QTY,             
139800                 SALES_AMT        = SALES_AMT +                           
139900                                     :PV-WKSL-SALES-AMT,                  
140000                 SUN_SLS_UNIT_QTY = SUN_SLS_UNIT_QTY +                    
140100                                     :PV-WKSL-SUN-SLS-UNIT-QTY,           
140200                 MON_SLS_UNIT_QTY = MON_SLS_UNIT_QTY +                    
140300                                     :PV-WKSL-MON-SLS-UNIT-QTY,           
140400                 TUE_SLS_UNIT_QTY = TUE_SLS_UNIT_QTY +                    
140500                                     :PV-WKSL-TUE-SLS-UNIT-QTY,           
140600                 WED_SLS_UNIT_QTY = WED_SLS_UNIT_QTY +                    
140700                                     :PV-WKSL-WED-SLS-UNIT-QTY,           
140800                 THU_SLS_UNIT_QTY = THU_SLS_UNIT_QTY +                    
140900                                     :PV-WKSL-THU-SLS-UNIT-QTY,           
141000                 FRI_SLS_UNIT_QTY = FRI_SLS_UNIT_QTY +                    
141100                                     :PV-WKSL-FRI-SLS-UNIT-QTY,           
141200                 SAT_SLS_UNIT_QTY = SAT_SLS_UNIT_QTY +                    
141300                                     :PV-WKSL-SAT-SLS-UNIT-QTY            
141400             WHERE ITEM_NMBR       = :SK-ITEM-NUMBER                      
141500               AND STR_NBR         = :SKSTWKS-STR-NBR                     
\41600               AND FSCL_BOW_DTE    = :SK-WKLY-FSCL-BOW-DTE                
141700               AND SALES_TYPE_CODE = :PV-DB2-SALES-TYPE                   
141800     END-EXEC.                                                            
141900*                                                                         
142000     IF SQLCODE = ZEROES                                                  
142100         ADD 1 TO PA-DB2-TSKSTWK-UPD                                      
142200         MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                             
142300     ELSE                                                                 
142400         MOVE '5200-UPDATE-SKU-STORE-WKLY      '                          
142500                             TO PV-PARAGRAPH-NAME                         
142600         MOVE 'UPDATE ERROR ON TSKSTWKS      '                            
142700                             TO DL-ERR-MSG                                
142800         MOVE SK-ITEM-NUMBER TO PE-ITEM-NUMBER                            
142900         MOVE  PV-STORENO-NUM TO PE-STORENO                               
143000         MOVE SK-WKLY-FSCL-BOW-DTE TO PE-BOW-DTE                          
143100         MOVE PV-DB2-SALES-TYPE    TO PE-SALE-TYPE                        
143200         MOVE PV-WKSL-SALES-UNIT-QTY TO PE-SALE-QTY                       
143300         MOVE PV-WKSL-SALES-AMT      TO PE-SALE-AMT                       
143400         MOVE PE-ERROR-MSG-L1 TO PE-ERROR-DB2-PARMS-L1                    
143500         MOVE PE-ERROR-MSG-L2 TO PE-ERROR-DB2-PARMS-L2                    
143600         PERFORM 9500-ERROR-ROUTINE                                       
143700     END-IF.                                                              
143800                                                                          
143900*                                                                         
144000******************************************************************        
144100*  SET-UP HOST VARIABLES FOR SQL UPDATE FOR TSKSTWKS             *        
144200******************************************************************        
144300 5210-SETUP-DAILY-SALES.                                                  
144400*                                                                         
144500     MOVE +0 TO                                                           
144600         PV-WKSL-SUN-SLS-UNIT-QTY                                         
144700         PV-WKSL-MON-SLS-UNIT-QTY                                         
144800         PV-WKSL-TUE-SLS-UNIT-QTY                                         
144900         PV-WKSL-WED-SLS-UNIT-QTY                                         
145000         PV-WKSL-THU-SLS-UNIT-QTY                                         
145100         PV-WKSL-FRI-SLS-UNIT-QTY                                         
145200         PV-WKSL-SAT-SLS-UNIT-QTY.                                        
145300*                                                                         
145400     IF  PV-STORENO-NUM = PC-000-STORENO                                  
145500         MOVE PA-STORE-000-WK-QTY (PV-STORE-000-SUB)                      
145600                                 TO PV-WKSL-SALES-UNIT-QTY                
145700         MOVE PA-STORE-000-WK-AMT (PV-STORE-000-SUB)                      
145800                                 TO PV-WKSL-SALES-AMT                     
145900         MOVE PA-STORE-000-SALES-QTY (PV-STORE-000-SUB)                   
146000                                 TO PV-SALES-QTY                          
146100     ELSE                                                                 
146200         MOVE PV-TOTAL-SLD-QTY   TO PV-WKSL-SALES-UNIT-QTY                
146300                                    PV-SALES-QTY                          
146400         MOVE PV-TOTAL-CHRGD-AMT TO PV-WKSL-SALES-AMT                     
146500     END-IF.                                                              
146600*                                                                         
146700*  MOVE SALES QUANTITY TO APPROPRIATE DAILY FIELD                         
146800*                                                                         
146900     EVALUATE TRUE                                                        
147000         WHEN PV-SUNDAY                                                   
147100             MOVE PV-SALES-QTY TO PV-WKSL-SUN-SLS-UNIT-QTY                
147200         WHEN PV-MONDAY                                                   
147300             MOVE PV-SALES-QTY TO PV-WKSL-MON-SLS-UNIT-QTY                
147400         WHEN PV-TUESDAY                                                  
147500             MOVE PV-SALES-QTY TO PV-WKSL-TUE-SLS-UNIT-QTY                
147600         WHEN PV-WEDNESDAY                                                
147700             MOVE PV-SALES-QTY TO PV-WKSL-WED-SLS-UNIT-QTY                
147800         WHEN PV-THURSDAY                                                 
147900             MOVE PV-SALES-QTY TO PV-WKSL-THU-SLS-UNIT-QTY                
148000         WHEN PV-FRIDAY                                                   
148100             MOVE PV-SALES-QTY TO PV-WKSL-FRI-SLS-UNIT-QTY                
148200         WHEN PV-SATURDAY                                                 
148300             MOVE PV-SALES-QTY TO PV-WKSL-SAT-SLS-UNIT-QTY                
148400     END-EVALUATE.                                                        
148500                                                                          
148600*                                                                         
148700******************************************************************        
148800*  SQL INSERT FOR TSKSTMTH                                       *        
148900******************************************************************        
149000*6100-INSERT-SKU-STORE-MTHLY.                                             
149100*                                                                         
149200*    IF PV-STORENO = PC-000-STORENO                                       
149300*        MOVE PA-STORE-000-MTH-QTY (PV-STORE-000-SUB)                     
149400*                                  TO SKSTMTH-SALES-UNIT-QTY              
149500*        MOVE PA-STORE-000-MTH-AMT (PV-STORE-000-SUB)                     
149600*                                  TO SKSTMTH-SALES-AMT                   
149700*    ELSE                                                                 
149800*        MOVE PV-TOTAL-SLD-QTY     TO SKSTMTH-SALES-UNIT-QTY              
149900*        MOVE PV-TOTAL-CHRGD-AMT   TO SKSTMTH-SALES-AMT                   
150000*    END-IF.                                                              
150100*                                                                         
150200*    EXEC SQL                                                             
150300*        INSERT INTO TSKSTMTH                                             
150400*                    (ITEM_NMBR,                                          
150500*                     STORE_NMBR,                                         
150600*                     FSCL_YR_MN_NBR,                                     
150700*                     SALES_TYPE_CODE,                                    
150800*                     SALES_UNIT_QTY,                                     
150900*                     SALES_AMT)                                          
151000*            VALUES  (:SK-ITEM-NUMBER,                                    
151100*                     :PV-STORENO,                                        
151200*                     :SK-MTHLY-ACCTG-PERIOD-X,                           
151300*                     :PV-DB2-SALES-TYPE,                                 
151400*                     :SKSTMTH-SALES-UNIT-QTY,                            
151500*                     :SKSTMTH-SALES-AMT)                                 
151600*    END-EXEC.                                                            
151700*                                                                         
151800*    IF SQLCODE = ZEROES                                                  
151900*        ADD 1 TO PA-DB2-TSKSTMTH-INS                                     
152000*        MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                             
152100*    ELSE                                                                 
152200*        IF SQLCODE NOT = PC-DUP-DB2-REC                                  
152300*            MOVE '6100-INSERT-SKU-STORE-MTHLY   '                        
152400*                                TO PV-PARAGRAPH-NAME                     
152500*            MOVE 'INSERT ERROR ON TSKSTMTH      '                        
152600*                                TO DL-ERR-MSG                            
152700*            MOVE SK-ITEM-NUMBER TO PE-ITEM-NUMBER                        
152800*            MOVE PV-STORENO     TO PE-STORENO                            
152900*            MOVE SK-MTHLY-ACCTG-PERIOD TO PE-BOW-DTE                     
153000*            MOVE PV-DB2-SALES-TYPE     TO PE-SALE-TYPE                   
153100*            MOVE SKSTMTH-SALES-UNIT-QTY TO PE-SALE-QTY                   
153200*            MOVE SKSTMTH-SALES-AMT      TO PE-SALE-AMT                   
153300*            MOVE PE-ERROR-MSG-L1 TO PE-ERROR-DB2-PARMS-L1                
153400*            MOVE PE-ERROR-MSG-L2 TO PE-ERROR-DB2-PARMS-L2                
153500*            PERFORM 9500-ERROR-ROUTINE                                   
153600*        ELSE                                                             
153700*            MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                         
153800*        END-IF                                                           
153900*    END-IF.                                                              
154000*                                                                         
154100*                                                                         
154200******************************************************************        
154300*  SQL UPDATE FOR TSKSTMTH                                       *        
154400******************************************************************        
154500*6200-UPDATE-SKU-STORE-MTHLY.                                             
154600*                                                                         
154700*    IF PV-STORENO = PC-000-STORENO                                       
154800*        MOVE PA-STORE-000-MTH-QTY (PV-STORE-000-SUB)                     
154900*                                TO PV-MTHSL-SALES-UNIT-QTY               
155000*        MOVE PA-STORE-000-MTH-AMT (PV-STORE-000-SUB)                     
155100*                                TO PV-MTHSL-SALES-AMT                    
155200*    ELSE                                                                 
155300*        MOVE PV-TOTAL-SLD-QTY   TO PV-MTHSL-SALES-UNIT-QTY               
155400*        MOVE PV-TOTAL-CHRGD-AMT TO PV-MTHSL-SALES-AMT                    
155500*    END-IF.                                                              
155600*                                                                         
155700*    EXEC SQL                                                             
155800*        UPDATE TSKSTMTH                                                  
155900*            SET SALES_UNIT_QTY = SALES_UNIT_QTY +                        
156000*                                    :PV-MTHSL-SALES-UNIT-QTY,            
156100*                SALES_AMT      = SALES_AMT +                             
156200*                                    :PV-MTHSL-SALES-AMT                  
156300*            WHERE ITEM_NMBR       = :SK-ITEM-NUMBER                      
156400*              AND STORE_NMBR      = :PV-STORENO                          
156500*              AND FSCL_YR_MN_NBR  = :SK-MTHLY-ACCTG-PERIOD-X             
156600*              AND SALES_TYPE_CODE = :PV-DB2-SALES-TYPE                   
156700*    END-EXEC.                                                            
156800*    IF SQLCODE = ZEROES                                                  
156900*        ADD 1 TO PA-DB2-TSKSTMTH-UPD                                     
157000*        MOVE 'Y' TO PI-DB2-CALL-COMPLETE-IND                             
157100*    ELSE                                                                 
157200*        MOVE '6200-UPDATE-SKU-STORE-MTHLY   '                            
157300*                            TO PV-PARAGRAPH-NAME                         
157400*        MOVE 'UPDATE ERROR ON TSKSTMTH      '                            
157500*                            TO DL-ERR-MSG                                
157600*        MOVE SK-ITEM-NUMBER TO PE-ITEM-NUMBER                            
157700*        MOVE PV-STORENO TO PE-STORENO                                    
157800*        MOVE SK-MTHLY-ACCTG-PERIOD TO PE-BOW-DTE                         
157900*        MOVE PV-DB2-SALES-TYPE TO PE-SALE-TYPE                           
158000*        MOVE PV-MTHSL-SALES-UNIT-QTY TO PE-SALE-QTY                      
158100*        MOVE PV-MTHSL-SALES-AMT TO PE-SALE-AMT                           
158200*        MOVE PE-ERROR-MSG-L1 TO PE-ERROR-DB2-PARMS-L1                    
158300*        MOVE PE-ERROR-MSG-L2 TO PE-ERROR-DB2-PARMS-L2                    
158400*        PERFORM 9500-ERROR-ROUTINE                                       
158500*    END-IF.                                                              
158600*                                                                         
158700*                                                                         
158800******************************************************************        
158900*  FETCH A ROW FROM THE SALES CURSOR                                      
159000******************************************************************        
159100 7000-FETCH-SALES-CSR.                                                    
159101*                                                                         
159102     EXEC SQL                                                             
159103         FETCH SALES_CSR                                                  
159104          INTO :FST00005-ITM-NBR,                                         
159105               :FST00005-LOC-NBR,                                         
159106               :FST00005-FS-SLS-TYP-CDE,                                  
159107               :FST00005-ACCTG-PERD-YR,                                   
159108               :FST00005-ACCTG-PERD-MN,                                   
159109               :FST00005-ACCTG-BOW-DTE,                                   
159110               :PV-TOTAL-SLD-QTY:PV-NULL-IND-SLD,                         
159111               :PV-TOTAL-CHRGD-AMT:PV-NULL-IND-CHRGD                      
159112     END-EXEC                                                             
159113     EVALUATE TRUE                                                        
159114         WHEN SQLCODE = ZERO                                              
159115             IF (PV-NULL-IND-SLD = -1)                                    
159116                MOVE ZERO TO PV-TOTAL-SLD-QTY                             
159117             END-IF                                                       
159118             IF (PV-NULL-IND-CHRGD = -1)                                  
159119                MOVE ZERO TO PV-TOTAL-CHRGD-AMT                           
159120             END-IF                                                       
159121                                                                          
159122         WHEN SQLCODE = +100                                              
159123             SET EOF-SALES-CURSOR TO TRUE                                 
159124         WHEN SQLWARN0 NOT = SPACES                                       
159125         WHEN SQLCODE  NOT = ZERO                                         
159126             MOVE '7000-FETCH-SALES-CSR          '                        
159127                                 TO PV-PARAGRAPH-NAME                     
159128             MOVE 'FETCH ERROR ON SALES_CSR      '                        
159129                                 TO DL-ERR-MSG                            
159130             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
159140                            PE-ERROR-DB2-PARMS-L2                         
159150             PERFORM 9500-ERROR-ROUTINE                                   
159160     END-EVALUATE                                                         
159170*                                                                         
159180     IF NOT EOF-SALES-CURSOR                                              
159190        PERFORM 7100-PREPARE-INPUT-RECORD                                 
159200     END-IF.                                                              
159300                                                                          
159400*                                                                         
159500******************************************************************        
159600*  PREPARATION OF FIELDS BASED ON ROW JUST READ                  *        
159700*     1.  INITIALIZE WS FIELDS & INCREMENT COUNTERS              *        
159800*     2.  MOVE KEYS TO WORK-AREA FOR LATER COMPARISON            *        
159900******************************************************************        
160000 7100-PREPARE-INPUT-RECORD.                                               
160100*                                                                         
160200     INITIALIZE DCLTSKSTWKS.                                              
160300*               DCLTSKSTMTH                                               
160400     ADD 1 TO PA-ST-INPUT.                                                
160500     ADD PV-TOTAL-SLD-QTY   TO PA-INPUT-QTY.                              
160600     ADD PV-TOTAL-CHRGD-AMT TO PA-INPUT-AMT.                              
160700*                                                                         
160800     MOVE FST00005-ITM-NBR        TO WK-ITEM-NUMBER.                      
160900     MOVE FST00005-FS-SLS-TYP-CDE TO WK-SALES-TYPE.                       
161000     MOVE FST00005-ACCTG-BOW-DTE  TO WK-WKLY-FSCL-BOW-DTE.                
161100     MOVE FST00005-ACCTG-PERD-YR  TO WK-MTHLY-PERD-YR.                    
161200     MOVE FST00005-ACCTG-PERD-MN  TO WK-MTHLY-PERD-MN.                    
161300                                                                          
211000*****************************************************************         
211100*                                                                         
211200*  READ CONTROL CARD CONTAINING PROCESSING DATE                           
211600*                                                                         
211700*****************************************************************         
211800 7500-READ-CONTROL-FILE.                                                  
211900*                                                                         
212000     READ CONTROL-FILE INTO CONTROL-DATA-IN                               
212100          AT END SET EOF-CONTROL-FILE TO TRUE.                            
212200*                                                                         
212300     IF EOF-CONTROL-FILE                                                  
221500        DISPLAY '** ABEND **'                                             
221600        DISPLAY '** PROGRAM    = ' PC-PROGRAM-NAME                        
221700        DISPLAY '** PARAGRAPH  = 7500-READ-CONTROL-FILE'                  
221800        DISPLAY '** MESSAGE    = CONTROL FILE DATE NOT FOUND'             
222000        MOVE +4003      TO ABEND-CODE                                     
222100        CALL 'ILBOABN0' USING ABEND-CODE                                  
222200     END-IF.                                                              
222300                                                                          
222400*                                                                         
222500******************************************************************        
222600*     OPEN CURSOR                                                *        
222700******************************************************************        
222800 7600-OPEN-SALES-CURSOR.                                                  
222801*                                                                         
222802*                                                                         
222803     EXEC SQL                                                             
222804         OPEN SALES_CSR                                                   
222805     END-EXEC                                                             
222806                                                                          
222807     EVALUATE TRUE                                                        
222808         WHEN SQLCODE = ZERO                                              
222809         WHEN SQLCODE = +100                                              
222810             CONTINUE                                                     
222820         WHEN OTHER                                                       
222830             MOVE '7600-OPEN-SALES-CURSOR        '                        
222840                                 TO PV-PARAGRAPH-NAME                     
222850             MOVE 'OPEN ERROR ON SALES_CSR       '                        
222860                                 TO DL-ERR-MSG                            
222870             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
222880                            PE-ERROR-DB2-PARMS-L2                         
222881             PERFORM 9500-ERROR-ROUTINE                                   
222882     END-EVALUATE.                                                        
222883*                                                                         
222884******************************************************************        
222885*     CLOSE CURSOR                                               *        
222886******************************************************************        
222887 7700-CLOSE-SALES-CURSOR.                                                 
222888*                                                                         
222889     EXEC SQL                                                             
222890         CLOSE SALES_CSR                                                  
222891     END-EXEC                                                             
222892                                                                          
222893     EVALUATE TRUE                                                        
222894         WHEN SQLCODE = ZERO                                              
222895             CONTINUE                                                     
222896         WHEN OTHER                                                       
222897             MOVE '7700-CLOSE-SALES-CURSOR       '                        
222898                                 TO PV-PARAGRAPH-NAME                     
222899             MOVE 'CLOSE ERROR ON SALES_CSR      '                        
222900                                 TO DL-ERR-MSG                            
223000             MOVE SPACES TO PE-ERROR-DB2-PARMS-L1                         
223100                            PE-ERROR-DB2-PARMS-L2                         
223101             PERFORM 9500-ERROR-ROUTINE                                   
223102       END-EVALUATE.                                                      
223103*                                                                         
223104******************************************************************        
223105*  ==> PROCESSES:                                                *        
223106*    - SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE  *        
223107*      IF WE ARE IN A RESTART CONDITION.                         *        
223108******************************************************************        
223109 8100-INIT-CHECKPOINT-SELECT.                                             
223110                                                                          
223120     EXEC SQL                                                             
223130       SELECT                                                             
223140            CKPT_STAT_CODE                                                
223150         INTO                                                             
223160           :TCKRSTCNTL-CKPT-STAT-CODE                                     
223170         FROM  TCKRSTCNTL                                                 
223180        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
223190          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
223200     END-EXEC.                                                            
223300                                                                          
223400     EVALUATE TRUE                                                        
223500         WHEN SQLCODE = ZERO                                              
223600             CONTINUE                                                     
223700         WHEN OTHER                                                       
223800             MOVE '8100-INIT-CHECKPOINT-SELECT '                          
223900                             TO PV-PARAGRAPH-NAME                         
224000             MOVE 'ERROR ON SELECT OF TCKRSTCNTL '                        
224100                             TO DL-ERR-MSG                                
224200             PERFORM 9500-ERROR-ROUTINE                                   
224300     END-EVALUATE.                                                        
224400                                                                          
224500******************************************************************        
224600*  ==> PROCESSES:                                                *        
224700*    - SELECTS FROM THE CHECKPOINT RESTART INFORMATION TABLE TO  *        
224800*      OBTAIN THE PROGRAM'S SAVED VARIABLES.                     *        
224900******************************************************************        
225000 8200-SELECT-RESTART-INFO.                                                
225100                                                                          
225200     EXEC SQL                                                             
225300         SELECT                                                           
225400            WORK_STRG_DESC                                                
225500         INTO                                                             
225600           :TCKRSTINF-WORK-STRG-DESC                                      
225700         FROM  TCKRSTINF                                                  
225800        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
225900          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
226000     END-EXEC.                                                            
226100                                                                          
226200     EVALUATE TRUE                                                        
226300         WHEN SQLCODE = ZERO                                              
226400             CONTINUE                                                     
226500         WHEN OTHER                                                       
226600             MOVE '8200-SELECT-RESTART-INFO '                             
226700                             TO PV-PARAGRAPH-NAME                         
226800             MOVE 'ERROR ON SELECT OF TCKRSTINF '                         
226900                             TO DL-ERR-MSG                                
227000             PERFORM 9500-ERROR-ROUTINE                                   
227100     END-EVALUATE.                                                        
227200                                                                          
227300     EJECT                                                                
227400******************************************************************        
227500*  ==> PROCESSES:                                                *        
227600*    - CHECK IF WE NEED TO TAKE A COMMIT.                        *        
227700*    - COMMIT THIS UNIT OF WORK.                                 *        
227800******************************************************************        
227900 8500-COMMIT-PROCESSING.                                                  
228000                                                                          
228100     PERFORM 8800-SET-CURRENT-TIMESTAMP.                                  
228200                                                                          
228300     IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                        
228400         IF PS-FIRST-COMMIT                                               
228500             MOVE '9'        TO TCKRSTCNTL-CKPT-STAT-CODE                 
228600             PERFORM 8600-UPDATE-CHECKPOINT-STATUS                        
228700             MOVE 'N'        TO PS-FIRST-COMMIT-SW                        
228800         END-IF                                                           
228900         PERFORM 8900-COMMIT                                              
229000         PERFORM 8700-GET-COMMIT-TIMESTAMP                                
229100     END-IF.                                                              
229200                                                                          
229300******************************************************************        
229400*  ==> PROCESSES:                                                *        
229500*    - UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE        *        
229600******************************************************************        
229700 8600-UPDATE-CHECKPOINT-STATUS.                                           
229800                                                                          
229900     EXEC SQL                                                             
230000       UPDATE  TCKRSTCNTL                                                 
230100          SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
230200        WHERE  JOB_NAME  = :PC-JOB-NAME                                   
230300          AND  PLAN_NAME = :PC-PROGRAM-NAME                               
230400     END-EXEC.                                                            
230500                                                                          
230600     EVALUATE TRUE                                                        
230700         WHEN SQLCODE = ZERO                                              
230800             CONTINUE                                                     
230900         WHEN OTHER                                                       
231000             MOVE '8600-UPDATE-CHECKPOINT-STATUS '                        
231100                             TO PV-PARAGRAPH-NAME                         
231200             MOVE 'ERROR ON UPDATE OF TCKRSTCNTL '                        
231300                             TO DL-ERR-MSG                                
231400             PERFORM 9500-ERROR-ROUTINE                                   
231500     END-EVALUATE.                                                        
231600                                                                          
231700******************************************************************        
231800*  ==> PROCESSES:                                                *        
231900*    - GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT CONTROL      *        
232000*      TABLE.                                                    *        
232100******************************************************************        
232200 8700-GET-COMMIT-TIMESTAMP.                                               
232300                                                                          
232400     EXEC SQL                                                             
232500       SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)               
232600         INTO :PV-COMMIT-TIMESTAMP                                        
232700         FROM TCKRSTCNTL                                                  
232800        WHERE JOB_NAME  = :PC-JOB-NAME                                    
232900          AND PLAN_NAME = :PC-PROGRAM-NAME                                
233000     END-EXEC.                                                            
233100                                                                          
233200     IF SQLCODE = 0                                                       
233300         CONTINUE                                                         
233400     ELSE                                                                 
233500         MOVE '8700-GET-COMMIT-TIMESTAMP       '                          
233600                             TO PV-PARAGRAPH-NAME                         
233700         MOVE 'ERROR ON SELECT OF TIMESTAMP  '                            
233800                             TO DL-ERR-MSG                                
233900         PERFORM 9500-ERROR-ROUTINE                                       
234000     END-IF.                                                              
234100                                                                          
234200******************************************************************        
234300*  ==> PROCESSES:                                                *        
234400*    - GET THE CURRENT TIMESTAMP FROM DB2.                       *        
234500******************************************************************        
234600 8800-SET-CURRENT-TIMESTAMP.                                              
234700                                                                          
234800     EXEC SQL                                                             
234900          SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP                   
235000     END-EXEC.                                                            
235100                                                                          
235200     IF SQLCODE = 0                                                       
235300         CONTINUE                                                         
235400     ELSE                                                                 
235500         MOVE '8800-SET-CURRENT-TIMESTAMP      '                          
235600                             TO PV-PARAGRAPH-NAME                         
235700         MOVE 'ERROR ON SET OF TIMESTAMP     '                            
235800                             TO DL-ERR-MSG                                
235900         PERFORM 9500-ERROR-ROUTINE                                       
236000     END-IF.                                                              
236100                                                                          
236200******************************************************************        
236300*  ==> PROCESSES:                                                *        
236400*    - PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.           *        
236500*    - TAKE A COMMIT.                                            *        
236600******************************************************************        
236700 8900-COMMIT.                                                             
236800                                                                          
236900     MOVE CR-CHECKPOINT-RESTART-AREA TO TCKRSTINF-WORK-STRG-DESC.         
237000                                                                          
237100     EXEC SQL                                                             
237200       UPDATE TCKRSTINF                                                   
237300          SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
237400        WHERE JOB_NAME  = :PC-JOB-NAME                                    
237500          AND PLAN_NAME = :PC-PROGRAM-NAME                                
237600     END-EXEC.                                                            
237700                                                                          
237800     IF SQLCODE = 0                                                       
237900         CONTINUE                                                         
238000     ELSE                                                                 
238100         MOVE '8900-COMMIT                     '                          
238200                             TO PV-PARAGRAPH-NAME                         
238300         MOVE 'ERROR ON UPDATE OF TCKRSTINF  '                            
238400                             TO DL-ERR-MSG                                
238500         PERFORM 9500-ERROR-ROUTINE                                       
238600     END-IF.                                                              
238700                                                                          
238800     EXEC SQL                                                             
238900         COMMIT                                                           
239000     END-EXEC.                                                            
239100                                                                          
239200     IF SQLCODE = 0                                                       
239300         ADD 1  TO PA-COMMIT-CNT                                          
239400     ELSE                                                                 
239500         MOVE '8900-COMMIT                     '                          
239600                             TO PV-PARAGRAPH-NAME                         
239700         MOVE 'ERROR ON COMMIT               '                            
239800                             TO DL-ERR-MSG                                
239900         PERFORM 9500-ERROR-ROUTINE                                       
240000     END-IF.                                                              
240100                                                                          
240200*                                                                         
240300******************************************************************        
240400*     1.  CLOSE SALES CURSOR                                     *        
240500*     2.  UPDATE STATUS ON CHECKPOINT TABLE TO INDICATE          *        
240600*         SUCCESSFUL COMPLETION                                  *        
240700*     3.  DISPLAY CONTROL TOTALS                                 *        
240800******************************************************************        
240900 9000-EOF-ROUTINE.                                                        
241000*                                                                         
241100     PERFORM 7700-CLOSE-SALES-CURSOR.                                     
241200*                                                                         
241300     MOVE '0'  TO TCKRSTCNTL-CKPT-STAT-CODE.                              
241400     PERFORM 8600-UPDATE-CHECKPOINT-STATUS.                               
241500*                                                                         
241600     MOVE PA-ST-INPUT         TO PV-DIS-RECORDS-READ.                     
241700     MOVE PA-INPUT-QTY        TO PV-DIS-SALES-QTY.                        
241800     MOVE PA-INPUT-AMT        TO PV-DIS-SALES-AMT.                        
241900     MOVE PA-DB2-TSKSTWK-UPD  TO PV-DIS-TSKSTWK-UPD.                      
242000     MOVE PA-DB2-TSKSTWK-INS  TO PV-DIS-TSKSTWK-INS.                      
242100*    MOVE PA-DB2-TSKSTMTH-UPD TO PV-DIS-TSKSTMTH-UPD.                     
242200*    MOVE PA-DB2-TSKSTMTH-INS TO PV-DIS-TSKSTMTH-INS.                     
UNCMNT     MOVE PA-COMMIT-CNT       TO PV-DIS-COMMITS.                          
242400                                                                          
242500     DISPLAY '*************************************************'.         
242600     DISPLAY '*** FSKUP071 - CONTROL TOTALS                 ***'.         
242700     DISPLAY '*************************************************'.         
242800     DISPLAY 'TOTAL RECORDS READ  - ' PV-DIS-RECORDS-READ.                
242900     DISPLAY '*************************************************'.         
243000     DISPLAY 'TOTAL SALES UNITS   - ' PV-DIS-SALES-QTY.                   
243100     DISPLAY '*************************************************'.         
243200     DISPLAY 'TOTAL SALES DOLLARS - ' PV-DIS-SALES-AMT.                   
243300     DISPLAY '*************************************************'.         
243400     DISPLAY 'TSKSTWKS DB2 UPDATE - ' PV-DIS-TSKSTWK-UPD.                 
243500     DISPLAY '*************************************************'.         
24360      DISPLAY 'TSKSTWKS DB2 INSERT - ' PV-DIS-TSKSTWK-INS.                 
24370      DISPLAY '*************************************************'.         
243800*    DISPLAY 'TSKSTMTH DB2 UPDATE - ' PV-DIS-TSKSTMTH-UPD.                
243900*    DISPLAY '*************************************************'.         
244000*    DISPLAY 'TSKSTMTH DB2 INSERT - ' PV-DIS-TSKSTMTH-INS.                
244100*    DISPLAY '*************************************************'.         
244200     DISPLAY 'TOTAL DB2 COMMITS   - ' PV-DIS-COMMITS.                     
244300     DISPLAY '*************************************************'.         
244400     DISPLAY '*************************************************'.         
244500     DISPLAY '*** FSKUP071 SALES SUMMARY TABLE UPDT COMPLETE  *'.         
244600     DISPLAY '*************************************************'.         
244700                                                                          
244800*                                                                         
244900******************************************************************        
245000*     SQL EXIT WHEN SQL ERRORS ARE ENCOUNTERED                   *        
245100******************************************************************        
245200 9100-SQL-ERROR.                                                          
245300     MOVE SQLCODE     TO SQLCODE2.                                        
245400     MOVE SQLERRD(3)  TO SQLERRD3.                                        
245500     DISPLAY '** ABEND **    = SQL ERROR'.                                
245600     DISPLAY 'SQL ERROR MSG  = ' SQLERRMC.                                
245700     DISPLAY 'PROGRAM        = ' PC-PROGRAM-NAME.                         
245800     DISPLAY 'PARAGRAPH      = ' PV-PARAGRAPH-NAME.                       
245900     DISPLAY 'SQLCODE        = ' SQLCODE2.                                
246000     IF PE-ERROR-DB2-PARMS-L1 = SPACES                                    
246100        CONTINUE                                                          
246200     ELSE                                                                 
246300        DISPLAY 'PARMS IN DB2 CALL:'                                      
246400        DISPLAY PE-ERROR-DB2-PARMS-L1                                     
246500        IF PE-ERROR-DB2-PARMS-L2 = SPACES                                 
246600           CONTINUE                                                       
246700        ELSE                                                              
246800           DISPLAY PE-ERROR-DB2-PARMS-L2                                  
246900        END-IF                                                            
247000     END-IF.                                                              
247100     DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                                
247200     DISPLAY 'S/T  PROCESSED = ' PA-ST-INPUT.                             
247300*                                                                         
247400     IF SQLCODE = -911                                                    
247500         CONTINUE                                                         
247600     ELSE                                                                 
247700         EXEC SQL                                                         
247800             ROLLBACK                                                     
247900         END-EXEC                                                         
248000     END-IF.                                                              
248100                                                                          
248200     MOVE PC-SQL-ERROR TO ABEND-CODE.                                     
248300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
248400*                                                                         
248500 9200-SQL-WARNING.                                                        
248600******************************************************************        
248700*     SQL EXIT WHEN SQL WARNINGS ARE ENCOUNTERED                 *        
248800******************************************************************        
248900     MOVE SQLCODE     TO SQLCODE2.                                        
249000     MOVE SQLERRD(3)  TO SQLERRD3.                                        
249100     DISPLAY 'SQLCODE = ' SQLCODE2.                                       
249200     DISPLAY '** ABEND **    = SQL WARNING'.                              
249300     DISPLAY 'PROGRAM        = ' PC-PROGRAM-NAME.                         
249400     DISPLAY 'PARAGRAPH      = ' PV-PARAGRAPH-NAME.                       
249500     DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                                
249600     IF PE-ERROR-DB2-PARMS-L1 = SPACES                                    
249700        CONTINUE                                                          
249800     ELSE                                                                 
249900        DISPLAY 'PARMS IN DB2 CALL:'                                      
250000        DISPLAY PE-ERROR-DB2-PARMS-L1                                     
250100        IF PE-ERROR-DB2-PARMS-L2 = SPACES                                 
250200           CONTINUE                                                       
250300        ELSE                                                              
250400           DISPLAY PE-ERROR-DB2-PARMS-L2                                  
250500        END-IF                                                            
250600     END-IF.                                                              
250700     DISPLAY 'S/T  PROCESSED = ' PA-ST-INPUT.                             
250800     DISPLAY '      SQLWARN0 = ' SQLWARN0.                                
250900     DISPLAY '      SQLWARN1 = ' SQLWARN1.                                
251000     DISPLAY '      SQLWARN2 = ' SQLWARN2.                                
251100     DISPLAY '      SQLWARN3 = ' SQLWARN3.                                
251200     DISPLAY '      SQLWARN4 = ' SQLWARN4.                                
251300     DISPLAY '      SQLWARN5 = ' SQLWARN5.                                
251400     DISPLAY '      SQLWARN6 = ' SQLWARN6.                                
251500     DISPLAY '      SQLWARN7 = ' SQLWARN7.                                
251600*                                                                         
251700     EXEC SQL                                                             
251800         ROLLBACK                                                         
251900     END-EXEC.                                                            
252000                                                                          
252100     MOVE PC-SQL-ERROR TO ABEND-CODE.                                     
252200     CALL 'ILBOABN0' USING ABEND-CODE.                                    
252300                                                                          
252400*                                                                         
252500******************************************************************        
252600*   AN ERROR LINE IS WRITTEN AND THE DB2 SQL ERROR HANDLING      *        
252700*   PARAGRAPH IS CALLED IN THIS MODULE.                          *        
252800******************************************************************        
252900 9500-ERROR-ROUTINE.                                                      
253000*                                                                         
253100     IF SQLCODE = PC-DB2-DEADLOCK                                         
253200         MOVE FST00005-ITM-NBR        TO DL-ERR-ITEM                      
253300         MOVE FST00005-LOC-NBR        TO DL-ERR-STORE                     
253400         MOVE FST00005-FS-SLS-TYP-CDE TO DL-ERR-TYPE                      
253500         MOVE FST00005-ACCTG-BOW-DTE  TO DL-ERR-BOW-DATE                  
253600         MOVE SQLCODE TO DL-ERR-SQLCODE                                   
253700         DISPLAY DL-ERROR-LINE                                            
253800         MOVE SPACES TO DL-ERR-MSG                                        
253900                        DL-ERR-KEY                                        
254000         PERFORM 9100-SQL-ERROR                                           
254100     ELSE                                                                 
254200         IF SQLCODE = PC-DB2-CONTENTION                                   
254300             ADD +1 TO PA-DB2-CONTENTION-CTR                              
254400             IF PA-DB2-CONTENTION-CTR >= PC-DB2-CONTENTION-LIMIT          
254500                 MOVE FST00005-ITM-NBR        TO DL-ERR-ITEM              
254600                 MOVE FST00005-LOC-NBR        TO DL-ERR-STORE             
254700                 MOVE FST00005-FS-SLS-TYP-CDE TO DL-ERR-TYPE              
254800                 MOVE FST00005-ACCTG-BOW-DTE  TO DL-ERR-BOW-DATE          
254900                 MOVE SQLCODE TO DL-ERR-SQLCODE                           
255000                 DISPLAY DL-ERROR-LINE                                    
255100                 MOVE SPACES TO DL-ERR-MSG                                
255200                                DL-ERR-KEY                                
255300                 PERFORM 9100-SQL-ERROR                                   
255400             END-IF                                                       
255500         ELSE                                                             
255600             MOVE FST00005-ITM-NBR        TO DL-ERR-ITEM                  
255700             MOVE FST00005-LOC-NBR        TO DL-ERR-STORE                 
255800             MOVE FST00005-FS-SLS-TYP-CDE TO DL-ERR-TYPE                  
255900             MOVE FST00005-ACCTG-BOW-DTE  TO DL-ERR-BOW-DATE              
256000             MOVE SQLCODE                 TO DL-ERR-SQLCODE               
256100             DISPLAY DL-ERROR-LINE                                        
256200             MOVE SPACES                  TO DL-ERR-MSG                   
256300                                             DL-ERR-KEY                   
256400             IF ((SQLCODE > ZERO AND SQLCODE NOT = +100)                  
256500               OR SQLWARN0 = 'W')                                         
256600                 PERFORM 9200-SQL-WARNING                                 
256700             ELSE                                                         
256800                 PERFORM 9100-SQL-ERROR                                   
256900             END-IF                                                       
257000     END-IF.                                                              
