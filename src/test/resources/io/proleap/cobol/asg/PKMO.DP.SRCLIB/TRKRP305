000100*************************                                                 
000200 IDENTIFICATION DIVISION.                                                 
000300*************************                                                 
000400                                                                          
000500 PROGRAM-ID.      TRKRP305.                                               
000600 AUTHOR.          ABRAHAM IVERSON.                                        
000700 DATE-WRITTEN.    02/20/02.                                               
      ******************************************************************        
      *                                                                         
      *                                                                         
002400******************************************************************        
002500                                                                          
002600 ENVIRONMENT DIVISION.                                                    
002700 CONFIGURATION SECTION.                                                   
002800                                                                          
002900 SOURCE-COMPUTER. IBM-3090.                                               
003000 OBJECT-COMPUTER. IBM-3090.                                               
003200 INPUT-OUTPUT SECTION.                                                    
003300 FILE-CONTROL.                                                            
003400                                                                          
003500     SELECT PO-DOWNLOAD-FILE                                              
003600         ASSIGN                      TO INP01.                            
003700     SELECT REPORT-FILE                                                   
003800         ASSIGN                      TO RPT01.                            
004700 EJECT                                                                    
                                                                                
004800***************                                                           
004900 DATA DIVISION.                                                           
005000***************                                                           
004100 FILE SECTION.                                                            
005200                                                                          
005300 FD  PO-DOWNLOAD-FILE                                                     
005400     RECORDING MODE F                                                     
005500     BLOCK CONTAINS 0 RECORDS                                             
005600     RECORD CONTAINS 300 CHARACTERS                                       
005700     LABEL RECORDS ARE STANDARD                                           
005800     DATA RECORD IS PO-DOWNLOAD-RECORD.                                   
005900 01  PO-DOWNLOAD-RECORD               PIC X(300).                         
005100                                                                          
005200 FD  REPORT-FILE                                                          
005300     RECORDING MODE F                                                     
005400     BLOCK CONTAINS 0 RECORDS                                             
005500     RECORD CONTAINS 132 CHARACTERS                                       
005600     LABEL RECORDS ARE STANDARD                                           
005700     DATA RECORD IS REPORT-RECORD.                                        
005800 01  REPORT-RECORD                    PIC  X(132).                        
008600                                                                          
004700 EJECT                                                                    
      *************************                                                 
008700 WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PGM-NAME                  PIC X(08)  VALUE                    
                                                       'TRKRP305'.              
           05  PC-MAX-RETRIES               PIC S9(04) COMP VALUE +3.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-CURRENT-TMST              PIC X(26)  VALUE SPACES.            
           05  PV-SAVE-ENT-ID               PIC X(10)  VALUE SPACES.            
           05  PV-SAVE-DC-NBR               PIC 9(04)  VALUE ZEROES.            
           05  PV-CHANGE-COUNT              PIC 9(05)  VALUE ZEROES.            
           05  PV-ADD-COUNT                 PIC 9(05)  VALUE ZEROES.            
           05  PV-LINE-COUNT                PIC 9(03)  VALUE ZEROES.            
           05  PV-PAGE-COUNT                PIC 9(03)  VALUE ZEROES.            
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-SW           PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-INPUT                     VALUE 'Y'.               
                                                                                
       01  RP-REPROT-HEADER-LINE.                                               
           05  FILLER                       PIC X(54)  VALUE SPACES.            
           05  FILLER                       PIC X(26)  VALUE                    
               'LEGACY PO DOWNLOAD FOR TMS'.                                    
                                                                                
       01  RP-VENDOR-LINE.                                                      
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(08)  VALUE                    
               'VENDOR:'.                                                       
           05  RP-VENDOR-NAME               PIC X(30)  VALUE SPACES.            
           05  FILLER                       PIC X(09)  VALUE                    
               '  ENT ID'.                                                      
           05  RP-ENT-ID                    PIC ZZZZZZZZZZ VALUE ZERO.          
           05  FILLER                       PIC X(30)  VALUE SPACES.            
           05  FILLER                       PIC X(15)  VALUE                    
               'LOAD TIMESTAMP '.                                               
           05  RP-LOAD-TMST                 PIC X(30)  VALUE SPACES.            
                                                                                
       01  RP-VENDOR-TOTALS-LINE-1.                                             
           05  FILLER                       PIC X(116) VALUE SPACES.            
           05  FILLER                       PIC X(12)  VALUE                    
               '=====  ====='.                                                  
                                                                                
       01  RP-VENDOR-TOTALS-LINE-2.                                             
           05  FILLER                       PIC X(89)  VALUE SPACES.            
           05  FILLER                       PIC X(27)  VALUE                    
               'TOTAL CHANGES AND ADDS ='.                                      
           05  RP-CHANGES                   PIC ZZZZ9  VALUE ZEROES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  RP-ADDS                      PIC ZZZZ9  VALUE ZEROES.            
                                                                                
       01  RP-PO-HEADER-LINE-1.                                                 
           05  FILLER                       PIC X(28)  VALUE SPACES.            
           05  FILLER                       PIC X(38)  VALUE                    
               'STAT START SHIP   CANCEL    NEW  STR'.                          
           05  FILLER                       PIC X(43)  VALUE                    
               'SEASONAL  DC  STAT  QTY  DTE NW STR SEASET'.                    
           05  FILLER                       PIC X(20)  VALUE                    
               'ALLOC CHANGE INIT PO'.                                          
                                                                                
       01  RP-PO-HEADER-LINE-2.                                                 
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(54)  VALUE                    
               'DC   PO NUMBER  ITEM QTY   CDE    DATE       DATE'.             
           05  FILLER                       PIC X(40)  VALUE                    
               'STR ALLOC   SET    DROP CHGD CHGD CHGD'.                        
           05  FILLER                       PIC X(32)  VALUE                    
               'CHGD   CHGD   CHGD  MADE    LOAD'.                              
                                                                                
       01  RP-PO-HEADER-LINE-3.                                                 
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(55)  VALUE                    
               '---- ---------- ---------- ---- ---------- ----------'.         
           05  FILLER                       PIC X(39)  VALUE                    
               '--- ----- -------- ---- ---- ---- ----'.                        
           05  FILLER                       PIC X(34)  VALUE                    
               '------ ------ ----- ------ -------'.                            
                                                                                
       01  RP-PO-DETAIL-LINE.                                                   
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  RP-DC-NBR                    PIC ZZZZ   VALUE ZERO.              
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  RP-PO-NBR                    PIC ZZZZZZZZZZ VALUE ZERO.          
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  RP-ITEM-QTY                  PIC ZZZZZZZZZZ VALUE ZERO.          
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  RP-STAT-CDE                  PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  RP-START-SHIP-DATE           PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(01)  VALUE SPACES.            
           05  RP-CANCEL-DATE               PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(03)  VALUE SPACES.            
           05  RP-NW-STR                    PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(04)  VALUE SPACES.            
           05  RP-STR-ALLOC                 PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(06)  VALUE SPACES.            
           05  RP-SEASONAL-SET              PIC X(05)  VALUE SPACES.            
           05  FILLER                       PIC X(03)  VALUE SPACES.            
           05  RP-DC-DROP                   PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(04)  VALUE SPACES.            
           05  RP-STAT-CHGD                 PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(04)  VALUE SPACES.            
           05  RP-QTY-CHGD                  PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(04)  VALUE SPACES.            
           05  RP-DTE-CHDG-IND              PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(05)  VALUE SPACES.            
           05  RP-NW-STR-CHGD-IND           PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(06)  VALUE SPACES.            
           05  RP-SEASET-CHGD-IND           PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(05)  VALUE SPACES.            
           05  RP-STR-ALLOC-CHDG-IND        PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(06)  VALUE SPACES.            
           05  RP-CHANGE-MADE-IND           PIC X(01)  VALUE SPACES.            
           05  FILLER                       PIC X(06)  VALUE SPACES.            
           05  RP-INIT-LOD-IND              PIC X(01)  VALUE SPACES.            
                                                                                
      *                                                                         
102100*  PO HEADER AND VENDOR/DC DETAIL FILE LAYOUT                             
102200*                                                                         
102300     COPY TRRD302.                                                        
009400*                                                                         
009500*  STANDARD HEADER LINE - KOHLS 132 COL                                   
009600*                                                                         
009700     COPY DPWS132O.                                                       
117600*                                                                         
117700*  DB2 COMMUNICATION AREA.                                                
117800*                                                                         
117900     EXEC SQL                                                             
118000         INCLUDE SQLCA                                                    
118100     END-EXEC.                                                            
                                                                                
119400*****************************************************************         
119500 PROCEDURE DIVISION.                                                      
119600*****************************************************************         
119700                                                                          
071900*--------------------------------------------------------------*          
072000* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE  *          
072100* PROGRAM.                                                     *          
072200*--------------------------------------------------------------*          
072400 0000-PROGRAM-MAINLINE.                                                   
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 2000-PROCESS-RECORDS                                         
              UNTIL PS-END-OF-INPUT.                                            
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  PO-DOWNLOAD-FILE                                         
                OUTPUT REPORT-FILE.                                             
                                                                                
      * DO INITIAL READ                                                         
           PERFORM 8000-READ-INPUT-FILE.                                        
                                                                                
      *----------------------------------------------------------------*        
      * PROCESS RECORDS                                                         
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       2000-PROCESS-RECORDS.                                                    
                                                                                
      * IF VENDOR CHANGES OR FIRST VENDOR WRITE REPORT HEADERS                  
           IF TR302-ORIGIN-ENT-ID = PV-SAVE-ENT-ID                              
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7000-WRITE-REPORT-VENDOR                                 
           END-IF.                                                              
                                                                                
      * WRITE PO DETAIL LINE                                                    
           PERFORM 7200-WRITE-REPORT-PO.                                        
                                                                                
      * READ NEXT LINE OF INPUT FILE                                            
           PERFORM 8000-READ-INPUT-FILE.                                        
                                                                                
      * IF VENDOR CHANGES OR LAST VENDOR WRITE REPORT FOOTER                    
           IF TR302-ORIGIN-ENT-ID = PV-SAVE-ENT-ID                              
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7100-WRITE-PREV-VENDOR-TOTALS                            
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      * WRITE ERROR TO REPORT                                                   
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       3000-EOJ-PROCESSING.                                                     
                                                                                
           CLOSE  PO-DOWNLOAD-FILE                                              
                  REPORT-FILE.                                                  
                                                                                
      *----------------------------------------------------------------*        
      * WRITE ERROR TO REPORT                                                   
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       6000-PROCESS-HEADING-LINES.                                              
                                                                                
           ADD  +1                     TO PV-PAGE-COUNT.                        
           MOVE PV-PAGE-COUNT          TO DP132O-PAGE-NUMBER.                   
                                                                                
           MOVE PC-PGM-NAME            TO DP132O-PROGRAM-NAME.                  
           MOVE 1                      TO DP132O-REPORT-NUMBER.                 
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST =  CURRENT TIMESTAMP                        
           END-EXEC.                                                            
                                                                                
           MOVE PV-CURRENT-TMST(6:2)      TO DP132O-RUN-MONTH.                  
           MOVE PV-CURRENT-TMST(9:2)      TO DP132O-RUN-DAY.                    
           MOVE PV-CURRENT-TMST(3:2)      TO DP132O-RUN-YEAR.                   
           MOVE PV-CURRENT-TMST(12:2)     TO DP132O-RUN-HOUR.                   
           MOVE PV-CURRENT-TMST(15:2)     TO DP132O-RUN-MINUTE.                 
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM RP-REPROT-HEADER-LINE                                       
               AFTER ADVANCING 1 LINE.                                          
                                                                                
      *----------------------------------------------------------------*        
      * WRITE ERROR TO REPORT                                                   
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       7000-WRITE-REPORT-VENDOR.                                                
                                                                                
           PERFORM 6000-PROCESS-HEADING-LINES.                                  
                                                                                
           MOVE TR302-ORIGIN-ENT-ID   TO PV-SAVE-ENT-ID                         
                                         RP-ENT-ID.                             
           MOVE TR302-ENT-NAME        TO RP-VENDOR-NAME.                        
           MOVE TR302-LOAD-TIMESTAMP  TO RP-LOAD-TMST.                          
           MOVE TR302-DC-NBR          TO PV-SAVE-DC-NBR.                        
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM RP-VENDOR-LINE                                              
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM RP-PO-HEADER-LINE-1                                         
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM RP-PO-HEADER-LINE-2                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-RECORD                                                  
               FROM RP-PO-HEADER-LINE-3                                         
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE +7 TO PV-LINE-COUNT.                                            
                                                                                
      *----------------------------------------------------------------*        
      * WRITE ERROR TO REPORT                                                   
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       7100-WRITE-PREV-VENDOR-TOTALS.                                           
                                                                                
           MOVE PV-CHANGE-COUNT  TO RP-CHANGES.                                 
           MOVE PV-ADD-COUNT     TO RP-ADDS.                                    
                                                                                
           MOVE ZERO TO PV-CHANGE-COUNT                                         
                        PV-ADD-COUNT.                                           
                                                                                
           WRITE REPORT-RECORD                                                  
              FROM RP-VENDOR-TOTALS-LINE-1                                      
              AFTER ADVANCING 1 LINE.                                           
                                                                                
           WRITE REPORT-RECORD                                                  
              FROM RP-VENDOR-TOTALS-LINE-2                                      
              AFTER ADVANCING 1 LINE.                                           
                                                                                
      *----------------------------------------------------------------*        
      * WRITE ERROR TO REPORT                                                   
      *                                                                         
      * PERFORMED FROM                                                          
      *----------------------------------------------------------------*        
       7200-WRITE-REPORT-PO.                                                    
                                                                                
      * FILL IN FIELDS TO BE WRITEN TO THE REPORT                               
           MOVE TR302-DC-NBR                 TO RP-DC-NBR.                      
           MOVE TR302-LINE-NBR               TO RP-PO-NBR.                      
           MOVE TR302-REQ-ITEM-QTY           TO RP-ITEM-QTY.                    
           MOVE TR302-PO-STATUS-CDE          TO RP-STAT-CDE.                    
           MOVE TR302-REQ-EARLY-AVAIL-DTE    TO RP-START-SHIP-DATE.             
           MOVE TR302-REQ-LATE-AVAIL-DTE     TO RP-CANCEL-DATE.                 
           MOVE TR302-NEW-STORE-PO-IND       TO RP-NW-STR.                      
           MOVE TR302-STORE-PO-ALLOCATED-IND TO RP-STR-ALLOC.                   
           MOVE TR302-DC-QTY-CHG-IND         TO RP-QTY-CHGD.                    
           MOVE TR302-PO-STATUS-CHG-IND      TO RP-STAT-CHGD.                   
           MOVE TR302-DC-DROP-IND            TO RP-DC-DROP.                     
           MOVE TR302-SEASET-SHRT-NM         TO RP-SEASONAL-SET.                
           MOVE TR302-DATE-CHGD-IND          TO RP-DTE-CHDG-IND.                
           MOVE TR302-NW-STR-CHGD-IND        TO RP-NW-STR-CHGD-IND.             
           MOVE TR302-SEASET-CHGD-IND        TO RP-SEASET-CHGD-IND.             
           MOVE TR302-STR-ALLOC-CHGD-IND     TO RP-STR-ALLOC-CHDG-IND.          
           MOVE TR302-INIT-PO-LOD-IND        TO RP-INIT-LOD-IND.                
                                                                                
      * CHECK IF ANY FIELD WAS CHANGED FOR PO                                   
           IF TR302-DC-QTY-CHG-INCREASE                                         
           OR TR302-DC-QTY-CHG-DECREASE                                         
           OR TR302-PO-STATUS-CHG-YES                                           
           OR TR302-DC-DROP-IND-YES                                             
           OR TR302-DATE-CHGD-YES                                               
           OR TR302-NW-STR-CHGD-YES                                             
           OR TR302-SEASET-CHGD-YES                                             
           OR TR302-STR-ALLOC-CHGD-YES                                          
               MOVE 'Y' TO RP-CHANGE-MADE-IND                                   
               ADD +1 TO PV-CHANGE-COUNT                                        
           ELSE                                                                 
               MOVE 'N' TO RP-CHANGE-MADE-IND                                   
               IF TR302-INIT-PO-LOD-YES                                         
                   ADD +1 TO PV-ADD-COUNT                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
      * IF AT THE END OF A PAGE WRITE THE HEADER ON NEXT PAGE                   
           IF PV-LINE-COUNT >= 66                                               
               PERFORM 7000-WRITE-REPORT-VENDOR                                 
           END-IF.                                                              
                                                                                
      * WRITE DETAIL LINE                                                       
           IF PV-SAVE-DC-NBR = TR302-DC-NBR                                     
               WRITE REPORT-RECORD                                              
                   FROM RP-PO-DETAIL-LINE                                       
                   AFTER ADVANCING 1 LINE                                       
               ADD +1 TO PV-LINE-COUNT                                          
           ELSE                                                                 
               WRITE REPORT-RECORD                                              
                   FROM RP-PO-DETAIL-LINE                                       
                   AFTER ADVANCING 2 LINE                                       
               ADD +2 TO PV-LINE-COUNT                                          
               MOVE TR302-DC-NBR TO PV-SAVE-DC-NBR                              
           END-IF.                                                              
                                                                                
           MOVE SPACES TO RP-PO-DETAIL-LINE.                                    
                                                                                
      *----------------------------------------------------------------*        
      * READ DATA FILE                                                          
      *                                                                         
      * PERFORMED FROM                                                          
020640*----------------------------------------------------------------*        
031587 8000-READ-INPUT-FILE.                                                    
031588                                                                          
631400     READ PO-DOWNLOAD-FILE                                                
631500         INTO TR302-TMS-PO-HDR-DTL-RECORD                                 
631600         AT END                                                           
631700             SET  PS-END-OF-INPUT TO TRUE                                 
031700             MOVE SPACES TO PV-SAVE-ENT-ID.                               
