      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.             PMKUT122.                                        
       AUTHOR.                 AMANDA NEWHART                                   
       INSTALLATION.           KOHLS DEPARTMENT STORES.                         
       DATE-WRITTEN.           DECEMBER 2003.                                   
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                                                                *        
      *    PMKUT122 -                                                  *        
      *                                                                *        
      *    LOG ONTO ORACLE.  IF THIS FAILS ABEND OUT OF THE PROGRAM    *        
      *    OTHERWISE, OPEN THE COSA INPUT FILE.  NEXT READ THE FIRST   *        
      *    LINE ONLY FROM THE COSA FILE.  IF THERE IS NOTHING IN       *        
      *    THE FILE THEN ABEND.  IF THERE IS DATA THEN GRAB THE        *        
      *    TRANSACTION DATE FROM THE FIRST RECORD.  THEN RUN A SELECT  *        
      *    STATEMENT TO SEE IF THAT DATE IS ON EITHER THE              *        
      *    PMTT_PROMO_ITM_SLS_RSLT_LOD OR THE PMTT_SLS_RSLT_LOD TABLE. *        
      *    IF IT IS THEN ABEND THE PROGRAM AND DO NOT CONTINUE ON.  IF *        
      *    IT IS ON THEN CONTINUE ON LIKE NORMAL.                      *        
      *                                                                *        
      ******************************************************************        
      **   CHANGE LOG:                                                 *        
      ******************************************************************        
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SALES-RESULT-FILE                                             
               ASSIGN                       TO INP01.                           
                                                                                
070941      SELECT ORACLE-LOGON-FILE                                            
070941          ASSIGN                      TO ORALOGON.                        
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  SALES-RESULT-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
       01  SALES-RESULT-LINE                PIC  X(250).                        
                                                                                
070941 FD  ORACLE-LOGON-FILE                                                    
070941     RECORDING MODE IS F                                                  
070941     LABEL RECORDS ARE STANDARD                                           
070941     BLOCK CONTAINS 0 RECORDS                                             
070941     RECORD CONTAINS 80 CHARACTERS                                        
070941     DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
070941 01  OL-ORACLE-LOGON-RECORD           PIC X(80).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  ABEND-CODE                       PIC S9(04)   COMP VALUE +0.         
           88  ABEND-4001-WRITE-ERROR                    VALUE +4001.           
           88  ABEND-4002-READ-ERROR                     VALUE +4002.           
           88  ABEND-4003-CTRL-CARD-ERROR                VALUE +4003.           
           88  ABEND-4004-TABLE-ERROR                    VALUE +4004.           
           88  ABEND-4005-OPEN-ERROR                     VALUE +4005.           
           88  ABEND-4006-CLOSE-ERROR                    VALUE +4006.           
           88  ABEND-4007-SEQUENCE-ERROR                 VALUE +4007.           
           88  ABEND-4008-DATA-ERROR                     VALUE +4008.           
           88  ABEND-4009-KEY-DATA-ERROR                 VALUE +4009.           
           88  ABEND-4013-DB2-ERROR                      VALUE +4013.           
           88  ABEND-4014-ORACLE-LOGON-ERROR             VALUE +4014.           
           88  ABEND-4019-DATE-ROUTINE-ERROR             VALUE +4019.           
           88  ABEND-4444-FORCED-ABEND                   VALUE +4444.           
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
070941     05  PS-END-OF-LOGON-FILE-SW     PIC  X(01)  VALUE 'N'.               
070941         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
070941     05  PS-ORA-SRC-CTL-DONE-SW      PIC  X(01)  VALUE 'N'.               
070941         88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-SALES-RESULT-FILE-SW     PIC  X(01)  VALUE 'N'.               
               88  PS-SALES-RESULT-FILE-END            VALUE 'Y'.               
               88  PS-SALES-RESULT-FILE-START          VALUE 'N'.               
                                                                                
070941 01  CC-ORACLE-CONNECTION.                                                
070941     05  CC-ORACLE-USERID            PIC X(40).                           
070941         88 CC-AUTO-LOGON                        VALUE '/'.               
070941     05  CC-ORACLE-PASSWORD          PIC X(40).                           
                                                                                
       01  CC-PROGRAM-CONSTANTS.                                                
070941*    05  PC-CURRENT-PROGRAM-NAME      PIC  X(08)  VALUE                   
070941     05  PC-AUTO-LOGON                PIC  X(1)   VALUE '/'.              
070941     05  PC-PROGRAM-NAME              PIC  X(08)  VALUE                   
                                                           'PMKUT122'.          
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
070941     05  PV-ORACLE-USERID-LEN         PIC  S9(4)  VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PV-ORACLE-PASSWORD-LEN       PIC  S9(4)  VALUE ZEROS             
070941                                                  COMP SYNC.              
070941     05  PC-AUTO-LOGON                PIC  X(01)  VALUE '/'.              
           05  PV-SQL                       PIC  S9(04) VALUE ZEROS.            
           05  PV-TABLE-NAME                PIC  X(12)  VALUE SPACES.           
           05  PV-OPERATION-MSG             PIC  X(60)  VALUE SPACES.           
           05  PV-OPERATION-MSG-1           PIC  X(40)  VALUE SPACES.           
           05  PV-PARAGRAPH-NAME            PIC  X(30)  VALUE SPACES.           
           05  PV-TRN-SLS-DTE               PIC  X(10)  VALUE SPACES.           
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-READ-COUNTER              PIC S9(11)  COMP-3                  
                                                        VALUE ZEROES.           
                                                                                
      ******************************************************************        
      *    COPYBOOK FOR THE SALES RESULTS FILE FROM COSA                        
      ******************************************************************        
           COPY SAWS046.                                                        
                                                                                
      *****************************************************************         
      *                      O  R  A  C  L  E                                   
      *================================================================         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *****************************************************************         
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *---------------------------------------------------------------*         
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *---------------------------------------------------------------*         
       01      PC-ENT01-TYPE-CDE          PIC X(02)      VALUE '01'.            
       01      PV-ORACLE-ID               PIC X(1)       VALUE '/'.             
                                                                                
       01      USERNAME                   PIC X(10)      VARYING.               
       01      PASSWD                     PIC X(10)      VARYING.               
                                                                                
       01      ORACLE-RETURN              PIC X(200)     VALUE SPACES.          
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
      ********************                                                      
       EJECT                                                                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *****************************************************************         
      * A100-MAINLINE.                                                *         
      * ==> PROCESSES:                                                *         
      *  - SET UP ORACLE                                              *         
      *  - OPEN INPUT FILE                                            *         
      *  - READ THE SALES RESULT FILE FROM COSA UNTIL THE END OF      *         
      *     THE FILE                                                  *         
      *  - CLOSE FILES                                                *         
      * ==> PERFORMED FROM: START                                     *         
      *****************************************************************         
       A100-MAINLINE.                                                           
                                                                                
           SET PS-SALES-RESULT-FILE-START TO TRUE.                              
070941*    PERFORM PM700-A000-ORACLE-SETUP.                                     
           PERFORM B100-OPEN-FILES.                                             
           PERFORM B300-READ-SALES-RESULT-FILE.                                 
           PERFORM B900-CLOSE-FILES.                                            
                                                                                
           DISPLAY ' **** END OF PMKUT122 **** '.                               
                                                                                
           STOP RUN.                                                            
                                                                                
      *****************************************************************         
      * B100-OPEN-FILES.                                              *         
      * ==> PROCESSES:                                                *         
      *  - OPEN THE INPUT FILE THAT WAS RECIEVED FROM COSA            *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B100-OPEN-FILES.                                                         
                                                                                
070941     DISPLAY 'ORACLE FILE OPEN'.                                          
070941     OPEN INPUT ORACLE-LOGON-FILE.                                        
070941                                                                          
070941     DISPLAY 'ORACLE FILE READ'.                                          
070941     PERFORM 7000-READ-ORACLE-LOGON.                                      
070941                                                                          
070941     DISPLAY 'ORACLE FILE CLOSE'.                                         
070941     CLOSE ORACLE-LOGON-FILE.                                             
070941                                                                          
070941     IF ((PS-END-OF-LOGON-FILE)                                           
070941             OR (CC-ORACLE-CONNECTION = SPACES))                          
070941         DISPLAY '** ABEND **'                                            
070941         DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
070941                   'NOT PROVIDED'                                         
070941         SET PS-ORA-SRC-EMPTY TO TRUE                                     
070941         CALL 'ILBOABN0' USING ABEND-CODE                                 
070941     END-IF.                                                              
070941                                                                          
070941     IF CC-AUTO-LOGON                                                     
070941         DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
070941         EXEC SQL                                                         
070941              CONNECT :USERNAME                                           
070941         END-EXEC                                                         
070941     ELSE                                                                 
070941         INITIALIZE PV-ORACLE-USERID-LEN                                  
070941                    PV-ORACLE-PASSWORD-LEN                                
070941         INSPECT CC-ORACLE-USERID                                         
070941             TALLYING PV-ORACLE-USERID-LEN                                
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      INSPECT CC-ORACLE-PASSWORD                                          
070941          TALLYING PV-ORACLE-PASSWORD-LEN                                 
070941          FOR CHARACTERS BEFORE INITIAL SPACE                             
070941      MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
070941                USERNAME-ARR                                              
070941      MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
070941      MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
070941                PASSWD-ARR                                                
070941      MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
070941      DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
070941                USERNAME-ARR                                              
070941      EXEC SQL                                                            
070941        CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
070941      END-EXEC                                                            
070941      INITIALIZE CC-ORACLE-PASSWORD                                       
070941                 PASSWD                                                   
070941      END-IF.                                                             
070941                                                                          
070941     IF (SQLCODE NOT = 0)                                                 
070941         DISPLAY PV-PARAGRAPH-NAME                                        
070941         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
070941         PERFORM 9900-ORACLE-ABEND                                        
070941     END-IF.                                                              
                                                                                
           OPEN INPUT  SALES-RESULT-FILE.                                       
                                                                                
      *****************************************************************         
      * B300-READ-SALES-RESULT-FILE                                   *         
      * ==> PROCESSES:                                                *         
      *  - COMMIT LOGIC.                                              *         
      *  - READ IN THE SALES RESULT FILE.                             *         
      *  - ADD +1 TO RECORD COUNTER.                                  *         
      *  - IF WE HAVE NOT HIT THE END OF THE FILE THEN PROCESS THE    *         
      *    ROW THAT WAS READ.                                         *         
      *****************************************************************         
       B300-READ-SALES-RESULT-FILE.                                             
                                                                                
           READ SALES-RESULT-FILE INTO SA046-PM-SALES-RESULTS-REC               
              AT END SET PS-SALES-RESULT-FILE-END TO TRUE.                      
                                                                                
           DISPLAY 'SA046-SLS-DTE = ' SA046-SLS-DTE.                            
           IF PS-SALES-RESULT-FILE-START                                        
               PERFORM C100-CHECK-PROMO-ITM                                     
               PERFORM C110-CHECK-SLS-RSLT                                      
           ELSE                                                                 
               MOVE 'B300-READ-SALES-RESULT-FILE'  TO PV-PARAGRAPH-NAME         
               MOVE 'SALES FILE WAS EMPTY '        TO PV-OPERATION-MSG          
               PERFORM Z910-ABEND                                               
           END-IF.                                                              
                                                                                
      *****************************************************************         
      * B900-CLOSE-FILES                                              *         
      * ==> PROCESSES:                                                *         
      *  - CLOSE THE SALES RESULT FILE RECIEVED FROM COSA             *         
      * ==> PERFORMED FROM: A100-MAINLINE                             *         
      *****************************************************************         
       B900-CLOSE-FILES.                                                        
                                                                                
           CLOSE  SALES-RESULT-FILE.                                            
                                                                                
      *****************************************************************         
      * C100-CHECK-PROMO-ITM                                          *         
      * ==> PROCESSES:                                                *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      *  - 1403 = A ROW WAS NOT FOUND.                                *         
      *  - 2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C100-CHECK-PROMO-ITM.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT TRN_SLS_DTE                                               
               INTO   :PV-TRN-SLS-DTE                                           
               FROM   PMTT_PROMO_ITM_SLS_RSLT_LOD                               
               WHERE  TRN_SLS_DTE = TO_DATE(:SA046-SLS-DTE,'YYYY-MM-DD')        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
             WHEN SQLCODE = 2112                                                
               DISPLAY ' '                                                      
               DISPLAY '****FATAL ERROR: SLS-TRN-DTE, ' PV-TRN-SLS-DTE          
               ', IS ALREADY LOADED FOR FOR PMTT_PROMO_ITM_SLS_RSLT_LOD'        
               DISPLAY '****SALES WILL NOT BE LOADED - PROMO ON-CALL '          
                'NEEDS TO CONTACT COSA ON-CALL.'                                
               MOVE SQLCODE                        TO PV-SQL                    
               MOVE 'PMTT_PROMO_ITM_SLS_RSLT_LOD'  TO PV-TABLE-NAME             
               MOVE 'C100-CHECK-PROMO-ITM'         TO PV-PARAGRAPH-NAME         
               MOVE 'NO SALES WILL BE LOADED'      TO PV-OPERATION-MSG          
               PERFORM Z910-ABEND                                               
                                                                                
             WHEN SQLCODE = 1403                                                
               CONTINUE                                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                        TO PV-SQL                    
               MOVE 'PMTT_PROMO_ITM_SLS_RSLT_LOD'  TO PV-TABLE-NAME             
               MOVE 'C100-CHECK-PROMO-ITM'         TO PV-PARAGRAPH-NAME         
               MOVE 'ERROR ON SELECT FROM TABLE '  TO PV-OPERATION-MSG          
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *****************************************************************         
      * C110-CHECK-SLS-RSLT                                           *         
      * ==> PROCESSES:                                                *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = A ROW WAS FOUND.                                       *         
      *  - 1403 = A ROW WAS NOT FOUND.                                *         
      *  - 2112 OR -2112 = MORE THEN ONE ROW WAS FOUND.               *         
      * ==> PERFORMED FROM: B300-PROCESS-FILE                         *         
      *****************************************************************         
       C110-CHECK-SLS-RSLT.                                                     
                                                                                
           EXEC SQL                                                             
               SELECT TRN_SLS_DTE                                               
               INTO   :PV-TRN-SLS-DTE                                           
               FROM   PMTT_SLS_RSLT_LOD                                         
               WHERE  TRN_SLS_DTE = TO_DATE(:SA046-SLS-DTE,'YYYY-MM-DD')        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = -2112                                               
             WHEN SQLCODE = 2112                                                
               DISPLAY ' '                                                      
               DISPLAY '****FATAL ERROR: SLS-TRN-DTE, ' PV-TRN-SLS-DTE          
               ', IS ALREADY LOADED FOR FOR PMTT_SLS_RSLT_LOD'                  
               DISPLAY '****SALES WILL NOT BE LOADED - PROMO ON-CALL '          
                'NEEDS TO CONTACT COSA ON-CALL.'                                
               MOVE SQLCODE                        TO PV-SQL                    
               MOVE 'PMTT_SLS_RSLT_LOD'            TO PV-TABLE-NAME             
               MOVE 'C100-CHECK-PROMO-ITM'         TO PV-PARAGRAPH-NAME         
               MOVE 'NO SALES WILL BE LOADED'      TO PV-OPERATION-MSG          
               PERFORM Z910-ABEND                                               
                                                                                
             WHEN SQLCODE = 1403                                                
               CONTINUE                                                         
                                                                                
             WHEN OTHER                                                         
               MOVE SQLCODE                        TO PV-SQL                    
               MOVE 'PMTT_SLS_RSLT_LOD'            TO PV-TABLE-NAME             
               MOVE 'C110-CHECK-SLS-RSLT'          TO PV-PARAGRAPH-NAME         
               MOVE 'ERROR ON SELECT FROM TABLE '  TO PV-OPERATION-MSG          
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
070941****************************************************************          
070941** READ THE ORACLE LOGON CONTROL CARD.                        **          
070941****************************************************************          
070941 7000-READ-ORACLE-LOGON.                                                  
070941                                                                          
070941     READ ORACLE-LOGON-FILE                                               
070941         INTO CC-ORACLE-CONNECTION                                        
070941         AT END                                                           
070941             SET PS-END-OF-LOGON-FILE TO TRUE                             
070941     END-READ.                                                            
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT     *        
      * LOGONS TO ORACLE.                                              *        
      ******************************************************************        
070941*    COPY PMPD700.                                                        
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
070941*    DISPLAY '** PROGRAM        ** = ' PC-CURRENT-PROGRAM-NAME.           
070941     DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL.                            
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      ******************************************************************        
      * Z910-ABEND - PERFORMS A NON-SQL-ABEND                          *        
      ******************************************************************        
       Z910-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND           **'.                                     
070941*    DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.          
070941     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
           DISPLAY '** RECORDS READ   ** = ' PA-READ-COUNTER.                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
