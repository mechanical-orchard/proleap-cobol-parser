       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         ETKUT127.                                            
       DATE-WRITTEN.       08/07/2000.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:    THE PURPOSE OF THE MODULE IS TO COUNT BYTES AT THE           
      *         DOCUMENT DETAIL LEVEL. THE EXTRACTED BYTE COUNT WILL            
      *         THEN BE USED TO UPDATE THE FUNCTIONAL GROUP TRANSACTION         
      *         STATUS TABLE, TFGTRST.                                          
      *                                                                         
99999 *                                                                         
99999 *CHANGE HISTORY:                                                          
99999 *    WR/PITS#    DATE      DESCRIPTION                                    
99999 *    WR99999   08/28/2000 ROD JANSSEN                                     
99999 *                         DOCUMENT TRACKING 864S AND MULTIPLE             
99999 *                         MIT SEGMENT PROBLEM....                         
99999 ******************************************************************        
JF0901*DATE  09/24/2001                                                         
JF0901*    CHANGE MADE: CHANGED CODE TO ONLY EXTRACT ONE TP_DKEY FOR            
JF0901*    TRADING PARTNERS THAT SHARE THE SAME ID SO ONLY THE PARENT           
JF0901*    ENTITY RECEIVES BYTE COUNTS.                                         
JF0901*    MADE BY: JON FIEDLER JF0901      WR/IR #: 22790                      
JF0901******************************************************************        
JF0502*DATE  05/09/2002                                                         
JF0502*  CHANGE MADE: ADDED PROCESSING FOR THE 816 DOCUMENT AND                 
JF0502*   COMBINED ETKUT126 AND ETKUT127 INTO ONE PROGRAM. ETKUT127 WILL        
JF0502*   NOW PROCESS BYTE COUNTS FOR BOTH INBOUND AND OUTBOUND                 
JF0502*   DOCUMENTS.                                                            
JF0502*  MADE BY: JON FIEDLER JF0502      WR/IR #: 22778                        
JF1002******************************************************************        
JF1002*DATE  10/23/2002                                                         
JF1002*  CHANGE MADE: ADDED MISSED INITIALIZATION OF THE TPDKEY SWITCH.         
JF1002*  MADE BY: JON FIEDLER JF1002      WR/IR #: PM00501502                   
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS INPUT-RECORD.                                         
                                                                                
       01  INPUT-RECORD.                                                        
           05  IN-RECORD-TYPE          PIC X(3).                                
           05  IN-SHORT-RECORD-TYPE REDEFINES IN-RECORD-TYPE.                   
               10  IN-SHORT-REC-TYPE   PIC X(2).                                
               10  FILLER              PIC X(1).                                
           05  FILLER                  PIC X(147).                              
                                                                                
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 100 CHARACTERS                                       
           DATA RECORD IS OUTPUT-RECORD.                                        
                                                                                
       01  OUTPUT-RECORD               PIC X(100).                              
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  WS-ISA-RECORD.                                                       
           05  FILLER                  PIC X(3).                                
           05  WS-ELEMENT-SEPERATOR    PIC X(1).                                
           05  WS-ISA-01               PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-02               PIC X(10).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-03               PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-04               PIC X(10).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-05               PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-06               PIC X(15).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-07               PIC X(2).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-08               PIC X(15).                               
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-09               PIC 9(6).                                
           05  FILLER                  PIC X(1).                                
           05  WS-ISA-10               PIC 9(4).                                
           05  FILLER                  PIC X(9).                                
           05  WS-ISA-13               PIC 9(9).                                
           05  FILLER                  PIC X(3).                                
           05  WS-ISA-15               PIC X(1).                                
           05  FILLER                  PIC X(2).                                
           05  WS-SEGMENT-TERMINATOR   PIC X(1).                                
           05  FILLER                  PIC X(44).                               
                                                                                
       01  WS-GS-RECORD.                                                        
           05  WS-GS-RECORD-TYPE       PIC X(2).                                
           05  WS-GS-01                PIC X(2).                                
           05  WS-GS-02                PIC X(15).                               
           05  WS-GS-03                PIC X(15).                               
           05  WS-GS-04                PIC X(8).                                
           05  WS-GS-05                PIC X(8).                                
           05  WS-GS-06                PIC 9(9).                                
           05  WS-GS-07                PIC X(1).                                
           05  WS-GS-08                PIC X(12).                               
                                                                                
       01  WS-ST-RECORD.                                                        
           05  WS-ST-RECORD-TYPE       PIC X(2).                                
           05  WS-ST-01                PIC X(3).                                
           05  WS-ST-02                PIC X(9).                                
                                                                                
       01  WS-AK1-RECORD.                                                       
           05  WS-AK1-RECORD-TYPE      PIC X(3).                                
           05  WS-AK1-DETAIL.                                                   
               10  WS-AK1-01           PIC X(2).                                
               10  WS-AK1-02           PIC 9(9).                                
                                                                                
       01  WS-BAK-RECORD.                                                       
           05  WS-BAK-RECORD-TYPE      PIC X(3).                                
           05  WS-BAK-01               PIC X(2).                                
           05  WS-BAK-02               PIC X(2).                                
           05  WS-BAK-03               PIC X(22).                               
           05  WS-BAK-04               PIC 9(6).                                
           05  WS-BAK-05               PIC X(20).                               
                                                                                
       01  WS-BIG-RECORD.                                                       
           05  WS-BIG-RECORD-TYPE      PIC X(3).                                
           05  WS-BIG-01               PIC 9(6).                                
           05  WS-BIG-02               PIC X(22).                               
           05  WS-BIG-03               PIC 9(6).                                
           05  WS-BIG-04               PIC X(22).                               
           05  WS-BIG-05               PIC X(22).                               
                                                                                
       01  WS-BSN-RECORD.                                                       
           05  WS-BSN-RECORD-TYPE      PIC X(3).                                
           05  WS-BSN-01               PIC X(2).                                
           05  WS-BSN-02               PIC X(30).                               
           05  WS-BSN-03               PIC 9(6).                                
           05  WS-BSN-04               PIC 9(4).                                
                                                                                
       01  WS-B10-RECORD.                                                       
           05  WS-B10-RECORD-TYPE      PIC X(3).                                
           05  WS-B10-01               PIC X(22).                               
           05  WS-B10-02               PIC X(30).                               
           05  WS-B10-03               PIC X(4).                                
           05  WS-B10-04               PIC X(3).                                
                                                                                
       01  WS-BEG-RECORD.                                                       
           05  WS-BEG-RECORD-TYPE      PIC X(3).                                
           05  WS-BEG-01               PIC X(2).                                
           05  WS-BEG-02               PIC X(2).                                
           05  WS-BEG-03               PIC X(30).                               
           05  WS-BEG-04               PIC X(30).                               
           05  WS-BEG-05               PIC X(8).                                
                                                                                
SP0509 01  WS-BCH-RECORD.                                                       
SP0509     05  WS-BCH-RECORD-TYPE      PIC X(3).                                
SP0509     05  WS-BCH-01               PIC X(2).                                
SP0509     05  WS-BCH-02               PIC X(2).                                
SP0509     05  WS-BCH-03               PIC X(30).                               
                                                                                
       01  WS-XQ-RECORD.                                                        
           05  WS-XQ-RECORD-TYPE       PIC X(2).                                
           05  WS-XQ-01                PIC X(2).                                
           05  WS-XQ-02                PIC X(8).                                
           05  WS-XQ-03                PIC X(8).                                
                                                                                
       01  WS-MIT-RECORD.                                                       
           05  WS-MIT-RECORD-TYPE      PIC X(3).                                
           05  WS-MIT-01               PIC X(30).                               
           05  WS-MIT-02               PIC X(80).                               
           05  WS-MIT-03               PIC X(3).                                
           05  WS-MIT-04               PIC X(3).                                
                                                                                
       01  WS-TRN-RECORD.                                                       
           05  WS-TRN-RECORD-TYPE      PIC X(3).                                
           05  WS-TRN-01               PIC X(2).                                
           05  WS-TRN-02               PIC X(30).                               
           05  WS-TRN-03               PIC X(10).                               
           05  WS-TRN-04               PIC X(30).                               
                                                                                
       01  WS-B3-RECORD.                                                        
           05  WS-B3-RECORD-TYPE       PIC X(03).                               
           05  WS-B3-01                PIC X(01).                               
           05  WS-B3-02                PIC X(22).                               
           05  WS-B3-03                PIC X(30).                               
           05  WS-B3-04                PIC X(02).                               
           05  WS-B3-05                PIC X(01).                               
           05  WS-B3-06                PIC 9(06).                               
           05  WS-B3-07                PIC 9(12).                               
           05  WS-B3-08                PIC X(02).                               
           05  WS-B3-09                PIC 9(06).                               
           05  WS-B3-10                PIC X(03).                               
           05  WS-B3-11                PIC X(04).                               
                                                                                
       01  WS-BGN-RECORD.                                                       
           05  WS-BGN-RECORD-TYPE      PIC X(03).                               
           05  WS-BGN-01               PIC X(02).                               
           05  WS-BGN-02               PIC X(30).                               
           05  WS-BGN-03               PIC 9(06).                               
                                                                                
JF0502 01  WS-BHT-RECORD.                                                       
JF0502     05  WS-BHT-RECORD-TYPE      PIC X(03).                               
JF0502     05  WS-BHT-01               PIC X(04).                               
JF0502     05  WS-BHT-02               PIC X(02).                               
JF0502     05  WS-BHT-03               PIC X(30).                               
JF0502     05  WS-BHT-04               PIC X(08).                               
JF0502                                                                          
JF0502 01  WS-BHT-TRN-DTL-TXT          PIC X(11).                               
JF0502 01  FILLER REDEFINES WS-BHT-TRN-DTL-TXT.                                 
JF0502     05  WS-BHT-TRN-DTL-TXT-01   PIC X(03).                               
JF0502     05  WS-BHT-TRN-DTL-TXT-04   PIC X(08).                               
                                                                                
       01  WS-ISA-02-TMST              PIC X(10).                               
       01  FILLER REDEFINES WS-ISA-02-TMST.                                     
           05  WS-ISA-02-TMST-YEAR     PIC X(4).                                
           05  WS-ISA-02-TMST-MONTH    PIC X(2).                                
           05  WS-ISA-02-TMST-DAY      PIC X(2).                                
           05  WS-ISA-02-TMST-HOUR     PIC X(2).                                
                                                                                
       01  WS-ISA-04-TMST                PIC X(10).                             
       01  FILLER REDEFINES WS-ISA-04-TMST.                                     
           05  WS-ISA-04-TMST-MINUTE     PIC X(2).                              
           05  WS-ISA-04-TMST-SECOND     PIC X(2).                              
           05  WS-ISA-04-TMST-FRACTION   PIC X(6).                              
                                                                                
       01  WS-DB-TIMESTAMP               PIC X(26).                             
       01  FILLER REDEFINES WS-DB-TIMESTAMP.                                    
           05  WS-DB-YEAR                PIC X(4).                              
           05  WS-DASH-1                 PIC X(1).                              
           05  WS-DB-MONTH               PIC X(2).                              
           05  WS-DASH-2                 PIC X(1).                              
           05  WS-DB-DAY                 PIC X(2).                              
           05  WS-DASH-3                 PIC X(1).                              
           05  WS-DB-HOUR                PIC X(2).                              
           05  WS-PERIOD-1               PIC X(1).                              
           05  WS-DB-MINUTE              PIC X(2).                              
           05  WS-PERIOD-2               PIC X(1).                              
           05  WS-DB-SECOND              PIC X(2).                              
           05  WS-PERIOD-3               PIC X(1).                              
           05  WS-DB-FRACTION            PIC X(6).                              
                                                                                
       01  WS-SAVE-AUD-TMST            PIC X(26) VALUE SPACES.                  
                                                                                
       01  WS-ET027-EXTRACT-RECORD-SAVE.                                        
           05  WS-TP-DKEY-SAVE           PIC 9(09) VALUE ZEROS.         ETWS020 
           05  WS-TP-DKEY-TMST-SAVE      PIC X(26) VALUE SPACES.        ETWS020 
           05  WS-FUNC-CDE-CNTL-NBR-SAVE PIC 9(09) VALUE ZEROS.         ETWS020 
           05  WS-TRN-SET-CNTL-NBR-SAVE  PIC X(09) VALUE SPACES.        ETWS020 
           05  WS-TRN-DTL-TXT-SAVE       PIC X(30) VALUE SPACES.                
           05  WS-BYTE-COUNT-SAVE        PIC S9(15) COMP VALUE ZEROS.           
           05  WS-SOURCE                 PIC X(01) VALUE SPACE.                 
           05  WS-DOCUMENT               PIC X(03) VALUE SPACES.                
                                                                                
       01  WS-MESSAGE.                                                          
           05  WS-MESSAGE-TYPE           PIC X(02) VALUE SPACES.                
           05  WS-MESSAGE-NBR            PIC X(28) VALUE SPACES.                
                                                                                
JF0502 01  WS-VEND-ID-DESC             PIC X(2).                                
JF0502 01  WS-VEND-ID                  PIC X(15).                               
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  END-OF-FILE-SW          PIC X(1)  VALUE 'N'.                     
               88  END-OF-FILE                   VALUE 'Y'.                     
JF0901     05  NO-TP-DKEY-SW           PIC X(1)  VALUE 'N'.                     
JF0901         88  TP-DKEY-FOUND                 VALUE 'Y'.                     
JF0901         88  TP-DKEY-NOT-FOUND             VALUE 'N'.                     
           05  WS-PROCESS-TRADE-SW     PIC X(01) VALUE 'N'.                     
               88  PROCESS-THE-TRADE             VALUE 'Y'.                     
99999      05  WS-FIRST-MIT-SW         PIC X(01) VALUE 'N'.                     
99999          88  FIRST-MIT-FOUND               VALUE 'Y'.                     
                                                                                
       01  WS-EDIT-SEGMENT-TABLE.                                               
           05  WS-EDIT-ELEMENTS    OCCURS 150 TIMES.                            
               10  WS-EDIT-BYTE    PIC X(01).                                   
                                                                                
       01  WS-TRN-SET-CTL-TABLE.                                                
           05  WS-TRN-SET-ELEMENTS OCCURS 9 TIMES.                              
               10  WS-TRN-SET-BYTE PIC X(01).                                   
                                                                                
       01  WS-EDIT-TRN-SET-CTL-TABLE.                                           
           05  WS-EDT-TRN-SET-ELE  OCCURS 9 TIMES.                              
               10  WS-EDT-TRNSET   PIC X(01).                                   
                                                                                
       01  VNDR-SUB                PIC S9(05) COMP VALUE ZEROS.                 
       01  BYTE-SUB                PIC S9(05) COMP VALUE ZEROS.                 
       01  TRNSET-CTL-SUB          PIC S9(05) COMP VALUE ZEROS.                 
       01  EDT-TRNSET-SUB          PIC S9(05) COMP VALUE ZEROS.                 
       01  WS-BYTE-COUNT           PIC S9(15) COMP VALUE ZEROS.                 
       01  WS-BYTE-ACCUM           PIC S9(15) COMP VALUE ZEROS.                 
       01  WS-30-CHAR-FIELD        PIC X(30) VALUE SPACES.                      
       01  WS-TALLY                PIC S9(04) COMP VALUE +0.                    
                                                                                
       01  WS-TP-DKEY              PIC S9(9) COMP.                              
                                                                                
JF0502 01  PC-PROGRAM-CONSTANTS.                                                
JF0502     05  PC-KOHLS-ID1        PIC X(11) VALUE '14147844480'.               
JF0502     05  PC-KOHLS-ID2        PIC X(10) VALUE '4147037000'.                
                                                                                
       01  PV-PARAGRAPH-NAME       PIC X(30).                                   
       01  PV-DB-OPER-ATTEMPTED    PIC X(30).                                   
                                                                                
       01  ABEND-CODE              PIC S9(04)  VALUE +0 COMP SYNC.              
           88 ABEND-4013                       VALUE +4013.                     
                                                                                
      ******************************************************************        
      * SQLCA MESSAGE MODULE DSNTIAR ARGUMENTS......                            
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      * DOCUMENT TRACKING - BYTE COUNTS EXTRACT RECORD DESCRIPTION.             
      ******************************************************************        
           COPY ETRD027.                                                        
                                                                                
      ******************************************************************        
      * SQL ERROR HANDLING ARGUMENTS.......                                     
      ******************************************************************        
           COPY SQLCA2.                                                         
                                                                                
      ******************************************************************        
      * DB2 ERROR HANDLING ARGUMENTS....                                        
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * TRADING PARTNER TABLE ARGUMENTS.....                                    
      ******************************************************************        
           EXEC SQL                                                             
              INCLUDE TTRDPRT                                                   
           END-EXEC.                                                            
                                                                                
JF0901     EXEC SQL                                                             
JF0901        INCLUDE TTRDMLT                                                   
JF0901     END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE DAILY DOCUMENT                
      *      TRACKING UNWRAPPED DATASETS. THE MODULE WILL TAKE THE              
      *      UNWRAPPED EDI FILES AND CREATE AN EXTRACT STATUS RECORD            
      *      FILE WHICH WILL BE USED TO LOAD BYTE COUNTS TO DOCUMENT            
      *      TRACKING.                                                          
      *                                                                         
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION                                          
              THRU 1000-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 2000-MAIN-PROCESS-LOOP                                       
              THRU 2000-MAIN-PROCESS-LOOP-EXIT                                  
                UNTIL END-OF-FILE.                                              
                                                                                
           PERFORM 9000-EOJ-TASKS                                               
              THRU 9000-EOJ-TASKS-EXIT.                                         
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION....                                                 
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           OPEN INPUT  INPUT-FILE                                               
                OUTPUT OUTPUT-FILE.                                             
                                                                                
           EXEC SQL                                                             
               SET :WS-SAVE-AUD-TMST = CURRENT_TIMESTAMP                        
           END-EXEC.                                                            
                                                                                
           INITIALIZE ET027-EXTRACT-RECORD.                                     
           INITIALIZE WS-ET027-EXTRACT-RECORD-SAVE.                             
                                                                                
           MOVE ZEROS          TO WS-BYTE-COUNT.                                
           MOVE ZEROS          TO WS-BYTE-ACCUM.                                
           MOVE 'N'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       1000-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-MAIN PROCESS LOOP...                                               
      ******************************************************************        
       2000-MAIN-PROCESS-LOOP.                                                  
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
           IF END-OF-FILE                                                       
               GO TO 2000-MAIN-PROCESS-LOOP-EXIT.                               
                                                                                
           IF IN-RECORD-TYPE IS EQUAL TO 'ISA'                                  
              PERFORM 3000-PROCESS-ISA                                          
                 THRU 3000-PROCESS-ISA-EXIT.                                    
                                                                                
           PERFORM 8300-COUNT-BYTES                                             
              THRU 8300-COUNT-BYTES-EXIT.                                       
                                                                                
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'GS'                                
              PERFORM 3100-PROCESS-GS                                           
                 THRU 3100-PROCESS-GS-EXIT                                      
           ELSE                                                                 
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'ST'                                
              PERFORM 3200-PROCESS-ST                                           
                 THRU 3200-PROCESS-ST-EXIT                                      
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'AK1'                                  
              PERFORM 3300-PROCESS-AK1                                          
                 THRU 3300-PROCESS-AK1-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'BAK'                                  
              PERFORM 3400-PROCESS-BAK                                          
                 THRU 3400-PROCESS-BAK-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'BIG'                                  
              PERFORM 3500-PROCESS-BIG                                          
                 THRU 3500-PROCESS-BIG-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'BSN'                                  
              PERFORM 3600-PROCESS-BSN                                          
                 THRU 3600-PROCESS-BSN-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'B10'                                  
              PERFORM 3700-PROCESS-B10                                          
                 THRU 3700-PROCESS-B10-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'MIT'                                  
              PERFORM 3800-PROCESS-MIT                                          
                 THRU 3800-PROCESS-MIT-EXIT                                     
           ELSE                                                                 
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'XQ'                                
              PERFORM 3900-PROCESS-XQ                                           
                 THRU 3900-PROCESS-XQ-EXIT                                      
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'BEG'                                  
              PERFORM 4000-PROCESS-BEG                                          
                 THRU 4000-PROCESS-BEG-EXIT                                     
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'TRN'                                  
              PERFORM 4100-PROCESS-TRN                                          
                 THRU 4100-PROCESS-TRN-EXIT                                     
           ELSE                                                                 
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'B3'                                
              PERFORM 4200-PROCESS-B3                                           
                 THRU 4200-PROCESS-B3-EXIT                                      
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'BGN'                                  
              PERFORM 4300-PROCESS-BGN                                          
                 THRU 4300-PROCESS-BGN-EXIT                                     
JF0502     ELSE                                                                 
JF0502     IF IN-RECORD-TYPE IS EQUAL TO 'BHT'                                  
JF0502        PERFORM 4400-PROCESS-BHT                                          
JF0502           THRU 4400-PROCESS-BHT-EXIT                                     
SP0509     ELSE                                                                 
SP0509     IF IN-RECORD-TYPE IS EQUAL TO 'BCH'                                  
SP0509        PERFORM 4500-PROCESS-BCH                                          
SP0509           THRU 4500-PROCESS-BCH-EXIT                                     
           ELSE                                                                 
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'SE'                                
              ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM                               
              MOVE WS-BYTE-ACCUM TO WS-BYTE-COUNT-SAVE                          
              MOVE ZEROS         TO WS-BYTE-ACCUM                               
           ELSE                                                                 
           IF IN-SHORT-REC-TYPE IS EQUAL TO 'GE'                                
              ADD WS-BYTE-COUNT  TO WS-BYTE-COUNT-SAVE                          
              MOVE ZEROS         TO WS-BYTE-ACCUM                               
           ELSE                                                                 
           IF IN-RECORD-TYPE IS EQUAL TO 'IEA'                                  
              ADD WS-BYTE-COUNT  TO WS-BYTE-COUNT-SAVE                          
              MOVE ZEROS         TO WS-BYTE-ACCUM                               
              PERFORM 8200-WRITE-EXTRACT-FILE                                   
                 THRU 8200-WRITE-EXTRACT-FILE-EXIT                              
           ELSE                                                                 
              ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM                               
           END-IF.                                                              
                                                                                
       2000-MAIN-PROCESS-LOOP-EXIT.                                             
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3000-PROCESS-ISA.                                                       
      ******************************************************************        
       3000-PROCESS-ISA.                                                        
                                                                                
99999      INITIALIZE WS-ET027-EXTRACT-RECORD-SAVE.                             
                                                                                
           MOVE 'N'                     TO WS-PROCESS-TRADE-SW.                 
99999      MOVE 'N'                     TO WS-FIRST-MIT-SW.                     
           MOVE ZEROS                   TO WS-BYTE-ACCUM.                       
                                                                                
           MOVE INPUT-RECORD            TO WS-ISA-RECORD.                       
                                                                                
           MOVE WS-ISA-02               TO WS-ISA-02-TMST.                      
           MOVE WS-ISA-04               TO WS-ISA-04-TMST.                      
                                                                                
           MOVE WS-ISA-02-TMST-YEAR     TO WS-DB-YEAR.                          
           MOVE '-'                     TO WS-DASH-1.                           
           MOVE WS-ISA-02-TMST-MONTH    TO WS-DB-MONTH.                         
           MOVE '-'                     TO WS-DASH-2.                           
           MOVE WS-ISA-02-TMST-DAY      TO WS-DB-DAY.                           
           MOVE '-'                     TO WS-DASH-3.                           
           MOVE WS-ISA-02-TMST-HOUR     TO WS-DB-HOUR.                          
           MOVE '.'                     TO WS-PERIOD-1.                         
           MOVE WS-ISA-04-TMST-MINUTE   TO WS-DB-MINUTE.                        
           MOVE '.'                     TO WS-PERIOD-2.                         
           MOVE WS-ISA-04-TMST-SECOND   TO WS-DB-SECOND.                        
           MOVE '.'                     TO WS-PERIOD-3.                         
           MOVE WS-ISA-04-TMST-FRACTION TO WS-DB-FRACTION.                      
                                                                                
           MOVE WS-DB-TIMESTAMP         TO WS-TP-DKEY-TMST-SAVE.                
                                                                                
       3000-PROCESS-ISA-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3100-PROCESS-GS.                                                        
      ******************************************************************        
       3100-PROCESS-GS.                                                         
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-GS-RECORD-TYPE, WS-GS-01, WS-GS-02,                    
                      WS-GS-03, WS-GS-04, WS-GS-05,                             
                      WS-GS-06, WS-GS-07, WS-GS-08.                             
                                                                                
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
                                                                                
       3100-PROCESS-GS-EXIT.                                                    
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3200-PROCESS-ST.                                                        
      ******************************************************************        
       3200-PROCESS-ST.                                                         
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-ST-RECORD-TYPE, WS-ST-01, WS-ST-02.                    
                                                                                
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
                                                                                
       3200-PROCESS-ST-EXIT.                                                    
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3300-PROCESS-AK1.                                                       
      ******************************************************************        
       3300-PROCESS-AK1.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-AK1-RECORD-TYPE, WS-AK1-01, WS-AK1-02.                 
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-AK1-DETAIL TO WS-TRN-DTL-TXT-SAVE.                           
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '997'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3300-PROCESS-AK1-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3400-PROCESS-BAK.                                                       
      ******************************************************************        
       3400-PROCESS-BAK.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-BAK-RECORD-TYPE, WS-BAK-01, WS-BAK-02,                 
                 WS-BAK-03, WS-BAK-04, WS-BAK-05.                               
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-BAK-03 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '855'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3400-PROCESS-BAK-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3500-PROCESS-BIG.                                                       
      ******************************************************************        
       3500-PROCESS-BIG.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-BIG-RECORD-TYPE, WS-BIG-01, WS-BIG-02,                 
                 WS-BIG-03, WS-BIG-04, WS-BIG-05.                               
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-BIG-02 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '810'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3500-PROCESS-BIG-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3600-PROCESS-BSN.                                                       
      ******************************************************************        
       3600-PROCESS-BSN.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-BSN-RECORD-TYPE, WS-BSN-01, WS-BSN-02,                 
                 WS-BSN-03, WS-BSN-04.                                          
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-BSN-02 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '856'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3600-PROCESS-BSN-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3700-PROCESS-B10.                                                       
      ******************************************************************        
       3700-PROCESS-B10.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                 INTO WS-B10-RECORD-TYPE, WS-B10-01, WS-B10-02,                 
                 WS-B10-03, WS-B10-04.                                          
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-B10-02 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '214'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3700-PROCESS-B10-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3800-PROCESS-MIT.                                                       
      ******************************************************************        
       3800-PROCESS-MIT.                                                        
                                                                                
99999      IF FIRST-MIT-FOUND                                                   
99999          ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM                              
99999          GO TO 3800-PROCESS-MIT-EXIT.                                     
99999                                                                           
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-MIT-RECORD-TYPE, WS-MIT-01, WS-MIT-02,              
                         WS-MIT-03, WS-MIT-04.                                  
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '864'     TO WS-DOCUMENT.                                       
                                                                                
           IF WS-MIT-02 IS EQUAL TO 'GS Control Number - Missing FA'            
               MOVE 'FA'       TO WS-MESSAGE-TYPE                               
               MOVE WS-MIT-01  TO WS-MESSAGE-NBR                                
               MOVE WS-MESSAGE TO WS-TRN-DTL-TXT-SAVE                           
           ELSE                                                                 
               MOVE 'TX'       TO WS-MESSAGE-TYPE                               
               MOVE WS-MIT-01  TO WS-MESSAGE-NBR                                
               MOVE WS-MESSAGE TO WS-TRN-DTL-TXT-SAVE                           
           END-IF.                                                              
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'Y'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3800-PROCESS-MIT-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 3900-PROCESS-XQ.                                                        
      ******************************************************************        
       3900-PROCESS-XQ.                                                         
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-XQ-RECORD-TYPE, WS-XQ-01, WS-XQ-02,                 
                         WS-XQ-03.                                              
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-XQ-02  TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '852'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       3900-PROCESS-XQ-EXIT.                                                    
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4000-PROCESS-BEG                                                        
      ******************************************************************        
       4000-PROCESS-BEG.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-BEG-RECORD-TYPE, WS-BEG-01, WS-BEG-02,              
                         WS-BEG-03, WS-BEG-04, WS-BEG-05.                       
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-BEG-03 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '850'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       4000-PROCESS-BEG-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4100-PROCESS-TRN.                                                       
      ******************************************************************        
       4100-PROCESS-TRN.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-TRN-RECORD-TYPE, WS-TRN-01, WS-TRN-02,              
                         WS-TRN-03, WS-TRN-04.                                  
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-TRN-02 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '820'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       4100-PROCESS-TRN-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4200-PROCESS-B3.                                                        
      ******************************************************************        
       4200-PROCESS-B3.                                                         
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-B3-RECORD-TYPE, WS-B3-01, WS-B3-02,                 
                         WS-B3-03, WS-B3-04, WS-B3-05, WS-B3-06,                
                         WS-B3-07, WS-B3-08, WS-B3-09, WS-B3-10,                
                         WS-B3-11.                                              
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-B3-02  TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '210'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       4200-PROCESS-B3-EXIT.                                                    
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 4300-PROCESS-BGN.                                                       
      ******************************************************************        
       4300-PROCESS-BGN.                                                        
                                                                                
           IF PROCESS-THE-TRADE                                                 
               PERFORM 8200-WRITE-EXTRACT-FILE                                  
                  THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
                                                                                
           UNSTRING INPUT-RECORD DELIMITED BY                                   
              WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
                    INTO WS-BGN-RECORD-TYPE, WS-BGN-01, WS-BGN-02,              
                         WS-BGN-03.                                             
                                                                                
           MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
           MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
           MOVE WS-BGN-02 TO WS-TRN-DTL-TXT-SAVE.                               
           ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
           MOVE '824'     TO WS-DOCUMENT.                                       
                                                                                
           MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
99999      MOVE 'N'            TO WS-FIRST-MIT-SW.                              
                                                                                
       4300-PROCESS-BGN-EXIT.                                                   
           EXIT.                                                                
                                                                                
JF0502******************************************************************        
JF0502* 4400-PROCESS-BHT.                                                       
JF0502******************************************************************        
JF0502 4400-PROCESS-BHT.                                                        
JF0502                                                                          
JF0502     IF PROCESS-THE-TRADE                                                 
JF0502         PERFORM 8200-WRITE-EXTRACT-FILE                                  
JF0502            THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
JF0502                                                                          
JF0502     UNSTRING INPUT-RECORD DELIMITED BY                                   
JF0502        WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
JF0502              INTO WS-BHT-RECORD-TYPE, WS-BHT-01, WS-BHT-02,              
JF0502                   WS-BHT-03, WS-BHT-04.                                  
JF0502                                                                          
JF0502     MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
JF0502     MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
JF0502                                                                          
JF0502* MAKE TRN-DTL-TXT UNIQUE BY ADDING REL(FOR RELATIONSHIP DOC,             
JF0502* BHT_01 = 0057) OR ADD(FOR ADDRESS DOC, BHT_01 = 00065).                 
JF0502     IF WS-BHT-01 = '0057'                                                
JF0502        MOVE 'REL' TO WS-BHT-TRN-DTL-TXT-01                               
JF0502     ELSE                                                                 
JF0502        MOVE 'ADD' TO WS-BHT-TRN-DTL-TXT-01.                              
JF0502                                                                          
JF0502     MOVE WS-BHT-04 TO WS-BHT-TRN-DTL-TXT-04.                             
JF0502     MOVE WS-BHT-TRN-DTL-TXT TO WS-TRN-DTL-TXT-SAVE.                      
JF0502     ADD WS-BYTE-COUNT TO WS-BYTE-ACCUM.                                  
JF0502     MOVE '816' TO WS-DOCUMENT.                                           
JF0502                                                                          
JF0502     MOVE 'Y' TO WS-PROCESS-TRADE-SW.                                     
JF0502     MOVE 'N' TO WS-FIRST-MIT-SW.                                         
JF0502                                                                          
JF0502 4400-PROCESS-BHT-EXIT.                                                   
JF0502     EXIT.                                                                
JF0502                                                                          
SP0509                                                                          
SP0509******************************************************************        
SP0509* 4500-PROCESS-BCH                                                        
SP0509******************************************************************        
SP0509 4500-PROCESS-BCH.                                                        
SP0509                                                                          
SP0509     IF PROCESS-THE-TRADE                                                 
SP0509         PERFORM 8200-WRITE-EXTRACT-FILE                                  
SP0509            THRU 8200-WRITE-EXTRACT-FILE-EXIT.                            
SP0509                                                                          
SP0509     UNSTRING INPUT-RECORD DELIMITED BY                                   
SP0509        WS-ELEMENT-SEPERATOR OR WS-SEGMENT-TERMINATOR                     
SP0509              INTO WS-BCH-RECORD-TYPE, WS-BCH-01, WS-BCH-02,              
SP0509                   WS-BCH-03.                                             
SP0509                                                                          
SP0509     MOVE WS-GS-06  TO WS-FUNC-CDE-CNTL-NBR-SAVE.                         
SP0509     MOVE WS-ST-02  TO WS-TRN-SET-CNTL-NBR-SAVE.                          
SP0509     MOVE WS-BCH-03 TO WS-TRN-DTL-TXT-SAVE.                               
SP0509     ADD WS-BYTE-COUNT  TO WS-BYTE-ACCUM.                                 
SP0509     MOVE '860'     TO WS-DOCUMENT.                                       
SP0509                                                                          
SP0509     MOVE 'Y'            TO WS-PROCESS-TRADE-SW.                          
SP0509     MOVE 'N'            TO WS-FIRST-MIT-SW.                              
SP0509                                                                          
SP0509 4500-PROCESS-BCH-EXIT.                                                   
SP0509     EXIT.                                                                
SP0509                                                                          
      /*****************************************************************        
      * 5000-EDIT-TRN-SET-CTL-NBR.                                              
      ******************************************************************        
       5000-EDIT-TRN-SET-CTL-NBR.                                               
                                                                                
           INITIALIZE WS-TRN-SET-CTL-TABLE.                                     
           INITIALIZE WS-EDIT-TRN-SET-CTL-TABLE.                                
           MOVE ZEROS          TO TRNSET-CTL-SUB.                               
           MOVE 10             TO EDT-TRNSET-SUB.                               
                                                                                
           MOVE WS-TRN-SET-CNTL-NBR-SAVE TO WS-TRN-SET-CTL-TABLE.               
                                                                                
       5000-CHECK-FOR-END.                                                      
                                                                                
           ADD 1               TO TRNSET-CTL-SUB.                               
                                                                                
           IF TRNSET-CTL-SUB IS GREATER THAN 9                                  
               GO TO 5000-EDIT-TRN-SET-CTL-NBR-EXIT.                            
                                                                                
           IF WS-TRN-SET-BYTE (9) NOT EQUAL SPACE                               
               GO TO 5000-EDIT-TRN-SET-CTL-NBR-EXIT.                            
                                                                                
           IF WS-TRN-SET-BYTE (TRNSET-CTL-SUB) NOT EQUAL SPACE                  
               GO TO 5000-CHECK-FOR-END.                                        
                                                                                
           SUBTRACT 1          FROM TRNSET-CTL-SUB.                             
                                                                                
       5000-BYTE-SHIFT.                                                         
                                                                                
           SUBTRACT 1          FROM EDT-TRNSET-SUB.                             
                                                                                
           IF EDT-TRNSET-SUB IS EQUAL TO ZERO                                   
             MOVE WS-EDIT-TRN-SET-CTL-TABLE TO WS-TRN-SET-CNTL-NBR-SAVE         
             GO TO 5000-EDIT-TRN-SET-CTL-NBR-EXIT.                              
                                                                                
           IF TRNSET-CTL-SUB IS EQUAL TO ZERO                                   
               MOVE '0'        TO WS-EDT-TRNSET (EDT-TRNSET-SUB)                
                GO TO 5000-BYTE-SHIFT.                                          
                                                                                
           MOVE WS-TRN-SET-BYTE (TRNSET-CTL-SUB)                                
                               TO WS-EDT-TRNSET (EDT-TRNSET-SUB).               
                                                                                
           SUBTRACT 1          FROM TRNSET-CTL-SUB.                             
                                                                                
           GO TO 5000-BYTE-SHIFT.                                               
                                                                                
       5000-EDIT-TRN-SET-CTL-NBR-EXIT.                                          
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 8000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE                                                      
              AT END  SET END-OF-FILE TO TRUE.                                  
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 8100-GET-TP-DKEY.                                                       
      ******************************************************************        
       8100-GET-TP-DKEY.                                                        
                                                                                
JF1002     INITIALIZE NO-TP-DKEY-SW.                                            
                                                                                
JF0901     EXEC SQL                                                             
JF0901       SELECT TP_DKEY                                                     
JF0901       INTO :WS-TP-DKEY                                                   
JF0901       FROM   TTRDPRT                                                     
JF0502       WHERE  VEND_INCHG_ID_DESC = :WS-VEND-ID-DESC                       
JF0502         AND  VEND_INCHG_ID      = :WS-VEND-ID                            
JF0901         AND  INACTV_DTE         = '9999-09-09'                           
JF0901     END-EXEC.                                                            
JF0901                                                                          
JF0901     IF SQLCODE = +000                                                    
JF0901        CONTINUE                                                          
JF0901     ELSE                                                                 
JF0901       IF SQLCODE = -811                                                  
JF0901          EXEC SQL                                                        
JF0901            SELECT TP_DKEY                                                
JF0901            INTO :WS-TP-DKEY                                              
JF0901            FROM   TTRDPRT A                                              
JF0901                  ,TTRDMLT B                                              
JF0502            WHERE  VEND_INCHG_ID_DESC = :WS-VEND-ID-DESC                  
JF0502              AND  VEND_INCHG_ID      = :WS-VEND-ID                       
JF0901              AND  INACTV_DTE         = '9999-09-09'                      
JF0901              AND  PRNT_ENT_ID = ENT_ID                                   
JF0901          END-EXEC                                                        
JF0901       END-IF                                                             
JF0901     END-IF.                                                              
JF0901                                                                          
JF0901     IF  SQLCODE  = +000                                                  
JF0901        SET TP-DKEY-FOUND TO TRUE                                         
JF0901     ELSE                                                                 
JF0901       IF SQLCODE NOT = +100 AND SQLCODE NOT = -811                       
JF0901        MOVE '8100-GET-TP-DKEY' TO PV-PARAGRAPH-NAME                      
JF0901        MOVE 'SELECT TP-DKEY'   TO PV-DB-OPER-ATTEMPTED                   
JF0901        PERFORM 9500-SQL-ERROR                                            
JF0901     END-IF.                                                              
                                                                                
       8100-GET-TP-DKEY-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       8200-WRITE-EXTRACT-FILE.                                                 
                                                                                
           MOVE WS-TP-DKEY-TMST-SAVE      TO ET027-TP-DKEY-TMST.        ETWS020 
           MOVE WS-FUNC-CDE-CNTL-NBR-SAVE TO ET027-FUNC-CDE-CNTL-NBR.   ETWS020 
                                                                                
JF0502     IF  WS-DOCUMENT = '816'                                              
JF0502      OR WS-DOCUMENT = '820'                                              
JF0502      OR WS-DOCUMENT = '850'                                              
JF0502      OR WS-DOCUMENT = '852'                                              
SP0509      OR WS-DOCUMENT = '860'                                              
JF0502      OR WS-DOCUMENT = '864'                                              
JF0502      OR (WS-DOCUMENT = '997'                                             
JF0502      AND (WS-ISA-06 = PC-KOHLS-ID1                                       
JF0502      OR WS-ISA-06 = PC-KOHLS-ID2))                                       
JF0502        MOVE WS-ISA-07 TO WS-VEND-ID-DESC                                 
JF0502        MOVE WS-ISA-08 TO WS-VEND-ID                                      
JF0502        MOVE 'O' TO ET027-SOURCE                                          
JF0502        PERFORM 5000-EDIT-TRN-SET-CTL-NBR                                 
JF0502          THRU 5000-EDIT-TRN-SET-CTL-NBR-EXIT                             
JF0502     ELSE                                                                 
JF0502        MOVE WS-ISA-05 TO WS-VEND-ID-DESC                                 
JF0502        MOVE WS-ISA-06 TO WS-VEND-ID                                      
JF0502        MOVE 'I' TO ET027-SOURCE                                          
JF0502     END-IF.                                                              
JF0502                                                                          
JF0502     PERFORM 8100-GET-TP-DKEY                                             
JF0502        THRU 8100-GET-TP-DKEY-EXIT.                                       
                                                                                
           MOVE WS-TRN-SET-CNTL-NBR-SAVE  TO ET027-TRN-SET-CNTL-NBR.    ETWS020 
                                                                                
           MOVE WS-TRN-DTL-TXT-SAVE       TO ET027-TRN-DTL-TXT.                 
           MOVE WS-BYTE-COUNT-SAVE        TO ET027-BYTE-COUNT.                  
           MOVE WS-DOCUMENT               TO ET027-TRN-SET-CDE.                 
                                                                                
JF0901     IF TP-DKEY-FOUND                                                     
              MOVE SPACES TO WS-30-CHAR-FIELD                                   
              MOVE +0 TO WS-TALLY                                               
              INSPECT ET027-TRN-DTL-TXT                                         
                  TALLYING WS-TALLY                                             
                  FOR                                                           
                      LEADING ZEROES                                            
              MOVE ET027-TRN-DTL-TXT(1 + WS-TALLY:30 - WS-TALLY) TO             
                   WS-30-CHAR-FIELD                                             
              MOVE WS-30-CHAR-FIELD TO ET027-TRN-DTL-TXT                        
JF0901        MOVE WS-TP-DKEY       TO ET027-TP-DKEY                            
              WRITE OUTPUT-RECORD FROM ET027-EXTRACT-RECORD                     
JF0901     END-IF.                                                              
                                                                                
           MOVE 'N'            TO WS-PROCESS-TRADE-SW.                          
                                                                                
       8200-WRITE-EXTRACT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 8300-GENERATE THE EXTRACT RECORD...                                     
      ******************************************************************        
       8300-COUNT-BYTES.                                                        
                                                                                
           MOVE 1                 TO BYTE-SUB.                                  
           MOVE ZEROS             TO WS-BYTE-COUNT.                             
                                                                                
           MOVE INPUT-RECORD      TO WS-EDIT-SEGMENT-TABLE.                     
                                                                                
       8300-COUNT.                                                              
                                                                                
           IF BYTE-SUB IS GREATER THAN 150                                      
               GO TO 8300-COUNT-BYTES-EXIT.                                     
                                                                                
           IF WS-EDIT-BYTE (BYTE-SUB) IS EQUAL TO WS-SEGMENT-TERMINATOR         
               ADD 1                TO WS-BYTE-COUNT                            
               GO TO 8300-COUNT-BYTES-EXIT.                                     
                                                                                
           ADD 1                    TO WS-BYTE-COUNT.                           
           ADD 1                    TO BYTE-SUB.                                
                                                                                
           GO TO 8300-COUNT.                                                    
                                                                                
       8300-COUNT-BYTES-EXIT.                                                   
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9000-EOJ-TASKS.                                                         
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
                                                                                
           CLOSE INPUT-FILE                                                     
                 OUTPUT-FILE.                                                   
                                                                                
       9000-EOJ-TASKS-EXIT.                                                     
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 9500-SQL-ERROR.                                                         
      ******************************************************************        
       9500-SQL-ERROR.                                                          
                                                                                
           SET ABEND-4013 TO TRUE.                                              
                                                                                
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-DB-OPER-ATTEMPTED.                 
                                                                                
                                                                                
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
