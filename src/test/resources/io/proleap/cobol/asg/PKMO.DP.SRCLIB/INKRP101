       ID  DIVISION.                                                    12/21/94
00002  PROGRAM-ID.         INKRP101                                     INKRP101
00003  AUTHOR.             TROY BIRK.                                      LV001
00004  INSTALLATION.       KOHLS DEPARTMENT STORES.                     INKRP101
00005  DATE-WRITTEN.       8/93.                                        INKRP101
00006  DATE-COMPILED.                                                   INKRP101
00007                                                                   INKRP101
00008 ***********************************************************       INKRP101
00009 *   THIS PROGRAM READS THE EDITED PHYSICAL INVENTORY FILE         INKRP101
00010 *   AND PRINT A REPORT SHOWING ALL DETAIL TRANSACTIONS.           INKRP101
00011 ***********************************************************       INKRP101
00012 *                   CHANGE LOG                                    INKRP101
00013 ***********************************************************       INKRP101
00014 * 03/94 M. BLAZICH                                                INKRP101
00015 *       CHANGED LRECLS TO ALLOW FOR SCAN-INV-NBR ON RECORD        INKRP101
00016 *       LAYOUT                                                    INKRP101
00017 * *********************************************************       INKRP101
00014 * 05/98 J.SEIDLER                                                 INKRP101
00015 *       CHANGE THE QUANTITY FILE IN THE INPUT FILE INWS009        INKRP101
00016 *       TO BE 9(9) COMP INSTEAD OF 9(4). NOTE: REPORT WILL        INKRP101
      *       *NOT* NEED TO BE EXPANDED.                                        
00017 ***********************************************************       INKRP101
00024 * 02/99   MARIA KLAMIK                                               CL**6
00018 * EXPAND COPYBOOK INWS009 TO ACCOMMODATE NEW SCAN DATE AND           CL**3
      * TIMESTAMP FIELDS.                                                       
00017 ***********************************************************       INKRP101
00024 * 12/02   S. JACKSON                                                 CL**6
00018 * REMOVE DJDE PRINT LOGIC                                            CL**3
00044 ***********************************************************          CL**4
W21732* 12/2002 CHANGED BY IGSI              WR# W21732                *        
W21732*    CHANGED THE REPORT COLUMN HEADING  FROM 'SHEET' TO 'AREA'   *        
W21732*    AND 'CLASS CODE' TO 'SKU'.REPLACED CURRENT DEPT/CLASS/SEQ   *        
W21732*    NUMBER WITH NON-INTELLIGENT SKU WITHOUT THE DASHES.         *        
W21732******************************************************************        
25248 * 25248    10-06-2003  UPC CAPABILITIES                                   
25248 *                      -ROD JANSSEN                                       
25248 ******************************************************************        
W26600*                                                                         
W26600*  W26600  06-  -2004  PHYSICAL INVENTORY STORE EXPANSION.  INPUT         
W26600*                      RECORD LENGTH CHANGED FROM 70 TO 71 BYTES.         
W26600*                      REPORT LOCATION ID (STORE NUMBER) CHANGED          
W26600*                      FROM 3 TO 4 BYTES.                                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
W26600******************************************************************        
W26006* 26006    02-24-2005  REMOVE REFERENCES TO OBSOLETE STORE TABLE.         
W26006*                      -ERIN SEARL                                        
P28545* W28545 03-24-2006 ROD JANSSEN CYCLE INVENTORY PRICE MODULE SETUP        
P28545*                               (TINCNTL/TINVPAR MODIFICATIONS)           
W26006******************************************************************        
W33304* W33304   06-17-2008  HILO CHANGES    WR# W33304                *        
W33304*    PATTY JAEGER - CODEWORKS   INCLUDE INV_STAT_CDE AND         *        
W33304*    AND FIN_BK_DTE IN INV_PARMS CURSOR                          *        
W33304******************************************************************        
SR3403* WR3403   06-17-2012  LARGE RETAIL    WR# W3403                 *        
SR3403*    JALAJ KUMAR  - CHANGES FOR LARGE RETAIL EXPANSION           *        
SR3403******************************************************************        
00018                                                                   INKRP101
00019  ENVIRONMENT DIVISION.                                            INKRP101
00020  CONFIGURATION SECTION.                                           INKRP101
00021  SOURCE-COMPUTER.        IBM-3090.                                INKRP101
00022  OBJECT-COMPUTER.        IBM-3090.                                INKRP101
00023  INPUT-OUTPUT SECTION.                                            INKRP101
00024  FILE-CONTROL.                                                    INKRP101
00025      SELECT PHY-INVENTORY-FILE                                    INKRP101
00026          ASSIGN TO INP01.                                         INKRP101
00027      SELECT PHY-INVENTORY-REPORT                                  INKRP101
00028          ASSIGN TO RPT01.                                         INKRP101
00029                                                                   INKRP101
00030  DATA DIVISION.                                                   INKRP101
00031  FILE SECTION.                                                    INKRP101
00032  FD  PHY-INVENTORY-FILE                                           INKRP101
00033      LABEL RECORDS ARE STANDARD                                   INKRP101
00034      BLOCK CONTAINS 0 RECORDS                                     INKRP101
00035      RECORDING  MODE IS F                                         INKRP101
00036      DATA RECORD IS PHY-INVENTORY-RECORD.                         INKRP101
W21732*01  PHY-INVENTORY-RECORD         PIC X(70).                      INKRP101
SR3403 01  PHY-INVENTORY-RECORD         PIC X(100).                     INKRP101
00038                                                                   INKRP101
00039  FD  PHY-INVENTORY-REPORT                                         INKRP101
00040      LABEL RECORDS ARE OMITTED                                    INKRP101
00041      RECORD CONTAINS 132 CHARACTERS                               INKRP101
00042      RECORDING  MODE IS F                                         INKRP101
00043      DATA RECORD IS REPORT-LINE.                                  INKRP101
00044  01  REPORT-LINE         PIC X(132).                              INKRP101
00045                                                                   INKRP101
00046                                                                   INKRP101
00047  WORKING-STORAGE SECTION.                                         INKRP101
00048 *                                                                 INKRP101
00049  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.INKRP101
00050 *                                                                 INKRP101
00051  01  PC-PRINT-CONTROL.                                            INKRP101
00052      05  PC-ADVANCE              PIC S9(03)  VALUE ZERO COMP-3.   INKRP101
00053      05  PC-PAGE-COUNT           PIC S9(05)  VALUE ZERO.          INKRP101
00054 *                                                                 INKRP101
00055  01  SA-SAVE-AREAS.                                               INKRP101
W26600     05  SA-STORE-NUMBER         PIC X(04)   VALUE SPACES.        INKRP101
25248      05  SA-STORE-NUMBER-NUM REDEFINES                            INKRP101
W26600         SA-STORE-NUMBER         PIC 9(04).                       INKRP101
00057      05  SA-CURR-KEY.                                             INKRP101
00058          10  SA-CURR-SHEET       PIC X(6)    VALUE SPACES.        INKRP101
00059          10  SA-CURR-LINE        PIC X(2)    VALUE SPACES.        INKRP101
00060      05  SA-PRIOR-KEY.                                            INKRP101
00061          10  SA-PRIOR-SHEET      PIC X(6)    VALUE SPACES.        INKRP101
00062          10  SA-PRIOR-LINE       PIC X(2)    VALUE SPACES.        INKRP101
00063      05  SA-FIRST-SHEET          PIC X(6)    VALUE SPACES.        INKRP101
00064      05  SA-LAST-SHEET           PIC X(6)    VALUE SPACES.        INKRP101
00065 *                                                                 INKRP101
00066  01  PV-PROGRAM-VARIABLES.                                        INKRP101
00067      05  PV-PARAGRAPH-NAME       PIC X(30)    VALUE SPACES.       INKRP101
00068      05  PV-SQL-SUB              PIC 9(03)    VALUE ZERO.         INKRP101
00069      05  PV-DB2-OPER-ATTEMPTED   PIC X(30)    VALUE SPACES.       INKRP101
00070      05  PV-EXTENDED-RETAIL      PIC S9(09)V99 VALUE ZERO COMP-3. INKRP101
00071      05  PV-INVENTORY-DATE.                                       INKRP101
00072          10  PV-CC               PIC X(02)     VALUE SPACES.      INKRP101
00073          10  PV-YY               PIC X(02)     VALUE SPACES.      INKRP101
00074          10  FILLER              PIC X(01)     VALUE SPACES.      INKRP101
00075          10  PV-MM               PIC X(02)     VALUE SPACES.      INKRP101
00076          10  FILLER              PIC X(01)     VALUE SPACES.      INKRP101
00077          10  PV-DD               PIC X(02)     VALUE SPACES.      INKRP101
25248      05 PV-EDIT-LOC-NBR          PIC S9(04) COMP VALUE ZEROS.             
25248      05 PV-EDIT-UPC-CODE         PIC 9(15)     VALUE ZEROS.               
25248      05 PV-EDIT-UPC-CODE-ALPHA REDEFINES                                  
25248         PV-EDIT-UPC-CODE         PIC X(15).                               
00079 *                                                                 INKRP101
00080  01  FH-FORMAT-HOLD.                                              INKRP101
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
W26600     05  FH-SHEET                PIC  ZZZZZ9         VALUE ZEROS. INKRP101
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
W26600     05  FH-LINE                 PIC  Z9             VALUE ZEROS. INKRP101
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
W26600     05  FH-SKU                  PIC  X(08)          VALUE SPACE.         
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
W26600     05  FH-UPC                  PIC  X(15)          VALUE SPACE.         
SR3403     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
SR3403     05  FH-PRICE                PIC  ZZZZZ.99       VALUE ZEROS. INKRP101
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
W26600     05  FH-QUANTITY             PIC  ZZZ9           VALUE ZEROS. INKRP101
W26600     05  FILLER                  PIC  X(01)          VALUE SPACE. INKRP101
SR3403     05  FH-RETAIL               PIC  ZZZ,ZZZ.99     VALUE ZEROS. INKRP101
00098 *                                                                 INKRP101
00099  01  FF-FETCH-FIELDS.                                             INKRP101
00100      05  FF-OP-CO-NMBR           PIC X(02)   VALUE SPACES.        INKRP101
00101      05  FF-STORE-NMBR           PIC 9(04)   VALUE ZEROS.         INKRP101
00102      05  FF-STORE-NAME           PIC X(25)   VALUE SPACES.        INKRP101
00103 *                                                                 INKRP101
00104  01  SW-SWITCHES.                                                 INKRP101
00105      05  SW-END-OF-INVEN-FILE    PIC X       VALUE 'N'.           INKRP101
00106          88  SW-EOF-INVEN-FILE               VALUE 'Y'.           INKRP101
00107      05  SW-FIRST-STORE-SWITCH   PIC X       VALUE 'Y'.           INKRP101
00108          88  SW-FIRST-STORE                  VALUE 'Y'.           INKRP101
00109          88  SW-NOT-FIRST-STORE              VALUE 'N'.           INKRP101
00110 *                                                                 INKRP101
00111  01  CPU-DATE.                                                    INKRP101
00112      05  CPU-YEAR                    PIC X(02)     VALUE SPACES.  INKRP101
00113      05  CPU-MONTH                   PIC X(02)     VALUE SPACES.  INKRP101
00114      05  CPU-DAY                     PIC X(02)     VALUE SPACES.  INKRP101
00115 *                                                                 INKRP101
00116  01  CPU-TIME.                                                    INKRP101
00117      05  CPU-HOUR                    PIC X(02)     VALUE SPACES.  INKRP101
00118      05  CPU-MINUTE                  PIC X(02)     VALUE SPACES.  INKRP101
00119      05  FILLER                      PIC X(02)     VALUE SPACES.  INKRP101
00120 *                                                                 INKRP101
00124  01  CV-CONSTANT-VALUES.                                          INKRP101
25248      05  CV-ENTRY-MAX                PIC S9(02)    VALUE +02.     INKRP101
00126      05  CV-DETAIL-MAX               PIC S9(02)    VALUE +50.     INKRP101
00127      05  CV-DASH                     PIC X         VALUE '-'.     INKRP101
00128      05  CV-DOLLAR-MAX               PIC S9(3)V99  VALUE +999.99. INKRP101
00129      05  CV-MAX-CNT                  PIC S9(3)     VALUE +099.    INKRP101
25248      05  CV-MAX-RETRIES              PIC S9(03)    VALUE +02      INKRP101
00131                                                    COMP-3.        INKRP101
W26600                                                                          
W26600******************************************************************        
W26600 01  NI-NULL-INDICATORS.                                                  
W26600     05  NI-LOC-10-ABBR-NM       PIC S9(04)          VALUE +0             
W26600                                                     COMP.                
00132                                                                   INKRP101
00133 *                                                                 INKRP101
00134  01  DT-DETAIL-TABLE.                                             INKRP101
00135      05  DT-DESCRIPTION              PIC X(20)     VALUE SPACES.  INKRP101
25248      05  DT-ENTRY OCCURS 2.                                       INKRP101
00137          10  DT-DETAIL OCCURS 50.                                 INKRP101
00138              15  DT-ERROR-CODE       PIC X.                       INKRP101
00139              15  DT-SHEET            PIC  9(06)    COMP-3.        INKRP101
00140              15  DT-LINE             PIC  9(02)      COMP-3.      INKRP101
W26600             15  DT-SKU          PIC  X(08).                              
W26600             15  FILLER REDEFINES DT-SKU.                                 
W26600                 25  DT-DEPT-CL  PIC  X(05).                              
W26600                 25  DT-SKU-SEQ  PIC  X(03).                              
W26600             15  DT-UPC          PIC  X(15).                              
SR3403             15  DT-PRICE        PIC S9(05)V99       COMP-3.      INKRP101
W26600             15  DT-QUANTITY     PIC S9(05)          COMP-3.      INKRP101
W26600             15  DT-EXTENDED-RETAIL                               INKRP101
W26600                                 PIC S9(7)V99        COMP-3.      INKRP101
00147 *                                                                 INKRP101
00148  01  SS-SUBSCRIPTS.                                               INKRP101
00149      05  SS-ENTRY-SUB        PIC S9(04)  VALUE ZERO COMP SYNC.    INKRP101
00150      05  SS-DETAIL-SUB       PIC S9(04)  VALUE ZERO COMP SYNC.    INKRP101
00151      EJECT                                                        INKRP101
00152  01  H1-HEADING.                                                  INKRP101
00153      05  FILLER              PIC X(34)   VALUE SPACES.            INKRP101
00154      05  FILLER              PIC X(9)    VALUE 'PHYSICAL '.       INKRP101
00155      05  FILLER              PIC X(10)   VALUE 'INVENTORY '.      INKRP101
00156      05  FILLER              PIC X(10)   VALUE 'FOR STORE '.      INKRP101
W26600     05  H1-STORE-NO         PIC 9999    VALUE ZEROS.             INKRP101
00158      05  FILLER              PIC X       VALUE SPACES.            INKRP101
00159      05  H1-STORE-NAME       PIC X(10).                           INKRP101
00160      05  FILLER              PIC X(7)    VALUE ' TAKEN '.         INKRP101
00161      05  H1-MM               PIC XX.                              INKRP101
00162      05  FILLER              PIC X       VALUE '/'.               INKRP101
00163      05  H1-DD               PIC XX.                              INKRP101
00164      05  FILLER              PIC X       VALUE '/'.               INKRP101
00165      05  H1-YY               PIC XX.                              INKRP101
00166      05  FILLER              PIC X(40)   VALUE SPACES.            INKRP101
00167 *                                                                 INKRP101
00168  01  H2-HEADING.                                                  INKRP101
00169      05  FILLER              PIC X(40)   VALUE SPACES.            INKRP101
00170      05  FILLER              PIC X(11)   VALUE 'LISTING OF '.     INKRP101
00171      05  FILLER              PIC X(9)    VALUE 'ITEMS BY '.       INKRP101
W21732     05  FILLER             PIC X(13)    VALUE ' AREA & LINE '.           
00173      05  FILLER              PIC X(10)   VALUE 'NUMBER    '.      INKRP101
00174      05  FILLER              PIC X(49)   VALUE SPACES.            INKRP101
00175 *                                                                 INKRP101
00177  01  H3-HEADING.                                                  INKRP101
W26600     05  FILLER                  PIC  X(39)          VALUE SPACE. INKRP101
W26600     05  FILLER                  PIC  X(21)          VALUE        INKRP101
W26600         'RETAIL       EXTENDED'.                                         
W26600     05  FILLER                  PIC  X(40)          VALUE SPACE. INKRP101
W26600     05  FILLER                  PIC  X(21)          VALUE        INKRP101
W26600         'RETAIL       EXTENDED'.                                         
25248 *    05  FILLER                  PIC  X(44)          VALUE        INKRP101
25248 *        '                         RETAIL       EXTENDED'.        INKRP101
00185                                                                   INKRP101
00186  01  H4-HEADING.                                                  INKRP101
W26600     05  FILLER                  PIC  X(03)          VALUE SPACE. INKRP101
W26600     05  FILLER                  PIC  X(20)          VALUE        INKRP101
W26600         'AREA LN SKU      UPC'.                                          
W26600     05  FILLER                  PIC  X(17)          VALUE SPACE. INKRP101
W26600     05  FILLER                  PIC  X(20)          VALUE        INKRP101
W26600         'PRICE  QTY    RETAIL'.                                          
W26600     05  FILLER                  PIC  X(04)          VALUE SPACE.         
W26600     05  FILLER                  PIC  X(20)          VALUE        INKRP101
W26600         'AREA LN SKU      UPC'.                                          
W26600     05  FILLER                  PIC  X(17)          VALUE SPACE. INKRP101
W26600     05  FILLER                  PIC  X(20)          VALUE        INKRP101
W26600         'PRICE  QTY    RETAIL'.                                          
25248 *    05  FILLER                  PIC  X(44)          VALUE        INKRP101
25248 *        '   AREA LN   SKU       PRICE   QTY   RETAIL '.                  
00193                                                                   INKRP101
00195  01  D1-DETAIL.                                                   INKRP101
W26600     05  D1-ENTRY OCCURS 2.                                       INKRP101
W26600         10  FILLER              PIC X(61).                       INKRP101
00198 *                                                                 INKRP101
00199  01  D2-DETAIL.                                                   INKRP101
00200      05  FILLER              PIC X(114)       VALUE SPACES.       INKRP101
00201      05  D2-MSG              PIC X(12)        VALUE SPACES.       INKRP101
00202      05  D2-SHEET-NUMBER     PIC X(6)         VALUE SPACES.       INKRP101
00203 *                                                                 INKRP101
00204  01  MSG-MESSAGES.                                                INKRP101
00205      05  MSG-FIRST           PIC X(12)        VALUE               INKRP101
W21732     'FIRST AREA  '.                                              INKRP101
00207      05  MSG-LAST            PIC X(12)        VALUE               INKRP101
W21732     'LAST-AREA   '.                                              INKRP101
00209 *                                                                 INKRP101
00210  01  T1-TOTAL.                                                    INKRP101
00211      05  FILLER              PIC X VALUE SPACES.                  INKRP101
00212      05  T1-NAME             PIC X(12) VALUE SPACES.              INKRP101
00213      05  T1-NAME-A REDEFINES T1-NAME.                             INKRP101
00214          10  T1-NAME-B       PIC X(5).                            INKRP101
00215          10  FILLER          PIC X.                               INKRP101
00216          10  T1-NAME-NO      PIC X(6).                            INKRP101
00217          10  FILLER  REDEFINES  T1-NAME-NO.                       INKRP101
00218              15  T1-DEPT     PIC XXX.                             INKRP101
00219              15  T1-DASH     PIC X.                               INKRP101
00220              15  T1-CLASS    PIC XX.                              INKRP101
00221      05  FILLER              PIC X(7) VALUE ' TOTAL '.            INKRP101
00222      05  FILLER              PIC X(9) VALUE 'QUANTITY '.          INKRP101
00223      05  T1-QTY              PIC ZZ,ZZZ,ZZZ.                      INKRP101
00224      05  FILLER              PIC X(8) VALUE '  TOTAL '.           INKRP101
00225      05  FILLER              PIC X(7) VALUE 'RETAIL '.            INKRP101
00226      05  T1-RETAIL           PIC ZZ,ZZZ,ZZZ.99.                   INKRP101
00227      EJECT                                                        INKRP101
00228                                                                   INKRP101
00229 *  *******************************************                    INKRP101
00230 *     TOTAL FILE LISTS AND I/O AREAS         *                    INKRP101
00231 *  *******************************************                    INKRP101
00232                                                                   INKRP101
00233 * COPY FOR STANDARD HEADING                                       INKRP101
00234      COPY DPWS132O.                                               INKRP101
00235 *                                                                 INKRP101
00236 ***************************************************************** INKRP101
00237 *  EDITED INVENTORY RECORD - INPUT                                INKRP101
00238 ***************************************************************** INKRP101
25248      COPY INWS009.                                                INKRP101
00240 *                                                                 INKRP101
00241 ***************************************************************** INKRP101
00242 *  SQL ABEND ROUTINE WORKING STORAGE                              INKRP101
00243 ***************************************************************** INKRP101
00244 *                                                                 INKRP101
00245      COPY DPWS004.                                                INKRP101
00246 *                                                                 INKRP101
  |   *****************************************************************         
  |   * XMT_LOC DCLGEN                                                          
  |   *****************************************************************         
  |        EXEC SQL                                                             
  |           INCLUDE XMT00001                                                  
  |        END-EXEC.                                                            
25248                                                                           
00254      EXEC SQL                                                     INKRP101
00255          INCLUDE SQLCA                                            INKRP101
00256      END-EXEC.                                                    INKRP101
00257 *                                                                 INKRP101
00258 ***************************************************************** INKRP101
00259 * DB2 INVNETORY PARAMETERS DCLGEN.                                INKRP101
00260 ***************************************************************** INKRP101
00261      EXEC SQL                                                     INKRP101
00262          INCLUDE TINVPAR                                          INKRP101
00263      END-EXEC.                                                    INKRP101
00264 *                                                                 INKRP101
P28545***************************************************************** INKRP101
P28545* DB2 INVENTORY CONTRO TABLE DB2 DCLGEN                           INKRP101
P28545***************************************************************** INKRP101
P28545     EXEC SQL                                                     INKRP101
P28545         INCLUDE TINCNTL                                          INKRP101
P28545     END-EXEC.                                                    INKRP101
P28545                                                                  INKRP101
00265      COPY SQLCA2.                                                 INKRP101
00266 *                                                                 INKRP101
00267 ***************************************************************** INKRP101
00268 * DB2 INVENTORY PARAMETERS CURSOR.                                INKRP101
00269 ***************************************************************** INKRP101
00270      EXEC SQL                                                     INKRP101
00271         DECLARE INV_PARMS_CURSOR CURSOR FOR                       INKRP101
00272           SELECT PLND_INV_DTE                                     INKRP101
P28545            FROM TINVPAR A,                                       INKRP101
P28545                 TINCNTL B                                                
P28545            WHERE B.ACTV_IND = 'Y'                                        
P28545              AND A.INV_ID = B.INV_ID                                     
W33304              AND A.LOC_INV_STAT_CDE = 'IN'                               
W33304              AND ACTL_FIN_BK_DTE = '9999-09-09'                          
P28545              AND (LOC_NBR = :INVPAR-LOC-NBR)                     INKRP101
00275             ORDER BY 1 DESC                                       INKRP101
P28545            WITH UR                                               INKRP101
00277      END-EXEC.                                                    INKRP101
00278                                                                   INKRP101
00279                                                                   INKRP101
00280  LINKAGE SECTION.                                                 INKRP101
00281      EJECT                                                        INKRP101
00282  PROCEDURE DIVISION.                                              INKRP101
00283 *                                                                 INKRP101
00284 ******************************************************************INKRP101
00285 *                                                                 INKRP101
00286 ******************************************************************INKRP101
00287  A100-MAIN-MODULE.                                                INKRP101
00288                                                                   INKRP101
00289      PERFORM B100-OPEN-FILES.                                     INKRP101
00290      PERFORM Z100-READ-INVENTORY-FILE.                            INKRP101
00291                                                                   INKRP101
00292      PERFORM B050-PROCESS-STORE                                   INKRP101
00293              UNTIL SW-EOF-INVEN-FILE.                             INKRP101
00294                                                                   INKRP101
00295      PERFORM B600-CLOSE-FILES.                                    INKRP101
00296      STOP RUN.                                                    INKRP101
00297                                                                   INKRP101
00298 ******************************************************************INKRP101
00299 *                                                                 INKRP101
00300 ******************************************************************INKRP101
00301  B050-PROCESS-STORE.                                              INKRP101
00302      MOVE IN009-STORE-NUMBER TO SA-STORE-NUMBER                   INKRP101
W26600                                H1-STORE-NO                       INKRP101
W26600                                INVPAR-LOC-NBR.                   INKRP101
00304                                                                   INKRP101
00305      PERFORM B200-READ-INV-PARMS-TABLE.                           INKRP101
00306                                                                   INKRP101
00307          IF SW-FIRST-STORE                                        INKRP101
00308              PERFORM B300-INITIALIZE                              INKRP101
00310              SET SW-NOT-FIRST-STORE TO TRUE                       INKRP101
00311          END-IF.                                                  INKRP101
00312          MOVE SPACES TO REPORT-LINE.                              INKRP101
00313          WRITE REPORT-LINE                                        INKRP101
00314              AFTER ADVANCING PAGE.                                INKRP101
00315          MOVE 0 TO PC-PAGE-COUNT.                                 INKRP101
00316          MOVE INVPAR-PLND-INV-DTE  TO PV-INVENTORY-DATE.          INKRP101
00317          MOVE PV-MM    TO H1-MM.                                  INKRP101
00318          MOVE PV-DD    TO H1-DD.                                  INKRP101
00319          MOVE PV-YY    TO H1-YY.                                  INKRP101
00320          MOVE IN009-AREA-NUMBER TO SA-CURR-SHEET                  INKRP101
00321                                    SA-FIRST-SHEET.                INKRP101
00322          MOVE IN009-LINE-NUMBER TO SA-CURR-LINE.                  INKRP101
00323          PERFORM Z200-READ-STORE-DB.                              INKRP101
W26600         MOVE XMT00001-LOC-10-ABBR-NM                             INKRP101
W26600                                 TO H1-STORE-NAME.                INKRP101
00325          INITIALIZE   SS-ENTRY-SUB                                INKRP101
00326                       SS-DETAIL-SUB                               INKRP101
00327                       DT-DETAIL-TABLE.                            INKRP101
00328          PERFORM B500-PROCESS-RECORDS                             INKRP101
00329              UNTIL SW-EOF-INVEN-FILE OR                           INKRP101
00330                    IN009-STORE-NUMBER NOT = SA-STORE-NUMBER.      INKRP101
00331                                                                   INKRP101
00332          PERFORM Z400-PRINT-HEADINGS.                             INKRP101
00333          PERFORM D100-UNLOAD-DETAIL-TABLE.                        INKRP101
00334          MOVE SA-PRIOR-SHEET TO SA-LAST-SHEET.                    INKRP101
00335          PERFORM C300-PRINT-BEGIN-END-SHEET.                      INKRP101
00336 *                                                                 INKRP101
00337 ******************************************************************INKRP101
00338 *                                                                 INKRP101
00339 ******************************************************************INKRP101
00340  B100-OPEN-FILES.                                                 INKRP101
00341                                                                   INKRP101
00342      OPEN INPUT  PHY-INVENTORY-FILE                               INKRP101
00343           OUTPUT PHY-INVENTORY-REPORT.                            INKRP101
00345                                                                   INKRP101
00346 ******************************************************************INKRP101
00347 *                                                                 INKRP101
00348 ******************************************************************INKRP101
00349  B200-READ-INV-PARMS-TABLE.                                       INKRP101
00350 *                                                                 INKRP101
00351      PERFORM Z500-OPEN-INV-PARMS-CURSOR.                          INKRP101
00352      PERFORM Z600-FETCH-INV-PARMS-ROW.                            INKRP101
00353      PERFORM Z700-CLOSE-INV-PARMS-CURSOR.                         INKRP101
00354 *                                                                 INKRP101
00355 ******************************************************************INKRP101
00356 *                                                                 INKRP101
00357 ******************************************************************INKRP101
00358  B300-INITIALIZE.                                                 INKRP101
00359      ACCEPT CPU-DATE FROM DATE.                                   INKRP101
00360      MOVE CPU-YEAR TO DP132O-RUN-YEAR.                            INKRP101
00361      MOVE CPU-MONTH TO DP132O-RUN-MONTH.                          INKRP101
00362      MOVE CPU-DAY TO DP132O-RUN-DAY.                              INKRP101
00363      ACCEPT CPU-TIME FROM TIME.                                   INKRP101
00364      MOVE CPU-HOUR TO DP132O-RUN-HOUR.                            INKRP101
00365      MOVE CPU-MINUTE TO DP132O-RUN-MINUTE.                        INKRP101
00366      MOVE 'INKRP101' TO DP132O-PROGRAM-NAME.                      INKRP101
00367 *                                                                 INKRP101
00386 *                                                                 INKRP101
00387 ******************************************************************INKRP101
00388 *                                                                 INKRP101
00389 ******************************************************************INKRP101
00390  B500-PROCESS-RECORDS.                                            INKRP101
00391                                                                   INKRP101
00392      IF (SS-ENTRY-SUB IS EQUAL TO CV-ENTRY-MAX) AND               INKRP101
00393         (SS-DETAIL-SUB IS EQUAL TO CV-DETAIL-MAX)                 INKRP101
00394           MOVE SA-PRIOR-SHEET TO SA-LAST-SHEET                    INKRP101
00395           PERFORM Z400-PRINT-HEADINGS                             INKRP101
00396           PERFORM D100-UNLOAD-DETAIL-TABLE                        INKRP101
00397           INITIALIZE DT-DETAIL-TABLE                              INKRP101
00398                      SS-ENTRY-SUB                                 INKRP101
00399                      SS-DETAIL-SUB                                INKRP101
00400           PERFORM C300-PRINT-BEGIN-END-SHEET                      INKRP101
00401      END-IF.                                                      INKRP101
00402      PERFORM C200-LOAD-TABLE-DETAIL.                              INKRP101
00403      MOVE SA-CURR-KEY TO SA-PRIOR-KEY.                            INKRP101
00404      PERFORM Z100-READ-INVENTORY-FILE.                            INKRP101
00405      MOVE IN009-AREA-NUMBER TO SA-CURR-SHEET.                     INKRP101
00406      MOVE IN009-LINE-NUMBER TO SA-CURR-LINE.                      INKRP101
00407 *                                                                 INKRP101
00408 ******************************************************************INKRP101
00409 *                                                                 INKRP101
00410 ******************************************************************INKRP101
00411  B600-CLOSE-FILES.                                                INKRP101
00412                                                                   INKRP101
00413      CLOSE       PHY-INVENTORY-FILE                               INKRP101
00414                  PHY-INVENTORY-REPORT.                            INKRP101
00415                                                                   INKRP101
00424 *                                                                 INKRP101
00425 ******************************************************************INKRP101
00426 * THE SUBSCRIPTS ARE INCREMENTED AND THE INPUT DATA IS LOADED.    INKRP101
00427 *                                                                 INKRP101
00428 ******************************************************************INKRP101
00429  C200-LOAD-TABLE-DETAIL.                                          INKRP101
00430      IF SS-ENTRY-SUB IS EQUAL TO ZERO                             INKRP101
00431          ADD 1 TO SS-ENTRY-SUB                                    INKRP101
00432          MOVE IN009-AREA-NUMBER TO SA-FIRST-SHEET                 INKRP101
00433      END-IF.                                                      INKRP101
00434      IF SS-DETAIL-SUB IS EQUAL TO CV-DETAIL-MAX OR                INKRP101
00435         SS-DETAIL-SUB IS GREATER THAN CV-DETAIL-MAX               INKRP101
00436          ADD 1 TO SS-ENTRY-SUB                                    INKRP101
00437          MOVE 1 TO SS-DETAIL-SUB                                  INKRP101
00438      ELSE                                                         INKRP101
00439          ADD 1 TO SS-DETAIL-SUB                                   INKRP101
00440      END-IF.                                                      INKRP101
00441      MOVE IN009-AREA-NUMBER                                       INKRP101
00442          TO DT-SHEET (SS-ENTRY-SUB, SS-DETAIL-SUB).               INKRP101
00443      MOVE IN009-LINE-NUMBER                                       INKRP101
00444          TO DT-LINE (SS-ENTRY-SUB, SS-DETAIL-SUB).                INKRP101
25248                                                                           
W26600     MOVE IN009-SKU              TO DT-SKU                                
W26600                                    (SS-ENTRY-SUB, SS-DETAIL-SUB).        
W26600                                                                          
W26600     IF IN009-UPC-CODE NUMERIC                                            
W26600     AND IN009-UPC-CODE > 0                                               
W26600         MOVE IN009-UPC-CODE     TO PV-EDIT-UPC-CODE                      
W26600         MOVE PV-EDIT-UPC-CODE-ALPHA                                      
W26600                                 TO DT-UPC                                
W26600                                    (SS-ENTRY-SUB, SS-DETAIL-SUB)         
W26600     ELSE                                                                 
W26600         MOVE SPACE              TO DT-UPC                                
W26600                                    (SS-ENTRY-SUB, SS-DETAIL-SUB)         
25248      END-IF.                                                              
25248                                                                           
00451      MOVE IN009-PRICE                                             INKRP101
00452          TO DT-PRICE (SS-ENTRY-SUB, SS-DETAIL-SUB).               INKRP101
00453      MOVE IN009-QUANTITY-COMP                                     INKRP101
00454          TO DT-QUANTITY (SS-ENTRY-SUB, SS-DETAIL-SUB).            INKRP101
00455      COMPUTE PV-EXTENDED-RETAIL =                                 INKRP101
00456          IN009-QUANTITY-COMP  * IN009-PRICE.                      INKRP101
00457      MOVE PV-EXTENDED-RETAIL                                      INKRP101
00458          TO DT-EXTENDED-RETAIL (SS-ENTRY-SUB, SS-DETAIL-SUB).     INKRP101
00459 *                                                                 INKRP101
00460 ******************************************************************INKRP101
00461 * C300-PRINT-BEGIN-END-SHEET                                      INKRP101
00462 ******************************************************************INKRP101
00463  C300-PRINT-BEGIN-END-SHEET.                                      INKRP101
00464      MOVE SA-FIRST-SHEET TO D2-SHEET-NUMBER.                      INKRP101
00465      MOVE MSG-FIRST      TO D2-MSG.                               INKRP101
00466      MOVE D2-DETAIL TO REPORT-LINE.                               INKRP101
00467      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00468      MOVE 1 TO PC-ADVANCE.                                        INKRP101
00469      MOVE SA-LAST-SHEET  TO D2-SHEET-NUMBER.                      INKRP101
00470      MOVE MSG-LAST       TO D2-MSG.                               INKRP101
00471      MOVE D2-DETAIL TO REPORT-LINE.                               INKRP101
00472      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00473 *                                                                 INKRP101
00474 ******************************************************************INKRP101
00475 * D100-UNLOAD-DETAIL-TABLE  SUBSCRIPT FOR ROWS IS INCREMENTED     INKRP101
00476 ******************************************************************INKRP101
00477  D100-UNLOAD-DETAIL-TABLE.                                        INKRP101
00478      PERFORM E100-UNLOAD-DETAIL                                   INKRP101
00479          VARYING SS-DETAIL-SUB FROM 1 BY 1 UNTIL SS-DETAIL-SUB    INKRP101
00480          IS GREATER THAN CV-DETAIL-MAX.                           INKRP101
00481 *                                                                 INKRP101
00482 ******************************************************************INKRP101
00483 * E100-UNLOAD-DETAIL   SUBSCRIPT FOR COLUMNS IS INCREMENTED AND   INKRP101
00484 * NON BLANK DETAIL LINES ARE SENT TO PRINT ROUTINE                INKRP101
00485 ******************************************************************INKRP101
00486  E100-UNLOAD-DETAIL.                                              INKRP101
00487      PERFORM F100-FORMAT-REPORT-LINE                              INKRP101
00488          VARYING SS-ENTRY-SUB FROM 1 BY 1 UNTIL SS-ENTRY-SUB      INKRP101
00489          IS GREATER THAN CV-ENTRY-MAX.                            INKRP101
00490      IF D1-DETAIL IS GREATER THAN SPACES                          INKRP101
00491          MOVE D1-DETAIL TO REPORT-LINE                            INKRP101
00492          PERFORM Z300-WRITE-REPORT-LINE                           INKRP101
00493          MOVE 1 TO PC-ADVANCE                                     INKRP101
00494      END-IF.                                                      INKRP101
00495      MOVE 1 TO SS-ENTRY-SUB.                                      INKRP101
00496 *                                                                 INKRP101
00497 ******************************************************************INKRP101
00498 * F100-FORMAT-REPORT-LINE   IF A TABLE ENTRY CONTAINS VALUES THE  INKRP101
00499 * DATA IS FORMATTED IN A HOLD AREA AND THEN MOVED TO THE PROPER   INKRP101
00500 * COLUMN OF THE DETAIL LINE.  IF THE TOTAL RETAIL AMOUNT IS GREATEINKRP101
00501 * THAN $999.99  THEN THE ENTRY WILL ALSO BE WRITTEN TO THE EXCEPTIINKRP101
00502 * REPORT.                                                         INKRP101
00503 ******************************************************************INKRP101
00504  F100-FORMAT-REPORT-LINE.                                         INKRP101
00505      IF DT-SHEET(SS-ENTRY-SUB, SS-DETAIL-SUB) IS GREATER THAN ZEROINKRP101
00506          MOVE DT-SHEET(SS-ENTRY-SUB, SS-DETAIL-SUB) TO FH-SHEET   INKRP101
00507          MOVE DT-LINE(SS-ENTRY-SUB, SS-DETAIL-SUB) TO FH-LINE     INKRP101
00511                                                                   INKRP101
C21732*        IF DT-SKU-SEQ (SS-ENTRY-SUB, SS-DETAIL-SUB)                      
C21732*                             NOT EQUAL '000'                             
W26600         MOVE DT-SKU (SS-ENTRY-SUB, SS-DETAIL-SUB)                        
W26600                                 TO FH-SKU                                
W26600         MOVE DT-UPC (SS-ENTRY-SUB, SS-DETAIL-SUB)                        
W26600                                 TO FH-UPC                                
C21732*        END-IF                                                   INKRP101
00521          MOVE DT-QUANTITY(SS-ENTRY-SUB, SS-DETAIL-SUB)            INKRP101
00522              TO FH-QUANTITY                                       INKRP101
00523          MOVE DT-PRICE(SS-ENTRY-SUB, SS-DETAIL-SUB) TO FH-PRICE   INKRP101
00524          MOVE DT-EXTENDED-RETAIL(SS-ENTRY-SUB, SS-DETAIL-SUB)     INKRP101
00525              TO FH-RETAIL                                         INKRP101
00526          MOVE FH-FORMAT-HOLD TO D1-ENTRY (SS-ENTRY-SUB)           INKRP101
00527          INITIALIZE FH-FORMAT-HOLD                                INKRP101
00528      ELSE                                                         INKRP101
00529          MOVE SPACES TO D1-ENTRY(SS-ENTRY-SUB)                    INKRP101
00530      END-IF.                                                      INKRP101
00531 *                                                                 INKRP101
00532                                                                   INKRP101
00533 ******************************************************************INKRP101
00534 * Z100-READ-INVENTORY-FILE  READ FOR EDITED INVENTORY FILE        INKRP101
00535 ******************************************************************INKRP101
00536  Z100-READ-INVENTORY-FILE.                                        INKRP101
00537      READ PHY-INVENTORY-FILE                                      INKRP101
00538          INTO IN009-EDITED-INVENTORY-RECORD                       INKRP101
00539              AT END                                               INKRP101
00540                 MOVE 'Y' TO SW-END-OF-INVEN-FILE.                 INKRP101
00541 *                                                                 INKRP101
00542 ***************************************************************** INKRP101
00543 * Z200-READ-STORE-DB   READS THE DB2 STORE TABLE                  INKRP101
00544 ***************************************************************** INKRP101
00545  Z200-READ-STORE-DB.                                              INKRP101
00546                                                                   INKRP101
00547      PERFORM                                                      INKRP101
00548          WITH TEST AFTER                                          INKRP101
00549          VARYING PV-SQL-SUB                                       INKRP101
00550          FROM 1 BY 1                                              INKRP101
00551          UNTIL (PV-SQL-SUB > CV-MAX-RETRIES                       INKRP101
00552                    OR SQLCODE = 0                                 INKRP101
00553                    OR SQLCODE = +100)                             INKRP101
00554                                                                   INKRP101
  |            MOVE SA-STORE-NUMBER-NUM TO PV-EDIT-LOC-NBR                      
  |            MOVE PV-EDIT-LOC-NBR     TO XMT00001-LOC-NBR                     
  |                                                                             
  |            EXEC SQL                                                         
W26600             SELECT LOC_NBR,                                              
W26600                    LOC_NM,                                               
W26600                    LOC_10_ABBR_NM                                        
W26600             INTO :XMT00001-LOC-NBR,                                      
W26600                  :XMT00001-LOC-NM,                                       
W26600                  :XMT00001-LOC-10-ABBR-NM:NI-LOC-10-ABBR-NM              
W26600             FROM XMT_LOC                                                 
  |                WHERE LOC_NBR = :XMT00001-LOC-NBR                            
  |            END-EXEC                                                         
25248                                                                           
00567          EVALUATE TRUE                                            INKRP101
00568              WHEN SQLCODE = +100                                  INKRP101
00569                  DISPLAY '*** INKRP101 ABEND ***'                 INKRP101
00570                  DISPLAY '**** NO MATCH FOR STORE '               INKRP101
00571                                           SA-STORE-NUMBER         INKRP101
00572                  DISPLAY '**** ON DB2 STORE TABLE'                INKRP101
00573                  PERFORM Z900-SQL-ABEND                           INKRP101
00574              WHEN SQLCODE = -904                                  INKRP101
00575              WHEN SQLCODE = -911                                  INKRP101
00576              WHEN SQLCODE = 0                                     INKRP101
25248                MOVE XMT00001-LOC-NM  TO FF-STORE-NAME                     
25248                MOVE XMT00001-LOC-NBR TO FF-STORE-NMBR                     
00578              WHEN SQLWARN NOT = SPACES                            INKRP101
00579              WHEN SQLCODE NOT = ZERO                              INKRP101
00580                  MOVE 'Z200-READ-STORE-DB'                        INKRP101
00581                                TO  PV-PARAGRAPH-NAME              INKRP101
00582                  MOVE 'READING STORE TABLE'                       INKRP101
00583                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00584                  PERFORM Z900-SQL-ABEND                           INKRP101
00585          END-EVALUATE                                             INKRP101
00586      END-PERFORM.                                                 INKRP101
00587 *                                                                 INKRP101
00588      IF  PV-SQL-SUB GREATER CV-MAX-RETRIES                        INKRP101
00589          MOVE  'Z200-READ-STORE-DB'                               INKRP101
00590                                TO  PV-PARAGRAPH-NAME              INKRP101
00591          MOVE 'OPEN RETRIES EXCEEDED'                             INKRP101
00592                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00593          PERFORM Z900-SQL-ABEND                                   INKRP101
00594      END-IF.                                                      INKRP101
00595                                                                   INKRP101
00596      IF SQLCODE = +100                                            INKRP101
00597          MOVE +4008                 TO ABEND-CODE                 INKRP101
00598          DISPLAY '**  ABEND     **'                               INKRP101
00599          DISPLAY '**  PROGRAM   **  =  INKRP101'                  INKRP101
00600          DISPLAY 'NO INVENTORY PARAMETERS RECORD EXISTS'          INKRP101
00601          DISPLAY 'FOR THIS STORE : '  SA-STORE-NUMBER             INKRP101
00602          CALL 'ILBOABN0'  USING ABEND-CODE                        INKRP101
00603      END-IF.                                                      INKRP101
00605                                                                   INKRP101
00606 ***************************************************************** INKRP101
00607 * Z300-WRITE-REPORT-LINE  WRITE ROUTINE FOR DEPT/CLASS REPORT     INKRP101
00608 ***************************************************************** INKRP101
00609  Z300-WRITE-REPORT-LINE.                                          INKRP101
00610      WRITE REPORT-LINE                                            INKRP101
00611        AFTER ADVANCING PC-ADVANCE.                                INKRP101
00612                                                                   INKRP101
00613 ******************************************************************INKRP101
00614 * Z400-PRINT-HEADINGS  HEADING ROUTINE FOR DEPT/CLASS REPORT      INKRP101
00615 ******************************************************************INKRP101
00616  Z400-PRINT-HEADINGS.                                             INKRP101
00617      ADD 1 TO PC-PAGE-COUNT.                                      INKRP101
00618      MOVE PC-PAGE-COUNT TO DP132O-PAGE-NUMBER.                    INKRP101
00619      MOVE 1 TO DP132O-REPORT-NUMBER.                              INKRP101
00620      MOVE DP132O-STANDRD-HEADER-132-COLS TO REPORT-LINE.          INKRP101
00621      WRITE REPORT-LINE                                            INKRP101
00622          AFTER ADVANCING PAGE.                                    INKRP101
00623      MOVE H1-HEADING TO REPORT-LINE.                              INKRP101
00624      MOVE 1 TO PC-ADVANCE.                                        INKRP101
00625      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00626      MOVE H2-HEADING TO REPORT-LINE.                              INKRP101
00627      MOVE 1 TO PC-ADVANCE.                                        INKRP101
00628      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00629      MOVE H3-HEADING TO REPORT-LINE.                              INKRP101
00630      MOVE 2 TO PC-ADVANCE.                                        INKRP101
00631      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00632      MOVE H4-HEADING TO REPORT-LINE.                              INKRP101
00633      MOVE 1 TO PC-ADVANCE.                                        INKRP101
00634      PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00635      MOVE SPACES TO REPORT-LINE.                                  INKRP101
W26600     PERFORM Z300-WRITE-REPORT-LINE.                              INKRP101
00636 *                                                                 INKRP101
00637 *                                                                 INKRP101
00638 ******************************************************************INKRP101
00639 *    OPEN THE CURSOR WHICH WILL RETRIEVE ALL ROWS FROM THE INV    INKRP101
00640 *    PARAMETERS TABLE FOR THE STORE ON THE INPUT FILE.            INKRP101
00641 ******************************************************************INKRP101
00642  Z500-OPEN-INV-PARMS-CURSOR.                                      INKRP101
00643                                                                   INKRP101
00644 *    INITIALIZE DCLTINVPAR.                                       INKRP101
00645                                                                   INKRP101
00646      PERFORM                                                      INKRP101
00647          WITH TEST AFTER                                          INKRP101
00648          VARYING PV-SQL-SUB                                       INKRP101
00649          FROM 1 BY 1                                              INKRP101
00650          UNTIL (PV-SQL-SUB > CV-MAX-RETRIES                       INKRP101
00651                    OR SQLCODE = 0                                 INKRP101
00652                    OR SQLCODE = +100)                             INKRP101
00653                                                                   INKRP101
00654          EXEC SQL                                                 INKRP101
00655              OPEN INV_PARMS_CURSOR                                INKRP101
00656          END-EXEC                                                 INKRP101
00657                                                                   INKRP101
00658          EVALUATE TRUE                                            INKRP101
00659              WHEN SQLCODE = -904                                  INKRP101
00660              WHEN SQLCODE = -911                                  INKRP101
00661              WHEN SQLCODE = +100                                  INKRP101
00662              WHEN SQLCODE = 0                                     INKRP101
00663                  CONTINUE                                         INKRP101
00664              WHEN SQLWARN NOT = SPACES                            INKRP101
00665              WHEN SQLCODE NOT = ZERO                              INKRP101
00666                  MOVE 'Z500-OPEN-INV-PARMS-CURSOR'                INKRP101
00667                                TO  PV-PARAGRAPH-NAME              INKRP101
00668                  MOVE 'OPEN ON CURSOR'                            INKRP101
00669                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00670                  PERFORM Z900-SQL-ABEND                           INKRP101
00671          END-EVALUATE                                             INKRP101
00672      END-PERFORM.                                                 INKRP101
00673 *                                                                 INKRP101
00674      IF  PV-SQL-SUB GREATER CV-MAX-RETRIES                        INKRP101
00675          MOVE 'Z500-OPEN-INV-PARMS-CURSOR'                        INKRP101
00676                                TO  PV-PARAGRAPH-NAME              INKRP101
00677          MOVE 'OPEN RETRIES EXCEEDED'                             INKRP101
00678                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00679          PERFORM Z900-SQL-ABEND                                   INKRP101
00680      END-IF.                                                      INKRP101
00681                                                                   INKRP101
00682      IF SQLCODE = +100                                            INKRP101
00683          MOVE +4008                 TO ABEND-CODE                 INKRP101
00684          DISPLAY '**  ABEND     **'                               INKRP101
00685          DISPLAY '**  PROGRAM   **  =  INKRP101'                  INKRP101
00686          DISPLAY 'NO INVENTORY PARAMETERS RECORD EXISTS'          INKRP101
00687          DISPLAY 'FOR THIS STORE : '  SA-STORE-NUMBER             INKRP101
00688          CALL 'ILBOABN0'  USING ABEND-CODE                        INKRP101
00689      END-IF.                                                      INKRP101
00690 *                                                                 INKRP101
00691 ******************************************************************INKRP101
00692 *    FETCH THE NEXT ROW OF THE CURSOR.                            INKRP101
00693 ******************************************************************INKRP101
00694  Z600-FETCH-INV-PARMS-ROW.                                        INKRP101
00695                                                                   INKRP101
00696      EXEC SQL                                                     INKRP101
00697          FETCH INV_PARMS_CURSOR                                   INKRP101
00698          INTO  :INVPAR-PLND-INV-DTE                               INKRP101
00699      END-EXEC.                                                    INKRP101
00700                                                                   INKRP101
00701      IF SQLCODE = +100                                            INKRP101
00702          MOVE +4013                 TO ABEND-CODE                 INKRP101
00703          DISPLAY '**  ABEND     **'                               INKRP101
00704          DISPLAY '**  PROGRAM   **  =  INKRP101'                  INKRP101
00705          DISPLAY 'NO INVENTORY PARAMETERS RECORD EXISTS'          INKRP101
00706          DISPLAY 'FOR THIS STORE : '  SA-STORE-NUMBER             INKRP101
00707          CALL 'ILBOABN0'  USING ABEND-CODE                        INKRP101
00708      ELSE                                                         INKRP101
00709          IF SQLCODE NOT = 0                                       INKRP101
00710               MOVE 'Z600-FETCH-INV-PARMS-ROW'                     INKRP101
00711                                        TO PV-PARAGRAPH-NAME       INKRP101
00712               MOVE 'FETCH ROW FROM CURSOR'                        INKRP101
00713                                       TO PV-DB2-OPER-ATTEMPTED    INKRP101
00714              PERFORM Z900-SQL-ABEND                               INKRP101
00715          END-IF                                                   INKRP101
00716      END-IF.                                                      INKRP101
00717 *                                                                 INKRP101
00718 ******************************************************************INKRP101
00719 *    CLOSE THE INVENTORY PARAMTERS CURSOR.                        INKRP101
00720 ******************************************************************INKRP101
00721  Z700-CLOSE-INV-PARMS-CURSOR.                                     INKRP101
00722                                                                   INKRP101
00723      PERFORM                                                      INKRP101
00724          WITH TEST AFTER                                          INKRP101
00725          VARYING PV-SQL-SUB                                       INKRP101
00726          FROM 1 BY 1                                              INKRP101
00727          UNTIL (PV-SQL-SUB > CV-MAX-RETRIES                       INKRP101
00728                    OR SQLCODE = 0)                                INKRP101
00729                                                                   INKRP101
00730          EXEC SQL                                                 INKRP101
00731              CLOSE INV_PARMS_CURSOR                               INKRP101
00732          END-EXEC                                                 INKRP101
00733                                                                   INKRP101
00734          EVALUATE TRUE                                            INKRP101
00735              WHEN SQLCODE = -904                                  INKRP101
00736              WHEN SQLCODE = -911                                  INKRP101
00737              WHEN SQLCODE = 0                                     INKRP101
00738                  CONTINUE                                         INKRP101
00739              WHEN SQLWARN NOT = SPACES                            INKRP101
00740              WHEN SQLCODE NOT = ZERO                              INKRP101
00741                  MOVE 'Z700-CLOSE-INV-PARMS-CURSOR'               INKRP101
00742                                TO  PV-PARAGRAPH-NAME              INKRP101
00743                  MOVE 'CLOSE ON CURSOR'                           INKRP101
00744                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00745                  PERFORM Z900-SQL-ABEND                           INKRP101
00746          END-EVALUATE                                             INKRP101
00747      END-PERFORM.                                                 INKRP101
00748 *                                                                 INKRP101
00749      IF  PV-SQL-SUB GREATER CV-MAX-RETRIES                        INKRP101
00750          MOVE 'Z700-CLOSE-INV-PARMS-CURSOR'                       INKRP101
00751                                TO  PV-PARAGRAPH-NAME              INKRP101
00752          MOVE 'CLOSE RETRIES EXCEEDED'                            INKRP101
00753                                TO  PV-DB2-OPER-ATTEMPTED          INKRP101
00754          PERFORM Z900-SQL-ABEND                                   INKRP101
00755      END-IF.                                                      INKRP101
00756                                                                   INKRP101
00757 ******************************************************************INKRP101
00758 *  STANDARD DB2 ERROR ABEND ROUTINE                               INKRP101
00759 *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             INKRP101
00760 ******************************************************************INKRP101
00761  Z900-SQL-ABEND.                                                  INKRP101
00762                                                                   INKRP101
00763      MOVE +4013                 TO ABEND-CODE.                    INKRP101
00764      DISPLAY '**  ABEND     **'.                                  INKRP101
00765      DISPLAY '**  PROGRAM   **  =  INKRP101'.                     INKRP101
00766      DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            INKRP101
00767      DISPLAY '**  OPERATION **  = ' PV-DB2-OPER-ATTEMPTED.        INKRP101
00768                                                                   INKRP101
00769 *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         INKRP101
00770 *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        INKRP101
00771                                                                   INKRP101
00772      COPY DPPD004.                                                INKRP101
00773                                                                   INKRP101
00774      CALL 'ILBOABN0'  USING ABEND-CODE.                           INKRP101
