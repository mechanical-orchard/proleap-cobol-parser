      *----------------------------------------------------------       00010099
       IDENTIFICATION DIVISION.                                         00020099
      *----------------------------------------------------------       00030099
       PROGRAM-ID.             PMKUT127.                                00040099
       AUTHOR.                 KELLY LARUM                              00050099
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00060099
       DATE-WRITTEN.           APRIL 2004.                              00070099
       DATE-COMPILED.                                                   00080099
      *----------------------------------------------------------       00090099
      *          PMKUT127 - EARLY RECEIPT NOTIFICATION EXTRACT          00100099
      *          (DB2)                                                  00101099
      *----------------------------------------------------------       00110099
      * THIS PROGRAM WILL EXTRACT SKUS WHERE THE ACTUAL RECEIPT         00120099
      * DATE IS EARLIER THAN THE EXPECTED RECEIPT DATE                  00130099
      * RUNDATE PASSED IN IS ACTUALLY RUNDATE MINUS 1 DAY.              00140099
      * THE PROGRAM WRITES TO AN OUTPUT FILE THAT IS INPUT TO           00150099
      * PMKRP137 - PROMO EARLY NOTIFICATION REPORT (ORACLE PGM)         00160099
      *----------------------------------------------------------       00310099
      * HISTORY LOG:                                                    00320099
      *                                                                 00321099
      * 05/24/2004 - KBL  CHANGED CURSOR TO EXCLUDE DEPARTMENTS         00322099
      *                   026 AND 830 (HALLMARK AND KCK) FROM           00323099
      *                   THE EXTRACT FILE - THESE DEPARTMENTS          00324099
      *                   DO NOT PUT THEIR MERCHANDISE ON SALE          00325099
      *                   (HALLMARK MAYBE TWICE A YEAR) -               00326099
      *                   NO NEED TO RECEIVE THIS REPORT.               00327099
      *                                                                 00328099
      * 06/03/2004 - KBL  REMOVED CHECK AGAINST TSKSTOO TABLE           00329099
      *                   ONLY LOOK AT TPURITM.                         00329199
      *                                                                 00329299
      * 06/24/2004 - KBL  PUT THE TSKSTOO TABLE CHECK BACK IN           00329399
      *                   THE CHANGE FROM 6/3 MADE THE PROGRAM          00329499
      *                   CRAWL.                                        00329599
      *                                                                 00329699
      * 07/26/2004 - KBL  TOOK THE TSKSTOO TABLE BACK OUT - NOW         00329799
      *                   THERE'S AN INDEX ON TPURITM THAT WILL         00329899
      *                   HELP PERFORMANCE.                             00329999
      *                                                                 00330099
      * 01/30/2005 - KBW  STORE NUMBER EXPANSION PROJECT                00330199
      *                   AND ADDING COMMIT LOGIC                       00330299
      *----------------------------------------------------------       00331099
                                                                        00340099
                                                                        00350099
      *----------------------------------------------------------       00570099
       ENVIRONMENT DIVISION.                                            00580099
      *----------------------------------------------------------       00590099
       CONFIGURATION SECTION.                                           00600099
       SOURCE-COMPUTER. IBM-3090.                                       00610099
       OBJECT-COMPUTER. IBM-3090.                                       00620099
                                                                        00630099
       INPUT-OUTPUT SECTION.                                            00640099
                                                                        00650099
       FILE-CONTROL.                                                    00660099
                                                                        00670099
           SELECT CC-RUN-DTE-FILE                                       00680099
                  ASSIGN TO INP01.                                      00690099
                                                                        00700099
           SELECT EARLY-RECEIPT-FILE                                    00710099
                  ASSIGN TO OUT01.                                      00720099
                                                                        00730099
      *----------------------------------------------------------       00740099
       DATA DIVISION.                                                   00750099
      *----------------------------------------------------------       00760099
       FILE SECTION.                                                    00770099
                                                                        00780099
       FD  CC-RUN-DTE-FILE                                              00790099
           RECORDING MODE F                                             00800099
           BLOCK CONTAINS 0 RECORDS                                     00810099
           LABEL RECORDS ARE OMITTED                                    00820099
           DATA RECORD IS CC-RUN-DTE-REC.                               00830099
       01  CC-RUN-DTE-REC                   PIC X(80).                  00840099
                                                                        00850099
       FD  EARLY-RECEIPT-FILE                                           00860099
           RECORDING MODE F                                             00870099
           BLOCK CONTAINS 0 RECORDS                                     00880099
           LABEL RECORDS ARE OMITTED                                    00890099
           DATA RECORD IS EARLY-RECEIPT-REC.                            00900099
       01  EARLY-RECEIPT-REC                PIC X(300).                 00910099
                                                                        00920099
       WORKING-STORAGE SECTION.                                         00930099
      *----------------------------------------------------------       00940099
      * ABEND- ABEND VARIABLES                                          00950099
      *----------------------------------------------------------       00960099
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.   00970099
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     00980099
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     00990099
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     01000099
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     01010099
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     01020099
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     01030099
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     01040099
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     01050099
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     01060099
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.     01070099
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.     01080099
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     01090099
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     01100099
                                                                        01110099
      *----------------------------------------------------------       01120099
      * CC- CONTROL CARD                                                01130099
      *----------------------------------------------------------       01140099
       01  CC-DATE-RECORD.                                              01150099
           05  CC-RUN-DATE.                                             01160099
              10 CC-RUN-DATE-CC                PIC X(02).               01163099
              10 CC-RUN-DATE-YY                PIC X(02).               01163199
              10 CC-RUN-DATE-MM                PIC X(02).               01164099
              10 CC-RUN-DATE-DD                PIC X(02).               01165099
                                                                        01166099
       01  CC-DATE-MANIPULATION.                                        01166199
           05  PV-RUN-DATE-NUM                 PIC S9(7)V     COMP-3    01166299
                                                       VALUE ZEROS.     01166399
           05 CC-RUN-DATE-NUM-6            PIC  9(06).                  01167199
           05 CC-RUN-DATE-RDF REDEFINES CC-RUN-DATE-NUM-6.              01167299
              10 CC-RUN-DATE-YY-NUM            PIC 9(02).               01167399
              10 CC-RUN-DATE-MM-NUM            PIC 9(02).               01167499
              10 CC-RUN-DATE-DD-NUM            PIC 9(02).               01167599
                                                                        01168099
       01  PV-RUN-DATE                  PIC  X(10).                     01169299
       01  PV-RUN-DATE-RDF REDEFINES PV-RUN-DATE.                       01169399
           05  PV-RUN-DATE-CC           PIC  X(02).                     01169499
           05  PV-RUN-DATE-YY           PIC  X(02).                     01169599
           05  PV-RUN-DATE-D1           PIC  X(01).                     01169699
           05  PV-RUN-DATE-MM           PIC  X(02).                     01169799
           05  PV-RUN-DATE-D2           PIC  X(01).                     01169899
           05  PV-RUN-DATE-DD           PIC  X(02).                     01169999
                                                                        01170099
      *----------------------------------------------------------       01250099
      * PS- PROGRAM SWITCHES                                            01260099
      *----------------------------------------------------------       01270099
       01  PS-PROGRAM-SWITCHES.                                         01280099
           05  PS-CONTROL-CARD-SW           PIC X(01)  VALUE 'N'.       01290099
               88  PS-CONTROL-CARD-EOF                 VALUE 'Y'.       01300099
               88  PS-CONTROL-CARD-NOT-EOF             VALUE 'N'.       01310099
           05  PS-END-OF-SKU-CSR-SW         PIC X(01)  VALUE 'N'.       01320099
               88  PS-END-OF-SKU-CSR                   VALUE 'Y'.       01330099
               88  PS-NOT-END-OF-SKU-CSR               VALUE 'N'.       01340099
      *----------------------------------------------------------       01510099
      * PC- PROGRAM CONSTANTS                                           01520099
      *----------------------------------------------------------       01530099
       01  PC-PROGRAM-CONSTANTS.                                        01540099
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE            01550099
                                                      'PMKUT127'.       01560099
      *----------------------------------------------------------       01580099
      * PV- PROGRAM VARIABLES                                           01590099
      *----------------------------------------------------------       01600099
       01  PV-PROGRAM-VARIABLES.                                        01610099
           05  PV-TABLE-NAME                PIC X(30)  VALUE SPACES.    01620099
           05  PV-SQL-CODE                  PIC 9(09)  VALUE 0.         01630099
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.    01640099
           05  PV-OPERATION-MSG-1           PIC X(40)  VALUE SPACES.    01650199
           05  PV-OPERATION-MSG-2           PIC X(40)  VALUE SPACES.    01651099
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.    01660099
           05  PV-RUN-TIME                  PIC X(26)  VALUE SPACES.    01670099
           05  WS-TMST                      PIC X(26)  VALUE SPACES.    01670199
                                                                        01671099
       01  PV-SELECT-VARIABLES.                                         01673099
           05  PV-GMM-FIRST-NAME            PIC X(12)  VALUE SPACES.    01680099
           05  PV-GMM-LAST-NAME             PIC X(18)  VALUE SPACES.    01690099
           05  PV-GMA-NBR                   PIC X(02)  VALUE SPACES.    01690399
           05  PV-DMM-FIRST-NAME            PIC X(12)  VALUE SPACES.    01700099
           05  PV-DMM-LAST-NAME             PIC X(18)  VALUE SPACES.    01710099
           05  PV-DMA-NBR                   PIC X(02)  VALUE SPACES.    01712099
           05  PV-BUYER-FIRST-NAME          PIC X(12)  VALUE SPACES.    01720099
           05  PV-BUYER-LAST-NAME           PIC X(18)  VALUE SPACES.    01730099
           05  PV-BYR-NBR                   PIC X(03)  VALUE SPACES.    01740099
           05  PV-FIRST-RCVD-DATE           PIC X(10)  VALUE SPACES.    01750099
           05  PV-ITEM-NMBR                 PIC  S9(15)V COMP-3         01761099
                                                       VALUE ZEROES.    01762099
           05  PV-SKU-NBR                   PIC X(08)  VALUE SPACES.    01770099
           05  PV-SKU-DESC                  PIC X(30)  VALUE SPACES.    01771099
           05  PV-DEPT-NBR                  PIC X(04)  VALUE SPACES.    01780099
           05  PV-MAJ-CL-NBR                PIC X(04)  VALUE SPACES.    01790099
           05  PV-SUB-CL-NBR                PIC X(04)  VALUE SPACES.    01800099
           05  PV-VS-ID                     PIC X(08)  VALUE SPACES.    01810099
           05  PV-VS-NBR                    PIC X(20)  VALUE SPACES.    01820099
           05  PV-VS-DESC                   PIC X(30)  VALUE SPACES.    01830099
           05  PV-GP-CDE                    PIC X(03)  VALUE SPACES.    01830199
           05  PV-DEPT-DESC                 PIC X(25)  VALUE SPACES.    01831099
           05  PV-DONT-SHIP-BEF-PLS8        PIC X(10)  VALUE SPACES.    01840099
           05  PV-INTR-UPC-ID               PIC S9(9) COMP VALUE +0.    01840199
           05  PV-UNT-RTL-AMT           PIC S9(7)V9(2) COMP-3 VALUE +0. 01840299
           05  PV-COUNT-ROWS                PIC S9(05) COMP-3 VALUE 0.  01841099
           05  PV-FIRST-RCVD-DTE-NUM        PIC 9(06) VALUE 0.          01850099
           05  PV-FIRST-RCVD-RDF REDEFINES PV-FIRST-RCVD-DTE-NUM.       01860099
               10  PV-FIRST-RCVD-RDF-YY     PIC 9(02).                  01870099
               10  PV-FIRST-RCVD-RDF-MM     PIC 9(02).                  01880099
               10  PV-FIRST-RCVD-RDF-DD     PIC 9(02).                  01890099
           05  PV-FIRST-RCVD-DTE.                                       01900099
               10  PV-FIRST-RCVD-DTE-CC     PIC X(02) VALUE SPACES.     01910099
               10  PV-FIRST-RCVD-DTE-YY     PIC X(02) VALUE SPACES.     01920099
               10  PV-FIRST-RCVD-DTE-D1     PIC X(01) VALUE '-'.        01930099
               10  PV-FIRST-RCVD-DTE-MM     PIC X(02) VALUE SPACES.     01940099
               10  PV-FIRST-RCVD-DTE-D2     PIC X(01) VALUE '-'.        01950099
               10  PV-FIRST-RCVD-DTE-DD     PIC X(02) VALUE SPACES.     01960099
                                                                        01961099
           05  PV-NULL-INDICATORS.                                      01970099
               10  PV-DONT-SHIP-BEF-PLS8-NI PIC S9(04) COMP VALUE 0.    01980099
                                                                        02240099
      *----------------------------------------------------------       03230099
      * PA- PROGRAM ACCUMULATORS                                        03240099
      *----------------------------------------------------------       03250099
       01  PA-PROGRAM-ACCUMULATORS.                                     03260099
           05  PA-RECS-WRITTEN              PIC 9(09) VALUE ZEROES.     03270099
           05  PA-ITEM-CNT                  PIC 9(09) VALUE ZEROES.     03280099
                                                                        03290099
      ****************************************************************  03400099
      * COPYBOOK FOR THE EARLY RECEIPT EXTRACT FILE LAYOUT - OUTPUT     03410099
      ****************************************************************  03420099
           COPY PMRD123.                                                03430099
                                                                        03430199
      ******************************************************************03430299
      *    VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004      03430399
      ******************************************************************03430499
           COPY DPWS004.                                                03430599
                                                                        03430699
      ******************************************************************03430799
      *    TABLE INCLUDES                                               03430899
      ******************************************************************03430999
           EXEC SQL INCLUDE TSKST END-EXEC.                             03431099
           EXEC SQL INCLUDE TSKSTOO END-EXEC.                           03432099
           EXEC SQL INCLUDE TPURITM END-EXEC.                           03432199
           EXEC SQL INCLUDE TUPC END-EXEC.                              03432299
           EXEC SQL INCLUDE TSKXREF END-EXEC.                           03433099
           EXEC SQL INCLUDE TPLNSHP END-EXEC.                           03434099
           EXEC SQL INCLUDE TMDARVS END-EXEC.                           03435099
           EXEC SQL INCLUDE TMDSEAR END-EXEC.                           03436099
           EXEC SQL INCLUDE TRSPBAR END-EXEC.                           03436199
      *    XST_SKU:                                                     03437099
           EXEC SQL INCLUDE XST00010 END-EXEC.                          03438099
      *    XST_SKU_LOC:                                                 03438199
           EXEC SQL INCLUDE XST00008 END-EXEC.                          03438299
      *    XST_VND_STYL:                                                03439099
           EXEC SQL INCLUDE XST00013 END-EXEC.                          03440099
                                                                        03490099
      ******************************************************************03500099
      *    SQL COMMUNICATIONS AREA DCLGEN                               03510099
      ******************************************************************03520099
           EXEC SQL                                                     03530099
               INCLUDE SQLCA                                            03540099
           END-EXEC.                                                    03550099
                                                                        03560099
      ******************************************************************03570099
      *    EXTENSION TO THE SQL COMMUNICATIONS AREA FOR EDITING        *03580099
      *    SQL CODES FOR DISPLAY                                       *03590099
      ******************************************************************03600099
           COPY SQLCA2.                                                 03610099
                                                                        03620099
      ******************************************************************03630099
      *    SKU-CSR                                                      03640099
      *    12/13/04 - ADDED THE WITH HOLD TO THIS CURSOR                03641099
      *    FOR THE COMMIT LOGIC - PREVENTS THE CURSOR FROM              03642099
      *    CLOSING WHEN A COMMIT IS TAKEN.                              03643099
      ******************************************************************03650099
           EXEC SQL                                                     03660099
                DECLARE SKU-CSR CURSOR WITH HOLD                        03670099
                FOR SELECT                                              03680099
                      S.FIRST_RCVD_DATE                                 03690099
                     ,S.ITEM_NMBR                                       03700099
                     ,X.SKU                                             03700199
                     ,A.SKU_DESC                                        03700299
                     ,B.DEPT_NBR                                        03700399
                     ,B.MAJ_CL_NBR                                      03700499
                     ,B.SUB_CL_NBR                                      03700599
                     ,B.VS_ID                                           03700699
                     ,B.VS_NBR                                          03700799
                     ,C.VS_DESC                                         03700899
                     ,A.INTR_UPC_ID                                     03700999
                     ,U.GP_CDE                                          03701099
                     ,L.UNT_RTL_AMT                                     03701199
                FROM TSKXREF X                                          03701299
                    ,TSKST S                                            03701399
                    ,XST_SKU A                                          03701499
                    ,XST_VND_STYL B                                     03701599
                    ,TMDARVS C                                          03701699
                    ,XST_SKU_LOC L                                      03701799
                    ,TUPC U                                             03701899
               WHERE X.ITM_NBR         = S.ITEM_NMBR                    03701999
                 AND X.SKU_STAT_CDE NOT IN ('30', '98')                 03702099
                 AND A.STYL_ID         = B.STYL_ID                      03702199
                 AND B.STYL_ID         = C.RMS_STYL_ID                  03702299
                 AND A.INTR_UPC_ID     = L.INTR_UPC_ID                  03702399
                 AND L.LOC_NBR         = 0                              03702499
                 AND (DATE(L.END_TMST) >= DATE(:PV-RUN-DATE) OR         03702599
                      L.END_TMST IS NULL)                               03702699
                 AND DATE(L.BEG_TMST) <= DATE(:PV-RUN-DATE)             03702799
                 AND C.SUB_CL_MDSEAR_ID > 0                             03702899
                 AND B.VS_ID           = C.VS_ID                        03702999
                 AND B.DEPT_NBR        = X.DEPT                         03703099
                 AND B.DEPT_NBR NOT IN (026, 830)                       03703199
                 AND B.MAJ_CL_NBR      = X.CLASS                        03703299
                 AND B.SUB_CL_NBR      = X.SUBCLASS                     03703399
                 AND A.SKU_NBR         = X.SKU                          03703499
                 AND S.LOC_NBR         = SMALLINT(0)                    03703599
                 AND S.FIRST_RCVD_DATE = :PV-RUN-DATE-NUM               03703699
                 AND A.INTR_UPC_ID     = U.UPC_ID                       03704099
                WITH UR                                                 03710199
           END-EXEC.                                                    03710299
                                                                        03710399
                                                                        03711099
      *----------------------------------------------------------       03720099
       PROCEDURE DIVISION.                                              03730099
      *----------------------------------------------------------       03740099
       0000-MAINLINE.                                                   03750099
                                                                        03760099
           PERFORM 1000-INITIALIZATION.                                 03770099
           PERFORM 2000-FIND-SKUS.                                      03771099
           PERFORM 9999-WRAPUP.                                         03910099
                                                                        03920099
           GOBACK.                                                      03930099
                                                                        03940099
      *0000-EXIT.                                                       03950099
                                                                        03970099
      *----------------------------------------------------------       03980099
      * 1000-INITIALIZATION.                                            03990099
      *     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS      04000099
      *     FILES, READS INPUT FILES, AND PROCESSES ANY INPUT FILE      04010099
      *     PARAMETERS IT MAY NEED TO USE TO FETCH LOCKDOWN             04020099
      *     EXCEPTIONS.                                                 04030099
      *----------------------------------------------------------       04040099
       1000-INITIALIZATION.                                             04050099
                                                                        04060099
           INITIALIZE PM123-EARLY-REC-FILE.                             04070099
                                                                        04110099
           OPEN INPUT  CC-RUN-DTE-FILE                                  04120099
                OUTPUT EARLY-RECEIPT-FILE.                              04130099
                                                                        04140099
           PERFORM 1100-READ-RUN-DTE-FILE.                              04150099
                                                                        04440099
      *1000-EXIT.                                                       04450099
                                                                        04470099
      *---------------------------------------------------------------  04480099
      * 1100-READ-RUN-DTE-FILE.                                         04490099
      *     READ IN THE RUN DATE FROM THE CONTROL CARD.                 04500099
      *---------------------------------------------------------------  04510099
       1100-READ-RUN-DTE-FILE.                                          04520099
                                                                        04530099
           READ CC-RUN-DTE-FILE INTO CC-DATE-RECORD                     04540099
              AT END                                                    04550099
                 SET PS-CONTROL-CARD-EOF TO TRUE                        04560099
           END-READ.                                                    04570099
                                                                        04580099
           IF PS-CONTROL-CARD-EOF                                       04590099
               MOVE '1100-READ-RUN-DTE-FILE'   TO PV-PARAGRAPH-NAME     04600099
               MOVE 'NO RUN DATE FILE TO PROCESS'                       04610099
                                               TO PV-OPERATION-MSG      04620099
               SET ABEND-4003-CTRL-CARD-ERROR  TO TRUE                  04630099
               PERFORM 9800-ABEND                                       04640099
           ELSE                                                         04650099
               DISPLAY 'RUN DATE IS: ' CC-RUN-DATE                      04660099
               MOVE CC-RUN-DATE-CC      TO PV-RUN-DATE-CC               04670099
               MOVE CC-RUN-DATE-YY      TO CC-RUN-DATE-YY-NUM           04761099
                                           PV-RUN-DATE-YY               04761199
               MOVE CC-RUN-DATE-MM      TO CC-RUN-DATE-MM-NUM           04762099
                                           PV-RUN-DATE-MM               04762199
               MOVE CC-RUN-DATE-DD      TO CC-RUN-DATE-DD-NUM           04763099
                                           PV-RUN-DATE-DD               04763199
               MOVE '-'                 TO PV-RUN-DATE-D1               04763299
                                           PV-RUN-DATE-D2               04763399
               MOVE CC-RUN-DATE-NUM-6   TO PV-RUN-DATE-NUM              04764099
               PERFORM 1200-FIND-TIME                                   04765099
           END-IF.                                                      04770099
                                                                        04780099
      *1100-EXIT.                                                       04790099
                                                                        04791099
       1200-FIND-TIME.                                                  04800099
                                                                        04801099
           EXEC SQL                                                     04802099
               SELECT (CURRENT_TIMESTAMP + 30 SECONDS)                  04803099
               INTO   :PV-RUN-TIME                                      04804099
               FROM   SYSIBM.SYSDUMMY1                                  04805099
               WITH UR                                                  04806099
           END-EXEC.                                                    04807099
                                                                        04808099
           EVALUATE TRUE                                                04809099
               WHEN SQLCODE = ZERO                                      04809199
                   DISPLAY 'PV-RUN-TIME = ' PV-RUN-TIME                 04809299
                                                                        04809399
                WHEN OTHER                                              04809499
                   MOVE 'SYSIBM.SYSDUMMY1'    TO PV-OPERATION-MSG-1     04809599
                   MOVE '1200-FIND-TIME '     TO PV-PARAGRAPH-NAME      04809699
                   PERFORM Z900-SQL-ABEND                               04809799
           END-EVALUATE.                                                04809899
      *1200-EXIT.                                                       04809999
                                                                        04810099
       1300-CHECK-TIME.                                                 04810199
                                                                        04811099
           EXEC SQL                                                     04812099
               SET :WS-TMST = CURRENT_TIMESTAMP                         04813099
           END-EXEC.                                                    04814099
                                                                        04815099
           EVALUATE TRUE                                                04816099
               WHEN SQLCODE = ZERO                                      04817099
                   DISPLAY 'WS-TMST = ' WS-TMST                         04818099
                                                                        04819099
                WHEN OTHER                                              04819199
                   MOVE 'SYSIBM.SYSDUMMY1'    TO PV-OPERATION-MSG-1     04819299
                   MOVE '1300-CHECK-TIME '     TO PV-PARAGRAPH-NAME     04819399
                   PERFORM Z900-SQL-ABEND                               04819499
           END-EVALUATE.                                                04819599
                                                                        04819699
      *1300-EXIT.                                                       04819799
                                                                        04819899
       1400-COMMIT.                                                     04819999
                                                                        04820099
           EXEC SQL                                                     04820199
               COMMIT                                                   04820299
           END-EXEC.                                                    04821099
                                                                        04822099
           EVALUATE TRUE                                                04823099
               WHEN SQLCODE = ZERO                                      04824099
                   DISPLAY 'COMMIT'                                     04825099
                                                                        04826099
                WHEN OTHER                                              04827099
                   MOVE '1400-COMMIT' TO PV-PARAGRAPH-NAME              04828099
                   PERFORM Z900-SQL-ABEND                               04829099
           END-EVALUATE.                                                04829199
      *1400-EXIT.                                                       04829299
                                                                        04830099
      *----------------------------------------------------------       06300099
      * 2000-FIND-SKUS.                                                 06310099
      *     FIND SKUS WHERE THE ACTUAL RECEIPT DATE = YESTERDAY         06320099
      *     (DATE PASSED INTO PROGRAM) FOR STORE 000.                   06330099
      *----------------------------------------------------------       06340099
       2000-FIND-SKUS.                                                  06350099
                                                                        06360099
           PERFORM 3000-OPEN-SKU-CSR.                                   06360599
           PERFORM 3100-FETCH-SKU-CSR.                                  06360699
                                                                        06360799
           IF NOT PS-END-OF-SKU-CSR                                     06360899
             PERFORM 3300-PROCESS-SKU-CSR                               06360999
               UNTIL PS-END-OF-SKU-CSR                                  06361099
           END-IF.                                                      06361199
                                                                        06361299
           PERFORM 3500-CLOSE-SKU-CSR.                                  06362099
                                                                        06440099
      *2000-EXIT.                                                       06450099
                                                                        06460099
      ******************************************************************06470099
      * OPEN THE SKU CURSOR                                             06480099
      ******************************************************************06490099
       3000-OPEN-SKU-CSR.                                               06500099
                                                                        06510099
      *    ACCEPT PV-CPU-TIME FROM TIME                                 06520099
      *    DISPLAY ' '                                                  06530099
      *    DISPLAY 'START CURSOR OPEN: ' PV-CPU-TIME                    06540099
                                                                        06550099
           EXEC SQL                                                     06560099
               OPEN SKU-CSR                                             06570099
           END-EXEC.                                                    06580099
                                                                        06590099
           EVALUATE TRUE                                                06600099
               WHEN SQLCODE = +0                                        06610099
                   CONTINUE                                             06620099
      *            ACCEPT PV-CPU-TIME FROM TIME                         06630099
      *            DISPLAY 'SKU CURSOR OPENED: ' PV-CPU-TIME            06640099
               WHEN OTHER                                               06650099
                   MOVE '3000-OPEN-SKU-CSR            '                 06660099
                                     TO PV-PARAGRAPH-NAME               06670099
                   MOVE 'OPEN ERROR ON SKU-CSR      '                   06680099
                                     TO PV-OPERATION-MSG-1              06690099
                   PERFORM Z900-SQL-ABEND                               06700099
           END-EVALUATE.                                                06710099
                                                                        06720099
      ******************************************************************06730099
      * FETCH THE SKU CURSOR                                            06740099
      ******************************************************************06750099
       3100-FETCH-SKU-CSR.                                              06760099
                                                                        06770099
           EXEC SQL                                                     06780099
               FETCH SKU-CSR                                            06790099
                INTO :TSKST-FIRST-RCVD-DATE                             06800099
                    ,:TSKST-ITEM-NMBR                                   06810099
                    ,:SKXREF-SKU                                        06820099
                    ,:XST00010-SKU-DESC                                 06821099
                    ,:XST00013-DEPT-NBR                                 06830099
                    ,:XST00013-MAJ-CL-NBR                               06840099
                    ,:XST00013-SUB-CL-NBR                               06850099
                    ,:XST00013-VS-ID                                    06860099
                    ,:XST00013-VS-NBR                                   06870099
                    ,:MDARVS-VS-DESC                                    06880099
                    ,:XST00010-INTR-UPC-ID                              06890099
                    ,:UPC-GP-CDE                                        06891099
                    ,:XST00008-UNT-RTL-AMT                              06892099
           END-EXEC.                                                    06900099
                                                                        06910099
           EVALUATE TRUE                                                06920099
               WHEN SQLCODE = +0                                        06930099
                   IF TSKST-FIRST-RCVD-DATE > 0                         06931099
      *                CONVERT FIRST-RCVD-DATE TO A DATE FORMAT         06932099
                       MOVE TSKST-FIRST-RCVD-DATE TO                    06933099
                                                  PV-FIRST-RCVD-DTE-NUM 06934099
                       MOVE '20'                 TO PV-FIRST-RCVD-DTE-CC06936099
                       MOVE PV-FIRST-RCVD-RDF-YY TO PV-FIRST-RCVD-DTE-YY06939199
                       MOVE PV-FIRST-RCVD-RDF-MM TO PV-FIRST-RCVD-DTE-MM06939299
                       MOVE PV-FIRST-RCVD-RDF-DD TO PV-FIRST-RCVD-DTE-DD06939399
                       MOVE '-'                  TO PV-FIRST-RCVD-DTE-D106939499
                                                    PV-FIRST-RCVD-DTE-D206939599
                       MOVE PV-FIRST-RCVD-DTE    TO PV-FIRST-RCVD-DATE  06939699
                   ELSE                                                 06939799
                       MOVE ' ' TO PV-FIRST-RCVD-DATE                   06939899
                   END-IF                                               06939999
                   MOVE TSKST-ITEM-NMBR       TO PV-ITEM-NMBR           06950099
                   MOVE SKXREF-SKU            TO PV-SKU-NBR             06960099
                   MOVE XST00010-SKU-DESC     TO PV-SKU-DESC            06970099
                   MOVE XST00013-DEPT-NBR     TO PV-DEPT-NBR            06971099
                   MOVE XST00013-MAJ-CL-NBR   TO PV-MAJ-CL-NBR          06980099
                   MOVE XST00013-SUB-CL-NBR   TO PV-SUB-CL-NBR          06990099
                   MOVE XST00013-VS-ID        TO PV-VS-ID               06991099
                   MOVE XST00013-VS-NBR       TO PV-VS-NBR              06992099
                   MOVE MDARVS-VS-DESC        TO PV-VS-DESC             06993099
                   MOVE XST00010-INTR-UPC-ID  TO PV-INTR-UPC-ID         06994099
                   MOVE XST00008-UNT-RTL-AMT  TO PV-UNT-RTL-AMT         06994199
                   MOVE UPC-GP-CDE            TO PV-GP-CDE              06995099
               WHEN SQLCODE = +100                                      07000099
                   SET PS-END-OF-SKU-CSR TO TRUE                        07010099
               WHEN OTHER                                               07020099
                   MOVE '3100-FETCH-SKU-CSR           '                 07030099
                                     TO PV-PARAGRAPH-NAME               07040099
                   MOVE 'FETCH ERROR ON SKU-CSR     '                   07050099
                                     TO PV-OPERATION-MSG-1              07060099
                   PERFORM Z900-SQL-ABEND                               07070099
           END-EVALUATE.                                                07080099
                                                                        07080199
           PERFORM 1300-CHECK-TIME.                                     07081099
           IF WS-TMST > PV-RUN-TIME                                     07082099
              PERFORM 1400-COMMIT                                       07083099
              PERFORM 1200-FIND-TIME                                    07084099
           END-IF.                                                      07085099
                                                                        07090099
      ******************************************************************07100099
      * PROCESS THE SKU  CURSOR                                         07110099
      ******************************************************************07120099
       3300-PROCESS-SKU-CSR.                                            07130099
                                                                        07140099
           PERFORM 4050-GET-EARLY-REC-OTHER.                            07150299
                                                                        07150399
           IF PV-FIRST-RCVD-DATE < PV-DONT-SHIP-BEF-PLS8                07150599
              PERFORM 4100-GET-GMM-INFO                                 07170099
              PERFORM 4200-GET-DMM-INFO                                 07171099
              PERFORM 4300-GET-BYR-INFO                                 07172099
              PERFORM 9000-WRITE-EARLY-RECEIPT-REC                      07173099
           END-IF.                                                      07180099
                                                                        07190099
           INITIALIZE PV-SELECT-VARIABLES.                              07890099
           INITIALIZE PM123-EARLY-REC-FILE.                             07891099
                                                                        07892099
           PERFORM 3100-FETCH-SKU-CSR.                                  07900099
                                                                        07910099
      ******************************************************************07920099
      * CLOSE THE SKU CURSOR                                            07930099
      ******************************************************************07940099
       3500-CLOSE-SKU-CSR.                                              07950099
                                                                        07960099
           EXEC SQL                                                     07970099
               CLOSE SKU-CSR                                            07980099
           END-EXEC.                                                    07990099
                                                                        08000099
           EVALUATE TRUE                                                08010099
               WHEN SQLCODE = +0                                        08020099
                   CONTINUE                                             08030099
               WHEN OTHER                                               08040099
                   MOVE '3500-CLOSE-SKU-CSR          '                  08050099
                                     TO PV-PARAGRAPH-NAME               08060099
                   MOVE 'CLOSE ERROR ON SKU-CSR      '                  08070099
                                     TO PV-OPERATION-MSG-1              08080099
                   PERFORM Z900-SQL-ABEND                               08090099
           END-EVALUATE.                                                08100099
                                                                        08110099
      ******************************************************************13891099
      * 4050-GET-EARLY-REC-OTHER - THE PO HAS BEEN COMPLETELY RECEIVED *13892099
      * SO ITS NO LONGER ON TSKSTOO - SO WE NEED TO LOOK FOR THE EARLY *13892199
      * RECEIPT DATE A DIFFERENT WAY.                                  *13892299
      ******************************************************************13893099
       4050-GET-EARLY-REC-OTHER.                                        13900099
                                                                        13900199
           EXEC SQL                                                     13900599
                SELECT MIN(P.DONT_SHIP_BEF_DTE + 8 DAYS)                13900699
                  INTO :PV-DONT-SHIP-BEF-PLS8 :PV-DONT-SHIP-BEF-PLS8-NI 13900799
                  FROM TPLNSHP P                                        13900899
                      ,TPURITM I                                        13900999
                 WHERE I.ITM_NBR = :PV-ITEM-NMBR                        13901099
                   AND I.PO_NBR = P.PO_NBR                              13901199
                   AND P.VER_STATUS_CDE = 'A'                           13901299
                   AND I.VER_STATUS_CDE = 'A'                           13901399
                  WITH UR                                               13901499
           END-EXEC.                                                    13901599
                                                                        13901699
           EVALUATE TRUE                                                13901799
               WHEN SQLCODE = +0                                        13901899
                   CONTINUE                                             13901999
                                                                        13902099
               WHEN SQLCODE = +100                                      13902199
                   MOVE PV-FIRST-RCVD-DATE TO PV-DONT-SHIP-BEF-PLS8     13902299
                                                                        13902399
               WHEN OTHER                                               13902499
                   DISPLAY ' ITEM-NBR = ' PV-ITEM-NMBR                  13902599
                   MOVE '4050-GET-EARLY-REC-OTHER   '                   13902699
                                     TO PV-PARAGRAPH-NAME               13902799
                   MOVE 'ERROR READING TPURITM TABLE '                  13902899
                                     TO PV-OPERATION-MSG-1              13902999
                   PERFORM Z900-SQL-ABEND                               13903099
           END-EVALUATE.                                                13903199
                                                                        13903299
                                                                        13903399
      ******************************************************************13903499
      * QUERIES THE DATABASE FOR THE GMM NAME/NBR.                     *13903599
      ******************************************************************13903699
       4100-GET-GMM-INFO.                                               13903799
                                                                        13903899
                                                                        13904099
           EXEC SQL                                                     13910099
               SELECT C.WORKER_LAST_NAME,                               13920099
                      C.WORKER_FIR_NAME,                                13930099
                      A.GMA_NBR                                         13931099
               INTO                                                     13940099
                    :PV-GMM-LAST-NAME,                                  13950099
                    :PV-GMM-FIRST-NAME,                                 13960099
                    :PV-GMA-NBR                                         13961099
               FROM TMDSEAR A                                           13970099
                  , TMDSEAR B                                           13971099
                  , TRSPBAR C                                           13980099
               WHERE A.OPERCO_NBR   = '03'                              13990099
                 AND A.DEPT_NBR     = SUBSTR(:PV-DEPT-NBR,2,3)          14000099
                 AND A.MDSELV_CDE   = 'DPT'                             14010099
                 AND A.GMA_NBR      = B.GMA_NBR                         14011099
                 AND A.OPERCO_NBR   = B.OPERCO_NBR                      14012099
                 AND B.MDSELV_CDE   = 'GMA'                             14013099
                 AND B.MDSEAR_DKEY  = C.MDSEAR_DKEY                     14020099
                 AND C.RSPLVL_CDE   = 'GMM'                             14030099
                 AND (:PV-RUN-DATE                                      14040099
                      BETWEEN A.STRT_DTE                                14050099
                      AND A.END_DTE)                                    14060099
                 AND (:PV-RUN-DATE                                      14070099
                      BETWEEN C.WORKER_STRT_DTE                         14080099
                      AND C.WORKER_END_DTE)                             14090099
                 WITH UR                                                14100099
           END-EXEC.                                                    14110099
                                                                        14120099
           EVALUATE TRUE                                                14130099
              WHEN SQLCODE = +0                                         14140099
                  CONTINUE                                              14150099
              WHEN SQLCODE = +100                                       14160099
                  MOVE SPACES TO PV-GMM-LAST-NAME                       14190099
                  MOVE SPACES TO PV-GMM-FIRST-NAME                      14200099
                  MOVE ZEROES TO PV-GMA-NBR                             14201099
              WHEN OTHER                                                14210099
                  MOVE '4100-GET-GMM-INFO' TO  PV-PARAGRAPH-NAME        14220099
                  MOVE 'TMDSEAR/TRSPBAR'   TO  PV-TABLE-NAME            14230099
                  MOVE 'GET GMM NAME     ' TO  PV-OPERATION-MSG-1       14250099
                  PERFORM Z900-SQL-ABEND                                14260099
           END-EVALUATE.                                                14270099
                                                                        14280099
      ******************************************************************14290099
      * QUERIES THE DATABASE FOR THE DMM NAME/NBR.                     *14300099
      ******************************************************************14310099
       4200-GET-DMM-INFO.                                               14320099
           EXEC SQL                                                     14330099
               SELECT C.WORKER_LAST_NAME,                               14340099
                      C.WORKER_FIR_NAME,                                14350099
                      A.DMA_NBR                                         14351099
               INTO                                                     14360099
                    :PV-DMM-LAST-NAME,                                  14370099
                    :PV-DMM-FIRST-NAME,                                 14380099
                    :PV-DMA-NBR                                         14381099
               FROM TMDSEAR A                                           14390099
                  , TMDSEAR B                                           14391099
                  , TRSPBAR C                                           14400099
               WHERE A.OPERCO_NBR = '03'                                14410099
                 AND A.DEPT_NBR     = SUBSTR(:PV-DEPT-NBR,2,3)          14411099
                 AND A.MDSELV_CDE = 'DPT'                               14430099
                 AND A.DMA_NBR = B.DMA_NBR                              14431099
                 AND A.OPERCO_NBR = B.OPERCO_NBR                        14432099
                 AND B.MDSELV_CDE = 'DMA'                               14433099
                 AND B.MDSEAR_DKEY = C.MDSEAR_DKEY                      14440099
                 AND C.RSPLVL_CDE  = 'DMM'                              14450099
                 AND (:PV-RUN-DATE                                      14460099
                      BETWEEN A.STRT_DTE                                14470099
                      AND A.END_DTE)                                    14480099
                 AND (:PV-RUN-DATE                                      14490099
                      BETWEEN C.WORKER_STRT_DTE                         14500099
                      AND C.WORKER_END_DTE)                             14510099
                 WITH UR                                                14520099
           END-EXEC.                                                    14530099
                                                                        14540099
           EVALUATE TRUE                                                14550099
              WHEN SQLCODE = +0                                         14560099
                  CONTINUE                                              14570099
              WHEN SQLCODE = +100                                       14580099
                  MOVE SPACES TO PV-DMM-LAST-NAME                       14610099
                  MOVE SPACES TO PV-DMM-FIRST-NAME                      14620099
                  MOVE ZEROES TO PV-DMA-NBR                             14621099
              WHEN OTHER                                                14630099
                  MOVE '4200-GET-DMM-INFO'  TO  PV-PARAGRAPH-NAME       14640099
                  MOVE 'TMDSEAR/TRSPBAR'   TO  PV-TABLE-NAME            14650099
                  MOVE 'GET DMM NAME     ' TO  PV-OPERATION-MSG-1       14670099
                  PERFORM Z900-SQL-ABEND                                14680099
           END-EVALUATE.                                                14690099
                                                                        14700099
      ******************************************************************14710099
      * QUERIES THE DATABASE FOR THE BUYER NAME AND DEPT DESCRIPTION   *14720099
      ******************************************************************14730099
       4300-GET-BYR-INFO.                                               14740099
           EXEC SQL                                                     14750099
               SELECT C.WORKER_LAST_NAME,                               14760099
                      C.WORKER_FIR_NAME,                                14770099
                      B.MDSEAR_DESC,                                    14780099
                      B.BYR_NBR                                         14781099
               INTO                                                     14790099
                    :PV-BUYER-LAST-NAME,                                14800099
                    :PV-BUYER-FIRST-NAME,                               14810099
                    :PV-DEPT-DESC,                                      14820099
                    :PV-BYR-NBR                                         14821099
               FROM TMDSEAR A                                           14830099
                  , TMDSEAR B                                           14831099
                  , TRSPBAR C                                           14840099
               WHERE A.OPERCO_NBR   = '03'                              14850099
                 AND A.DEPT_NBR     = SUBSTR(:PV-DEPT-NBR,2,3)          14860099
                 AND A.MDSELV_CDE   = 'DPT'                             14880099
                 AND A.OPERCO_NBR   = B.OPERCO_NBR                      14881099
                 AND A.BYR_NBR      = B.BYR_NBR                         14882099
                 AND A.DEPT_NBR     = B.DEPT_NBR                        14883099
                 AND B.MDSEAR_DKEY  = C.MDSEAR_DKEY                     14890099
                 AND C.RSPLVL_CDE   = 'BYR'                             14900099
                 AND (:PV-RUN-DATE                                      14910099
                      BETWEEN A.STRT_DTE                                14920099
                      AND A.END_DTE)                                    14930099
                 AND (:PV-RUN-DATE                                      14940099
                      BETWEEN C.WORKER_STRT_DTE                         14950099
                      AND C.WORKER_END_DTE)                             14960099
                 WITH UR                                                14970099
           END-EXEC.                                                    14980099
                                                                        14990099
           EVALUATE TRUE                                                15000099
              WHEN SQLCODE = +0                                         15010099
                  CONTINUE                                              15020099
              WHEN SQLCODE = +100                                       15030099
                  MOVE SPACES TO PV-DEPT-DESC                           15070099
                  MOVE SPACES TO PV-BUYER-FIRST-NAME                    15080099
                  MOVE SPACES TO PV-BUYER-LAST-NAME                     15090099
                  MOVE ZEROES TO PV-BYR-NBR                             15091099
              WHEN OTHER                                                15100099
                  MOVE '4300-GET-BYR-INFO'       TO  PV-PARAGRAPH-NAME  15110099
                  MOVE 'TMDSEAR/TRSPBAR'   TO  PV-TABLE-NAME            15120099
                  MOVE 'GET BYR NAME     ' TO  PV-OPERATION-MSG-1       15140099
                  PERFORM Z900-SQL-ABEND                                15150099
           END-EVALUATE.                                                15160099
                                                                        15170099
      ******************************************************************20131099
      * 9000-WRITE-EARLY-RECEIPT-REC.                                   20140099
      *     WRITES THE EARLY RECEIPT RECORD TO THE FILE.                20150099
      ******************************************************************20151099
       9000-WRITE-EARLY-RECEIPT-REC.                                    20170099
                                                                        20180099
           MOVE PV-GMA-NBR             TO PM123-GMA-NBR.                20181099
           MOVE PV-GMM-FIRST-NAME      TO PM123-GMM-FIRST-NAME.         20181199
           MOVE PV-GMM-LAST-NAME       TO PM123-GMM-LAST-NAME.          20181299
           MOVE PV-DMA-NBR             TO PM123-DMA-NBR.                20181399
           MOVE PV-DMM-FIRST-NAME      TO PM123-DMM-FIRST-NAME.         20181499
           MOVE PV-DMM-LAST-NAME       TO PM123-DMM-LAST-NAME.          20181599
           MOVE PV-BYR-NBR             TO PM123-BYR-NBR.                20181699
           MOVE PV-BUYER-FIRST-NAME    TO PM123-BUYER-FIRST-NAME.       20181799
           MOVE PV-BUYER-LAST-NAME     TO PM123-BUYER-LAST-NAME.        20181899
           MOVE PV-DEPT-NBR            TO PM123-DEPT-NBR.               20181999
           MOVE PV-DEPT-DESC           TO PM123-DEPT-DESC.              20182099
           MOVE PV-MAJ-CL-NBR          TO PM123-MAJ-CL-NBR.             20182199
           MOVE PV-SUB-CL-NBR          TO PM123-SUB-CL-NBR.             20182299
           MOVE PV-VS-ID               TO PM123-VS-ID.                  20182399
           MOVE PV-VS-NBR              TO PM123-VS-NBR.                 20182499
           MOVE PV-VS-DESC             TO PM123-VS-DESC.                20182599
           MOVE PV-SKU-NBR             TO PM123-SKU.                    20182699
           MOVE PV-SKU-DESC            TO PM123-SKU-DESC.               20182799
           MOVE PV-DONT-SHIP-BEF-PLS8  TO PM123-DONT-SHIP-BEF-PLS8.     20182899
           MOVE PV-FIRST-RCVD-DATE     TO PM123-FIRST-RCVD-DATE.        20182999
           MOVE PV-INTR-UPC-ID         TO PM123-INTR-UPC-ID.            20183099
           MOVE PV-GP-CDE              TO PM123-GP-CDE.                 20183199
           MOVE PV-UNT-RTL-AMT         TO PM123-UNT-RTL-AMT.            20183299
                                                                        20184099
           WRITE EARLY-RECEIPT-REC                                      20190099
               FROM PM123-EARLY-REC-FILE.                               20200099
                                                                        20210099
           ADD 1 TO PA-RECS-WRITTEN.                                    20220099
                                                                        20230099
      *9000-EXIT.                                                       20240099
                                                                        20260099
      ****************************************************************  20321099
      * 9800-ABEND.                                                     20340099
      *     PERFORMS A NON-SQL-ABEND.                                   20350099
      ****************************************************************  20351099
       9800-ABEND.                                                      20370099
                                                                        20380099
           DISPLAY '** ABEND           **'.                             20390099
           DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.          20400099
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        20410099
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.         20420099
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.       20430099
                                                                        20440099
           CALL 'ILBOABN0' USING ABEND-CODE.                            20450099
                                                                        20460099
      *9800-EXIT.                                                       20470099
                                                                        20490099
                                                                        20730099
      *----------------------------------------------------------       20740099
      * 9999-WRAPUP.                                                    20750099
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS      20760099
      *     ANY DATA FROM THE PROGRAM.                                  20770099
      *----------------------------------------------------------       20780099
       9999-WRAPUP.                                                     20790099
                                                                        20800099
           CLOSE CC-RUN-DTE-FILE                                        20810099
                 EARLY-RECEIPT-FILE.                                    20820099
                                                                        20821099
           DISPLAY ' '.                                                 20840099
           DISPLAY '*********************************************'.     20850099
           DISPLAY 'PROGRAM NAME: ' PC-PROGRAM-NAME.                    20860099
           DISPLAY '*********************************************'.     20870099
           DISPLAY 'NUMBER OF RECS WRITTEN: ' PA-RECS-WRITTEN.          20930099
           DISPLAY '*********************************************'.     20940099
                                                                        20950099
      *9999-EXIT.                                                       20960099
                                                                        20980099
      ******************************************************************20990099
      * SQL ABEND                                                       21000099
      ******************************************************************21010099
       Z900-SQL-ABEND.                                                  21020099
                                                                        21030099
           DISPLAY 'Z900-SQL-ABEND'.                                    21040099
           DISPLAY '**  ABEND     **'.                                  21050099
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.              21060099
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            21070099
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-MSG-1.           21080099
           DISPLAY '**            **  = ' PV-OPERATION-MSG-2.           21090099
                                                                        21100099
      ******************************************************************21110099
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *21120099
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *21130099
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *21140099
      * FORMAT.                                                        *21150099
      ******************************************************************21160099
           COPY DPPD004.                                                21170099
                                                                        21180099
           SET ABEND-4013-SQL-ERROR TO TRUE.                            21190099
           CALL 'ILBOABN0' USING ABEND-CODE.                            21200099
