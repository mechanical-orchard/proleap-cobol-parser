       IDENTIFICATION DIVISION.                                         00010099
                                                                        00020099
       PROGRAM-ID.              MLKUT616.                               00030099
       AUTHOR.                  PAUL JORGENSEN.                         00040099
       INSTALLATION.            KOHLS DEPARTMENT STORES.                00050099
       DATE-WRITTEN.            OCTOBER 2006.                           00060099
       DATE-COMPILED.                                                   00070099
                                                                        00080099
      ******************************************************************00090099
      *      BUILD REPORT FILE FOR DATA SMOOTHING OUTLYER REPORTING     00100099
      *      PROGRAM MLKUT616                                           00101099
      *                                                                 00110099
      * THIS PROGRAM WILL READ A M/L SORT SUMMED FILE OF COMBINED       00120099
      * FILES OUT OF MLK185 A THRU H, ADD MSB STRUCTURE AND DO          00130099
      * CALCULATIONS IN PREPARATION FOR INPUT INTO REPORT PROGRAM       00140099
      * MLKRP804.                                                       00150099
      *                                                                 00180099
      *CHANGE HISTORY:                                                  00181099
      * 01/01/07  PAUL JORGENSEN              WR/ID - 28582             00183099
      *   NEW PROGRAM.                                                  00184099
      *                                                                 00185099
      ******************************************************************00190099
                                                                        00200099
       ENVIRONMENT DIVISION.                                            00210099
                                                                        00220099
       CONFIGURATION SECTION.                                           00230099
                                                                        00240099
       SOURCE-COMPUTER. IBM-3090.                                       00250099
       OBJECT-COMPUTER. IBM-3090.                                       00260099
                                                                        00270099
       INPUT-OUTPUT SECTION.                                            00280099
                                                                        00290099
       FILE-CONTROL.                                                    00300099
                                                                        00310099
           SELECT CONTROL-CARD-FILE                                     00320099
               ASSIGN TO INP01.                                         00330099
                                                                        00340099
           SELECT SMOOTH-OUTLYER-EXT-FILE                               00350099
               ASSIGN TO INP02.                                         00360099
                                                                        00370099
           SELECT SMOOTH-OUTLYER-RPT-FILE                               00380099
               ASSIGN TO OUT01.                                         00390099
                                                                        00400099
       DATA DIVISION.                                                   00410099
                                                                        00420099
       FILE SECTION.                                                    00430099
                                                                        00440099
       FD  CONTROL-CARD-FILE                                            00450099
           RECORDING MODE IS F                                          00460099
           LABEL RECORDS ARE STANDARD                                   00470099
           BLOCK CONTAINS 0 RECORDS.                                    00480099
       01  CONTROL-CARD-RECORD              PIC X(80).                  00490099
                                                                        00500099
       FD  SMOOTH-OUTLYER-EXT-FILE                                      00510099
           RECORDING MODE IS F                                          00520099
           LABEL RECORDS ARE STANDARD                                   00530099
           BLOCK CONTAINS 0 RECORDS.                                    00540099
       01  SMOOTH-OUTLYER-EXT-REC           PIC X(400).                 00550099
                                                                        00560099
       FD  SMOOTH-OUTLYER-RPT-FILE                                      00570099
           RECORDING MODE IS F                                          00580099
           LABEL RECORDS ARE STANDARD                                   00590099
           BLOCK CONTAINS 0 RECORDS.                                    00600099
       01  SMOOTH-OUTLYER-RPT-REC           PIC X(350).                 00610099
                                                                        00620099
       WORKING-STORAGE SECTION.                                         00630099
                                                                        00640099
      ****************************************************************  00650099
      * RECORD LAYOUT FOR CONTROL CARD EXTRACT DATE RECORD              00660099
      ****************************************************************  00670099
           COPY MLRD114.                                                00680099
                                                                        00690099
      ****************************************************************  00700099
      * RECORD LAYOUT FOR SMOOTHING OUTLYER EXTRACT RECORD              00710099
      ****************************************************************  00720099
           COPY MLRD070.                                                00730099
                                                                        00740099
      ****************************************************************  00741099
      * RECORD LAYOUT FOR SMOOTHING OUTLYER REPORT DATA RECORD          00742099
      ****************************************************************  00743099
           COPY MLRD204.                                                00744099
                                                                        00745099
      ***************************************************************** 00750099
      *    WORKING STORAGE AREA FOR ROUTINE TO FORMAT NAME              00760099
      ***************************************************************** 00770099
           COPY DPWS024.                                                00780099
                                                                        00790099
      ****************************************************************  00800099
      *  ERROR MESSAGE AREA FOR DB2                                     00810099
      ****************************************************************  00820099
           COPY DPWS004.                                                00830099
                                                                        00840099
      ****************************************************************  00850099
      * MISC WORKING STORAGE FIELDS                                     00860099
      ****************************************************************  00870099
                                                                        00880099
       01  PC-PROGRAM-CONSTANTS.                                        00890099
           05  PC-OPERCO-NBR                PIC X(02)  VALUE '03'.      00900099
           05  PC-MDSELV-GMA                PIC X(03)  VALUE 'GMA'.     00910099
           05  PC-MDSELV-DMA                PIC X(03)  VALUE 'DMA'.     00920099
           05  PC-MDSELV-DPT                PIC X(03)  VALUE 'DPT'.     00930099
           05  PC-MDSELV-MCL                PIC X(03)  VALUE 'MCL'.     00940099
           05  PC-RSPLVL-CDE-GMM            PIC X(03)  VALUE 'GMM'.     00950099
           05  PC-RSPLVL-CDE-DMM            PIC X(03)  VALUE 'DMM'.     00960099
           05  PC-RSPLVL-CDE-BYR            PIC X(03)  VALUE 'BYR'.     00970099
           05  PC-UNKNOWN                   PIC X(07)  VALUE 'UNKNOWN'. 00971099
                                                                        00980099
       01  PS-PROGRAM-SWITCHES.                                         00990099
           05  PS-END-OF-CONTROL-CARDS-SW   PIC X      VALUE 'N'.       01000099
               88  END-OF-CONTROL-CARDS                VALUE 'Y'.       01010099
           05  PS-END-OF-INPUT-SW           PIC X      VALUE 'N'.       01020099
               88  END-OF-INPUT                        VALUE 'Y'.       01030099
           05  PS-MBS-INFO-FOUND-SW         PIC X      VALUE 'N'.       01040099
               88  MBS-INFO-FOUND                      VALUE 'Y'.       01050099
               88  MBS-INFO-NOT-FOUND                  VALUE 'N'.       01060099
           05  PS-MBS-MAX-DATE-FOUND-SW     PIC X      VALUE 'N'.       01070099
               88  MBS-MAX-DATE-FOUND                  VALUE 'Y'.       01080099
               88  MBS-MAX-DATE-NOT-FOUND              VALUE 'N'.       01090099
           05  PS-MBS-USING-MAX-DATE-SW     PIC X      VALUE 'N'.       01100099
               88  MBS-USING-MAX-DATE                  VALUE 'Y'.       01110099
               88  MBS-NOT-USING-MAX-DATE              VALUE 'N'.       01120099
                                                                        01130099
       01  PV-PROGRAM-VARIABLES.                                        01140099
           05  PV-DB2-OPER-ATTEMPTED        PIC X(30)  VALUE SPACES.    01150099
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.    01160099
           05  PV-MAX-MCL-DATE-NULL-IND     PIC S9(04) VALUE ZEROS      01170099
                                                       COMP.            01180099
           05  PV-MAX-DEPT-DATE-NULL-IND    PIC S9(04) VALUE ZEROS      01181099
                                                       COMP.            01182099
           05  PV-EXTRACT-DTE               PIC X(10)  VALUE SPACES.    01190099
           05  PV-DEPT-DESC                 PIC X(25)  VALUE SPACES.    01200099
           05  PV-MAJ-CL-DESC               PIC X(25)  VALUE SPACES.    01210099
           05  PV-GMM-FIR-NAME              PIC X(12)  VALUE SPACES.    01220099
           05  PV-GMM-LAST-NAME             PIC X(18)  VALUE SPACES.    01230099
           05  PV-DMM-FIR-NAME              PIC X(12)  VALUE SPACES.    01240099
           05  PV-DMM-LAST-NAME             PIC X(18)  VALUE SPACES.    01250099
           05  PV-BYR-FIR-NAME              PIC X(12)  VALUE SPACES.    01260099
           05  PV-BYR-LAST-NAME             PIC X(18)  VALUE SPACES.    01270099
           05  PV-VENDOR-NAME               PIC X(30)  VALUE SPACES.    01280099
           05  PV-KEY-DEPT-NBR              PIC  9(03).                 01290099
           05  PV-PREV-DEPT-NBR             PIC S9(04)  COMP-3          01290199
                                                       VALUE ZERO.      01291099
           05  PV-PREV-MAJ-CL-NBR-N         PIC 9(02).                  01300099
           05  PV-PREV-MAJ-CL-NBR REDEFINES PV-PREV-MAJ-CL-NBR-N        01301099
                                            PIC X(02).                  01302099
           05  PV-ML-LWST-ID                PIC 9(09).                  01303099
           05  PV-INPUT-REC-CNT             PIC 9(11)  VALUE ZEROS      01310099
                                                        COMP-3.         01311000
           05  PV-OUTPUT-REC-CNT            PIC 9(11)  VALUE ZEROS      01320099
                                                        COMP-3.         01321000
           05  PV-MBS-NOT-FOUND-CNT         PIC 9(11)  VALUE ZEROS      01322099
                                                        COMP-3.         01323000
           05  PV-INPUT-REC-CNT-DIS         PIC ZZ,ZZZ,ZZZ,ZZZ.         01330099
           05  PV-OUTPUT-REC-CNT-DIS        PIC ZZ,ZZZ,ZZZ,ZZZ.         01340099
           05  PV-MBS-NOT-FOUND-CNT-DIS     PIC ZZ,ZZZ,ZZZ,ZZZ.         01341099
                                                                        01350099
       01  ABEND-CODE                       PIC S9(04) COMP             01360099
                                                       VALUE ZERO.      01370099
      ******************************************************************01371075
      * COBOL DECLARATION FOR TABLE MLV_BRND_VND                        01372099
      ******************************************************************01373075
           EXEC SQL                                                     01374099
               INCLUDE MLV00000                                         01375099
           END-EXEC.                                                    01376099
                                                                        01380099
      ****************************************************************  01390099
      * MERCHANDISE LEDGER CONTROL TABLE                                01400099
      ****************************************************************  01410099
           EXEC SQL                                                     01420099
               INCLUDE TMLCNTL                                          01430099
           END-EXEC.                                                    01440099
                                                                        01450099
      ****************************************************************  01460099
      * DATE ATTRIBUTE TABLE                                            01470099
      ****************************************************************  01480099
           EXEC SQL                                                     01490099
               INCLUDE TDTATTR                                          01500099
           END-EXEC.                                                    01510099
                                                                        01520099
      ****************************************************************  01530099
      * MBS MERCHANDISE AREA TABLE                                      01540099
      ****************************************************************  01550099
           EXEC SQL                                                     01560099
               INCLUDE TMDSEAR                                          01570099
           END-EXEC.                                                    01580099
                                                                        01590099
      ****************************************************************  01600099
      * MBS RESPONSIBILITY LEVEL TABLE                                  01610099
      ****************************************************************  01620099
           EXEC SQL                                                     01630099
               INCLUDE TRSPBAR                                          01640099
           END-EXEC.                                                    01650099
                                                                        01660099
      ****************************************************************  01740099
      * EXTERNAL ENTITY ID TABLE                                        01750099
      ****************************************************************  01760099
           EXEC SQL                                                     01770099
               INCLUDE TEXTENT                                          01780099
           END-EXEC.                                                    01790099
                                                                        01800099
      ****************************************************************  01810099
      * EXTERNAL ENTITY ID NAME TABLE                                   01820099
      ****************************************************************  01830099
           EXEC SQL                                                     01840099
               INCLUDE TENTNME                                          01850099
           END-EXEC.                                                    01860099
                                                                        01870099
      ****************************************************************  01880099
      *  COMMUNICATION AREAS FOR DB2                                    01890099
      ****************************************************************  01900099
           EXEC SQL                                                     01910099
               INCLUDE SQLCA                                            01920099
           END-EXEC.                                                    01930099
                                                                        01940099
       LINKAGE SECTION.                                                 01950099
                                                                        01960099
       PROCEDURE DIVISION.                                              01970099
                                                                        01980099
      ***************************************************************** 01990099
      *  MAIN MODULE - BEGINNING OF PGM LOGIC                           02000099
      ***************************************************************** 02010099
       1000-MAIN-MODULE.                                                02020099
                                                                        02030099
           OPEN INPUT  CONTROL-CARD-FILE                                02040099
                INPUT  SMOOTH-OUTLYER-EXT-FILE                          02050099
                OUTPUT SMOOTH-OUTLYER-RPT-FILE.                         02060099
                                                                        02070099
           PERFORM 1100-GET-EXTRACT-DTE.                                02080099
                                                                        02090099
           PERFORM 7100-READ-OUTLYER-EXT-FILE.                          02100099
                                                                        02110099
      *   INTIALIZE FIRST/LAST NAME LENGTH INFO                         02120099
      *   FOR NAME FORMATTING ROUTINE                                   02130099
           MOVE +30               TO DP024-FLNAME-LENGTH.               02140099
                                                                        02150099
           PERFORM 2000-BUILD-OUTLYER-RPT-FILE                          02160099
               UNTIL END-OF-INPUT.                                      02170099
                                                                        02180099
           MOVE PV-INPUT-REC-CNT     TO PV-INPUT-REC-CNT-DIS.           02190099
           MOVE PV-OUTPUT-REC-CNT    TO PV-OUTPUT-REC-CNT-DIS.          02200099
           MOVE PV-MBS-NOT-FOUND-CNT TO PV-MBS-NOT-FOUND-CNT-DIS.       02201099
           DISPLAY ' '.                                                 02210099
           DISPLAY '***     MLKUT616 RECORD COUNTS     ***'             02220099
           DISPLAY '***  RECORDS READ      ' PV-INPUT-REC-CNT-DIS.      02230099
           DISPLAY '***  RECORDS WRITTEN   ' PV-OUTPUT-REC-CNT-DIS.     02240099
           DISPLAY ' '.                                                 02241099
           DISPLAY '***  MBS NOT FOUND CNT ' PV-MBS-NOT-FOUND-CNT-DIS.  02242099
                                                                        02250099
           CLOSE CONTROL-CARD-FILE                                      02260099
                 SMOOTH-OUTLYER-EXT-FILE                                02270099
                 SMOOTH-OUTLYER-RPT-FILE.                               02280099
                                                                        02290099
           STOP RUN.                                                    02300099
                                                                        02310099
      ***************************************************************** 02320099
      *  GET CONTROL CARD EXTRACT DATE FOR RETRIEVING MBS DATA          02330099
      ***************************************************************** 02340099
       1100-GET-EXTRACT-DTE.                                            02350099
                                                                        02360099
           PERFORM 7000-READ-CONTROL-CARD-FILE.                         02370099
                                                                        02380099
           IF END-OF-CONTROL-CARDS OR                                   02390099
              ML114-EXTRACT-DTE = SPACES                                02400099
               DISPLAY '** ABEND **'                                    02410099
               DISPLAY '** PROGRAM MLKUT616 **'                         02420099
               DISPLAY '** PARAGRAPH 1100-GET-EXTRACT-DTE'              02430099
               DISPLAY '** CONTROL CARD NOT FOUND OR WAS SPACES'        02440099
               MOVE +4008 TO ABEND-CODE                                 02450099
               PERFORM 9999-ABEND                                       02460099
           ELSE                                                         02470099
              MOVE ML114-EXTRACT-DTE TO PV-EXTRACT-DTE                  02480099
           END-IF.                                                      02490099
                                                                        02500099
      ***************************************************************** 02510099
      *  BUILD OUTLYER REPORT FILE WITH THE ADDITIONAL MBS INFO         02520099
      ***************************************************************** 02530099
       2000-BUILD-OUTLYER-RPT-FILE.                                     02540099
                                                                        02552099
           ADD 1 TO PV-INPUT-REC-CNT.                                   02560099
           PERFORM 3000-MOVE-DATA-TO-OUTPUT-FILE.                       02570099
                                                                        02580099
           PERFORM 7200-WRITE-OUTLYER-RPT-REC.                          02590099
           ADD 1 TO PV-OUTPUT-REC-CNT.                                  02600099
                                                                        02610099
           INITIALIZE ML204-OUTLYER-EXTRACT.                            02620099
                                                                        02630099
           PERFORM 7100-READ-OUTLYER-EXT-FILE.                          02640099
                                                                        02650099
       3000-MOVE-DATA-TO-OUTPUT-FILE.                                   02700099
           MOVE ML070-PARTN-NBR         TO ML204-PARTN-NBR              02700192
           MOVE ML114-EXTRACT-MN-NBR    TO ML204-FSCL-PERIOD            02700299
           MOVE ML114-EXTRACT-DTE       TO ML204-FSCL-END-DTE           02700399
           MOVE ML070-DEPT-NBR          TO ML204-DEPT-NBR               02700492
           MOVE ML070-MAJ-CL-NBR        TO ML204-MAJ-CL-NBR             02700599
           MOVE ML070-SUB-CL-NBR        TO ML204-SUB-CL-NBR             02700699
           MOVE ML070-LOC-NBR           TO ML204-LOC-NBR                02700799
           MOVE ML070-PARTN-GROUP       TO ML204-PARTN-GROUP            02700899
           MOVE ML070-ML-LWST-ID        TO ML204-ML-LWST-ID             02700992
           MOVE ML070-OUTLYER-CANDI-IND TO ML204-OUTLYER-CANDI-IND      02701073
           MOVE ML070-CURR-SLS-RTL-AMT  TO ML204-ORIG-SLS-RTL-AMT       02702087
           MOVE ML070-CURR-END-INV-RTL-AMT                              02702199
                                        TO ML204-ORIG-END-INV-RTL-AMT   02703074
           MOVE ML070-CURR-END-INV-COST-AMT                             02704079
                                        TO ML204-ORIG-END-INV-COST-AMT  02705079
           MOVE ML070-CURR-CUM-MKUP-PCT TO ML204-ORIG-CUM-MKUP-PCT      02706080
           MOVE ML070-CURR-GRO-MRGN-AMT TO ML204-ORIG-GRO-MRGN-AMT      02707099
           MOVE ML070-TGT-CUM-MKUP-PCT  TO ML204-TGT-CUM-MKUP-PCT       02707186
           MOVE ML070-END-INV-COST-AMT1 TO ML204-ADJ-END-INV-COST-AMT1  02707299
           MOVE ML070-END-INV-COST-AMT2 TO ML204-ADJ-END-INV-COST-AMT2  02707499
           MOVE ML070-INV-COST-ADJ1     TO ML204-INV-COST-ADJ1          02707699
           MOVE ML070-INV-COST-ADJ2     TO ML204-INV-COST-ADJ2          02707799
           MOVE ML070-ADJ-GRO-MRGN-AMT  TO ML204-ADJ-GRO-MRGN-AMT       02707899
           IF ML070-CURR-SLS-RTL-AMT = ZERO                             02707999
               MOVE ZERO                TO ML204-ADJ-GRO-MRGN-PCT       02708099
           ELSE                                                         02708199
               COMPUTE ML204-ADJ-GRO-MRGN-PCT =                         02708299
                   ML070-ADJ-GRO-MRGN-AMT / ML070-CURR-SLS-RTL-AMT      02708399
           END-IF.                                                      02708499
           IF ML070-CURR-GRO-MRGN-AMT = ZERO                            02708599
               MOVE ZERO                 TO ML204-ORIG-GRO-MRGN-PCT     02708699
           ELSE                                                         02708799
               COMPUTE ML204-ORIG-GRO-MRGN-PCT =                        02708899
               (ML204-ORIG-SLS-RTL-AMT / ML070-CURR-GRO-MRGN-AMT) * 100 02709099
           END-IF.                                                      02709199
           IF ML204-ORIG-END-INV-RTL-AMT = ZERO                         02709299
               MOVE ZERO                 TO ML204-SMOOTHED-CUM-MKUP-PCT 02709399
           ELSE                                                         02709499
               COMPUTE ML204-SMOOTHED-CUM-MKUP-PCT =                    02709599
               (1 - ML204-ADJ-END-INV-COST-AMT2 /                       02709699
                    ML204-ORIG-END-INV-RTL-AMT) * 100                   02709799
           END-IF                                                       02709899
                                                                        02709999
           PERFORM 3010-MLV-BRND-VND-DATA.                              02710093
                                                                        02711099
      *    ONLY GET THE MBS FIELDS IF THE DEPT OR MAJ CLASS CHANGED     02720099
           IF ML070-DEPT-NBR   = PV-PREV-DEPT-NBR AND                   02730099
              ML070-MAJ-CL-NBR = PV-PREV-MAJ-CL-NBR-N                   02740099
              NEXT SENTENCE                                             02750099
           ELSE                                                         02760099
              PERFORM 3100-GET-MBS-DESCS                                02770099
           END-IF.                                                      02780099
                                                                        02790099
      *    MOVE MISC MBS FIELDS TO OUTPUT                               02800099
           MOVE PV-DEPT-DESC           TO ML204-NAME-DEPT.              02810099
           MOVE PV-MAJ-CL-DESC         TO ML204-NAME-MCL.               02820099
           MOVE MDSEAR-GMA-NBR         TO ML204-GMM.                    02830099
           MOVE MDSEAR-DMA-NBR         TO ML204-DMM.                    02840099
           MOVE MDSEAR-BYR-NBR         TO ML204-BUYER.                  02850099
                                                                        02860099
      *    MOVE THE GMM NAME (GENERAL MERCH MGR) TO OUTPUT              02870099
           MOVE PV-GMM-FIR-NAME        TO DP024-FNAME.                  02880099
           MOVE PV-GMM-LAST-NAME       TO DP024-LNAME.                  02890099
           PERFORM DP024-0000-FORMAT-NAME.                              02900099
           MOVE DP024-FLNAME           TO ML204-NAME-GMM.               02910099
                                                                        02920099
      *    MOVE THE DMM NAME (DIVISIONAL MERCH MGR) TO OUTPUT           02930099
           MOVE PV-DMM-FIR-NAME        TO DP024-FNAME.                  02940099
           MOVE PV-DMM-LAST-NAME       TO DP024-LNAME.                  02950099
           PERFORM DP024-0000-FORMAT-NAME.                              02960099
           MOVE DP024-FLNAME           TO ML204-NAME-DMM.               02970099
                                                                        02980099
      *    MOVE THE BUYER NAME TO OUTPUT                                02990099
           MOVE PV-BYR-FIR-NAME        TO DP024-FNAME.                  03000099
           MOVE PV-BYR-LAST-NAME       TO DP024-LNAME.                  03010099
           PERFORM DP024-0000-FORMAT-NAME.                              03020099
           MOVE DP024-FLNAME           TO ML204-NAME-BUYER.             03030099
                                                                        03040099
      *    MOVE THE VENDOR NAME TO OUTPUT                               03050099
           MOVE ML204-ENT-ID          TO ENTNME-ENT-ID.                 03061099
           PERFORM 8400-GET-VENDOR-NAME.                                03070099
           MOVE PV-VENDOR-NAME        TO ML204-NAME-VENDOR.             03080099
                                                                        03090099
       3010-MLV-BRND-VND-DATA.                                          03091093
                                                                        03091299
           EXEC SQL                                                     03091399
              SELECT LBL_SEQ_NBR                                        03091499
                    ,ENT_ID                                             03091595
              INTO  :MLV00000-LBL-SEQ-NBR                               03092499
                   ,:MLV00000-ENT-ID                                    03092596
               FROM MLV_BRND_VND                                        03093599
              WHERE ML_LO_MDSE_LVL_ID = :ML070-ML-LWST-ID               03094299
           END-EXEC.                                                    03097499
                                                                        03097599
           EVALUATE TRUE                                                03097699
               WHEN SQLCODE = ZERO                                      03097799
                   MOVE MLV00000-LBL-SEQ-NBR TO ML204-LBL-SEQ-NBR       03097899
                   MOVE MLV00000-ENT-ID      TO ML204-ENT-ID            03097999
               WHEN SQLCODE = +100                                      03098099
                   MOVE ZERO                 TO ML204-LBL-SEQ-NBR       03098199
                                                ML204-ENT-ID            03098299
                   MOVE ML070-ML-LWST-ID     TO PV-ML-LWST-ID           03098399
                      DISPLAY ' '                                       03098499
                      DISPLAY 'LBL_SEQ_NBR NOT FOUND FOR '              03098599
                               PV-ML-LWST-ID                            03098699
               WHEN OTHER                                               03098899
                   MOVE '3010-MLV-BRND-VND-DATA'                        03098999
                                          TO PV-PARAGRAPH-NAME          03099099
                   MOVE 'SELECT ROW FROM MLV_BRND_VND'                  03099199
                                          TO PV-DB2-OPER-ATTEMPTED      03099299
                   PERFORM 9998-SQL-ABEND                               03099399
           END-EVALUATE.                                                03099499
                                                                        03099593
      ***************************************************************** 03100099
      * GET THE CORRECT MBS DESCRIPTION/NAME FIELDS                     03110099
      ***************************************************************** 03120099
       3100-GET-MBS-DESCS.                                              03130099
                                                                        03140099
           MOVE ML070-DEPT-NBR    TO ML204-DEPT-NBR                     03150099
                                     PV-PREV-DEPT-NBR                   03160099
                                     PV-KEY-DEPT-NBR.                   03161099
           MOVE PV-KEY-DEPT-NBR   TO MDSEAR-DEPT-NBR.                   03161199
           MOVE ML070-MAJ-CL-NBR  TO PV-PREV-MAJ-CL-NBR-N               03162099
           MOVE PV-PREV-MAJ-CL-NBR                                      03170099
                                  TO MDSEAR-MAJ-CL-NBR                  03180099
                                                                        03190151
           IF ML070-MAJ-CL-NBR = ZERO                                   03190287
              PERFORM 3200-GET-DEPT-LVL-MBS                             03190351
           ELSE                                                         03190451
              PERFORM 3300-GET-MCL-LVL-MBS                              03190551
           END-IF.                                                      03190651
                                                                        03191099
                                                                        03470099
      ***************************************************************** 03471099
      * GET THE DEPT LEVEL MBS INFORMATION                              03472099
      ***************************************************************** 03473099
        3200-GET-DEPT-LVL-MBS.                                          03474099
                                                                        03474151
           PERFORM 8100-GET-EFF-MBS-DEPT-INFO.                          03474299
                                                                        03474399
           IF MBS-INFO-NOT-FOUND                                        03474499
              PERFORM 8300-GET-LATEST-MBS-DEPT-DATE                     03474599
              IF MBS-MAX-DATE-FOUND                                     03474699
                 MOVE MDSEAR-END-DTE TO PV-EXTRACT-DTE                  03474799
                 PERFORM 8100-GET-EFF-MBS-DEPT-INFO                     03474899
                 IF MBS-INFO-NOT-FOUND                                  03474999
                    PERFORM 3600-SET-MBS-TO-UNKNOWN                     03475099
                 END-IF                                                 03475199
              ELSE                                                      03475299
                 PERFORM 3600-SET-MBS-TO-UNKNOWN                        03475399
              END-IF                                                    03475499
           END-IF.                                                      03475599
                                                                        03475651
      ***************************************************************** 03475799
      * GET THE MAJOR CLASS LEVEL MBS INFORMATION                       03475899
      ***************************************************************** 03475999
        3300-GET-MCL-LVL-MBS.                                           03476099
                                                                        03476151
           PERFORM 8000-GET-EFF-MBS-MCL-INFO.                           03476299
                                                                        03476399
           IF MBS-INFO-NOT-FOUND                                        03476499
              PERFORM 8200-GET-LATEST-MBS-MCL-DATE                      03476599
              IF MBS-MAX-DATE-FOUND                                     03476699
                 MOVE MDSEAR-END-DTE TO PV-EXTRACT-DTE                  03476799
                 PERFORM 8000-GET-EFF-MBS-MCL-INFO                      03476899
                 IF MBS-INFO-NOT-FOUND                                  03476999
                    PERFORM 3600-SET-MBS-TO-UNKNOWN                     03477099
                 END-IF                                                 03477199
              ELSE                                                      03477299
                 PERFORM 3600-SET-MBS-TO-UNKNOWN                        03477399
              END-IF                                                    03477499
           END-IF.                                                      03477599
                                                                        03477651
      ***************************************************************** 03477799
      * SET MBS MBS DESCRIPTION/NAME FIELDS TO "UNKNOWN" SINCE NOTHING  03477899
      * WAS FOUND FOR THE DEPT/MAJ CLASS BEING PROCESSED.               03477999
      ***************************************************************** 03478099
       3600-SET-MBS-TO-UNKNOWN.                                         03478199
                                                                        03478299
           ADD 1 TO PV-MBS-NOT-FOUND-CNT.                               03478351
                                                                        03478451
           IF ML070-MAJ-CL-NBR = ZERO                                   03478588
              MOVE SPACES         TO PV-MAJ-CL-DESC                     03478699
           ELSE                                                         03478751
              MOVE PC-UNKNOWN     TO PV-MAJ-CL-DESC                     03478899
           END-IF.                                                      03478951
                                                                        03479051
           MOVE PC-UNKNOWN        TO PV-DEPT-DESC                       03479199
                                     PV-GMM-FIR-NAME                    03479299
                                     PV-DMM-FIR-NAME                    03479399
                                     PV-BYR-FIR-NAME.                   03479499
                                                                        03479599
           MOVE SPACES            TO PV-GMM-LAST-NAME                   03479699
                                     PV-DMM-LAST-NAME                   03479799
                                     PV-BYR-LAST-NAME.                  03479899
                                                                        03479999
      ***************************************************************** 03480099
      * DPPD024 COMBINES SEPARATE FIRST AND LAST NAME FIELDS INTO ONE   03490099
      * FIRST/LAST NAME FIELD.                                          03500099
      ***************************************************************** 03510099
           COPY DPPD024.                                                03520099
                                                                        03530099
      ***************************************************************** 03540099
      * READ THE CONTROL FILE TO GET THE MBS EXTRACT DATE               03550099
      ***************************************************************** 03560099
       7000-READ-CONTROL-CARD-FILE.                                     03570099
                                                                        03580099
           READ CONTROL-CARD-FILE                                       03590099
                INTO ML114-LEDGER-EXTRACT-DATE-REC                      03600099
                AT END SET END-OF-CONTROL-CARDS TO TRUE.                03610099
                                                                        03620099
      ***************************************************************** 03630099
      * READ SMOOTHING OUTLYER EXTRACT FILE SO A NEW EXTRACT REPORT     03640099
      * FILE MBS INFO AND CALCULATIONS CAN BE CREATED                   03641099
      ***************************************************************** 03650099
       7100-READ-OUTLYER-EXT-FILE.                                      03660099
                                                                        03670099
           READ SMOOTH-OUTLYER-EXT-FILE                                 03680099
               INTO ML070-COMMON-RECALC-EXTRACT                         03690099
               AT END SET END-OF-INPUT TO TRUE.                         03700099
                                                                        03710099
      ***************************************************************** 03720099
      *  WRITE NEW EXTRACT WITH THE ADDITIONAL MBS INFO                 03730099
      ***************************************************************** 03740099
       7200-WRITE-OUTLYER-RPT-REC.                                      03750099
                                                                        03760099
           WRITE SMOOTH-OUTLYER-RPT-REC                                 03770099
               FROM ML204-OUTLYER-EXTRACT.                              03780099
                                                                        03790099
      ***************************************************************** 03800099
      * GET THE DIFFERENT LEVELS OF MBS INFO BASED DEPT/MAJCL/EXTR DTE  03810099
      ***************************************************************** 03820099
       8000-GET-EFF-MBS-MCL-INFO.                                       03830099
                                                                        03840099
           EXEC SQL                                                     03850099
              SELECT A.GMA_NBR                                          03860099
                    ,E.WORKER_FIR_NAME                                  03870099
                    ,E.WORKER_LAST_NAME                                 03880099
                    ,B.DMA_NBR                                          03890099
                    ,F.WORKER_FIR_NAME                                  03900099
                    ,F.WORKER_LAST_NAME                                 03910099
                    ,C.MDSEAR_DESC                                      03920099
                    ,C.BYR_NBR                                          03930099
                    ,G.WORKER_FIR_NAME                                  03940099
                    ,G.WORKER_LAST_NAME                                 03950099
                    ,D.MDSEAR_DESC                                      03960099
              INTO  :MDSEAR-GMA-NBR                                     03970099
                   ,:PV-GMM-FIR-NAME                                    03980099
                   ,:PV-GMM-LAST-NAME                                   03990099
                   ,:MDSEAR-DMA-NBR                                     04000099
                   ,:PV-DMM-FIR-NAME                                    04010099
                   ,:PV-DMM-LAST-NAME                                   04020099
                   ,:PV-DEPT-DESC                                       04030099
                   ,:MDSEAR-BYR-NBR                                     04040099
                   ,:PV-BYR-FIR-NAME                                    04050099
                   ,:PV-BYR-LAST-NAME                                   04060099
                   ,:PV-MAJ-CL-DESC                                     04070099
               FROM TMDSEAR A                                           04080099
                   ,TMDSEAR B                                           04090099
                   ,TMDSEAR C                                           04100099
                   ,TMDSEAR D                                           04110099
                   ,TRSPBAR E                                           04120099
                   ,TRSPBAR F                                           04130099
                   ,TRSPBAR G                                           04140099
              WHERE A.OPERCO_NBR = :PC-OPERCO-NBR                       04150099
                AND B.OPERCO_NBR = A.OPERCO_NBR                         04151099
                AND C.OPERCO_NBR = A.OPERCO_NBR                         04152099
                AND D.OPERCO_NBR = A.OPERCO_NBR                         04153099
                AND A.MDSELV_CDE = :PC-MDSELV-GMA                       04160099
                AND B.MDSELV_CDE = :PC-MDSELV-DMA                       04170099
                AND C.MDSELV_CDE = :PC-MDSELV-DPT                       04180099
                AND D.MDSELV_CDE = :PC-MDSELV-MCL                       04190099
                AND A.GMA_NBR    = B.GMA_NBR                            04200099
                AND B.DMA_NBR    = C.DMA_NBR                            04210099
                AND C.DEPT_NBR   = D.DEPT_NBR                           04220099
                AND C.DEPT_NBR   = :MDSEAR-DEPT-NBR                     04230099
                AND D.MAJ_CL_NBR = :MDSEAR-MAJ-CL-NBR                   04240099
                AND :PV-EXTRACT-DTE BETWEEN A.STRT_DTE AND A.END_DTE    04250099
                AND :PV-EXTRACT-DTE BETWEEN B.STRT_DTE AND B.END_DTE    04260099
                AND :PV-EXTRACT-DTE BETWEEN C.STRT_DTE AND C.END_DTE    04270099
                AND :PV-EXTRACT-DTE BETWEEN D.STRT_DTE AND D.END_DTE    04280099
                                                                        04290099
                AND A.MDSEAR_DKEY = E.MDSEAR_DKEY                       04300099
                AND E.RSPLVL_CDE  = :PC-RSPLVL-CDE-GMM                  04310099
                AND :PV-EXTRACT-DTE BETWEEN E.WORKER_STRT_DTE AND       04320099
                                            E.WORKER_END_DTE            04330099
                                                                        04340099
                AND B.MDSEAR_DKEY = F.MDSEAR_DKEY                       04350099
                AND F.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM                  04360099
                AND :PV-EXTRACT-DTE BETWEEN F.WORKER_STRT_DTE AND       04370099
                                            F.WORKER_END_DTE            04380099
                                                                        04390099
                AND C.MDSEAR_DKEY = G.MDSEAR_DKEY                       04400099
                AND G.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR                  04410099
                AND :PV-EXTRACT-DTE BETWEEN G.WORKER_STRT_DTE AND       04420099
                                            G.WORKER_END_DTE            04430099
           END-EXEC.                                                    04440099
                                                                        04450099
           EVALUATE TRUE                                                04460099
               WHEN SQLCODE = ZERO                                      04470099
                   SET MBS-INFO-FOUND     TO TRUE                       04480099
               WHEN SQLCODE = +100                                      04490099
                   IF MBS-USING-MAX-DATE                                04500099
                      DISPLAY ' '                                       04510099
                      DISPLAY 'MBS INFO NOT FOUND FOR '                 04520099
                              'DEPT/MAJ CL ' MDSEAR-DEPT-NBR            04530099
                                         '/' MDSEAR-MAJ-CL-NBR          04540099
                      SET MBS-NOT-USING-MAX-DATE TO TRUE                04550099
                   END-IF                                               04560099
                   SET MBS-INFO-NOT-FOUND        TO TRUE                04570099
               WHEN OTHER                                               04580099
                   MOVE '8000-GET-EFF-MBS-MCL-INFO'                     04590099
                                          TO PV-PARAGRAPH-NAME          04600099
                   MOVE 'SELECT ROW FROM TMDSEAR/TRSPBAR'               04610099
                                          TO PV-DB2-OPER-ATTEMPTED      04620099
                   PERFORM 9998-SQL-ABEND                               04630099
           END-EVALUATE.                                                04640099
                                                                        04650099
      ***************************************************************** 04651099
      * GET THE DIFFERENT LEVELS OF MBS INFO BASED DEPT/MAJCL/EXTR DTE  04652099
      ***************************************************************** 04653099
       8100-GET-EFF-MBS-DEPT-INFO.                                      04654099
                                                                        04655099
           EXEC SQL                                                     04656099
              SELECT A.GMA_NBR                                          04657099
                    ,E.WORKER_FIR_NAME                                  04658099
                    ,E.WORKER_LAST_NAME                                 04659099
                    ,B.DMA_NBR                                          04659199
                    ,F.WORKER_FIR_NAME                                  04659299
                    ,F.WORKER_LAST_NAME                                 04659399
                    ,C.MDSEAR_DESC                                      04659499
                    ,C.BYR_NBR                                          04659599
                    ,G.WORKER_FIR_NAME                                  04659699
                    ,G.WORKER_LAST_NAME                                 04659799
              INTO  :MDSEAR-GMA-NBR                                     04659999
                   ,:PV-GMM-FIR-NAME                                    04660099
                   ,:PV-GMM-LAST-NAME                                   04660199
                   ,:MDSEAR-DMA-NBR                                     04660299
                   ,:PV-DMM-FIR-NAME                                    04660399
                   ,:PV-DMM-LAST-NAME                                   04660499
                   ,:PV-DEPT-DESC                                       04660599
                   ,:MDSEAR-BYR-NBR                                     04660699
                   ,:PV-BYR-FIR-NAME                                    04660799
                   ,:PV-BYR-LAST-NAME                                   04660899
               FROM TMDSEAR A                                           04661099
                   ,TMDSEAR B                                           04661199
                   ,TMDSEAR C                                           04661299
                   ,TRSPBAR E                                           04661499
                   ,TRSPBAR F                                           04661599
                   ,TRSPBAR G                                           04661699
              WHERE A.OPERCO_NBR = :PC-OPERCO-NBR                       04661799
                AND B.OPERCO_NBR = A.OPERCO_NBR                         04661899
                AND C.OPERCO_NBR = A.OPERCO_NBR                         04661999
                AND A.MDSELV_CDE = :PC-MDSELV-GMA                       04662099
                AND B.MDSELV_CDE = :PC-MDSELV-DMA                       04662199
                AND C.MDSELV_CDE = :PC-MDSELV-DPT                       04662299
                AND A.GMA_NBR    = B.GMA_NBR                            04662399
                AND B.DMA_NBR    = C.DMA_NBR                            04662499
                AND C.DEPT_NBR   = :MDSEAR-DEPT-NBR                     04662599
                AND :PV-EXTRACT-DTE BETWEEN A.STRT_DTE AND A.END_DTE    04662799
                AND :PV-EXTRACT-DTE BETWEEN B.STRT_DTE AND B.END_DTE    04662899
                AND :PV-EXTRACT-DTE BETWEEN C.STRT_DTE AND C.END_DTE    04662999
                                                                        04663199
                AND A.MDSEAR_DKEY = E.MDSEAR_DKEY                       04663299
                AND E.RSPLVL_CDE  = :PC-RSPLVL-CDE-GMM                  04663399
                AND :PV-EXTRACT-DTE BETWEEN E.WORKER_STRT_DTE AND       04663499
                                            E.WORKER_END_DTE            04663599
                                                                        04663699
                AND B.MDSEAR_DKEY = F.MDSEAR_DKEY                       04663799
                AND F.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM                  04663899
                AND :PV-EXTRACT-DTE BETWEEN F.WORKER_STRT_DTE AND       04663999
                                            F.WORKER_END_DTE            04664099
                                                                        04664199
                AND C.MDSEAR_DKEY = G.MDSEAR_DKEY                       04664299
                AND G.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR                  04664399
                AND :PV-EXTRACT-DTE BETWEEN G.WORKER_STRT_DTE AND       04664499
                                            G.WORKER_END_DTE            04664599
           END-EXEC.                                                    04664699
                                                                        04664799
           EVALUATE TRUE                                                04664899
               WHEN SQLCODE = ZERO                                      04664999
                   SET MBS-INFO-FOUND     TO TRUE                       04665099
               WHEN SQLCODE = +100                                      04665199
                   IF MBS-USING-MAX-DATE                                04665299
                      DISPLAY ' '                                       04665399
                      DISPLAY 'MBS INFO NOT FOUND FOR '                 04665499
                              'DEPT ' MDSEAR-DEPT-NBR                   04665599
                      SET MBS-NOT-USING-MAX-DATE TO TRUE                04665799
                   END-IF                                               04665899
                   SET MBS-INFO-NOT-FOUND        TO TRUE                04665999
               WHEN OTHER                                               04666099
                   MOVE '8100-GET-EFF-MBS-DEPT-INFO'                    04666199
                                          TO PV-PARAGRAPH-NAME          04666299
                   MOVE 'SELECT ROW FROM TMDSEAR/TRSPBAR'               04666399
                                          TO PV-DB2-OPER-ATTEMPTED      04666499
                   PERFORM 9998-SQL-ABEND                               04666599
           END-EVALUATE.                                                04666699
                                                                        04666799
      ***************************************************************** 04667099
      * GET THE MAX MBS DATE FOR THE DEPT/MCL AT THE MCL LEVEL.  ONLY   04670099
      * DONE WHEN THE MBS INFO CAN NOT BE FOUND FOR EXTRACT DATE RANGE  04671099
      ***************************************************************** 04680099
       8200-GET-LATEST-MBS-MCL-DATE.                                    04690099
                                                                        04700099
           EXEC SQL                                                     04710099
               SELECT MAX(END_DTE)                                      04720099
                 INTO :MDSEAR-END-DTE :PV-MAX-MCL-DATE-NULL-IND         04730099
                 FROM TMDSEAR                                           04740099
                WHERE MDSELV_CDE = :PC-MDSELV-MCL                       04750099
                  AND DEPT_NBR   = :MDSEAR-DEPT-NBR                     04760099
                  AND MAJ_CL_NBR = :MDSEAR-MAJ-CL-NBR                   04770099
           END-EXEC.                                                    04780099
                                                                        04790099
           EVALUATE TRUE                                                05030099
               WHEN SQLCODE = ZERO                                      05040099
      *             IF NULL IND IS < 0, MAX END DATE WAS NOT FOUND      05050099
                    IF PV-MAX-MCL-DATE-NULL-IND < 0                     05060099
                       DISPLAY ' '                                      05070099
                       DISPLAY 'MBS MAX END DATE NOT FOUND FOR '        05080099
                               'DEPT/MAJ CL ' MDSEAR-DEPT-NBR           05090099
                                          '/' MDSEAR-MAJ-CL-NBR         05100099
                       DISPLAY 'UNKNOWN WILL BE LOADED TO MBS FIELDS'   05110099
                       SET MBS-MAX-DATE-NOT-FOUND   TO TRUE             05120099
                    ELSE                                                05130099
                       SET MBS-USING-MAX-DATE       TO TRUE             05140099
                       SET MBS-MAX-DATE-FOUND       TO TRUE             05150099
                    END-IF                                              05160099
                 WHEN OTHER                                             05220099
                     MOVE '8200-GET-LATEST-MBS-MCL-DATE'                05230099
                                                TO PV-PARAGRAPH-NAME    05240099
                     MOVE 'SELECT ROW FROM TMDSEAR'                     05250099
                                                TO PV-DB2-OPER-ATTEMPTED05260099
                     PERFORM 9998-SQL-ABEND                             05270099
           END-EVALUATE.                                                05280099
                                                                        05290099
      ***************************************************************** 05291099
      * GET THE MAX MBS DATE FOR THE DEPT AT THE DPT LEVEL.  ONLY       05292099
      * DONE WHEN THE MBS INFO CAN NOT BE FOUND FOR EXTRACT DATE RANGE  05293099
      ***************************************************************** 05294099
       8300-GET-LATEST-MBS-DEPT-DATE.                                   05295099
                                                                        05296099
           EXEC SQL                                                     05297099
               SELECT MAX(END_DTE)                                      05298099
                 INTO :MDSEAR-END-DTE :PV-MAX-DEPT-DATE-NULL-IND        05299099
                 FROM TMDSEAR                                           05299199
                WHERE MDSELV_CDE = :PC-MDSELV-DPT                       05299299
                  AND DEPT_NBR   = :MDSEAR-DEPT-NBR                     05299399
           END-EXEC.                                                    05299599
                                                                        05299699
           EVALUATE TRUE                                                05299799
               WHEN SQLCODE = ZERO                                      05299899
      *             IF NULL IND IS < 0, MAX END DATE WAS NOT FOUND      05299999
                    IF PV-MAX-DEPT-DATE-NULL-IND < 0                    05300099
                       DISPLAY ' '                                      05300199
                       DISPLAY 'MBS MAX END DATE NOT FOUND FOR '        05300299
                               'DEPT ' MDSEAR-DEPT-NBR                  05300399
                       DISPLAY 'UNKNOWN WILL BE LOADED TO MBS FIELDS'   05300599
                       SET MBS-MAX-DATE-NOT-FOUND   TO TRUE             05300699
                    ELSE                                                05300799
                       SET MBS-USING-MAX-DATE       TO TRUE             05300899
                       SET MBS-MAX-DATE-FOUND       TO TRUE             05300999
                    END-IF                                              05301099
                 WHEN OTHER                                             05301199
                     MOVE '8300-GET-LATEST-MBS-DEPT-DATE'               05301299
                                                TO PV-PARAGRAPH-NAME    05301399
                     MOVE 'SELECT ROW FROM TMDSEAR'                     05301499
                                                TO PV-DB2-OPER-ATTEMPTED05301599
                     PERFORM 9998-SQL-ABEND                             05301699
           END-EVALUATE.                                                05301799
                                                                        05301899
      ***************************************************************** 05302099
      * GET VENDOR NAME (PRIMARY NAME - TYPE CODE = 01)                 05310099
      ***************************************************************** 05320099
       8400-GET-VENDOR-NAME.                                            05330099
                                                                        05340099
           EXEC SQL                                                     05350099
               SELECT ENT_NAME_DESC                                     05360099
                INTO: ENTNME-ENT-NAME-DESC                              05370099
                FROM  TENTNME A                                         05380099
                     ,TEXTENT B                                         05390099
               WHERE A.ENT_ID   = B.ENT_ID                              05400099
                 AND A.TYPE_CDE = '01'                                  05410099
                 AND A.ENT_ID   = :ENTNME-ENT-ID                        05420099
           END-EXEC.                                                    05430099
                                                                        05440099
           EVALUATE TRUE                                                05450099
               WHEN SQLCODE = 0                                         05460099
                   MOVE ENTNME-ENT-NAME-DESC   TO PV-VENDOR-NAME        05470099
               WHEN SQLCODE = +100                                      05480099
                   MOVE 'VENDOR NOT FOUND'     TO PV-VENDOR-NAME        05490099
               WHEN OTHER                                               05500099
                   MOVE '8400-GET-VENDOR-NAME' TO PV-DB2-OPER-ATTEMPTED 05510099
                   PERFORM 9998-SQL-ABEND                               05520099
           END-EVALUATE.                                                05530099
                                                                        05540099
      ***************************************************************** 05550099
      * BUILD SQL ABEND INFO                                            05560099
      ***************************************************************** 05570099
       9998-SQL-ABEND.                                                  05580099
                                                                        05590099
           MOVE +4013 TO ABEND-CODE.                                    05600099
           DISPLAY ' '.                                                 05610099
           DISPLAY '** ABEND **         ** = SQLERROR'.                 05620099
           DISPLAY '** PROGRAM          ** = MLKUT616'.                 05630099
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.       05640099
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.   05650099
                                                                        05660099
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         05670099
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        05680099
           COPY DPPD004.                                                05690099
                                                                        05700099
           PERFORM 9999-ABEND.                                          05710099
                                                                        05720099
      ***************************************************************** 05730099
      * ABEND PROCESSING                                                05740099
      ***************************************************************** 05750099
       9999-ABEND.                                                      05760099
                                                                        05770099
           CALL 'ILBOABN0'  USING ABEND-CODE.                           05780099
