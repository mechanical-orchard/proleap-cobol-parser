      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.     APKUP014.                                                
       AUTHOR.         COGNIZANT.                                               
       INSTALLATION.   KOHLS DEPARTMENT STORES.                                 
       DATE-WRITTEN.   FEBRUARY 2019.                                           
       DATE-COMPILED.                                                           
                                                                                
      *---------------------------------------------------------------*         
      *  LOADS DATA TO APT_ACCT_TBL DB2 TABLE                         *         
      *---------------------------------------------------------------*         
      *                 COBOL 2 WITH DB2 SQL                          *         
      *---------------------------------------------------------------*         
      *                                                               *         
      *    THIS PROGRAM  INSERTS RECORDS FROM AN INPUT FILE TO THE    *         
      *  APT_ACCT_TBL DB2 TABLE.  THIS IS A LOAD PROGRAM THAT         *         
      *  REPLACES THE BMC UTILITY.                                    *         
      *                                                               *         
      *---------------------------------------------------------------*         
      *                                                               *         
      *  DB2 TABLES:                                DCLGENS:          *         
      *   1. ACCOUNTS TABLE                         (APT00002)        *         
      *                                                               *         
      *  INPUT:                                     COPYBOOK:         *         
      *   1. INPUT-FILE                             APRD526           *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHANGE LOG:                                                   *         
      *   DATE  02/21/2019                                            *         
      *       CHANGE MADE: NEW PROGRAM                                *         
      *       MADE BY: COGNIZANT               WR/IR #: CHG0220043    *         
      *                                                               *         
      *---------------------------------------------------------------*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE   ASSIGN TO INP01.                                 
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 100 CHARACTERS                                       
           BLOCK CONTAINS 0  RECORDS.                                           
       01  INP-REC                     PIC X(100).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                      PIC  X(27)  VALUE                        
           '** START OF APKUP014 W/S **'.                                       
                                                                                
       01  PROGRAM-CONSTANTS.                                                   
           05  PC-PROGRAM-NAME         PIC  X(08)  VALUE 'APKUP014'.            
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-EOF-SW               PIC X(01)   VALUE 'N'.                   
               88  PS-EOF                          VALUE 'Y'.                   
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-DISP-NBR             PIC Z,ZZZ,ZZ9.                           
           05  PV-ABEND-CODE           PIC S9(04)  COMP SYNC                    
                                                   VALUE ZERO.                  
                                                                                
       01  PV-COUNTERS.                                                         
           05  PV-INPUT-COUNT          PIC S9(07)  COMP-3                       
                                                   VALUE ZERO.                  
           05  PV-INSERT-COUNT         PIC S9(07)  COMP-3                       
                                                   VALUE ZERO.                  
                                                                                
      *----------------------------------------------------------------*        
      *    SQL COMMUNICATIONS AREA DCLGEN                                       
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
           COPY SQLCA2.                                                         
                                                                                
      *----------------------------------------------------------------*        
      *    AP NEW STORE DISCOUNT TABLE - APT_ACCT_TBL                           
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE APT00002                                                
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    CHECKPOINT/RESTART CONTROL TABLE - TCKRSTCNTL                        
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE TCKRSTCN                                                
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    CHECKPOINT/RESTART INFO TABLE - DCLTCKRSTINF                         
      *----------------------------------------------------------------*        
           EXEC SQL                                                             
                INCLUDE TCKRSTIN                                                
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    ABEND DATA AREAS                                                     
      *----------------------------------------------------------------*        
       01  ABEND-CODE                  PIC S9(4)   VALUE ZEROES                 
                                       COMP        SYNC.                        
                                                                                
           88  AC-ANY-ERROR                        VALUE  +4001                 
                                                   THRU   +4019.                
           88  AC-OPEN-ERROR                       VALUE  +4005.                
           88  AC-CLOSE-ERROR                      VALUE  +4006.                
           88  AC-INVALID-DATA                     VALUE  +4008.                
           88  AC-DB2-ERROR                        VALUE  +4013.                
           88  AC-UNSUCCESSFUL-SELECT              VALUE  +4015.                
                                                                                
       01  ABEND-AREAS.                                                         
           05  AA-ABEND-LIT            PIC  X(40)  VALUE                        
                   '*****       ABEND'.                                         
           05  AA-PROGRAM-LIT          PIC  X(40)  VALUE                        
                   '*****   PROGRAM: APKUP014'.                                 
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER              PIC  X(17)  VALUE                        
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME   PIC  X(35)  VALUE SPACES.                
           05  AA-MESSAGE-LINE-1.                                               
               10  FILLER              PIC  X(06)  VALUE '*****'.               
               10  AA-MESSAGE-1        PIC  X(80)  VALUE SPACES.                
           05  AA-MESSAGE-LINE-2.                                               
               10  FILLER              PIC  X(06)  VALUE '*****'.               
               10  AA-MESSAGE-2        PIC  X(80)  VALUE SPACES.                
           05  AA-DB2-ERROR-LIT        PIC  X(40)  VALUE                        
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER              PIC  X(17)  VALUE                        
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1     PIC  X(25)  VALUE SPACES.                
                   15  AA-DB2-OP-2     PIC  X(25)  VALUE SPACES.                
           05  AA-DB2-TABLE-1          PIC  X(08)  VALUE SPACES.                
           05  AA-DB2-TABLE-2          PIC  X(08)  VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    SQLCA FORMATTED MESSAGE AREA                                         
      *----------------------------------------------------------------*        
           COPY DPWS004.                                                        
                                                                                
      *                                                                         
       01  FILLER                      PIC  X(25)  VALUE                        
           '** END OF APKUP014 W/S **'.                                         
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
       A100-MAIN.                                                               
                                                                                
           PERFORM B100-INITIALIZE.                                             
                                                                                
           PERFORM B200-PROCESS UNTIL PS-EOF.                                   
                                                                                
           PERFORM B300-EOJ.                                                    
                                                                                
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    INITIALIZATION PROCESSING                                            
      *    - OPEN FILE                                                          
      *    - INITIALIZE DCLGENS AND SWITCHES                                    
      *    - PERFORM SELECTS                                                    
      *----------------------------------------------------------------*        
       B100-INITIALIZE.                                                         
                                                                                
           EXEC SQL                                                             
               LOCK TABLE APT_ACCT_TBL IN EXCLUSIVE MODE                        
           END-EXEC.                                                            
                                                                                
           DISPLAY '**------  APKUP014  (PROCESSING MSGS)  ------**'.           
                                                                                
           OPEN INPUT  INPUT-FILE.                                              
                                                                                
           INITIALIZE  DCLAPT00002                                              
                       DCLTCKRSTCNTL                                            
                       DCLTCKRSTINF                                             
                       PROGRAM-SWITCHES.                                        
                                                                                
           PERFORM R100-READ-INPUT-FILE                                         
                                                                                
           IF PS-EOF                                                            
              DISPLAY '**********EMPTY INPUT FILE**********'                    
           END-IF.                                                              
                                                                                
      *----------------------------------------------------------------*        
      *    PROCESS INPUT FILE                                                   
      *----------------------------------------------------------------*        
       B200-PROCESS.                                                            
                                                                                
           PERFORM I100-INSERT-RECORD.                                          
                                                                                
           PERFORM R100-READ-INPUT-FILE.                                        
                                                                                
      *----------------------------------------------------------------*        
      *    END OF PROGRAM                                                       
      *    - CLOSE FILE                                                         
      *    - DISPLAY COUNTS                                                     
      *----------------------------------------------------------------*        
       B300-EOJ.                                                                
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           PERFORM V100-COMMIT.                                                 
                                                                                
           DISPLAY '****************************************'.                  
           MOVE PV-INPUT-COUNT              TO PV-DISP-NBR.                     
           DISPLAY '** RECORDS READ         = ' PV-DISP-NBR.                    
           MOVE PV-INSERT-COUNT             TO PV-DISP-NBR.                     
           DISPLAY '** RECORDS INSERTED     = ' PV-DISP-NBR.                    
           DISPLAY '****************************************'.                  
           DISPLAY '**     END OF APKUP014 PROCESSING     **'.                  
           DISPLAY '****************************************'.                  
                                                                                
      *----------------------------------------------------------------*        
      *    INSERT ROW TO APT_ACCT_TBL                                           
      *----------------------------------------------------------------*        
       I100-INSERT-RECORD.                                                      
                                                                                
           EXEC SQL                                                             
               INSERT INTO APT_ACCT_TBL                                         
                   (SETID                                                       
                   ,ACCOUNT                                                     
                   ,EFFDT                                                       
                   ,EFF_STATUS                                                  
                   ,DESCR                                                       
                   )                                                            
               VALUES                                                           
                  (:APT00002-SETID                                              
                  ,:APT00002-ACCOUNT                                            
                  ,:APT00002-EFFDT                                              
                  ,:APT00002-EFF-STATUS                                         
                  ,:APT00002-DESCR                                              
                  )                                                             
           END-EXEC                                                             
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  ADD 1                  TO PV-INSERT-COUNT                     
              WHEN SQLCODE = -803                                               
                  CONTINUE                                                      
              WHEN SQLWARN0 NOT = SPACES                                        
              WHEN SQLCODE     NOT = ZERO                                       
                  MOVE 'I100-INSERT-RECORD'                                     
                                      TO AA-PARAGRAPH-NAME                      
                  DISPLAY '** PLAN_NAME: ' PC-PROGRAM-NAME                      
                  MOVE 'UNSUCCESSFUL INSERT' TO AA-DB2-OPERATION                
                  MOVE 'APT00002'        TO AA-DB2-TABLE-1                      
                  MOVE SPACES            TO AA-DB2-TABLE-2                      
                  PERFORM Z998-DB2-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *    READ INPUT FILE                                                      
      *----------------------------------------------------------------*        
       R100-READ-INPUT-FILE.                                                    
                                                                                
           READ INPUT-FILE INTO DCLAPT00002                                     
                AT END                                                          
                    SET PS-EOF                   TO TRUE                        
                NOT AT END                                                      
                    ADD 1                        TO PV-INPUT-COUNT              
           END-READ.                                                            
                                                                                
      *----------------------------------------------------------------*        
      *    ISSUES AN SQL COMMIT WORK AFTER A LUW HAS BEEN COMPLETED.   *        
      *----------------------------------------------------------------*        
       V100-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROS                                             
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'V100-COMMIT' TO AA-PARAGRAPH-NAME                      
                   MOVE 'UNSUCCESSFUL COMMIT' TO AA-DB2-OPERATION               
                   MOVE SPACES        TO AA-DB2-TABLE-1                         
                                         AA-DB2-TABLE-2                         
                   PERFORM Z998-DB2-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *    DB2 ABEND ROUTINE                                                    
      *----------------------------------------------------------------*        
       Z998-DB2-ABEND.                                                          
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-MESSAGE-LINE-1.                                           
           DISPLAY AA-MESSAGE-LINE-2.                                           
           DISPLAY AA-DB2-ERROR-LIT.                                            
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-DB2-OPERATION-LIT.                                        
           DISPLAY AA-DB2-TABLE-1.                                              
           DISPLAY AA-DB2-TABLE-2.                                              
           SET AC-DB2-ERROR TO TRUE.                                            
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
