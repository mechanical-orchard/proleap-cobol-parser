00001 ******************************************************************07/18/95
00002  IDENTIFICATION DIVISION.                                         DBKRP015
00003 ******************************************************************   LV030
00004                                                                      CL**1
00005  PROGRAM-ID.             DBKRP015.                                   CL**1
00006  AUTHOR.                 BRAD DORN.                                  CL**1
00007  INSTALLATION.           KOHLS.                                      CL**1
00008  DATE-WRITTEN            AUGUST 2003.                                CL**1
00009  DATE-COMPILED.                                                      CL**1
00010                                                                      CL**1
00011 ******************************************************************   CL**1
00012 * THIS PROGRAM REPORTS ON PERCENT USED OF THE MAXIMUM ALLOWABLE      CL**1
00013 * DATASET SIZE FOR PARTITIONED TABLESPACES AND INDEXES.              CL**1
00014 *                                                                    CL**1
00015 * THIS INFORMATION CAN BE USED TO DETERMINE WHICH PARTITIONS ARE     CL**1
00015 * CLOSE TO FILLING UP.                                               CL**1
00014 *                                                                    CL**1
00016 ******************************************************************   CL**1
00020                                                                      CL**1
00021 ******************************************************************   CL**1
00022  ENVIRONMENT DIVISION.                                               CL**1
00023 ******************************************************************   CL**1
00024                                                                      CL**1
00025  CONFIGURATION SECTION.                                              CL**1
00026                                                                      CL**1
00027  SOURCE-COMPUTER.        IBM-3090.                                   CL**1
00028  OBJECT-COMPUTER.        IBM-3090.                                   CL**1
00029                                                                      CL**1
00030  INPUT-OUTPUT SECTION.                                               CL**1
00031                                                                      CL**1
00032  FILE-CONTROL.                                                       CL**1
00033                                                                      CL**1
00034      SELECT LISTCAT-DATA             ASSIGN TO INP01.                CL**3
00037      SELECT OBJECT-REPORT            ASSIGN TO RPT01.                CL**1
00039                                                                      CL**1
00040 ******************************************************************   CL**1
00041  DATA DIVISION.                                                      CL**1
00042 ******************************************************************   CL**1
00043                                                                      CL**1
00044  FILE SECTION.                                                       CL**1
00045                                                                      CL**1
00046  FD  LISTCAT-DATA                                                    CL**3
00047      RECORDING MODE IS F                                             CL**1
00048      LABEL RECORDS ARE STANDARD                                      CL**1
00049      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00050                                                                      CL**1
00051  01  LISTCAT-DATA-REC                PIC  X(56).                     CL**3
00052                                                                      CL**1
00067  FD  OBJECT-REPORT                                                   CL**1
00068      RECORDING MODE IS F                                             CL**1
00069      LABEL RECORDS ARE STANDARD                                      CL**1
00070      BLOCK CONTAINS 0 RECORDS.                                       CL**1
00071                                                                      CL**1
00072  01  OBJECT-REPORT-REC               PIC  X(132).                    CL**1
00073                                                                      CL**1
00081 ******************************************************************   CL**1
00082  WORKING-STORAGE SECTION.                                            CL**1
00083 ******************************************************************   CL**1
00084                                                                      CL**1
00085 *  STANDARD REPORT HEADER FOR 132 COLUMN REPORT                      CL**1
00086                                                                      CL**1
00087  01  DP132O1-STANDRD-HEADER.                                         CL**1
00088      05  DP132O1-NEW-PAGE-INDICATR         PIC X(02) VALUE SPACE.    CL**1
00089      05  DP132O1-RUN-DATE.                                           CL**1
00090          10  DP132O1-RUN-MONTH             PIC 9(02) VALUE ZERO.     CL**1
00091          10  FILLER                        PIC X(01) VALUE '/'.      CL**1
00092          10  DP132O1-RUN-DAY               PIC 9(02) VALUE ZERO.     CL**1
00093          10  FILLER                        PIC X(01) VALUE '/'.      CL**1
00094          10  DP132O1-RUN-YEAR              PIC 9(02) VALUE ZERO.     CL**1
00095      05  FILLER                            PIC X(02) VALUE SPACE.    CL**1
00096      05  DP132O1-RUN-TIME.                                           CL**1
00097          10  DP132O1-RUN-HOUR              PIC 9(02) VALUE ZERO.     CL**1
00098          10  FILLER                        PIC X(01) VALUE ':'.      CL**1
00099          10  DP132O1-RUN-MINUTE            PIC 9(02) VALUE ZERO.     CL**1
00100      05  FILLER                            PIC X(26) VALUE SPACE.    CL**1
00101      05  FILLER                            PIC X(45)                 CL**1
00102          VALUE 'K O H L S   D E P A R T M E N T   S T O R E S'.      CL**1
00103      05  FILLER                            PIC X(21) VALUE SPACE.    CL**1
00104      05  DP132O1-PROGRAM-NAME              PIC X(08) VALUE SPACE.    CL**1
00105      05  FILLER                            PIC X(01) VALUE '-'.      CL**1
00107      05  FILLER                            PIC X(02) VALUE SPACE.    CL**1
00108      05  FILLER                            PIC X(05) VALUE 'PAGE'.   CL**1
00109      05  DP132O1-PAGE-NUMBER               PIC ZZZZ9 VALUE ZERO.     CL**1
00110      05  FILLER                            PIC X(01) VALUE SPACE.    CL**1
00111                                                                      CL**1
00112  01  HL1-HEADING-LINE-1.                                             CL**1
00113      05  FILLER                      PIC  X(50)      VALUE SPACE.    CL**1
00114      05  FILLER                      PIC  X(82)      VALUE           CL**1
00115          'PARTITIONED OBJECT DATASET REPORT'.                        CL**1
00116                                                                      CL**1
00117  01  DH1-DETAIL-HEADING-LINE-1.                                      CL**1
00118      05  FILLER                      PIC  X(12)      VALUE SPACES.   CL*26
00119      05  FILLER                      PIC  X(17)      VALUE           CL*12
00120          'PARTITION DATASET'.                                        CL**1
00123      05  FILLER                      PIC  X(29)      VALUE SPACES.   CL*26
00123      05  FILLER                      PIC  X(03)      VALUE           CL*26
00124          'MAX'.                                                      CL**1
00123      05  FILLER                      PIC  X(18)      VALUE SPACES.   CL*26
00125      05  FILLER                      PIC  X(04)      VALUE           CL*26
00126          'USED'.                                                     CL*26
00123      05  FILLER                      PIC  X(20)      VALUE SPACES.   CL*26
00127      05  FILLER                      PIC  X(03)      VALUE           CL*26
00128          'PCT'.                                                      CL*26
00129                                                                      CL**1
00130  01  DH1-DETAIL-HEADING-LINE-2.                                      CL**1
00131      05  FILLER                      PIC  X(58)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(03)      VALUE           CL*12
00133          'GIG'.                                                      CL**1
00131      05  FILLER                      PIC  X(19)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(03)      VALUE           CL*12
00133          'GIG'.                                                      CL**1
00131      05  FILLER                      PIC  X(17)      VALUE SPACES.   CL*26
00132      05  FILLER                      PIC  X(06)      VALUE           CL*12
00133          'FILLED'.                                                   CL**1
00150                                                                      CL**1
00151  01  DL-DETAIL-LINE.                                                 CL**1
00152      05  FILLER                      PIC  X(05)      VALUE SPACES.   CL*26
00153      05  DL-DATASET                  PIC  X(42)      VALUE SPACES.   CL**1
00154      05  FILLER                      PIC  X(05)      VALUE SPACES.   CL*12
00155      05  DL-MAX-GIG                  PIC  ZZZZZZZ99  VALUE ZERO.     CL**1
00156      05  FILLER                      PIC  X(10)      VALUE SPACES.   CL*12
00157      05  DL-USED-GIG                 PIC  ZZZZZZZ99.99                    
                                                           VALUE ZERO.          
00158      05  FILLER                      PIC  X(19)      VALUE SPACES.   CL*26
00159      05  DL-PCT-FULL                 PIC  ZZ9        VALUE ZERO.     CL*26
           05  FILLER                      PIC  X(35)      VALUE '%'.           
00171                                                                      CL**1
00287                                                                      CL**1
00288  01  END-OF-REPORT-LINE.                                             CL*30
00289      05  FILLER                      PIC  X(54)      VALUE SPACE.    CL*30
00290      05  FILLER                      PIC  X(25)      VALUE           CL*30
00291          '***** END OF REPORT *****'.                                CL*30
00292      05  FILLER                      PIC  X(53)      VALUE SPACE.    CL*30
00293                                                                      CL*30
00294  01  BLANK-LINE                      PIC X(132)      VALUE SPACES.   CL**1
00295                                                                      CL**1
00296  01  SD-SYSTEM-DATE.                                                 CL**1
00297      05  SD-SYSTEM-YEAR              PIC  9(02)      VALUE ZERO.     CL**1
00298      05  SD-SYSTEM-MONTH             PIC  9(02)      VALUE ZERO.     CL**1
00299      05  SD-SYSTEM-DAY               PIC  9(02)      VALUE ZERO.     CL**1
00300                                                                      CL**1
00301  01  ST-SYSTEM-TIME.                                                 CL**1
00302      05  ST-SYSTEM-HOUR              PIC  9(02)      VALUE ZERO.     CL**1
00303      05  ST-SYSTEM-MINUTE            PIC  9(02)      VALUE ZERO.     CL**1
00304      05  FILLER                      PIC  9(04)      VALUE ZERO.     CL**1
00305                                                                      CL**1
00310  01  WS-CONSTANTS.                                                   CL**1
00311      05  WS-PAGE-SIZE                PIC S9(04)      VALUE +55       CL**1
00312                                                      COMP SYNC.      CL**1
00315                                                                      CL**1
00316  01  WS-SWITCHES.                                                    CL**1
00317      05  WS-LISTCAT-DATA-EOF-SW      PIC  X(01)  VALUE 'N'.          CL**3
00319                                                                      CL**1
00329                                                                      CL**1
00330  01  WS-COUNTERS.                                                    CL**1
00331      05  WS-LINE-COUNT               PIC S9(04)      VALUE +56       CL**1
00332                                                      COMP SYNC.      CL**1
00335      05  WS-PAGE-COUNT               PIC S9(04)      VALUE ZERO      CL**1
00336                                                      COMP SYNC.      CL**1
00343                                                                      CL**1
00344  01  WS-LISTCAT-DATA-REC.                                            CL**3
00345      05  WS-LISTCAT-DSN            PIC  X(42)      VALUE SPACES.     CL**6
00347      05  WS-LISTCAT-MAX-GIG        PIC  9(09)      COMP  VALUE 0.    CL**6
00347      05  WS-LISTCAT-USED-GIG       PIC  9(09)V99   COMP  VALUE 0.    CL**6
00347      05  WS-LISTCAT-PCT-FULL       PIC  9V99       COMP  VALUE 0.    CL**6
00367                                                                      CL**1
00367                                                                      CL**1
00432 ******************************************************************   CL**1
00433  LINKAGE SECTION.                                                    CL**1
00434 ******************************************************************   CL**1
00435                                                                      CL**1
00436 ******************************************************************   CL**1
00437  PROCEDURE DIVISION.                                                 CL**1
00438 ******************************************************************   CL**1
00439  0000-PROGRAM-MAINLINE.                                              CL**1
00440                                                                      CL**1
00441      PERFORM 1000-INITIALIZATION.                                    CL**6
00442                                                                      CL**1
00443      PERFORM 1100-READ-LISTCAT.                                      CL**6
00444                                                                      CL**1
00454      PERFORM 2000-PROCESS-LISTCAT                                    CL**6
00455         UNTIL WS-LISTCAT-DATA-EOF-SW = 'Y'.                          CL**6
00456                                                                      CL**1
00466      PERFORM 3000-END-OF-JOB.                                        CL**1
00467                                                                      CL**1
00468      GOBACK.                                                         CL**1
00469                                                                      CL**1
00470 ******************************************************************   CL**1
00471 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**1
00472 * - OPENS FILES                                                      CL**6
00473 * - SETS UP THE REPORT HEADINGS                                      CL**1
00476 ******************************************************************   CL**1
00477                                                                      CL**1
00478  1000-INITIALIZATION.                                                CL**1
00479                                                                      CL**1
00480      OPEN INPUT  LISTCAT-DATA                                        CL**6
00482      OPEN OUTPUT OBJECT-REPORT.                                      CL**6
00495                                                                      CL**1
00496      ACCEPT SD-SYSTEM-DATE FROM DATE.                                CL**1
00497      ACCEPT ST-SYSTEM-TIME FROM TIME.                                CL**1
00498                                                                      CL**1
00499      MOVE SD-SYSTEM-YEAR         TO DP132O1-RUN-YEAR.                CL**1
00501      MOVE SD-SYSTEM-MONTH        TO DP132O1-RUN-MONTH.               CL**1
00503      MOVE SD-SYSTEM-DAY          TO DP132O1-RUN-DAY.                 CL**1
00505      MOVE ST-SYSTEM-HOUR         TO DP132O1-RUN-HOUR.                CL**1
00507      MOVE ST-SYSTEM-MINUTE       TO DP132O1-RUN-MINUTE.              CL**1
00509      MOVE 'DBKRP015'             TO DP132O1-PROGRAM-NAME.            CL**1
00513                                                                      CL**1
00548                                                                      CL*17
00580 ******************************************************************   CL**6
00581 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**6
00582 * - READS THE TABLE FILE                                             CL**6
00583 ******************************************************************   CL**6
00584                                                                      CL**6
00585  1100-READ-LISTCAT.                                                  CL**6
00586                                                                      CL**6
00587      READ LISTCAT-DATA INTO WS-LISTCAT-DATA-REC                      CL**6
00588          AT END MOVE 'Y' TO WS-LISTCAT-DATA-EOF-SW.                  CL**6
00589                                                                      CL**6
00611                                                                      CL**6
00549 ******************************************************************   CL**1
00550 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**1
00551 * - FOR EACH INPUT RECORD, FORMAT AND WRITE A REPORT RECORD.         CL*21
00555 ******************************************************************   CL**1
00556                                                                      CL**1
00557  2000-PROCESS-LISTCAT.                                               CL**6
                                                                                
           MOVE WS-LISTCAT-DSN       TO DL-DATASET.                             
           MOVE WS-LISTCAT-MAX-GIG   TO DL-MAX-GIG.                             
           MOVE WS-LISTCAT-USED-GIG  TO DL-USED-GIG.                            
           COMPUTE DL-PCT-FULL = WS-LISTCAT-PCT-FULL * 100.                     
                                                                                
00742      IF WS-LINE-COUNT > WS-PAGE-SIZE                                 CL**1
00743          PERFORM 2411-WRITE-PAGE-HEADINGS                            CL**6
00744      END-IF.                                                         CL**1
00745                                                                      CL**1
00746      WRITE OBJECT-REPORT-REC FROM DL-DETAIL-LINE.                    CL**1
00747                                                                      CL**1
00748      ADD +1 TO WS-LINE-COUNT.                                        CL**1
00749                                                                      CL**1
00731      WRITE OBJECT-REPORT-REC FROM BLANK-LINE.                        CL*12
00732                                                                      CL*12
00733      ADD +1 TO WS-LINE-COUNT.                                        CL*12
                                                                                
           PERFORM 1100-READ-LISTCAT.                                      CL**6
                                                                                
00741                                                                      CL**1
00750 ******************************************************************   CL**1
00751 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**1
00752 * - WRITES THE PAGE HEADINGS                                         CL*21
00753 ******************************************************************   CL**1
00754                                                                      CL**1
00755  2411-WRITE-PAGE-HEADINGS.                                           CL**6
00756                                                                      CL**1
00757      ADD +1               TO WS-PAGE-COUNT.                          CL**1
00758      MOVE WS-PAGE-COUNT   TO DP132O1-PAGE-NUMBER.                    CL**1
00759                                                                      CL**1
00760      WRITE OBJECT-REPORT-REC                                         CL**1
00761        FROM DP132O1-STANDRD-HEADER                                   CL**1
00762          AFTER ADVANCING PAGE.                                       CL**1
00763                                                                      CL**1
00764      MOVE SPACES          TO DP132O1-NEW-PAGE-INDICATR.              CL**1
00765                                                                      CL**1
00766      WRITE OBJECT-REPORT-REC                                         CL**1
00767        FROM HL1-HEADING-LINE-1                                       CL**1
00768          AFTER ADVANCING 2 LINES.                                    CL**1
00769                                                                      CL**1
00770      WRITE OBJECT-REPORT-REC                                         CL**1
00771        FROM DH1-DETAIL-HEADING-LINE-1                                CL**1
00772          AFTER ADVANCING 2 LINES.                                    CL**1
00773                                                                      CL**1
00774      WRITE OBJECT-REPORT-REC                                         CL**1
00775        FROM DH1-DETAIL-HEADING-LINE-2                                CL**1
00776          AFTER ADVANCING 1 LINES.                                    CL**1
00777                                                                      CL**1
00778      WRITE OBJECT-REPORT-REC                                         CL**1
00779        FROM BLANK-LINE                                               CL**1
00780          AFTER ADVANCING 1 LINE.                                     CL**1
00781                                                                      CL**1
00782      MOVE +7              TO WS-LINE-COUNT.                          CL**1
00783                                                                      CL**1
01055 ******************************************************************   CL**1
01056 * THIS PARAGRAPH PERFORMS THE FOLLOWING:                             CL**1
01057 * - WRITE THE END OF REPORT LINES                                    CL**1
01058 ******************************************************************   CL**1
01059                                                                      CL**1
01060  3000-END-OF-JOB.                                                    CL**1
01061                                                                      CL**1
01065      WRITE OBJECT-REPORT-REC FROM END-OF-REPORT-LINE                 CL*30
01066          AFTER ADVANCING 2 LINES.                                    CL*30
01067                                                                      CL*30
01074      CLOSE LISTCAT-DATA                                              CL*10
01076            OBJECT-REPORT.                                            CL**6
01078                                                                      CL**1
00424                                                                   DBKRP015
01087                                                                      CL**1
