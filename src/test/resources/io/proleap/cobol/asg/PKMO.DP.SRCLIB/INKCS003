      *-----------------------*                                         12/20/94
00002  IDENTIFICATION DIVISION.                                         INKCS003
00003 *-----------------------*                                            LV001
00004  PROGRAM-ID.    INKCS003.                                         INKCS003
00005  AUTHOR.        M. FURLICK - STRATAGEM                            INKCS003
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          INKCS003
00007  DATE-WRITTEN.  08/93.                                            INKCS003
00008  DATE-COMPILED.                                                   INKCS003
00009                                                                   INKCS003
00010 *---------------------------------------------------------------* INKCS003
00011 *    I003 - PHYSICAL INVENTORY INVALID DATA DETAIL UPDATE       * INKCS003
00012 *                                                               * INKCS003
00013 *    THIS PROGRAM GENERATES A LIST OF THE LINES WITHIN AN AREA  * INKCS003
00014 *    FOR THE SELECTED STORE, THAT STILL HAVE ERRORS.  THIS LIST * INKCS003
00015 *    IS USED BY STORE PERSONNEL PRIMARILY.                      * INKCS003
00016 *                                                               * INKCS003
00017 *    THIS IS AN "UPDATABLE" LIST WHERE THE USER MAKES THEIR     * INKCS003
00018 *    CHANGES DIRECTLY ON THE SCREEN.  REGULAR LIST FUNCTIONS    * INKCS003
00019 *    ARE AVAILABLE AS WELL.                                     * INKCS003
00020 *---------------------------------------------------------------* INKCS003
00021 *                       CHANGE LOG                              * INKCS003
00022 *---------------------------------------------------------------* INKCS003
00023 *  M BLAZICH     ADDED SCAN LINE NUMBER LOGIC                   * INKCS003
00024 *  ALSO EXPANDED EXTD_AMT COLUMN ON TABLE ON TADJDTL            * INKCS003
00025 *---------------------------------------------------------------* INKCS003
00026 *  KIM WRIGHT                                   9/16/94         * INKCS003
00027 *  CHANGED THE SELECTION OF CUTOFF PRICE ON THE TSKST TABLE TO  * INKCS003
00028 *  SELECT FROM STORE '000' INSTEAD OF THE SPECIFIC STORE        * INKCS003
00025 *---------------------------------------------------------------* INKCS003
00026 *  SCOTT JACKSON                                3/15/99         * INKCS003
00027 *  CHANGED THE SELECTION OF CUTOFF PRICE ON THE TSKST TABLE TO  * INKCS003
00028 *  SELECT FROM THE SPECIFIC STORE INSTEAD OF STORE '000'. ADD   * INKCS003
00029 *  SELECT OF TSKST-INV-CTOFF-STAT-CDE TO 3610-GET-CUTOFF-INFO   * INKCS003
00029 *  TO RETRIEVE INDIVIDUAL STORE ITEM STATUS AS WELL AS CTOF-RTL * INKCS003
00029 *---------------------------------------------------------------* INKCS003
JS0699*  JULIE SEIDLER                                6/18/99         * INKCS003
JS0699*  FIXED PROGRAM TO NOT ABEND WHEN ITEM WITH SKU 000 OR 999     * INKCS003
JS0699*  WAS ENTERED.                                                 * INKCS003
00029 *---------------------------------------------------------------* INKCS003
JS1099*  JULIE SEIDLER                               10/18/99         * INKCS003
JS1099*  CHANGED PROGRAM TO OBTAIN PRICE AND STATUS INFO FROM TUPCPLS * INKCS003
JS1099*  INSTEAD OF TSKST.  THIS IS PART OF THE RCP PROJECT           * INKCS003
      *----------------------------------------------------------------*        
IN2001* 09/25/2000  SCOTT JACKSON      CHG ID:IN2001                  *         
IN2001*  AN ITEM IN RCP STATUS MAY BE RETURNED TO NON-RCP STATUS      *         
IN2001*  PRIOR TO PROCESSING ALL OF THE STORE'S DATA. THEREFORE USING *         
IN2001*  TSK.REGN_PRICE_IND AS THE IND TO OBTAINING CURRENT PRICE MAY *         
IN2001*  BE INCORRECT AND UNRELIABLE BASED ON WHEN DATA IS PROCESSED. *         
IN2001*  TUPCPLS WILL BE USED SOLELY IN DETERMINING AN ITEM'S STATUS. *         
IN2001*  THE APPROPRIATE ROW IS THE ONE ACTIVE AS OF A STORES INV DTE.*         
IN2001*  CHECK STORE 000 ROW 1ST (IF STATUS = '25' - THEN CHECK FOR   *         
IN2001*  STORE-SPECIFIC ROW). USE STORE 000 PRICING IF NO STORE ROW.  *         
IN2001*  IF STATUS <> '25' THEN STORE-SPECIFIC ROW WILL NOT BE CHECKED*         
W03072*----------------------------------------------------------------*        
W03072* 03/07/2002  ROD JANSSEN        CHG ID:99999                             
W03072*  BUILD IN EFFICIENCIES TO TUPCPLS (PER BRAD DORN - DBA)                 
W03072*---------------------------------------------------------------*         
23511C* 05/21/2002 CAROL WOJCIECHOWSKI CHG ID:23511C                  *         
23511C*  DISPLAY THE PRICE FOR A YELLOW TICKET SKU WHEN THE           *         
23511C*  UPC_STAT_CHG_DTE IS EQUAL TO THE UNT_RTL_CHG_DTE             *         
W03072*---------------------------------------------------------------*         
W21732* 10/24/2002 CHANGED BY IGSI           WR# W21732               *         
W21732*    DISPAY AND ACCEPT NON-INTELLIGENT SKU INSTEAD OF           *         
W21732*    DEPT/CLASS/SEQ NUMBER AND CHANGED  TINVDTL, TADJDTL AND    *         
W21732*    TMDSEAR TABLES TO LOOK UP TO USE THE NON-INTELLIGENT SKU.  *         
W21732*    REPLACED THE USE OF TSK TABLE WITH TSKXREF TABLE TO GET THE*         
C21732*  3/17/2003  S.JACKSON           CHG ID#  C21732               *         
C21732*    MODULE UPC CHK  REPLACES DPKUT040 FOR CHECK-DIGIT COMPUTES *         
C21732*    OR VALIDATION.                                                       
C21732*---------------------------------------------------------------*         
25248 * 25248    10-06-2003  UPC CAPABILITIES                                   
25248 *                      -ROD JANSSEN                                       
      * 26600    04-20-2004  STORE NUMBER EXPANSION FOR INV. LEDGER             
      *                      TEMPORARILY ADD LOC-NBR TO INSERT STMT             
      *                      -V. LEE YANG                                       
W26600* W26600   06-29-2004  INV STORE EXPANSION                                
W26600*                      -ROD JANSSEN                                       
J28545*  28545   05-30-2006  USE THE PRICING MODULE, INPD400, TO LOOK           
J28545*                      UP PRICE AND VALIDATE SKU OR UPC.                  
J28545* W28545 06-16-2006 JAN BLAZICH  TINVPAR/TINCNTL ARE USED TO GET          
J28545*                                THE PROPER INV-ID AND STORE              
X28545* W28545 08-11-2006 ROD JANSSEN     NOT SHOWING SKU WHEN UPC IS           
X28545*                                ENTERED THE PRICE IS CLEARED WHEN        
X28545*                                ENTERED AND THE ENTER KEY IS             
X28545*                                SELECTED NOT FINDING CORP SKU_DLT        
X28545*                                PRICE PROPERLY.                          
X28545*                                   AZRA ABEND WITH DUMMY SKU(S)          
X28545*                                AND TRAVELING FROM SUMMARY/LIST          
X28545*                                SCREENS TO THE UPDATE SCREENS.           
S28545* W28545 10-16-2006 S JACKSON    UPC HANDLING FOR RENUMBER SKU            
S28545*                                LOGIC WAS RETURNING 0.00 RETAIL          
32318 * W32318 02-22-2007 ROD JANSSEN  INVALID CORRECTION SCREENS ARE           
32318 *                                NOT TESTING FOR PLU = 'N'. ADD AN        
32318 *                                ERROR LOGIC TO ACCOMPLISH THIS.          
      * W33304 05-01-2008 JILL LIN     HOLDING INVENTORY LEDGER OPEN            
      *                                ACROSS PERIODS INVENTORY SHORTAGE        
      *                                REPORTING                                
SR3403* SR3403 03-27-2012 JALAJ KUMAR  AMT FIELD EXPANSIONS FOR LARGE           
SR3403*                                RETAIL PROJECT                           
      *****************************************************************         
263126* CHANGED 10/25/2021    BY JIM HUNTER      WR: CHG0263126                 
263126* CHANGE CALL OF DPKUT100 TO CALL DP010I-NUMERIC-EDIT-ROUTINE             
263126* MAKING THE CALL DYNAMIC VS STATIC.                                      
263126*----------------------------------------------------------------*   CL*17
1022RM* CHANGED 10/26/2022    BY BOB MCASEY      WR: CHG0277188                 
1022RM* ADDED THE DPKCS930 COPYBOOK. CHANGED THE PARAMETERS FOR CALLING         
1022RM* THE CICS ARCHITECTURE API.                                              
263126*----------------------------------------------------------------*   CL*17
1022RM* CHANGED 02/14/2023    BY JEAN KURK       WR: CHG0281369                 
1022RM* CHANGED CALL TO UPC CHECK DIGIT ROUTINE DYNAMIC                         
      *****************************************************************         
00030                                                                   INKCS003
00031  ENVIRONMENT DIVISION.                                            INKCS003
00032                                                                   INKCS003
00033  DATA DIVISION.                                                   INKCS003
00034                                                                   INKCS003
00035  WORKING-STORAGE SECTION.                                         INKCS003
00036                                                                   INKCS003
00037  01  PC-PROGRAM-CONSTANTS.                                        INKCS003
00038      05  PC-CURRENT-MAP-NAME            PIC  X(08)  VALUE         INKCS003
00039                                                     'IN003A  '.   INKCS003
00040      05  PC-CURRENT-MAPSET-NAME         PIC  X(08)  VALUE         INKCS003
00041                                                     'INKM003 '.   INKCS003
00042      05  PC-CURRENT-PROGRAM-NAME        PIC  X(08)  VALUE         INKCS003
00043                                                     'INKCS003'.   INKCS003
00044      05  PC-LIST-QUEUE-SEQ              PIC  9(01)  VALUE 1.      INKCS003
00045      05  PC-SEL-QUEUE-SEQ               PIC  9(01)  VALUE 2.      INKCS003
00046      05  PC-SEL-QUEUE-SEQ2              PIC  9(01)  VALUE 3.      INKCS003
00047      05  PC-CRIT-SPEC-FORMAT            PIC  X(01)  VALUE '1'.    INKCS003
00048      05  PC-ITEMS-PER-PANEL             PIC S9(04)  VALUE +13     INKCS003
00049                                                     COMP SYNC.    INKCS003
00050      05  PC-MAX-BLOCKS-IN-ARRAY         PIC S9(04)  VALUE +2      INKCS003
00051                                                     COMP SYNC.    INKCS003
00052      05  PC-MAX-ITEMS                   PIC S9(09)  VALUE         INKCS003
00053                                                     +1000000      INKCS003
00054                                                     COMP-3.       INKCS003
00055      05  PC-MSG-NUMBER-LENGTH           PIC S9(04)  VALUE +5      INKCS003
00056                                                     COMP SYNC.    INKCS003
00057      05  PC-Y                           PIC  X(01)  VALUE 'Y'.    INKCS003
00058      05  PC-N                           PIC  X(01)  VALUE 'N'.    INKCS003
00059      05  PC-SCL                         PIC  X(03)  VALUE 'SCL'.  INKCS003
J28545     05  PC-MAX-PRICE                   PIC S9(05)V99 COMP-3      INKCS005
J28545                                                  VALUE +99999.99.INKCS005
J28545     05  PC-TWENTY-ONE-CENTS            PIC S9(05)V99 COMP-3      INKCS005
J28545                                                    VALUE +.21.   INKCS005
J28545     05  PC-8999                        PIC S9(08)  VALUE 8999    INKCS003
J28545                                                          COMP.   INKCS003
J28545     05  PC-10000                       PIC S9(08)  VALUE 10000   INKCS003
J28545                                                          COMP.   INKCS003
00060      05  PC-9000                        PIC S9(08)  VALUE 9000    INKCS003
00061                                                           COMP.   INKCS003
00062      05  PC-9999                        PIC S9(08)  VALUE 9999    INKCS003
00063                                                           COMP.   INKCS003
00064      05  PC-500                         PIC  9(03)  VALUE 500.    INKCS003
JS1099     05  PC-98                          PIC  9(03)  VALUE 098.    INKCS003
JS1099     05  PC-98-X                        PIC  X(02)  VALUE '98'.   INKCS003
JS1099     05  PC-30                          PIC  X(02)  VALUE '30'.   INKCS003
IN2001     05  PC-25                          PIC  X(02)  VALUE '25'.   INKCS003
00067      05  PC-15                          PIC  9(02)  VALUE 15.     INKCS003
00068      05  PC-7                           PIC  9(01)  VALUE 7.      INKCS003
00069      05  PC-03                          PIC  X(02)  VALUE '03'.   INKCS003
W21732     05  PC-8                           PIC  9(01)  VALUE 8.      INKCS003
00071      05  PC-2                           PIC  9(01)  VALUE 2.      INKCS003
00072      05  PC-20-CENTS                    PIC S9(01)V99 VALUE .20   INKCS003
00073                                                     COMP-3.       INKCS003
00074      05  PC-CO                          PIC  X(02)  VALUE 'CO'.   INKCS003
00075      05  PC-CORRECTION                  PIC  X(10)  VALUE         INKCS003
00076                                                     'CORRECTION'. INKCS003
           05  PC-STORE-000            PIC S9(04) COMP        VALUE +0.         
00078      05  PC-INTER-APPL-CODES.                                     INKCS003
00079          10  PC-INTER-APPL-CRIT-SPEC                              INKCS003
00080                                   PIC  X(01)  VALUE '1'.          INKCS003
00081          10  PC-INTER-APPL-LIST   PIC  X(01)  VALUE '2'.          INKCS003
W03072     05  PC-ZERO-STORE           PIC S9(4)    VALUE +0 COMP.              
JS1099     05  PC-NULL-DATE            PIC X(10)    VALUE '9999-09-09'.         
00082                                                                   INKCS003
00083      05  PC-TSYMSG-NUMBERS.                                       INKCS003
00084          10  PC-TSYMSG-00005            PIC  9(05)  VALUE 00005.  INKCS003
00085          10  PC-TSYMSG-00007            PIC  9(05)  VALUE 00007.  INKCS003
00086          10  PC-TSYMSG-00008            PIC  9(05)  VALUE 00008.  INKCS003
00087          10  PC-TSYMSG-00010            PIC  9(05)  VALUE 00010.  INKCS003
00088          10  PC-TSYMSG-00046            PIC  9(05)  VALUE 00046.  INKCS003
00089          10  PC-TSYMSG-00050            PIC  9(05)  VALUE 00050.  INKCS003
00090          10  PC-TSYMSG-00051            PIC  9(05)  VALUE 00051.  INKCS003
00091          10  PC-TSYMSG-00052            PIC  9(05)  VALUE 00052.  INKCS003
00092          10  PC-TSYMSG-00053            PIC  9(05)  VALUE 00053.  INKCS003
00093          10  PC-TSYMSG-00054            PIC  9(05)  VALUE 00054.  INKCS003
00094          10  PC-TSYMSG-00055            PIC  9(05)  VALUE 00055.  INKCS003
00095          10  PC-TSYMSG-00057            PIC  9(05)  VALUE 00057.  INKCS003
00096          10  PC-TSYMSG-00059            PIC  9(05)  VALUE 00059.  INKCS003
00097          10  PC-TSYMSG-00069            PIC  9(05)  VALUE 00069.  INKCS003
00098          10  PC-TSYMSG-00070            PIC  9(05)  VALUE 00070.  INKCS003
00099          10  PC-TSYMSG-00073            PIC  9(05)  VALUE 00073.  INKCS003
00100          10  PC-TSYMSG-00100            PIC  9(05)  VALUE 00100.  INKCS003
00101          10  PC-TSYMSG-00101            PIC  9(05)  VALUE 00101.  INKCS003
00102          10  PC-TSYMSG-00148            PIC  9(05)  VALUE 00148.  INKCS003
00103          10  PC-TSYMSG-00254            PIC  9(05)  VALUE 00254.  INKCS003
00104          10  PC-TSYMSG-00279            PIC  9(05)  VALUE 00279.  INKCS003
00105          10  PC-TSYMSG-00316            PIC  9(05)  VALUE 00316.  INKCS003
J28545         10  PC-TSYMSG-00453            PIC  9(05)  VALUE 00453.  INKCS003
00106          10  PC-TSYMSG-00474            PIC  9(05)  VALUE 00474.  INKCS003
00107          10  PC-TSYMSG-00544            PIC  9(05)  VALUE 00544.  INKCS003
00108          10  PC-TSYMSG-00547            PIC  9(05)  VALUE 00547.  INKCS003
00109          10  PC-TSYMSG-00551            PIC  9(05)  VALUE 00551.  INKCS003
00110          10  PC-TSYMSG-00686            PIC  9(05)  VALUE 00686.  INKCS003
00111          10  PC-TSYMSG-00688            PIC  9(05)  VALUE 00688.  INKCS003
IN2001         10  PC-TSYMSG-02309            PIC  9(05)  VALUE 02309.  INKCS003
J28545         10  PC-TSYMSG-03090            PIC  9(05)  VALUE 03090.  INKCS003
00112          10  PC-TSYMSG-99901            PIC  9(05)  VALUE 99901.  INKCS003
32318          10  PC-TSYMSG-03549            PIC  9(05)  VALUE 03549.  INKCS005
00113 *                                                                 INKCS003
00114  01  PS-PROGRAM-SWITCHES.                                         INKCS003
00115      05  PS-ERROR-SW                    PIC  X(01)  VALUE 'Y'.    INKCS003
00116          88  PS-NO-ERROR                            VALUE 'Y'.    INKCS003
00117          88  PS-ERROR                               VALUE 'N'.    INKCS003
00118          88  PS-WARNING                             VALUE 'W'.    INKCS003
00119      05  PS-COMMAREA-SW                 PIC  X(01)  VALUE 'N'.    INKCS003
00120          88  PS-COMMAREA-VALID                      VALUE 'Y'.    INKCS003
00121          88  PS-COMMAREA-NOT-VALID                  VALUE 'N'.    INKCS003
00122      05  PS-DONE-SW                     PIC  X(01)  VALUE 'N'.    INKCS003
00123          88  PS-DONE                                VALUE 'Y'.    INKCS003
00124          88  PS-NOT-DONE                            VALUE 'N'.    INKCS003
00125      05  PS-UPDATE-SW                   PIC  X(01)  VALUE 'N'.    INKCS003
00126          88  PS-UPDATED                             VALUE 'Y'.    INKCS003
00127          88  PS-NOT-UPDATED                         VALUE 'N'.    INKCS003
00128      05  PS-SOFT-ABORT-SW               PIC  X(01)  VALUE 'N'.    INKCS003
00129          88  PS-SOFT-ABORT                          VALUE 'Y'.    INKCS003
00130 *                                                                 INKCS003
00131  01  PV-PROGRAM-VARIABLES.                                        INKCS003
W21732     05  PV-DUMMY-SKU                   PIC  X(08).               INKCS003
00132      05  PV-SUB                         PIC S9(04)  VALUE +0      INKCS003
00133                                                     COMP SYNC.    INKCS003
00134      05  PV-REMAINDER                   PIC S9(04)  VALUE +0      INKCS003
00135                                                     COMP SYNC.    INKCS003
00136      05  PV-READ-COUNT                  PIC S9(09)  VALUE +0      INKCS003
00137                                                     COMP-3.       INKCS003
00138      05  PV-ITEMS-PROCESSED             PIC S9(09)  VALUE +0      INKCS003
00139                                                     COMP-3.       INKCS003
00140      05  PV-HOLD-SELECTED-ITEMS         PIC S9(09)  VALUE +0      INKCS003
00141                                                     COMP-3.       INKCS003
00142      05  PV-ITEM-IN-USE                 PIC S9(09)  VALUE +0      INKCS003
00143                                                     COMP-3.       INKCS003
00144      05  PV-TOP-ITEM                    PIC S9(09)  VALUE +0      INKCS003
00145                                                     COMP-3.       INKCS003
00146      05  PV-BOTTOM-ITEM                 PIC S9(09)  VALUE +0      INKCS003
00147                                                     COMP-3.       INKCS003
00148      05  PV-TEMP-BLOCK-NUMBER           PIC S9(09)  VALUE +0      INKCS003
00149                                                     COMP-3.       INKCS003
00150      05  PV-BLOCK-NUMBER                PIC S9(04)  VALUE +0      INKCS003
00151                                                     COMP SYNC.    INKCS003
00152      05  PV-CICS-RESP                   PIC S9(04)  VALUE +0      INKCS003
00153                                                     COMP SYNC.    INKCS003
00154      05  PV-ITEM                        PIC S9(04)  VALUE +0      INKCS003
00155                                                     COMP SYNC.    INKCS003
00156      05  PV-SEL-QUEUE-ITEM              PIC S9(04)  VALUE +0      INKCS003
00157                                                     COMP SYNC.    INKCS003
00158      05  PV-SAVE-ITEM                   PIC S9(04)  VALUE +0      INKCS003
00159                                                     COMP SYNC.    INKCS003
00160      05  PV-HIGHEST-BLOCK-WRITTEN       PIC S9(04)  VALUE +0      INKCS003
00161                                                     COMP SYNC.    INKCS003
00162      05  PV-LIST-ITEM-NUMBER            PIC S9(04)  VALUE +0      INKCS003
00163                                                     COMP SYNC.    INKCS003
00164      05  PV-FIRST-BLOCK-IN-ARRAY        PIC S9(04)  VALUE +0      INKCS003
00165                                                     COMP SYNC.    INKCS003
00166      05  PV-NEXT-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS003
00167                                                     COMP SYNC.    INKCS003
00168      05  PV-PREV-BLOCK-IN-ARRAY         PIC S9(04)  VALUE +0      INKCS003
00169                                                     COMP SYNC.    INKCS003
00170      05  PV-FIRST-BLOCK-IN-RANGE        PIC S9(04)  VALUE +0      INKCS003
00171                                                     COMP SYNC.    INKCS003
00172      05  PV-LAST-BLOCK-IN-RANGE         PIC S9(04)  VALUE +0      INKCS003
00173                                                     COMP SYNC.    INKCS003
00174      05  PV-START-OF-SELECTED-RANGE     PIC S9(04)  VALUE +0      INKCS003
00175                                                     COMP SYNC.    INKCS003
00176      05  PV-END-OF-SELECTED-RANGE       PIC S9(04)  VALUE +0      INKCS003
00177                                                     COMP SYNC.    INKCS003
00178      05  PV-ITM-CORRECTED               PIC S9(05)  VALUE ZERO    INKCS003
00179                                                     COMP-3.       INKCS003
00180      05  PV-REC-CORRECTED               PIC S9(05)  VALUE ZERO    INKCS003
00181                                                     COMP-3.       INKCS003
00182      05  PV-WORK-UPC              PIC 9(13).                      INKCS003
00183      05  PV-WORK-UPC-X  REDEFINES  PV-WORK-UPC.                   INKCS003
00184          10  FILLER               PIC X(4).                       INKCS003
00185              88  PV-KOHLS-UPC                 VALUE '0400'.       INKCS003
W21732         10  PV-UPC-END           PIC X(8).                       INKCS003
00190          10  PV-UPC-CHK-DIG       PIC 9.                          INKCS003
00191      05  PV-WORK-UPC-X2.                                          INKCS003
00192          10  FILLER               PIC X(4)    VALUE '0400'.       INKCS003
W21732         10  PV-UPC-SKU2          PIC X(8).                       INKCS003
00196          10  PV-UPC-CHK-DIG2      PIC 9       VALUE ZERO.         INKCS003
00197      05  PV-WORK-UPC2  REDEFINES  PV-WORK-UPC-X2                  INKCS003
00198                                   PIC 9(13).                      INKCS003
00199      05  PV-MAJ-CL-NBR.                                           INKCS003
00200          10  PV-MAJ-CL-NBR1       PIC X.                          INKCS003
00201          10  PV-MAJ-CL-NBR2       PIC X.                          INKCS003
00202      05  PV-SUB-CL-NBR.                                           INKCS003
00203          10  PV-SUB-CL-NBR1       PIC X      VALUE '0'.           INKCS003
00204          10  PV-SUB-CL-NBR2-3     PIC X(02).                      INKCS003
W21732     05  PV-EIGHT                 PIC 9(08).                      INKCS003
W21732     05  FILLER  REDEFINES  PV-EIGHT.                             INKCS003
W21732         10  PV-EIGHT-X           PIC X(08).                      INKCS003
W26600     05  PV-CHAR-STORE            PIC X(04)    VALUE SPACES.              
JS1099     05  PV-UNT-RTL-AMT           PIC S9(7)V9(2) COMP-3.                  
JS1099     05  PV-GROUP-UNIT-RTL        PIC S9(7)V9(2) COMP-3.                  
W03072     05  PV-UPCPLS-STR-NBR        PIC S9(4) COMP VALUE +0.                
           05  PV-NUMERIC-STORE         PIC S9(9)    VALUE +0.                  
JS1099     05  PV-PLND-INV-TMST.                                                
JS1099         10  PV-DB2-PLND-INV-DTE     PIC X(10) VALUE SPACES.              
JS1099         10  FILLER                  PIC X(16)                            
JS1099             VALUE '-23.59.59.999999'.                                    
JS1099     05  FILLER REDEFINES PV-PLND-INV-TMST.                               
JS1099         10  PV-PLND-INV-TMST-REDEF  PIC X(26).                           
00181                                                                   INKCS003
IN2001     05  PV-STR000-ROW.                                                   
IN2001         10  PV-STR000-UPC-STAT-CDE  PIC X(02)    VALUE SPACES.           
IN2001         10  PV-STR000-UNT-RTL-AMT   PIC S9(7)V99 VALUE +0 COMP-3.        
IN2001         10  PV-STR000-MEITGP-NBR    PIC S9(9)    VALUE +0 COMP-3.        
W21732     05  PV-SKU                      PIC X(08).                           
W21372     05  FILLER REDEFINES PV-SKU.                                         
W21732         10  PV-SKU-NBR              PIC 9(08).                           
W26600     05  PV-STORE-NBR-X              PIC X(04)    VALUE ZEROES.           
W26600     05  PV-STORE-NBR-9 REDEFINES PV-STORE-NBR-X                          
W26600                                     PIC 9(04).                           
J28545     05  PV-INV-ID                   PIC S9(9)    COMP.                   
                                                                                
00210                                                                   INKCS003
00211 *----------------------------------------------------------------*INKCS003
00212 *   THIS IS THE LAYOUT FOR THE LIST ITEMS TEMP STORAGE QUEUE.    *INKCS003
00213 *   THIS MUST BE THE EXACT SAME FORMAT A BLOCK IN THE            *INKCS003
00214 *   APPLICATION-SPECIFIC COMMAREA.                               *INKCS003
00215 *----------------------------------------------------------------*INKCS003
00216  01  LQW-LIST-QUEUE-WORK-AREA.                                    INKCS003
00217      05  LQW-BLOCK-MODIFIED             PIC  X(01)    VALUE SPACE.INKCS003
00218      05  LQW-ITEM-ENTRIES               OCCURS 13 TIMES           INKCS003
00219                                         INDEXED BY LQW-IDX.       INKCS003
00220          10  LQW-LIST-ITEM-NUMBER       PIC S9(09)    COMP-3.     INKCS003
W21732         10  LQW-CSKU                   PIC  X(08).               INKCS003
00225          10  LQW-CUPC-X.                                          INKCS003
00226             15  LQW-CUPC                PIC  9(15).               INKCS003
00227                                                                   INKCS003
00228          10  LQW-INV-LINE-NBR           PIC S9(08) COMP.          INKCS003
00229          10  LQW-SCAN-LINE-NBR          PIC S9(08) COMP.          INKCS003
W21732         10  LQW-ISKU                   PIC  X(08).               INKCS003
00233          10  LQW-IUPC                   PIC  9(15) COMP-3.        INKCS003
00234          10  LQW-INV-QTY                PIC  9(07) COMP-3.        INKCS003
00235          10  LQW-INV-PR-AMT-X.                                    INKCS003
00236              15  LQW-INV-PR-AMT         PIC  9(07)V99.            INKCS003
00237          10  LQW-ORIG-INV-PR-AMT        PIC  9(07)V99 COMP-3.     INKCS003
00238          10  LQW-RENUMBERED-SKU.                                  INKCS003
W21732             15  LQW-RSKU               PIC  X(08).               INKCS003
00242          10  LQW-INVALID-ITM-IND        PIC  X(01).               INKCS003
00243          10  LQW-INVALID-REC-IND        PIC  X(01).               INKCS003
00244          10  LQW-CHKDIG                 PIC  9(01).               INKCS003
00245                                                                   INKCS003
00246          10  LQW-PR-AMT-PROT-SW         PIC  X(01).               INKCS003
00247              88  LQW-PROTECT-PR-AMT          VALUE 'P'.           INKCS003
00248              88  LQW-ENTER-PR-AMT            VALUE 'A'.           INKCS003
00249              88  LQW-PR-AMT-ERROR            VALUE 'A'.           INKCS003
00250          10  LQW-ENTRY-PROT-SW          PIC X(01).                INKCS003
00251              88  LQW-PROTECT-ENTRY           VALUE 'P'.           INKCS003
00252          10  LQW-SKU-SW                 PIC X(01).                INKCS003
00253              88  LQW-SKU-ENTERED             VALUE 'Y'.           INKCS003
00254              88  LQW-SKU-NOT-ENTERED         VALUE 'N'.           INKCS003
00255              88  LQW-SKU-ERROR               VALUE 'E'.           INKCS003
00256          10  LQW-UPC-SW                 PIC X(01).                INKCS003
00257              88  LQW-UPC-ENTERED             VALUE 'Y'.           INKCS003
00258              88  LQW-UPC-NOT-ENTERED         VALUE 'N'.           INKCS003
00259              88  LQW-UPC-NOTFND              VALUE 'F'.           INKCS003
00260              88  LQW-UPC-CHKDIG              VALUE 'C'.           INKCS003
00261          10  LQW-RECORD-UPDATED-SW      PIC X(01).                INKCS003
00262              88  LQW-UPDATED                 VALUE 'Y'.           INKCS003
00263              88  LQW-NOT-UPDATED             VALUE 'N'.           INKCS003
00264              88  LQW-UPDATE-DONE             VALUE 'D'.           INKCS003
00265          10  LQW-SKU-ERROR-ENTER-SW     PIC X(01).                INKCS003
00266              88  LQW-SKU-WAS-ENTERED         VALUE 'Y'.           INKCS003
00267          10  LQW-UPC-ERROR-ENTER-SW     PIC X(01).                INKCS003
00268              88  LQW-UPC-WAS-ENTERED         VALUE 'Y'.           INKCS003
00269          10  LQW-PR-AMT-ENTERED-SW      PIC X(01).                INKCS003
00270              88  LQW-PR-AMT-ENTERED          VALUE 'Y'.           INKCS003
00271              88  LQW-PR-AMT-NOT-ENTERED      VALUE 'N'.           INKCS003
00272                                                                   INKCS003
00273      EJECT                                                        INKCS003
00274 *---------------------------------------------------------------* INKCS003
00275 *  RECORD LAYOUT FOR THE AREA SELECTION TEMP STORAGE QUEUE.     * INKCS003
00276 *---------------------------------------------------------------* INKCS003
00277                                                                   INKCS003
00278      COPY INWS002.                                                INKCS003
00279                                                                   INKCS003
00280      EJECT                                                        INKCS003
00281 *---------------------------------------------------------------* INKCS003
00282 *    MAP LAYOUT                                                 * INKCS003
00283 *---------------------------------------------------------------* INKCS003
00284                                                                   INKCS003
W26600     COPY INKM003.                                                INKCS003
00286 *                                                                 INKCS003
00287  01  MR-OCCURS-LAYOUT                   REDEFINES  IN003AO.       INKCS003
W26600     05  FILLER                         PIC X(92).                INKCS003
00289      05  MR-SCROLL-AREA                 OCCURS 13 TIMES.          INKCS003
00290                                                                   INKCS003
00291          10  MR-LINEL                   PIC S9(04) COMP.          INKCS003
00292          10  MR-LINEA                   PIC  X(01).               INKCS003
00293          10  MR-LINEC                   PIC  X(01).               INKCS003
00294          10  MR-LINEH                   PIC  X(01).               INKCS003
00295          10  MR-LINE-X.                                           INKCS003
00296              15  MR-LINE                PIC ZZZZZZ9.              INKCS003
00297                                                                   INKCS003
00298          10  MR-IUPCL                   PIC S9(04) COMP.          INKCS003
00299          10  MR-IUPCA                   PIC  X(01).               INKCS003
00300          10  MR-IUPCC                   PIC  X(01).               INKCS003
00301          10  MR-IUPCH                   PIC  X(01).               INKCS003
00302          10  MR-IUPC-X.                                           INKCS003
00303              15  MR-IUPC                PIC Z(15).                INKCS003
00304                                                                   INKCS003
W21732         10  MR-ISKUL                   PIC S9(04) COMP.          INKCS003
W21732         10  MR-ISKUA                   PIC  X(01).               INKCS003
W21732         10  MR-ISKUC                   PIC  X(01).               INKCS003
W21732         10  MR-ISKUH                   PIC  X(01).               INKCS003
W21732         10  MR-ISKU                    PIC  X(08).               INKCS003
00322                                                                   INKCS003
00323          10  MR-INV-QTYL                PIC S9(04) COMP.          INKCS003
00324          10  MR-INV-QTYA                PIC  X(01).               INKCS003
00325          10  MR-INV-QTYC                PIC  X(01).               INKCS003
00326          10  MR-INV-QTYH                PIC  X(01).               INKCS003
00327          10  MR-INV-QTY-X.                                        INKCS003
00328              15  MR-INV-QTY             PIC  Z(4)9.               INKCS003
00329                                                                   INKCS003
00330          10  MR-INV-PR-AMTL             PIC S9(04) COMP.          INKCS003
00331          10  MR-INV-PR-AMTA             PIC  X(01).               INKCS003
00332          10  MR-INV-PR-AMTC             PIC  X(01).               INKCS003
00333          10  MR-INV-PR-AMTH             PIC  X(01).               INKCS003
00334          10  MR-INV-PR-AMT-X.                                     INKCS003
SR3403             15  MR-INV-PR-AMT          PIC  Z(5).99.             INKCS003
00336                                                                   INKCS003
W21732         10  MR-CSKUL                   PIC S9(04) COMP.          INKCS003
W21732         10  MR-CSKUA                   PIC  X(01).               INKCS003
W21732         10  MR-CSKUC                   PIC  X(01).               INKCS003
W21732         10  MR-CSKUH                   PIC  X(01).               INKCS003
W21732         10  MR-CSKU                    PIC  X(08).               INKCS003
00354                                                                   INKCS003
00355          10  MR-CUPCL                   PIC S9(04) COMP.          INKCS003
00356          10  MR-CUPCA                   PIC  X(01).               INKCS003
00357          10  MR-CUPCC                   PIC  X(01).               INKCS003
00358          10  MR-CUPCH                   PIC  X(01).               INKCS003
00359          10  MR-CUPC-X.                                           INKCS003
00360              15  MR-CUPC                PIC Z(15).                INKCS003
00361                                                                   INKCS003
00362 /                                                                 INKCS003
00363 *---------------------------------------------------------------* INKCS003
00364 *    ATTRIBUTE SETTINGS COPYBOOK.                               * INKCS003
00365 *---------------------------------------------------------------* INKCS003
00366                                                                   INKCS003
00367      COPY DPWS015.                                                INKCS003
00368                                                                   INKCS003
00369 *---------------------------------------------------------------* INKCS003
00370 *    FUNCTION KEYS COPYBOOK.                                    * INKCS003
00371 *---------------------------------------------------------------* INKCS003
00372                                                                   INKCS003
00373      COPY DPWS016.                                                INKCS003
00374                                                                   INKCS003
00375 *---------------------------------------------------------------* INKCS003
00376 *    CHECK DIGIT SUBROUTINE COPYBOOK                            * INKCS003
00377 *---------------------------------------------------------------* INKCS003
00378                                                                   INKCS003
00379      COPY DPWS040I.                                               INKCS003
00380                                                                   INKCS003
00381 *---------------------------------------------------------------* INKCS003
00382 *    NUMERIC EDIT LAYOUT.                                       * INKCS003
00383 *---------------------------------------------------------------* INKCS003
00384                                                                   INKCS003
00385      COPY DPWS010I.                                               INKCS003
00386 /                                                                 INKCS003
00387 *---------------------------------------------------------------* INKCS003
00388 *    PARAMETERS FOR CALLING CICS ARCHITECTURE API (DPKCS030).   * INKCS003
00389 *---------------------------------------------------------------* INKCS003
00390                                                                   INKCS003
00391      COPY DPWS030.                                                INKCS003
1022RM     COPY DPWS930.                                                INKCS003
00392 /                                                                 INKCS003
00393 *---------------------------------------------------------------* INKCS003
00394 *    STANDARD COMMAREA.                                         * INKCS003
00395 *---------------------------------------------------------------* INKCS003
00396                                                                   INKCS003
00397      COPY DPWS020.                                                INKCS003
00398      05  INKCS003-VAR-COMM REDEFINES DP020-VARIABLE-COMMAREA.     INKCS003
00399                                                                   INKCS003
00400          10  INTER-APPL-COMM-AREA       PIC X(200).               INKCS003
W26600     COPY INWS001.                                                INKCS003
00402 *---------------------------------------------------------------* INKCS003
00403 *  THIS IS THE APPLICATION-SPECIFIC COMMAREA.                   * INKCS003
00404 *---------------------------------------------------------------* INKCS003
00405                                                                   INKCS003
00406          10  ASC-SPECIFIC-COMMAREA.                               INKCS003
00407              15  ASC-LIST-KEYS.                                   INKCS003
W26600                 20  ASC-KEY-STORE-NBR               PIC X(04).   INKCS003
00409                  20  ASC-KEY-AREA-NBR                PIC S9(08)   INKCS003
00410                                                      COMP.        INKCS003
00411                  20  ASC-KEY-LINE-NBR                PIC S9(08)   INKCS003
00412                                                      COMP.        INKCS003
00413              15  ASC-BLOCK-ENTRIES OCCURS 2 TIMES.                INKCS003
00414                  20  ASC-BLOCK-MODIFIED-SW           PIC  X(01).  INKCS003
00415                      88  ASC-BLOCK-NOT-MODIFIED      VALUE 'N'.   INKCS003
00416                      88  ASC-BLOCK-MODIFIED          VALUE 'Y'.   INKCS003
00417                                                                   INKCS003
00418                  20  ASC-ITEM-ARRAY.                              INKCS003
00419                      25  ASC-ITEM-ENTRIES OCCURS 13 TIMES.        INKCS003
00420                          30  ASC-LIST-ITEM-NUMBER                 INKCS003
00421                                                      PIC S9(09)   INKCS003
00422                                                      COMP-3.      INKCS003
00423                                                                   INKCS003
00424                          30  ASC-CSKU-NBR.                        INKCS003
W21732                             35  ASC-CSKU            PIC  X(08).  INKCS003
00430                          30  ASC-CUPC-X.                          INKCS003
00431                              35  ASC-CUPC            PIC  9(15).  INKCS003
00432                          30  ASC-INV-LINE-NBR        PIC S9(08)   INKCS003
00433                                                           COMP.   INKCS003
00434                          30  ASC-SCAN-LINE-NBR       PIC S9(08)   INKCS003
00435                                                           COMP.   INKCS003
W21732                         30  ASC-ISKU                PIC  X(08).  INKCS003
00439                          30  ASC-IUPC                PIC  9(15)   INKCS003
00440                                                           COMP-3. INKCS003
00441                          30  ASC-INV-QTY             PIC  9(07)   INKCS003
00442                                                           COMP-3. INKCS003
00443                          30  ASC-INV-PR-AMT-X.                    INKCS003
00444                              35  ASC-INV-PR-AMT     PIC  9(07)V99.INKCS003
00445                          30  ASC-ORIG-INV-PR-AMT    PIC  9(07)V99 INKCS003
00446                                                           COMP-3. INKCS003
00447                          30  ASC-RENUMBERED-SKU.                  INKCS003
W21732                             35  ASC-RSKU            PIC X(08).   INKCS003
00451                          30  ASC-INVALID-ITM-IND     PIC X(01).   INKCS003
00452                          30  ASC-INVALID-REC-IND     PIC X(01).   INKCS003
00453                          30  ASC-CHKDIG              PIC 9(01).   INKCS003
00454                                                                   INKCS003
00455                          30  ASC-PR-AMT-PROT-SW      PIC X(01).   INKCS003
00456                              88  ASC-PROTECT-PR-AMT  VALUE 'P'.   INKCS003
00457                              88  ASC-ENTER-PR-AMT    VALUE 'A'.   INKCS003
00458                              88  ASC-PR-AMT-ERROR    VALUE 'E'.   INKCS003
00459                              88  ASC-PR-AMT-E500     VALUE '5'.   INKCS003
00460                              88  ASC-PR-AMT-E20      VALUE '2'.   INKCS003
00461                              88  ASC-PR-AMT-ZERO     VALUE 'Z'.   INKCS003
00462                              88  ASC-PR-AMT-OK       VALUE 'O'.   INKCS003
00463                          30  ASC-ENTRY-PROT-SW       PIC X(01).   INKCS003
00464                              88  ASC-PROTECT-ENTRY   VALUE 'P'.   INKCS003
00465                          30  ASC-SKU-SW              PIC X(01).   INKCS003
00466                              88  ASC-SKU-ENTERED     VALUE 'Y'.   INKCS003
00467                              88  ASC-SKU-NOT-ENTERED VALUE 'N'.   INKCS003
32318                              88  ASC-SKU-PLU-ERR     VALUE 'P'.   INKCS003
00468                              88  ASC-SKU-ERROR       VALUE 'E'.   INKCS003
00469                          30  ASC-UPC-SW              PIC X(01).   INKCS003
00470                              88  ASC-UPC-ENTERED     VALUE 'Y'.   INKCS003
00471                              88  ASC-UPC-NOT-ENTERED VALUE 'N'.   INKCS003
00472                              88  ASC-UPC-NOTFND      VALUE 'F'.   INKCS003
00473                              88  ASC-UPC-NOTFND2     VALUE 'M'.   INKCS003
00474                              88  ASC-UPC-CHKDIG      VALUE 'C'.   INKCS003
32318                              88  ASC-UPC-PLU-ERR     VALUE 'P'.   INKCS003
00475                              88  ASC-UPC-ERROR       VALUE 'E'.   INKCS003
00476                          30  ASC-RECORD-UPDATED-SW   PIC X(01).   INKCS003
00477                              88  ASC-UPDATED         VALUE 'Y'.   INKCS003
00478                              88  ASC-NOT-UPDATED     VALUE 'N'.   INKCS003
00479                          30  ASC-SKU-ENTER-ERROR-SW  PIC X(01).   INKCS003
00480                              88  ASC-SKU-WAS-ENTERED VALUE 'Y'.   INKCS003
00481                          30  ASC-UPC-ENTER-ERROR-SW  PIC X(01).   INKCS003
00482                              88  ASC-UPC-WAS-ENTERED VALUE 'Y'.   INKCS003
00483                          30  ASC-PR-AMT-ENTERED-SW   PIC X(01).   INKCS003
00484                              88  ASC-PR-AMT-ENTERED  VALUE 'Y'.   INKCS003
00485                              88  ASC-PR-AMT-NOT-ENTERED VALUE 'N'.INKCS003
00486                                                                   INKCS003
00487              15  ASC-BLK-SUB                         PIC S9(04)   INKCS003
00488                                                           COMP    INKCS003
00489                                                           SYNC.   INKCS003
00490              15  ASC-ITEM-SUB                        PIC S9(04)   INKCS003
00491                                                           COMP    INKCS003
00492                                                           SYNC.   INKCS003
00493              15  ASC-NUMBER-OF-ITEMS-READ            PIC S9(09)   INKCS003
00494                                                           COMP-3. INKCS003
00495              15  ASC-NUMBER-OF-SELECTED-ITEMS        PIC S9(09)   INKCS003
00496                                                           COMP-3. INKCS003
00497              15  ASC-ITEMS-DISPLAYED                 PIC S9(03)   INKCS003
00498                                                           COMP-3. INKCS003
00499              15  ASC-ITEM-AT-TOP-OF-PANEL            PIC S9(09)   INKCS003
00500                                                           COMP-3. INKCS003
00501              15  ASC-ITEM-AT-BOT-OF-PANEL            PIC S9(09)   INKCS003
00502                                                           COMP-3. INKCS003
00503              15  ASC-HIGHEST-BLOCK-WRITTEN           PIC S9(04)   INKCS003
00504                                                           COMP    INKCS003
00505                                                           SYNC.   INKCS003
00506              15  ASC-START-OF-SELECTED-RANGE         PIC S9(09)   INKCS003
00507                                                           COMP-3. INKCS003
00508              15  ASC-END-OF-SELECTED-RANGE           PIC S9(09)   INKCS003
00509                                                           COMP-3. INKCS003
00510              15  ASC-ITEMS-LEFT-INDICATOR            PIC  X(01).  INKCS003
00511                  88  ASC-ITEMS-LEFT-ON-DB            VALUE 'Y'.   INKCS003
00512                  88  ASC-NO-ITEMS-LEFT-ON-DB         VALUE 'N'.   INKCS003
00513              15  ASC-TEMP-QUEUE-UPDATE-SW            PIC  X(01).  INKCS003
00514                  88  ASC-UPDATE-TSQ                  VALUE 'Y'.   INKCS003
00515                  88  ASC-NO-TSQ-UPDATE               VALUE ' '.   INKCS003
00516              15  ASC-LIST-QUEUE-NAME.                             INKCS003
00517                  20  ASC-LIST-TERM-ID                PIC  X(04).  INKCS003
00518                  20  ASC-LIST-TASK-NUMBER            PIC S9(05)   INKCS003
00519                                                           COMP-3. INKCS003
00520                  20  ASC-LIST-SEQ                    PIC  9(01).  INKCS003
00521              15  ASC-SEL-QUEUE-NAME.                              INKCS003
00522                  20  ASC-SEL-TERM-ID                 PIC  X(04).  INKCS003
00523                  20  ASC-SEL-TASK-NUMBER             PIC S9(05)   INKCS003
00524                                                           COMP-3. INKCS003
00525                  20  ASC-SEL-SEQ                     PIC  9(01).  INKCS003
00526                                                                   INKCS003
00527              15  ASC-INT-APPL-FORMAT-IND             PIC  X(01).  INKCS003
00528              15  ASC-USE-SELECTION-QUEUE-IND                      INKCS003
00529                                            PIC  X(01).            INKCS003
00530                  88  ASC-USE-SELECTION-QUEUE           VALUE 'Y'. INKCS003
00531              15  ASC-NUMBER-OF-TS-ITEMS    PIC S9(05)  COMP-3.    INKCS003
00532              15  ASC-SEL-SUB               PIC S9(04)  COMP.      INKCS003
00533              15  ASC-SEL-ITEM              PIC S9(04)  COMP.      INKCS003
00534                                                                   INKCS003
00535              15  ASC-CURSOR-SW                       PIC  X(01).  INKCS003
00536                  88  ASC-NO-CURSOR                   VALUE 'Z'.   INKCS003
00537                  88  ASC-SYSTEM-ID-CURSOR            VALUE 'S'.   INKCS003
00538                  88  ASC-MESSAGE-NUMBER-CURSOR       VALUE 'N'.   INKCS003
00539                  88  ASC-MESSAGE-TEXT-CURSOR         VALUE 'T'.   INKCS003
00540              15  ASC-START-ITEM-REQUEST.                          INKCS003
00541                  20  ASC-START-ITEM-REQUEST-N        PIC  9(05).  INKCS003
00542              15  ASC-NBR-ITEMS-CORRECTED             PIC S9(05)   INKCS003
00543                                                          COMP-3.  INKCS003
00544              15  ASC-HOLD-INTER-APPL                 PIC X(200).  INKCS003
W26600*****        15  FILLER                              PIC X(264).  INKCS003
00545 /                                                                 INKCS003
00546 *---------------------------------------------------------------* INKCS003
00547 *    ABEND PROCESSING COPYBOOK.                                 * INKCS003
00548 *---------------------------------------------------------------* INKCS003
00550      COPY DPWS013.                                                INKCS003
00552 /                                                                 INKCS003
00553 *---------------------------------------------------------------* INKCS003
00554 *    DB2 AREA FOR THE INVALID DETAIL TABLE                      * INKCS003
00555 *---------------------------------------------------------------* INKCS003
00556      EXEC SQL                                                     INKCS003
00557           INCLUDE TINVDTL                                         INKCS003
00558      END-EXEC.                                                    INKCS003
00559 *---------------------------------------------------------------* INKCS003
00560 *    DB2 AREA FOR THE ADJUSTMENT DETAIL TABLE                   * INKCS003
00561 *---------------------------------------------------------------* INKCS003
00562      EXEC SQL                                                     INKCS003
W26600          INCLUDE TADJDTL                                         INKCS003
00564      END-EXEC.                                                    INKCS003
00565 *---------------------------------------------------------------* INKCS003
00566 *    DB2 AREA FOR THE PHYSICAL LOCATION TABLE                   * INKCS003
00567 *---------------------------------------------------------------* INKCS003
00568      EXEC SQL                                                     INKCS003
00569           INCLUDE TINVLOC                                         INKCS003
00570      END-EXEC.                                                    INKCS003
JS1099*****************************************************************         
JS1099*  DCLGEN FOR THE TINVPAR DB2 TABLE.                                      
JS1099*****************************************************************         
JS1099                                                                          
JS1099     EXEC SQL                                                             
JS1099         INCLUDE TINVPAR                                                  
JS1099     END-EXEC.                                                            
W28545                                                                          
W28545*****************************************************************         
W28545*  DCLGEN FOR THE TINCNTL DB2 TABLE.                                      
W28545*****************************************************************         
W28545                                                                          
W28545     EXEC SQL                                                             
W28545         INCLUDE TINCNTL                                                  
W28545     END-EXEC.                                                            
W28545                                                                          
00595 *---------------------------------------------------------------* INKCS003
00596 *    DB2 AREA FOR COMMUNICATIONS                                * INKCS003
00597 *---------------------------------------------------------------* INKCS003
00598      EXEC SQL                                                     INKCS003
00599           INCLUDE SQLCA                                           INKCS003
00600      END-EXEC.                                                    INKCS003
00601                                                                   INKCS003
00602 *---------------------------------------------------------------* INKCS003
00603 *    TINVDTL CURSOR DECLARE.                                    * INKCS003
00604 *---------------------------------------------------------------* INKCS003
00605                                                                   INKCS003
00606      EXEC SQL                                                     INKCS003
00607           DECLARE LINE-CSR CURSOR                                 INKCS003
00608           FOR SELECT                                              INKCS003
00609                 REC_UPC_NBR                                       INKCS003
W21732              , REC_SKU_NBR                                       INKCS003
00613               , REC_QTY                                           INKCS003
00614               , REC_PR_AMT                                        INKCS003
00615               , INV_LINE_NBR                                      INKCS003
00616               , SCAN_LINE_NBR                                     INKCS003
00617               , INVALID_ITM_IND                                   INKCS003
00618               , INVALID_REC_IND                                   INKCS003
00619           FROM                                                    INKCS003
00620                 TINVDTL                                           INKCS003
00621           WHERE                                                   INKCS003
W26600                LOC_NBR          =  :INVDTL-LOC-NBR               INKCS003
00623             AND                                                   INKCS003
W21732                AREA_NBR         =  :INVDTL-AREA-NBR              INKCS003
00625             AND                                                   INKCS003
W21732                SCAN_LINE_NBR    >  :INVDTL-SCAN-LINE-NBR         INKCS003
00627             AND                                                   INKCS003
00628                 INVALID_ITM_IND  =  :INVDTL-INVALID-ITM-IND       INKCS003
00629           ORDER BY                                                INKCS003
00630                 SCAN_LINE_NBR                                     INKCS003
00631      END-EXEC.                                                    INKCS003
00632                                                                   INKCS003
J28545     COPY INWS400.                                                        
00633 /                                                                 INKCS003
00634  LINKAGE SECTION.                                                 INKCS003
00635                                                                   INKCS003
00636  01  DFHCOMMAREA.                                                 INKCS003
00637      05  FILLER                         OCCURS  1 TO 4072 TIMES   INKCS003
00638                                         DEPENDING ON EIBCALEN.    INKCS003
00639          10  FILLER                     PIC X.                    INKCS003
00640                                                                   INKCS003
00641  PROCEDURE DIVISION.                                              INKCS003
00642 **--------------------------------------------------------------**INKCS003
00643 **  THIS MODULE IS THE HIGHEST PROCESSING LEVEL IN THE PROGRAM. **INKCS003
00644 **  FIRST, CALL THE CICS ARCHITECTURE API, PERFORM THE PROGRAM  **INKCS003
00645 **  PROCESSING AND THEN CALL THE CICS ARCHITECTURE API TO EXIT. **INKCS003
00646 **--------------------------------------------------------------**INKCS003
00647                                                                   INKCS003
00648  0000-MAIN-MODULE.                                                INKCS003
00649                                                                   INKCS003
00650      INITIALIZE DP030-CICS-API-FIELDS.                            INKCS003
00651      MOVE +1                       TO DP030-NUMBER-OF-MAPS.       INKCS003
00652      MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          INKCS003
00653      MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         INKCS003
00654      SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       INKCS003
00656                                                                   INKCS003
00657      MOVE LENGTH OF IN003AI        TO DP030-MAP-LENGTH (1).       INKCS003
00658      MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     INKCS003
00659      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS003
00660                                                                   INKCS003
00661      PERFORM 1000-CONTROL-PROCESSING                              INKCS003
00662                                                                   INKCS003
00663      MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     INKCS003
00664      PERFORM 0001-CALL-CICS-ARCH-API.                             INKCS003
00665                                                                   INKCS003
00666 *----------------------------------------------------------------*INKCS003
00667 *   THIS PARAGRAPH CALLS THE CICS ARCHITECTURE API SUBROUTINE    *INKCS003
00668 *   AND WILL ABEND IF ANY ERRORS OCCURRED IN THAT CALL.          *INKCS003
00669 *----------------------------------------------------------------*INKCS003
00670                                                                   INKCS003
00671  0001-CALL-CICS-ARCH-API.                                         INKCS003
00672                                                                   INKCS003
1022RM*    CALL 'DPKCS030' USING DFHEIBLK                               INKCS003
1022RM     CALL DP930-CICS-ARCH-API                                     INKCS002
1022RM                     USING DFHEIBLK                                       
00674                            DFHCOMMAREA                            INKCS003
00675                            DP030-CICS-API-FIELDS                  INKCS003
00676                            DP020-STANDARD-COMMAREA                INKCS003
00677                            IN003AI.                               INKCS003
00678                                                                   INKCS003
00679      IF  DP030-RC-CALL-SUCCESSFUL                                 INKCS003
00680          CONTINUE                                                 INKCS003
00681      ELSE                                                         INKCS003
00682          SET DP013-NO-ROLLBACK                                    INKCS003
00683              DP013-XCTL-DISPLAY-RESTART                           INKCS003
00684              DP013-CICS-ABEND      TO TRUE                        INKCS003
00685          MOVE '0001-CALL-CICS-ARCH-API'                           INKCS003
00686                                    TO DP013-PARAGRAPH             INKCS003
1022RM*        MOVE 'CALL TO DPKCS030 NOT SUCCESSFUL, RETURN-CODE ON NEXINKCS003
1022RM*             'T LINE'             TO DP013-MESSAGE-TEXT (2)      INKCS003
1022RM         MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
1022RM-             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
00689          MOVE DP030-RETURN-CODE    TO DP013-MESSAGE-TEXT (3)      INKCS003
00690          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
00691      END-IF.                                                      INKCS003
00692                                                                   INKCS003
00693 *----------------------------------------------------------------*INKCS003
00694 * PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT      *INKCS003
00695 * COURSE OF ACTION IS FOR THIS TRANSACTION.                      *INKCS003
00696 *                                                                *INKCS003
00697 * NOTE:  PV-TOP-ITEM IS THE ITEM THAT IS REQUESTED TO BE AT THE  *INKCS003
00698 *  TOP OF THE PANEL.  MOVING ASC-ITEM-AT-TOP-OF-PANEL TO THIS    *INKCS003
00699 *  MEANS THAT POSITION WITHIN THE LIST WILL NOT CHANGE -- THIS   *INKCS003
00700 *  IS THE DEFAULT WHICH WILL BE OVERRIDDEN BY SCROLLING ACTIONS. *INKCS003
00701 *----------------------------------------------------------------*INKCS003
00702                                                                   INKCS003
00703  1000-CONTROL-PROCESSING.                                         INKCS003
00704                                                                   INKCS003
00705      EVALUATE TRUE                                                INKCS003
00706          WHEN DP020-NEXT-ACT-INITIAL                              INKCS003
00707               PERFORM 1100-FIND-OBJECT-KEYS                       INKCS003
00708               IF  PS-ERROR                                        INKCS003
00709                   CONTINUE                                        INKCS003
00710               ELSE                                                INKCS003
00711                   MOVE INTER-APPL-COMM-AREA                       INKCS003
00712                                    TO  ASC-HOLD-INTER-APPL        INKCS003
00713                   MOVE EIBTRMID    TO ASC-LIST-TERM-ID            INKCS003
00714                   MOVE DP020-SRC-TASK-NUMBER                      INKCS003
00715                                    TO ASC-LIST-TASK-NUMBER        INKCS003
00716                   MOVE PC-LIST-QUEUE-SEQ                          INKCS003
00717                                    TO ASC-LIST-SEQ                INKCS003
00718                                                                   INKCS003
00719                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS003
00720                   IF  SQLCODE = +100                              INKCS003
00722                       SET DP020-FK-RETURN (DP020-SRC-AID)         INKCS003
00723                                    TO  TRUE                       INKCS003
00724                       MOVE PC-TSYMSG-00148                        INKCS003
00725                                    TO  DP020-MSG-NUMBER           INKCS003
00726                       SET DP020-MSG-INFORMATIONAL TO TRUE         INKCS003
00727                   END-IF                                          INKCS003
00728                   IF  ASC-NUMBER-OF-ITEMS-READ = ZERO             INKCS003
00729                       SET DP020-NEXT-ACT-APPL-ERROR TO TRUE       INKCS003
00730                   END-IF                                          INKCS003
00731               END-IF                                              INKCS003
00732                                                                   INKCS003
00733          WHEN DP020-NEXT-ACT-READ-MAP                             INKCS003
00734               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS003
00735                                    TO PV-TOP-ITEM                 INKCS003
00736               PERFORM 2000-PROCESS-PANEL                          INKCS003
00737                                                                   INKCS003
00738          WHEN DP020-NEXT-ACT-RETURN                               INKCS003
00739               MOVE ASC-ITEM-AT-TOP-OF-PANEL                       INKCS003
00740                                    TO PV-TOP-ITEM                 INKCS003
00741               MOVE ZERO            TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00742               MOVE ASC-HOLD-INTER-APPL                            INKCS003
00743                                    TO INTER-APPL-COMM-AREA        INKCS003
00744               PERFORM 3000-EDIT-DATA-IN-COMMAREA                  INKCS003
00745               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS003
00746                                                                   INKCS003
00747          WHEN OTHER                                               INKCS003
00748               SET DP013-LOGIC-ABEND TO TRUE                       INKCS003
00749               SET DP013-NO-ROLLBACK TO TRUE                       INKCS003
00750               MOVE '1000-CONTROL-PROCESSING'                      INKCS003
00751                                    TO DP013-PARAGRAPH             INKCS003
00752               MOVE 'INVALID NEXT APPLICATION ACTIVITY RECEIVED'   INKCS003
00753                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
00754               PERFORM DP013-0000-PROCESS-ABEND                    INKCS003
00755      END-EVALUATE.                                                INKCS003
00756                                                                   INKCS003
00757 *----------------------------------------------------------------*INKCS003
00758 *  DETERMINE THE KEY OF THE OBJECT TO BE UPDATED BASED ON THE    *INKCS003
00759 *  INTER-APPLICATION FORMAT CODE.                                *INKCS003
00760 *----------------------------------------------------------------*INKCS003
00761                                                                   INKCS003
00762  1100-FIND-OBJECT-KEYS.                                           INKCS003
00763                                                                   INKCS003
00764      INITIALIZE ASC-SPECIFIC-COMMAREA.                            INKCS003
00765      MOVE DP020-INT-APPL-FORMAT-IND                               INKCS003
00766                                    TO  ASC-INT-APPL-FORMAT-IND.   INKCS003
00767      MOVE IN001-STORE-NBR          TO  ASC-KEY-STORE-NBR.         INKCS003
00768                                                                   INKCS003
00769      EVALUATE DP020-INT-APPL-FORMAT-IND                           INKCS003
00770          WHEN PC-INTER-APPL-CRIT-SPEC                             INKCS003
00771               IF  IN001-AREA-NBR NUMERIC                          INKCS003
00772                   MOVE IN001-AREA-NBR                             INKCS003
00773                                    TO  ASC-KEY-AREA-NBR           INKCS003
00774               ELSE                                                INKCS003
00775                   MOVE PC-TSYMSG-00148                            INKCS003
00776                                    TO  DP020-MSG-NUMBER           INKCS003
00777                   SET DP020-MSG-FATAL                             INKCS003
00779                       PS-ERROR                                    INKCS003
00780                       DP020-FK-RETURN (DP020-SRC-AID)             INKCS003
00781                                    TO  TRUE                       INKCS003
00782               END-IF                                              INKCS003
00783                                                                   INKCS003
00784          WHEN PC-INTER-APPL-LIST                                  INKCS003
00785               SET ASC-USE-SELECTION-QUEUE TO TRUE                 INKCS003
00786               MOVE IN001-NUMBER-OF-SELECTED-ITEMS                 INKCS003
00787                                    TO ASC-NUMBER-OF-SELECTED-ITEMSINKCS003
00788               MOVE IN001-NUMBER-OF-TS-ITEMS                       INKCS003
00789                                    TO  ASC-NUMBER-OF-TS-ITEMS     INKCS003
00790               MOVE IN001-SEL-QUEUE-NAME                           INKCS003
00791                                    TO  ASC-SEL-QUEUE-NAME         INKCS003
00792               PERFORM 1120-PREPARE-LIST-ENTRY                     INKCS003
00793                                                                   INKCS003
00794          WHEN OTHER                                               INKCS003
00795               MOVE PC-TSYMSG-00148 TO  DP020-MSG-NUMBER           INKCS003
00796               SET DP020-MSG-FATAL                                 INKCS003
00798                   PS-ERROR                                        INKCS003
00799                   DP020-FK-RETURN (DP020-SRC-AID)                 INKCS003
00800                                    TO  TRUE                       INKCS003
00801      END-EVALUATE.                                                INKCS003
00802                                                                   INKCS003
00803 *----------------------------------------------------------------*INKCS003
00804 * ENTRY IS FROM A LIST.  GET THE MESSAGE NUMBER FROM THE TEMP    *INKCS003
00805 * STORAGE QUEUE PASSED FROM THE LIST.                            *INKCS003
00806 *----------------------------------------------------------------*INKCS003
00807                                                                   INKCS003
00808  1120-PREPARE-LIST-ENTRY.                                         INKCS003
00809                                                                   INKCS003
00810      MOVE IN002-MAX-ITEMS          TO ASC-SEL-SUB.                INKCS003
00811      MOVE ZERO                     TO ASC-SEL-ITEM.               INKCS003
00812      PERFORM 2101-GET-NEXT-AREA.                                  INKCS003
00813                                                                   INKCS003
00814 /                                                                 INKCS003
00815 *----------------------------------------------------------------*INKCS003
00816 * DO ANY PROCESSING FOR KEYS FOR WHICH NO EDITTING OF THE DATA   *INKCS003
00817 * ON THE SCREEN NEEDS TO BE DONE.  IF ONE OF THOSE KEYS IS NOT   *INKCS003
00818 * USED, EDIT THE DATA ON THE SCREEN AND GO ON TO CHECK THE REST  *INKCS003
00819 * OF THE FUNCTION KEYS.  NOTE THAT WITH THE API, NO INVALID      *INKCS003
00820 * FUNCTION KEYS CAN GET THIS FAR.                                *INKCS003
00821 *----------------------------------------------------------------*INKCS003
00822                                                                   INKCS003
00823  2000-PROCESS-PANEL.                                              INKCS003
00824                                                                   INKCS003
00825      SET  DP030-SET-CURSOR-ABSOLUTE TO TRUE.                      INKCS003
00826      IF  IN001-STORE-NBR NOT NUMERIC                              INKCS003
00827          MOVE ASC-HOLD-INTER-APPL TO INTER-APPL-COMM-AREA         INKCS003
00828      END-IF.                                                      INKCS003
00829                                                                   INKCS003
00830      EVALUATE TRUE                                                INKCS003
00831          WHEN DP020-SRC-AID = DP016-CLEAR                         INKCS003
00832              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00833              PERFORM 3000-EDIT-DATA-IN-COMMAREA                   INKCS003
00834              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS003
00835                                                                   INKCS003
00836          WHEN DP020-FK-REFRESH (DP020-SRC-AID)                    INKCS003
00837              PERFORM 2050-DELETE-LIST-QUEUE                       INKCS003
00838              PERFORM 4000-BUILD-INITIAL-PANEL                     INKCS003
00839                                                                   INKCS003
00840          WHEN OTHER                                               INKCS003
00841               PERFORM 2200-MOVE-SCREEN-TO-COMMAREA                INKCS003
00842                   PERFORM 3000-EDIT-DATA-IN-COMMAREA              INKCS003
00843                   IF  PS-NO-ERROR                                 INKCS003
00844                       SET DP030-CONTINUE-GO                       INKCS003
00845                                  TO TRUE                          INKCS003
00846                       PERFORM 2100-CHECK-FUNCTION-KEY             INKCS003
00847                   ELSE                                            INKCS003
00848                       MOVE ZERO  TO ASC-ITEM-AT-TOP-OF-PANEL      INKCS003
00849                       PERFORM 4400-MOVE-COMMAREA-TO-SCREEN        INKCS003
00850                       SET DP030-OVERRIDE-GO                       INKCS003
00851                                  TO TRUE                          INKCS003
00852                   END-IF                                          INKCS003
00853      END-EVALUATE.                                                INKCS003
00854 /                                                                 INKCS003
00855 *----------------------------------------------------------------*INKCS003
00856 *  DELETE THE LIST ITEMS TEMP STORAGE QUEUE.                     *INKCS003
00857 *----------------------------------------------------------------*INKCS003
00858  2050-DELETE-LIST-QUEUE.                                          INKCS003
00859                                                                   INKCS003
00860      EXEC CICS                                                    INKCS003
00861          DELETEQ TS                                               INKCS003
00862              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
00863              RESP  (PV-CICS-RESP)                                 INKCS003
00864      END-EXEC.                                                    INKCS003
00865                                                                   INKCS003
00866      IF (PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS003
00867      OR  PV-CICS-RESP = DFHRESP(QIDERR))                          INKCS003
00868          CONTINUE                                                 INKCS003
00869      ELSE                                                         INKCS003
00870          SET DP013-CICS-ABEND      TO TRUE                        INKCS003
00871          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS003
00872                                                                   INKCS003
00873          MOVE '2050-DELETE-LIST-QUEUE'                            INKCS003
00874                                    TO DP013-PARAGRAPH             INKCS003
00875          MOVE 'ERROR TRYING TO DELETE LIST TEMP STORAGE QUEUE'    INKCS003
00876                                    TO DP013-MESSAGE-TEXT (1)      INKCS003
00877          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
00878      END-IF.                                                      INKCS003
00879                                                                   INKCS003
00880      MOVE +0                       TO ASC-HIGHEST-BLOCK-WRITTEN.  INKCS003
00881 /                                                                 INKCS003
00882 *----------------------------------------------------------------*INKCS003
00883 * ACT ON ANY FUNCTION KEYS THAT REQUIRE EDITS TO BE PASSED FIRST.*INKCS003
00884 * NOTE THAT INVALID FUNCTION KEYS WILL NOT BE RETURNED FROM THE  *INKCS003
00885 * CICS ARCHITECTURE API.                                         *INKCS003
00886 *----------------------------------------------------------------*INKCS003
00887                                                                   INKCS003
00888  2100-CHECK-FUNCTION-KEY.                                         INKCS003
00889                                                                   INKCS003
00890      EVALUATE TRUE                                                INKCS003
00891                                                                   INKCS003
00892          WHEN DP020-FK-FIRST-PAGE(DP020-SRC-AID)                  INKCS003
00893              MOVE +1               TO PV-TOP-ITEM                 INKCS003
00894              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00895              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS003
00896                                                                   INKCS003
00897          WHEN DP020-FK-PREV-PAGE(DP020-SRC-AID)                   INKCS003
00898              PERFORM 2110-BROWSE-BACKWARD                         INKCS003
00899              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00900              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS003
00901                                                                   INKCS003
00902          WHEN DP020-FK-NEXT-PAGE(DP020-SRC-AID)                   INKCS003
00903              PERFORM 2120-BROWSE-FORWARD                          INKCS003
00904              MOVE ZERO             TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00905              PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 INKCS003
00906                                                                   INKCS003
00907          WHEN DP020-FK-NEXT-SELECTION (DP020-SRC-AID)             INKCS003
00908               IF  ASC-USE-SELECTION-QUEUE                         INKCS003
00909                   PERFORM 2101-GET-NEXT-AREA                      INKCS003
00910                   PERFORM 3200-RESET-ATTRIBUTES                   INKCS003
00911                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS003
00912               ELSE                                                INKCS003
00913 *                 --- BOTTOM OF LIST ---                          INKCS003
00914                   MOVE PC-TSYMSG-00070                            INKCS003
00915                                    TO DP020-MSG-NUMBER            INKCS003
00916                   SET DP020-MSG-INFORMATIONAL                     INKCS003
00917                                    TO TRUE                        INKCS003
00918                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            INKCS003
00919               END-IF                                              INKCS003
00920                                                                   INKCS003
00921          WHEN DP020-FK-PREV-SELECTION (DP020-SRC-AID)             INKCS003
00922               IF  ASC-USE-SELECTION-QUEUE                         INKCS003
00923                   PERFORM 2102-GET-PREV-AREA                      INKCS003
00924                   PERFORM 3200-RESET-ATTRIBUTES                   INKCS003
00925                   PERFORM 4000-BUILD-INITIAL-PANEL                INKCS003
00926               ELSE                                                INKCS003
00927 *                 --- TOP OF LIST ---                             INKCS003
00928                   MOVE PC-TSYMSG-00069                            INKCS003
00929                                    TO DP020-MSG-NUMBER            INKCS003
00930                   SET DP020-MSG-INFORMATIONAL                     INKCS003
00931                                    TO TRUE                        INKCS003
00932                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN            INKCS003
00933               END-IF                                              INKCS003
00934                                                                   INKCS003
00935           WHEN DP020-FK-LOCAL-FUNC-01 (DP020-SRC-AID)             INKCS003
00936                IF  PS-NO-ERROR                                    INKCS003
00937                    SET DP030-CONTINUE-GO TO TRUE                  INKCS003
00938                    PERFORM 5000-UPDATE-PROCESS                    INKCS003
00939                    MOVE ZERO       TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
00940 *              --- UPDATES SUCCESSFUL ---                         INKCS003
00941                    IF  PS-UPDATED                                 INKCS003
00942                        MOVE PC-TSYMSG-00059                       INKCS003
00943                                          TO DP020-MSG-NUMBER      INKCS003
00944                    ELSE                                           INKCS003
00945                        MOVE PC-TSYMSG-00046                       INKCS003
00946                                          TO DP020-MSG-NUMBER      INKCS003
00947                    END-IF                                         INKCS003
00948                    SET DP020-MSG-INFORMATIONAL                    INKCS003
00949                                          TO TRUE                  INKCS003
00950                ELSE                                               INKCS003
00951                    MOVE PC-TSYMSG-00254                           INKCS003
00952                                    TO  DP020-MSG-NUMBER           INKCS003
00953                    SET DP020-MSG-FATAL   TO TRUE                  INKCS003
00954                END-IF                                             INKCS003
00955                PERFORM 4400-MOVE-COMMAREA-TO-SCREEN               INKCS003
00956                                                                   INKCS003
00957          WHEN DP020-SRC-AID = DP016-ENTER                         INKCS003
00958               PERFORM 2130-CHECK-FOR-JUMP-BROWSE                  INKCS003
00959               MOVE ZERO           TO ASC-ITEM-AT-TOP-OF-PANEL     INKCS003
00960               PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                INKCS003
00961                                                                   INKCS003
00962          WHEN OTHER                                               INKCS003
00963              SET DP013-NO-ROLLBACK                                INKCS003
00964                  DP013-XCTL-DISPLAY-RESTART                       INKCS003
00965                  DP013-CICS-ABEND  TO TRUE                        INKCS003
00966              MOVE '2100-CHECK-FUNCTION-KEY'                       INKCS003
00967                                    TO DP013-PARAGRAPH             INKCS003
00968              MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      INKCS003
00969                                    TO DP013-MESSAGE-TEXT (1)      INKCS003
00970              PERFORM DP013-0000-PROCESS-ABEND                     INKCS003
00971      END-EVALUATE.                                                INKCS003
00972                                                                   INKCS003
00973 *----------------------------------------------------------------*INKCS003
00974 *    RETRIEVE THE NEXT KEY FOR PROCESSING.                       *INKCS003
00975 *----------------------------------------------------------------*INKCS003
00976                                                                   INKCS003
00977  2101-GET-NEXT-AREA.                                              INKCS003
00978                                                                   INKCS003
00979      ADD +1                        TO  ASC-SEL-SUB.               INKCS003
00980      IF  ASC-SEL-SUB > IN002-MAX-ITEMS                            INKCS003
00981          ADD +1                    TO  ASC-SEL-ITEM               INKCS003
00982          MOVE +1                   TO  ASC-SEL-SUB                INKCS003
00983      END-IF.                                                      INKCS003
00984                                                                   INKCS003
00985      IF  ASC-SEL-ITEM NOT > ASC-NUMBER-OF-TS-ITEMS                INKCS003
00986          PERFORM 9100-READ-SEL-QUEUE                              INKCS003
00987          IF  IN002-ITEM (ASC-SEL-SUB) > ZERO                      INKCS003
00988              MOVE IN002-AREA-NBR (ASC-SEL-SUB)                    INKCS003
00989                                    TO  ASC-KEY-AREA-NBR           INKCS003
00990              SET IN002-INQ (ASC-SEL-SUB)                          INKCS003
00991                                    TO  TRUE                       INKCS003
00992              PERFORM 9200-REWRITE-LIST-QUEUE                      INKCS003
00993          ELSE                                                     INKCS003
00994              SUBTRACT +1 FROM ASC-SEL-SUB                         INKCS003
00995 *            --- BOTTOM OF LIST ---                               INKCS003
00996              MOVE PC-TSYMSG-00070  TO  DP020-MSG-NUMBER           INKCS003
00997              SET DP020-MSG-INFORMATIONAL                          INKCS003
00998                                    TO  TRUE                       INKCS003
00999          END-IF                                                   INKCS003
01000      ELSE                                                         INKCS003
01001          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS003
01002          MOVE IN002-MAX-ITEMS      TO  ASC-SEL-SUB                INKCS003
01003          MOVE PC-TSYMSG-00070      TO  DP020-MSG-NUMBER           INKCS003
01004          SET DP020-MSG-INFORMATIONAL                              INKCS003
01005                                    TO  TRUE                       INKCS003
01006      END-IF.                                                      INKCS003
01007                                                                   INKCS003
01008 *----------------------------------------------------------------*INKCS003
01009 *  GET THE MESSAGE NUMBER (KEY) OF THE PREVIOUS SELECTED         *INKCS003
01010 *  OBJECT FROM THE SELECTED-ITEMS TEMP. STORAGE QUEUE.           *INKCS003
01011 *----------------------------------------------------------------*INKCS003
01012                                                                   INKCS003
01013  2102-GET-PREV-AREA.                                              INKCS003
01014                                                                   INKCS003
01015      SUBTRACT +1 FROM ASC-SEL-SUB.                                INKCS003
01016      IF  ASC-SEL-SUB < +1                                         INKCS003
01017          SUBTRACT +1 FROM ASC-SEL-ITEM                            INKCS003
01018          MOVE IN002-MAX-ITEMS      TO ASC-SEL-SUB                 INKCS003
01019      END-IF.                                                      INKCS003
01020                                                                   INKCS003
01021      IF  ASC-SEL-ITEM > ZERO                                      INKCS003
01022          PERFORM 9100-READ-SEL-QUEUE                              INKCS003
01023          MOVE IN002-AREA-NBR (ASC-SEL-SUB)                        INKCS003
01024                                    TO ASC-KEY-AREA-NBR            INKCS003
01025      ELSE                                                         INKCS003
01026          MOVE +1                   TO ASC-SEL-ITEM                INKCS003
01027                                       ASC-SEL-SUB                 INKCS003
01028 *        --- TOP OF LIST ---                                      INKCS003
01029          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS003
01030          SET DP020-MSG-INFORMATIONAL                              INKCS003
01031                                    TO TRUE                        INKCS003
01032      END-IF.                                                      INKCS003
01033                                                                   INKCS003
01034 *----------------------------------------------------------------*INKCS003
01035 *    IF THE USER HITS THE PAGE BACKWARD KEY, COMPUTE THE TOP     *INKCS003
01036 *    ITEM NUMBER FOR THE NEW PAGE.  IF IT IS ABOVE THE TOP,      *INKCS003
01037 *    SET THE TOP ITEM NUMBER TO +1 AND DISPLAY THE TOP OF LIST   *INKCS003
01038 *    MESSAGE.                                                    *INKCS003
01039 *----------------------------------------------------------------*INKCS003
01040                                                                   INKCS003
01041  2110-BROWSE-BACKWARD.                                            INKCS003
01042                                                                   INKCS003
01043      SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
01044          GIVING PV-TOP-ITEM.                                      INKCS003
01045                                                                   INKCS003
01046      IF  ((PV-TOP-ITEM < +1)                                      INKCS003
01047        OR (PV-TOP-ITEM = +1))                                     INKCS003
01048          MOVE +1                   TO PV-TOP-ITEM                 INKCS003
01049 *        -- TOP OF LIST --                                        INKCS003
01050          MOVE PC-TSYMSG-00069      TO DP020-MSG-NUMBER            INKCS003
01051          SET DP020-MSG-INFORMATIONAL TO TRUE                      INKCS003
01052      END-IF.                                                      INKCS003
01053                                                                   INKCS003
01054 /                                                                 INKCS003
01055 *----------------------------------------------------------------*INKCS003
01056 *    IF THE USER HITS THE PAGE FORWARD KEY, COMPUTE THE TOP      *INKCS003
01057 *    ITEM NUMBER FOR THE NEW PAGE.  IF THE USER ATTEMPTS TO GO   *INKCS003
01058 *    PAST THE END, ISSUE THE BOTTOM OF LIST MESSAGE.             *INKCS003
01059 *----------------------------------------------------------------*INKCS003
01060                                                                   INKCS003
01061  2120-BROWSE-FORWARD.                                             INKCS003
01062                                                                   INKCS003
01063      ADD PC-ITEMS-PER-PANEL ASC-ITEM-AT-TOP-OF-PANEL              INKCS003
01064          GIVING PV-TOP-ITEM.                                      INKCS003
01065      IF  PV-TOP-ITEM > ASC-NUMBER-OF-ITEMS-READ                   INKCS003
01066          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS003
01067                                    TO PV-TOP-ITEM                 INKCS003
01068 *        -- BOTTOM OF LIST --                                     INKCS003
01069          MOVE PC-TSYMSG-00070      TO DP020-MSG-NUMBER            INKCS003
01070          SET DP020-MSG-INFORMATIONAL                              INKCS003
01071                                    TO TRUE                        INKCS003
01072      ELSE                                                         INKCS003
01073          COMPUTE PV-BOTTOM-ITEM =                                 INKCS003
01074              PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1                 INKCS003
01075          IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READ            INKCS003
01076 *            -- BOTTOM OF LIST --                                 INKCS003
01077              MOVE PC-TSYMSG-00070  TO DP020-MSG-NUMBER            INKCS003
01078              SET DP020-MSG-INFORMATIONAL                          INKCS003
01079                                    TO TRUE                        INKCS003
01080          END-IF                                                   INKCS003
01081      END-IF.                                                      INKCS003
01082 /                                                                 INKCS003
01083 *----------------------------------------------------------------*INKCS003
01084 *    THE USER CAN CHANGE THE CURRENT BEGINNING LIST NUMBER SO    *INKCS003
01085 *    THAT THE TRANSACTION WILL JUMP TO THAT POINT IN THE LIST.   *INKCS003
01086 *    THIS IS A FASTER WAY TO GET TO A CERTAIN POINT IN THE LIST  *INKCS003
01087 *    RATHER THAN HITTING NEXT-PAGE TO GET TO THAT POINT.         *INKCS003
01088 *    THE NEW LIST NUMBER ENTERED IS EDITED TO MAKE SURE THAT     *INKCS003
01089 *    IT IS NUMERIC AND FALLS BETWEEN A SPECIFIED RANGE.          *INKCS003
01090 *----------------------------------------------------------------*INKCS003
01091                                                                   INKCS003
01092  2130-CHECK-FOR-JUMP-BROWSE.                                      INKCS003
01093                                                                   INKCS003
01094      SET PS-NO-ERROR               TO TRUE.                       INKCS003
01095      IF (ASC-START-ITEM-REQUEST = SPACE)                          INKCS003
01096          MOVE ASC-ITEM-AT-TOP-OF-PANEL                            INKCS003
01097                                    TO PV-TOP-ITEM                 INKCS003
01098      ELSE                                                         INKCS003
01099          MOVE ASC-START-ITEM-REQUEST                              INKCS003
01100                                    TO DP010I-UNEDITED-FIELD       INKCS003
01101          MOVE PC-MSG-NUMBER-LENGTH TO DP010I-MAXIMUM-DIGITS       INKCS003
01102          MOVE +0                   TO DP010I-MAXIMUM-DECIMALS     INKCS003
01103          SET DP010I-NEGATIVE-NOT-ALLOWED                          INKCS003
01104                                    TO TRUE                        INKCS003
263126         CALL DP010I-NUMERIC-EDIT-ROUTINE                                 
263126              USING DP010I-NUMERIC-EDIT-AREA                              
01106                                                                   INKCS003
01107          IF  NOT DP010I-ERROR-DETECTED                            INKCS003
01108              MOVE DP010I-NUMERIC-FIELD                            INKCS003
01109                                    TO  ASC-START-ITEM-REQUEST-N   INKCS003
01110              IF  ASC-START-ITEM-REQUEST-N > ZERO                  INKCS003
01111                  IF  ASC-START-ITEM-REQUEST-N >                   INKCS003
01112                                           ASC-NUMBER-OF-ITEMS-READINKCS003
01113                      MOVE SPACE TO ASC-START-ITEM-REQUEST         INKCS003
01114                      COMPUTE PV-TOP-ITEM =                        INKCS003
01115                              ASC-NUMBER-OF-ITEMS-READ             INKCS003
01116                              - PC-ITEMS-PER-PANEL + 1             INKCS003
01117                      IF  PV-TOP-ITEM < +1                         INKCS003
01118                          MOVE +1   TO PV-TOP-ITEM                 INKCS003
01119                          MOVE PC-TSYMSG-00070                     INKCS003
01120                                    TO DP020-MSG-NUMBER            INKCS003
01121                          SET DP020-MSG-INFORMATIONAL              INKCS003
01122                                    TO TRUE                        INKCS003
01123                      END-IF                                       INKCS003
01124                  ELSE                                             INKCS003
01125                      MOVE ASC-START-ITEM-REQUEST-N                INKCS003
01126                                    TO PV-TOP-ITEM                 INKCS003
01127                      MOVE SPACE    TO ASC-START-ITEM-REQUEST      INKCS003
01128                      COMPUTE PV-BOTTOM-ITEM =                     INKCS003
01129                          PV-TOP-ITEM + PC-ITEMS-PER-PANEL - 1     INKCS003
01130                      IF  PV-BOTTOM-ITEM > ASC-NUMBER-OF-ITEMS-READINKCS003
01131 *                        -- BOTTOM OF LIST --                     INKCS003
01132                          MOVE PC-TSYMSG-00070                     INKCS003
01133                                    TO DP020-MSG-NUMBER            INKCS003
01134                          SET DP020-MSG-INFORMATIONAL              INKCS003
01135                                    TO TRUE                        INKCS003
01136                      END-IF                                       INKCS003
01137                  END-IF                                           INKCS003
01138              ELSE                                                 INKCS003
01139 *                --- VALUE OUT OF RANGE ----                      INKCS003
01140                  MOVE PC-TSYMSG-00101                             INKCS003
01141                                    TO DP020-MSG-NUMBER            INKCS003
01142                  MOVE -1           TO ASTRTITL                    INKCS003
01143                  SET DP030-SET-CURSOR-APPL-1                      INKCS003
01144                                    TO TRUE                        INKCS003
01145                  MOVE DP015-REVERSE                               INKCS003
01146                                    TO ASTRTITH                    INKCS003
01147                  MOVE DP015-RED    TO ASTRTITC                    INKCS003
01148                  MOVE DP015-UNP-NUM-BRT-MDT                       INKCS003
01149                                    TO ASTRTITA                    INKCS003
01150                  SET DP020-MSG-FATAL                              INKCS003
01151                                    TO TRUE                        INKCS003
01152                  SET PS-ERROR      TO TRUE                        INKCS003
01153              END-IF                                               INKCS003
01154                                                                   INKCS003
01155          ELSE                                                     INKCS003
01156              MOVE PC-TSYMSG-00008  TO DP020-MSG-NUMBER            INKCS003
01157              MOVE -1               TO ASTRTITL                    INKCS003
01158              SET DP030-SET-CURSOR-APPL-1 TO TRUE                  INKCS003
01159              MOVE DP015-REVERSE    TO ASTRTITH                    INKCS003
01160              MOVE DP015-RED        TO ASTRTITC                    INKCS003
01161              MOVE DP015-UNP-NUM-BRT-MDT                           INKCS003
01162                                    TO ASTRTITA                    INKCS003
01163              SET DP020-MSG-FATAL   TO TRUE                        INKCS003
01164              SET PS-ERROR          TO TRUE                        INKCS003
01165          END-IF                                                   INKCS003
01166      END-IF.                                                      INKCS003
01167                                                                   INKCS003
01168 *----------------------------------------------------------------*INKCS003
01169 *  MOVE ALL OF THE FIELDS THAT WERE ENTERED / CHANGED ON THE     *INKCS003
01170 *  SCREEN INTO THE COMMAREA.  ALL EDITTING IS DONE IN THE COMM.  *INKCS003
01171 *----------------------------------------------------------------*INKCS003
01172                                                                   INKCS003
01173  2200-MOVE-SCREEN-TO-COMMAREA.                                    INKCS003
01174                                                                   INKCS003
01175      MOVE PV-TOP-ITEM              TO PV-ITEM-IN-USE.             INKCS003
01176      MOVE +1                       TO PV-SUB.                     INKCS003
01177                                                                   INKCS003
01178      IF  ASTRTITL > ZERO                                          INKCS003
01179          MOVE ASTRTITI         TO ASC-START-ITEM-REQUEST          INKCS003
01180      ELSE                                                         INKCS003
01181          IF  ASTRTITF = DP015-ERASE-EOF                           INKCS003
01182              INITIALIZE ASC-START-ITEM-REQUEST                    INKCS003
01183          END-IF                                                   INKCS003
01184      END-IF.                                                      INKCS003
01185                                                                   INKCS003
01186      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS003
01187                                                                   INKCS003
01188      PERFORM 2210-MOVE-LINES-TO-COMMAREA                          INKCS003
01189          VARYING PV-SUB                                           INKCS003
01190          FROM 1 BY 1                                              INKCS003
01191          UNTIL PV-SUB > ASC-ITEMS-DISPLAYED.                      INKCS003
01192                                                                   INKCS003
01193 *----------------------------------------------------------------*INKCS003
01194 *  MOVE THE ENTERABLE FIELDS ON EACH LIST LINE INTO THE COMMAREA *INKCS003
01195 *----------------------------------------------------------------*INKCS003
01196                                                                   INKCS003
01197  2210-MOVE-LINES-TO-COMMAREA.                                     INKCS003
01198                                                                   INKCS003
W21732     IF  MR-CSKUL   (PV-SUB) > ZERO                               INKCS003
W21732         SET ASC-SKU-WAS-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
W21732             ASC-SKU-ENTERED     (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
W21732                                   TO  TRUE                       INKCS003
W21732         SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS003
W21732         MOVE MR-CSKU    (PV-SUB)  TO  ASC-CSKU                   INKCS003
W21732                                      (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
W21732     ELSE                                                         INKCS003
W21732         IF  MR-CSKUA   (PV-SUB) = DP015-ERASE-EOF                INKCS003
W21732             SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                 INKCS003
W21732                 ASC-SKU-ENTERED    (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
W21732                                   TO  TRUE                       INKCS003
W21732                 MOVE SPACE        TO  ASC-CSKU                   INKCS003
W21732                                      (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
W21732         END-IF                                                   INKCS003
W21732     END-IF.                                                      INKCS003
01241 *                                                                 INKCS003
01242 *    CHECK IF THE SKU NUMBER WAS SPACED OUT OR EOF'D              INKCS003
01243 *                                                                 INKCS003
W21732         IF (MR-CSKUA   (PV-SUB) = DP015-ERASE-EOF                INKCS003
W21732         OR (MR-CSKU    (PV-SUB) = SPACES                         INKCS003
W21732         AND MR-CSKUL   (PV-SUB) > ZERO))                         INKCS003
W21732             SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                 INKCS003
W21732                 ASC-SKU-NOT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
W21732                                   TO  TRUE                       INKCS003
W21732             MOVE SPACE            TO  ASC-CSKU                   INKCS003
W21732                                      (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
W21732                                       ASC-SKU-ENTER-ERROR-SW     INKCS003
W21732                                      (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01258              IF (ASC-CUPC(ASC-BLK-SUB, ASC-ITEM-SUB) NUMERIC      INKCS003
01259              AND ASC-CUPC(ASC-BLK-SUB, ASC-ITEM-SUB) > ZERO)      INKCS003
01260                  SET ASC-UPC-WAS-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)INKCS003
01261                      ASC-UPC-ENTERED  (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
01262                                    TO  TRUE                       INKCS003
01263              ELSE                                                 INKCS003
01264                  MOVE SPACE         TO ASC-RECORD-UPDATED-SW      INKCS003
01265                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01266                  MOVE ASC-ORIG-INV-PR-AMT                         INKCS003
01267                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01268                                     TO ASC-INV-PR-AMT             INKCS003
01269                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01270                  SET ASC-PROTECT-PR-AMT                           INKCS003
01271                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01272                                     TO TRUE                       INKCS003
01273              END-IF                                               INKCS003
01274          END-IF.                                                  INKCS003
01275                                                                   INKCS003
01276      IF  MR-CUPCL   (PV-SUB) > ZERO                               INKCS003
01277          SET ASC-UPC-WAS-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01278              ASC-UPC-ENTERED     (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01279                                    TO  TRUE                       INKCS003
01280          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB) TO TRUE             INKCS003
01281          MOVE MR-CUPC    (PV-SUB)  TO  ASC-CUPC-X                 INKCS003
01282                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01283      END-IF.                                                      INKCS003
01284                                                                   INKCS003
01285          IF (MR-CUPCA   (PV-SUB) = DP015-ERASE-EOF                INKCS003
01286          OR (MR-CUPC-X  (PV-SUB) = SPACES                         INKCS003
01287          AND MR-CUPCL   (PV-SUB) > ZERO))                         INKCS003
01288              SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                 INKCS003
01289                  ASC-UPC-NOT-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)   INKCS003
01290                                    TO  TRUE                       INKCS003
01291                  MOVE SPACE        TO  ASC-CUPC-X                 INKCS003
01292                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01293                                        ASC-UPC-ENTER-ERROR-SW     INKCS003
01294                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01295                                        ASC-UPC-SW                 INKCS003
01296                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01297                                                                   INKCS003
W21732             IF  ASC-CSKU             (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
W21732                 NOT GREATER THAN SPACES                          INKCS003
01300                  MOVE SPACE         TO ASC-RECORD-UPDATED-SW      INKCS003
01301                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01302                  MOVE ASC-ORIG-INV-PR-AMT                         INKCS003
01303                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01304                                     TO ASC-INV-PR-AMT             INKCS003
01305                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01306                  SET ASC-PROTECT-PR-AMT                           INKCS003
01307                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01308                                     TO TRUE                       INKCS003
01309              ELSE                                                 INKCS003
01310                  SET ASC-SKU-WAS-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)INKCS003
01311                      ASC-SKU-ENTERED  (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
01312                                    TO  TRUE                       INKCS003
01313              END-IF                                               INKCS003
01314          END-IF.                                                  INKCS003
01315                                                                   INKCS003
01316      IF  MR-INV-PR-AMTL (PV-SUB) > ZERO                           INKCS003
01317          SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)                     INKCS003
01318              ASC-PR-AMT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
01319                                       TO  TRUE                    INKCS003
01320          MOVE MR-INV-PR-AMT (PV-SUB)  TO  ASC-INV-PR-AMT-X        INKCS003
01321                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01322      ELSE                                                         INKCS003
01323          IF  MR-INV-PR-AMTA (PV-SUB) = DP015-ERASE-EOF            INKCS003
01324                  SET ASC-BLOCK-MODIFIED (ASC-BLK-SUB)             INKCS003
01325                      ASC-PR-AMT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
01326                                    TO  TRUE                       INKCS003
01327                  MOVE SPACE        TO  ASC-INV-PR-AMT-X           INKCS003
01328                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01329          ELSE                                                     INKCS003
01335              SET ASC-PR-AMT-NOT-ENTERED                           INKCS003
01336                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
01337                                    TO  TRUE                       INKCS003
01338          END-IF                                                   INKCS003
01339      END-IF.                                                      INKCS003
01340                                                                   INKCS003
01341      IF (ASC-SKU-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)                INKCS003
01342      OR  ASC-UPC-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB))               INKCS003
01343          IF  ASC-PR-AMT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
01344              CONTINUE                                             INKCS003
01345          ELSE                                                     INKCS003
01346              IF  IN001-SCANNING-STORE                             INKCS003
01347                  IF  (ASC-KEY-AREA-NBR LESS THAN PC-9000          INKCS003
01348                  OR   ASC-KEY-AREA-NBR GREATER   PC-9999)         INKCS003
01349                      MOVE ZERO        TO  ASC-INV-PR-AMT          INKCS003
01350                                       (ASC-BLK-SUB, ASC-ITEM-SUB) INKCS003
01351                  END-IF                                           INKCS003
01352              END-IF                                               INKCS003
01353          END-IF                                                   INKCS003
01354      END-IF.                                                      INKCS003
01355                                                                   INKCS003
01356      ADD +1                        TO ASC-ITEM-SUB.               INKCS003
01357      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS003
01358          ADD +1                    TO ASC-BLK-SUB                 INKCS003
01359          MOVE +1                   TO ASC-ITEM-SUB                INKCS003
01360      END-IF.                                                      INKCS003
01361                                                                   INKCS003
01362 *----------------------------------------------------------------*INKCS003
01363 *  EDIT ALL OF THE DATA ON THE SCREEN AS IT RESIDES IN THE       *INKCS003
01364 *  COMMAREA (EDITTING IS NOT DONE AGAINST THE SCREEN DIRECTLY).  *INKCS003
01365 *----------------------------------------------------------------*INKCS003
01366                                                                   INKCS003
01367  3000-EDIT-DATA-IN-COMMAREA.                                      INKCS003
01368                                                                   INKCS003
      *    *********************************************************            
      *    **  GET THE STORE'S PLANNED INVENTORY DATE FROM TINVPAR.             
      *    **  THIS DATE WILL BE USED TO GET STATUS AND PRICE INFO              
      *    **  FROM TUPCPLS.                                                    
      *    *********************************************************            
J28545     PERFORM 3690-SELECT-TINVPAR.                                         
                                                                                
01369      SET PS-NO-ERROR               TO TRUE.                       INKCS003
01370      PERFORM 3200-RESET-ATTRIBUTES.                               INKCS003
01371      MOVE ASC-ITEMS-DISPLAYED      TO PV-SUB.                     INKCS003
01372 **                                                                INKCS003
01373 * CALCULATE THE POSITION OF THE LAST ITEM DISPLAYED.              INKCS003
01374 * EDITTING IS DONE FROM THE BOTTOM UP.                            INKCS003
01375 **                                                                INKCS003
01376      COMPUTE PV-ITEM-IN-USE = PV-TOP-ITEM +                       INKCS003
01377          ASC-ITEMS-DISPLAYED - 1.                                 INKCS003
01378                                                                   INKCS003
01379      PERFORM 3100-SET-SUBSCRIPTS.                                 INKCS003
01380      PERFORM 3300-EDIT-SELECTIONS                                 INKCS003
01381          VARYING PV-SUB                                           INKCS003
01382          FROM PV-SUB BY -1                                        INKCS003
01383          UNTIL PV-SUB < +1.                                       INKCS003
01384 /                                                                 INKCS003
01385 *----------------------------------------------------------------*INKCS003
01386 *    DETERMINE THE VALUE OF THE ITEM SUBSCRIPT BASED ON THE ITEM *INKCS003
01387 *    CURRENTLY IN USE AND THE FIRST ITEM IN THE ARRAY.  THIS WILL*INKCS003
01388 *    POSITION US IN THE 1ST BLOCK OF THE ARRAY.                  *INKCS003
01389 *                                                                *INKCS003
01390 *    IF THE ITEM SUBSCRIPT EXCEEDS THE MAXIMUM NUMBER OF ITEMS   *INKCS003
01391 *    IN A BLOCK, RECALCULATE THE SUBSCRIPT BY SUBTRACTING OUT    *INKCS003
01392 *    THE MAXIMUM NUMBER OF ITEMS.  THIS WILL CORRECTLY POSITION  *INKCS003
01393 *    US IN THE 2ND BLOCK OF THE ARRAY.                           *INKCS003
01394 *----------------------------------------------------------------*INKCS003
01395                                                                   INKCS003
01396  3100-SET-SUBSCRIPTS.                                             INKCS003
01397                                                                   INKCS003
01398      COMPUTE ASC-ITEM-SUB =                                       INKCS003
01399          (PV-ITEM-IN-USE - ASC-LIST-ITEM-NUMBER(1 1)) + 1.        INKCS003
01400                                                                   INKCS003
01401      IF ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                         INKCS003
01402          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS003
01403                                                                   INKCS003
01404          MOVE PC-MAX-BLOCKS-IN-ARRAY TO ASC-BLK-SUB               INKCS003
01405      ELSE                                                         INKCS003
01406          MOVE +1                     TO ASC-BLK-SUB               INKCS003
01407      END-IF.                                                      INKCS003
01408                                                                   INKCS003
01409 *----------------------------------------------------------------*INKCS003
01410 *  RESET THE ATTRIBUTES OF ALL ENTERABLE FIELDS ON THE MAP.      *INKCS003
01411 *----------------------------------------------------------------*INKCS003
01412                                                                   INKCS003
01413  3200-RESET-ATTRIBUTES.                                           INKCS003
01414                                                                   INKCS003
01415      MOVE DP015-UNP-ALP-NOR-OFF    TO ASTRTITA.                   INKCS003
01416      MOVE DP015-GREEN              TO ASTRTITC.                   INKCS003
01417      MOVE DP015-UNDERLINE          TO ASTRTITH.                   INKCS003
01418                                                                   INKCS003
01419      PERFORM                                                      INKCS003
01420          VARYING PV-SUB                                           INKCS003
01421          FROM 1 BY 1                                              INKCS003
01422          UNTIL PV-SUB > PC-ITEMS-PER-PANEL                        INKCS003
01423          MOVE DP015-UNP-ALP-NOR-OFF                               INKCS003
W21732                                   TO MR-CSKUA   (PV-SUB)         INKCS003
01427                                       MR-CUPCA   (PV-SUB)         INKCS003
W21732         MOVE DP015-GREEN          TO MR-CSKUC   (PV-SUB)         INKCS003
01431                                       MR-CUPCC   (PV-SUB)         INKCS003
W21732         MOVE DP015-UNDERLINE      TO MR-CSKUH   (PV-SUB)         INKCS003
01435                                       MR-CUPCH   (PV-SUB)         INKCS003
01436      END-PERFORM.                                                 INKCS003
01437                                                                   INKCS003
01438 *----------------------------------------------------------------*INKCS003
01439 *  EDIT THE SCREEN.                                              *INKCS003
01440 *----------------------------------------------------------------*INKCS003
01441                                                                   INKCS003
01442  3300-EDIT-SELECTIONS.                                            INKCS003
01443                                                                   INKCS003
01444      IF  ASC-PROTECT-ENTRY(ASC-BLK-SUB ASC-ITEM-SUB)              INKCS003
01445          CONTINUE                                                 INKCS003
01446      ELSE                                                         INKCS003
01447                                                                   INKCS003
01448          IF  ASC-PR-AMT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
01449              PERFORM 3700-VALIDATE-PR-AMT                         INKCS003
01450          ELSE                                                     INKCS003
01451              IF (ASC-ENTER-PR-AMT(ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01452              OR  ASC-PR-AMT-ERROR(ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01453              OR  ASC-PR-AMT-E500 (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01454              OR  ASC-PR-AMT-E20  (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01455              OR  ASC-PR-AMT-ZERO (ASC-BLK-SUB ASC-ITEM-SUB))      INKCS003
01456                  SET PS-ERROR   TO TRUE                           INKCS003
01457              END-IF                                               INKCS003
01458          END-IF                                                   INKCS003
01459                                                                   INKCS003
01460          IF  ASC-UPC-ENTERED  (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS003
01461              PERFORM 3500-VALIDATE-UPC                            INKCS003
01462          ELSE                                                     INKCS003
01463              IF (ASC-UPC-ERROR  (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS003
01464              OR  ASC-UPC-NOTFND (ASC-BLK-SUB ASC-ITEM-SUB)        INKCS003
01465              OR  ASC-UPC-NOTFND2(ASC-BLK-SUB ASC-ITEM-SUB)        INKCS003
01466              OR  ASC-UPC-CHKDIG (ASC-BLK-SUB ASC-ITEM-SUB))       INKCS003
01467                  SET PS-ERROR   TO  TRUE                          INKCS003
01468              END-IF                                               INKCS003
01469          END-IF                                                   INKCS003
01470                                                                   INKCS003
01471          IF  ASC-SKU-ENTERED  (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS003
01472              PERFORM 3400-VALIDATE-SKU                            INKCS003
01473          ELSE                                                     INKCS003
01474              IF  ASC-SKU-ERROR(ASC-BLK-SUB ASC-ITEM-SUB)          INKCS003
01475                  SET PS-ERROR   TO  TRUE                          INKCS003
01476              END-IF                                               INKCS003
01477          END-IF                                                   INKCS003
01478                                                                   INKCS003
01479          IF (ASC-UPC-WAS-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)       INKCS003
01480          AND ASC-SKU-WAS-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB))      INKCS003
01481              SET PS-ERROR                                         INKCS003
01482                  ASC-SKU-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
01483                  ASC-UPC-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
01484                                    TO  TRUE                       INKCS003
01485                                                                   INKCS003
01486          END-IF                                                   INKCS003
01487      END-IF.                                                      INKCS003
01488                                                                   INKCS003
01489      SUBTRACT +1             FROM PV-ITEM-IN-USE.                 INKCS003
01490      SUBTRACT +1 FROM ASC-ITEM-SUB.                               INKCS003
01491      IF  ASC-ITEM-SUB < 1                                         INKCS003
01492          SUBTRACT +1 FROM ASC-BLK-SUB                             INKCS003
01493          MOVE PC-ITEMS-PER-PANEL   TO ASC-ITEM-SUB                INKCS003
01494      END-IF.                                                      INKCS003
01495 /                                                                 INKCS003
01496 *----------------------------------------------------------------*INKCS003
01497 *    ENSURE THE SKU EXISTS.                                      *INKCS003
01498 *----------------------------------------------------------------*INKCS003
01499                                                                   INKCS003
01500  3400-VALIDATE-SKU.                                               INKCS003
01501                                                                   INKCS003
01503      SET ASC-SKU-NOT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)            INKCS003
01504                                    TO  TRUE.                      INKCS003
W21732     MOVE ASC-CSKU      (ASC-BLK-SUB ASC-ITEM-SUB)                INKCS003
W21732                                   TO  DP010I-UNEDITED-FIELD.     INKCS003
W21732     MOVE PC-8                     TO  DP010I-MAXIMUM-DIGITS.     INKCS003
01547      MOVE ZERO                     TO  DP010I-MAXIMUM-DECIMALS.   INKCS003
01548      SET DP010I-NEGATIVE-NOT-ALLOWED                              INKCS003
01549                                    TO  TRUE.                      INKCS003
263126     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263126          USING DP010I-NUMERIC-EDIT-AREA.                                 
01551                                                                   INKCS003
01552      IF  DP010I-ERROR-DETECTED                                    INKCS003
01553          SET  ASC-SKU-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS003
01554                                    TO  TRUE                       INKCS003
01555      ELSE                                                         INKCS003
W21732         MOVE DP010I-NUMERIC-FIELD TO  PV-EIGHT                   INKCS003
J28545         MOVE PV-EIGHT-X           TO  ASC-CSKU                   INKCS003
01562                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
01564      END-IF.                                                      INKCS003
01565                                                                   INKCS003
01566      IF  ASC-SKU-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)                 INKCS003
01567          SET PS-ERROR              TO  TRUE                       INKCS003
01568      ELSE                                                         INKCS003
J28545         PERFORM 3410-CONTINUE-SKU-VALIDATE                       INKCS003
01570      END-IF.                                                      INKCS003
01571                                                                   INKCS003
01572 *----------------------------------------------------------------*INKCS003
J28545*    PASSED NUMERIC CHECK, NOW REALLY VALIDATE BY SKU            *INKCS003
01574 *----------------------------------------------------------------*INKCS003
J28545*                                                                 INKCS003
J28545 3410-CONTINUE-SKU-VALIDATE.                                      INKCS003
01577                                                                           
J28545*  PRICING                                                                
J28545     SET IN400-PV-SKU-LOOKUP     TO TRUE                                  
X28545**** SET ASC-SKU-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)  TO TRUE              
J28545     MOVE ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)                             
J28545                                 TO IN400-PV-SKU-IN                       
J28545     MOVE INVPAR-PLND-INV-DTE    TO IN400-PV-EFF-DTE-IN.                  
J28545     MOVE INVPAR-LOC-NBR         TO IN400-PV-LOC-IN-NUM                   
J28545     PERFORM IN400-LOOKUP-RETAIL.                                         
J28545                                                                          
J28545     EVALUATE TRUE                                                        
J28545         WHEN IN400-PV-NOT-FOUND                                          
J28545             MOVE IN400-PV-SKU-IN-NUM  TO PV-SKU-NBR                      
J28545             MOVE PV-SKU-NBR                                              
J28545                   TO ASC-CSKU   (ASC-BLK-SUB ASC-ITEM-SUB)               
J28545             MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                     
J28545             SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS003
J28545             SET ASC-SKU-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545                 PS-ERROR                                                 
J28545                                  TO TRUE                                 
J28545         WHEN IN400-PV-FOUND                                              
J28545             MOVE IN400-PRC-SKU-OUT TO PV-SKU-NBR                 INKCS005
J28545             MOVE PV-SKU-NBR                                              
J28545                  TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)                  
32318              IF IN400-ATT-PLU-DWNLD-IND EQUAL 'N'                         
32318                MOVE PC-TSYMSG-03549 TO DP020-MSG-NUMBER                   
32318                SET DP020-MSG-INFORMATIONAL TO TRUE                INKCS003
32318                SET ASC-SKU-PLU-ERR (ASC-BLK-SUB ASC-ITEM-SUB)             
32318                    PS-ERROR         TO TRUE                               
32318              ELSE                                                         
B28545               SET ASC-UPDATED                                            
B28545                  (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE                      
J28545               IF IN400-PS-RENUMBER-SKU                                   
J28545               OR IN400-PS-DUMMY-SKU                                      
J28545               OR IN400-PRC-STATUS-OUT EQUAL PC-30                INKCS005
J28545                 PERFORM 3625-SKU-UPC-FOUND                               
X28545*                MOVE IN400-PRC-UNIT-RTL-OUT                              
X28545*                     TO ASC-INV-PR-AMT                                   
X28545*                          (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545               ELSE                                                       
X28545*                MOVE PV-SKU-NBR                                          
X28545*                    TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)               
X28545*                MOVE IN400-PRC-UPC-OUT                                   
X28545*                    TO ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)               
J28545                 IF IN400-PRC-UNIT-RTL-OUT  > ZERO                        
J28545                     MOVE IN400-PRC-UNIT-RTL-OUT TO                       
J28545                        ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)         
J28545                 ELSE                                                     
J28545                     SET ASC-PR-AMT-ERROR                                 
J28545                              (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE          
J28545                 END-IF                                                   
J28545                 PERFORM 3600-CHECK-ZERO-AMT                              
J28545               END-IF                                                     
32318              END-IF                                                       
J28545         WHEN IN400-PV-SQL-ERROR                                          
J28545             MOVE IN400-PV-RETURN-RSN                             INKCS005
J28545                               TO DP013-MESSAGE-TEXT (1)          INKCS005
J28545             MOVE IN400-PV-PARAGRAPH                              INKCS005
J28545                                   TO DP013-MESSAGE-TEXT (2)      INKCS005
J28545             MOVE SQLCA            TO DP013-SQLCA                 INKCS005
J28545             MOVE '3410-CONTINUE-SKU-VALIDATE'                    INKCS005
J28545                               TO DP013-MESSAGE-TEXT (3)          INKCS005
J28545             SET DP013-DB2-ABEND                                  INKCS005
J28545                 DP013-XCTL-DISPLAY-RESTART                       INKCS005
J28545                               TO TRUE                            INKCS005
J28545             PERFORM DP013-0000-PROCESS-ABEND                     INKCS005
J28545                                                                          
J28545                                                                          
J28545     END-EVALUATE.                                                        
J28545                                                                          
J28545/                                                                         
01802 *----------------------------------------------------------------*INKCS003
01803 *    UPC NUMBER WAS ENTERED.  VALIDATE THE CHECK DIGIT FIRST,    *INKCS003
01804 *    AND IF OK, CHECK THE UPCSKU TABLE.                          *INKCS003
01805 *----------------------------------------------------------------*INKCS003
01806                                                                   INKCS003
01807  3500-VALIDATE-UPC.                                               INKCS003
01808                                                                   INKCS003
JS1099     MOVE SPACES                   TO  UPCPLS-UPC-STAT-CDE.       INKCS003
01810      SET ASC-UPC-NOT-ENTERED(ASC-BLK-SUB ASC-ITEM-SUB)            INKCS003
01811                                    TO  TRUE.                      INKCS003
01812      MOVE ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)                     INKCS003
01813                                    TO  DP010I-UNEDITED-FIELD.     INKCS003
01814      MOVE PC-15                    TO  DP010I-MAXIMUM-DIGITS.     INKCS003
01815      MOVE ZERO                     TO  DP010I-MAXIMUM-DECIMALS.   INKCS003
01816      SET DP010I-NEGATIVE-NOT-ALLOWED                              INKCS003
01817                                    TO  TRUE.                      INKCS003
263126     CALL DP010I-NUMERIC-EDIT-ROUTINE                                     
263126          USING DP010I-NUMERIC-EDIT-AREA.                                 
01819                                                                   INKCS003
01820      IF  DP010I-ERROR-DETECTED                                    INKCS003
01821          SET PS-ERROR                                             INKCS003
01822              ASC-UPC-NOTFND(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS003
01823                                    TO  TRUE                       INKCS003
01824      ELSE                                                         INKCS003
01825          MOVE DP010I-NUM-FIELD-15-DIG-2-DEC                       INKCS003
01826                                    TO  ASC-CUPC                   INKCS003
01827                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
01828          PERFORM 3510-CONTINUE-UPC-VALIDATE                       INKCS003
01829      END-IF.                                                      INKCS003
01830                                                                   INKCS003
J28545/                                                                         
01831 *----------------------------------------------------------------*INKCS003
01832 *    UPC NUMBER NUMERIC.  NOW CHECK TO SEE IF IT EXISTS ON THE   *INKCS003
01833 *    APPROPRIATE TABLE.                                          *INKCS003
01834 *----------------------------------------------------------------*INKCS003
01835                                                                   INKCS003
01836  3510-CONTINUE-UPC-VALIDATE.                                      INKCS003
01837                                                                   INKCS003
X28545     MOVE ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB) TO DP040I-UPC-CODE. INKCS003
X28545     SET DP040I-COMP-CHECK-DIGIT-OPTION       TO TRUE.            INKCS003
X28545                                                                  INKCS003
X28545     CALL DP040I-UPC-CHECK-DIGIT-WO-IUPC                          INKCS003
X28545                            USING DP040I-UPC-CHECK-DIGIT-PARMS.   INKCS003
X28545                                                                  INKCS003
X28545     IF DP040I-UPC-CHECK-DIGIT = DP040I-COMPUTED-UPC-CHK-DIGIT    INKCS003
J28545                                                                          
X28545         MOVE ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)                 INKCS003
X28545                                   TO PV-WORK-UPC                 INKCS003
J28545                                                                          
J28545*  PRICING                                                                
J28545         SET IN400-PV-UPC-NBR-LOOKUP  TO TRUE                             
X28545***      SET ASC-UPC-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)  TO TRUE          
J28545         MOVE ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)                         
J28545                                   TO IN400-PV-UPC-NBR-IN                 
J28545         MOVE INVPAR-PLND-INV-DTE  TO IN400-PV-EFF-DTE-IN                 
J28545         MOVE INVPAR-LOC-NBR       TO IN400-PV-LOC-IN-NUM                 
J28545         PERFORM IN400-LOOKUP-RETAIL                                      
J28545                                                                          
J28545         EVALUATE TRUE                                                    
J28545           WHEN IN400-PV-FOUND                                            
J28545             MOVE IN400-PRC-SKU-OUT  TO PV-SKU-NBR                        
X28545             MOVE PV-SKU-NBR                                              
X28545                  TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)                  
X28545             MOVE IN400-PRC-UPC-OUT  TO PV-WORK-UPC                       
X28545             MOVE PV-WORK-UPC                                             
X28545                  TO ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)                  
32318              IF IN400-ATT-PLU-DWNLD-IND EQUAL 'N'                         
32318                MOVE PC-TSYMSG-03549 TO DP020-MSG-NUMBER                   
32318                SET DP020-MSG-INFORMATIONAL TO TRUE                INKCS003
32318                SET ASC-UPC-PLU-ERR                                        
32318                  (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE                       
32318              ELSE                                                         
X28545               SET ASC-UPDATED (ASC-BLK-SUB ASC-ITEM-SUB)                 
X28545                                     TO TRUE                      INKCS003
S28545*              IF IN400-PS-RENUMBER-SKU                                   
J28545               IF IN400-PS-DUMMY-SKU                                      
J28545               OR IN400-PRC-STATUS-OUT EQUAL PC-30                INKCS005
J28545                 PERFORM 3625-SKU-UPC-FOUND                               
J28545               ELSE                                                       
X28545*                SET ASC-UPDATED                                          
X28545*                     (ASC-BLK-SUB ASC-ITEM-SUB)                          
X28545*                          TO TRUE                                INKCS003
X28545*                MOVE PV-SKU-NBR                                          
X28545*                    TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)               
X28545*                MOVE IN400-PRC-UPC-OUT                                   
X28545*                    TO ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)               
J28545                 IF IN400-PRC-UNIT-RTL-OUT > ZERO                         
J28545                     MOVE IN400-PRC-UNIT-RTL-OUT TO                       
J28545                        ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)         
J28545                 ELSE                                                     
J28545                     SET ASC-PR-AMT-ERROR                                 
J28545                              (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE          
J28545                 END-IF                                                   
J28545                 PERFORM 3600-CHECK-ZERO-AMT                              
X28545               END-IF                                                     
32318              END-IF                                                       
J28545           WHEN IN400-PV-NOT-FOUND                                        
X28545             MOVE IN400-PV-SKU-IN-NUM  TO PV-SKU-NBR                      
X28545             MOVE PV-SKU-NBR                                              
X28545                   TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)                 
X28545             MOVE IN400-PV-UPC-NBR-IN-NUM TO PV-WORK-UPC                  
X28545             MOVE PV-WORK-UPC                                             
X28545                     TO ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)               
X28545             MOVE PC-TSYMSG-00005 TO DP020-MSG-NUMBER                     
X28545             SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS003
X28545             SET ASC-UPC-ERROR                                            
X28545                 (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE                       
J28545                                                                          
J28545           WHEN IN400-PV-SQL-ERROR                                        
X28545             MOVE IN400-PV-RETURN-RSN                             INKCS005
X28545                               TO DP013-MESSAGE-TEXT (1)          INKCS005
X28545             MOVE IN400-PV-PARAGRAPH                              INKCS005
X28545                                   TO DP013-MESSAGE-TEXT (2)      INKCS005
X28545             MOVE SQLCA            TO DP013-SQLCA                 INKCS005
X28545             MOVE '3510-CONTINUE-UPC-VALIDATE'                    INKCS005
X28545                               TO DP013-MESSAGE-TEXT (3)          INKCS005
X28545             SET DP013-DB2-ABEND                                  INKCS005
X28545                 DP013-XCTL-DISPLAY-RESTART                       INKCS005
X28545                               TO TRUE                            INKCS005
X28545             PERFORM DP013-0000-PROCESS-ABEND                     INKCS005
X28545*            PERFORM 9900-SOFT-ABORT                                      
J28545       END-EVALUATE                                                       
X28545                                                                          
X28545     ELSE                                                         INKCS003
X28545         MOVE DP040I-COMPUTED-UPC-CHK-DIGIT                       INKCS003
X28545                                   TO  ASC-CHKDIG                 INKCS003
X28545                                      (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
X28545         SET PS-ERROR                                             INKCS003
X28545             ASC-UPC-CHKDIG(ASC-BLK-SUB ASC-ITEM-SUB)             INKCS003
X28545                                   TO  TRUE                       INKCS003
X28545     END-IF.                                                      INKCS003
X28545                                                                  INKCS003
01957 /----------------------------------------------------------------*INKCS003
01958 *  COMMON ROUTINE TO ENSURE PRICE ISN'T ZERO.                    *INKCS003
01959 *----------------------------------------------------------------*INKCS003
01960                                                                   INKCS003
01961  3600-CHECK-ZERO-AMT.                                             INKCS003
01962                                                                   INKCS003
01963      IF  IN001-SCANNING-STORE                                     INKCS003
01964          IF  (ASC-KEY-AREA-NBR LESS THAN PC-9000                  INKCS003
01965          OR   ASC-KEY-AREA-NBR GREATER   PC-9999)                 INKCS003
01966                                                                   INKCS003
J28545             IF IN400-PS-DUMMY-SKU                                INKCS003
J28545                 IF IN400-PRC-STATUS-OUT EQUAL PC-30                      
J28545                 AND IN400-PRC-STAT-CHG-DTE-OUT =                         
J28545                         IN400-PRC-UPC-CHG-DTE-OUT                        
J28545                     MOVE IN400-PRC-UNIT-RTL-OUT                          
23511C                         TO PV-UNT-RTL-AMT                                
23511C                            ASC-INV-PR-AMT                                
23511C                                (ASC-BLK-SUB ASC-ITEM-SUB)                
23511C                     SET ASC-PROTECT-PR-AMT                               
23511C                             (ASC-BLK-SUB ASC-ITEM-SUB)                   
23511C                         TO TRUE                                          
23511C                 ELSE                                                     
23511C                     MOVE ZERO TO  ASC-INV-PR-AMT                         
23511C                                   (ASC-BLK-SUB ASC-ITEM-SUB)             
23511C                     SET PS-ERROR                                         
23511C                         ASC-ENTER-PR-AMT                                 
23511C                         (ASC-BLK-SUB ASC-ITEM-SUB)                       
23511C                                                    TO  TRUE              
23511C                 END-IF                                                   
01975              ELSE                                                 INKCS003
J28545                 PERFORM 3610-VALID-SKU                                   
01986              END-IF                                               INKCS003
01987          END-IF                                                   INKCS003
01988      END-IF.                                                      INKCS003
01989                                                                   INKCS003
01990      IF  ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB) EQUAL ZERO     INKCS003
01991          SET PS-ERROR                                             INKCS003
01992              ASC-ENTER-PR-AMT                                     INKCS003
01993             (ASC-BLK-SUB ASC-ITEM-SUB)                            INKCS003
01994                                    TO  TRUE                       INKCS003
01995      END-IF.                                                      INKCS003
01996                                                                   INKCS003
J28545/                                                                         
J28545*----------------------------------------------------------------         
J28545* CHECKS STATUS AND PRICE FOR NON DUMMY SKUS                              
J28545*----------------------------------------------------------------         
J28545 3610-VALID-SKU.                                                          
J28545     IF ASC-PR-AMT-ENTERED                                                
J28545                      (ASC-BLK-SUB ASC-ITEM-SUB)                          
J28545     AND (ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)                       
J28545                      < .21                                               
J28545          OR ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)                    
J28545                      >  99999.99)                                        
J28545         MOVE PC-TSYMSG-00688 TO DP020-MSG-NUMBER                         
J28545         SET DP020-MSG-INFORMATIONAL TO TRUE                              
J28545         SET ASC-SKU-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545             TO TRUE                                                      
J28545         PERFORM 3640-SET-ENTER-PRICE                                     
J28545     ELSE                                                                 
J28545         IF IN400-PRC-STATUS-OUT EQUAL PC-30                              
J28545             IF IN400-PRC-STAT-CHG-DTE-OUT =                              
J28545                 IN400-PRC-UPC-CHG-DTE-OUT                                
J28545                 SET ASC-UPDATED                                          
J28545                      (ASC-BLK-SUB ASC-ITEM-SUB)                          
J28545                           TO TRUE                                INKCS003
J28545                 MOVE IN400-PRC-UNIT-RTL-OUT                              
J28545                      TO PV-UNT-RTL-AMT                                   
J28545                 MOVE PV-UNT-RTL-AMT TO ASC-INV-PR-AMT            INKCS003
J28545                          (ASC-BLK-SUB ASC-ITEM-SUB)              INKCS003
J28545                 SET ASC-PROTECT-PR-AMT                           INKCS003
J28545                          (ASC-BLK-SUB ASC-ITEM-SUB)              INKCS003
J28545                               TO TRUE                            INKCS003
J28545             ELSE                                                         
J28545                 SET ASC-ENTER-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)          
J28545                     DP020-MSG-FATAL                                      
J28545                     PS-WARNING            TO  TRUE                       
J28545             END-IF                                               INKCS003
J28545         END-IF                                                   INKCS003
J28545     END-IF.                                                      INKCS003
J28545/                                                                         
J28545*                                                                         
J28545 3625-SKU-UPC-FOUND.                                                      
J28545                                                                          
X28545*    IF IN400-PRC-STATUS-OUT EQUAL PC-30                          INKCS005
X28545*        PERFORM 3650-CHECK-RED-YELLOW-PRICES                             
X28545*    END-IF.                                                              
X28545*                                                                         
J28545     IF IN400-PS-RENUMBER-SKU                                             
J28545         MOVE IN400-PRC-SKU-OUT          TO  PV-SKU-NBR                   
J28545         MOVE PV-SKU-NBR                 TO                               
J28545             ASC-RSKU (ASC-BLK-SUB ASC-ITEM-SUB)                          
**SJ??         SET ASC-PR-AMT-ERROR                                             
**SJ??                  (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE                      
J28545         SET DP020-MSG-INFORMATIONAL TO TRUE                              
J28545         MOVE IN400-PRC-SKU-OUT TO DP020-MSG-TEXT                         
**SJ??         MOVE PC-TSYMSG-00453 TO DP020-MSG-NUMBER                         
**SJ??         IF IN400-PRC-STATUS-OUT EQUAL PC-30                      INKCS005
**SJ??             PERFORM 3650-CHECK-RED-YELLOW-PRICES                         
**SJ??         END-IF                                                           
J28545     ELSE                                                                 
J28545     IF IN400-PS-DUMMY-SKU                                                
X28545         MOVE IN400-ATT-SKU-OUT  TO PV-SKU-NBR                            
X28545         MOVE PV-SKU-NBR                                                  
X28545                  TO ASC-CSKU (ASC-BLK-SUB ASC-ITEM-SUB)                  
X28545                                                                          
X28545         IF IN400-PV-UPC-NBR-IN-NUM NOT EQUAL ZEROS                       
X28545             MOVE IN400-ATT-UPC-OUT  TO PV-WORK-UPC                       
X28545             MOVE PV-WORK-UPC                                             
X28545                  TO ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB)                  
X28545         END-IF                                                           
X28545                                                                          
J28545         IF ASC-PR-AMT-NOT-ENTERED                                        
J28545                 (ASC-BLK-SUB ASC-ITEM-SUB)                               
J28545             PERFORM 3640-SET-ENTER-PRICE                         INKCS005
J28545         END-IF                                                           
X28545     ELSE                                                                 
X28545     IF IN400-PRC-STATUS-OUT EQUAL PC-30                          INKCS005
X28545         PERFORM 3650-CHECK-RED-YELLOW-PRICES                             
X28545     ELSE                                                                 
X28545         IF IN400-PRC-UNIT-RTL-OUT > ZERO                                 
X28545             MOVE IN400-PRC-UNIT-RTL-OUT TO                               
X28545                ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)                 
X28545         ELSE                                                             
X28545             SET ASC-PR-AMT-ERROR                                         
X28545                      (ASC-BLK-SUB ASC-ITEM-SUB) TO TRUE                  
X28545         END-IF                                                           
X28545         PERFORM 3600-CHECK-ZERO-AMT                                      
J28545     END-IF.                                                              
J28545/                                                                         
J28545*----------------------------------------------------------------         
J28545*                                                                         
J28545*----------------------------------------------------------------         
J28545 3640-SET-ENTER-PRICE.                                            INKCS005
J28545                                                                  INKCS005
J28545     IF ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB) NOT NUMERIC             
J28545         MOVE ZEROS TO                                                    
J28545                   ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)              
J28545                   PV-UNT-RTL-AMT                                         
J28545     END-IF.                                                              
J28545     IF ((ASC-PR-AMT-ENTERED (ASC-BLK-SUB ASC-ITEM-SUB)           INKCS005
J28545     AND ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB) > ZERO)                
J28545                                                                  INKCS005
J28545     OR                                                           INKCS005
J28545        (NOT IN001-SCANNING-STORE                                 INKCS005
J28545     AND (ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB) > ZERO                
J28545          OR ASC-ORIG-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)               
J28545                  GREATER THAN ZERO))                                     
J28545                                                                  INKCS005
J28545     OR                                                           INKCS005
J28545        (IN001-SCANNING-STORE                                     INKCS005
X28545     AND (ASC-KEY-AREA-NBR GREATER THAN PC-8999                   INKCS005
X28545     AND  ASC-KEY-AREA-NBR LESS THAN PC-10000)                    INKCS005
X28545***  AND (IN001-AREA-NBR GREATER THAN PC-8999                     INKCS005
X28545***  AND  IN001-AREA-NBR LESS THAN PC-10000)                      INKCS005
J28545     AND (ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB) > ZERO                
J28545          OR ASC-ORIG-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)               
J28545                  GREATER THAN ZERO)))                                    
J28545                                                                  INKCS005
J28545         CONTINUE                                                 INKCS005
J28545     ELSE                                                         INKCS005
J28545         SET ASC-ENTER-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)          INKCS005
J28545             DP020-MSG-FATAL                                      INKCS005
J28545             PS-WARNING            TO  TRUE                       INKCS005
J28545                                                                  INKCS005
J28545         IF  ASC-RSKU (ASC-BLK-SUB ASC-ITEM-SUB)    = LOW-VALUES  INKCS005
J28545         OR  ASC-RSKU (ASC-BLK-SUB ASC-ITEM-SUB)    = SPACES      INKCS005
J28545             NEXT SENTENCE                                                
J28545         ELSE                                                             
J28545             MOVE PC-TSYMSG-00007  TO  DP020-MSG-NUMBER           INKCS005
J28545             SET DP020-MSG-WARNING TO TRUE                                
J28545         END-IF                                                   INKCS005
J28545     END-IF.                                                      INKCS005
J28545                                                                          
J28545*----------------------------------------------------------------         
J28545*  WHEN AN ITEM IS FIRST PUT ON CLEARANCE, THE PRICE IS CHANGED           
J28545*  AND THE STATUS IS CHANGED TO '30'.  THE STATUS CHANGE DATE             
J28545*  AND THE RETAIL CHANGE DATE ARE ALSO UPDATED.  THE ITEM IS NOW          
J28545*  A YELLOW TICKET ITEM. THE SYSTEM WILL GET THE PRICE FOR THESE          
J28545*  AND THEN IT CAN BE CHANGED.                                            
J28545*  WHEN THE ITEM GOES TO RED, THE RETAIL CHANGE DATE IS UPDATED           
J28545*  WHEN THE NEW PRICE IS ENTERED.                                         
J28545*  RED TICKET ITEMS REQUIRE THE PRICE TO BE ENTERED.                      
J28545*----------------------------------------------------------------         
J28545 3650-CHECK-RED-YELLOW-PRICES.                                            
J28545     IF IN400-PRC-UPC-CHG-DTE-OUT =                                       
J28545                      IN400-PRC-STAT-CHG-DTE-OUT                          
J28545*  YELLOW TICKET ITEM                                                     
J28545         MOVE IN400-PRC-UNIT-RTL-OUT                                      
J28545                          TO ASC-INV-PR-AMT                               
J28545                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545     ELSE                                                                 
J28545*  RED TICKET ITEM, REQUIRES PRICE ENTRY                                  
J28545         PERFORM 3640-SET-ENTER-PRICE                             INKCS005
J28545     END-IF.                                                      INKCS005
02038 *----------------------------------------------------------------*INKCS003
02039 *    SELECT PLANNED INVENTORY DATE FROM TINVPAR                   INKCS003
02040 *----------------------------------------------------------------*INKCS003
J28545 3690-SELECT-TINVPAR.                                             INKUT102
JS1099     INITIALIZE DCLTINVPAR.                                               
JS1099                                                                          
W26600     MOVE IN001-STORE-NBR TO  PV-STORE-NBR-X.                             
W26600     MOVE PV-STORE-NBR-9  TO  INVPAR-LOC-NBR.                             
J28545     MOVE IN001-INV-ID    TO  PV-INV-ID.                                  
W26600                                                                          
JS1099     EXEC SQL                                                             
JS1099       SELECT   A.PLND_INV_DTE                                            
JS1099         INTO  :INVPAR-PLND-INV-DTE                                       
JS1099         FROM   TINVPAR A                                                 
W28545               ,TINCNTL B                                                 
W28545       WHERE A.INV_ID = B.INV_ID                                          
W28545         AND A.INV_ID = :PV-INV-ID                                        
W28545         AND B.ACTV_IND = 'Y'                                             
W28545         AND A.LOC_NBR = :INVPAR-LOC-NBR                                  
W28545         AND A.ACTL_FIN_BK_DTE = :PC-NULL-DATE                            
W33304         AND A.LOC_INV_STAT_CDE = 'IN'                                    
JS1099     END-EXEC.                                                            
JS1099                                                                          
JS1099     EVALUATE SQLCODE                                                     
JS1099         WHEN +0                                                          
JS1099             MOVE INVPAR-PLND-INV-DTE TO PV-DB2-PLND-INV-DTE              
J28545                                      IN400-PV-EFF-DTE-IN                 
JS0999         WHEN OTHER                                                       
J28545             MOVE '3690-SELECT-INVPAR  '  TO DP013-PARAGRAPH      INKCS003
ABEND              MOVE 'SELECT TINVPAR' TO DP013-MESSAGE-TEXT (1)      INKCS003
ABEND              MOVE SQLCA        TO DP013-SQLCA                     INKCS003
ABEND              MOVE 'TINVPAR'    TO DP013-DB2-TABLE-NAME (1)        INKCS003
ABEND              SET DP013-DB2-ABEND                                  INKCS003
ABEND                  DP013-XCTL-DISPLAY-RESTART                       INKCS003
ABEND                     TO TRUE                                       INKCS003
ABEND              PERFORM DP013-0000-PROCESS-ABEND                     INKCS003
           END-EVALUATE.                                                        
02037 /                                                                 INKCS003
02038 *----------------------------------------------------------------*INKCS003
02039 *    ENSURE THE PRICE IS ACCEPTABLE.                             *INKCS003
02040 *----------------------------------------------------------------*INKCS003
02041                                                                   INKCS003
02042  3700-VALIDATE-PR-AMT.                                            INKCS003
02043                                                                   INKCS003
02044       MOVE ASC-INV-PR-AMT-X (ASC-BLK-SUB ASC-ITEM-SUB)            INKCS003
02045                                 TO  DP010I-UNEDITED-FIELD         INKCS003
02046       MOVE PC-7                 TO  DP010I-MAXIMUM-DIGITS         INKCS003
02047       MOVE PC-2                 TO  DP010I-MAXIMUM-DECIMALS       INKCS003
02048       SET DP010I-NEGATIVE-NOT-ALLOWED                             INKCS003
02049                                 TO  TRUE                          INKCS003
263126      CALL DP010I-NUMERIC-EDIT-ROUTINE                                    
263126           USING DP010I-NUMERIC-EDIT-AREA                                 
02051                                                                   INKCS003
02052       IF  DP010I-ERROR-DETECTED                                   INKCS003
02053           SET PS-ERROR                                            INKCS003
02054               ASC-PR-AMT-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)         INKCS003
02055                                 TO  TRUE                          INKCS003
02056       ELSE                                                        INKCS003
02057           MOVE DP010I-NUMERIC-FIELD                               INKCS003
02058                                 TO  ASC-INV-PR-AMT                INKCS003
02059                                    (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS003
02060           IF  DP010I-NUMERIC-FIELD GREATER PC-500                 INKCS003
02061               SET PS-ERROR                                        INKCS003
02062                   ASC-PR-AMT-E500  (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS003
02063                                 TO  TRUE                          INKCS003
02064           ELSE                                                    INKCS003
02065               IF  DP010I-NUMERIC-FIELD EQUAL ZERO                 INKCS003
02066                   SET PS-ERROR                                    INKCS003
02067                       ASC-PR-AMT-ZERO  (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02068                                 TO  TRUE                          INKCS003
02069               ELSE                                                INKCS003
02070                  IF  DP010I-NUMERIC-FIELD LESS PC-20-CENTS        INKCS003
02071                      SET ASC-PR-AMT-E20(ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02072                                    TO  TRUE                       INKCS003
02073                  ELSE                                             INKCS003
02074                      SET ASC-PR-AMT-OK (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02075                                 TO  TRUE                          INKCS003
02076                  END-IF                                           INKCS003
02077               END-IF                                              INKCS003
02078           END-IF                                                  INKCS003
02079       END-IF.                                                     INKCS003
02080 /                                                                 INKCS003
02081 *----------------------------------------------------------------*INKCS003
02082 *    INITIALIZE BOTH BLOCKS IN THE ARRAY.                        *INKCS003
02083 *    INITIALIZE ALL COUNTERS AND FLAGS.                          *INKCS003
02084 *    READ THE DATA BASE FOR THE 1ST TIME AND FILL UP THE ARRAY.  *INKCS003
02085 *    DISPLAY THE PANEL TO USER.                                  *INKCS003
02086 *----------------------------------------------------------------*INKCS003
02087                                                                   INKCS003
02088  4000-BUILD-INITIAL-PANEL.                                        INKCS003
02089                                                                   INKCS003
02090      PERFORM 5120-DELETE-ASC-LISTQ.                               INKCS003
02091      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS003
02092                                  ASC-BLOCK-ENTRIES (2).           INKCS003
02093      MOVE +1                 TO ASC-BLK-SUB.                      INKCS003
02094      SET ASC-ITEMS-LEFT-ON-DB                                     INKCS003
02095                              TO TRUE.                             INKCS003
02096                                                                   INKCS003
02097      MOVE +0                 TO ASC-ITEM-SUB                      INKCS003
02098                                 ASC-NUMBER-OF-ITEMS-READ          INKCS003
02099                                 ASC-NUMBER-OF-SELECTED-ITEMS      INKCS003
02100                                 ASC-HIGHEST-BLOCK-WRITTEN         INKCS003
02101                                 ASC-START-OF-SELECTED-RANGE       INKCS003
02102                                 ASC-END-OF-SELECTED-RANGE         INKCS003
02103                                 ASC-ITEM-AT-TOP-OF-PANEL.         INKCS003
02104      PERFORM 4100-PREPARE-CURSOR.                                 INKCS003
02105                                                                   INKCS003
02106      PERFORM 4010-OPEN-CURSOR.                                    INKCS003
02107                                                                   INKCS003
02108      PERFORM 4300-READ-ITEMS-FROM-DB.                             INKCS003
02109                                                                   INKCS003
02110      PERFORM 4020-CLOSE-CURSOR.                                   INKCS003
02111                                                                   INKCS003
02112      MOVE +1                   TO  PV-TOP-ITEM.                   INKCS003
02113      MOVE SPACE                TO  ASC-START-ITEM-REQUEST.        INKCS003
02114      IF  ASC-NUMBER-OF-ITEMS-READ = ZERO                          INKCS003
02116          SET DP020-FK-RETURN (DP020-SRC-AID)                      INKCS003
02117              DP020-MSG-FATAL   TO  TRUE                           INKCS003
02118          MOVE PC-TSYMSG-00050  TO  DP020-MSG-NUMBER               INKCS003
02119      ELSE                                                         INKCS003
W21732         MOVE -1               TO MR-CSKUL   (1)                  INKCS003
02121          SET DP030-SET-CURSOR-APPL-1                              INKCS003
02122                                TO TRUE                            INKCS003
02123          PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                     INKCS003
02124      END-IF.                                                      INKCS003
02125 /                                                                 INKCS003
02126 *----------------------------------------------------------------*INKCS003
02127 * OPEN THE CURSOR.                                               *INKCS003
02128 *----------------------------------------------------------------*INKCS003
02129                                                                   INKCS003
02130  4010-OPEN-CURSOR.                                                INKCS003
02131                                                                   INKCS003
W26600         MOVE ASC-KEY-STORE-NBR      TO PV-STORE-NBR-X.                   
W26600         MOVE PV-STORE-NBR-9         TO INVDTL-LOC-NBR.                   
W21732         MOVE ASC-KEY-AREA-NBR       TO INVDTL-AREA-NBR.                  
W21732         MOVE ASC-KEY-LINE-NBR       TO INVDTL-SCAN-LINE-NBR.             
W21732         MOVE PC-Y                   TO INVDTL-INVALID-ITM-IND.           
W21732                                                                          
02132          EXEC SQL                                                 INKCS003
02133               OPEN LINE-CSR                                       INKCS003
02134          END-EXEC.                                                INKCS003
02135                                                                   INKCS003
02136          EVALUATE TRUE                                            INKCS003
02137              WHEN SQLCODE = ZERO                                  INKCS003
02138              WHEN SQLCODE = -904                                  INKCS003
02139              WHEN SQLCODE = -913                                  INKCS003
02140                  CONTINUE                                         INKCS003
02141              WHEN SQLWARN0 NOT = SPACES                           INKCS003
02142              WHEN SQLCODE < ZERO                                  INKCS003
02143              WHEN SQLCODE > ZERO                                  INKCS003
02144                  MOVE '4010-OPEN-CURSOR'                          INKCS003
02145                                    TO DP013-PARAGRAPH             INKCS003
02146                  MOVE 'OPEN LINE CURSOR - '                       INKCS003
02147                                    TO DP013-MESSAGE-TEXT (1)      INKCS003
02148                  MOVE SQLCA        TO DP013-SQLCA                 INKCS003
02149                  MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)    INKCS003
02150                  SET DP013-DB2-ABEND                              INKCS003
02151                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS003
02152                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS003
02153          END-EVALUATE.                                            INKCS003
02154 /                                                                 INKCS003
02155 *----------------------------------------------------------------*INKCS003
02156 *                                                                *INKCS003
02157 * CLOSE THE LINE CURSOR.                                         *INKCS003
02158 *                                                                *INKCS003
02159 *----------------------------------------------------------------*INKCS003
02160                                                                   INKCS003
02161  4020-CLOSE-CURSOR.                                               INKCS003
02162                                                                   INKCS003
02163      EXEC SQL                                                     INKCS003
02164           CLOSE LINE-CSR                                          INKCS003
02165      END-EXEC.                                                    INKCS003
02166                                                                   INKCS003
02167      EVALUATE TRUE                                                INKCS003
02168          WHEN SQLCODE = ZERO                                      INKCS003
02169              CONTINUE                                             INKCS003
02170          WHEN SQLWARN0 NOT = SPACES                               INKCS003
02171          WHEN SQLCODE < ZERO                                      INKCS003
02172          WHEN SQLCODE > ZERO                                      INKCS003
02173              MOVE '4020-CLOSE-CURSOR'                             INKCS003
02174                                TO DP013-PARAGRAPH                 INKCS003
02175              MOVE 'CLOSE LINE CURSOR '                            INKCS003
02176                                TO DP013-MESSAGE-TEXT (1)          INKCS003
02177              MOVE SQLCA        TO DP013-SQLCA                     INKCS003
02178              MOVE 'TINVDTL '   TO DP013-DB2-TABLE-NAME (1)        INKCS003
02179              SET DP013-DB2-ABEND                                  INKCS003
02180                  DP013-XCTL-DISPLAY-RESTART TO TRUE               INKCS003
02181              PERFORM DP013-0000-PROCESS-ABEND                     INKCS003
02182      END-EVALUATE.                                                INKCS003
02183 /                                                                 INKCS003
02184 *----------------------------------------------------------------*INKCS003
02185 *    SET UP THE HOST VARIABLES.                                  *INKCS003
02186 *----------------------------------------------------------------*INKCS003
02187                                                                   INKCS003
02188  4100-PREPARE-CURSOR.                                             INKCS003
02189                                                                   INKCS003
02190      IF  ASC-USE-SELECTION-QUEUE                                  INKCS003
02191          CONTINUE                                                 INKCS003
02192      ELSE                                                         INKCS003
02193          MOVE IN001-AREA-NBR   TO  ASC-KEY-AREA-NBR               INKCS003
02194      END-IF.                                                      INKCS003
02195      MOVE ZERO                 TO  ASC-KEY-LINE-NBR.              INKCS003
02196                                                                   INKCS003
02197 /                                                                 INKCS003
02198 *----------------------------------------------------------------*INKCS003
02199 *    READ THE ITEMS FROM THE DATA BASE AND MOVE THEM INTO THE    *INKCS003
02200 *    BLOCKS IN THE ARRAY.  WHENEVER BOTH BLOCKS BECOME FULL,     *INKCS003
02201 *    WRITE OUT THE 1ST BLOCK TO TEMPORARY STORAGE, MOVE THE 2ND  *INKCS003
02202 *    BLOCK IN THE ARRAY TO THE 1ST BLOCK, AND INITIALIZE THE     *INKCS003
02203 *    2ND BLOCK MAKING IT AVAILABLE TO BE USED.                   *INKCS003
02204 *                                                                *INKCS003
02205 *    DUE TO THE SIZE OF THE TABLE, AND THE ORDER IN WHICH THE    *INKCS003
02206 *    ROWS ARE SORTED, THE ENTIRE TABLE IS READ INTO THE PROGRAM  *INKCS003
02207 *    UPON INITIALIZATION.                                        *INKCS003
02208 *----------------------------------------------------------------*INKCS003
02209                                                                   INKCS003
02210  4300-READ-ITEMS-FROM-DB.                                         INKCS003
02211                                                                   INKCS003
02212      PERFORM                                                      INKCS003
02213          VARYING PV-READ-COUNT                                    INKCS003
02214          FROM 1 BY 1                                              INKCS003
02215          UNTIL ((PV-READ-COUNT > PC-MAX-ITEMS)                    INKCS003
02216             OR (ASC-NO-ITEMS-LEFT-ON-DB))                         INKCS003
02217                                                                   INKCS003
02218          PERFORM 4310-FETCH                                       INKCS003
02219                                                                   INKCS003
02220          IF  ASC-ITEMS-LEFT-ON-DB                                 INKCS003
02221              IF  ASC-ITEM-SUB < PC-ITEMS-PER-PANEL                INKCS003
02222                  ADD +1        TO ASC-ITEM-SUB                    INKCS003
02223              ELSE                                                 INKCS003
02224                  ADD +1        TO ASC-BLK-SUB                     INKCS003
02225                  MOVE +1       TO ASC-ITEM-SUB                    INKCS003
02226                                                                   INKCS003
02227                  IF  ASC-BLK-SUB > PC-MAX-BLOCKS-IN-ARRAY         INKCS003
02228                      MOVE +1   TO ASC-BLK-SUB                     INKCS003
02229                      SET ASC-BLOCK-MODIFIED (1)                   INKCS003
02230                                TO TRUE                            INKCS003
02231                      PERFORM 5100-WRITE-TEMP-STORAGE              INKCS003
02232                      MOVE ASC-BLOCK-ENTRIES                       INKCS003
02233                                (PC-MAX-BLOCKS-IN-ARRAY)           INKCS003
02234                                TO ASC-BLOCK-ENTRIES(ASC-BLK-SUB)  INKCS003
02235                      ADD +1    TO ASC-BLK-SUB                     INKCS003
02236                      INITIALIZE ASC-BLOCK-ENTRIES(ASC-BLK-SUB)    INKCS003
02237                      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB)      INKCS003
02238                                TO TRUE                            INKCS003
02239                  END-IF                                           INKCS003
02240              END-IF                                               INKCS003
02241                                                                   INKCS003
02242              ADD +1            TO ASC-NUMBER-OF-ITEMS-READ        INKCS003
02243              MOVE ASC-NUMBER-OF-ITEMS-READ                        INKCS003
02244                                    TO ASC-LIST-ITEM-NUMBER        INKCS003
02245                                       (ASC-BLK-SUB ASC-ITEM-SUB)  INKCS003
02246              MOVE INVDTL-INV-LINE-NBR                             INKCS003
02247                                    TO  ASC-INV-LINE-NBR           INKCS003
02248                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02249              MOVE INVDTL-SCAN-LINE-NBR                            INKCS003
02250                                    TO  ASC-SCAN-LINE-NBR          INKCS003
02251                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
W21732             MOVE INVDTL-REC-SKU-NBR  TO  ASC-ISKU                INKCS003
W21732                                       (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02261              MOVE INVDTL-REC-UPC-NBR                              INKCS003
02262                                    TO  ASC-IUPC                   INKCS003
02263                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02264              MOVE INVDTL-REC-QTY   TO  ASC-INV-QTY                INKCS003
02265                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02266              MOVE INVDTL-REC-PR-AMT TO ASC-INV-PR-AMT             INKCS003
02267                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02268                                        ASC-ORIG-INV-PR-AMT        INKCS003
02269                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02270              MOVE INVDTL-INVALID-ITM-IND                          INKCS003
02271                                    TO  ASC-INVALID-ITM-IND        INKCS003
02272                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02273              MOVE INVDTL-INVALID-REC-IND                          INKCS003
02274                                    TO  ASC-INVALID-REC-IND        INKCS003
02275                                        (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02276                                                                   INKCS003
02277              SET ASC-PROTECT-PR-AMT    (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02278                  ASC-SKU-NOT-ENTERED   (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02279                  ASC-UPC-NOT-ENTERED   (ASC-BLK-SUB ASC-ITEM-SUB) INKCS003
02280                                    TO  TRUE                       INKCS003
02281          END-IF                                                   INKCS003
02282      END-PERFORM.                                                 INKCS003
02283                                                                   INKCS003
02284      IF  ASC-NUMBER-OF-ITEMS-READ > ZERO                          INKCS003
02285          MOVE +1               TO ASC-BLK-SUB                     INKCS003
02286          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS003
02287          ADD +1                TO ASC-BLK-SUB                     INKCS003
02288          PERFORM 5100-WRITE-TEMP-STORAGE                          INKCS003
02289                                                                   INKCS003
02290          MOVE 1                TO ASC-BLK-SUB                     INKCS003
02291                                   PV-BLOCK-NUMBER                 INKCS003
02292          PERFORM 4410-READ-TEMP-STORAGE                           INKCS003
02293          IF  ASC-HIGHEST-BLOCK-WRITTEN GREATER THAN 1             INKCS003
02294              MOVE 2            TO ASC-BLK-SUB                     INKCS003
02295                                   PV-BLOCK-NUMBER                 INKCS003
02296              PERFORM 4410-READ-TEMP-STORAGE                       INKCS003
02297          END-IF                                                   INKCS003
02298      END-IF.                                                      INKCS003
02299                                                                   INKCS003
02300      MOVE INVDTL-SCAN-LINE-NBR TO  ASC-KEY-LINE-NBR.              INKCS003
02301 /                                                                 INKCS003
02302 *----------------------------------------------------------------*INKCS003
02303 *    FETCH THE CURSOR.                                           *INKCS003
02304 *----------------------------------------------------------------*INKCS003
02305                                                                   INKCS003
02306  4310-FETCH.                                                      INKCS003
02307                                                                   INKCS003
02308      EXEC SQL                                                     INKCS003
02309           FETCH                                                   INKCS003
02310                 LINE-CSR                                          INKCS003
02311           INTO                                                    INKCS003
02312                :INVDTL-REC-UPC-NBR                                INKCS003
W21732             , :INVDTL-REC-SKU-NBR                                INKCS003
02316              , :INVDTL-REC-QTY                                    INKCS003
02317              , :INVDTL-REC-PR-AMT                                 INKCS003
02318              , :INVDTL-INV-LINE-NBR                               INKCS003
02319              , :INVDTL-SCAN-LINE-NBR                              INKCS003
02320              , :INVDTL-INVALID-ITM-IND                            INKCS003
02321              , :INVDTL-INVALID-REC-IND                            INKCS003
02322      END-EXEC.                                                    INKCS003
02323                                                                   INKCS003
02324      EVALUATE TRUE                                                INKCS003
02325          WHEN SQLCODE = ZERO                                      INKCS003
02326          WHEN SQLCODE = +100                                      INKCS003
02327               CONTINUE                                            INKCS003
02328          WHEN SQLWARN0 NOT = SPACE                                INKCS003
02329          WHEN SQLCODE NOT = ZERO                                  INKCS003
02330               MOVE '4310-FETCH-CURSOR'                            INKCS003
02331                                TO DP013-PARAGRAPH                 INKCS003
02332               MOVE 'FETCH LINE CURSOR'                            INKCS003
02333                                TO DP013-MESSAGE-TEXT (1)          INKCS003
02334               MOVE SQLCA       TO DP013-SQLCA                     INKCS003
02335               SET DP013-DB2-ABEND                                 INKCS003
02336                                TO TRUE                            INKCS003
02337               PERFORM DP013-0000-PROCESS-ABEND                    INKCS003
02338      END-EVALUATE.                                                INKCS003
02339                                                                   INKCS003
02340      IF  SQLCODE EQUAL +100                                       INKCS003
02341          SET ASC-NO-ITEMS-LEFT-ON-DB                              INKCS003
02342                                    TO TRUE                        INKCS003
02343      END-IF.                                                      INKCS003
02344 /                                                                 INKCS003
02345 *----------------------------------------------------------------*INKCS003
02346 * MOVE THE DISPLAYED FIELDS FROM THE COMMAREA TO THE SCREEN.     *INKCS003
02347 *----------------------------------------------------------------*INKCS003
02348                                                                   INKCS003
02349  4400-MOVE-COMMAREA-TO-SCREEN.                                    INKCS003
02350                                                                   INKCS003
W26600     MOVE IN001-STORE-NBR          TO  ASTRNBRO.                  INKCS003
02351      MOVE IN001-STORE-NAME         TO  ASTRNAMO.                  INKCS003
02352      MOVE ASC-KEY-AREA-NBR         TO  AAREAO.                    INKCS003
02353                                                                   INKCS003
J28545***  IF  (((PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1))             INKCS003
J28545***      AND (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1)))          INKCS003
J28545***      OR  (PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1)))          INKCS003
J28545                                                                          
J28545*   IF THE FIRST TWO CONDITIONS ARE TRUE                                  
J28545*   -OR- THE THIRD CONDITION IS TRUE                                      
J28545                                                                          
J28545     IF  ( (PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (1 1)              INKCS003
J28545         AND PV-TOP-ITEM > ASC-LIST-ITEM-NUMBER (2 1) )           INKCS003
J28545                                                                          
J28545         OR  PV-TOP-ITEM < ASC-LIST-ITEM-NUMBER (1 1) )           INKCS003
02357              MOVE +1    TO ASC-BLK-SUB                            INKCS003
02358              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS003
02359              ADD +1     TO ASC-BLK-SUB                            INKCS003
02360              PERFORM 5100-WRITE-TEMP-STORAGE                      INKCS003
02361              DIVIDE PV-TOP-ITEM BY PC-ITEMS-PER-PANEL             INKCS003
02362                  GIVING PV-BLOCK-NUMBER                           INKCS003
02363                  REMAINDER PV-REMAINDER                           INKCS003
02364              IF  PV-REMAINDER > ZERO                              INKCS003
02365                  ADD +1 TO PV-BLOCK-NUMBER                        INKCS003
02366              END-IF                                               INKCS003
02367              MOVE +1    TO ASC-BLK-SUB                            INKCS003
02368              PERFORM 4410-READ-TEMP-STORAGE                       INKCS003
02369              ADD +1     TO ASC-BLK-SUB                            INKCS003
02370                            PV-BLOCK-NUMBER                        INKCS003
02371              PERFORM 4410-READ-TEMP-STORAGE                       INKCS003
02372      END-IF.                                                      INKCS003
02373                                                                   INKCS003
02374      MOVE +1            TO ASC-BLK-SUB.                           INKCS003
02375      COMPUTE ASC-ITEM-SUB =                                       INKCS003
02376          ((PV-TOP-ITEM - ASC-LIST-ITEM-NUMBER (1 1))              INKCS003
02377          + PC-ITEMS-PER-PANEL).                                   INKCS003
02378                                                                   INKCS003
02379      IF  ASC-ITEM-SUB > PC-ITEMS-PER-PANEL                        INKCS003
02380          ADD +1         TO ASC-BLK-SUB                            INKCS003
02381          SUBTRACT PC-ITEMS-PER-PANEL FROM ASC-ITEM-SUB            INKCS003
02382      END-IF.                                                      INKCS003
02383                                                                   INKCS003
02384      COMPUTE ASC-ITEM-AT-BOT-OF-PANEL =                           INKCS003
02385          ((PV-TOP-ITEM + PC-ITEMS-PER-PANEL) - 1).                INKCS003
02386      PERFORM 4420-FORMAT-LIST-LINES                               INKCS003
02387          VARYING PV-SUB                                           INKCS003
02388          FROM PC-ITEMS-PER-PANEL BY -1                            INKCS003
02389          UNTIL PV-SUB < +1.                                       INKCS003
02390                                                                   INKCS003
02391      IF  NOT DP030-SET-CURSOR-APPL-1                              INKCS003
02392          PERFORM                                                  INKCS003
02393              VARYING PV-SUB                                       INKCS003
02394              FROM 1 BY 1                                          INKCS003
02395              UNTIL  PV-SUB GREATER PC-ITEMS-PER-PANEL             INKCS003
W21732                IF (MR-CSKUC  (PV-SUB) NOT EQUAL DP015-BLUE       INKCS003
W21732                AND MR-CSKUA  (PV-SUB) NOT = DP015-ASK-DRK-OFF)   INKCS003
W21732                    MOVE -1        TO  MR-CSKUL  (PV-SUB)         INKCS003
02399                     ADD PC-ITEMS-PER-PANEL                        INKCS003
02400                                    TO  PV-SUB                     INKCS003
02401                     SET DP030-SET-CURSOR-APPL-1                   INKCS003
02402                                    TO  TRUE                       INKCS003
02403                 END-IF                                            INKCS003
02404          END-PERFORM                                              INKCS003
02405          IF  NOT DP030-SET-CURSOR-APPL-1                          INKCS003
02406              SET DP030-SET-CURSOR-TRAILER                         INKCS003
02407                                    TO  TRUE                       INKCS003
02408          END-IF                                                   INKCS003
02409      END-IF.                                                      INKCS003
02410                                                                   INKCS003
02411      COMPUTE ASC-ITEMS-DISPLAYED =                                INKCS003
02412          ((ASC-ITEM-AT-BOT-OF-PANEL - PV-TOP-ITEM) + 1).          INKCS003
02413      MOVE PV-TOP-ITEM              TO ASC-ITEM-AT-TOP-OF-PANEL    INKCS003
02414                                       ASTRTITO.                   INKCS003
02415      MOVE ASC-ITEM-AT-BOT-OF-PANEL TO AENDITO.                    INKCS003
02416      MOVE ASC-NUMBER-OF-ITEMS-READ TO ATOTITO.                    INKCS003
02417                                                                   INKCS003
02418      IF  PS-SOFT-ABORT                                            INKCS003
02419          MOVE PC-TSYMSG-00254      TO  DP020-MSG-NUMBER           INKCS003
02420      END-IF.                                                      INKCS003
02421                                                                   INKCS003
02422      IF  DP020-MSG-NUMBER-X = SPACE                               INKCS003
02423          IF  ASC-ITEM-AT-BOT-OF-PANEL < ASC-NUMBER-OF-ITEMS-READ  INKCS003
02424 *            --- MORE ITEMS TO DISPLAY ----                       INKCS003
02425              MOVE PC-TSYMSG-00279  TO DP020-MSG-NUMBER            INKCS003
02426              SET DP020-MSG-INFORMATIONAL TO TRUE                  INKCS003
02427          END-IF                                                   INKCS003
02428      END-IF.                                                      INKCS003
02429                                                                   INKCS003
02430 *----------------------------------------------------------------*INKCS003
02431 *    READ A BLOCK FROM TEMPORARY STORAGE INTO THE ARRAY.         *INKCS003
02432 *----------------------------------------------------------------*INKCS003
02433                                                                   INKCS003
02434  4410-READ-TEMP-STORAGE.                                          INKCS003
02435                                                                   INKCS003
02436      EXEC CICS                                                    INKCS003
02437          READQ TS                                                 INKCS003
02438              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
02439              INTO  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS003
02440              ITEM  (PV-BLOCK-NUMBER)                              INKCS003
02441              RESP  (PV-CICS-RESP)                                 INKCS003
02442      END-EXEC.                                                    INKCS003
02443                                                                   INKCS003
02444      IF  ((PV-CICS-RESP = DFHRESP (NORMAL))                       INKCS003
02445        OR (PV-CICS-RESP = DFHRESP (ITEMERR)))                     INKCS003
02446          CONTINUE                                                 INKCS003
02447      ELSE                                                         INKCS003
02448          SET DP013-CICS-ABEND     TO TRUE                         INKCS003
02449          SET DP013-NO-ROLLBACK    TO TRUE                         INKCS003
02450          MOVE '4410-READ-TEMP-STORAGE'                            INKCS003
02451                                   TO DP013-PARAGRAPH              INKCS003
02452          MOVE 'ERROR TRYING TO READ FROM TEMP STORAGE QUEUE'      INKCS003
02453                                   TO DP013-MESSAGE-TEXT(1)        INKCS003
02454          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
02455      END-IF.                                                      INKCS003
02456                                                                   INKCS003
02457      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS003
02458                                                                   INKCS003
02459      IF  PV-CICS-RESP = DFHRESP (ITEMERR)                         INKCS003
02460          INITIALIZE ASC-BLOCK-ENTRIES (ASC-BLK-SUB)               INKCS003
02461      END-IF.                                                      INKCS003
02462 /                                                                 INKCS003
02463 *----------------------------------------------------------------*INKCS003
02464 *    PROCESS THE ITEMS IN THE ARRAY AND MOVE THEM TO THE MAP.    *INKCS003
02465 *----------------------------------------------------------------*INKCS003
02466                                                                   INKCS003
02467  4420-FORMAT-LIST-LINES.                                          INKCS003
02468                                                                   INKCS003
02469      IF  ASC-ITEM-AT-BOT-OF-PANEL > ASC-NUMBER-OF-ITEMS-READ      INKCS003
02470          SUBTRACT +1 FROM ASC-ITEM-AT-BOT-OF-PANEL                INKCS003
W21732         MOVE DP015-ASK-DRK-OFF  TO MR-CSKUA        (PV-SUB)      INKCS003
02474                                     MR-CUPCA        (PV-SUB)      INKCS003
02475                                     MR-INV-PR-AMTA  (PV-SUB)      INKCS003
W21732         MOVE DP015-HL-OFF       TO MR-CSKUH        (PV-SUB)      INKCS003
02479                                     MR-CUPCH        (PV-SUB)      INKCS003
02480                                     MR-INV-PR-AMTH  (PV-SUB)      INKCS003
W21732         MOVE SPACE              TO MR-CSKU         (PV-SUB)      INKCS003
02484                                     MR-CUPC-X       (PV-SUB)      INKCS003
02485                                     MR-LINE-X       (PV-SUB)      INKCS003
W21732                                    MR-ISKU         (PV-SUB)      INKCS003
02489                                     MR-IUPC-X       (PV-SUB)      INKCS003
02490                                     MR-INV-QTY-X    (PV-SUB)      INKCS003
02491                                     MR-INV-PR-AMT-X (PV-SUB)      INKCS003
02492      ELSE                                                         INKCS003
02493          IF  PV-TOP-ITEM NOT = ASC-ITEM-AT-TOP-OF-PANEL           INKCS003
02494              MOVE ASC-SCAN-LINE-NBR (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
02495                                  TO MR-LINE          (PV-SUB)     INKCS003
W21732             MOVE ASC-CSKU          (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
W21732                                 TO MR-CSKU          (PV-SUB)     INKCS003
02502              IF  ASC-CUPC (ASC-BLK-SUB ASC-ITEM-SUB) NUMERIC      INKCS003
02503                  MOVE ASC-CUPC      (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
02504                                  TO MR-CUPC          (PV-SUB)     INKCS003
02505              END-IF                                               INKCS003
W21732             MOVE ASC-ISKU          (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
W21732                                 TO MR-ISKU          (PV-SUB)     INKCS003
02512              MOVE ASC-IUPC          (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
02513                                  TO MR-IUPC          (PV-SUB)     INKCS003
02514              MOVE ASC-INV-QTY       (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
02515                                  TO MR-INV-QTY       (PV-SUB)     INKCS003
J28545             IF  ASC-PR-AMT-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS003
X28545              OR ASC-INV-PR-AMT   (ASC-BLK-SUB ASC-ITEM-SUB)      INKCS003
02518                  NOT NUMERIC                                      INKCS003
02519                  MOVE ASC-INV-PR-AMT-X  (ASC-BLK-SUB ASC-ITEM-SUB)INKCS003
02520                                  TO MR-INV-PR-AMT-X  (PV-SUB)     INKCS003
02521              ELSE                                                 INKCS003
02522                  MOVE ASC-INV-PR-AMT    (ASC-BLK-SUB ASC-ITEM-SUB)INKCS003
02523                                  TO MR-INV-PR-AMT    (PV-SUB)     INKCS003
02524              END-IF                                               INKCS003
02525                                                                   INKCS003
02526              IF  ASC-PROTECT-ENTRY  (ASC-BLK-SUB ASC-ITEM-SUB)    INKCS003
02527                  MOVE DP015-ASK-NOR-OFF                           INKCS003
W21732                                   TO MR-CSKUA   (PV-SUB)         INKCS003
02531                                       MR-CUPCA   (PV-SUB)         INKCS003
02532                                       MR-INV-PR-AMTA(PV-SUB)      INKCS003
W21732                 MOVE DP015-BLUE   TO MR-CSKUC   (PV-SUB)         INKCS003
02537                                       MR-CUPCC   (PV-SUB)         INKCS003
02538                                       MR-INV-PR-AMTC(PV-SUB)      INKCS003
W21732                 MOVE DP015-HL-OFF TO MR-CSKUH   (PV-SUB)         INKCS003
02543                                       MR-CUPCH   (PV-SUB)         INKCS003
02544                                       MR-INV-PR-AMTH(PV-SUB)      INKCS003
02545              ELSE                                                 INKCS003
J28545                 PERFORM 4430-EVALUATE-PRICE                              
J28545                 PERFORM 4440-EVALUATE-UPC                                
J28545                 PERFORM 4450-EVALUATE-SKU                                
02687              END-IF                                               INKCS003
02688          END-IF                                                   INKCS003
02689      END-IF.                                                      INKCS003
02690                                                                   INKCS003
02691      SUBTRACT +1 FROM ASC-ITEM-SUB                                INKCS003
02692      IF  ASC-ITEM-SUB < + 1                                       INKCS003
02693          MOVE PC-ITEMS-PER-PANEL TO ASC-ITEM-SUB                  INKCS003
02694          MOVE +1                 TO ASC-BLK-SUB                   INKCS003
02695      END-IF.                                                      INKCS003
J28545*----------------------------------------------------------------         
J28545*  DETERMINE WHAT ATTRIBUTES TO SET FOR PRICE                             
J28545*----------------------------------------------------------------         
J28545 4430-EVALUATE-PRICE.                                                     
J28545     EVALUATE TRUE                                                        
J28545         WHEN ASC-ENTER-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545             MOVE DP015-UNP-ALP-NOR-OFF                                   
J28545                           TO MR-INV-PR-AMTA(PV-SUB)                      
J28545             MOVE DP015-RED                                               
J28545                           TO MR-INV-PR-AMTC(PV-SUB)                      
J28545             MOVE DP015-REVERSE                                           
J28545                           TO MR-INV-PR-AMTH(PV-SUB)                      
J28545             MOVE -1       TO MR-INV-PR-AMTL(PV-SUB)                      
J28545             SET DP030-SET-CURSOR-APPL-1                                  
J28545                           TO TRUE                                        
J28545             MOVE PC-TSYMSG-00007                                         
J28545                           TO  DP020-MSG-NUMBER                           
J28545             SET DP020-MSG-FATAL                                          
J28545                 PS-ERROR  TO  TRUE                                       
J28545                                                                          
J28545         WHEN ASC-PR-AMT-OK    (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545             MOVE DP015-UNP-ALP-NOR-OFF                                   
J28545                           TO MR-INV-PR-AMTA(PV-SUB)                      
J28545             MOVE DP015-GREEN                                             
J28545                           TO MR-INV-PR-AMTC(PV-SUB)                      
J28545             MOVE DP015-UNDERLINE                                         
J28545                           TO MR-INV-PR-AMTH(PV-SUB)                      
J28545                                                                          
J28545         WHEN ASC-PROTECT-PR-AMT(ASC-BLK-SUB ASC-ITEM-SUB)                
J28545             MOVE DP015-ASK-NOR-OFF                                       
J28545                           TO MR-INV-PR-AMTA(PV-SUB)                      
J28545             MOVE DP015-BLUE                                              
J28545                           TO MR-INV-PR-AMTC(PV-SUB)                      
J28545             MOVE DP015-HL-OFF                                            
J28545                           TO MR-INV-PR-AMTH(PV-SUB)                      
J28545                                                                          
J28545         WHEN ASC-PR-AMT-ERROR (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545         WHEN ASC-PR-AMT-E500  (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545         WHEN ASC-PR-AMT-E20   (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545         WHEN ASC-PR-AMT-ZERO  (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545             IF  ASC-PR-AMT-ERROR                                         
J28545                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
X28545*                IF ASC-INV-PR-AMT (ASC-BLK-SUB ASC-ITEM-SUB)     INKCS003
X28545*                              NOT NUMERIC                        INKCS003
J28545                     MOVE PC-TSYMSG-00008                                 
J28545                           TO  DP020-MSG-NUMBER                           
X28545*                END-IF                                                   
J28545             ELSE                                                         
J28545                 IF  ASC-PR-AMT-ZERO                                      
J28545                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545                     MOVE PC-TSYMSG-00010                                 
J28545                           TO  DP020-MSG-NUMBER                           
J28545                 ELSE                                                     
J28545                     IF  ASC-PR-AMT-E20                                   
J28545                               (ASC-BLK-SUB ASC-ITEM-SUB)                 
J28545                         MOVE PC-TSYMSG-00688                             
J28545                            TO  DP020-MSG-NUMBER                          
J28545                     ELSE                                                 
J28545                         MOVE PC-TSYMSG-00544                             
J28545                           TO  DP020-MSG-NUMBER                           
J28545                     END-IF                                               
J28545                 END-IF                                                   
J28545             END-IF                                                       
J28545                                                                          
J28545             MOVE DP015-UNP-ALP-NOR-OFF                                   
J28545                           TO MR-INV-PR-AMTA(PV-SUB)                      
J28545             MOVE DP015-RED                                               
J28545                           TO MR-INV-PR-AMTC(PV-SUB)                      
J28545             MOVE DP015-REVERSE                                           
J28545                           TO MR-INV-PR-AMTH(PV-SUB)                      
J28545             MOVE -1       TO MR-INV-PR-AMTL(PV-SUB)                      
J28545             SET DP030-SET-CURSOR-APPL-1                                  
J28545                 DP020-MSG-FATAL                                          
J28545                           TO TRUE                                        
J28545                                                                          
J28545     END-EVALUATE.                                                        
J28545*----------------------------------------------------------------         
J28545*  DETERMINE WHAT ATTRIBUTES TO SET FOR UPC                               
J28545*----------------------------------------------------------------         
J28545 4440-EVALUATE-UPC.                                                       
J28545                                                                          
J28545     IF (ASC-UPC-CHKDIG (ASC-BLK-SUB ASC-ITEM-SUB)                        
J28545     OR  ASC-UPC-NOTFND (ASC-BLK-SUB ASC-ITEM-SUB)                        
J28545     OR  ASC-UPC-NOTFND2(ASC-BLK-SUB ASC-ITEM-SUB)                        
J28545     OR  ASC-UPC-ERROR  (ASC-BLK-SUB ASC-ITEM-SUB)                        
32318      OR  ASC-UPC-PLU-ERR (ASC-BLK-SUB ASC-ITEM-SUB))                      
J28545         MOVE DP015-UNP-ALP-NOR-OFF                                       
J28545                       TO MR-CUPCA   (PV-SUB)                             
J28545         MOVE DP015-RED                                                   
J28545                       TO MR-CUPCC   (PV-SUB)                             
J28545         MOVE DP015-REVERSE                                               
J28545                       TO MR-CUPCH   (PV-SUB)                             
J28545         MOVE -1       TO MR-CUPCL   (PV-SUB)                             
J28545         SET DP030-SET-CURSOR-APPL-1                                      
J28545             DP020-MSG-FATAL                                              
J28545                       TO TRUE                                            
J28545         EVALUATE TRUE                                                    
J28545             WHEN ASC-UPC-ERROR                                           
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                 IF ASC-SKU-NOT-ENTERED                                   
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                     MOVE PC-TSYMSG-00005                                 
J28545                       TO  DP020-MSG-NUMBER                               
J28545                 ELSE                                                     
J28545                     MOVE PC-TSYMSG-00547                                 
J28545                       TO  DP020-MSG-NUMBER                               
J28545                 END-IF                                                   
J28545             WHEN ASC-UPC-CHKDIG                                          
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                 MOVE ASC-CHKDIG                                          
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                       TO  DP020-MSG-TEXT                                 
J28545                 MOVE PC-TSYMSG-00474                                     
J28545                       TO  DP020-MSG-NUMBER                               
J28545             WHEN ASC-UPC-NOTFND                                          
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                 MOVE PC-TSYMSG-00100                                     
J28545                       TO  DP020-MSG-NUMBER                               
J28545             WHEN ASC-UPC-NOTFND2                                         
J28545                           (ASC-BLK-SUB ASC-ITEM-SUB)                     
J28545                 MOVE PC-TSYMSG-00005                                     
J28545                       TO  DP020-MSG-NUMBER                               
32318              WHEN ASC-UPC-PLU-ERR                                         
32318                            (ASC-BLK-SUB ASC-ITEM-SUB)                     
32318                  MOVE PC-TSYMSG-03549                                     
32318                        TO  DP020-MSG-NUMBER                               
L28545         END-EVALUATE                                                     
J28545     END-IF.                                                              
J28545*----------------------------------------------------------------         
J28545*  DETERMINE WHAT ATTRIBUTES TO SET FOR SKU                               
J28545*----------------------------------------------------------------         
J28545 4450-EVALUATE-SKU.                                                       
J28545     IF  ASC-SKU-ERROR  (ASC-BLK-SUB ASC-ITEM-SUB)                        
32318       OR ASC-SKU-PLU-ERR (ASC-BLK-SUB ASC-ITEM-SUB)                       
J28545         MOVE DP015-UNP-ALP-NOR-OFF                                       
J28545                       TO MR-CSKUA   (PV-SUB)                             
J28545         MOVE DP015-RED                                                   
J28545                       TO MR-CSKUC   (PV-SUB)                             
J28545         MOVE DP015-REVERSE                                               
J28545                       TO MR-CSKUH   (PV-SUB)                             
J28545         MOVE -1       TO MR-CSKUL   (PV-SUB)                             
J28545         SET DP030-SET-CURSOR-APPL-1                                      
J28545             DP020-MSG-FATAL                                              
J28545                       TO TRUE                                            
J28545         IF  ASC-UPC-ERROR(ASC-BLK-SUB ASC-ITEM-SUB)                      
32318            OR ASC-SKU-PLU-ERR (ASC-BLK-SUB ASC-ITEM-SUB)                  
J28545             CONTINUE                                                     
J28545         ELSE                                                             
J28545             MOVE PC-TSYMSG-00005                                         
J28545                       TO  DP020-MSG-NUMBER                               
J28545         END-IF                                                           
J28545     END-IF.                                                              
02696 /                                                                 INKCS003
02697 *----------------------------------------------------------------*INKCS003
02698 *    HERE, WE WRITE THE CURRENT ASC TO THE QUEUE.  NEXT WE READ  *INKCS003
02699 *    THE TSQUEUE TO UPDATE THE DB2 TABLES.  FINALLY, WE MUST     *INKCS003
02700 *    RELOAD THE ASC WITH THE UPDATED TSQUEUE INFO.               *INKCS003
02701 *----------------------------------------------------------------*INKCS003
02702                                                                   INKCS003
02703  5000-UPDATE-PROCESS.                                             INKCS003
02704                                                                   INKCS003
02705      MOVE +1                   TO  ASC-BLK-SUB.                   INKCS003
02706      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS003
02707      ADD +1                    TO  ASC-BLK-SUB.                   INKCS003
02708      PERFORM 5100-WRITE-TEMP-STORAGE.                             INKCS003
02709                                                                   INKCS003
02710      PERFORM 5200-UPDATE-IN-TSQUEUE                               INKCS003
02711              VARYING PV-ITEM                                      INKCS003
02712              FROM 1 BY 1                                          INKCS003
02713              UNTIL PV-ITEM GREATER ASC-HIGHEST-BLOCK-WRITTEN.     INKCS003
02714                                                                   INKCS003
02715      PERFORM 5500-UPDATE-TINVLOC.                                 INKCS003
02716                                                                   INKCS003
02717      IF  ASC-USE-SELECTION-QUEUE                                  INKCS003
02718          PERFORM 9100-READ-SEL-QUEUE                              INKCS003
02719          SET IN002-UPD (ASC-SEL-SUB)   TO  TRUE                   INKCS003
02720          PERFORM 9200-REWRITE-LIST-QUEUE                          INKCS003
02721      END-IF                                                       INKCS003
02722                                                                   INKCS003
02723      INITIALIZE                  ASC-BLOCK-ENTRIES (1)            INKCS003
02724                                  ASC-BLOCK-ENTRIES (2).           INKCS003
02725                                                                   INKCS003
02726 /                                                                 INKCS003
02727 *----------------------------------------------------------------*INKCS003
02728 *    WRITE A NEW BLOCK FROM THE ARRAY TO TEMPORARY STORAGE.      *INKCS003
02729 *    REWRITE AN EXISITING BLOCK ONLY IF IT HAS BEEN MODIFIED.    *INKCS003
02730 *----------------------------------------------------------------*INKCS003
02731                                                                   INKCS003
02732  5100-WRITE-TEMP-STORAGE.                                         INKCS003
02733                                                                   INKCS003
02734      MOVE ASC-LIST-ITEM-NUMBER(ASC-BLK-SUB 1)                     INKCS003
02735                                    TO PV-LIST-ITEM-NUMBER.        INKCS003
02736      COMPUTE PV-BLOCK-NUMBER =                                    INKCS003
02737          1 + ((PV-LIST-ITEM-NUMBER - 1) / PC-ITEMS-PER-PANEL).    INKCS003
02738                                                                   INKCS003
02739      IF (PV-BLOCK-NUMBER < ASC-HIGHEST-BLOCK-WRITTEN)             INKCS003
02740      OR (PV-BLOCK-NUMBER = ASC-HIGHEST-BLOCK-WRITTEN)             INKCS003
02741          IF  ASC-BLOCK-MODIFIED(ASC-BLK-SUB)                      INKCS003
02742              PERFORM 5105-REWRITE-ASC-LISTQ                       INKCS003
02743          END-IF                                                   INKCS003
02744      ELSE                                                         INKCS003
02745          COMPUTE PV-HIGHEST-BLOCK-WRITTEN =                       INKCS003
02746          ASC-HIGHEST-BLOCK-WRITTEN + 1                            INKCS003
02747          PERFORM 5110-WRITE-ASC-LISTQ                             INKCS003
02748          ADD +1                    TO ASC-HIGHEST-BLOCK-WRITTEN   INKCS003
02749      END-IF.                                                      INKCS003
02750                                                                   INKCS003
02751      SET ASC-BLOCK-NOT-MODIFIED(ASC-BLK-SUB) TO TRUE.             INKCS003
02752                                                                   INKCS003
02753 ******************************************************************INKCS003
02754 ** REWRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-    **INKCS003
02755 ** SPECIFIC COMMAREA.                                           **INKCS003
02756 ******************************************************************INKCS003
02757                                                                   INKCS003
02758  5105-REWRITE-ASC-LISTQ.                                          INKCS003
02759                                                                   INKCS003
02760      EXEC CICS                                                    INKCS003
02761          WRITEQ TS                                                INKCS003
02762              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
02763              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS003
02764              ITEM  (PV-BLOCK-NUMBER)                              INKCS003
02765              REWRITE                                              INKCS003
02766              RESP  (PV-CICS-RESP)                                 INKCS003
02767      END-EXEC.                                                    INKCS003
02768                                                                   INKCS003
02769      IF  PV-CICS-RESP = DFHRESP(NORMAL)                           INKCS003
02770          CONTINUE                                                 INKCS003
02771      ELSE                                                         INKCS003
02772          SET DP013-CICS-ABEND            TO TRUE                  INKCS003
02773          SET DP013-NO-ROLLBACK           TO TRUE                  INKCS003
02774          MOVE '5105-REWRITE-ASC-LISTQ'   TO DP013-PARAGRAPH       INKCS003
02775          MOVE 'ATTEMPTING TO REWRITE LIST TEMP STORAGE QUEUE'     INKCS003
02776                                          TO DP013-MESSAGE-TEXT(1) INKCS003
02777          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
02778      END-IF.                                                      INKCS003
02779                                                                   INKCS003
02780 ******************************************************************INKCS003
02781 ** WRITE THE LIST TEMP STORAGE QUEUE FROM THE APPLICATION-      **INKCS003
02782 ** SPECIFIC COMMAREA.                                           **INKCS003
02783 ******************************************************************INKCS003
02784                                                                   INKCS003
02785  5110-WRITE-ASC-LISTQ.                                            INKCS003
02786                                                                   INKCS003
02787      EXEC CICS                                                    INKCS003
02788          WRITEQ TS                                                INKCS003
02789              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
02790              FROM  (ASC-BLOCK-ENTRIES(ASC-BLK-SUB))               INKCS003
02791              ITEM  (PV-BLOCK-NUMBER)                              INKCS003
02792              RESP  (PV-CICS-RESP)                                 INKCS003
02793      END-EXEC.                                                    INKCS003
02794      IF  PV-CICS-RESP NOT = DFHRESP (NORMAL)                      INKCS003
02795          SET DP013-CICS-ABEND      TO TRUE                        INKCS003
02796          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS003
02797          MOVE '5110-WRITE-ASC-LISTQ'                              INKCS003
02798                                    TO DP013-PARAGRAPH             INKCS003
02799          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS003
02800                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
02801          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
02802      END-IF.                                                      INKCS003
02803                                                                   INKCS003
02804 ******************************************************************INKCS003
02805 ** DELETE THE LIST TEMP STORAGE QUEUE.                          **INKCS003
02806 ******************************************************************INKCS003
02807                                                                   INKCS003
02808  5120-DELETE-ASC-LISTQ.                                           INKCS003
02809                                                                   INKCS003
02810      EXEC CICS                                                    INKCS003
02811           DELETEQ TS                                              INKCS003
02812              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
02813              RESP  (PV-CICS-RESP)                                 INKCS003
02814      END-EXEC.                                                    INKCS003
02815                                                                   INKCS003
02816      IF (PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS003
02817      OR  PV-CICS-RESP = DFHRESP (QIDERR))                         INKCS003
02818          CONTINUE                                                 INKCS003
02819      ELSE                                                         INKCS003
02820          SET DP013-CICS-ABEND      TO TRUE                        INKCS003
02821          SET DP013-NO-ROLLBACK     TO TRUE                        INKCS003
02822          MOVE '5120-DELETE-ASC-LISTQ'                             INKCS003
02823                                    TO DP013-PARAGRAPH             INKCS003
02824          MOVE 'ATTEMPTING TO DELETE TEMP STORAGE QUEUE'           INKCS003
02825                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
02826          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
02827      END-IF.                                                      INKCS003
02828 /                                                                 INKCS003
02829 *----------------------------------------------------------------*INKCS003
02830 *    READ A LIST QUEUE RECORD AND UPDATE THE DB2 TABLES FOR EACH *INKCS003
02831 *    ITEM THAT HAS CHANGED.                                      *INKCS003
02832 *----------------------------------------------------------------*INKCS003
02833                                                                   INKCS003
02834  5200-UPDATE-IN-TSQUEUE.                                          INKCS003
02835                                                                   INKCS003
02836      PERFORM 6112-READ-LIST-QUEUE.                                INKCS003
02837                                                                   INKCS003
02838      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS003
02839          PERFORM                                                  INKCS003
02840          VARYING LQW-IDX                                          INKCS003
02841          FROM 1 BY 1                                              INKCS003
02842          UNTIL LQW-IDX GREATER THAN PC-ITEMS-PER-PANEL            INKCS003
02843            OR  LQW-LIST-ITEM-NUMBER (LQW-IDX) EQUAL ZERO          INKCS003
02844                                                                   INKCS003
02845              IF  LQW-UPDATED(LQW-IDX)                             INKCS003
W21732                 MOVE LQW-CSKU     (LQW-IDX)                      INKCS003
W21732                                   TO  INVDTL-CORR-SKU-NBR        INKCS003
W21732                                                                  INKCS003
W21732                 IF  LQW-RENUMBERED-SKU (LQW-IDX) > SPACES        INKCS003
W21732                     MOVE LQW-RSKU     (LQW-IDX)                  INKCS003
W21732                                   TO  ADJDTL-SKU-NBR             INKCS003
W21732                 ELSE                                             INKCS003
W21732                     MOVE LQW-CSKU     (LQW-IDX)                  INKCS003
W21732                                   TO  ADJDTL-SKU-NBR             INKCS003
W21732                 END-IF                                           INKCS003
02868                                                                   INKCS003
02869                  MOVE LQW-CUPC         (LQW-IDX)                  INKCS003
02870                                    TO  INVDTL-CORR-UPC-NBR        INKCS003
25248                  MOVE LQW-CUPC         (LQW-IDX)                  INKCS003
25248                                    TO  ADJDTL-UPC-NBR             INKCS003
02871                  MOVE LQW-INV-PR-AMT   (LQW-IDX)                  INKCS003
02872                                    TO  ADJDTL-ADJ-UNIT-PR-AMT     INKCS003
02873                  MOVE ZERO         TO  INVDTL-CORR-QTY            INKCS003
02874                  MOVE LQW-INV-LINE-NBR (LQW-IDX)                  INKCS003
02875                                    TO  INVDTL-INV-LINE-NBR        INKCS003
02876                                                                   INKCS003
02877                  MOVE LQW-SCAN-LINE-NBR (LQW-IDX)                 INKCS003
02878                                    TO  INVDTL-SCAN-LINE-NBR       INKCS003
02879                                                                   INKCS003
02880                  IF  LQW-INV-PR-AMT (LQW-IDX) NOT EQUAL           INKCS003
02881                      LQW-ORIG-INV-PR-AMT (LQW-IDX)                INKCS003
02882                      MOVE LQW-INV-PR-AMT   (LQW-IDX)              INKCS003
02883                                    TO  INVDTL-CORR-PR-AMT         INKCS003
02884                  ELSE                                             INKCS003
02885                      MOVE ZERO     TO  INVDTL-CORR-PR-AMT         INKCS003
02886                  END-IF                                           INKCS003
02887                                                                   INKCS003
02888                  SET LQW-PROTECT-ENTRY (LQW-IDX)                  INKCS003
02889                      LQW-PROTECT-PR-AMT(LQW-IDX)                  INKCS003
02890                      LQW-UPDATE-DONE (LQW-IDX)                    INKCS003
02891                      PS-UPDATED    TO  TRUE                       INKCS003
02892                  PERFORM 5300-UPDATE-TINVDTL                      INKCS003
02893                  PERFORM 5400-PREPARE-TADJDTL                     INKCS003
02894              END-IF                                               INKCS003
02895                                                                   INKCS003
02896          END-PERFORM                                              INKCS003
02897      END-IF.                                                      INKCS003
02898                                                                   INKCS003
02899      PERFORM 6111-WRITE-LIST-QUEUE.                               INKCS003
02900 /                                                                 INKCS003
02901 *----------------------------------------------------------------*INKCS003
02902 *    UPDATE THE INVENTORY DETAIL TABLE.                          *INKCS003
02903 *----------------------------------------------------------------*INKCS003
02904                                                                   INKCS003
02905  5300-UPDATE-TINVDTL.                                             INKCS003
02906                                                                   INKCS003
02907      MOVE LQW-INVALID-REC-IND(LQW-IDX)                            INKCS003
02908                                  TO  INVDTL-INVALID-REC-IND.      INKCS003
02909      MOVE LQW-INVALID-ITM-IND(LQW-IDX)                            INKCS003
02910                                  TO  INVDTL-INVALID-ITM-IND.      INKCS003
W26600     MOVE ASC-KEY-STORE-NBR      TO PV-STORE-NBR-X.               INKCS003
W26600     MOVE PV-STORE-NBR-9         TO INVDTL-LOC-NBR.               INKCS003
W21732     MOVE ASC-KEY-AREA-NBR       TO INVDTL-AREA-NBR.              INKCS003
02911                                                                   INKCS003
02912      IF  INVDTL-INVALID-REC-IND  EQUAL PC-Y                       INKCS003
02913          IF (LQW-INV-QTY   (LQW-IDX) NOT EQUAL ZERO               INKCS003
02914          AND LQW-INV-PR-AMT(LQW-IDX) NOT EQUAL ZERO)              INKCS003
02915              ADD 1               TO  PV-REC-CORRECTED             INKCS003
02916              MOVE PC-N           TO  INVDTL-INVALID-REC-IND       INKCS003
02917              IF  INVDTL-INVALID-ITM-IND EQUAL PC-Y                INKCS003
02918                  MOVE PC-N       TO  INVDTL-INVALID-ITM-IND       INKCS003
02919                  ADD 1           TO  PV-ITM-CORRECTED             INKCS003
02920              END-IF                                               INKCS003
02921          ELSE                                                     INKCS003
02922              IF  INVDTL-INVALID-ITM-IND EQUAL PC-Y                INKCS003
02923                  MOVE PC-N       TO  INVDTL-INVALID-ITM-IND       INKCS003
02924                  ADD 1           TO  PV-ITM-CORRECTED             INKCS003
02925              END-IF                                               INKCS003
02926          END-IF                                                   INKCS003
02927      END-IF.                                                      INKCS003
02928                                                                   INKCS003
02929          EXEC SQL                                                 INKCS003
02930               UPDATE                                              INKCS003
02931                      TINVDTL                                      INKCS003
02932                 SET                                               INKCS003
W21732                     CORR_SKU_NBR    =  :INVDTL-CORR-SKU-NBR      INKCS003
02936                    , CORR_UPC_NBR    =  :INVDTL-CORR-UPC-NBR      INKCS003
02937                    , CORR_PR_AMT     =  :INVDTL-CORR-PR-AMT       INKCS003
02938                    , INVALID_ITM_IND =  :INVDTL-INVALID-ITM-IND   INKCS003
02939                    , INVALID_REC_IND =  :INVDTL-INVALID-REC-IND   INKCS003
02940                    , CHG_TMST        =   CURRENT TIMESTAMP        INKCS003
02941                    , CHG_ID_NBR      =  :DP020-USERID             INKCS003
02942              WHERE                                                INKCS003
W26600                     LOC_NBR       =  :INVDTL-LOC-NBR             INKCS003
W21732               AND   AREA_NBR        =  :INVDTL-AREA-NBR          INKCS003
02945                AND   INV_LINE_NBR    =  :INVDTL-INV-LINE-NBR      INKCS003
02946                AND   SCAN_LINE_NBR   =  :INVDTL-SCAN-LINE-NBR     INKCS003
02947          END-EXEC.                                                INKCS003
02948                                                                   INKCS003
02949          EVALUATE TRUE                                            INKCS003
02950              WHEN SQLCODE = ZERO                                  INKCS003
02951                  CONTINUE                                         INKCS003
02952              WHEN SQLCODE = -904                                  INKCS003
02953              WHEN SQLCODE = -913                                  INKCS003
02954                   PERFORM 9900-SOFT-ABORT                         INKCS003
02955              WHEN SQLWARN0 NOT = SPACES                           INKCS003
02956              WHEN SQLCODE < ZERO                                  INKCS003
02957              WHEN SQLCODE > ZERO                                  INKCS003
02958                  MOVE '5100-UPDATE-TINVDTL-ROW'                   INKCS003
02959                                    TO DP013-PARAGRAPH             INKCS003
02960                  MOVE 'UPDATE A ROW ON THE TINVDTL TABLE'         INKCS003
02961                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
02962                  MOVE SQLCA        TO DP013-SQLCA                 INKCS003
02963                  MOVE 'TINVDTL'    TO DP013-DB2-TABLE-NAME (1)    INKCS003
02964                  SET DP013-DB2-ABEND                              INKCS003
02965                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS003
02966                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS003
02967          END-EVALUATE.                                            INKCS003
02968 /                                                                 INKCS003
02969 *----------------------------------------------------------------*INKCS003
02970 *    CREATE AND ADJUSTMENT ROW.                                  *INKCS003
02971 *----------------------------------------------------------------*INKCS003
02972                                                                   INKCS003
02973  5400-PREPARE-TADJDTL.                                            INKCS003
02974                                                                   INKCS003
W21732     MOVE INVDTL-CORR-SKU-NBR      TO  PV-SKU-NBR.                INKCS003
02977      MOVE ZERO                     TO  ADJDTL-DKEY-NBR.           INKCS003
W26600     MOVE ASC-KEY-STORE-NBR        TO  PV-STORE-NBR-X.                    
W26600     MOVE PV-STORE-NBR-9           TO  ADJDTL-LOC-NBR.                    
02979                                                                   INKCS003
02980      MOVE ASC-KEY-AREA-NBR         TO  ADJDTL-AREA-NBR.           INKCS003
02981      MOVE PC-CO                    TO  ADJDTL-ADJ-TYPE-CDE.       INKCS003
02982      MOVE PC-CORRECTION            TO  ADJDTL-ADJ-RSN-TXT.        INKCS003
02983      MOVE LQW-INV-QTY (LQW-IDX)    TO  ADJDTL-ADJ-QTY.            INKCS003
02984      COMPUTE ADJDTL-ADJ-EXTD-AMT   =   ADJDTL-ADJ-QTY             INKCS003
02985                                    *   ADJDTL-ADJ-UNIT-PR-AMT.    INKCS003
02986      IF  ADJDTL-ADJ-EXTD-AMT NOT EQUAL ZERO                       INKCS003
02987          PERFORM 5410-INSERT-TADJDTL                              INKCS003
02988      END-IF.                                                      INKCS003
02989                                                                   INKCS003
02990 *----------------------------------------------------------------*INKCS003
02991 *    CREATE AND ADJUSTMENT ROW.                                  *INKCS003
02992 *----------------------------------------------------------------*INKCS003
02993                                                                   INKCS003
02994  5410-INSERT-TADJDTL.                                             INKCS003
02995                                                                   INKCS003
02996          EXEC SQL                                                 INKCS003
02997               INSERT                                              INKCS003
02998                 INTO TADJDTL                                      INKCS003
W26600                  (LOC_NBR                                                
03000                  , AREA_NBR                                       INKCS003
W21732                 , SKU_NBR                                        INKCS003
25248                  , UPC_NBR                                                
03004                  , CRE_TMST                                       INKCS003
03005                  , ADJ_RSN_TXT                                    INKCS003
03006                  , ADJ_TYPE_CDE                                   INKCS003
03007                  , ADJ_UNIT_PR_AMT                                INKCS003
03008                  , ADJ_QTY                                        INKCS003
03009                  , ADJ_EXTD_AMT                                   INKCS003
03010                  , DKEY_NBR                                       INKCS003
03011                  , CRE_ID_NBR)                                    INKCS003
03012               VALUES                                              INKCS003
W26600                  (:ADJDTL-LOC-NBR                                        
03014                  , :ADJDTL-AREA-NBR                               INKCS003
W21732                 , :ADJDTL-SKU-NBR                                INKCS003
25248                  , :ADJDTL-UPC-NBR                                INKCS003
03018                  ,  CURRENT TIMESTAMP                             INKCS003
03019                  , :ADJDTL-ADJ-RSN-TXT                            INKCS003
03020                  , :ADJDTL-ADJ-TYPE-CDE                           INKCS003
03021                  , :ADJDTL-ADJ-UNIT-PR-AMT                        INKCS003
03022                  , :ADJDTL-ADJ-QTY                                INKCS003
03023                  , :ADJDTL-ADJ-EXTD-AMT                           INKCS003
03024                  , :ADJDTL-DKEY-NBR                               INKCS003
03025                  , :DP020-USERID)                                 INKCS003
03026          END-EXEC.                                                INKCS003
03027                                                                   INKCS003
03028          EVALUATE TRUE                                            INKCS003
03029              WHEN SQLCODE = ZERO                                  INKCS003
03030                  CONTINUE                                         INKCS003
03031              WHEN SQLCODE = -904                                  INKCS003
03032              WHEN SQLCODE = -913                                  INKCS003
03033                   PERFORM 9900-SOFT-ABORT                         INKCS003
03034              WHEN SQLWARN0 NOT = SPACES                           INKCS003
03035              WHEN SQLCODE < ZERO                                  INKCS003
03036              WHEN SQLCODE > ZERO                                  INKCS003
03037                  MOVE '5410-INSERT-TADJDTL'                       INKCS003
03038                                    TO DP013-PARAGRAPH             INKCS003
03039                  MOVE 'INSERT A ROW IN THE TADJDTL TABLE'         INKCS003
03040                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
03041                  MOVE SQLCA        TO DP013-SQLCA                 INKCS003
03042                  MOVE 'TADJDTL'    TO DP013-DB2-TABLE-NAME (1)    INKCS003
03043                  SET DP013-DB2-ABEND                              INKCS003
03044                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS003
03045                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS003
03046          END-EVALUATE.                                            INKCS003
03047 /                                                                 INKCS003
03048 *----------------------------------------------------------------*INKCS003
03049 *    READ AND UPDATE THE TINVLOC TABLE.                          *INKCS003
03050 *----------------------------------------------------------------*INKCS003
03051                                                                   INKCS003
03052  5500-UPDATE-TINVLOC.                                             INKCS003
03053                                                                   INKCS003
W26600     MOVE ASC-KEY-STORE-NBR        TO  PV-STORE-NBR-X.            INKCS003
J28545     MOVE PV-STORE-NBR-9           TO  INVLOC-LOC-NBR.            INKCS003
03055      MOVE ASC-KEY-AREA-NBR         TO  INVLOC-AREA-NBR.           INKCS003
03056      PERFORM 5510-READ-TINVLOC-ROW.                               INKCS003
03057                                                                   INKCS003
03058      ADD PV-ITM-CORRECTED          TO  INVLOC-TOT-ITM-CORR-QTY.   INKCS003
03059      ADD PV-REC-CORRECTED          TO  INVLOC-TOT-REC-CORR-QTY.   INKCS003
03060                                                                   INKCS003
03061      PERFORM 5520-UPDATE-TINVLOC-ROW.                             INKCS003
03062                                                                   INKCS003
03063 *----------------------------------------------------------------*INKCS003
03064 *    READ THE TINVLOC TABLE.                                     *INKCS003
03065 *----------------------------------------------------------------*INKCS003
03066                                                                   INKCS003
03067  5510-READ-TINVLOC-ROW.                                           INKCS003
03068                                                                   INKCS003
03069          EXEC SQL                                                 INKCS003
03070               SELECT                                              INKCS003
03071                     TOT_REC_CORR_QTY                              INKCS003
03072                   , TOT_ITM_CORR_QTY                              INKCS003
03073               INTO                                                INKCS003
03074                    :INVLOC-TOT-REC-CORR-QTY                       INKCS003
03075                   ,:INVLOC-TOT-ITM-CORR-QTY                       INKCS003
03076               FROM                                                INKCS003
03077                     TINVLOC                                       INKCS003
03078               WHERE                                               INKCS003
J28545                    LOC_NBR             =  :INVLOC-LOC-NBR        INKCS003
03080                AND                                                INKCS003
03081                     AREA_NBR            =  :INVLOC-AREA-NBR       INKCS003
03082          END-EXEC.                                                INKCS003
03083                                                                   INKCS003
03084          EVALUATE TRUE                                            INKCS003
03085              WHEN SQLCODE = ZERO                                  INKCS003
03086                  CONTINUE                                         INKCS003
03087              WHEN SQLCODE = -904                                  INKCS003
03088              WHEN SQLCODE = -913                                  INKCS003
03089                   PERFORM 9900-SOFT-ABORT                         INKCS003
03090              WHEN SQLWARN0 NOT = SPACES                           INKCS003
03091              WHEN SQLCODE < ZERO                                  INKCS003
03092              WHEN SQLCODE > ZERO                                  INKCS003
03093                  MOVE '5510-READ-TINVLOC-ROW'                     INKCS003
03094                                    TO DP013-PARAGRAPH             INKCS003
03095                  MOVE 'READ A ROW ON THE TINVLOC TABLE'           INKCS003
03096                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
03097                  MOVE SQLCA        TO DP013-SQLCA                 INKCS003
03098                  MOVE 'TINVLOC'    TO DP013-DB2-TABLE-NAME (1)    INKCS003
03099                  SET DP013-DB2-ABEND                              INKCS003
03100                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS003
03101                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS003
03102          END-EVALUATE.                                            INKCS003
03103                                                                   INKCS003
03104 *----------------------------------------------------------------*INKCS003
03105 *    UPDATE THE PHYSICAL LOCATION ROW.                           *INKCS003
03106 *----------------------------------------------------------------*INKCS003
03107                                                                   INKCS003
03108  5520-UPDATE-TINVLOC-ROW.                                         INKCS003
03109                                                                   INKCS003
03110          EXEC SQL                                                 INKCS003
03111               UPDATE                                              INKCS003
03112                      TINVLOC                                      INKCS003
03113                 SET                                               INKCS003
03114                      TOT_REC_CORR_QTY = :INVLOC-TOT-REC-CORR-QTY  INKCS003
03115                    , TOT_ITM_CORR_QTY = :INVLOC-TOT-ITM-CORR-QTY  INKCS003
03116                    , CHG_TMST         =  CURRENT TIMESTAMP        INKCS003
03117              WHERE                                                INKCS003
J28545                     LOC_NBR     =  :INVLOC-LOC-NBR               INKCS003
03119                AND                                                INKCS003
J28545                     AREA_NBR    =  :INVLOC-AREA-NBR              INKCS003
03121          END-EXEC.                                                INKCS003
03122                                                                   INKCS003
03123          EVALUATE TRUE                                            INKCS003
03124              WHEN SQLCODE = ZERO                                  INKCS003
03125                  CONTINUE                                         INKCS003
03126              WHEN SQLCODE = -904                                  INKCS003
03127              WHEN SQLCODE = -913                                  INKCS003
03128                   PERFORM 9900-SOFT-ABORT                         INKCS003
03129              WHEN SQLWARN0 NOT = SPACES                           INKCS003
03130              WHEN SQLCODE < ZERO                                  INKCS003
03131              WHEN SQLCODE > ZERO                                  INKCS003
03132                  MOVE '5520-UPDATE-TINVLOC-ROW'                   INKCS003
03133                                    TO DP013-PARAGRAPH             INKCS003
03134                  MOVE 'UPDATE A ROW ON THE TINVLOC TABLE'         INKCS003
03135                                    TO DP013-MESSAGE-TEXT(1)       INKCS003
03136                  MOVE SQLCA        TO DP013-SQLCA                 INKCS003
03137                  MOVE 'TINVLOC'    TO DP013-DB2-TABLE-NAME (1)    INKCS003
03138                  SET DP013-DB2-ABEND                              INKCS003
03139                      DP013-XCTL-DISPLAY-RESTART TO TRUE           INKCS003
03140                  PERFORM DP013-0000-PROCESS-ABEND                 INKCS003
03141          END-EVALUATE.                                            INKCS003
03142 /                                                                 INKCS003
03198 *----------------------------------------------------------------*INKCS003
03199 *    WRITE THE TS QUEUE RECORD JUST UPDATED IN C600.             *INKCS003
03200 *----------------------------------------------------------------*INKCS003
03201                                                                   INKCS003
03202  6111-WRITE-LIST-QUEUE.                                           INKCS003
03203                                                                   INKCS003
03204      EXEC CICS                                                    INKCS003
03205          WRITEQ TS                                                INKCS003
03206              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
03207              FROM  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS003
03208              ITEM  (PV-ITEM)                                      INKCS003
03209              REWRITE                                              INKCS003
03210              RESP  (PV-CICS-RESP)                                 INKCS003
03211      END-EXEC.                                                    INKCS003
03212                                                                   INKCS003
03213      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS003
03214          CONTINUE                                                 INKCS003
03215      ELSE                                                         INKCS003
03216          SET DP013-CICS-ABEND  TO  TRUE                           INKCS003
03217          SET DP013-NO-ROLLBACK TO  TRUE                           INKCS003
03218          MOVE '6111-WRITE-LIST-QUEUE'                             INKCS003
03219                                TO  DP013-PARAGRAPH                INKCS003
03220          MOVE 'ATTEMPTING TO REWRITE LIST QUEUE'                  INKCS003
03221                                TO  DP013-MESSAGE-TEXT(1)          INKCS003
03222          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
03223      END-IF.                                                      INKCS003
03224 /                                                                 INKCS003
03225 *----------------------------------------------------------------*INKCS003
03226 *    READ THE TEMPORARY STORAGE QUEUE INTO A TEMPORARY WORK AREA.*INKCS003
03227 *----------------------------------------------------------------*INKCS003
03228                                                                   INKCS003
03231  6112-READ-LIST-QUEUE.                                            INKCS003
03230                                                                   INKCS003
03231      EXEC CICS                                                    INKCS003
03232          READQ TS                                                 INKCS003
03233              QUEUE (ASC-LIST-QUEUE-NAME)                          INKCS003
03234              INTO  (LQW-LIST-QUEUE-WORK-AREA)                     INKCS003
03235              ITEM  (PV-ITEM)                                      INKCS003
03236              RESP  (PV-CICS-RESP)                                 INKCS003
03237      END-EXEC.                                                    INKCS003
03238                                                                   INKCS003
03239      IF  PV-CICS-RESP = DFHRESP (NORMAL)                          INKCS003
03240          CONTINUE                                                 INKCS003
03241      ELSE                                                         INKCS003
03242          SET DP013-CICS-ABEND        TO TRUE                      INKCS003
03243          SET DP013-NO-ROLLBACK       TO TRUE                      INKCS003
03244          MOVE '6112-READ-LIST-QUEUE' TO DP013-PARAGRAPH           INKCS003
03245          MOVE 'ATTEMPTING TO READQ LIST QUEUE'                    INKCS003
03246                                      TO DP013-MESSAGE-TEXT(1)     INKCS003
03247          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
03248      END-IF.                                                      INKCS003
03249 /                                                                 INKCS003
03250 *----------------------------------------------------------------*INKCS003
03251 * READ AN ITEM FROM THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS003
03252 *----------------------------------------------------------------*INKCS003
03253                                                                   INKCS003
03254  9100-READ-SEL-QUEUE.                                             INKCS003
03255                                                                   INKCS003
03256      EXEC CICS                                                    INKCS003
03257          READQ TS                                                 INKCS003
03258              QUEUE (ASC-SEL-QUEUE-NAME)                           INKCS003
03259              INTO  (IN002-SELECTED-AREAS)                         INKCS003
03260              ITEM  (ASC-SEL-ITEM)                                 INKCS003
03261              RESP  (PV-CICS-RESP)                                 INKCS003
03262      END-EXEC.                                                    INKCS003
03263                                                                   INKCS003
03264      IF  PV-CICS-RESP EQUAL DFHRESP(NORMAL)                       INKCS003
03265          CONTINUE                                                 INKCS003
03266      ELSE                                                         INKCS003
03267          SET DP013-LOGIC-ABEND                                    INKCS003
03268              DP013-NO-ROLLBACK     TO TRUE                        INKCS003
03269          MOVE '9100-READ-SEL-QUEUE'                               INKCS003
03270                                    TO DP013-PARAGRAPH             INKCS003
03271          MOVE 'ATTEMPTING TO READ THE LIST SELECTIONS TS QUEUE'   INKCS003
03272                                    TO DP013-MESSAGE-TEXT (1)      INKCS003
03273          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
03274      END-IF.                                                      INKCS003
03275                                                                   INKCS003
03276 *----------------------------------------------------------------*INKCS003
03277 * UPDATE AN ITEM IN THE SELECTED ITEMS TEMP STORAGE QUEUE.       *INKCS003
03278 *----------------------------------------------------------------*INKCS003
03279                                                                   INKCS003
03280  9200-REWRITE-LIST-QUEUE.                                         INKCS003
03281                                                                   INKCS003
03282      EXEC CICS                                                    INKCS003
03283           WRITEQ TS                                               INKCS003
03284               QUEUE  (ASC-SEL-QUEUE-NAME)                         INKCS003
03285               FROM   (IN002-SELECTED-AREAS)                       INKCS003
03286               ITEM   (ASC-SEL-ITEM)                               INKCS003
03287               RESP   (PV-CICS-RESP)                               INKCS003
03288               REWRITE                                             INKCS003
03289      END-EXEC.                                                    INKCS003
03290                                                                   INKCS003
03291      IF  PV-CICS-RESP NOT EQUAL DFHRESP(NORMAL)                   INKCS003
03292          SET DP013-CICS-ABEND                                     INKCS003
03293              DP013-NO-ROLLBACK     TO TRUE                        INKCS003
03294          MOVE '9200-REWRITE-LIST-QUEUE'                           INKCS003
03295                                    TO DP013-PARAGRAPH             INKCS003
03296          MOVE 'ATTEMPTING TO REWRITE THE LIST SELECTIONS QUEUE'   INKCS003
03297                                    TO DP013-MESSAGE-TEXT (1)      INKCS003
03298          PERFORM DP013-0000-PROCESS-ABEND                         INKCS003
03299      END-IF.                                                      INKCS003
03300 /                                                                 INKCS003
03301 *----------------------------------------------------------------*INKCS003
03302 *    THE DB2 RESOURCE IS NOT AVAILABLE, SET THE ERROR MESSAGE    *INKCS003
03303 *    AND THE ERROR SWITCHES.                                     *INKCS003
03304 *----------------------------------------------------------------*INKCS003
03305                                                                   INKCS003
03306  9900-SOFT-ABORT.                                                 INKCS003
03307                                                                   INKCS003
03308      SET PS-ERROR                                                 INKCS003
03309          PS-SOFT-ABORT           TO  TRUE.                        INKCS003
03310      SET DP020-MSG-FATAL         TO  TRUE.                        INKCS003
03311 /                                                                 INKCS003
03312 *----------------------------------------------------------------*INKCS003
03313 *    ABEND PROCESSOR MODULE                                      *INKCS003
03314 *----------------------------------------------------------------*INKCS003
03315                                                                   INKCS003
03316      COPY DPPD013.                                                INKCS003
J28545*----------------------------------------------------------------*INKCS003
J28545*  PERFORM THE COMMON PRICING LOGIC IN INPD400                            
J28545*  CHECK THE RESULTS AND FILL THE APPLICATION SPECIFIC COMMAREA           
J28545*  THIS ROUTINE USES THE FOLLOWING FIELDS TO DETERMINE PRICE              
J28545*     IN400-PV-SKU-IN = THE SKU NUMBER                                    
J28545*     IN400-PV-SKU-LOOKUP SHOULD BE TRUE                                  
J28545*     IN400-PV-EFF-DTE-IN SHOULD = INVPAR-PLND-INV-DTE                    
J28545*     IN400-PV-LOC-IN-NUM SHOULD = INVPAR-LOC-NBR                         
J28545*  AND CHECKS THE RESULTING VALUES                                        
J28545*----------------------------------------------------------------         
J28545     COPY INPD400.                                                        
