       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKRP180.                                       
       AUTHOR.                  SCOTT JACKSON.                                  
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/18/99.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *                 EXCEPTION CRITERIA REPORT                      *        
      *                                                                *        
      *  THIS REPORT IS SUBMITTED FOR EXECUTION IN JOB INK301. THE JOB *        
      *  CARD IS BUILT WITHIN A CICS ONLINE APPLICATION. PGM INKCS048  *        
      *  CONTROLS PROCESSING OF 'INVENTORY EXCEPTIONS' AND HAS THE     *        
      *  CAPABILITY OF SUBMITTING A REPORT FOR PROCESSING (PF20 / I048)*        
      *  AND WILL BE BASED ON CRITERIA SPECIFIED ON THE INITIAL SCREEN.*        
      *                                                                *        
      *  DEPENDING ON THE INPUT CONTROL CARD, THE PROGRAM WILL EITHER  *        
      *  REPORT ON ALL DEPARTMENTS WHERE CRITERIA HAS BEEN ASSIGNED    *        
      *  OR IT WILL REPORT ON ALL DEPTS WHERE CRITERIA IS NOT ASSIGNED *        
      ******                                                       *****        
      *  LAYOUT OF REPORT IS A 132-BYTE LAYOUT WITH THE DATA BEING     *        
      *  ALIGNED INTO FOUR IDENTICAL BLOCKS. THE DATA IS TO BE WRITTEN *        
      *  DOWN COLUMN ONE FIRST AND THEN CONTINUED IN COLUMN 2,3 AND 4. *        
      *  AN INITIAL CURSOR WILL SELECT ALL RECORDS FOR PROCESSING. EACH*        
      *  RECORD IS THEN FETCHED INTO EACH OF FOUR TABLES UNTIL FILLED. *        
      *  REPORT IS THEN WRITTEN FROM TABLES. THE DATA FROM THE CURSOR  *        
      *  IS LOADED TO THE TABLES, WRITTEN FROM THE TABLES AND THEN     *        
      *  LOADING IS CONTINUED FROM CURSOR TO TABLE UNTIL ALL CRITERIA  *        
      *  IS PROCESSED. RPT LAYOUT...LOAD TO TABLES /WRITTEN FROM TABLES*        
      *** TABLE 1  *******  TABLE 2  *******  TABLE 3 *****  TABLE 4 ***        
      *   RECORD 1  ****    RECORD 4  ****    RECORD 7   *** RECORD 10 *        
      *   RECORD 2  ****    RECORD 5  ****    RECORD 8   *** RECORD 11 *        
      *   RECORD 3  ****    RECORD 6  ****    RECORD 9   *** RECORD 12 *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-FILE                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT CRITERIA-REPORT                                               
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS REPORT-LINE.                                          
       01  INPUT-CONTROL-CARD    PIC X(80).                                     
                                                                                
       FD  CRITERIA-REPORT                                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE           PIC X(132).                                    
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-CRITERIA-IND         PIC X(01).                               
               88  CC-VALID-INPUT            VALUE 'Y' 'N'.                     
               88  CC-CRITERIA-ASSIGNED      VALUE 'Y'.                         
               88  CC-NO-CRITERIA-ASSIGNED   VALUE 'N'.                         
      *                                                                         
       01  PS-SWITCHES.                                                         
           05  PS-END-OF-CC-SW             PIC X       VALUE 'N'.               
               88  PS-END-OF-CC-INPUT                  VALUE 'Y'.               
           05  PS-END-OF-RECORDS-SW        PIC X       VALUE 'N'.               
               88  PS-END-OF-RECORDS                   VALUE 'Y'.               
           05  PS-END-OF-CURSOR-SW         PIC X       VALUE 'N'.               
               88  PS-MORE-CURSOR-RECORDS              VALUE 'N'.               
               88  PS-END-OF-CURSOR-RECORDS            VALUE 'Y'.               
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC  X(08)  VALUE 'INKRP180'.        
           05  PC-RPT-VERSION              PIC  X.                              
               88  PC-RPT-CRITERIA                     VALUE '1'.               
               88  PC-RPT-NO-CRITERIA                  VALUE '2'.               
           05  PC-LINE-MAX                 PIC S9(03)  VALUE +57.               
           05  PC-ROW-ENTRIES              PIC S9(03)  VALUE +50.               
      *    05  PC-COLUMN-ENTRIES           PIC S9(03)  VALUE +04.               
           05  PC-DASH                     PIC  X(04)  VALUE '----'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-SUB1                     PIC S9(02)  VALUE +0.                
           05  PV-CRITERIA-IND             PIC  X(01)  VALUE SPACES.            
           05  PV-RECORD-COUNT             PIC S9(05)  VALUE +0.                
           05  PV-PAGE-COUNT               PIC 9(5)    VALUE 0.                 
           05  PV-LINE-COUNT               PIC 9(2)    VALUE 0.                 
           05  PV-OPERATION-ATTEMPTED      PIC X(30).                           
           05  PV-PROCESSING-DATE          PIC X(10).                           
           05  PV-CORPORATE-CRITERIA.                                           
               10  PV-CORP-VAR-PCT         PIC 9(03)V    VALUE ZEROES.          
               10  PV-CORP-VAR-QTY         PIC 9(05)V    VALUE ZEROES.          
           05  PV-TODAYS-DATE.                                                  
               10  PV-YY                       PIC 9(02)   VALUE ZERO.          
               10  PV-MM                       PIC 9(02)   VALUE ZERO.          
               10  PV-DD                       PIC 9(02)   VALUE ZERO.          
           05  PV-TODAYS-TIME.                                                  
               10  PV-HR                       PIC 9(02)   VALUE ZERO.          
               10  PV-MIN                      PIC 9(02)   VALUE ZERO.          
               10  PV-SEC                      PIC 9(02)   VALUE ZERO.          
               10  FILLER                      PIC 9(02)   VALUE ZERO.          
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES.         
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
           88  ABEND-4003                                  VALUE +4003.         
                                                                                
       01  PE-PROGRAM-ERROR-MESSAGES.                                           
           05  PE-MSG-01                       PIC X(30) VALUE                  
                'INSPECT WHERE CLAUSE VARIABLE'.                                
           05  PE-MSG-02                       PIC X(30) VALUE                  
                'DB2 SELECT ATTEMPTED         '.                                
           05  PE-MSG-03                       PIC X(30) VALUE                  
                'ATTEMPTING TO OPEN DB2 CURSOR'.                                
           05  PE-MSG-04                       PIC X(30) VALUE                  
                'ATTEMPT TO FETCH CURSOR ROW  '.                                
           05  PE-MSG-05                       PIC X(30) VALUE                  
                'FAIL IN LOADING REPORT TABLES'.                                
                                                                                
      *THIS TABLE IS LOADED WITH CSR DATA FOR 1ST COLUMN OF REPORT              
       01  TB-C1-REPORT-TABLE.                                                  
           05  TB-C1-REPORT-ENTRIES         OCCURS 50 TIMES.                    
               10  TB-C1-DEPT-NBR           PIC  X(03)     VALUE SPACES.        
               10  TB-C1-MCL-NBR            PIC  X(02)     VALUE SPACES.        
               10  TB-C1-VAR-PCT            PIC  9(3)V99   VALUE ZEROES.        
               10  TB-C1-VAR-QTY            PIC  9(04)     VALUE ZEROES.        
                                                                                
      *THIS TABLE IS LOADED WITH CSR DATA FOR 2ND COLUMN OF REPORT              
       01  TB-C2-REPORT-TABLE.                                                  
           05  TB-C2-REPORT-ENTRIES         OCCURS 50 TIMES.                    
               10  TB-C2-DEPT-NBR           PIC  X(03)     VALUE SPACES.        
               10  TB-C2-MCL-NBR            PIC  X(02)     VALUE SPACES.        
               10  TB-C2-VAR-PCT            PIC  9(3)V99   VALUE ZEROES.        
               10  TB-C2-VAR-QTY            PIC  9(04)     VALUE ZEROES.        
                                                                                
      *THIS TABLE IS LOADED WITH CSR DATA FOR 3RD COLUMN OF REPORT              
       01  TB-C3-REPORT-TABLE.                                                  
           05  TB-C3-REPORT-ENTRIES         OCCURS 50 TIMES.                    
               10  TB-C3-DEPT-NBR           PIC  X(03)     VALUE SPACES.        
               10  TB-C3-MCL-NBR            PIC  X(02)     VALUE SPACES.        
               10  TB-C3-VAR-PCT            PIC  9(3)V99   VALUE ZEROES.        
               10  TB-C3-VAR-QTY            PIC  9(04)     VALUE ZEROES.        
                                                                                
      *THIS TABLE IS LOADED WITH CSR DATA FOR 4TH COLUMN OF REPORT              
       01  TB-C4-REPORT-TABLE.                                                  
           05  TB-C4-REPORT-ENTRIES         OCCURS 50 TIMES.                    
               10  TB-C4-DEPT-NBR           PIC  X(03)     VALUE SPACES.        
               10  TB-C4-MCL-NBR            PIC  X(02)     VALUE SPACES.        
               10  TB-C4-VAR-PCT            PIC  9(3)V99   VALUE ZEROES.        
               10  TB-C4-VAR-QTY            PIC  9(04)     VALUE ZEROES.        
                                                                                
       01  RH-REPORT-HEADING-ONE.                                               
           05  FILLER                      PIC X(50) VALUE SPACES.              
           05  FILLER                      PIC X(83) VALUE                      
               'INVENTORY EXCEPTION CRITERIA LIST'.                             
                                                                                
       01  RH-REPORT-HEADING-TWO-A.                                             
           05  FILLER                      PIC X(45) VALUE SPACES.              
           05  FILLER                      PIC X(44) VALUE                      
               'DEPARTMENTS WHERE CRITERIA HAS BEEN ASSIGNED'.                  
           05  FILLER                      PIC X(43) VALUE SPACES.              
                                                                                
       01  RH-REPORT-HEADING-TWO-B.                                             
           05  FILLER                      PIC X(43) VALUE SPACES.              
           05  FILLER                      PIC X(48) VALUE                      
               'DEPARTMENTS WHERE CRITERIA HAS NOT BEEN ASSIGNED'.              
           05  FILLER                      PIC X(41) VALUE SPACES.              
                                                                                
       01  DH-CORP-LINE.                                                        
           05  DH-DATA.                                                         
               10  FILLER                  PIC X(23)                            
                   VALUE 'CORP VARIANCE DEFAULT: '.                             
               10  DH-CORP-DEFAULT         PIC ZZZZ.9.                          
               10  DH-CORP-DEFAULT-RED REDEFINES                                
                   DH-CORP-DEFAULT         PIC X(06).                           
               10  FILLER                  PIC X(01)  VALUE SPACE.              
               10  DH-MEASURE              PIC X(05).                           
                   88  DH-MEASURE-PCT   VALUE '%    '.                          
                   88  DH-MEASURE-QTY   VALUE 'UNITS'.                          
                   88  DH-MEASURE-NONE  VALUE 'NONE '.                          
                                                                                
       01  RH-REPORT-COLUMN-HEADING.                                            
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(33)                                   
                   VALUE 'DEPT   CLASS     PCT   QTY       '.                   
           05  FILLER               PIC X(33)                                   
                   VALUE 'DEPT   CLASS     PCT   QTY       '.                   
           05  FILLER               PIC X(33)                                   
                   VALUE 'DEPT   CLASS     PCT   QTY       '.                   
           05  FILLER               PIC X(31)                                   
                   VALUE 'DEPT   CLASS     PCT   QTY     '.                     
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  DL-DATA.                                                         
               10  FILLER                  PIC X(03)  VALUE SPACE.              
               10  DL1-DEPT                PIC X(03).                           
               10  FILLER                  PIC X(04)  VALUE SPACE.              
               10  DL1-MCL                 PIC X(02).                           
               10  FILLER                  PIC X(05)  VALUE SPACE.              
               10  DL1-PCT                 PIC Z(03).Z    VALUE ZEROES.         
               10  DL1-PCTX REDEFINES                                           
                   DL1-PCT                 PIC X(05).                           
               10  FILLER                  PIC X(02)  VALUE SPACE.              
               10  DL1-QTY                 PIC ZZZZ   VALUE ZEROES.             
               10  DL1-QTYX REDEFINES                                           
                   DL1-QTY                 PIC X(04).                           
               10  FILLER                  PIC X(08)  VALUE SPACE.              
               10  DL2-DEPT                PIC X(03).                           
               10  FILLER                  PIC X(04)  VALUE SPACE.              
               10  DL2-MCL                 PIC X(02).                           
               10  FILLER                  PIC X(05)  VALUE SPACE.              
               10  DL2-PCT                 PIC Z(03).Z    VALUE ZEROES.         
               10  DL2-PCTX REDEFINES                                           
                   DL2-PCT                 PIC X(05).                           
               10  FILLER                  PIC X(02)  VALUE SPACE.              
               10  DL2-QTY                 PIC ZZZZ   VALUE ZEROES.             
               10  DL2-QTYX REDEFINES                                           
                   DL2-QTY                 PIC X(04).                           
               10  FILLER                  PIC X(08)  VALUE SPACE.              
               10  DL3-DEPT                PIC X(03).                           
               10  FILLER                  PIC X(04)  VALUE SPACE.              
               10  DL3-MCL                 PIC X(02).                           
               10  FILLER                  PIC X(05)  VALUE SPACE.              
               10  DL3-PCT                 PIC Z(03).Z   VALUE ZEROES.          
               10  DL3-PCTX REDEFINES                                           
                   DL3-PCT                 PIC X(05).                           
               10  FILLER                  PIC X(02)  VALUE SPACE.              
               10  DL3-QTY                 PIC ZZZZ   VALUE ZEROES.             
               10  DL3-QTYX REDEFINES                                           
                   DL3-QTY                 PIC X(04).                           
               10  FILLER                  PIC X(08)  VALUE SPACE.              
               10  DL4-DEPT                PIC X(04).                           
               10  FILLER                  PIC X(03)  VALUE SPACE.              
               10  DL4-MCL                 PIC X(02).                           
               10  FILLER                  PIC X(05)  VALUE SPACE.              
               10  DL4-PCT                 PIC Z(03).Z    VALUE ZEROES.         
               10  DL4-PCTX REDEFINES                                           
                   DL4-PCT                 PIC X(05).                           
               10  FILLER                  PIC X(02)  VALUE SPACE.              
               10  DL4-QTY                 PIC ZZZZ   VALUE ZEROES.             
               10  DL4-QTYX REDEFINES                                           
                   DL4-QTY                 PIC X(04).                           
               10  FILLER                  PIC X(06)  VALUE SPACE.              
           05  DL-NO-DETAIL-MESSAGE.                                            
               10  FILLER                  PIC X(39)                            
                   VALUE '*** NO RECORDS MATCH SELECTION MADE ***'.             
               10  FILLER                  PIC X(93)  VALUE SPACES.             
                                                                                
       01  DL-TOTAL-LINE.                                                       
           05  FILLER                      PIC X(100) VALUE SPACES.             
           05  FILLER                      PIC X(23) VALUE                      
               'RECORDS SELECTED. . . .'.                                       
           05  DL-RECORD-COUNT             PIC ZZ,ZZ9.                          
           05  FILLER                      PIC X(03) VALUE SPACES.              
                                                                                
       01  RE-END-OF-REPORT-MARKER.                                             
           05  FILLER                      PIC X(51) VALUE ALL '*'.             
           05  FILLER                      PIC X(29) VALUE                      
               '  E N D   O F   R E P O R T  '.                                 
           05  FILLER                      PIC X(51) VALUE ALL '*'.             
                                                                                
      *                                                                         
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
           COPY DPWS132O.                                                       
                                                                                
      ****************************************************                      
      *  COMMUNICATION AREAS FOR DB2                     *                      
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ****************************************************                      
      *  MERCHANDISE AREA TABLE                                                 
      ****************************************************                      
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      *---------------------------------------------------------------* INKCS048
      *    TMDSEAR CURSOR TO SELECT DEPT WHERE CRITERIA HAS BEEN SETUP* INKCS048
      *---------------------------------------------------------------* INKCS048
                                                                        INKCS048
           EXEC SQL                                                     INKCS048
               DECLARE CRITERIA_CSR CURSOR FOR                          INKCS048
               SELECT DEPT_NBR                                                  
                     ,MAJ_CL_NBR                                                
CRIT                 ,VALUE (PHY_INV_OH_VAR_PCT,0)                              
CRIT                 ,VALUE (PHY_INV_OH_VAR_QTY,0)                              
                FROM  TMDSEAR                                           INKCS048
                WHERE STRT_DTE     <= CURRENT DATE                              
                  AND END_DTE       = '9999-09-09'                              
                  AND (PHY_INV_OH_VAR_PCT IS NOT NULL OR                        
                      PHY_INV_OH_VAR_QTY  IS NOT NULL)                          
                  AND DEPT_NBR     >  '000'                                     
TEMP **           AND SUB_CL_NBR   =  '000'                                     
TEMP              AND MDSELV_CDE IN ('DPT','MCL')                               
                ORDER BY DEPT_NBR                                               
                        ,MAJ_CL_NBR                                     INKCS048
           END-EXEC.                                                    INKCS048
                                                                        INKCS048
      *---------------------------------------------------------------* INKCS048
      * TMDSEAR CURSOR TO SELECT DEPT WHERE NO CRITERIA HAS BEEN SETUP* INKCS048
      *---------------------------------------------------------------* INKCS048
                                                                        INKCS048
           EXEC SQL                                                     INKCS048
               DECLARE NO_CRIT_CSR CURSOR FOR                           INKCS048
               SELECT DISTINCT DEPT_NBR                                         
                     ,MAJ_CL_NBR                                                
NOCRIT               ,VALUE (PHY_INV_OH_VAR_PCT,0)                              
NOCRIT               ,VALUE (PHY_INV_OH_VAR_QTY,0)                              
                FROM  TMDSEAR                                           INKCS048
                WHERE STRT_DTE     <= CURRENT DATE                              
                  AND END_DTE       = '9999-09-09'                              
                  AND (PHY_INV_OH_VAR_PCT IS NULL AND                           
                      PHY_INV_OH_VAR_QTY  IS NULL)                              
                  AND DEPT_NBR     >  '000'                                     
TEMP  *           AND SUB_CL_NBR   =  '000'                                     
TEMP              AND MDSELV_CDE IN ('DPT','MCL')                               
                ORDER BY DEPT_NBR                                               
                        ,MAJ_CL_NBR                                     INKCS048
           END-EXEC.                                                    INKCS048
                                                                        INKCS048
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       1000-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM 1100-HOUSEKEEPING.                                           
                                                                                
           IF CC-CRITERIA-ASSIGNED                                              
               PERFORM 7000-PROCESS-CRITERIA-ASSIGNED                           
           ELSE                                                                 
               IF CC-NO-CRITERIA-ASSIGNED                                       
                   PERFORM 7500-PROCESS-NO-CRITERIA                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 8300-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      *                                                                         
      *                                                                         
      *                                                                         
       1100-HOUSEKEEPING.                                                       
      ******************************************************************        
      * INITIALIZE TABLES.                                                      
      * READ CONTROL-CARD INPUT                                                 
      ******************************************************************        
                                                                                
           OPEN INPUT  CONTROL-FILE                                             
                OUTPUT CRITERIA-REPORT.                                         
                                                                                
           INITIALIZE TB-C1-REPORT-TABLE                                        
                      TB-C2-REPORT-TABLE                                        
                      TB-C3-REPORT-TABLE                                        
                      TB-C4-REPORT-TABLE.                                       
                                                                                
           MOVE ZEROES TO PV-SUB1.                                              
                                                                                
           PERFORM 1200-EDIT-INPUT-CONTROL-CARD.                                
                                                                                
      *    PERFORM 1250-BUILD-JCL.                                              
                                                                                
                                                                                
      *    *************************************************************        
      *    **  PRINT THE REPORT HEADINGS BASED ON CRITERIA SPECIFIED            
      *    *************************************************************        
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
                                                                                
           MOVE PV-MM           TO DP132O-RUN-MONTH.                            
           MOVE PV-DD           TO DP132O-RUN-DAY.                              
           MOVE PV-YY           TO DP132O-RUN-YEAR.                             
           MOVE PV-HR           TO DP132O-RUN-HOUR.                             
           MOVE PV-MIN          TO DP132O-RUN-MINUTE.                           
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
           MOVE PC-RPT-VERSION  TO DP132O-REPORT-NUMBER-CHAR.                   
                                                                                
      * RETRIEVE DEFAULT CORPORATE VALUE AND PRINT ON FIRST PAGE                
           PERFORM 7940-PROCESS-CORP-CRITERIA.                                  
                                                                                
           PERFORM 8200-PRINT-HEADINGS.                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *    READ INPUT CONTROL CARD                                              
      ******************************************************************        
       1200-EDIT-INPUT-CONTROL-CARD.                                            
                                                                                
           READ CONTROL-FILE                                                    
               INTO CC-CONTROL-CARD                                             
                AT END                                                          
                SET PS-END-OF-CC-INPUT TO TRUE                                  
           END-READ.                                                            
                                                                                
           IF PS-END-OF-CC-INPUT                                                
               DISPLAY '**********************'                                 
               DISPLAY '***  INPUT CONTROL CARD IS EMPTY'                       
               DISPLAY '**********************'                                 
               MOVE '1200-EDIT-INPUT-CONTROL-CARD' TO PV-PARAGRAPH-NAME         
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
           DISPLAY '**********************'                                     
           DISPLAY '** INPUT CONTROL CARD'                                      
           DISPLAY '** CC: ' CC-CONTROL-CARD, ' (CRITERIA PROCESS TYPE)'        
           DISPLAY '**********************'                                     
                                                                                
           IF CC-VALID-INPUT                                                    
               MOVE CC-CRITERIA-IND TO PV-CRITERIA-IND                          
               IF CC-CRITERIA-ASSIGNED                                          
                   SET PC-RPT-CRITERIA TO TRUE                                  
               ELSE                                                             
                   SET PC-RPT-NO-CRITERIA TO TRUE                               
               END-IF                                                           
           ELSE                                                                 
               MOVE '1200-EDIT-INPUT-CONTROL-CARD' TO PV-PARAGRAPH-NAME         
               PERFORM Z999-ABEND                                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * PROCESS ALL RECORDS THAT HAVE HAD EXCEPTION CRITERIA ESTABLISHED        
      ******************************************************************        
       7000-PROCESS-CRITERIA-ASSIGNED.                                          
                                                                                
           PERFORM 7100-OPEN-CRITERIA-ASSIGNED.                                 
           PERFORM 7150-FETCH-CRITERIA-ASSIGNED.                                
           IF PS-END-OF-CURSOR-RECORDS                                          
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 7200-PROCESS-SELECTED-RECORDS                            
                   UNTIL PS-END-OF-CURSOR-RECORDS                               
               PERFORM 7400-CLOSE-CRITERIA-CURSOR                               
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * OPEN CURSOR CONTAINING EXCEPTION RECORDS WITH ASSIGNED VALUES           
      ******************************************************************        
       7100-OPEN-CRITERIA-ASSIGNED.                                             
      *                                                                         
           EXEC SQL                                                             
               OPEN CRITERIA_CSR                                                
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 MOVE '7100-OPEN-CRITERIA-ASSIGNED' TO PV-PARAGRAPH-NAME        
                 MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7150-FETCH-CRITERIA-ASSIGNED.                                            
                                                                                
      *    INITIALIZE DCLTMDSEAR.                                               
                                                                                
           EXEC SQL                                                             
              FETCH CRITERIA_CSR                                        INKCS048
               INTO  :MDSEAR-DEPT-NBR                                           
                    ,:MDSEAR-SUB-CL-NBR                                         
                    ,:MDSEAR-PHY-INV-OH-VAR-PCT                                 
                    ,:MDSEAR-PHY-INV-OH-VAR-QTY                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
                 ADD +1 TO PV-RECORD-COUNT                                      
             WHEN SQLCODE = +100                                                
                 SET PS-END-OF-CURSOR-RECORDS TO TRUE                           
             WHEN OTHER                                                         
                 MOVE 'FETCH TMDSEAR CURSOR'                                    
                               TO  PV-OPERATION-ATTEMPTED                       
                 MOVE '7150-FETCH-CRITERIA-ASSIGNED'                            
                      TO PV-PARAGRAPH-NAME                                      
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7200-PROCESS-SELECTED-RECORDS.                                           
      *                                                                         
           MOVE +1 TO PV-SUB1.                                                  
                                                                                
           PERFORM 7300-LOAD-CURSOR-ENTRIES.                                    
      *                                                                         
      *    MOVE MDSEAR-DEPT-NBR TO PV-HOLD-DEPT-NBR.                            
      *    MOVE MDSEAR-SUB-CL-NBR TO PV-HOLD-CLASS-NBR.                         
      *                                                                         
           PERFORM 8000-WRITE-REPORT-RECORDS.                                   
      *                                                                         
           INITIALIZE TB-C1-REPORT-TABLE                                        
                      TB-C2-REPORT-TABLE                                        
                      TB-C3-REPORT-TABLE                                        
                      TB-C4-REPORT-TABLE.                                       
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7300-LOAD-CURSOR-ENTRIES.                                                
      * LOAD COLUMN1                                                            
           IF PS-MORE-CURSOR-RECORDS                                            
               PERFORM                                                          
                 UNTIL ((PV-SUB1 > PC-ROW-ENTRIES)  OR                          
                         PS-END-OF-CURSOR-RECORDS)                              
                   MOVE MDSEAR-DEPT-NBR           TO                            
                                                 TB-C1-DEPT-NBR(PV-SUB1)        
                   MOVE MDSEAR-SUB-CL-NBR         TO                            
                                                 TB-C1-MCL-NBR (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-PCT TO                            
                                                 TB-C1-VAR-PCT (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-QTY TO                            
                                                 TB-C1-VAR-QTY (PV-SUB1)        
                   ADD +1 TO PV-SUB1                                            
                   IF CC-CRITERIA-ASSIGNED                                      
                       PERFORM 7150-FETCH-CRITERIA-ASSIGNED                     
                   ELSE                                                         
                       PERFORM 7700-FETCH-NO-CRITERIA-RECORDS                   
                   END-IF                                                       
               END-PERFORM                                                      
               MOVE +1 TO PV-SUB1                                               
           END-IF.                                                              
                                                                                
      * LOAD COLUMN2                                                            
           IF PS-MORE-CURSOR-RECORDS                                            
               PERFORM                                                          
                 UNTIL ((PV-SUB1 > PC-ROW-ENTRIES)  OR                          
                         PS-END-OF-CURSOR-RECORDS)                              
                   MOVE MDSEAR-DEPT-NBR           TO                            
                                                 TB-C2-DEPT-NBR(PV-SUB1)        
                   MOVE MDSEAR-SUB-CL-NBR         TO                            
                                                 TB-C2-MCL-NBR (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-PCT TO                            
                                                 TB-C2-VAR-PCT (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-QTY TO                            
                                                 TB-C2-VAR-QTY (PV-SUB1)        
                   ADD +1 TO PV-SUB1                                            
                   IF CC-CRITERIA-ASSIGNED                                      
                       PERFORM 7150-FETCH-CRITERIA-ASSIGNED                     
                   ELSE                                                         
                       PERFORM 7700-FETCH-NO-CRITERIA-RECORDS                   
                   END-IF                                                       
               END-PERFORM                                                      
               MOVE +1 TO PV-SUB1                                               
           END-IF.                                                              
                                                                                
      * LOAD COLUMN3                                                            
           IF PS-MORE-CURSOR-RECORDS                                            
               PERFORM                                                          
                 UNTIL ((PV-SUB1 > PC-ROW-ENTRIES)  OR                          
                         PS-END-OF-CURSOR-RECORDS)                              
                   MOVE MDSEAR-DEPT-NBR           TO                            
                                                 TB-C3-DEPT-NBR(PV-SUB1)        
                   MOVE MDSEAR-SUB-CL-NBR         TO                            
                                                 TB-C3-MCL-NBR (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-PCT TO                            
                                                 TB-C3-VAR-PCT (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-QTY TO                            
                                                 TB-C3-VAR-QTY (PV-SUB1)        
                   ADD +1 TO PV-SUB1                                            
                   IF CC-CRITERIA-ASSIGNED                                      
                       PERFORM 7150-FETCH-CRITERIA-ASSIGNED                     
                   ELSE                                                         
                       PERFORM 7700-FETCH-NO-CRITERIA-RECORDS                   
                   END-IF                                                       
               END-PERFORM                                                      
               MOVE +1 TO PV-SUB1                                               
           END-IF.                                                              
                                                                                
      * LOAD COLUMN4                                                            
           IF PS-MORE-CURSOR-RECORDS                                            
               PERFORM                                                          
                 UNTIL ((PV-SUB1 > PC-ROW-ENTRIES)  OR                          
                         PS-END-OF-CURSOR-RECORDS)                              
                   MOVE MDSEAR-DEPT-NBR           TO                            
                                                 TB-C4-DEPT-NBR(PV-SUB1)        
                   MOVE MDSEAR-SUB-CL-NBR         TO                            
                                                 TB-C4-MCL-NBR (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-PCT TO                            
                                                 TB-C4-VAR-PCT (PV-SUB1)        
                   MOVE MDSEAR-PHY-INV-OH-VAR-QTY TO                            
                                                 TB-C4-VAR-QTY (PV-SUB1)        
                   ADD +1 TO PV-SUB1                                            
                   IF CC-CRITERIA-ASSIGNED                                      
                       PERFORM 7150-FETCH-CRITERIA-ASSIGNED                     
                   ELSE                                                         
                       PERFORM 7700-FETCH-NO-CRITERIA-RECORDS                   
                   END-IF                                                       
               END-PERFORM                                                      
               MOVE +1 TO PV-SUB1                                               
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * CLOSE CURSOR                                                            
      ******************************************************************        
       7400-CLOSE-CRITERIA-CURSOR.                                              
      *                                                                         
           EXEC SQL                                                             
               CLOSE CRITERIA_CSR                                               
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 MOVE '7400-CLOSE-CRITERIA-CURSOR'  TO PV-PARAGRAPH-NAME        
                 MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
                                                                                
      *                                                                         
      ******************************************************************        
      * PROCESS SELECTIONS WITH NO VARIANCE EXCEPTION CRITERIA ASSIGNED         
      ******************************************************************        
       7500-PROCESS-NO-CRITERIA.                                                
      *                                                                         
               PERFORM 7600-OPEN-NO-CRITERIA-ASSIGNED                           
               PERFORM 7700-FETCH-NO-CRITERIA-RECORDS                           
               IF PS-END-OF-CURSOR-RECORDS                                      
                   WRITE REPORT-LINE FROM DL-NO-DETAIL-MESSAGE                  
               ELSE                                                             
                   PERFORM 7200-PROCESS-SELECTED-RECORDS                        
                       UNTIL PS-END-OF-CURSOR-RECORDS                           
                   PERFORM 7900-CLOSE-NO-CRITERIA-ASSIGN                        
               END-IF.                                                          
      *                                                                         
      *                                                                         
      ******************************************************************        
      * OPEN CURSOR CONTAINING EXCEPTION RECORDS WITH ASSIGNED VALUES           
      ******************************************************************        
       7600-OPEN-NO-CRITERIA-ASSIGNED.                                          
      *                                                                         
           EXEC SQL                                                             
               OPEN NO_CRIT_CSR                                                 
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 MOVE '7600-OPEN-NO-CRITERIA-ASSIGNED' TO                       
                                                       PV-PARAGRAPH-NAME        
                 MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
      *                                                                         
      ******************************************************************        
      *                                                                         
      ******************************************************************        
       7700-FETCH-NO-CRITERIA-RECORDS.                                          
                                                                                
      *    INITIALIZE DCLTMDSEAR.                                               
                                                                                
           EXEC SQL                                                             
              FETCH NO_CRIT_CSR                                         INKCS048
               INTO  :MDSEAR-DEPT-NBR                                           
                    ,:MDSEAR-SUB-CL-NBR                                         
-->                 ,:MDSEAR-PHY-INV-OH-VAR-PCT                                 
-->                 ,:MDSEAR-PHY-INV-OH-VAR-QTY                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = +0                                                  
                 ADD +1 TO PV-RECORD-COUNT                                      
             WHEN SQLCODE = +100                                                
                 SET PS-END-OF-CURSOR-RECORDS TO TRUE                           
             WHEN OTHER                                                         
                 MOVE 'FETCH TMDSEAR CURSOR'                                    
                               TO  PV-OPERATION-ATTEMPTED                       
                 MOVE '7700-FETCH-NO-CRITERIA-RECORDS'                          
                      TO PV-PARAGRAPH-NAME                                      
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * CLOSE                                                                   
      ******************************************************************        
       7900-CLOSE-NO-CRITERIA-ASSIGN.                                           
      *                                                                         
           EXEC SQL                                                             
               CLOSE NO_CRIT_CSR                                                
           END-EXEC                                                             
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                 CONTINUE                                                       
               WHEN OTHER                                                       
                 MOVE '7900-CLOSE-NO-CRITERIA-ASSIGN' TO                        
                                                       PV-PARAGRAPH-NAME        
                 MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED                       
                 PERFORM 9999-SQL-ABEND                                         
           END-EVALUATE.                                                        
      *                                                                         
      ******************************************************************        
      * PROCESS ROW CONTAINING DEFAULT CORPORATE CRITERIA               INKCS048
      ******************************************************************        
      *                                                                         
       7940-PROCESS-CORP-CRITERIA.                                      INKCS048
                                                                        INKCS048
           EXEC SQL                                                     INKCS048
               SELECT VALUE(PHY_INV_OH_VAR_PCT,0)                               
                     ,VALUE(PHY_INV_OH_VAR_QTY,0)                               
               INTO   :MDSEAR-PHY-INV-OH-VAR-PCT                                
                     ,:MDSEAR-PHY-INV-OH-VAR-QTY                                
                FROM  TMDSEAR                                           INKCS048
                WHERE OPERCO_NBR    = '03'                                      
                  AND MDSELV_CDE    = 'CO'                                      
                  AND STRT_DTE     <= CURRENT DATE                              
                  AND END_DTE       = '9999-09-09'                              
           END-EXEC                                                     INKCS048
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    PERFORM 7950-PREPARE-CORPORATE-RECORD                       
               WHEN OTHER                                                       
                  MOVE '7940-PROCESS-CORP-CRITERIA'                             
                      TO PV-PARAGRAPH-NAME                                      
                  MOVE PE-MSG-03 TO PV-OPERATION-ATTEMPTED                      
                  PERFORM 9999-SQL-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * WRITE CORPORATE CRITERIA DEFAULT TO DETAIL REPORT.                      
      ******************************************************************        
       7950-PREPARE-CORPORATE-RECORD.                                           
                                                                                
           IF (MDSEAR-PHY-INV-OH-VAR-PCT  > 0   OR                              
               MDSEAR-PHY-INV-OH-VAR-PCT  < 0)                                  
               MOVE MDSEAR-PHY-INV-OH-VAR-PCT  TO PV-CORP-VAR-PCT               
               MOVE PV-CORP-VAR-PCT            TO DH-CORP-DEFAULT               
               MOVE ZEROES                     TO PV-CORP-VAR-QTY               
               SET  DH-MEASURE-PCT             TO TRUE                          
           ELSE                                                                 
               IF (MDSEAR-PHY-INV-OH-VAR-QTY  > 0   OR                          
                   MDSEAR-PHY-INV-OH-VAR-QTY  < 0)                              
                   MOVE MDSEAR-PHY-INV-OH-VAR-QTY  TO PV-CORP-VAR-QTY           
                   MOVE PV-CORP-VAR-QTY            TO DH-CORP-DEFAULT           
                   MOVE ZEROES                     TO PV-CORP-VAR-PCT           
                   SET  DH-MEASURE-QTY             TO TRUE                      
               ELSE                                                             
                   MOVE SPACES TO DH-MEASURE                                    
                   SET  DH-MEASURE-NONE TO TRUE                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE ZEROES  TO MDSEAR-PHY-INV-OH-VAR-PCT                            
                           MDSEAR-PHY-INV-OH-VAR-QTY.                           
                                                                                
      *                                                                         
      *                                                                         
      ******************************************************************        
      * RECORDS FROM STORED TABLES ARE WRITTEN TO REPORT                        
      *   IF 'NO CRITERIA' PROCESSING MOVE '----' TO PCT AND QTY FIELDS         
      ******************************************************************        
       8000-WRITE-REPORT-RECORDS.                                               
                                                                                
      *                                                                         
           MOVE +1 TO PV-SUB1.                                                  
      *                                                                         
           PERFORM UNTIL PV-SUB1 > PC-ROW-ENTRIES                               
      *                                                                         
               MOVE TB-C1-DEPT-NBR(PV-SUB1) TO DL1-DEPT                         
               MOVE TB-C1-MCL-NBR (PV-SUB1) TO DL1-MCL                          
               IF TB-C1-DEPT-NBR(PV-SUB1) = SPACES                              
                   MOVE SPACES TO DL1-PCTX                                      
                                  DL1-QTYX                                      
               ELSE                                                             
                   IF PC-RPT-NO-CRITERIA                                        
                       MOVE PC-DASH TO DL1-PCTX                                 
                                       DL1-QTYX                                 
                   ELSE                                                         
                       MOVE TB-C1-VAR-PCT (PV-SUB1) TO DL1-PCT                  
                       MOVE TB-C1-VAR-QTY (PV-SUB1) TO DL1-QTY                  
                   END-IF                                                       
               END-IF                                                           
      *****                                                                     
               MOVE TB-C2-DEPT-NBR(PV-SUB1) TO DL2-DEPT                         
               MOVE TB-C2-MCL-NBR (PV-SUB1) TO DL2-MCL                          
               IF TB-C2-DEPT-NBR(PV-SUB1) = SPACES                              
                   MOVE SPACES TO DL2-PCTX                                      
                                  DL2-QTYX                                      
               ELSE                                                             
                   IF PC-RPT-NO-CRITERIA                                        
                       MOVE PC-DASH TO DL2-PCTX                                 
                                       DL2-QTYX                                 
                   ELSE                                                         
                       MOVE TB-C2-VAR-PCT (PV-SUB1) TO DL2-PCT                  
                       MOVE TB-C2-VAR-QTY (PV-SUB1) TO DL2-QTY                  
                   END-IF                                                       
               END-IF                                                           
      *****                                                                     
               MOVE TB-C3-DEPT-NBR(PV-SUB1) TO DL3-DEPT                         
               MOVE TB-C3-MCL-NBR (PV-SUB1) TO DL3-MCL                          
               IF TB-C3-DEPT-NBR(PV-SUB1) = SPACES                              
                   MOVE SPACES TO DL3-PCTX                                      
                                  DL3-QTYX                                      
               ELSE                                                             
                   IF PC-RPT-NO-CRITERIA                                        
                       MOVE PC-DASH TO DL3-PCTX                                 
                                       DL3-QTYX                                 
                   ELSE                                                         
                       MOVE TB-C3-VAR-PCT (PV-SUB1) TO DL3-PCT                  
                       MOVE TB-C3-VAR-QTY (PV-SUB1) TO DL3-QTY                  
                   END-IF                                                       
               END-IF                                                           
      *****                                                                     
               MOVE TB-C4-DEPT-NBR(PV-SUB1) TO DL4-DEPT                         
               MOVE TB-C4-MCL-NBR (PV-SUB1) TO DL4-MCL                          
               IF TB-C4-DEPT-NBR(PV-SUB1) = SPACES                              
                   MOVE SPACES TO DL4-PCTX                                      
                                  DL4-QTYX                                      
               ELSE                                                             
                   IF PC-RPT-NO-CRITERIA                                        
                       MOVE PC-DASH TO DL4-PCTX                                 
                                       DL4-QTYX                                 
                   ELSE                                                         
                       MOVE TB-C4-VAR-PCT (PV-SUB1) TO DL4-PCT                  
                       MOVE TB-C4-VAR-QTY (PV-SUB1) TO DL4-QTY                  
                   END-IF                                                       
               END-IF                                                           
      *****                                                                     
               WRITE REPORT-LINE FROM DL-DETAIL-LINE                            
               ADD +1 TO PV-SUB1                                                
           END-PERFORM.                                                         
                                                                                
           IF NOT PS-END-OF-CURSOR-RECORDS                                      
               PERFORM 8200-PRINT-HEADINGS.                                     
      *                                                                         
      *                                                                         
       8200-PRINT-HEADINGS.                                                     
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      *    ALSO OBTAIN AND PRINT THE CORPORATE 'DEFAULT' CRITERIA RECORD        
      ******************************************************************        
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP132O-PAGE-NUMBER.                            
           WRITE REPORT-LINE FROM DP132O-STANDRD-HEADER-132-COLS                
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-LINE FROM RH-REPORT-HEADING-ONE                         
                   AFTER ADVANCING 2 LINES.                                     
                                                                                
           IF CC-CRITERIA-ASSIGNED                                              
               WRITE REPORT-LINE FROM RH-REPORT-HEADING-TWO-A                   
                   AFTER ADVANCING 1 LINE                                       
           ELSE                                                                 
               WRITE REPORT-LINE FROM RH-REPORT-HEADING-TWO-B                   
                   AFTER ADVANCING 1 LINE                                       
           END-IF.                                                              
                                                                                
           WRITE REPORT-LINE FROM DH-CORP-LINE                                  
                   AFTER ADVANCING 1 LINES.                                     
                                                                                
           ADD +1 TO PV-LINE-COUNT.                                             
      * PRINT COLUMN HEADINGS                                                   
           WRITE REPORT-LINE FROM RH-REPORT-COLUMN-HEADING                      
                   AFTER ADVANCING 2 LINES.                                     
                                                                                
           MOVE 9 TO PV-LINE-COUNT.                                             
      *                                                                         
      *                                                                         
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
       8300-EOJ.                                                                
                                                                                
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
               PERFORM 8200-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE PV-RECORD-COUNT TO DL-RECORD-COUNT.                             
                                                                                
           WRITE REPORT-LINE FROM DL-TOTAL-LINE                                 
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           WRITE REPORT-LINE FROM RE-END-OF-REPORT-MARKER                       
               AFTER ADVANCING 2 LINES.                                         
                                                                                
           CLOSE   CRITERIA-REPORT.                                             
           DISPLAY '*****************************************'.                 
      *                                                                         
      *                                                                         
      *                                                                         
      ******************************************************************        
      *  THIS ROUTINE FORCES AN ABEND DUE TO A NON-DB2 RELATED PROBLEM.         
      ******************************************************************        
       Z999-ABEND.                                                              
                                                                                
           DISPLAY '**  ABEND     **'.                                          
           MOVE +4003                  TO ABEND-CODE.                           
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
                                                                                
      *                                                                         
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  = ' PC-PROGRAM-NAME.                      
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.                    
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.               
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
