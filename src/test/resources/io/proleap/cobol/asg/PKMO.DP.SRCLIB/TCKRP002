      ***************************************************************** 00010002
       IDENTIFICATION DIVISION.                                         00020002
      ***************************************************************** 00030002
                                                                        00040002
       PROGRAM-ID.             TCKRP002.                                00050002
       AUTHOR.                 CHRISTOPHER CLARK.                       00060002
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070002
       DATE-WRITTEN            FEBRUARY 2002.                           00080002
       DATE-COMPILED.                                                   00090002
                                                                        00100002
      ***************************************************************** 00110002
      *                                                               * 00120002
      *  THIS PROGRAM CREATES THE DAILY DEBIT CARD TOTALS REPORT.     * 00130002
      *                                                               * 00140002
      *   INPUT: INP01 - CONTAINS POS DATE, WHICH IS USED AS THE      * 00150002
      *                  PROCESS DATE TO CREATE THE REPORT.           * 00160002
      *          INP02 - CONTAINS DEBIT CARD TRANSACTIONS FOR THE PAST* 00170002
      *                  TWO DAYS SORTED BY STORE NUMBER, DATE, TIME, * 00180002
      *                  REGISTER, AND TRANSACTION NUMBER.            * 00190002
      *  OUTPUT: RPT01 - REPORT SHOWING STORE TOTALS FOR ALL DEBIT    * 00200002
      *                  CARDS PROCESSED FROM INPUT FILE.             * 00210002
      *                                                               * 00220002
      ***************************************************************** 00230002
      *                                                               * 00240002
      *  THE PROGRAM READS A CONTROL CARD FILE TO GET THE POS DATE.   * 00250002
      *                                                               * 00260002
      *  WRITE TOTALS FOR EACH STORE BY DAY TO THE TOTALS REPORT.     * 00270002
      *                                                               * 00280002
      ***************************************************************** 00290002
      *  PLEASE SEE PROGRAM DOC FOR A DESCRIPTION OF ALL UPDATES.     * 00300002
      ***************************************************************** 00310002
C26614*  10/23/2015  CHG0126614 - COGNIZANT                           * 00311003
C26614*  MODIFIED THE RECORD LENGTH OF DBT-CRD-INPUT-FILE             * 00312003
C26614*           FROM 69 TO 72.                                      * 00313003
C26614***************************************************************** 00314003
       ENVIRONMENT DIVISION.                                            00320002
      ***************************************************************** 00330002
                                                                        00340002
       CONFIGURATION SECTION.                                           00350002
                                                                        00360002
       SOURCE-COMPUTER.        IBM-3090.                                00370002
                                                                        00380002
       OBJECT-COMPUTER.        IBM-3090.                                00390002
                                                                        00400002
       INPUT-OUTPUT SECTION.                                            00410002
                                                                        00420002
       FILE-CONTROL.                                                    00430002
                                                                        00440002
           SELECT POS-DATE-CARD-FILE                                    00450002
               ASSIGN TO INP01.                                         00460002
                                                                        00470002
           SELECT DBT-CRD-INPUT-FILE                                    00480002
               ASSIGN TO INP02.                                         00490002
                                                                        00500002
           SELECT UNMTCH-NEW-FILE                                       00510002
               ASSIGN TO INP03.                                         00520002
                                                                        00530002
           SELECT DBT-CRD-AUDIT-REPORT-FILE                             00540002
               ASSIGN TO RPT01.                                         00550002
                                                                        00560002
       EJECT                                                            00570002
      ******************************************************************00580002
       DATA DIVISION.                                                   00590002
      ******************************************************************00600002
                                                                        00610002
       FILE SECTION.                                                    00620002
                                                                        00630002
       FD  POS-DATE-CARD-FILE                                           00640002
           RECORDING MODE IS F                                          00650002
           BLOCK CONTAINS 0 RECORDS                                     00660002
           LABEL RECORDS ARE STANDARD.                                  00670002
                                                                        00680002
       01  POS-DATE-CARD-RECORD            PIC X(80).                   00690002
                                                                        00700002
       FD  DBT-CRD-INPUT-FILE                                           00710002
           RECORDING MODE IS F                                          00720002
           BLOCK CONTAINS 0 RECORDS                                     00730002
           LABEL RECORDS ARE STANDARD.                                  00740002
                                                                        00750002
C26614 01  DBT-CRD-INPUT-RECORD            PIC X(72).                   00760003
                                                                        00770002
       FD  UNMTCH-NEW-FILE                                              00780002
           RECORDING MODE IS F                                          00790002
           BLOCK CONTAINS 0 RECORDS                                     00800002
           LABEL RECORDS ARE STANDARD.                                  00810002
                                                                        00820002
       01  UNMTCH-NEW-RECORD               PIC X(69).                   00830002
                                                                        00840002
       FD  DBT-CRD-AUDIT-REPORT-FILE                                    00850002
           RECORDING MODE IS F                                          00860002
           BLOCK CONTAINS 0  RECORDS.                                   00870002
                                                                        00880002
       01  DBT-CRD-AUDIT-REPORT-RECORD     PIC  X(132).                 00890002
                                                                        00900002
      ******************************************************************00910002
       WORKING-STORAGE SECTION.                                         00920002
      ******************************************************************00930002
                                                                        00940002
      ******************************************************************00950002
      * RECORD LAYOUT FOR INPUT DATE CARD (POS DATE)                   *00960002
      ******************************************************************00970002
       01  CC-CONTROL-CARD.                                             00980002
           05  CC-CONTROL-CARD-DISPLAY.                                 00990002
               10  CC-CONTROL-NAME     PIC X(14)    VALUE SPACE.        01000002
                   88  CC-VALID-CONTROL-NAME        VALUE               01010002
                       'P.O.S. DATE = '.                                01020002
               10  CC-POS-DATE-MMDDYY  PIC 9(6)     VALUE ZERO.         01030002
               10  FILLER REDEFINES CC-POS-DATE-MMDDYY.                 01040002
                   15  CC-POS-DATE-MM      PIC X(02).                   01050002
                   15  CC-POS-DATE-DD      PIC X(02).                   01060002
                   15  CC-POS-DATE-YY      PIC X(02).                   01070002
           05  FILLER                  PIC X(60)    VALUE SPACE.        01080002
                                                                        01090002
       01 CC-RFRM-POS-DATE.                                             01100002
           05  CC-RFRM-POS-DATE-CCYYMMDD   PIC 9(08)    VALUE ZERO.     01110002
           05  FILLER REDEFINES CC-RFRM-POS-DATE-CCYYMMDD.              01120002
               10  CC-RFRM-POS-DATE-CC     PIC X(02).                   01130002
               10  CC-RFRM-POS-DATE-YY     PIC X(02).                   01140002
               10  CC-RFRM-POS-DATE-MM     PIC X(02).                   01150002
               10  CC-RFRM-POS-DATE-DD     PIC X(02).                   01160002
                                                                        01170002
       01  SUBSCRIPTS.                                                  01180002
           05  SUB                         PIC S9(04) VALUE ZERO        01190002
                                                       COMP.            01200002
                                                                        01210002
      ******************************************************************01220002
      * DATE AND TIME FORMAT FOR RECORDS                               *01230002
      ******************************************************************01240002
       01  PV-DATE-FORMAT.                                              01250002
           05  PV-DATE-CCYYMMDD            PIC X(08)     VALUE SPACE.   01260002
           05  FILLER REDEFINES PV-DATE-CCYYMMDD.                       01270002
               10  PV-DATE-CCYY            PIC X(04).                   01280002
               10  PV-DATE-MM              PIC X(02).                   01290002
               10  PV-DATE-DD              PIC X(02).                   01300002
           05  PV-P-MMDDYY.                                             01310002
               10  PV-P-MM                 PIC 9(02)     VALUE ZEROES.  01320002
               10  PV-P-DD                 PIC 9(02)     VALUE ZEROES.  01330002
               10  PV-P-YY                 PIC 9(02)     VALUE ZEROES.  01340002
           05  PV-TABLE-DATE-CCYYMMDD.                                  01350002
               10  PV-TABLE-DATE-CCYY      PIC  9(04)    VALUE ZEROES.  01360002
               10  PV-TABLE-DATE-MM        PIC  9(02)    VALUE ZEROES.  01370002
               10  PV-TABLE-DATE-DD        PIC  9(02)    VALUE ZEROES.  01380002
                                                                        01390002
           05  PV-HOLD-CCYY.                                            01400002
               10  PV-HOLD-CC              PIC 9(02)     VALUE ZEROES.  01410002
               10  PV-HOLD-YY              PIC 9(02)     VALUE ZEROES.  01420002
                                                                        01430002
      ******************************************************************01440002
      *  PARAMETER LIST FOR THE CALENDAR ROUTINE (DPKUT500)            *01450002
      ******************************************************************01460002
           COPY DPWS500.                                                01470002
                                                                        01480002
      ******************************************************************01490002
      *   ABEND CODE - AND NUMBER FOR EACH POSSIBLE TYPE OF ABEND.     *01500002
      ******************************************************************01510002
       01  AC-ABEND-CODE                     PIC S9(04) VALUE ZERO      01520002
                                                        COMP SYNC.      01530002
           88  AC-CONTROL-CARD-ERROR                    VALUE +4003.    01540002
           88  AC-CALENDAR-ROUTINE-ERROR                VALUE +4019.    01550002
                                                                        01560002
      ******************************************************************01570002
      *   PROGRAM SWITCHES                                             *01580002
      ******************************************************************01590002
       01  PS-PROGRAM-SWITCHES.                                         01600002
           05  PS-END-OF-CARD-FILE-SW        PIC X(01)  VALUE 'N'.      01610002
               88  PS-END-OF-CARD-FILE                  VALUE 'Y'.      01620002
           05  PS-END-OF-INPUT-FILE-SW       PIC X(01)  VALUE 'N'.      01630002
               88  PS-END-OF-INPUT-FILE                 VALUE 'Y'.      01640002
           05  PS-FIRST-RECORD               PIC X(01)  VALUE 'Y'.      01650002
               88  PS-FIRST-RECORD-NO                   VALUE 'N'.      01660002
               88  PS-FIRST-RECORD-YES                  VALUE 'Y'.      01670002
           05  PS-FOUND-DATE-SW              PIC X(01)  VALUE 'Y'.      01680002
               88  PS-NOT-FOUND-DATE                    VALUE 'N'.      01690002
               88  PS-FOUND-DATE                        VALUE 'Y'.      01700002
           05  PS-END-OF-UNMTCH-FILE-SW      PIC X(01)  VALUE 'N'.      01710002
               88  PS-END-OF-UNMTCH-FILE                VALUE 'Y'.      01720002
                                                                        01730002
      ******************************************************************01740002
      *   PROGRAM CONSTANTS                                            *01750002
      ******************************************************************01760002
       01  PC-CONSTANTS.                                                01770002
           05  PC-CENTURY                  PIC X(02)     VALUE '20'.    01780002
           05  PC-LINE-LIMIT               PIC S9(04)    VALUE +60.     01790002
           05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE          01800002
                                                         'TCKRP002'.    01810002
           05  PC-REPORT-NBR-1             PIC 9(01)     VALUE 1.       01820002
           05  PC-EXCEPT-DAYS-NUMBER       PIC 9(02)     VALUE 30.      01830002
                                                                        01840002
      ******************************************************************01850002
      *   PROGRAM ACCUMULATERS.                                        *01860002
      ******************************************************************01870002
       01  PA-ACCUMULATORS                 COMP-3.                      01880002
           05  PA-PAGE-CNT                 PIC S9(05)    VALUE ZERO.    01890002
           05  PA-LINE-CNT                 PIC S9(03)    VALUE ZERO.    01900002
                                                                        01910002
      ******************************************************************01920002
      *   PROGRAM VARIABLES.                                           *01930002
      ******************************************************************01940002
       01  PV-PROGRAM-VARIABLES.                                        01950002
           05  PV-PRINT-RECORD             PIC X(200)    VALUE SPACE.   01960002
           05  PV-BLANK-RECORD             PIC X(200)    VALUE SPACE.   01970002
           05  PV-LINE-SPACING             PIC S9(04)    VALUE ZERO     01980002
                                                         COMP SYNC.     01990002
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   02000002
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   02010002
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   02020002
           05  PV-SAVE-TRAN-DATE           PIC  X(08)    VALUE SPACES.  02030002
           05  PV-SAVE-STR-NBR             PIC  S9(4)    COMP.          02040002
                                                                        02050002
           05  PV-STORE-GRAND-PROC-TOT     PIC S9(09)V99 VALUE ZERO.    02060002
           05  PV-STORE-GRAND-UNPROC-TOT   PIC S9(09)V99 VALUE ZERO.    02070002
           05  PV-STORE-GRAND-TOTAL-TOT    PIC S9(09)V99 VALUE ZERO.    02080002
                                                                        02090002
       01  PV-STORE-DAY-TOTAL.                                          02100002
           05  PV-STORE-DAY-TOTALS    OCCURS 30 TIMES                   02110002
                                      INDEXED BY PV-IDX.                02120002
               10  PV-DATE                 PIC X(08).                   02130002
               10  PV-STORE-DAY-PROC-TOT   PIC S9(09)V99.               02140002
               10  PV-STORE-DAY-UNPROC-TOT PIC S9(09)V99.               02150002
               10  PV-STORE-DAY-TOTAL-TOT  PIC S9(09)V99.               02160002
                                                                        02170002
       01  PV-CORP-DAY-TOTAL.                                           02180002
           05  PV-CORP-DAY-TOTALS     OCCURS 30 TIMES                   02190002
                                      INDEXED BY PV-CORP-IDX.           02200002
               10  PV-CORP-DATE            PIC X(08).                   02210002
               10  PV-CORP-DAY-PROC-TOT    PIC S9(09)V99.               02220002
               10  PV-CORP-DAY-UNPROC-TOT  PIC S9(09)V99.               02230002
               10  PV-CORP-DAY-TOTAL-TOT   PIC S9(09)V99.               02240002
                                                                        02250002
      ******************************************************************02260002
      *   DATE AND TIME FIELDS.                                        *02270002
      ******************************************************************02280002
       01  PV-DATE-AND-TIME-FIELDS.                                     02290002
           05  PV-POS-CCYY-MM-DD           PIC X(10)     VALUE SPACE.   02300002
           05  FILLER REDEFINES PV-POS-CCYY-MM-DD.                      02310002
               10  PV-POS-CCYY             PIC X(04).                   02320002
               10  FILLER                  PIC X(01).                   02330002
               10  PV-POS-MM               PIC X(02).                   02340002
               10  FILLER                  PIC X(01).                   02350002
               10  PV-POS-DD               PIC X(02).                   02360002
           05  PV-CURR-DATE-CCYYMMDD.                                   02370002
               10  PV-CURR-DATE-CCYY       PIC  9(04).                  02380002
               10  PV-CURR-DATE-MM         PIC  9(02).                  02390002
               10  PV-CURR-DATE-DD         PIC  9(02).                  02400002
           05  PV-CURR-TIME.                                            02410002
               10  PV-CURR-TIME-HH         PIC  9(02)    VALUE ZERO.    02420002
               10  PV-CURR-TIME-MM         PIC  9(02)    VALUE ZERO.    02430002
               10  PV-CURR-TIME-SSHH       PIC  9(04)    VALUE ZERO.    02440002
                                                                        02450002
      ******************************************************************02460002
      *  TOP OF PAGE HEADING LINE FOR PRINTING A 132 CHARACTER REPORT. *02470002
      ******************************************************************02480002
           COPY DPWS132.                                                02490002
                                                                        02500002
      ******************************************************************02510002
      *  HEADING LINES DAILY DEBIT CARD AUDIT REPORT.                  *02520002
      ******************************************************************02530002
       01  H1-TITLE-HEADING.                                            02540002
           05  FILLER                      PIC  X(49)    VALUE SPACES.  02550002
           05  H1-TITLE                    PIC  X(28)    VALUE          02560002
               'DAILY DEBIT CARD TOTALS FOR '.                          02570002
           05  H1-PROCESS-DTE.                                          02580002
               10  H1-MM                   PIC  X(02)    VALUE SPACES.  02590002
               10  FILLER                  PIC  X(01)    VALUE '/'.     02600002
               10  H1-DD                   PIC  X(02)    VALUE SPACES.  02610002
               10  FILLER                  PIC  X(01)    VALUE '/'.     02620002
               10  H1-CCYY                 PIC  X(04)    VALUE SPACES.  02630002
           05  FILLER                      PIC  X(47)    VALUE SPACES.  02640002
                                                                        02650002
       01  H2-COLUMN-HEADING.                                           02660002
           05  FILLER                      PIC  X(04)    VALUE SPACES.  02670002
           05  FILLER                      PIC  X(04)    VALUE          02680002
               'DATE'.                                                  02690002
           05  FILLER                      PIC  X(08)    VALUE SPACES.  02700002
           05  FILLER                      PIC  X(07)    VALUE          02710002
               'STORE #'.                                               02720002
           05  FILLER                      PIC  X(11)    VALUE SPACES.  02730002
           05  FILLER                      PIC  X(15)    VALUE          02740002
               'PROCESSED SALES'.                                       02750002
           05  FILLER                      PIC  X(07)    VALUE SPACES.  02760002
           05  FILLER                      PIC  X(17)    VALUE          02770002
               'UNPROCESSED SALES'.                                     02780002
           05  FILLER                      PIC  X(09)    VALUE SPACES.  02790002
           05  FILLER                      PIC  X(11)    VALUE          02800002
               'TOTAL SALES'.                                           02810002
           05  FILLER                      PIC  X(10)    VALUE SPACES.  02820002
                                                                        02830002
      ******************************************************************02840002
      *  TOTAL LINE BY STORE BY DAY.                                    02850002
      ******************************************************************02860002
       01  T1-TOTAL-STORE.                                              02870002
           05  FILLER                      PIC  X(01)    VALUE SPACES.  02880002
           05  T1-TRN-DATE-MM              PIC  X(02).                  02890002
           05  FILLER                      PIC  X(01)    VALUE '/'.     02900002
           05  T1-TRN-DATE-DD              PIC  X(02).                  02910002
           05  FILLER                      PIC  X(01)    VALUE '/'.     02920002
           05  T1-TRN-DATE-CCYY            PIC  X(04).                  02930002
           05  FILLER                      PIC  X(06)    VALUE SPACES.  02940002
           05  T1-STR-NBR                  PIC  X(04).                  02950002
           05  FILLER                      PIC  X(13)    VALUE SPACES.  02960002
           05  T1-PROCESSED                PIC  Z,ZZZ,ZZ9.99-.          02970002
           05  FILLER                      PIC  X(11)    VALUE SPACES.  02980002
           05  T1-UNPROCESSED              PIC  Z,ZZZ,ZZ9.99-.          02990002
           05  FILLER                      PIC  X(12)    VALUE SPACES.  03000002
           05  T1-TOTAL                    PIC  Z,ZZZ,ZZ9.99-.          03010002
           05  FILLER                      PIC  X(36)    VALUE SPACES.  03020002
                                                                        03030002
      ******************************************************************03040002
      *  TOTAL LINE BY CORP BY DAY.                                     03050002
      ******************************************************************03060002
       01  T3-TOTAL-CORP.                                               03070002
           05  FILLER                      PIC  X(10)    VALUE          03080002
               'CORPORATE '.                                            03090002
           05  T3-TRN-DATE-MM              PIC  X(02).                  03100002
           05  FILLER                      PIC  X(01)    VALUE '/'.     03110002
           05  T3-TRN-DATE-DD              PIC  X(02).                  03120002
           05  FILLER                      PIC  X(01)    VALUE '/'.     03130002
           05  T3-TRN-DATE-CCYY            PIC  X(04).                  03140002
           05  FILLER                      PIC  X(06)    VALUE          03150002
               ' TOTAL'.                                                03160002
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03170002
           05  T3-CORP-PROCESSED           PIC  ZZZ,ZZZ,ZZ9.99-.        03180002
           05  FILLER                      PIC  X(09)    VALUE SPACES.  03190002
           05  T3-CORP-UNPROCESSED         PIC  ZZZ,ZZZ,ZZ9.99-.        03200002
           05  FILLER                      PIC  X(10)    VALUE SPACES.  03210002
           05  T3-CORP-TOTAL               PIC  ZZZ,ZZZ,ZZ9.99-.        03220002
           05  FILLER                      PIC  X(13)    VALUE SPACES.  03230002
                                                                        03240002
      ******************************************************************03250002
      *  GRAND TOTAL LINE BY CORP.                                      03260002
      ******************************************************************03270002
       01  T4-GRAND-TOTAL-CORP.                                         03280002
           05  FILLER                      PIC  X(28)    VALUE          03290002
               'GRAND TOTAL CORPORATE SALES '.                          03300002
           05  FILLER                      PIC  X(04)    VALUE SPACES.  03310002
           05  T4-CORP-PROCESSED           PIC  ZZZ,ZZZ,ZZ9.99-.        03320002
           05  FILLER                      PIC  X(09)    VALUE SPACES.  03330002
           05  T4-CORP-UNPROCESSED         PIC  ZZZ,ZZZ,ZZ9.99-.        03340002
           05  FILLER                      PIC  X(10)    VALUE SPACES.  03350002
           05  T4-CORP-TOTAL               PIC  ZZZ,ZZZ,ZZ9.99-.        03360002
           05  FILLER                      PIC  X(13)    VALUE SPACES.  03370002
                                                                        03380002
      ******************************************************************03390002
      *  UNMATCH NEW TOTAL LINE                                         03400002
      ******************************************************************03410002
       01  T5-UNMTCH-NEW.                                               03420002
           05  FILLER                      PIC  X(10)    VALUE          03430002
               'CORPORATE '.                                            03440002
           05  T5-TRN-DATE-MM              PIC  X(02).                  03450002
           05  FILLER                      PIC  X(01)    VALUE '/'.     03460002
           05  T5-TRN-DATE-DD              PIC  X(02).                  03470002
           05  FILLER                      PIC  X(01)    VALUE '/'.     03480002
           05  T5-TRN-DATE-CCYY            PIC  X(04).                  03490002
           05  FILLER                      PIC  X(06)    VALUE          03500002
               ' TOTAL'.                                                03510002
           05  FILLER                      PIC  X(06)    VALUE SPACES.  03520002
           05  T5-CORP-PROCESSED           PIC  ZZZ,ZZZ,ZZ9.99-.        03530002
           05  FILLER                      PIC  X(09)    VALUE SPACES.  03540002
           05  T5-CORP-UNPROCESSED         PIC  ZZZ,ZZZ,ZZ9.99-.        03550002
           05  FILLER                      PIC  X(10)    VALUE SPACES.  03560002
           05  T5-CORP-TOTAL               PIC  ZZZ,ZZZ,ZZ9.99-.        03570002
           05  FILLER                      PIC  X(13)    VALUE SPACES.  03580002
                                                                        03590002
                                                                        03600002
      ******************************************************************03610002
      *  INPUT FILE LAYOUT                                             *03620002
      ******************************************************************03630002
           COPY TCWS002.                                                03640002
                                                                        03650002
      ******************************************************************03660002
      *  INPUT TOTAL FILE LAYOUT                                       *03670002
      ******************************************************************03680002
       01  TOTAL-FILE-RECORD.                                           03690002
           05  TOTAL-PARTN-NBR         PIC  9(04).                      03700002
           05  TOTAL-STR-NBR           PIC  9(04).                      03710002
           05  TOTAL-SLS-DTE           PIC  X(08).                      03720002
           05  TOTAL-STRT-TIME         PIC  X(06).                      03730002
           05  TOTAL-RGST-ID           PIC  9(04).                      03740002
           05  TOTAL-TRN-NBR           PIC  9(04).                      03750002
           05  TOTAL-SLS-CORR-SEQ-NBR  PIC  S9(04).                     03760002
           05  TOTAL-TNDR-SEQ-NBR      PIC  S9(04).                     03770002
           05  TOTAL-CARD-NBR          PIC  X(19).                      03780002
           05  TOTAL-TNDR-AMT          PIC  S9(7)V99.                   03790002
           05  TOTAL-MATCH-CDE         PIC  X(01).                      03800002
           05  TOTAL-SALE-TYPE         PIC  X(01).                      03810002
           05  TOTAL-REC-IND           PIC  X(01).                      03820002
                                                                        03830002
                                                                        03840002
      ***************************************************************** 03850002
      * PROCEDURE DIVISION.                                           * 03860002
      ***************************************************************** 03870002
       PROCEDURE DIVISION.                                              03880002
                                                                        03890002
      ******************************************************************03900002
      * 0000-MAINLINE-PARAGRAPH                                        *03910002
      *      PRODUCE DAILY DEBIT CARD AUDIT REPORT                     *03920002
      ******************************************************************03930002
       0000-MAINLINE-PARAGRAPH.                                         03940002
                                                                        03950002
           PERFORM 1000-INITIALIZE-PROGRAM.                             03960002
                                                                        03970002
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                        03980002
                                                                        03990002
           PERFORM 2000-PROCESS-RECORDS                                 04000002
               UNTIL PS-END-OF-INPUT-FILE.                              04010002
                                                                        04020002
           PERFORM 2300-FINAL-TOTALS.                                   04030002
                                                                        04040002
           PERFORM 4000-UNMATCH-NEW-TOTAL.                              04050002
                                                                        04060002
           CLOSE DBT-CRD-AUDIT-REPORT-FILE                              04070002
                 DBT-CRD-INPUT-FILE                                     04080002
                 UNMTCH-NEW-FILE.                                       04090002
                                                                        04100002
           GOBACK.                                                      04110002
                                                                        04120002
      ******************************************************************04130002
      * 1000-INITIALIZE-PROGRAM                                         04140002
      *  -  OPEN FILES                                                  04150002
      *  -  PROCESS CONTROL CARD                                        04160002
      *  -  FORMAT REPORT HEADINGS                                      04170002
      *  -  SELECT PARTN-NBR FROM TEPPART                               04180002
      *  -  OPEN TENDER CURSOR                                          04190002
      *  -  ATTEMPT FIRST FETCH OF TENDER CURSOR.                       04200002
      ******************************************************************04210002
                                                                        04220002
       1000-INITIALIZE-PROGRAM.                                         04230002
                                                                        04240002
           OPEN INPUT  POS-DATE-CARD-FILE                               04250002
                       DBT-CRD-INPUT-FILE                               04260002
                       UNMTCH-NEW-FILE                                  04270002
                OUTPUT DBT-CRD-AUDIT-REPORT-FILE.                       04280002
                                                                        04290002
           PERFORM 1100-PROCESS-CONTROL-CARD.                           04300002
                                                                        04310002
           PERFORM 1400-FORMAT-HEADINGS.                                04320002
                                                                        04330002
           PERFORM 2200-WRITE-PAGE-HEADINGS.                            04340002
                                                                        04350002
           INITIALIZE PV-STORE-DAY-TOTAL                                04360002
                      PV-CORP-DAY-TOTAL.                                04370002
                                                                        04380002
           PERFORM 9000-ADD-DATE-2-CORP-TOTAL-TBL.                      04390002
                                                                        04400002
      ******************************************************************04410002
      * 1100-PROCESS-CONTROL-CARD                                       04420002
      *     READ CONTROL CARD TO GET POS DATE IN MMDDYY FORMAT.  CALL   04430002
      *     DATE ROUTINE TO CONVERT DATE TO A CCYY-MM-DD PROCESS DATE.  04440002
      ******************************************************************04450002
                                                                        04460002
       1100-PROCESS-CONTROL-CARD.                                       04470002
                                                                        04480002
           PERFORM 1110-READ-POS-DATE-CARD-FILE.                        04490002
                                                                        04500002
           CLOSE POS-DATE-CARD-FILE.                                    04510002
                                                                        04520002
           IF PS-END-OF-CARD-FILE                                       04530002
               MOVE '1100-PROCESS-CONTROL-CARD'  TO PV-PARAGRAPH-NAME   04540002
               MOVE 'CONTROL CARD IS MISSING'    TO PV-OPERATION-MSG-1  04550002
               SET AC-CONTROL-CARD-ERROR TO TRUE                        04560002
               PERFORM 9999-ABEND                                       04570002
           END-IF.                                                      04580002
                                                                        04590002
           DISPLAY PC-CURRENT-PROGRAM-NAME                              04600002
                   ' - CONTROL CARD = (' CC-CONTROL-CARD-DISPLAY  ')'.  04610002
                                                                        04620002
           PERFORM 1115-CONVERT-INPUT-DATE.                             04630002
                                                                        04640002
      ******************************************************************04650002
      * 1110-READ-POS-DATE-CARD-FILE                                    04660002
      *     READ DATE CONTROL CARD.                                     04670002
      ******************************************************************04680002
       1110-READ-POS-DATE-CARD-FILE.                                    04690002
                                                                        04700002
           READ POS-DATE-CARD-FILE INTO CC-CONTROL-CARD                 04710002
              AT END                                                    04720002
                 SET PS-END-OF-CARD-FILE TO TRUE                        04730002
           END-READ.                                                    04740002
                                                                        04750002
           MOVE PC-CENTURY      TO  CC-RFRM-POS-DATE-CC.                04760002
           MOVE CC-POS-DATE-MM  TO  CC-RFRM-POS-DATE-MM.                04770002
           MOVE CC-POS-DATE-DD  TO  CC-RFRM-POS-DATE-DD.                04780002
           MOVE CC-POS-DATE-YY  TO  CC-RFRM-POS-DATE-YY.                04790002
                                                                        04800002
      ******************************************************************04810002
      * 1115-CONVERT-INPUT-DATE                                         04820002
      *     CALL KOHLS CALENDAR ROUTINE:                                04830002
      *         PASS: CONTROL CARD POS DATE (MMDDYY FORMAT).            04840002
      *       RETURN: ACTUAL CALENDAR DB2 ISO DATE (CCYY-MM-DD FORMAT). 04850002
      ******************************************************************04860002
       1115-CONVERT-INPUT-DATE.                                         04870002
                                                                        04880002
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              04890002
                                                                        04900002
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   04910002
           SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                   04920002
           SET  DPG52-LK-DTE-GREG            TO TRUE.                   04930002
           MOVE CC-POS-DATE-MMDDYY           TO DPG52-LK-DATE-INPUT.    04940002
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    04950002
                                                   DPG54 DPG55 DPG56.   04960002
           EVALUATE TRUE                                                04970002
               WHEN DPG54-SEVERE-ERROR                                  04980002
               WHEN DPG54-DATE-INVALID                                  04990002
                   MOVE '1115-CONVERT-INPUT-DATE'                       05000002
                                                 TO PV-PARAGRAPH-NAME   05010002
                   MOVE 'ERROR CONVERTING INPUT CARD DATE'              05020002
                                                 TO PV-OPERATION-MSG-1  05030002
                   SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                05040002
                   MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2  05050002
                   PERFORM 9999-ABEND                                   05060002
           END-EVALUATE.                                                05070002
                                                                        05080002
           MOVE DPG55-DB2-ISO-DATE-FMT       TO PV-POS-CCYY-MM-DD.      05090002
                                                                        05100002
      ******************************************************************05110002
      * 1400-FORMAT-HEADINGS                                            05120002
      *     GET CURRENT DATE AND TIME FROM SYSTEM.                      05130002
      *     FORMAT REPORT FIELDS IN THE TOP PAGE HEADING LINE.          05140002
      *     FORMAT HEADING LINE 2 WITH CONVERTED POS DATE.              05150002
      ******************************************************************05160002
       1400-FORMAT-HEADINGS.                                            05170002
                                                                        05180002
           MOVE FUNCTION CURRENT-DATE (1:8) TO PV-CURR-DATE-CCYYMMDD.   05190002
           ACCEPT PV-CURR-TIME FROM TIME.                               05200002
                                                                        05210002
           MOVE PV-CURR-DATE-MM             TO DP132-RUN-MONTH.         05220002
           MOVE PV-CURR-DATE-DD             TO DP132-RUN-DAY.           05230002
           MOVE PV-CURR-DATE-CCYY           TO DP132-RUN-YEAR.          05240002
                                                                        05250002
           MOVE PV-CURR-TIME-HH             TO DP132-RUN-HOUR.          05260002
           MOVE PV-CURR-TIME-MM             TO DP132-RUN-MINUTE.        05270002
           MOVE PC-CURRENT-PROGRAM-NAME     TO DP132-PROGRAM-NAME.      05280002
           MOVE PC-REPORT-NBR-1             TO DP132-REPORT-NUMBER.     05290002
                                                                        05300002
           MOVE PV-POS-CCYY                 TO H1-CCYY.                 05310002
           MOVE PV-POS-MM                   TO H1-MM.                   05320002
           MOVE PV-POS-DD                   TO H1-DD.                   05330002
                                                                        05340002
      ******************************************************************05350002
      * 2000-PROCESS-RECORDS                                           *05360002
      ******************************************************************05370002
       2000-PROCESS-RECORDS.                                            05380002
                                                                        05390002
           IF PS-FIRST-RECORD-YES                                       05400002
              MOVE TC002-STR-NBR      TO PV-SAVE-STR-NBR                05410002
              MOVE TC002-SLS-DTE      TO PV-SAVE-TRAN-DATE              05420002
              SET  PS-FIRST-RECORD-NO TO TRUE                           05430002
           END-IF.                                                      05440002
                                                                        05450002
           IF TC002-STR-NBR > PV-SAVE-STR-NBR                           05460002
              SET PV-IDX TO +1                                          05470002
              PERFORM 2500-WRITE-STR-DAY-TOTAL                          05480002
                  UNTIL PV-IDX > PC-EXCEPT-DAYS-NUMBER                  05490002
                    OR  PV-DATE(PV-IDX) = SPACES                        05500002
              MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD                   05510002
              MOVE 1               TO PV-LINE-SPACING                   05520002
              PERFORM 3000-WRITE-RECORD                                 05530002
              SET PV-IDX TO +1                                          05540002
              INITIALIZE PV-STORE-DAY-TOTAL                             05550002
           END-IF.                                                      05560002
                                                                        05570002
           PERFORM 2400-PROCESS-TOTALS.                                 05580002
                                                                        05590002
           PERFORM 2100-READ-DBT-CRD-INPUT-FILE.                        05600002
                                                                        05610002
      ******************************************************************05620002
      *2100-READ-DBT-CRD-INPUT-FILE                                    *05630002
      ******************************************************************05640002
       2100-READ-DBT-CRD-INPUT-FILE.                                    05650002
           READ DBT-CRD-INPUT-FILE                                      05660002
               INTO TC002-DATA                                          05670002
               AT END                                                   05680002
                   SET PS-END-OF-INPUT-FILE TO TRUE                     05690002
           END-READ.                                                    05700002
                                                                        05710002
      ******************************************************************05720002
      * 2200-WRITE-PAGE-HEADINGS.                                      *05730002
      ******************************************************************05740002
       2200-WRITE-PAGE-HEADINGS.                                        05750002
                                                                        05760002
           MOVE +3 TO PA-LINE-CNT.                                      05770002
           ADD  +1 TO PA-PAGE-CNT.                                      05780002
                                                                        05790002
           MOVE PA-PAGE-CNT TO DP132-PAGE-NUMBER.                       05800002
                                                                        05810002
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            05820002
            FROM DP132-STANDRD-HEADER-132-COLS                          05830002
            AFTER ADVANCING PAGE                                        05840002
           END-WRITE.                                                   05850002
                                                                        05860002
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            05870002
            FROM H1-TITLE-HEADING                                       05880002
            AFTER ADVANCING 1                                           05890002
           END-WRITE.                                                   05900002
                                                                        05910002
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            05920002
            FROM H2-COLUMN-HEADING                                      05930002
            AFTER ADVANCING 1                                           05940002
           END-WRITE.                                                   05950002
                                                                        05960002
      ******************************************************************05970002
      * 2300-FINAL-TOTALS.                                             *05980002
      ******************************************************************05990002
       2300-FINAL-TOTALS.                                               06000002
           SET PV-IDX TO +1.                                            06010002
           PERFORM 2500-WRITE-STR-DAY-TOTAL                             06020002
                  UNTIL PV-IDX > PC-EXCEPT-DAYS-NUMBER                  06030002
                    OR  PV-DATE(PV-IDX) = SPACES.                       06040002
           MOVE PV-BLANK-RECORD TO PV-PRINT-RECORD.                     06050002
           MOVE 1               TO PV-LINE-SPACING.                     06060002
           PERFORM 3000-WRITE-RECORD.                                   06070002
           SET PV-IDX TO +1.                                            06080002
           INITIALIZE PV-STORE-DAY-TOTAL.                               06090002
           SET PV-CORP-IDX TO +1.                                       06100002
           PERFORM 2600-WRITE-CORP-TOTALS                               06110002
                  UNTIL PV-CORP-IDX > PC-EXCEPT-DAYS-NUMBER             06120002
                    OR  PV-CORP-DATE(PV-CORP-IDX) = SPACES.             06130002
                                                                        06140002
           MOVE PV-BLANK-RECORD          TO PV-PRINT-RECORD.            06150002
           MOVE 1                        TO PV-LINE-SPACING.            06160002
           PERFORM 3000-WRITE-RECORD.                                   06170002
                                                                        06180002
           MOVE PV-STORE-GRAND-PROC-TOT   TO T4-CORP-PROCESSED.         06190002
           MOVE PV-STORE-GRAND-UNPROC-TOT TO T4-CORP-UNPROCESSED.       06200002
           MOVE PV-STORE-GRAND-TOTAL-TOT  TO T4-CORP-TOTAL.             06210002
                                                                        06220002
           MOVE T4-GRAND-TOTAL-CORP  TO PV-PRINT-RECORD.                06230002
           MOVE 2                    TO PV-LINE-SPACING.                06240002
           PERFORM 3000-WRITE-RECORD.                                   06250002
                                                                        06260002
      ******************************************************************06270002
      * 2400-PROCESS-TOTALS.                                           *06280002
      ******************************************************************06290002
       2400-PROCESS-TOTALS.                                             06300002
                                                                        06310002
           SET PS-NOT-FOUND-DATE TO TRUE.                               06320002
           SET PV-IDX TO +1.                                            06330002
           SEARCH PV-STORE-DAY-TOTALS                                   06340002
             AT END                                                     06350002
               IF PS-NOT-FOUND-DATE                                     06360002
                  PERFORM 2450-ADD-DATE-TO-TABLE                        06370002
               END-IF                                                   06380002
             WHEN PV-DATE (PV-IDX) = TC002-SLS-DTE                      06390002
               PERFORM 2475-ADD-DETAIL-TO-TOTAL                         06400002
               SET PS-FOUND-DATE TO TRUE                                06410002
           END-SEARCH.                                                  06420002
                                                                        06430002
           SET PS-NOT-FOUND-DATE   TO TRUE.                             06440002
           MOVE TC002-STR-NBR      TO PV-SAVE-STR-NBR.                  06450002
           MOVE TC002-SLS-DTE      TO PV-SAVE-TRAN-DATE.                06460002
                                                                        06470002
      ******************************************************************06480002
      * ADD DATE TO TABLE AND ADD THE SALES AMT TO THE TOTAL           *06490002
      ******************************************************************06500002
       2450-ADD-DATE-TO-TABLE.                                          06510002
                                                                        06520002
           SET PV-IDX TO +1.                                            06530002
           SEARCH  PV-STORE-DAY-TOTALS                                  06540002
             AT END                                                     06550002
                 CONTINUE                                               06560002
             WHEN PV-DATE (PV-IDX) = SPACES OR LOW-VALUES               06570002
                 MOVE TC002-SLS-DTE TO PV-DATE(PV-IDX)                  06580002
                 PERFORM 2475-ADD-DETAIL-TO-TOTAL                       06590002
           END-SEARCH.                                                  06600002
                                                                        06610002
      ******************************************************************06620002
      * ADD THE DETAIL TO THE CORRECT DAY'S TOTAL                      *06630002
      ******************************************************************06640002
       2475-ADD-DETAIL-TO-TOTAL.                                        06650002
           IF TC002-MATCH                                               06660002
             IF TC002-SALE-TYPE = 'S' OR                                06670002
                TC002-SALE-TYPE = 'L' OR                                06680002
                TC002-SALE-TYPE = 'R' OR                                06690002
                TC002-SALE-TYPE = 'X' OR                                06700002
                TC002-SALE-TYPE = 'C'                                   06710002
                 ADD  TC002-TNDR-AMT TO                                 06720002
                             PV-STORE-DAY-PROC-TOT (PV-IDX)             06730002
             END-IF                                                     06740002
           ELSE                                                         06750002
              IF TC002-NOT-MATCHED                                      06760002
                IF TC002-SALE-TYPE = 'S' OR                             06770002
                   TC002-SALE-TYPE = 'L' OR                             06780002
                   TC002-SALE-TYPE = 'R' OR                             06790002
                   TC002-SALE-TYPE = 'X' OR                             06800002
                   TC002-SALE-TYPE = 'C'                                06810002
                     ADD  TC002-TNDR-AMT TO                             06820002
                             PV-STORE-DAY-UNPROC-TOT (PV-IDX)           06830002
                END-IF                                                  06840002
              END-IF                                                    06850002
           END-IF.                                                      06860002
                                                                        06870002
      ******************************************************************06880002
      * 2500-WRITE-STR-DAY-TOTAL.                                      *06890002
      ******************************************************************06900002
       2500-WRITE-STR-DAY-TOTAL.                                        06910002
                                                                        06920002
           MOVE PV-DATE (PV-IDX)         TO PV-DATE-CCYYMMDD.           06930002
           MOVE PV-DATE-CCYY             TO T1-TRN-DATE-CCYY.           06940002
           MOVE PV-DATE-MM               TO T1-TRN-DATE-MM.             06950002
           MOVE PV-DATE-DD               TO T1-TRN-DATE-DD.             06960002
           MOVE PV-SAVE-STR-NBR          TO T1-STR-NBR.                 06970002
                                                                        06980002
           ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                          06990002
                           TO PV-STORE-DAY-TOTAL-TOT (PV-IDX)           07000002
                              PV-STORE-GRAND-PROC-TOT                   07010002
                              PV-STORE-GRAND-TOTAL-TOT.                 07020002
           ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                        07030002
                           TO PV-STORE-DAY-TOTAL-TOT (PV-IDX)           07040002
                              PV-STORE-GRAND-UNPROC-TOT                 07050002
                              PV-STORE-GRAND-TOTAL-TOT.                 07060002
                                                                        07070002
           PERFORM 2550-ADD-CORP-TOTAL.                                 07080002
                                                                        07090002
           MOVE PV-STORE-DAY-PROC-TOT (PV-IDX)   TO T1-PROCESSED.       07100002
           MOVE PV-STORE-DAY-UNPROC-TOT (PV-IDX) TO T1-UNPROCESSED.     07110002
           MOVE PV-STORE-DAY-TOTAL-TOT (PV-IDX)  TO T1-TOTAL.           07120002
                                                                        07130002
           MOVE T1-TOTAL-STORE           TO PV-PRINT-RECORD.            07140002
           MOVE 1                        TO PV-LINE-SPACING.            07150002
           PERFORM 3000-WRITE-RECORD.                                   07160002
                                                                        07170002
           SET PV-IDX UP BY +1.                                         07180002
                                                                        07190002
      ******************************************************************07200002
      * 2550-ADD-CORP-TOTAL                                            *07210002
      ******************************************************************07220002
       2550-ADD-CORP-TOTAL.                                             07230002
                                                                        07240002
           SET PS-NOT-FOUND-DATE TO TRUE.                               07250002
           SET PV-CORP-IDX TO +1.                                       07260002
           SEARCH PV-CORP-DAY-TOTALS                                    07270002
             AT END                                                     07280002
               IF PS-NOT-FOUND-DATE                                     07290002
                  PERFORM 2575-ADD-DATE-TO-TABLE                        07300002
               END-IF                                                   07310002
             WHEN PV-CORP-DATE (PV-CORP-IDX) = PV-DATE (PV-IDX)         07320002
               ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                      07330002
                           TO PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)        07340002
                              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)       07350002
               ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                    07360002
                           TO PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)      07370002
                              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)       07380002
               SET PS-FOUND-DATE TO TRUE                                07390002
           END-SEARCH.                                                  07400002
                                                                        07410002
      ******************************************************************07420002
      * ADD DATE TO CORP TABLE AND ADD THE SALES AMT TO THE CORP TOTALS*07430002
      ******************************************************************07440002
       2575-ADD-DATE-TO-TABLE.                                          07450002
                                                                        07460002
           SET PV-CORP-IDX TO +1.                                       07470002
           SEARCH  PV-CORP-DAY-TOTALS                                   07480002
             AT END                                                     07490002
                 CONTINUE                                               07500002
             WHEN PV-CORP-DATE (PV-CORP-IDX) = SPACES OR LOW-VALUES     07510002
               MOVE PV-DATE (PV-IDX) TO PV-CORP-DATE (PV-CORP-IDX)      07520002
               ADD  PV-STORE-DAY-PROC-TOT (PV-IDX)                      07530002
                           TO PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)        07540002
                              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)       07550002
               ADD  PV-STORE-DAY-UNPROC-TOT (PV-IDX)                    07560002
                           TO PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)      07570002
                              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)       07580002
           END-SEARCH.                                                  07590002
                                                                        07600002
      ******************************************************************07610002
      * 2600-WRITE-CORP-TOTALS                                         *07620002
      ******************************************************************07630002
       2600-WRITE-CORP-TOTALS.                                          07640002
                                                                        07650002
           IF PV-CORP-DAY-PROC-TOT (PV-CORP-IDX) = 0 AND                07660002
              PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX) = 0 AND              07670002
              PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX) = 0                   07680002
                 CONTINUE                                               07690002
           ELSE                                                         07700002
              MOVE PV-CORP-DATE (PV-CORP-IDX) TO PV-DATE-CCYYMMDD       07710002
              MOVE PV-DATE-CCYY             TO T3-TRN-DATE-CCYY         07720002
              MOVE PV-DATE-MM               TO T3-TRN-DATE-MM           07730002
              MOVE PV-DATE-DD               TO T3-TRN-DATE-DD           07740002
              MOVE PV-CORP-DAY-PROC-TOT (PV-CORP-IDX)                   07750002
                                            TO T3-CORP-PROCESSED        07760002
              MOVE PV-CORP-DAY-UNPROC-TOT (PV-CORP-IDX)                 07770002
                                            TO T3-CORP-UNPROCESSED      07780002
              MOVE PV-CORP-DAY-TOTAL-TOT (PV-CORP-IDX)                  07790002
                                            TO T3-CORP-TOTAL            07800002
              MOVE T3-TOTAL-CORP            TO PV-PRINT-RECORD          07810002
              MOVE 1                        TO PV-LINE-SPACING          07820002
              PERFORM 3000-WRITE-RECORD                                 07830002
           END-IF.                                                      07840002
                                                                        07850002
           SET PV-CORP-IDX UP BY +1.                                    07860002
                                                                        07870002
      ******************************************************************07880002
      * 3000-WRITE-RECORD                                               07890002
      ******************************************************************07900002
       3000-WRITE-RECORD.                                               07910002
                                                                        07920002
           ADD PV-LINE-SPACING   TO PA-LINE-CNT.                        07930002
                                                                        07940002
           IF PA-LINE-CNT > PC-LINE-LIMIT                               07950002
               PERFORM 2200-WRITE-PAGE-HEADINGS                         07960002
           END-IF.                                                      07970002
                                                                        07980002
           WRITE DBT-CRD-AUDIT-REPORT-RECORD                            07990002
               FROM PV-PRINT-RECORD                                     08000002
               AFTER ADVANCING PV-LINE-SPACING                          08010002
           END-WRITE.                                                   08020002
           MOVE SPACES           TO PV-PRINT-RECORD.                    08030002
                                                                        08040002
      ******************************************************************08050002
      * 4000-UNMATCH-NEW-TOTAL                                          08060002
      ******************************************************************08070002
       4000-UNMATCH-NEW-TOTAL.                                          08080002
                                                                        08090002
           READ UNMTCH-NEW-FILE                                         08100002
               INTO TOTAL-FILE-RECORD                                   08110002
              AT END                                                    08120002
                 SET PS-END-OF-UNMTCH-FILE TO TRUE.                     08130002
                                                                        08140002
           IF PS-END-OF-UNMTCH-FILE                                     08150002
              NEXT SENTENCE                                             08160002
           ELSE                                                         08170002
              MOVE TOTAL-SLS-DTE            TO PV-DATE-CCYYMMDD         08180002
              MOVE PV-DATE-CCYY             TO T5-TRN-DATE-CCYY         08190002
              MOVE PV-DATE-MM               TO T5-TRN-DATE-MM           08200002
              MOVE PV-DATE-DD               TO T5-TRN-DATE-DD           08210002
              MOVE TOTAL-TNDR-AMT           TO T5-CORP-PROCESSED        08220002
              MOVE ZERO                     TO T5-CORP-UNPROCESSED      08230002
              MOVE TOTAL-TNDR-AMT           TO T5-CORP-TOTAL            08240002
              MOVE T5-UNMTCH-NEW            TO PV-PRINT-RECORD          08250002
                                                                        08260002
              WRITE DBT-CRD-AUDIT-REPORT-RECORD                         08270002
                  FROM PV-PRINT-RECORD                                  08280002
                  AFTER ADVANCING 3 LINES                               08290002
           END-IF.                                                      08300002
                                                                        08310002
      ******************************************************************08320002
      * 9000-ADD-DATE-2-CORP-TOTAL-TBL                                 *08330002
      *     LOAD THE LAST 30 BUSINESS DAYS TO THE PV-CORP-DAY-TOTAL    *08340002
      *     TABLE                                                      *08350002
      *     EXCEPTION DAYS NUMBER FROM INPUT CONTROL CARD WILL         *08360002
      *     DETERMINE HOW MANYS DAYS TO MOVE TO THE TABLE              *08370002
      *            ************  MAX IS 30  MAX OCCURS IN TABLE ***    *08380002
      ******************************************************************08390002
       9000-ADD-DATE-2-CORP-TOTAL-TBL.                                  08400002
                                                                        08410002
           SET PV-CORP-IDX        TO +1.                                08420002
           MOVE PC-EXCEPT-DAYS-NUMBER TO SUB.                           08430002
           SUBTRACT +1               FROM SUB.                          08440002
           PERFORM 9100-DATES-TO-ADD                                    08450002
                UNTIL SUB = 0.                                          08460002
           MOVE CC-POS-DATE-MMDDYY TO  PV-P-MMDDYY.                     08470002
           MOVE PV-P-MM            TO  PV-TABLE-DATE-MM.                08480002
           MOVE PV-P-DD            TO  PV-TABLE-DATE-DD.                08490002
           MOVE PV-P-YY            TO  PV-HOLD-YY.                      08500002
           MOVE '20'               TO  PV-HOLD-CC.                      08510002
           MOVE PV-HOLD-CCYY       TO  PV-TABLE-DATE-CCYY.              08520002
           MOVE PV-TABLE-DATE-CCYYMMDD TO PV-CORP-DATE (PV-CORP-IDX).   08530002
                                                                        08540002
      ******************************************************************08550002
      * 9100-DATES-TO-ADD.                                             *08560002
      *     LOAD DATES TO PV-CORP-DATE IN THE PV-CORP-DAY-TOTALS TABLE *08570002
      *     LOAD DATES IN ASCENDING SEQUENCE BY SETTING SUB TO THE     *08580002
      *     CONTROL CARD NUMBER AND SUBTRACT 1 UNTIL SUB = 0           *08590002
      ******************************************************************08600002
       9100-DATES-TO-ADD.                                               08610002
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              08620002
                                                                        08630002
           SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                   08640002
           SET  DPG51-DECREMENT-DATE   TO TRUE.                         08650002
           MOVE SUB                    TO DPG51-INCR-DECR-DAYS-9.       08660002
           SET  DPG52-LK-DTE-GREG      TO TRUE.                         08670002
           MOVE CC-POS-DATE-MMDDYY     TO DPG52-LK-DATE-INPUT.          08680002
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53    08690002
                                                   DPG54 DPG55 DPG56.   08700002
           EVALUATE TRUE                                                08710002
              WHEN DPG54-SEVERE-ERROR                                   08720002
              WHEN DPG54-DATE-INVALID                                   08730002
                  MOVE '9100-DATES-TO-ADD'                              08740002
                                                TO PV-PARAGRAPH-NAME    08750002
                  MOVE 'ERROR CONVERTING PROCESSING DATE'               08760002
                                                TO PV-OPERATION-MSG-1   08770002
                  SET AC-CALENDAR-ROUTINE-ERROR TO TRUE                 08780002
                  MOVE DPG54-ERROR-MESSAGE      TO PV-OPERATION-MSG-2   08790002
                  PERFORM 9999-ABEND                                    08800002
           END-EVALUATE.                                                08810002
                                                                        08820002
           MOVE DPG55-DB2-ISO-DATE     TO PV-CORP-DATE(PV-CORP-IDX).    08830002
           SET PV-CORP-IDX  UP BY +1.                                   08840002
           SUBTRACT +1  FROM SUB.                                       08850002
                                                                        08860002
      ******************************************************************08870002
      * 9999-ABEND                                                      08880002
      *     ABEND PROGRAM WHEN A NON-DB2 ERROR OCCURS.                  08890002
      ******************************************************************08900002
       9999-ABEND.                                                      08910002
           DISPLAY '** ABEND           **'.                             08920002
           DISPLAY '** PROGRAM         ** = ' PC-CURRENT-PROGRAM-NAME.  08930002
           DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.        08940002
           DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG-1.       08950002
           DISPLAY '**                 ** = ' PV-OPERATION-MSG-2.       08960002
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         08970002
