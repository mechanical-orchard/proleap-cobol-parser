000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.   TRKUP213.                                                  
000600 AUTHOR.       MICHAEL FRIESS.                                            
000700 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000800 DATE-WRITTEN. SEPTEMBER, 2010.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001002******************************************************************        
001003*  JOB TRK212 - AIR AND LANDED ASN PROCESS.  THIS PROGRAM WILL   *        
001009*  STRIP OFF SORT FIELDS FROM 500 BYTE RECORD CREATED TRKUT212   *        
001010*  AND GET RID OF EXTRA SETS OF HIERARCHICAL PO RECORDS WHERE    *        
001011*  NIKE DUPLICATE POS EXISTED ON ORIGINAL FILES.  THE OUTPUT     *        
001012*  RECORDS WILL BE BACK TO THEIR PRE SORT 400 BYTE LENGTH.       *        
001013*                                                                *        
001014*  JUNE 29, 2011 UPDATE - ALSO SET STAT-CDE FOR ASN TO 'D' FOR   *        
001015*  DELETE ON TABLE SNT_DAT_CNTL.  THIS INDICATES TO DOWN STREAM  *        
001016*  PROGRAM TRKUP211 THAT THE ASN NEEDS TO BE DELETED FROM TABLE  *        
001017*  SNT_DAT_XFER AND REINSERTED WITHOUT DUPLICATE PO RECORDS.     *        
001018*                                                                *        
001019*  INPUT/OUTPUT                                                  *        
001020*                                                                *        
001022*   PV-IN-SORT-REC - ASN RECORDS WITH ADDED SORT FIELDS FROM     *        
001023*                    TRKUT212.                                   *        
001024*                                                                *        
001025*   PV-OUT-STRIP-REC - ASN RECORDS WITH SORT FIELDS STRIPPED     *        
001026*                      OFF.                                      *        
001027*                                                                *        
002401******************************************************************        
002402 ENVIRONMENT DIVISION.                                                    
002403******************************************************************        
002404                                                                          
002405 CONFIGURATION SECTION.                                                   
002406                                                                          
002407 SOURCE-COMPUTER.        IBM-3090.                                        
002408 OBJECT-COMPUTER.        IBM-3090.                                        
002409                                                                          
002410 INPUT-OUTPUT SECTION.                                                    
002411                                                                          
002412 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT PV-IN-SORT-FILE                                               
002700         ASSIGN TO INP01.                                                 
002800                                                                          
002900     SELECT PV-OUT-STRIP-FILE                                             
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  PV-IN-SORT-FILE                                                      
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  TR063-ASN-RECORD                PIC X(500).                          
004700                                                                          
004800 FD  PV-OUT-STRIP-FILE                                                    
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  PV-OUT-STRIP-RECORD             PIC X(400).                          
005400                                                                          
006300 WORKING-STORAGE SECTION.                                                 
006310                                                                          
006400******************************************************************        
006410*  DB2 FORMATTED MESSAGE AREA                                             
006420******************************************************************        
006430     COPY DPWS004.                                                        
006440*                                                                         
006450******************************************************************        
006460*  COMMUNICATIONS AREA FOR DB2                                            
006470******************************************************************        
006480     EXEC SQL                                                             
006490          INCLUDE SQLCA                                                   
006491     END-EXEC.                                                            
006492*                                                                         
006500                                                                          
006600*****************************************************************         
006700*    ASN DETAIL INPUT RECORD LAYOUT                                       
006800*****************************************************************         
006900     COPY TRRD063.                                                        
007000                                                                          
127314*****************************************************************         
127315*    INPUT RECORD WITH ADDED SORT FIELDS LAYOUT.                          
127316*****************************************************************         
127317 01  PV-IN-SORT-REC-LAYOUT.                                               
127318   05 PV-IN-ORIGINAL-EDI-FIELDS.                                          
127319     10  PV-IN-LOD-DAT-TXT                  PIC X(350).                   
127320     10  PV-IN-ISA-CONTROL.                                               
127321         15  PV-IN-ISA-CNTL-NBR-SEQ         PIC 9(9).                     
127322         15  PV-IN-SHIPT-ID-SEQ             PIC X(30).                    
127323         15  PV-IN-DAT-XFER-CNTL            PIC 9(9).                     
127324     10  FILLER                             PIC X(2).                     
127325   05 PV-IN-SORT-FIELDS.                                                  
127326     10  PV-IN-CONTAINER-SORT.                                            
127327         15  PV-IN-TD301-EQUIP-CDE          PIC X(2).                     
127328         15  PV-IN-TD302-EQUIP-INIT-CDE     PIC X(4).                     
127329         15  PV-IN-TD303-EQUIP-NBR          PIC X(10).                    
127330     10  FILLER                             PIC X(01).                    
127331     10  PV-IN-PO-NBR-SORT                  PIC X(22).                    
127332     10  FILLER                             PIC X(01).                    
127333     10  PV-IN-ORIGINAL-REC-SEQ             PIC 9(9).                     
127334     10  FILLER                             PIC X(01).                    
127335     10  PS-IN-PO-GROUP-REC-ID              PIC X(02).                    
127336     10  FILLER                             PIC X(48).                    
127337                                                                          
127338*****************************************************************         
127339*    OUTPUT RECORD WITH SORT FIELDS STIPPED OFF.                          
127340*****************************************************************         
127341 01  PV-OUT-STRIP-REC-LAYOUT.                                             
127342     05  PV-OUT-ORIGINAL-EDI-FIELDS         PIC X(400).                   
127358                                                                          
127360 01  PA-PROGRAM-ACCUMULATORS.                                             
127361     05  PA-TOTALS.                                                       
127362         10 PA-INPUT-COUNT            PIC   9(13)  VALUE  0.              
127363         10 PA-DISCARD-COUNT          PIC   9(13)  VALUE  0.              
127364         10 PA-OUTPUT-COUNT           PIC   9(13)  VALUE  0.              
127365         10 PA-OUTPUT-PLUS-DISCARD    PIC   9(13)  VALUE  0.              
127366         10 PA-CNTL-UPDATES           PIC   9(13)  VALUE  0.            24
127367                                                                          
127368 01  PS-PROGRAM-SWITCHES.                                                 
127369     05  PS-END-OF-SORTIN-SW           PIC  X(01)    VALUE 'N'.           
127370         88  PS-END-OF-SORTIN                        VALUE 'Y'.           
127380         88  PS-END-OF-SORTIN-NO                     VALUE 'N'.           
127451     05  PS-THIS-IS-DUPLICATE-PO-SW    PIC  X(01)    VALUE 'N'.           
127452         88  PS-THIS-IS-DUPLICATE-PO                 VALUE 'Y'.           
127453         88  PS-THIS-IS-DUPLICATE-PO-NO              VALUE 'N'.           
127454     05  PS-CNTL-TBL-ALREADY-UPDT-SW PIC X(01)       VALUE 'N'.           
127455         88  PS-CNTL-TBL-ALREADY-UPDT                VALUE 'Y'.           
127456         88  PS-CNTL-TBL-ALREADY-UPDT-NO             VALUE 'N'.           
127457                                                                          
127458 01  PC-PROGRAM-CONSTANTS.                                                
127459     05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE                  
127460         'TRKUP213'.                                                      
127486                                                                          
127487 01 PV-PROGRAM-VARIABLES.                                                 
127500                                                                          
127501     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
127502     05  PV-ABEND-INFO               PIC X(80)     VALUE SPACES.          
127511                                                                          
127513     05  PV-PREV-ISA-CONTROL.                                             
127514        10  PV-PREV-ISA-CNTL-NBR-SEQ PIC 9(9)  VALUE ZEROS.               
127515        10  PV-PREV-SHIPT-ID-SEQ     PIC X(30) VALUE SPACES.              
127516        10  PV-PREV-DAT-XFER-CNTL    PIC 9(9)  VALUE ZEROS.               
127517     05  PV-PREV-CONTAINER-SORT      PIC X(16) VALUE LOW-VALUES.          
127518     05  PV-PREV-PO-NBR-SORT         PIC X(22) VALUE LOW-VALUES.          
127519     05  PV-CNTL-ID-DISPLAY          PIC 9(10) VALUE ZEROS.               
128410                                                                          
128420 01  ABEND-CODE       PIC S9(4) COMP SYNC VALUE ZERO.                     
128430       88  ABEND-4002-INCORRECT-TOTALS    VALUE +4002.                    
128440       88  ABEND-4004-SQL-INSERT-ERROR    VALUE +4004.                    
128441       88  ABEND-4005-SQL-COMMIT-ERROR    VALUE +4005.                    
128450       88  ABEND-4006-SQL-SELECT-ERROR    VALUE +4006.                    
247465                                                                          
247471* DB2 TABLE DECLARATION                                           02340000
247472* (SNT_DAT_CNTL - ASN DATA TRANSFER CONTROL TABLE)                02350000
247473     EXEC SQL                                                     02360000
247474         INCLUDE SNT00000                                         02370008
247475     END-EXEC.                                                    02380000
247476                                                                  02390000
247477* DB2 TABLE DECLARATION                                           02400000
247478* (SNT_DAT_XFER - ASN DATA TRANSFER DETAIL TABLE)                 02410000
247479     EXEC SQL                                                     02420000
247480         INCLUDE SNT00001                                         02430008
247481     END-EXEC.                                                    02440000
247482                                                                  02450000
247483* INPUT LAYOUT - ASN DTL RECORD COPYBOOK - UNPACKED.              02460039
247484     COPY SNRD005.                                                02470039
247492                                                                          
247493 PROCEDURE DIVISION.                                                      
247495******************************************************************        
247496*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
247497******************************************************************        
247498                                                                          
247499 0000-PROGRAM-MAINLINE.                                                   
247500                                                                          
494665***  DISPLAY '0000-PROGRAM-MAINLINE'.                                     
494666                                                                          
494667     PERFORM 1000-INITIALIZE.                                             
494668                                                                          
494669     IF PS-END-OF-SORTIN                                                  
494670        DISPLAY 'EMTY FILE'                                               
494671     ELSE                                                                 
494672        PERFORM 2000-MAIN-LINE-PROCESS                                    
494673           UNTIL PS-END-OF-SORTIN.                                        
494674                                                                          
494675     PERFORM 8000-EOJ-ROUTINE.                                            
494676     STOP RUN.                                                            
494677                                                                          
494678******************************************************************        
494679*  OPENS INPUT AND OUTPUT FILES AND PERFORMS OTHER BEGINNING OF  *        
494680*  JOB PROCESSES.                                                *        
494681******************************************************************        
494682                                                                          
494683 1000-INITIALIZE.                                                         
494684                                                                          
494685***  DISPLAY '1000-INITIALIZE'.                                           
494686                                                                          
494687     OPEN INPUT PV-IN-SORT-FILE                                           
494688         OUTPUT PV-OUT-STRIP-FILE.                                        
494693                                                                          
494694*    PERFORM INITIAL READ                                                 
494695     PERFORM 6000-READ-SORT-IN-FILE.                                      
494696                                                                          
494697     DISPLAY '+++ START LIST DISCARDED RECORDS +++'.                      
494698*    DETAIL GETS DISPLAYED IN 2000-MAIN-LINE-PROCESS                      
494699*      SEE - IF PS-IN-PO-GROUP-REC-ID = 'PO'                              
494700                                                                          
494710 EJECT                                                                    
494764                                                                          
494765******************************************************************        
494766* MAIN PROCESSING PARAGRAPH.                                     *        
494767******************************************************************        
494768 2000-MAIN-LINE-PROCESS.                                                  
494769                                                                          
494770***  DISPLAY '2000-MAIN-LINE-PROCESS'.                                    
494771                                                                          
494775     IF ID-IDENTIFICATION-REC                                             
494796       SET PS-CNTL-TBL-ALREADY-UPDT-NO TO TRUE                            
494797       MOVE TR063-DAT-XFER-CNTL-ID                                        
494798                             TO SNT00000-DAT-XFER-CNTL-ID                 
494801     END-IF.                                                              
494802                                                                          
494803     IF HL-HIERARCHICAL-REC                                               
494804       IF ORDER-LEVEL                                                     
494805         PERFORM 2100-CHECK-DUPLICATE-PO                                  
494806         IF PS-THIS-IS-DUPLICATE-PO                                       
494807           IF PS-CNTL-TBL-ALREADY-UPDT                                    
494808             CONTINUE                                                     
494809           ELSE                                                           
494811             PERFORM 2200-UPDATE-CNTL-TBL                                 
494812             SET PS-CNTL-TBL-ALREADY-UPDT TO TRUE                         
494813           END-IF                                                         
494814         END-IF                                                           
494815       END-IF                                                             
494816     END-IF.                                                              
494817                                                                          
494818     IF PS-IN-PO-GROUP-REC-ID = 'PO'                                      
494819      AND PS-THIS-IS-DUPLICATE-PO                                         
494820       ADD +1 TO PA-DISCARD-COUNT                                         
494821       IF PV-IN-ORIGINAL-EDI-FIELDS(1:2) = 'HL'                           
494822         DISPLAY ' - ASN        CONTAINER     ' 'PO        '              
494823                 'REC TYPE'                                               
494824       END-IF                                                             
494825       DISPLAY '   ' PV-IN-ISA-CNTL-NBR-SEQ ' '                           
494826*                    *** CONTAINER NUMBER BEFORE TRIM ETC.                
494827                     PV-IN-TD302-EQUIP-INIT-CDE                           
494828                     PV-IN-TD303-EQUIP-NBR  ' '                           
494829*                    *** END CONTAINER NUMBER                             
494830                     PV-IN-PO-NBR-SORT(1:10) ' '                          
494831                     PV-IN-ORIGINAL-EDI-FIELDS(1:10)                      
494832     ELSE                                                                 
494833       MOVE PV-IN-ORIGINAL-EDI-FIELDS TO                                  
494840                              PV-OUT-ORIGINAL-EDI-FIELDS                  
495331       PERFORM 6100-WRITE-OUT-REC                                         
495332     END-IF.                                                              
495344                                                                          
495345     PERFORM 6000-READ-SORT-IN-FILE.                                      
495346                                                                          
495347******************************************************************        
495348* IF CONTROL DATA HAS CHANGED, FLAG PO CONTROL RECS AS DUPLICATES         
495349* SO THEY CAN BE BYPASSED WHEN WRITING TO OUTPUT FILE.                    
495350******************************************************************        
495351                                                                          
495352 2100-CHECK-DUPLICATE-PO.                                                 
495353                                                                          
495354***  DISPLAY '2100-CHECK-DUPLICATE-PO'.                                   
495357                                                                          
495358     IF   PV-IN-ISA-CONTROL     = PV-PREV-ISA-CONTROL                     
495359      AND PV-IN-CONTAINER-SORT  = PV-PREV-CONTAINER-SORT                  
495360      AND PV-IN-PO-NBR-SORT     = PV-PREV-PO-NBR-SORT                     
495361         SET PS-THIS-IS-DUPLICATE-PO TO TRUE                              
495362     ELSE                                                                 
495363         SET PS-THIS-IS-DUPLICATE-PO-NO TO TRUE                           
495364         MOVE PV-IN-ISA-CONTROL    TO PV-PREV-ISA-CONTROL                 
495365         MOVE PV-IN-CONTAINER-SORT TO PV-PREV-CONTAINER-SORT              
495366         MOVE PV-IN-PO-NBR-SORT    TO PV-PREV-PO-NBR-SORT                 
495367     END-IF.                                                              
495400                                                                          
498500******************************************************************        
498600* MARK THIS ASN ID AS HAVING DUPLICATE PURCHASE ORDERS.                   
498700******************************************************************        
498800                                                                          
498900 2200-UPDATE-CNTL-TBL.                                                    
499000                                                                          
499100***  DISPLAY '2200-UPDATE-CNTL-TBL'.                                      
499200                                                                          
499300     EXEC SQL                                                             
499400         UPDATE SNT_DAT_CNTL                                              
499500            SET STAT_CDE         = 'D'                                    
499600          WHERE DAT_XFER_CNTL_ID = :SNT00000-DAT-XFER-CNTL-ID             
499700     END-EXEC.                                                            
499800                                                                          
499900     EVALUATE TRUE                                                        
500000       WHEN SQLCODE = ZERO                                                
500100           ADD +1 TO PA-CNTL-UPDATES                                      
500102           MOVE SNT00000-DAT-XFER-CNTL-ID                                 
500103                                     TO PV-CNTL-ID-DISPLAY                
500110           DISPLAY '    DAT_XFER_CNTL_ID UPDATED - '                      
500200                PV-CNTL-ID-DISPLAY                                        
500300                                                                          
500400       WHEN OTHER                                                         
500500           DISPLAY '**************************************'               
500600           DISPLAY '** ABEND                             *'               
500700           DISPLAY '** UPDATE SNT_DAT_CNTL               *'               
500900           DISPLAY '**************************************'               
501000           SET ABEND-4004-SQL-INSERT-ERROR TO TRUE                        
501100           MOVE '2200-UPDATE-CNTL-TBL' TO PV-PARAGRAPH-NAME               
501200           MOVE 'SNT00001-DAT-XFER-CNTL-ID - '                            
501201                                     TO PV-ABEND-INFO (1:28)              
501202           MOVE SNT00001-DAT-XFER-CNTL-ID                                 
501203                                     TO PV-CNTL-ID-DISPLAY                
501205           MOVE PV-CNTL-ID-DISPLAY   TO PV-ABEND-INFO (29:10)             
501300           PERFORM 9000-USER-ABEND                                        
501400     END-EVALUATE.                                                        
501500                                                                          
809217******************************************************************        
809218* READ A RECORD FROM THE SORTED IN FILE.                                  
809219******************************************************************        
809220 6000-READ-SORT-IN-FILE.                                                  
809221                                                                          
809222***  DISPLAY '6000-READ-SORT-IN-FILE'.                                    
809230                                                                          
809233     READ PV-IN-SORT-FILE                                                 
809234         INTO PV-IN-SORT-REC-LAYOUT                                       
809235      AT END                                                              
809236         SET PS-END-OF-SORTIN TO TRUE.                                    
809237                                                                          
809238     IF PS-END-OF-SORTIN-NO                                               
809301       ADD +1 TO PA-INPUT-COUNT                                           
809302       MOVE PV-IN-SORT-REC-LAYOUT TO TR063-SHIP-NOTICE-DTL-RECORD         
809305     END-IF.                                                              
810409                                                                          
810410*****************************************************************         
810411* WRITE UPDATE RECORD                                                     
810412*****************************************************************         
810413 6100-WRITE-OUT-REC.                                                      
810414                                                                          
810415***  DISPLAY '6100-WRITE-OUT-REC'.                                        
810416                                                                          
811502     WRITE PV-OUT-STRIP-RECORD FROM PV-OUT-STRIP-REC-LAYOUT.              
811503     ADD +1 TO PA-OUTPUT-COUNT.                                           
811504                                                                          
836191                                                                          
836192******************************************************************        
836193* CLOSE THE INPUT AND OUTPUT FILES AND DISPLAY MISCELLANEOUS     *        
836194* JOB SUMMARY INFORMATION.                                       *        
836200******************************************************************        
836300 8000-EOJ-ROUTINE.                                                        
836301                                                                          
836310***  DISPLAY '8000-EOJ-ROUTINE'.                                          
836400                                                                          
842300     CLOSE PV-IN-SORT-FILE                                                
842400           PV-OUT-STRIP-FILE.                                             
843002                                                                          
843003     DISPLAY '+++ END LIST DISCARDED RECORDS   +++'.                      
843004     DISPLAY ' '.                                                         
843005                                                                          
843006     COMPUTE PA-OUTPUT-PLUS-DISCARD =                                     
843007         (PA-DISCARD-COUNT + PA-OUTPUT-COUNT).                            
843008                                                                          
843009     DISPLAY '*************************************************'.         
843010     DISPLAY '** TRKUP213 SUMMARY                            **'.         
843011     DISPLAY '*************************************************'.         
843012     DISPLAY 'TOTAL RECORDS READ        = ' PA-INPUT-COUNT.               
843013     DISPLAY '*************************************************'.         
843014     DISPLAY 'TOTAL DISCARD DUP PO RECS = ' PA-DISCARD-COUNT.             
843015     DISPLAY '*************************************************'.         
843016     DISPLAY 'TOTAL RECORDS WRITTEN     = ' PA-OUTPUT-COUNT.              
843017     DISPLAY '*************************************************'.         
843018     DISPLAY 'TOTAL RECORDS PROCESSED   = '                               
843019                                     PA-OUTPUT-PLUS-DISCARD.              
843020     DISPLAY '*************************************************'.         
843021     DISPLAY 'TOTAL UPDATES MADE TO SNT_DAT_CNTL  = '                     
843022                                     PA-CNTL-UPDATES.                     
843023                                                                          
843024     IF PA-INPUT-COUNT = PA-OUTPUT-PLUS-DISCARD                           
843025        CONTINUE                                                          
843026     ELSE                                                                 
843027*       **** SOME INPUT REC(S) DID NOT GET PROCESSED OR COUNTED           
843028        SET ABEND-4002-INCORRECT-TOTALS TO TRUE                           
843029        MOVE '8000-EOJ-ROUTINE' TO PV-PARAGRAPH-NAME                      
843030        MOVE 'INPUT RECS NOT = OUTPUT + DISCARDED RECS'                   
843031                                TO PV-ABEND-INFO                          
843032        PERFORM 9000-USER-ABEND                                           
843033     END-IF.                                                              
843034                                                                          
843035     PERFORM 8100-COMMIT.                                                 
843036                                                                          
843037******************************************************************        
843038* DB2 COMMIT AT THE END OF SUCCESSFUL RUN ONLY.                           
843039******************************************************************        
843040                                                                          
843041 8100-COMMIT.                                                             
843042                                                                          
843043***  DISPLAY '8100-COMMIT'.                                               
843044                                                                          
843045     EXEC SQL                                                             
843046         COMMIT                                                           
843047     END-EXEC.                                                            
843048                                                                          
843049     EVALUATE TRUE                                                        
843050       WHEN SQLCODE = ZERO                                                
843051           DISPLAY '  **** COMMIT SUCCESSFUL ****'                        
843052                                                                          
843053       WHEN OTHER                                                         
843054           DISPLAY '**************************************'               
843055           DISPLAY '** ABEND                             *'               
843056           DISPLAY '** DB2 COMMIT                        *'               
843057           DISPLAY '**************************************'               
843058           SET ABEND-4005-SQL-COMMIT-ERROR TO TRUE                        
843059           MOVE '8100-COMMIT' TO PV-PARAGRAPH-NAME                        
843062           PERFORM 9000-USER-ABEND                                        
843063     END-EVALUATE.                                                        
843064                                                                          
843065******************************************************************        
843066* USER ABEND                                                     *        
843067******************************************************************        
843068 9000-USER-ABEND.                                                         
843069                                                                          
843070***  DISPLAY '9000-USER-ABEND'.                                           
843071                                                                          
843072     DISPLAY '+++++++++++++++++++++++++++++++++++++++++++++'.             
843073     DISPLAY 'PROGRAM = TRKUP213'                                         
843074     DISPLAY 'THIS IS A USER ABEND GENERATED BY PARAGRAPH -'.             
843075     DISPLAY ' ' PV-PARAGRAPH-NAME,                                       
843076     DISPLAY ' ' PV-ABEND-INFO.                                           
843077                                                                          
843078******************************************************************        
843079* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-   *        
843080* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE        *        
843081* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE    *        
843082* FORMAT.                                                        *        
843083******************************************************************        
843084     COPY DPPD004.                                                        
843085     CALL 'ILBOABN0' USING ABEND-CODE.                                    
843090                                                                          
