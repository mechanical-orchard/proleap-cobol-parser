       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         MLKUT611.                                            
       DATE-WRITTEN.       10/30/2000.                                          
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *PURPOSE:  THE MODULE IS USED FOR FIRE DAMAGE STORE TRANSFERS. THE        
      *        MODULE READS THE INPUT STORE TRANFER FILE AND REFORMATS          
      *        THE FILE TO THE PRICE CHANGE RECORD LAYOUT.                      
      *          A CONTROL CARD IS PASSED INTO THE MODULE TO IDENTIFY           
      *        THE STORE NUMBER EXTRACT INFORMATION IS BEING GENERATED          
      *        FOR.....                                                         
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                             
      * -------  ----------  ----------------------------------------           
507721* 507721   11-12-2002  NI-SKU CHANGES AND PRODUCTIONALIZE.                
507721*                      (CREATION) -ROD JANSSEN                            
21731 * 21731    04-30-2003  NI-SKU CHANGES TO USE MLRD019 INSTEAD OF           
21731 *                      MLRD005O.  -V. LEE YANG                            
RONNA * 21731    04-30-2003  THERE ARE HARDCODED DATES IN HERE ALONG            
RONNA *                      WITH OTHER HARDCODED STUFF                         
LY1003* LY1003   10-29-2003  DYNAMIC DEFINE OF THE FSCL MONTH DTE.              
LY1003*                      - ROD JANSSEN                                      
W26600* W26600   07-14-2004  INCORPORATE STORE EXPANSION CHANGES                
W26600*                      MLRD017 LRECL CHANGES FROM 100 TO 120 BYTES        
W26600*                      & ADD MISSING FIELDS T0 OUTPUT FILE (OUT01)        
W26600*                      EXPAND INP02 FOR STORE NUMBER LENGTH X(05).        
W26600*                      - SCOTT JACKSON                                    
W28545* W28545   08-22-2005  ROD JANSSEN THE NEW TRANSFER PROCESS               
W29145* W29145   11-20-2005  EXPAND MLRD017 FROM LRECL=120 TO LRECL=200         
W29145*                      FOR SAS OTIMIZATION AND VENDOR/BRAND NEEDS         
W29145*                      - SCOTT JACKSON                                    
PM3403* PM3403   04-20-2012   LARGE RETAIL RECOMPILE FOR MLRD017/MLRD019        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT INPUT2-FILE                                                   
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT OUTPUT-FILE                                                   
               ASSIGN TO OUT01.                                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT-TRANSFER-RECORD.                                
                                                                                
21731  01  INPUT-TRANSFER-RECORD    PIC X(80).                                  
                                                                        ETKUT004
       FD  INPUT2-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORD IS INPUT2-RECORD.                                        
                                                                                
LY1003 01  INPUT2-RECORD            PIC X(80).                                  
   |  *    05  IR-STORE-NUMBER      PIC X(03).                                  
   |  *    05  FILLER               PIC X(01).                                  
   |  *    05  IR-BATCH-NUMBER      PIC X(03).                                  
LY1003*    05  IR-REST-OF-RECORD    PIC X(73).                                  
                                                                        ETKUT004
       FD  OUTPUT-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
W26600*    RECORD CONTAINS 100 CHARACTERS                                       
W26600     RECORD CONTAINS 200 CHARACTERS                                       
21731      DATA RECORD IS OUTPUT-PRICE-CHANGE-RECORD.                           
                                                                                
W26600*01  OUTPUT-PRICE-CHANGE-RECORD    PIC X(120).                            
W29145 01  OUTPUT-PRICE-CHANGE-RECORD    PIC X(200).                            
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                        ETKUT005
LY1003 01  IR-INPUT-RECORD-CC.                                          MLKUT002
W26600*    05  IR-STORE-NUMBER      PIC X(03).                                  
W26600     05  IR-STORE-NUMBER      PIC X(05).                                  
LY1003     05  FILLER               PIC X(01).                                  
   |       05  IR-BATCH-NUMBER      PIC X(03).                                  
   |       05  FILLER               PIC X(01).                                  
   |       05  IR-TIMESTAMP.                                                    
   |           10  IR-TIMESTAMP-DATE-MM   PIC X(02).                            
   |           10  FILLER                 PIC X(01).                            
   |           10  IR-TIMESTAMP-DATE-DD   PIC X(02).                            
   |           10  FILLER                 PIC X(01).                            
   |           10  IR-TIMESTAMP-DATE-CCYY PIC X(04).                            
   |       05  FILLER               PIC X(01).                                  
LY1003     05  IR-PC-KOHLS-MM-FSCL  PIC X(02).                                  
W26600*    05  IR-REST-OF-RECORD    PIC X(59).                                  
W26600     05  IR-REST-OF-RECORD    PIC X(57).                                  
LY1003                                                                          
       01  PROGRAM-SWITCHES.                                            ETKUT004
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.           ETKUT004
               88  END-OF-FILE                     VALUE 'Y'.           ETKUT004
           05  ERRORS-FOUND-SW           PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
                                                                                
       01  PROGRAM-VARIABLES.                                           ETKUT004
           05  PV-MAJOR-CLASS            PIC 9(04) VALUE ZEROES.        ETKUT004
           05  FILLER REDEFINES PV-MAJOR-CLASS.                                 
               10  FILLER                PIC X(02).                             
               10  PV-MAJOR-CLASS-1      PIC X(01).                             
               10  PV-MAJOR-CLASS-2      PIC X(01).                             
           05  PV-TIMESTAMP.                                                    
               10  PV-TIMESTAMP-DATE-CCYY PIC X(04)     VALUE SPACES.           
               10  FILLER                 PIC X(01)     VALUE '-'.              
               10  PV-TIMESTAMP-DATE-MM   PIC X(02)     VALUE SPACES.           
               10  FILLER                 PIC X(01)     VALUE '-'.              
               10  PV-TIMESTAMP-DATE-DD   PIC X(02)     VALUE SPACES.           
               10  FILLER                 PIC X(01)     VALUE '-'.              
               10  PV-TIMESTAMP-TIME      PIC X(15)     VALUE                   
                   '23.59.59.999999'.                                           
LY1003     05  PV-PC-DTE-MMDD.                                                  
LY1003         10  PV-PC-DTE-MM     PIC 9(02).                                  
LY1003         10  PV-PC-DTE-DD     PIC 9(02).                                  
                                                                                
       01  WS-ACCUMULATOR-AREA.                                                 
           05  WS-RECS-READ-ACCUM        PIC S9(13) COMP VALUE ZEROS.           
           05  WS-RECS-WRITE-ACCUM       PIC S9(13) COMP VALUE ZEROS.           
                                                                                
21731 *01  ML019-TRANSFER-RECORD.                                               
21731      COPY MLRD019.                                                        
                                                                                
21731 *01  ML017-PRICE-CHANGE-RECORD.                                           
21731      COPY MLRD017.                                                        
                                                                                
      /*****************************************************************        
      *FLOW:    THE MODULE SEQUENTIALLY READS THE STORE TRANSFER RECORD.        
      *                                                                         
      *         FORMAT THE INPUT FILE ROWS TO REFLECT THE LEDGER PRICE          
      *      CHANGE RECORD FORMAT FOUND IN THE COPYBOOK MLRD017.                
      *                                                                         
      *         IF A PROBLEM OCCURS WHILE, ABEND WITH A 4095 ERROR CODE.        
      *      THE ERROR CODE TRIGGERS AN EMAIL TO THE LEDGER TEAM FOR            
      *      ERROR CORRECTION.                                                  
      *                                                                         
      ******************************************************************        
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAINLINE-DRIVER-SECTION.                                            
                                                                                
           PERFORM 1000-INITIALIZATION                                          
              THRU 1000-INITIALIZATION-EXIT.                                    
                                                                                
           PERFORM 2000-PROCESS-INPUT-FILE                                      
              THRU 2000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           PERFORM 9000-EOJ-TASKS                                               
              THRU 9000-EOJ-TASKS-EXIT.                                         
                                                                                
       9999-EXIT-PROGRAM.                                                       
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                                    
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      *** GET READY TO PROCESS INFORMATION.....                                 
                                                                                
           OPEN  INPUT INPUT-FILE                                               
                 INPUT INPUT2-FILE                                              
                OUTPUT OUTPUT-FILE.                                             
                                                                                
21731      INITIALIZE ML019-TRANSFER-RECORD.                                    
           INITIALIZE INPUT2-RECORD.                                            
                                                                                
           MOVE ZEROS          TO WS-RECS-READ-ACCUM.                           
           MOVE ZEROS          TO WS-RECS-WRITE-ACCUM.                          
           MOVE 'N'            TO END-OF-FILE-SW.                               
           MOVE 'N'            TO ERRORS-FOUND-SW.                      ETKUT004
                                                                        ETKUT004
      *** READ THE INBOUND CONTROL CARD TO DETERMINE WHAT STORE WE ARE          
      *** PROCESSING FOR.......                                                 
                                                                                
           PERFORM 8000-READ-INPUT-FILE                                         
              THRU 8000-READ-INPUT-FILE-EXIT.                                   
                                                                                
LY1003     MOVE IR-TIMESTAMP-DATE-MM TO PV-PC-DTE-MM.                           
LY1003     MOVE IR-TIMESTAMP-DATE-DD TO PV-PC-DTE-DD.                           
LY1003                                                                          
       1000-INITIALIZATION-EXIT.                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * 2000-PROCESS INPUT FILE.                                                
      ******************************************************************        
       2000-PROCESS-INPUT-FILE.                                                 
      *** READ THE FIRE DAMGE STORE TRANSFER DATASET AND FORMAT PRICE           
      *** CHANGE RECORDS ACCORDINGLY...                                         
                                                                                
21731      READ INPUT-FILE INTO ML019-TRANSFER-RECORD                           
               AT END  MOVE 'Y'         TO END-OF-FILE-SW                       
                       GO TO 2000-PROCESS-INPUT-FILE-EXIT.                      
                                                                                
           ADD 1                        TO WS-RECS-READ-ACCUM.                  
                                                                                
W26600*    MOVE SPACES                  TO ML017-PRICE-CHANGE-RECORD.           
W26600     INITIALIZE                      ML017-PRICE-CHANGE-RECORD.           
           MOVE '4'                     TO ML017-PC-CDE.                        
           MOVE '1'                     TO ML017-PC-TYP-CDE.                    
           MOVE SPACE                   TO ML017-SLS-CORR-CDE.                  
21731      MOVE ML019-TRANSFER-NBR      TO ML017-PC-DOC-5-NBR.                  
LY1003                                                                          
LY1003     MOVE PV-PC-DTE-MMDD          TO ML017-PC-DTE.                        
LY1003     MOVE IR-PC-KOHLS-MM-FSCL     TO ML017-PC-KOHLS-MM.                   
LY1003**** MOVE '0827'                  TO ML017-PC-DTE.                        
LY1003**** MOVE '07'                    TO ML017-PC-KOHLS-MM .                  
LY1003                                                                          
21731      MOVE ML019-SKU                TO ML017-PC-SKU.                       
21731      MOVE ML019-TRANSFER-DEPT-NBR  TO ML017-PC-DEPT-NBR.                  
21731      MOVE ML019-TRANSFER-CLASS-NBR TO ML017-PC-CLASS-NBR.                 
21731      MOVE ML019-TRANSFER-CLASS-NBR(1:1) TO PV-MAJOR-CLASS-1.              
21731      MOVE PV-MAJOR-CLASS           TO ML017-PC-MAJ-CLASS.                 
RONNA      MOVE '5'                      TO ML017-PC-RSN-CDE.                   
21731      MOVE ML019-TRANSFER-PER-UNT-RTL-AMT TO ML017-PC-OLD-PR-AMT.          
           MOVE ZEROS                    TO ML017-PC-NEW-PR-AMT.                
21731      MOVE ML019-TRANSFER-QTY       TO ML017-PC-QTY.                       
                                                                                
           IF IR-BATCH-NUMBER IS GREATER THAN SPACES                            
               MOVE IR-BATCH-NUMBER      TO ML017-PC-BATCH-NBR                  
           ELSE                                                                 
21731          MOVE ML019-TRANSFER-BATCH-NBR  TO ML017-PC-BATCH-NBR             
W26600     END-IF.                                                              
                                                                                
RONNA      MOVE '502'                   TO ML017-PC-MKDN-MKUP-RSN-CDE.          
           MOVE ZEROS                   TO ML017-PC-DIFFER-AMT.                 
21731      MOVE ML019-TRANSFER-QTY       TO ML017-PC-TOT-DOC-QTY.               
LY1003                                                                          
LY1003*    MOVE IR-STORE-NUMBER         TO ML017-PC-STORE-NBR.                  
W26600     IF IR-STORE-NUMBER > SPACES                                          
W26600         MOVE IR-STORE-NUMBER             TO ML017-PC-STORE-NBR           
W26600     ELSE                                                                 
LY1003         MOVE ML019-TRANSFER-TO-STORE-NBR TO ML017-PC-STORE-NBR           
W26600     END-IF.                                                              
LY1003                                                                          
RONNA      MOVE '5'                     TO ML017-PC-CATEG-CDE.                  
LY1003                                                                          
LY1003     STRING '20' DELIMITED BY SIZE                                        
LY1003            ML019-TRANSFER-NBR DELIMITED BY SIZE                          
LY1003                                  INTO ML017-PC-DOC-7-NBR.                
LY1003***  MOVE '2000000'               TO ML017-PC-DOC-7-NBR.                  
LY1003                                                                          
           MOVE SPACES                  TO ML017-PC-VENDOR-INHSE-NMBR.          
507721     MOVE ZEROS                   TO ML017-PC-LINE-NBR.                   
W26600                                                                          
W26600     MOVE ZEROS                   TO ML017-RGST-ID.                       
W26600     MOVE ZEROS                   TO ML017-TRN-NBR.                       
W26600     MOVE ZEROS                   TO ML017-LNE-ITM-SEQ-NBR.               
W26600     MOVE SPACES                  TO ML017-GMM.                           
W26600     MOVE ZEROS                   TO ML017-DIVISION-NUMBER.               
W26600     MOVE SPACES                  TO ML017-DMM.                           
W26600     MOVE SPACES                  TO ML017-BUYER.                         
W28545     MOVE ML019-TRANSFER-ENT-ID   TO ML017-PC-ENT-ID.                     
                                                                                
21731      WRITE OUTPUT-PRICE-CHANGE-RECORD                                     
21731          FROM ML017-PRICE-CHANGE-RECORD.                                  
                                                                                
           ADD 1                        TO WS-RECS-WRITE-ACCUM.                 
                                                                                
       2000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 8000-READ-INPUT-FILE.                                                   
      ******************************************************************        
       8000-READ-INPUT-FILE.                                                    
      *** READ IN THE CONTROL CARD WITH THE PASSED STORE NUMBER. IF WE          
      *** HAVE A PROBLEM OR HIT END OF FILE, MOVE "***" TO THE STORE            
      *** NUMBER TO IDENTIFY A PROBLEM. FLAG THE ABEND SWITH TO SEND            
      *** AN EMAIL TO THE LEDGER TEAM OF A PROBLEM.....                         
                                                                        ETKUT004
           READ INPUT2-FILE NEXT                                        ETKUT004
LY1003       INTO IR-INPUT-RECORD-CC                                            
               AT END  MOVE '***' TO IR-STORE-NUMBER                            
                       MOVE 'Y'   TO ERRORS-FOUND-SW                    ETKUT004
                       CLOSE INPUT2-FILE                                ETKUT004
                       OPEN INPUT INPUT2-FILE.                          ETKUT004
                                                                                
       8000-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
      /*****************************************************************        
      * 9000-END OF JOB TASKS.                                                  
      ******************************************************************        
       9000-EOJ-TASKS.                                                          
      *** CLOSE THE FILES. SET THE RETURN CODE IF WE HAVE A PROBLEM...          
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE.                                         
                                                                                
           CLOSE INPUT-FILE                                                     
                 INPUT2-FILE                                                    
                 OUTPUT-FILE.                                                   
                                                                                
       9000-EOJ-TASKS-EXIT.                                                     
           EXIT.                                                                
