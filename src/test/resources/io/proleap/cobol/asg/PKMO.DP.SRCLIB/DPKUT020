      ******************************************************************   LV028
       IDENTIFICATION     DIVISION.                                     DPKUT020
00003 ******************************************************************   LV028
00004                                                                      CL*28
00005  PROGRAM-ID.             DPKUT020.                                   CL*28
00006  AUTHOR.                 BRUCE JOHNSON.                              CL*28
00007  INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL*28
00008  DATE-WRITTEN.           01-31-92.                                   CL*28
00009  DATE-COMPILED.                                                      CL*28
00010                                                                      CL*28
00011 ******************************************************************   CL*28
00012 *     DPKUT020 WILL LOAD AN INTERNAL WORKING STORAGE TABLE FROM  *   CL*28
00013 *     THE MBS BASED UPON THE CALLING PROGRAMS REQUEST.           *   CL*28
00014 *     IF THE VALUE OF PS-REQUEST IS:                             *   CL*28
00015 *     1) DPT - ALL DEPARTMENTS AND DESCRIPTIONS WILL BE LOADED   *   CL*28
00016 *              INTO THE INTERNAL DEPARTMENT TABLE.               *   CL*28
00017 *     2) MGR - ALL MANAGER NUMBERS AND NAMES WILL BE LOADED INTO *   CL*28
00018 *              TO THE INTERNAL MANAGER TABLE.                    *   CL*28
00019 *     3) BYR - ALL BUYER NUMBERS AND NAMES WILL BE LOADED INTO   *   CL*28
00020 *              TO THE INTERNAL BUYER TABLE.                      *   CL*28
00021 *     THE FULLY LOADED TABLE WILL BE PASSED BACK TO THE CALLING  *   CL*28
00022 *     PROGRAM UPON DPKUT020 TERMINATION.                         *   CL*28
      *                                                                         
      *CHG ID/                                                                  
      *WR/PROJ DATE       PGMR        DESCRIPTION                               
      *------- ---------- ----------- ----------------------------------        
W29908* W29908 01-09-2006 ROD JANSSEN THE DEPT TABLE MAXED IN DPKUT020.         
W29908*                               CHANGE THE CALLED MODULE TO               
W29908*                               HANDLE 999 ENTRIES FOR DEPT.              
00023 ******************************************************************   CL*28
00024                                                                      CL*28
00025 ******************************************************************   CL*28
00026  ENVIRONMENT DIVISION.                                               CL*28
00027 ******************************************************************   CL*28
00028                                                                      CL*28
00029  CONFIGURATION SECTION.                                              CL*28
00030                                                                      CL*28
00031  SOURCE-COMPUTER.        IBM-4381.                                   CL*28
00032  OBJECT-COMPUTER.        IBM-4381.                                   CL*28
00033                                                                      CL*28
00034  INPUT-OUTPUT SECTION.                                               CL*28
00035                                                                      CL*28
00036  FILE-CONTROL.                                                       CL*28
00037                                                                      CL*28
00038 ******************************************************************   CL*28
00039  DATA DIVISION.                                                      CL*28
00040 ******************************************************************   CL*28
00041                                                                      CL*28
00042  WORKING-STORAGE SECTION.                                            CL*28
00043                                                                      CL*28
00044  01  PS-PROGRAM-SWITCHES.                                            CL*28
00045      05  PS-REQUEST                      PIC X(08)   VALUE SPACE.    CL*28
00046          88  LOAD-DEPARTMENT-TABLE       VALUE 'DPT'.                CL*28
00047          88  LOAD-BUYER-TABLE            VALUE 'BYR'.                CL*28
00048          88  LOAD-MANAGER-TABLE          VALUE 'MGR'.                CL*28
00049      05  PS-TABLE-LOADED                 PIC X(01)   VALUE 'N'.      CL*28
00050          88  TABLE-LOADED                VALUE 'Y'.                  CL*28
00051                                                                      CL*28
00052  01  PC-PROGRAM-CONSTANTS.                                           CL*28
00053      05  PC-SUB-CL-NBR                   PIC X(03)   VALUE '000'.    CL*28
00054      05  PC-MDSELV-CDE-DPT               PIC X(03)   VALUE 'DPT'.    CL*28
00055      05  PC-RSPLVL-CDE-BYR               PIC X(03)   VALUE 'BYR'.    CL*28
00056      05  PC-RSPLVL-CDE-DMM               PIC X(03)   VALUE 'DMM'.    CL*28
00057      05  PC-OPERCO-NBR                   PIC X(02)   VALUE '03'.     CL*28
00058      05  PC-APLSYS-CDE                   PIC X(02)   VALUE 'ML'.     CL*28
00059      05  PC-MAX-RETRIES                  PIC S9(4)                   CL*28
00060                                          COMP SYNC   VALUE +2.       CL*28
00061      05  PC-BYR-MAX                      PIC S9(3)   VALUE +100.     CL*28
00062      05  PC-MGR-MAX                      PIC S9(3)   VALUE +25.      CL*28
W29908     05  PC-DPT-MAX                PIC S9(5) COMP VALUE +999.        CL*28
W29908**** 05  PC-DPT-MAX                      PIC S9(3)   VALUE +200.     CL*28
00064                                                                      CL*28
00065  01  PV-PROGRAM-VARIABLES.                                           CL*28
00066      05  PV-RETRY-COUNT                  PIC S9(4)                   CL*28
00067                                          COMP SYNC   VALUE +0.       CL*28
00068      05  PV-DEPT-NBR                     PIC X(03)   VALUE SPACE.    CL*28
00069      05  PV-DMA-NBR                      PIC X(02)   VALUE SPACE.    CL*28
00070      05  PV-BYR-NBR-GROUP.                                           CL*28
00071          10  PV-BYR-NBR-1                PIC X(01)   VALUE '0'.      CL*28
00072          10  PV-BYR-NBR-2-3              PIC X(02)   VALUE SPACE.    CL*28
00073      05  PV-BYR-NBR REDEFINES PV-BYR-NBR-GROUP                       CL*28
00074                                          PIC X(03).                  CL*28
00075      05  PV-PARAGRAPH-NAME               PIC X(30)   VALUE SPACE.    CL*28
00076      05  PV-DB2-FUNCTION                 PIC X(30)   VALUE SPACE.    CL*28
00077                                                                      CL*28
00078  01  PD-PASSED-DATA.                                                 CL*28
00079      05 PD-REQUEST                         PIC X(08).                CL*28
00080      05 PD-OPERCO-NBR                      PIC X(02).                CL*28
00081      05 PD-APLSYS-CDE                      PIC X(02).                CL*28
00082      05 PD-DATE                            PIC X(10).                CL*28
W29908     05 PD-TABLE                           PIC X(27972).             CL*28
W29908***  05 PD-TABLE                           PIC X(10000).             CL*28
00084      EJECT                                                           CL*28
00085  01  TB-PROGRAM-TABLES.                                              CL*28
00086      05  TB-BYR-TABLE.                                               CL*28
00087          10  TB-BYR-ENTRY                OCCURS                      CL*28
00088                                          100                         CL*28
00089                                          TIMES                       CL*28
00090              INDEXED BY PV-BYR-INDEX.                                CL*28
00091              15  TB-BYR-MDSEAR-BYR-NBR   PIC X(03)   VALUE SPACE.    CL*28
00092              15  TB-BYR-RSPBAR-WORKER-LAST-NAME                      CL*28
00093                                          PIC X(18)   VALUE SPACE.    CL*28
00094              15  TB-BYR-RSPBAR-WORKER-FIR-NAME                       CL*28
00095                                          PIC X(12)   VALUE SPACE.    CL*28
00096      05  TB-MGR-TABLE.                                               CL*28
00097          10  TB-MGR-ENTRY                OCCURS                      CL*28
00098                                          25                          CL*28
00099                                          TIMES                       CL*28
00100              INDEXED BY PV-MGR-INDEX.                                CL*28
00101              15  TB-MGR-MDSEAR-DMA-NBR   PIC X(02)   VALUE SPACE.    CL*28
00102              15  TB-MGR-RSPBAR-WORKER-LAST-NAME                      CL*28
00103                                          PIC X(18)   VALUE SPACE.    CL*28
00104              15  TB-MGR-RSPBAR-WORKER-FIR-NAME                       CL*28
00105                                          PIC X(12)   VALUE SPACE.    CL*28
00106      05  TB-DPT-TABLE.                                               CL*28
00107          10  TB-DPT-ENTRY                OCCURS                      CL*28
W29908                                         999                         CL*28
00109                                          TIMES                       CL*28
00110              INDEXED BY PV-DPT-INDEX.                                CL*28
00111              15  TB-DPT-MDSEAR-DEPT-NBR  PIC X(03)   VALUE SPACE.    CL*28
00112              15  TB-DPT-MDSEAR-DESC      PIC X(25)   VALUE SPACE.    CL*28
00113                                                                      CL*28
00114  01  ABEND-CODE                          PIC S9(04)  VALUE ZEROES    CL*28
00115                                          COMP SYNC.                  CL*28
00116 ******************************************************************   CL*28
00117 *  SQL AND COBOL DECLARATIONS FOR THE                            *   CL*28
00118 *  MERCHANDISE AREA TABLE (TMDSEAR)                              *   CL*28
00119 ******************************************************************   CL*28
00120                                                                      CL*28
00121      EXEC SQL                                                        CL*28
00122          INCLUDE TMDSEAR                                             CL*28
00123      END-EXEC.                                                       CL*28
00124                                                                      CL*28
00125 ******************************************************************   CL*28
00126 *  SQL AND COBOL DECLARATIONS FOR THE                            *   CL*28
00127 *  RESPONSIBILITY AREA TABLE (TRSPBAR)                           *   CL*28
00128 ******************************************************************   CL*28
00129                                                                      CL*28
00130      EXEC SQL                                                        CL*28
00131          INCLUDE TRSPBAR                                             CL*28
00132      END-EXEC.                                                       CL*28
00133                                                                      CL*28
00134 ******************************************************************   CL*28
00135 *  SQL AND COBOL DECLARATIONS FOR THE                            *   CL*28
00136 *  APPLICATION AVAILABILITY TABLE (TAPLAVL)                      *   CL*28
00137 ******************************************************************   CL*28
00138                                                                      CL*28
00139      EXEC SQL                                                        CL*28
00140          INCLUDE TAPLAVL                                             CL*28
00141      END-EXEC.                                                       CL*28
00142                                                                      CL*28
00143 *                                                                    CL*28
00144 ******************************************************************   CL*28
00145 *  CURSOR SELECT FOR BUYER NUMBER AND NAME                       *   CL*28
00146 ******************************************************************   CL*28
00147      EXEC SQL                                                        CL*28
00148          DECLARE      BYR_CURSOR CURSOR FOR                          CL*28
00149           SELECT      DISTINCT                                       CL*28
00150                       A.BYR_NBR                                      CL*28
00151                ,      B.WORKER_LAST_NAME                             CL*28
00152                ,      B.WORKER_FIR_NAME                              CL*28
00153             FROM      TMDSEAR A                                      CL*28
00154                ,      TRSPBAR B                                      CL*28
00155                ,      TAPLAVL C                                      CL*28
00156            WHERE     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR)            CL*28
00157              AND     (A.OPERCO_NBR  = :PD-OPERCO-NBR)                CL*28
00158              AND     (C.APLSYS_CDE  = :PD-APLSYS-CDE)                CL*28
00159              AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                CL*28
00160              AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                CL*28
00161              AND     (:PD-DATE                                       CL*28
00162                       BETWEEN CHAR(B.WORKER_STRT_DTE,ISO)            CL*28
00163                       AND     CHAR(B.WORKER_END_DTE,ISO))            CL*28
00164              AND     (:PD-DATE                                       CL*28
00165                       BETWEEN CHAR(C.MDSEAR_STRT_DTE,ISO)            CL*28
00166                       AND     CHAR(C.MDSEAR_END_DTE,ISO))            CL*28
00167         ORDER BY      A.BYR_NBR                                      CL*28
00168      END-EXEC.                                                       CL*28
00169 *                                                                    CL*28
00170 ******************************************************************   CL*28
00171 *  CURSOR SELECT FOR MGR   NUMBER AND NAME                       *   CL*28
00172 ******************************************************************   CL*28
00173      EXEC SQL                                                        CL*28
00174          DECLARE      MGR_CURSOR CURSOR FOR                          CL*28
00175           SELECT      DISTINCT                                       CL*28
00176                       A.DMA_NBR                                      CL*28
00177                ,      B.WORKER_LAST_NAME                             CL*28
00178                ,      B.WORKER_FIR_NAME                              CL*28
00179             FROM      TMDSEAR A                                      CL*28
00180                ,      TRSPBAR B                                      CL*28
00181                ,      TAPLAVL C                                      CL*28
00182            WHERE     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-DMM)            CL*28
00183              AND     (A.OPERCO_NBR  = :PD-OPERCO-NBR)                CL*28
00184              AND     (C.APLSYS_CDE  = :PD-APLSYS-CDE)                CL*28
00185              AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                CL*28
00186              AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                CL*28
00187              AND     (:PD-DATE                                       CL*28
00188                       BETWEEN CHAR(B.WORKER_STRT_DTE,ISO)            CL*28
00189                       AND     CHAR(B.WORKER_END_DTE,ISO))            CL*28
00190              AND     (:PD-DATE                                       CL*28
00191                       BETWEEN CHAR(C.MDSEAR_STRT_DTE,ISO)            CL*28
00192                       AND     CHAR(C.MDSEAR_END_DTE,ISO))            CL*28
00193         ORDER BY      A.DMA_NBR                                      CL*28
00194      END-EXEC.                                                       CL*28
00195                                                                      CL*28
00196 ******************************************************************   CL*28
00197 *  CURSOR SELECT FOR DEPARTMENT AND DESCRIPTION                  *   CL*28
00198 ******************************************************************   CL*28
00199      EXEC SQL                                                        CL*28
00200          DECLARE      DPT_CURSOR CURSOR FOR                          CL*28
00201           SELECT      A.DEPT_NBR                                     CL*28
00202                ,      A.MDSEAR_DESC                                  CL*28
00203             FROM      TMDSEAR A                                      CL*28
00204                ,      TAPLAVL B                                      CL*28
00205            WHERE     (A.MDSELV_CDE  = :PC-MDSELV-CDE-DPT)            CL*28
00206              AND     (A.OPERCO_NBR  = :PD-OPERCO-NBR)                CL*28
00207              AND     (B.APLSYS_CDE  = :PD-APLSYS-CDE)                CL*28
00208              AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                CL*28
00209              AND     (:PD-DATE                                       CL*28
00210                       BETWEEN CHAR(B.MDSEAR_STRT_DTE,ISO)            CL*28
00211                       AND     CHAR(B.MDSEAR_END_DTE,ISO))            CL*28
00212              AND     (:PD-DATE                                       CL*28
00213                       BETWEEN CHAR(A.STRT_DTE,ISO)                   CL*28
00214                       AND     CHAR(A.END_DTE,ISO))                   CL*28
00215         ORDER BY      A.DEPT_NBR                                     CL*28
00216      END-EXEC.                                                       CL*28
00217      EJECT                                                           CL*28
00218                                                                      CL*28
00219      EXEC SQL                                                        CL*28
00220          INCLUDE SQLCA                                               CL*28
00221      END-EXEC.                                                       CL*28
00222                                                                      CL*28
00223      COPY SQLCA2.                                                    CL*28
00224                                                                      CL*28
00225                                                                      CL*28
00226                                                                      CL*28
00227  LINKAGE SECTION.                                                    CL*28
00228                                                                      CL*28
00229  01  PD-PASS-AREA                PIC X(10022).                       CL*28
00230                                                                      CL*28
00231 ******************************************************************   CL*28
00232  PROCEDURE DIVISION USING        PD-PASS-AREA.                       CL*28
00233 ******************************************************************   CL*28
00234                                                                      CL*28
00235  0000-MAINLINE.                                                      CL*28
00236                                                                      CL*28
00237      PERFORM                     1000-INITIALIZATION.                CL*28
00238                                                                      CL*28
00239      PERFORM                     2000-LOAD-TABLE UNTIL               CL*28
00240                                  TABLE-LOADED.                       CL*28
00241                                                                      CL*28
00242      PERFORM                     9000-FINALIZATION.                  CL*28
00243                                                                      CL*28
00244      GOBACK.                                                         CL*28
00245                                                                      CL*28
00246      EJECT                                                           CL*28
00247                                                                      CL*28
00248 ******************************************************************   CL*28
00249 *   THIS PARAGRAPH PERFORMS PROGRAM VARIABLE INITIALIZATION.     *   CL*28
00250 *                                                                *   CL*28
00251 *   PERFORMED BY : 0000-MAINLINE                                 *   CL*28
00252 ******************************************************************   CL*28
00253  1000-INITIALIZATION.                                                CL*28
00254                                                                      CL*28
00255      MOVE PD-PASS-AREA           TO PD-PASSED-DATA.                  CL*28
00256                                                                      CL*28
00257      MOVE 'N'                    TO PS-TABLE-LOADED.                 CL*28
00258      EJECT                                                           CL*28
00259                                                                      CL*28
00260 ******************************************************************   CL*28
00261 *   THIS PARAGRAPH PERFORMS THE PROCESSING TO LOAD THE REQUESTED *   CL*28
00262 *   TABLE.                                                       *   CL*28
00263 *                                                                *   CL*28
00264 *   PERFORMED BY : 0000-MAINLINE                                 *   CL*28
00265 ******************************************************************   CL*28
00266  2000-LOAD-TABLE.                                                    CL*28
00267                                                                      CL*28
00268      MOVE PD-REQUEST TO PS-REQUEST.                                  CL*28
00269                                                                      CL*28
00270      EVALUATE TRUE                                                   CL*28
00271          WHEN LOAD-BUYER-TABLE                                       CL*28
00272              PERFORM             2100-LOAD-BUYER-TABLE               CL*28
00273          WHEN LOAD-MANAGER-TABLE                                     CL*28
00274              PERFORM             2200-LOAD-MANAGER-TABLE             CL*28
00275          WHEN LOAD-DEPARTMENT-TABLE                                  CL*28
00276              PERFORM             2300-LOAD-DEPARTMENT-TABLE          CL*28
00277          WHEN OTHER                                                  CL*28
00278              DISPLAY '*** PROGRAM ==> DPKUT020 ***'                  CL*28
00279              DISPLAY '*** INVALID TABLE REQUEST ****'                CL*28
00280              DISPLAY '***  TABLE REQUEST = ' PS-REQUEST '***'        CL*28
00281              DISPLAY '*** PD-PASSED-DATA = ' PD-PASSED-DATA '***'    CL*28
00282              MOVE +4001          TO ABEND-CODE                       CL*28
00283              CALL 'ILBOABN0'     USING ABEND-CODE                    CL*28
00284      END-EVALUATE.                                                   CL*28
00285      EJECT                                                           CL*28
00286 ******************************************************************   CL*28
00287 *   THIS PARAGRAPH PERFORMS THE PROCESSING TO LOAD THE           *   CL*28
00288 *   BUYER TABLE                                                  *   CL*28
00289 *                                                                *   CL*28
00290 *   PERFORMED BY : 2000-LOAD-TABLE                               *   CL*28
00291 ******************************************************************   CL*28
00292  2100-LOAD-BUYER-TABLE.                                              CL*28
00293                                                                      CL*28
00294      PERFORM                                                         CL*28
00295          WITH TEST AFTER                                             CL*28
00296          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00297          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00298              OR (SQLCODE = 0)                                        CL*28
00299                                                                      CL*28
00300          EXEC SQL                                                    CL*28
00301              OPEN BYR_CURSOR                                         CL*28
00302          END-EXEC                                                    CL*28
00303                                                                      CL*28
00304          EVALUATE TRUE                                               CL*28
00305              WHEN SQLCODE = 0                                        CL*28
00306              WHEN SQLCODE = -904                                     CL*28
00307              WHEN SQLCODE = -911                                     CL*28
00308                  CONTINUE                                            CL*28
00309              WHEN OTHER                                              CL*28
00310                  MOVE '2100-LOAD-BUYER-TABLE'                        CL*28
00311                      TO PV-PARAGRAPH-NAME                            CL*28
00312                  MOVE 'OPEN THE BUYER CURSOR'                        CL*28
00313                      TO PV-DB2-FUNCTION                              CL*28
00314                  PERFORM 9900-DB2-ABEND                              CL*28
00315          END-EVALUATE                                                CL*28
00316                                                                      CL*28
00317      END-PERFORM.                                                    CL*28
00318                                                                      CL*28
00319      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00320          MOVE '2100-LOAD-BUYER-TABLE'                                CL*28
00321              TO PV-PARAGRAPH-NAME                                    CL*28
00322          MOVE 'OPEN CURSOR-EXCEEDED RETRIES'                         CL*28
00323              TO PV-DB2-FUNCTION                                      CL*28
00324          PERFORM 9900-DB2-ABEND                                      CL*28
00325      END-IF.                                                         CL*28
00326                                                                      CL*28
00327      SET PV-BYR-INDEX            TO +1.                              CL*28
00328                                                                      CL*28
00329      PERFORM                     2190-FETCH-AND-LOAD-BUYER-TAB.      CL*28
00330                                                                      CL*28
00331      IF  SQLCODE = +100                                              CL*28
00332          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00333          DISPLAY '*** NOW ROWS FOUND ****'                           CL*28
00334          DISPLAY '*** TABLE REQUEST = ' PS-REQUEST '***'             CL*28
00335          DISPLAY '*** PD-PASSED-DATA = ' PD-PASSED-DATA '***'        CL*28
00336          MOVE +4001              TO ABEND-CODE                       CL*28
00337          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00338      END-IF.                                                         CL*28
00339                                                                      CL*28
00340      PERFORM                     2190-FETCH-AND-LOAD-BUYER-TAB       CL*28
00341                                  UNTIL TABLE-LOADED.                 CL*28
00342                                                                      CL*28
00343      PERFORM                                                         CL*28
00344          WITH TEST AFTER                                             CL*28
00345          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00346          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00347              OR (SQLCODE = 0)                                        CL*28
00348                                                                      CL*28
00349          EXEC SQL                                                    CL*28
00350              CLOSE BYR_CURSOR                                        CL*28
00351          END-EXEC                                                    CL*28
00352                                                                      CL*28
00353          EVALUATE TRUE                                               CL*28
00354              WHEN SQLCODE = 0                                        CL*28
00355              WHEN SQLCODE = -904                                     CL*28
00356              WHEN SQLCODE = -911                                     CL*28
00357                  CONTINUE                                            CL*28
00358              WHEN OTHER                                              CL*28
00359                  MOVE '2100-LOAD-BUYER-TABLE'                        CL*28
00360                      TO PV-PARAGRAPH-NAME                            CL*28
00361                  MOVE 'CLOSE THE BUYER CURSOR'                       CL*28
00362                      TO PV-DB2-FUNCTION                              CL*28
00363                  PERFORM 9900-DB2-ABEND                              CL*28
00364          END-EVALUATE                                                CL*28
00365                                                                      CL*28
00366      END-PERFORM.                                                    CL*28
00367                                                                      CL*28
00368      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00369          MOVE '2100-LOAD-BUYER-TABLE'                                CL*28
00370              TO PV-PARAGRAPH-NAME                                    CL*28
00371          MOVE 'CLOSE CURSOR-EXCEEDED RETRIES'                        CL*28
00372              TO PV-DB2-FUNCTION                                      CL*28
00373          PERFORM 9900-DB2-ABEND                                      CL*28
00374      END-IF.                                                         CL*28
00375                                                                      CL*28
00376      MOVE TB-BYR-TABLE           TO PD-TABLE.                        CL*28
00377      EJECT                                                           CL*28
00378 ******************************************************************   CL*28
00379 *   THIS PARAGRAPH PERFORMS THE FETCH TO RETRIEVE BUYER          *   CL*28
00380 *   INFORMATION FROM THE MBS.                                    *   CL*28
00381 *                                                                *   CL*28
00382 *   PERFORMED BY :                                               *   CL*28
00383 ******************************************************************   CL*28
00384                                                                      CL*28
00385  2190-FETCH-AND-LOAD-BUYER-TAB.                                      CL*28
00386                                                                      CL*28
00387      EXEC SQL                                                        CL*28
00388          FETCH       BYR_CURSOR                                      CL*28
00389           INTO      :MDSEAR-BYR-NBR                                  CL*28
00390              ,      :RSPBAR-WORKER-LAST-NAME                         CL*28
00391              ,      :RSPBAR-WORKER-FIR-NAME                          CL*28
00392      END-EXEC.                                                       CL*28
00393                                                                      CL*28
00394      EVALUATE TRUE                                                   CL*28
00395          WHEN SQLCODE = 0                                            CL*28
00396              MOVE    MDSEAR-BYR-NBR                                  CL*28
00397                      TO TB-BYR-MDSEAR-BYR-NBR(PV-BYR-INDEX)          CL*28
00398              MOVE    RSPBAR-WORKER-LAST-NAME                         CL*28
00399                      TO TB-BYR-RSPBAR-WORKER-LAST-NAME               CL*28
00400                          (PV-BYR-INDEX)                              CL*28
00401              MOVE    RSPBAR-WORKER-FIR-NAME                          CL*28
00402                      TO TB-BYR-RSPBAR-WORKER-FIR-NAME                CL*28
00403                          (PV-BYR-INDEX)                              CL*28
00404              SET     PV-BYR-INDEX UP BY +1                           CL*28
00405          WHEN SQLCODE = +100                                         CL*28
00406              SET TABLE-LOADED TO TRUE                                CL*28
00407          WHEN OTHER                                                  CL*28
00408              MOVE '2190-FETCH-AND-LOAD-BUYER-TAB '                   CL*28
00409                  TO PV-PARAGRAPH-NAME                                CL*28
00410              MOVE 'FETCH BUYER ROWS              '                   CL*28
00411                  TO PV-DB2-FUNCTION                                  CL*28
00412              PERFORM 9900-DB2-ABEND                                  CL*28
00413      END-EVALUATE.                                                   CL*28
00414                                                                      CL*28
00415      IF  PV-BYR-INDEX > PC-BYR-MAX                                   CL*28
00416          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00417          DISPLAY '*** BUYER TABLE ENTRIES EXCEEDED ***'              CL*28
00418          DISPLAY '*** PLEASE INCREASE PV-BYR-INDEX ***'              CL*28
00419          MOVE +4001              TO ABEND-CODE                       CL*28
00420          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00421      END-IF.                                                         CL*28
00422                                                                      CL*28
00423                                                                      CL*28
00424      EJECT                                                           CL*28
00425 ******************************************************************   CL*28
00426 *   THIS PARAGRAPH PERFORMS THE PROCESSING TO LOAD THE           *   CL*28
00427 *   MANAGER TABLE.                                               *   CL*28
00428 *                                                                *   CL*28
00429 *   PERFORMED BY : 2000-LOAD-TABLE                               *   CL*28
00430 ******************************************************************   CL*28
00431  2200-LOAD-MANAGER-TABLE.                                            CL*28
00432                                                                      CL*28
00433      PERFORM                                                         CL*28
00434          WITH TEST AFTER                                             CL*28
00435          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00436          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00437              OR (SQLCODE = 0)                                        CL*28
00438                                                                      CL*28
00439          EXEC SQL                                                    CL*28
00440              OPEN MGR_CURSOR                                         CL*28
00441          END-EXEC                                                    CL*28
00442                                                                      CL*28
00443          EVALUATE TRUE                                               CL*28
00444              WHEN SQLCODE = 0                                        CL*28
00445              WHEN SQLCODE = -904                                     CL*28
00446              WHEN SQLCODE = -911                                     CL*28
00447                  CONTINUE                                            CL*28
00448              WHEN OTHER                                              CL*28
00449                  MOVE '2200-LOAD-MANAGER-TABLE'                      CL*28
00450                      TO PV-PARAGRAPH-NAME                            CL*28
00451                  MOVE 'OPEN THE MANAGER CURSOR'                      CL*28
00452                      TO PV-DB2-FUNCTION                              CL*28
00453                  PERFORM 9900-DB2-ABEND                              CL*28
00454          END-EVALUATE                                                CL*28
00455                                                                      CL*28
00456      END-PERFORM.                                                    CL*28
00457                                                                      CL*28
00458      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00459          MOVE '2200-LOAD-MANAGER-TABLE'                              CL*28
00460              TO PV-PARAGRAPH-NAME                                    CL*28
00461          MOVE 'OPEN CURSOR-EXCEEDED RETRIES'                         CL*28
00462              TO PV-DB2-FUNCTION                                      CL*28
00463          PERFORM 9900-DB2-ABEND                                      CL*28
00464      END-IF.                                                         CL*28
00465                                                                      CL*28
00466      SET PV-MGR-INDEX            TO +1.                              CL*28
00467                                                                      CL*28
00468      PERFORM                     2290-FETCH-AND-LOAD-MGR-TAB.        CL*28
00469                                                                      CL*28
00470      IF  SQLCODE = +100                                              CL*28
00471          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00472          DISPLAY '*** NOW ROWS FOUND ****'                           CL*28
00473          DISPLAY '***  TABLE REQUEST = ' PS-REQUEST '***'            CL*28
00474          DISPLAY '*** PD-PASSED-DATA = ' PD-PASSED-DATA '***'        CL*28
00475          MOVE +4001              TO ABEND-CODE                       CL*28
00476          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00477      END-IF.                                                         CL*28
00478                                                                      CL*28
00479      PERFORM                     2290-FETCH-AND-LOAD-MGR-TAB         CL*28
00480                                  UNTIL TABLE-LOADED.                 CL*28
00481                                                                      CL*28
00482      PERFORM                                                         CL*28
00483          WITH TEST AFTER                                             CL*28
00484          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00485          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00486              OR (SQLCODE = 0)                                        CL*28
00487                                                                      CL*28
00488          EXEC SQL                                                    CL*28
00489              CLOSE MGR_CURSOR                                        CL*28
00490          END-EXEC                                                    CL*28
00491                                                                      CL*28
00492          EVALUATE TRUE                                               CL*28
00493              WHEN SQLCODE = 0                                        CL*28
00494              WHEN SQLCODE = -904                                     CL*28
00495              WHEN SQLCODE = -911                                     CL*28
00496                  CONTINUE                                            CL*28
00497              WHEN OTHER                                              CL*28
00498                  MOVE '2200-LOAD-MANAGER-TABLE'                      CL*28
00499                      TO PV-PARAGRAPH-NAME                            CL*28
00500                  MOVE 'CLOSE THE MANAGER CURSOR'                     CL*28
00501                      TO PV-DB2-FUNCTION                              CL*28
00502                  PERFORM 9900-DB2-ABEND                              CL*28
00503          END-EVALUATE                                                CL*28
00504                                                                      CL*28
00505      END-PERFORM.                                                    CL*28
00506                                                                      CL*28
00507      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00508          MOVE '2200-LOAD-MANAGER-TABLE'                              CL*28
00509              TO PV-PARAGRAPH-NAME                                    CL*28
00510          MOVE 'CLOSE CURSOR-EXCEEDED RETRIES'                        CL*28
00511              TO PV-DB2-FUNCTION                                      CL*28
00512          PERFORM 9900-DB2-ABEND                                      CL*28
00513      END-IF.                                                         CL*28
00514                                                                      CL*28
00515      MOVE TB-MGR-TABLE           TO PD-TABLE.                        CL*28
00516      EJECT                                                           CL*28
00517 ******************************************************************   CL*28
00518 *   THIS PARAGRAPH PERFORMS THE FETCH TO RETRIEVE MANAGER        *   CL*28
00519 *   INFORMATION FROM THE MBS.                                    *   CL*28
00520 *                                                                *   CL*28
00521 *   PERFORMED BY :                                               *   CL*28
00522 ******************************************************************   CL*28
00523                                                                      CL*28
00524  2290-FETCH-AND-LOAD-MGR-TAB.                                        CL*28
00525                                                                      CL*28
00526      EXEC SQL                                                        CL*28
00527          FETCH       MGR_CURSOR                                      CL*28
00528           INTO      :MDSEAR-DMA-NBR                                  CL*28
00529              ,      :RSPBAR-WORKER-LAST-NAME                         CL*28
00530              ,      :RSPBAR-WORKER-FIR-NAME                          CL*28
00531      END-EXEC.                                                       CL*28
00532                                                                      CL*28
00533      EVALUATE TRUE                                                   CL*28
00534          WHEN SQLCODE = 0                                            CL*28
00535              MOVE    MDSEAR-DMA-NBR                                  CL*28
00536                      TO TB-MGR-MDSEAR-DMA-NBR(PV-MGR-INDEX)          CL*28
00537              MOVE    RSPBAR-WORKER-LAST-NAME                         CL*28
00538                      TO TB-MGR-RSPBAR-WORKER-LAST-NAME               CL*28
00539                          (PV-MGR-INDEX)                              CL*28
00540              MOVE    RSPBAR-WORKER-FIR-NAME                          CL*28
00541                      TO TB-MGR-RSPBAR-WORKER-FIR-NAME                CL*28
00542                          (PV-MGR-INDEX)                              CL*28
00543              SET     PV-MGR-INDEX UP BY +1                           CL*28
00544          WHEN SQLCODE = +100                                         CL*28
00545              SET TABLE-LOADED TO TRUE                                CL*28
00546          WHEN OTHER                                                  CL*28
00547              MOVE '2290-FETCH-AND-LOAD-MGR-TAB     '                 CL*28
00548                  TO PV-PARAGRAPH-NAME                                CL*28
00549              MOVE 'FETCH MANAGER ROWS              '                 CL*28
00550                  TO PV-DB2-FUNCTION                                  CL*28
00551              PERFORM 9900-DB2-ABEND                                  CL*28
00552      END-EVALUATE.                                                   CL*28
00553                                                                      CL*28
00554      IF  PV-MGR-INDEX > PC-MGR-MAX                                   CL*28
00555          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00556          DISPLAY '*** MANAGER TABLE ENTRIES EXCEEDED ***'            CL*28
00557          DISPLAY '*** PLEASE INCREASE PV-MGR-INDEX ***'              CL*28
00558          MOVE +4001              TO ABEND-CODE                       CL*28
00559          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00560      END-IF.                                                         CL*28
00561                                                                      CL*28
00562                                                                      CL*28
00563      EJECT                                                           CL*28
00564 ******************************************************************   CL*28
00565 *   THIS PARAGRAPH PERFORMS THE PROCESSING TO LOAD THE           *   CL*28
00566 *   DEPARTMENT TABLE.                                            *   CL*28
00567 *                                                                *   CL*28
00568 *   PERFORMED BY : 2000-LOAD-TABLE                               *   CL*28
00569 ******************************************************************   CL*28
00570  2300-LOAD-DEPARTMENT-TABLE.                                         CL*28
00571                                                                      CL*28
00572      PERFORM                                                         CL*28
00573          WITH TEST AFTER                                             CL*28
00574          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00575          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00576              OR (SQLCODE = 0)                                        CL*28
00577                                                                      CL*28
00578          EXEC SQL                                                    CL*28
00579              OPEN DPT_CURSOR                                         CL*28
00580          END-EXEC                                                    CL*28
00581                                                                      CL*28
00582          EVALUATE TRUE                                               CL*28
00583              WHEN SQLCODE = 0                                        CL*28
00584              WHEN SQLCODE = -904                                     CL*28
00585              WHEN SQLCODE = -911                                     CL*28
00586                  CONTINUE                                            CL*28
00587              WHEN OTHER                                              CL*28
00588                  MOVE '2300-LOAD-DEPARTMENT-TABLE'                   CL*28
00589                      TO PV-PARAGRAPH-NAME                            CL*28
00590                  MOVE 'OPEN THE DEPARTMENT CURSOR'                   CL*28
00591                      TO PV-DB2-FUNCTION                              CL*28
00592                  PERFORM 9900-DB2-ABEND                              CL*28
00593          END-EVALUATE                                                CL*28
00594                                                                      CL*28
00595      END-PERFORM.                                                    CL*28
00596                                                                      CL*28
00597      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00598          MOVE '2300-LOAD-DEPARTMENT-TABLE'                           CL*28
00599              TO PV-PARAGRAPH-NAME                                    CL*28
00600          MOVE 'OPEN CURSOR-EXCEEDED RETRIES'                         CL*28
00601              TO PV-DB2-FUNCTION                                      CL*28
00602          PERFORM 9900-DB2-ABEND                                      CL*28
00603      END-IF.                                                         CL*28
00604                                                                      CL*28
00605      SET PV-DPT-INDEX            TO +1.                              CL*28
00606                                                                      CL*28
00607      PERFORM                     2390-FETCH-AND-LOAD-DPT-TAB.        CL*28
00608                                                                      CL*28
00609      IF  SQLCODE = +100                                              CL*28
00610          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00611          DISPLAY '*** NOW ROWS FOUND ****'                           CL*28
00612          DISPLAY '***  TABLE REQUEST = ' PS-REQUEST '***'            CL*28
00613          DISPLAY '*** PD-PASSED-DATA = ' PD-PASSED-DATA '***'        CL*28
00614          MOVE +4001              TO ABEND-CODE                       CL*28
00615          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00616      END-IF.                                                         CL*28
00617                                                                      CL*28
00618      PERFORM                     2390-FETCH-AND-LOAD-DPT-TAB         CL*28
00619                                  UNTIL TABLE-LOADED.                 CL*28
00620                                                                      CL*28
00621      PERFORM                                                         CL*28
00622          WITH TEST AFTER                                             CL*28
00623          VARYING PV-RETRY-COUNT FROM 1 BY 1                          CL*28
00624          UNTIL (PV-RETRY-COUNT > PC-MAX-RETRIES)                     CL*28
00625              OR (SQLCODE = 0)                                        CL*28
00626                                                                      CL*28
00627          EXEC SQL                                                    CL*28
00628              CLOSE DPT_CURSOR                                        CL*28
00629          END-EXEC                                                    CL*28
00630                                                                      CL*28
00631          EVALUATE TRUE                                               CL*28
00632              WHEN SQLCODE = 0                                        CL*28
00633              WHEN SQLCODE = -904                                     CL*28
00634              WHEN SQLCODE = -911                                     CL*28
00635                  CONTINUE                                            CL*28
00636              WHEN OTHER                                              CL*28
00637                  MOVE '2300-LOAD-DEPARTMENT-TABLE'                   CL*28
00638                      TO PV-PARAGRAPH-NAME                            CL*28
00639                  MOVE 'CLOSE THE DEPARTMENT CURSOR'                  CL*28
00640                      TO PV-DB2-FUNCTION                              CL*28
00641                  PERFORM 9900-DB2-ABEND                              CL*28
00642          END-EVALUATE                                                CL*28
00643                                                                      CL*28
00644      END-PERFORM.                                                    CL*28
00645                                                                      CL*28
00646      IF  PV-RETRY-COUNT > PC-MAX-RETRIES                             CL*28
00647          MOVE '2300-LOAD-DEPARTMENT-TABLE'                           CL*28
00648              TO PV-PARAGRAPH-NAME                                    CL*28
00649          MOVE 'CLOSE CURSOR-EXCEEDED RETRIES'                        CL*28
00650              TO PV-DB2-FUNCTION                                      CL*28
00651          PERFORM 9900-DB2-ABEND                                      CL*28
00652      END-IF.                                                         CL*28
00653                                                                      CL*28
00654      MOVE TB-DPT-TABLE           TO PD-TABLE.                        CL*28
00655      EJECT                                                           CL*28
00656 ******************************************************************   CL*28
00657 *   THIS PARAGRAPH PERFORMS THE FETCH TO RETRIEVE MANAGER        *   CL*28
00658 *   INFORMATION FROM THE MBS.                                    *   CL*28
00659 *                                                                *   CL*28
00660 *   PERFORMED BY :                                               *   CL*28
00661 ******************************************************************   CL*28
00662                                                                      CL*28
00663  2390-FETCH-AND-LOAD-DPT-TAB.                                        CL*28
00664                                                                      CL*28
00665      EXEC SQL                                                        CL*28
00666          FETCH       DPT_CURSOR                                      CL*28
00667           INTO      :MDSEAR-DEPT-NBR                                 CL*28
00668              ,      :MDSEAR-DESC                                     CL*28
00669      END-EXEC.                                                       CL*28
00670                                                                      CL*28
00671      EVALUATE TRUE                                                   CL*28
00672          WHEN SQLCODE = 0                                            CL*28
00673              MOVE    MDSEAR-DEPT-NBR                                 CL*28
00674                      TO TB-DPT-MDSEAR-DEPT-NBR(PV-DPT-INDEX)         CL*28
00675              MOVE    MDSEAR-DESC                                     CL*28
00676                      TO TB-DPT-MDSEAR-DESC(PV-DPT-INDEX)             CL*28
00677              SET     PV-DPT-INDEX UP BY +1                           CL*28
00678          WHEN SQLCODE = +100                                         CL*28
00679              SET TABLE-LOADED TO TRUE                                CL*28
00680          WHEN OTHER                                                  CL*28
00681              MOVE '2390-FETCH-AND-LOAD-DPT-TAB     '                 CL*28
00682                  TO PV-PARAGRAPH-NAME                                CL*28
00683              MOVE 'FETCH DEPARTMENT ROWS          '                  CL*28
00684                  TO PV-DB2-FUNCTION                                  CL*28
00685              PERFORM 9900-DB2-ABEND                                  CL*28
00686      END-EVALUATE.                                                   CL*28
00687                                                                      CL*28
00688      IF  PV-DPT-INDEX > PC-DPT-MAX                                   CL*28
00689          DISPLAY '*** PROGRAM ==> DPKUT020 ***'                      CL*28
00690          DISPLAY '*** DEPARTMENT TABLE ENTRIES EXCEEDED ***'         CL*28
00691          DISPLAY '*** PLEASE INCREASE PV-DPT-INDEX ***'              CL*28
00692          MOVE +4001              TO ABEND-CODE                       CL*28
00693          CALL 'ILBOABN0'         USING ABEND-CODE                    CL*28
00694      END-IF.                                                         CL*28
00695                                                                      CL*28
00696                                                                      CL*28
00697      EJECT                                                           CL*28
00698  9000-FINALIZATION.                                                  CL*28
00699                                                                      CL*28
00700      MOVE PD-PASSED-DATA         TO PD-PASS-AREA.                    CL*28
00701 ******************************************************************   CL*28
00702 *   THIS PARAGRAPH PERFORMS THE PROCESSING TO ABEND THE PROGRAM  *   CL*28
00703 *   AFTER AN INVALID SQL CALL.                                   *   CL*28
00704 *                                                                *   CL*28
00705 *   PERFORMED BY :                                               *   CL*28
00706 ******************************************************************   CL*28
00707  9900-DB2-ABEND.                                                     CL*28
00708                                                                      CL*28
00709      MOVE SQLCODE    TO SQLCODE2.                                    CL*28
00710      MOVE SQLERRD(3) TO SQLERRD3.                                    CL*28
00711                                                                      CL*28
00712      DISPLAY '*** ABEND ***'                                         CL*28
00713      DISPLAY 'DPKUT020 - SQL ERROR'                                  CL*28
00714      DISPLAY 'SQLCODE        = ' SQLCODE2.                           CL*28
00715      DISPLAY 'SQLERRM        = ' SQLERRM.                            CL*28
00716      DISPLAY 'SQLERRMC       = ' SQLERRMC.                           CL*28
00717      DISPLAY 'ROWS PROCESSED = ' SQLERRD3.                           CL*28
00718      DISPLAY 'PARAGRAPH NAME = ' PV-PARAGRAPH-NAME.                  CL*28
00719      DISPLAY 'OPERATION ATTEMPTED = ' PV-DB2-FUNCTION.               CL*28
00720      DISPLAY '*** PD-PASSED-DATA = ' PD-PASSED-DATA '***'            CL*28
00721                                                                      CL*28
00722      MOVE +4013 TO ABEND-CODE.                                       CL*28
00723      CALL 'ILBOABN0' USING ABEND-CODE.                               CL*28
00724      EJECT                                                           CL*28
