000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT002.                                                 
000600 AUTHOR.        LARRY SCHLEICHER - STRATAGEM                              
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   SEPTEMBER, 1996.                                          
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE OF DATA FROM THE SHIPMENT         
001300*  UNIT TABLE(TSHPUNT).  IT ALSO CREATES A TIMESTAMP CONTROL FILE.        
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
002900                                                                          
003000     SELECT TIMESTAMP-FILE                                                
003100         ASSIGN TO IN01.                                                  
003200                                                                          
003300     SELECT SHIPMENT-UNIT-FILE                                            
003400         ASSIGN TO OUT02.                                                 
003410                                                                          
003420     SELECT SHIPUNIT-INACTIVE-FILE                                        
003430         ASSIGN TO OUT03.                                                 
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  TIMESTAMP-FILE                                                       
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  TIMESTAMP-RECORD           PIC  X(50).                               
004700                                                                          
004800 FD  SHIPMENT-UNIT-FILE                                                   
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  SHIPMENT-UNIT-RECORD       PIC  X(077).                              
005400*                                                                         
005410 FD  SHIPUNIT-INACTIVE-FILE                                               
005420     RECORDING MODE IS F                                                  
005430     LABEL RECORDS ARE STANDARD                                           
005440     BLOCK CONTAINS 0 RECORDS.                                            
005450                                                                          
005460 01  SHIPUNIT-INACTIVE-RECORD   PIC  X(077).                              
005470*                                                                         
005500*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
005800******************************************************************        
005900*                                                                         
006000*                                                                         
006500*                                                                         
006600*                                                                         
006700******************************************************************        
006800*  RECORD LAYOUT FOR THE TIMESTAMP FILE.                                  
006900******************************************************************        
007000     COPY RCRD011.                                                        
007100*                                                                         
007200*                                                                         
007300*                                                                         
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
007900*                                                                         
008000*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008700*                                                                         
008800*                                                                         
008900******************************************************************        
009000*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE ?????????????????          
009100*  TABLE                                                                  
009200******************************************************************        
009300     EXEC SQL                                                             
009400          INCLUDE TCOCNTL                                                 
009500     END-EXEC.                                                            
009600                                                                          
009700******************************************************************        
009800*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
009900*  TABLE                                                                  
010000******************************************************************        
010100     EXEC SQL                                                             
010200          INCLUDE TSHPUNT                                                 
010300     END-EXEC.                                                            
010400******************************************************************        
010500*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
010600*  LOCATION TABLE                                                         
010700******************************************************************        
010800     EXEC SQL                                                             
010900          INCLUDE TSHUNLO                                                 
011000     END-EXEC.                                                            
011100*                                                                         
011110******************************************************************        
011120*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011130*  AUDIT TRANSACTION TABLE                                                
011140******************************************************************        
011150     EXEC SQL                                                             
011160          INCLUDE TSUATRN                                                 
011170     END-EXEC.                                                            
011180*                                                                         
011200 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-SHIP-UNIT-CSR-SW     PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-SHIP-UNIT-CSR                  VALUE 'Y'.             
011800         88  PS-MORE-SHIP-UNIT-CSR                 VALUE 'N'.             
011810     05  PS-FOUND-ON-AUDIT-SW        PIC  X(01)    VALUE 'N'.             
011820         88  FOUND-ON-AUDIT                        VALUE 'Y'.             
011830         88  NOT-FOUND-ON-AUDIT                    VALUE 'N'.             
011900                                                                          
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'SUKUT002'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
013800     05  PV-CLOSE-COUNTER            PIC S9(04)    VALUE ZERO             
013900                                                   COMP SYNC.             
014000     05  PV-FETCH-COUNTER            PIC S9(04)    VALUE ZERO             
014100                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014310     05  PV-WRITE2-COUNTER           PIC S9(09)    VALUE ZERO             
014320                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014500                                                                          
014600     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014800                                                                          
014900                                                                          
015000 01  SD-SYSTEM-DATE.                                                      
015100     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
015200     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
015300     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
015400                                                                          
015500*                                                                         
015600*  CURSOR DECLARATION FOR ALL EXCEPTION ACTIVITY ON THE                   
015700*  UPC CERTIFICATION ACTIVITY LOG TABLE                                   
015800*                                                                         
015900                                                                          
016000     EXEC SQL                                                             
016100          DECLARE SHIPMT_UNIT_CURSOR CURSOR FOR                           
016200           SELECT DISTINCT                                                
016300             A.SHIPT_UNT_ID,                                              
016400             A.ORGN_OPER_UNT_ID,                                          
016500             A.ORGN_FCLTY_ID,                                             
016600             A.DEST_OPER_UNT_ID,                                          
016700             A.DEST_FCLTY_ID,                                             
016800             A.SHIPT_UNT_SRC_ID,                                          
016900             A.HIER_LVL_CDE,                                              
017000             A.NEXT_SHIPT_UNT_ID,                                         
017100             A.ORIG_SHIPT_UNT_ID,                                         
017200             A.ACTV_IND,                                                  
017300             A.CRE_DTE,                                                   
017400             A.CRE_TM,                                                    
017500             A.CRE_USR_ID,                                                
017600             A.CRE_PGM_NM                                                 
017700             FROM TSHPUNT A                                               
017800                , TSHUNLO B                                               
017900            WHERE A.SHIPT_UNT_ID   = B.SHIPT_UNT_ID                       
018000              AND B.CRE_TMST       < :PC-CURRENT-TIMESTAMP                
P41441*             AND B.SCN_LOC_ID   IN ('RECV','NSCN')                       
P41441              AND B.SCN_LOC_ID   IN ('RECV','NSCN','WRCV')                
018020              AND B.ACTV_CDE       = 'A'                                  
018100                                                                          
018200     END-EXEC.                                                            
018300                                                                          
018400                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018900 0000-PROGRAM-MAINLINE.                                                   
019000******************************************************************        
019100* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019200*      PROGRAM.                                                           
019300******************************************************************        
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019600     IF PS-MORE-SHIP-UNIT-CSR                                             
019700         PERFORM 2000-PROCESS-SHIPMENT-UNITS                              
019800            UNTIL PS-EOF-SHIP-UNIT-CSR                                    
019900     END-IF.                                                              
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020400 1000-INITIALIZE.                                                         
020500******************************************************************        
020600*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020700*    -- OPEN OUTPUT FILE                                                  
020800*    -- OPEN CURSOR                                                       
020900*    -- FETCH CURSOR                                                      
021000******************************************************************        
021100                                                                          
021200     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
021300                                                                          
021400     OPEN OUTPUT SHIPMENT-UNIT-FILE                                       
021410                 SHIPUNIT-INACTIVE-FILE.                                  
021500     OPEN INPUT  TIMESTAMP-FILE.                                          
021600                                                                          
021700     PERFORM 1100-READ-CONTROL-CARD.                                      
021800     PERFORM 7000-OPEN-SHIPMT-UNIT-CURSOR.                                
021900     PERFORM 7100-FETCH-SHIPMT-UNIT-CURSOR.                               
022000                                                                          
025400 1100-READ-CONTROL-CARD.                                                  
025401                                                                          
025402      READ TIMESTAMP-FILE INTO                                            
025403          RC011-CONTROL-RECORD.                                           
025404      MOVE RC011-CONTROL-TMST TO PC-CURRENT-TIMESTAMP.                    
025405     DISPLAY 'SUKUT003 CONTROL CARD -- ' RC011-CONTROL-TMST.              
025406                                                                          
025410 2000-PROCESS-SHIPMENT-UNITS.                                             
025500******************************************************************        
025600*  PROCESS THE CERTIFICATION REQUESTS (LOOP THROUGH THE RESULTS           
025700*  TABLE).  FORMAT THE OUTPUT RECORD.  WRITE THE RECORDS THAT             
025800*  MEET THE CRITERIA.                                                     
025900******************************************************************        
026000                                                                          
026100     PERFORM 8000-WRITE-SHIPMT-UNIT-RECORD.                               
026101                                                                          
026102     PERFORM 7200-CHECK-AUDIT-DATABASE.                                   
026110     IF FOUND-ON-AUDIT                                                    
026120        PERFORM 8200-WRITE-INACTIVE-RECORD                                
026130     END-IF.                                                              
026140                                                                          
026200     PERFORM 7100-FETCH-SHIPMT-UNIT-CURSOR.                               
026300                                                                          
026400 7000-OPEN-SHIPMT-UNIT-CURSOR.                                            
026500******************************************************************        
026600*  OPEN THE CURSOR.                                                       
026700******************************************************************        
026800     PERFORM WITH TEST AFTER                                              
026900       VARYING PV-OPEN-COUNTER FROM 1 BY 1                                
027000         UNTIL (PV-OPEN-COUNTER > PC-MAX-RETRIES                          
027100                OR                                                        
027200                SQLCODE = ZERO)                                           
027300                                                                          
027400         EXEC SQL                                                         
027500              OPEN SHIPMT_UNIT_CURSOR                                     
027600         END-EXEC                                                         
027700                                                                          
027800         EVALUATE TRUE                                                    
027900             WHEN SQLCODE = -904                                          
028000             WHEN SQLCODE = -911                                          
028100                 CONTINUE                                                 
028200             WHEN SQLWARN0 NOT = SPACES                                   
028300             WHEN SQLCODE < ZERO                                          
028400             WHEN SQLCODE > ZERO                                          
028500                 MOVE '7000-OPEN-SHIPMT-UNIT-CURSOR    '                  
028600                                 TO PV-PARAGRAPH-NAME                     
028700                 MOVE 'OPEN THE SHIPMT_UNIT_CURSOR    '                   
028800                                 TO PV-DB2-OPERATION-ATTEMPTED            
028900                 PERFORM 9100-SQL-ABEND                                   
029000             WHEN SQLCODE = ZERO                                          
029100                 CONTINUE                                                 
029200         END-EVALUATE                                                     
029300     END-PERFORM.                                                         
029400                                                                          
029500     IF PV-OPEN-COUNTER > PC-MAX-RETRIES                                  
029600         MOVE '7000-OPEN-SHIPMT-UNIT-CURSOR   '                           
029700                                 TO PV-PARAGRAPH-NAME                     
029800         MOVE 'OPEN MAX RETRIES EXCEEDED     '                            
029900                                 TO PV-DB2-OPERATION-ATTEMPTED            
030000         PERFORM 9100-SQL-ABEND                                           
030100     END-IF.                                                              
030200                                                                          
030300 7100-FETCH-SHIPMT-UNIT-CURSOR.                                           
030400******************************************************************        
030500*  FETCH THE ROW FROM THE RESULTS TABLE USING THE CURSOR.                 
030600******************************************************************        
030700     EXEC SQL                                                             
030800          FETCH SHIPMT_UNIT_CURSOR                                        
030900           INTO :SHPUNT-SHIPT-UNT-ID,                                     
031000                :SHPUNT-ORGN-OPER-UNT-ID,                                 
031100                :SHPUNT-ORGN-FCLTY-ID,                                    
031200                :SHPUNT-DEST-OPER-UNT-ID,                                 
031300                :SHPUNT-DEST-FCLTY-ID,                                    
031400                :SHPUNT-SHIPT-UNT-SRC-ID,                                 
031500                :SHPUNT-HIER-LVL-CDE,                                     
031600                :SHPUNT-NEXT-SHIPT-UNT-ID,                                
031700                :SHPUNT-ORIG-SHIPT-UNT-ID,                                
031800                :SHPUNT-ACTV-IND,                                         
031900                :SHPUNT-CRE-DTE,                                          
032000                :SHPUNT-CRE-TM,                                           
032100                :SHPUNT-CRE-USR-ID,                                       
032200                :SHPUNT-CRE-PGM-NM                                        
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-SHIP-UNIT-CSR                                     
033000                                 TO TRUE                                  
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7100-FETCH-SHIPMT-UNIT-CURSOR  '                       
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE SHIPMT-UNIT-CURSOR   '                       
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034010 7200-CHECK-AUDIT-DATABASE.                                               
034020******************************************************************        
034030*  PERFORM A SELECT AGAINST THE AUDIT DATABASE TO SEE IF THE CARTON       
034031*  IS INCLUDED IN AN AUDIT.  IF IT IS A RECORD WILL BE WRITTEN TO         
034032*  THE SHIPUNIT INACTIVE FILE.                                            
034040******************************************************************        
034041                                                                          
034050     EXEC SQL                                                             
034060          SELECT AUD_CNTL_NBR                                             
034061            INTO :SUATRN-AUD-CNTL-NBR                                     
034062            FROM TSUATRN                                                  
034063           WHERE SHIPT_UNT_ID = :SHPUNT-SHIPT-UNT-ID                      
034064             AND DEST_FCLTY_ID >= 0                                       
034102     END-EXEC.                                                            
034103                                                                          
034104     EVALUATE TRUE                                                        
034105         WHEN SQLCODE = ZERO                                              
034106         WHEN SQLCODE = -811                                              
034107             SET FOUND-ON-AUDIT TO TRUE                                   
034108         WHEN SQLCODE = +100                                              
034109             SET NOT-FOUND-ON-AUDIT                                       
034110                                 TO TRUE                                  
034111         WHEN SQLWARN0 NOT = SPACES                                       
034112         WHEN SQLCODE < ZERO                                              
034113         WHEN SQLCODE > ZERO                                              
034114             MOVE '7200-CHECK-AUDIT-DATABASE      '                       
034115                                 TO PV-PARAGRAPH-NAME                     
034116             DISPLAY 'SHIP UNIT ID BEING PROCESSED '                      
034117                SHPUNT-SHIPT-UNT-ID                                       
034118             PERFORM 9100-SQL-ABEND                                       
034119     END-EVALUATE.                                                        
034120                                                                          
034130 8000-WRITE-SHIPMT-UNIT-RECORD.                                           
034200******************************************************************        
034300*  WRITE THE SHIPMENT-UNIT-RECORD                                         
034400******************************************************************        
034500     INITIALIZE SHIPMENT-UNIT-RECORD.                                     
034600     WRITE SHIPMENT-UNIT-RECORD                                           
034700         FROM DCLTSHPUNT.                                                 
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
034900                                                                          
035000 8100-WRITE-TIMESTAMP-RECORD.                                             
035100******************************************************************        
035200*  WRITE THE TIMESTAMP RECORD                                             
035300******************************************************************        
035400     WRITE TIMESTAMP-RECORD                                               
035500         FROM RC011-CONTROL-RECORD.                                       
035600                                                                          
035610 8200-WRITE-INACTIVE-RECORD.                                              
035620******************************************************************        
035630*  WRITE THE SHIPUNIT-INACTIVE-RECORD                                     
035640******************************************************************        
035650     INITIALIZE SHIPUNIT-INACTIVE-RECORD.                                 
035660     WRITE SHIPUNIT-INACTIVE-RECORD                                       
035670         FROM DCLTSHPUNT.                                                 
035680     ADD +1 TO PV-WRITE2-COUNTER.                                         
035690                                                                          
035700                                                                          
035800 9000-EOJ-ROUTINE.                                                        
035900******************************************************************        
036000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
036100******************************************************************        
036200     CLOSE SHIPMENT-UNIT-FILE                                             
036300           SHIPUNIT-INACTIVE-FILE                                         
036310           TIMESTAMP-FILE.                                                
036400                                                                          
036500     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036600     DISPLAY 'TOTAL SHIP UNIT RECORDS WRITTEN  ' PV-DISPLAY-COUNT.        
036610     MOVE PV-WRITE2-COUNTER TO PV-DISPLAY-COUNT.                          
036620     DISPLAY 'TOTAL SHIPUNIT INACTIVE RECORDS WRITTEN '                   
036630                                                 PV-DISPLAY-COUNT.        
036700                                                                          
036800     PERFORM WITH TEST AFTER                                              
036900       VARYING PV-CLOSE-COUNTER FROM 1 BY 1                               
037000         UNTIL (PV-CLOSE-COUNTER > PC-MAX-RETRIES                         
037100                OR                                                        
037200                SQLCODE = ZERO)                                           
037300                                                                          
037400         EXEC SQL                                                         
037500              CLOSE SHIPMT_UNIT_CURSOR                                    
037600         END-EXEC                                                         
037700                                                                          
037800         EVALUATE TRUE                                                    
037900             WHEN SQLCODE = -904                                          
038000             WHEN SQLCODE = -911                                          
038100                 CONTINUE                                                 
038200             WHEN SQLWARN0 NOT = SPACES                                   
038300             WHEN SQLCODE < ZERO                                          
038400             WHEN SQLCODE > ZERO                                          
038500                 MOVE '9000-EOJ-ROUTINE               '                   
038600                                 TO PV-PARAGRAPH-NAME                     
038700                 MOVE 'CLOSE THE SHIPMT_UNIT_CURSOR    '                  
038800                                 TO PV-DB2-OPERATION-ATTEMPTED            
038900                 PERFORM 9100-SQL-ABEND                                   
039000             WHEN SQLCODE = ZERO                                          
039100                 CONTINUE                                                 
039200         END-EVALUATE                                                     
039300     END-PERFORM.                                                         
039400                                                                          
039500     IF PV-CLOSE-COUNTER > PC-MAX-RETRIES                                 
039600         MOVE '9000-EOJ-ROUTINE               '                           
039700                                 TO PV-PARAGRAPH-NAME                     
039800         MOVE 'CLOSE MAX RETRIES EXCEEDED     '                           
039900                                 TO PV-DB2-OPERATION-ATTEMPTED            
040000         PERFORM 9100-SQL-ABEND                                           
040100     END-IF.                                                              
040200                                                                          
040300 9100-SQL-ABEND.                                                          
040400******************************************************************        
040500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040600*  ERROR OR WARNING CODE OCCURS.                                          
040700******************************************************************        
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
