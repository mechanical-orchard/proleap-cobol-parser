       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKCS028.                                                 
                                                                                
       AUTHOR.        CALVIN BUSH.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  MARCH 3, 1992.                                            
       DATE-COMPILED.                                                           
      ******************************************************************        
      **                    CICS LOGON SUBROUTINE                     **        
      **                                                              **        
      **                                                              **        
      **  01/19/93  -  CHANGE COPYBOOK DPWS030 TO DPWS026 - NAME      **        
      **               CHANGE ONLY.                                   **        
      **               MADE BY: CAL BUSH                              **        
      **                                                              **        
      **  01/21/93  -  SAVE COMMAREA IF THE USER'S INITIAL TRANSACTION**        
      **               IS OUTSIDE OF THE ARCHITECTURE.  SEE PARAGRAPH **        
      **               5000-.                                         **        
      **               MADE BY: CAL BUSH                              **        
      **                                                              **        
      **  11/03/93  -  SEND NON-DISCLOSURE SCREEN.                    **        
      **               MADE BY: CAL BUSH                              **        
      **                                                              **        
      **  05/31/96  -  ADD NEW LOOK-UP OF LOCATION FOR USERS LOGGING  **        
      **               IN USING IP ADDRESS (FROM WORKSTATIONS).       **        
      **               THIS IS IN ADDITION TO THE EXISTING LOOK-UP    **        
      **               OF LOCATION FOR USERS LOGGING IN USING VTAM    **        
      **               ADDRESS OF LINE AND PHYSICAL UNIT (FROM DUMB   **        
      **               TERMINALS).                                    **        
      **               MADE BY: CAL BUSH                              **        
      **  11/15/96  -  DECREASED RETRIES FOR IP LOCATION AND REMOVED  **        
      **               DISPLAY OF 'LOGON IN PROGRESS' MESSAGE.        **        
      **               MADE BY: CAL BUSH                              **        
      **  08/24/98  -  CONVERTED "START" TO "RETURN IMMEDIATE".       **        
      **               MADE BY: JEFF ROUBIK                           **        
      **  09/23/99  -  DISPLAY 'LOCATION NOT FOUND' AND ALLOW LOCATION**        
      **               OVERRIDE IF USER HAS PROPER SECURITY           **        
      **               (F-DPLOCATN).                                  **        
      **               MADE BY: CAL BUSH                              **        
      **  08/01/02  -  GIVE A MESSAGE IF THEIR SESSION HAS TIMED OUT. **        
      **               MADE BY: JEFF ROUBIK                           **        
      **  12/22/03  -  UPDATED FOR 5 DIGIT LOCATION.                  **        
      **               MADE BY: JEFF ROUBIK                           **        
      **  03/03/04  -  UPDATED FOR 8 CHAR DEFAULT PRINTER ID          **        
      **               MADE BY: LYNN MCCANN                           **        
      **  10/25/04  -  PREVENT OVERRIDE OF LOCATION IF TYPE IS 'OP'   **        
      **               MADE BY: JEFF ROUBIK                           **        
      **                                                              **        
      ******************************************************************        
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
      *                                                                         
       DATA DIVISION.                                                           
      *                                                                         
           EJECT                                                                
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-GLOBAL-COMMAREA-LENGTH                                        
                                        PIC S9(04)  VALUE +1207                 
                                                    COMP.                       
           05  PC-TEMP-COMMAREA-LENGTH  PIC S9(04)  VALUE +71                   
                                                    COMP.                       
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE +0005                 
                                                    COMP.                       
           05  PC-IP-MAX-RETRIES        PIC S9(04)  VALUE +0001                 
                                                    COMP.                       
           05  PC-FIRST-ITEM            PIC S9(04)  VALUE +0001                 
                                                    COMP.                       
           05  PC-DEFAULT-MNEMONIC      PIC  X(08)  VALUE 'CORPMENU'.           
           05  PC-DEFAULT-OPERCO        PIC  X(02)  VALUE '03'.                 
           05  PC-DEFAULT-LOCATION-NBR  PIC S9(04)  VALUE 00090.                
           05  PC-DEFAULT-LOCATION-TYPE PIC  X(02)  VALUE 'HQ'.                 
           05  PC-DEFAULT-FCLTY-ID      PIC S9(04)  VALUE +1                    
                                                    COMP.                       
           05  PC-MENU-TRANID           PIC  X(04)  VALUE 'Z007'.               
           05  PC-LOGON-TRANID          PIC  X(04)  VALUE 'Z028'.               
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE 'DPKCS028'.           
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE 'DPKM028 '.           
           05  PC-CICSARCH-FUNC         PIC  X(08)  VALUE 'NEWARCH '.           
           05  PC-OVERRIDE-LOC-FUNC     PIC  X(08)  VALUE 'DPLOCATN'.           
           05  PC-NONDISCLOSURE-MAP     PIC  X(08)  VALUE 'DP028B  '.           
           05  PC-LOG-QUEUE-RESOURCE    PIC  X(04)  VALUE 'CSSL'.               
           05  PC-LOG-QUEUE             PIC  X(04)  VALUE 'CSSL'.               
           05  PC-SYSTEM-MESSAGE-NUMBER.                                        
               10  PC-TSYMSG-99900      PIC  9(05)  VALUE 99900.                
           05  PC-NETNAME-LENGTH        PIC S9(04)  VALUE 8                     
                                                    COMP.                       
           05  PC-SAVE-COMM-DD          PIC  X(08)  VALUE 'DPSAVCMK'.           
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-AVAILABILITY-ERROR-SW PIC  X(01)  VALUE 'N'.                  
               88  PS-AVAILABILITY-ERROR            VALUE 'Y'.                  
               88  PS-AVAILABILITY-GOOD             VALUE 'N'.                  
           05  PS-MNEMONIC-FOUND-SW     PIC  X(01)  VALUE 'Y'.                  
               88  PS-MNEMONIC-FOUND                VALUE 'Y'.                  
               88  PS-MNEMONIC-NOT-FOUND            VALUE 'N'.                  
           05  PS-MAIN-MENU-SW          PIC  X(01)  VALUE 'N'.                  
               88  PS-MAIN-MENU-CMMD-ENTERED        VALUE 'Y'.                  
               88  PS-MAIN-MENU-CMMD-NOT-ENTERED    VALUE 'N'.                  
           05  PS-TRAN-AVAILABLE-SW     PIC  X(01)  VALUE 'Y'.                  
               88  PS-TRAN-AVAILABLE                VALUE 'Y'.                  
               88  PS-TRAN-NOT-AVAILABLE            VALUE 'N'.                  
           05  PS-IP-FOUND-SW           PIC  X(01)  VALUE 'N'.                  
               88  PS-IP-FOUND                      VALUE 'Y'.                  
               88  PS-IP-NOT-FOUND                  VALUE 'N'.                  
           05  PS-ERROR-SW              PIC  X(01)  VALUE 'N'.                  
               88  PS-ERROR                         VALUE 'Y'.                  
               88  PS-NO-ERROR                      VALUE 'N'.                  
           05  PS-TIMEOUT-SW            PIC  X(01)  VALUE 'N'.                  
               88  PS-TIMEOUT                       VALUE 'Y'.                  
               88  PS-NO-TIMEOUT                    VALUE 'N'.                  
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INQUIRE-CODE          PIC S9(08)  VALUE +0                    
                                                        COMP.                   
           05  PV-CICS-RESPONSE         PIC S9(08)  VALUE ZERO                  
                                                        COMP SYNC.              
           05  PV-TS-ITEM               PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
           05  PV-TCT-USER-AREA-LENGTH  PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
               88  PV-RF-DEVICE-TCTUA-LENGTH        VALUE +255.                 
           05  PV-IP-RETRY-COUNT        PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
           05  PV-BLUEZONE-IP-CAPTURE   PIC  X(15)  VALUE SPACE.                
           05  PV-BLUEZONE-RETURN-CODE  PIC  9(4)   VALUE ZEROS.                
           05  PV-MAP-NAME              PIC  X(08)  VALUE SPACE.                
           05  PV-MESSAGE-BUFFER        PIC  X(80)  VALUE SPACE.                
           05  PV-NETNAME-MESSAGE.                                              
               10  FILLER               PIC  X(20)  VALUE                       
                   ' DPKCS028: NETNAME ('.                                      
               10  PV-MSG-NETNAME       PIC  X(08)  VALUE SPACE.                
               10  FILLER               PIC  X(16)  VALUE                       
                   ') NOT ON TPHYUNT'.                                          
           05  PV-IP-MESSAGE.                                                   
               10  FILLER               PIC  X(20)  VALUE                       
                   ' DPKCS028: IP ADDR ('.                                      
               10  PV-MSG-IP-ADDR       PIC  X(15)  VALUE SPACE.                
               10  FILLER               PIC  X(11)  VALUE                       
                   ') NETNAME ('.                                               
               10  PV-MSG-IP-NETNAME    PIC  X(08)  VALUE SPACE.                
               10  FILLER               PIC  X(16)  VALUE                       
                   ') NOT ON TPHYUNT'.                                          
           05  PV-OVERRIDE-MESSAGE.                                             
               10  FILLER               PIC  X(32)  VALUE                       
                   ' DPKCS028: OVERRIDE SYSTEM LOC: '.                          
               10  PV-MSG-SYSTEM-LOCATION                                       
                                        PIC  9(05)  VALUE ZERO.                 
               10  FILLER               PIC  X(15)  VALUE                       
                   ' NEW LOCATION: '.                                           
               10  PV-MSG-NEW-LOCATION  PIC  9(05)  VALUE ZERO.                 
               10  FILLER               PIC  X(07)  VALUE ' USER: '.            
               10  PV-MSG-USERID        PIC  X(08)  VALUE SPACE.                
           05  PV-CLEANUP-MESSAGE.                                              
               10                       PIC  X(10)  VALUE                       
                   ' DPKCS028:'.                                                
               10  PV-MSG-DEL-RECS      PIC   ZZZ9  VALUE SPACE.                
               10                       PIC  X(18)  VALUE                       
                   ' SAVED COMMAREAS &'.                                        
               10  PV-MSG-DEL-TSQS      PIC   ZZZ9  VALUE SPACE.                
               10                       PIC  X(23)  VALUE                       
                   ' TS QUEUES DELETED FOR '.                                   
               10  PV-USERID            PIC  X(08)  VALUE SPACE.                
           05  PV-DEL-RECS-TERM         PIC S9(04)  VALUE +0                    
                                                          COMP SYNC.            
           05  PV-DEL-TSQS-TERM         PIC S9(04)  VALUE +0                    
                                                          COMP SYNC.            
           05  PV-TS-KEY.                                                       
               10  PV-TS-KEY-TERM-ID    PIC  X(04)  VALUE LOW-VALUES.           
               10  FILLER               PIC  X(04)  VALUE LOW-VALUES.           
                                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    DATAKEYS FOR DB2 TABLE ACCESS                                        
      ******************************************************************        
       01  DK-IP-ADDR-FORMAT.                                                   
           05  DK-IP-ADDR-OCTET-1       PIC  X(03)  VALUE SPACE.                
           05  FILLER                   PIC  X(01)  VALUE '.'.                  
           05  DK-IP-ADDR-OCTET-2       PIC  X(03)  VALUE SPACE.                
           05  FILLER                   PIC  X(01)  VALUE '.'.                  
           05  DK-IP-ADDR-OCTET-3       PIC  X(03)  VALUE SPACE.                
           05  FILLER                   PIC  X(01)  VALUE '.'.                  
           05  DK-IP-ADDR-OCTET-4       PIC  X(03)  VALUE SPACE.                
       01  DK-IP-ADDR                   PIC  X(15)  VALUE SPACE.                
       01  DK-IP-ADDR-INITIAL           PIC  X(15)  VALUE                       
                                                    '000 000 000 000'.          
                                                                                
      ******************************************************************        
      *    LAYOUT USED TO GET THE SYSTEM DATE AND TIME                          
      ******************************************************************        
       01  DT-DATE-TIME-VARIABLES.                                              
           05  DT-ABSTIME                  PIC S9(15)  VALUE ZERO               
                                                       COMP-3.                  
           05  DT-YEAR-BIN                 PIC S9(09)  VALUE ZERO               
                                                       COMP SYNC.               
           05  DT-FORMATTED-TIME           PIC  X(08)  VALUE SPACE.             
           05  DT-FORMATTED-DATE           PIC  X(10)  VALUE SPACE.             
           05  FILLER                      REDEFINES DT-FORMATTED-DATE.         
               10  DT-FORMATTED-MMDDYY     PIC  X(08).                          
               10  FILLER                  PIC  X(02).                          
           05  FILLER                      REDEFINES DT-FORMATTED-DATE.         
               10  FILLER                  PIC  X(06).                          
               10  DT-FORMATTED-YYYY       PIC  9(04).                          
      *                                                                         
      **  FIELDS FOR STACK MANIPULATION                                         
      *                                                                         
       01  SM-STACK-MANIPULATION-FIELDS.                                        
           05  SM-AID-MAX               PIC S9(04)  VALUE +29                   
                                                    COMP SYNC.                  
           05  SM-FK-SUB                PIC S9(04)  VALUE ZERO                  
                                                    COMP SYNC.                  
           05  SM-CICS-RESPONSE         PIC S9(08)  VALUE ZERO                  
                                                    COMP SYNC.                  
       01  TS-TEMP-STORAGE-QUEUE-INFO.                                          
           05  TS-INFO-QUEUE-NAME.                                              
               10  TS-INFO-QUEUE-TERMINAL                                       
                                        PIC  X(04)  VALUE SPACE.                
               10  FILLER               PIC  X(04)  VALUE 'INFO'.               
           05  TS-SCA-QUEUE-INFO.                                               
               10  TS-SCA-QUEUE-NAME.                                           
                   15  TS-SCA-QUEUE-TERMINAL                                    
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-SCA-QUEUE-LITERAL                                     
                                        PIC  X(04)  VALUE 'SCA '.               
               10  TS-SCA-QUEUE-ITEM-NUMBER                                     
                                        PIC S9(04)  VALUE ZERO                  
                                        COMP SYNC.                              
                                                                                
      * REMEMBER TO ADJUST PC-TEMP-COMMAREA-LENGTH WHEN CA-COMMAREA             
      * IS LENGTHENED.                                                          
       01  CA-COMMAREA.                                                         
           05  CA-RF-DEVICE-SW          PIC  X(01)  VALUE 'N'.                  
               88  CA-RF-DEVICE                     VALUE 'Y'.                  
               88  CA-TERMINAL                      VALUE 'N'.                  
           05  CA-DISPLAY-MSG-SW        PIC  X(01)  VALUE 'N'.                  
               88  CA-DISPLAY-MSG                   VALUE 'Y'.                  
           05  CA-ACCESS-ALLOWED-SW     PIC  X(01)  VALUE 'N'.                  
               88  CA-ACCESS-ALLOWED                VALUE 'Y'.                  
                                                                                
           05  CA-SYSTEM-LOCATION-NBR   PIC S9(04)  COMP                        
                                                    VALUE ZERO.                 
           05  CA-LOCATION-NBR          PIC S9(04)  COMP                        
                                                    VALUE ZERO.                 
           05  CA-LOCATION-CHAR         PIC  X(05)  VALUE SPACE.                
           05  CA-LOCATION-PIC9  REDEFINES                                      
               CA-LOCATION-CHAR         PIC  9(05).                             
           05  CA-LOCATION-TYPE         PIC  X(02)  VALUE SPACE.                
           05  CA-OVERRIDE-LOCATION-SW  PIC  X(01)  VALUE 'N'.                  
               88  CA-OVERRIDE-LOCATION             VALUE 'Y'.                  
                                                                                
                                                                                
           05  CA-NETNAME.                                                      
               10  CA-LINE-ADDR         PIC  X(02)  VALUE SPACE.                
               10  CA-PHYSICAL-UNIT-ADDR                                        
                                        PIC  X(02)  VALUE SPACE.                
               10  CA-TERM-ID           PIC  X(04)  VALUE SPACE.                
           05  CA-FCLTY-ID              PIC S9(04)  COMP                        
                                                    VALUE ZERO.                 
           05  CA-USERID                PIC  X(08)  VALUE SPACE.                
           05  CA-OPERATING-COMPANY     PIC  X(02)  VALUE SPACE.                
           05  CA-SRC-MNEMONIC          PIC  X(08)  VALUE SPACE.                
           05  CA-INITIAL-MNEMONIC      PIC  X(08)  VALUE SPACE.                
           05  CA-SRC-TRAN-ID           PIC  X(04)  VALUE SPACE.                
           05  CA-LOCATION-ACRT-SW      PIC  X(01)  VALUE SPACE.                
               88  CA-LOCATION-ACRT                 VALUE 'Y'.                  
               88  CA-LOCATION-DFLT                 VALUE 'N'.                  
           05  CA-DFLT-PRTR-ID          PIC  X(08)  VALUE SPACE.                
      *                                                                         
      **  SYMBOLIC MAP                                                          
      *                                                                         
           COPY DPKM028.                                                        
      *                                                                         
      **  BMS ATTRIBUTE CONSTANTS                                               
      *                                                                         
           COPY DPWS015.                                                        
      *                                                                         
      **  SECURITY SUBROUTINE PARAMETERS                                        
      *                                                                         
           COPY DPWS008.                                                        
      *                                                                         
      **  TCP QUERY SUBROUTINE CALLING PARAMETERS                               
      *                                                                         
           COPY DPWS199.                                                        
      *                                                                         
      **  STANDARD COMMAREA LAYOUT                                              
      *                                                                         
           COPY DPWS020.                                                        
      *                                                                         
      **  INPUT RECORD DESCRIPTION FOR MNEMONIC FILE                            
      *                                                                         
           COPY DPRD503.                                                        
      *                                                                         
      **  'INFO' TEMP STORAGE QUEUE LAYOUT                                      
      *                                                                         
           COPY DPWS026.                                                        
      *                                                                         
      **  LAYOUT FOR ABEND-MODULE COMMAREA                                      
      *                                                                         
           COPY DPWS013.                                                        
                                                                                
      *                                                                         
      *    SQL AND COBOL DECLARATIONS FOR THE USER TABLE                        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TUSER                                                    
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *    SQL AND COBOL DECLARATIONS FOR THE PHYSICAL UNIT TABLE               
      *                                                                         
           EXEC SQL                                                             
               INCLUDE TPHYUNT                                                  
           END-EXEC.                                                            
                                                                                
      *                                                                         
      *    SQL AND COBOL DECLARATIONS FOR THE WORKER TABLE                      
      *                                                                         
      **** EXEC SQL                                                             
      ****     INCLUDE TWORKER                                                  
      **** END-EXEC.                                                            
                                                                                
      *                                                                         
      *    SQL AND COBOL DECLARATIONS FOR THE LOCATION TABLE                    
      *                                                                         
           EXEC SQL                                                             
               INCLUDE XIT00000                                                 
           END-EXEC.                                                            
                                                                                
                                                                                
                                                                                
      *                                                                         
      *    STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA  -  SQLCA            
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
           EJECT                                                                
       LINKAGE SECTION.                                                         
       01  DFHCOMMAREA                    PIC  X(4072).                         
           EJECT                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN-LINE.                                                          
           IF  EIBCALEN > ZERO                                                  
               IF DFHCOMMAREA(1:7) = 'TIMEOUT'                                  
                   SET PS-TIMEOUT TO TRUE                                       
                   PERFORM 1000-INITIALIZATION                                  
               ELSE                                                             
                   MOVE DFHCOMMAREA           TO CA-COMMAREA                    
               END-IF                                                           
           ELSE                                                                 
               PERFORM 1000-INITIALIZATION                                      
           END-IF.                                                              
      *                                                                         
           IF  CA-TERMINAL                                                      
               PERFORM 2000-TERMINAL-PROCESSING                                 
           END-IF.                                                              
                                                                                
      *                                                                         
           IF  CA-ACCESS-ALLOWED                                                
             AND CA-TERMINAL                                                    
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2600-CLEANUP-TERMID                                      
               PERFORM 3000-CREATE-INFO-TSQ                                     
               EXEC CICS                                                        
                    RETURN                                                      
               END-EXEC                                                         
           END-IF.                                                              
      *                                                                         
           IF  PS-ERROR                                                         
               PERFORM 2310-RETURN-TRANID                                       
           ELSE                                                                 
               PERFORM 2600-CLEANUP-TERMID                                      
               PERFORM 3000-CREATE-INFO-TSQ                                     
               PERFORM 5000-TRANSFER-CONTROL                                    
               EXEC CICS                                                        
                    RETURN                                                      
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
       1000-INITIALIZATION.                                                     
           PERFORM 1500-CHECK-FOR-RF-DEVICE.                                    
           SET DP008-RSRC-TYPE-FUNC       TO TRUE.                              
           SET DP008-ACCESS-TYPE-USE      TO TRUE.                              
           MOVE PC-CICSARCH-FUNC          TO DP008-FUNCTION-NAME.               
           PERFORM 2200-LINK-TO-SECURITY.                                       
           IF  DP008-ACCESS-ALLOWED                                             
               SET CA-ACCESS-ALLOWED     TO TRUE                                
           END-IF.                                                              
           PERFORM 2510-GET-INITIAL-VALUES.                                     
      ******************************************************************        
      **  SYMBOL RF DEVICES CANNOT SUPPORT THE ARCHITECTURE.  IF THE  **        
      **  CURRENT DEVICE PROVES TO BE AN LRT, IMMEDIATELY RETURN THEM **        
      **  TO NATIVE CICS, EVEN IF THE USER'S USERID IS VALID FOR THE  **        
      **  ARCHITECTURE.  IN THE TCT ENTRIES FOR ALL LRTS, A TCTUA     **        
      **  LENGTH OF 255 SHOULD BE SET UP.  BASED ON THAT USER AREA    **        
      **  LENGTH, WE WILL ASSUME THAT AN LRT IS BEING USED, AND       **        
      **  RETURN THEM TO NATIVE CICS.                                 **        
      ******************************************************************        
                                                                                
       1500-CHECK-FOR-RF-DEVICE.                                                
           EXEC CICS                                                            
                INQUIRE                                                         
                    TERMINAL    (EIBTRMID)                                      
                    USERAREALEN (PV-TCT-USER-AREA-LENGTH)                       
           END-EXEC.                                                            
                                                                                
           IF PV-RF-DEVICE-TCTUA-LENGTH                                         
               SET CA-RF-DEVICE TO TRUE                                         
           END-IF.                                                              
      *                                                                         
       2000-TERMINAL-PROCESSING.                                                
           IF EIBCALEN = 0                                                      
           OR PS-TIMEOUT                                                        
               SET DP008-ACCESS-DENIED        TO TRUE                           
               SET DP008-RSRC-TYPE-FUNC       TO TRUE                           
               SET DP008-ACCESS-TYPE-USE      TO TRUE                           
               MOVE PC-OVERRIDE-LOC-FUNC      TO DP008-FUNCTION-NAME            
               PERFORM 2200-LINK-TO-SECURITY                                    
               IF  DP008-ACCESS-ALLOWED                                         
                   SET CA-OVERRIDE-LOCATION   TO TRUE                           
               END-IF                                                           
               PERFORM 2110-RESET-ATTRIBUTES                                    
               SET PS-ERROR                   TO TRUE                           
           ELSE                                                                 
               PERFORM 2100-MAP-PROCESSING                                      
           END-IF.                                                              
      *                                                                         
           PERFORM 2120-DISPLAY-MSG-BOX.                                        
           MOVE CA-SYSTEM-LOCATION-NBR TO CLOCFLO.                              
           MOVE CA-LOCATION-CHAR       TO OLOCFLO.                              
           MOVE PC-NONDISCLOSURE-MAP   TO PV-MAP-NAME.                          
                                                                                
           IF PS-ERROR                                                          
               PERFORM 2320-SEND-MAP                                            
               PERFORM 2310-RETURN-TRANID                                       
           ELSE                                                                 
               PERFORM 2500-INIT-COMMAREA-AND-DEST                              
           END-IF.                                                              
                                                                                
      *                                                                         
       2100-MAP-PROCESSING.                                                     
           MOVE PC-NONDISCLOSURE-MAP  TO PV-MAP-NAME.                           
           PERFORM 2105-RECEIVE-MAP.                                            
           PERFORM 2130-EDIT-MAP.                                               
           IF  PS-NO-ERROR                                                      
               IF  CA-LOCATION-NBR = CA-SYSTEM-LOCATION-NBR                     
                   CONTINUE                                                     
               ELSE                                                             
      *            IF THE USER OVERRIDES, THEN IT MUST BE GOOD                  
                   SET CA-LOCATION-ACRT                                         
                                      TO TRUE                                   
                   MOVE CA-SYSTEM-LOCATION-NBR                                  
                                      TO PV-MSG-SYSTEM-LOCATION                 
                   MOVE CA-LOCATION-NBR                                         
                                      TO PV-MSG-NEW-LOCATION                    
                   MOVE CA-USERID     TO PV-MSG-USERID                          
                   MOVE PV-OVERRIDE-MESSAGE                                     
                                      TO PV-MESSAGE-BUFFER                      
                   PERFORM 2517-LOG-MESSAGE                                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *                                                                         
       2105-RECEIVE-MAP.                                                        
           EXEC CICS                                                            
               RECEIVE                                                          
                   MAP    (PC-NONDISCLOSURE-MAP)                                
                   MAPSET (PC-CURRENT-MAPSET-NAME)                              
                   INTO   (DP028BI)                                             
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
                                                                                
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
                               OR DFHRESP(MAPFAIL)                              
               CONTINUE                                                         
           ELSE                                                                 
               MOVE 'DPKCS028'        TO DP013-PROGRAM                          
               SET DP013-CICS-ABEND   TO TRUE                                   
               MOVE 'RECEIVING LOCATION OVERRIDE MAP'                           
                                      TO DP013-MESSAGE-TEXT (1)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       2110-RESET-ATTRIBUTES.                                                   
           IF  CA-OVERRIDE-LOCATION                                             
               MOVE DP015-BLUE        TO CLOCFLC                                
               MOVE DP015-PRO-NOR-OFF TO CLOCFLA                                
               MOVE DP015-GREEN       TO OLOCFLC                                
               MOVE DP015-UNP-ALP-NOR-OFF                                       
                                      TO OLOCFLA                                
               MOVE DP015-UNDERLINE   TO OLOCFLH                                
               MOVE -1                TO OLOCFLL                                
           ELSE                                                                 
               MOVE DP015-ASK-DRK-OFF TO CLOCTLA                                
                                         OLOCTLA                                
                                         CLOCFLA                                
                                         OLOCFLA                                
               MOVE DP015-HL-OFF      TO CLOCFLH                                
                                         OLOCFLH                                
           END-IF.                                                              
      ************************************************************              
      ** THIS PARAGRAPH WILL DISPLAY THE MESSAGE ABOVE THE      **              
      ** CONFIDENTIALITY SCREEN.  FOR NOW IT ONLY DISPLAYS      **              
      ** PROBLEMS, BUT COULD BE EASILY CHANGED FOR A 'MESSAGE   **              
      ** OF THE DAY' FEATURE IF WE NEED TO IMPLEMENT IT.        **              
      ************************************************************              
       2120-DISPLAY-MSG-BOX.                                                    
           IF CA-DISPLAY-MSG                                                    
             MOVE 'YOUR LOCATION COULD NOT BE DETERMINED. PLEASE'               
                                    TO MSGBXT1O                                 
             MOVE 'LOGOFF CICS AND LOGIN AGAIN. IF THE PROBLEM'                 
                                    TO MSGBXT2O                                 
             MOVE 'PERSISTS, PLEASE CONTACT THE HELPDESK.'                      
                                    TO MSGBXT3O                                 
             MOVE DP015-ASK-BRT-OFF TO MSGBX1A                                  
                                       MSGBX21A                                 
                                       MSGBX22A                                 
                                       MSGBX31A                                 
                                       MSGBX32A                                 
                                       MSGBX41A                                 
                                       MSGBX42A                                 
                                       MSGBX51A                                 
                                       MSGBX52A                                 
                                       MSGBXT1A                                 
                                       MSGBXT2A                                 
                                       MSGBXT3A                                 
                                       MSGBXT4A                                 
             MOVE DP015-HL-OFF      TO MSGBX1H                                  
                                       MSGBX21H                                 
                                       MSGBX22H                                 
                                       MSGBX31H                                 
                                       MSGBX32H                                 
                                       MSGBX41H                                 
                                       MSGBX42H                                 
                                       MSGBX51H                                 
                                       MSGBX52H                                 
                                       MSGBXT1H                                 
                                       MSGBXT2H                                 
                                       MSGBXT3H                                 
                                       MSGBXT4H                                 
             MOVE DP015-WHITE       TO MSGBX1C                                  
                                       MSGBX21C                                 
                                       MSGBX22C                                 
                                       MSGBX31C                                 
                                       MSGBX32C                                 
                                       MSGBX41C                                 
                                       MSGBX42C                                 
                                       MSGBX51C                                 
                                       MSGBX52C                                 
             MOVE DP015-YELLOW      TO MSGBXT1C                                 
                                       MSGBXT2C                                 
                                       MSGBXT3C                                 
                                       MSGBXT4C                                 
           ELSE                                                                 
             IF PS-TIMEOUT                                                      
               MOVE 'YOUR SESSION HAS TIMED OUT DUE TO INACTIVITY.'             
                                      TO MSGBXT1O                               
               MOVE 'TO AVOID THIS IN THE FUTURE, PLEASE REMEMBER TO'           
                                      TO MSGBXT3O                               
               MOVE 'LOGOFF OF CICS AT THE END OF YOUR SHIFT.'                  
                                      TO MSGBXT4O                               
               MOVE DP015-ASK-BRT-OFF TO MSGBX1A                                
                                         MSGBX21A                               
                                         MSGBX22A                               
                                         MSGBX31A                               
                                         MSGBX32A                               
                                         MSGBX41A                               
                                         MSGBX42A                               
                                         MSGBX51A                               
                                         MSGBX52A                               
                                         MSGBXT1A                               
                                         MSGBXT2A                               
                                         MSGBXT3A                               
                                         MSGBXT4A                               
               MOVE DP015-HL-OFF      TO MSGBX1H                                
                                         MSGBX21H                               
                                         MSGBX22H                               
                                         MSGBX31H                               
                                         MSGBX32H                               
                                         MSGBX41H                               
                                         MSGBX42H                               
                                         MSGBX51H                               
                                         MSGBX52H                               
                                         MSGBXT1H                               
                                         MSGBXT2H                               
                                         MSGBXT3H                               
                                         MSGBXT4H                               
               MOVE DP015-WHITE       TO MSGBX1C                                
                                         MSGBX21C                               
                                         MSGBX22C                               
                                         MSGBX31C                               
                                         MSGBX32C                               
                                         MSGBX41C                               
                                         MSGBX42C                               
                                         MSGBX51C                               
                                         MSGBX52C                               
               MOVE DP015-YELLOW      TO MSGBXT1C                               
                                         MSGBXT2C                               
                                         MSGBXT3C                               
                                         MSGBXT4C                               
             ELSE                                                               
               MOVE DP015-ASK-DRK-OFF TO MSGBX1A                                
                                         MSGBX21A                               
                                         MSGBX22A                               
                                         MSGBX31A                               
                                         MSGBX32A                               
                                         MSGBX41A                               
                                         MSGBX42A                               
                                         MSGBX51A                               
                                         MSGBX52A                               
                                         MSGBXT1H                               
                                         MSGBXT2H                               
                                         MSGBXT3H                               
                                         MSGBXT4H                               
               MOVE DP015-HL-OFF      TO MSGBX1H                                
                                         MSGBX21H                               
                                         MSGBX22H                               
                                         MSGBX31H                               
                                         MSGBX32H                               
                                         MSGBX41H                               
                                         MSGBX42H                               
                                         MSGBX51H                               
                                         MSGBX52H                               
                                         MSGBXT1H                               
                                         MSGBXT2H                               
                                         MSGBXT3H                               
                                         MSGBXT4H                               
             END-IF                                                             
           END-IF.                                                              
       2130-EDIT-MAP.                                                           
           IF OLOCFLL > 0                                                       
               IF OLOCFLI > SPACES                                              
                   MOVE OLOCFLI   TO CA-LOCATION-CHAR                           
                   EXEC CICS BIF DEEDIT                                         
                       FIELD  (CA-LOCATION-CHAR)                                
                       LENGTH (5)                                               
                   END-EXEC                                                     
                   MOVE CA-LOCATION-PIC9                                        
                                  TO CA-LOCATION-NBR                            
               ELSE                                                             
                   MOVE CA-SYSTEM-LOCATION-NBR                                  
                                  TO CA-LOCATION-PIC9                           
                                     CA-LOCATION-NBR                            
               END-IF                                                           
           ELSE                                                                 
               IF OLOCFLF = DP015-ERASE-EOF                                     
                   MOVE CA-SYSTEM-LOCATION-NBR                                  
                                  TO CA-LOCATION-PIC9                           
                                     CA-LOCATION-NBR                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2515-GET-LOCATION-USING-STORE.                               
           IF SQLCODE = +100                                                    
               SET PS-ERROR       TO TRUE                                       
               MOVE DP015-RED     TO OLOCFLC                                    
               MOVE DP015-UNP-ALP-BRT-OFF                                       
                                  TO OLOCFLA                                    
               MOVE DP015-REVERSE TO OLOCFLH                                    
               MOVE -1            TO OLOCFLL                                    
               MOVE 'LOCATION NOT FOUND'                                        
                                  TO BMSGO                                      
           END-IF.                                                              
                                                                                
           MOVE XIT00000-LOC-TYP-CDE TO CA-LOCATION-TYPE.                       
                                                                                
                                                                                
      *                                                                         
       2200-LINK-TO-SECURITY.                                                   
           EXEC CICS LINK                                                       
                     PROGRAM  (DP008-SECURITY-SUBROUTINE)                       
                     COMMAREA (DP008-SECURITY-COMMAREA)                         
                     LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)                  
           END-EXEC                                                             
           IF  DP008-CALL-SUCCESSFUL                                            
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               SET DP013-SECURITY-ABEND      TO TRUE                            
               MOVE '0000-MAIN-LINE'         TO DP013-PARAGRAPH                 
               MOVE DP008-SECURITY-COMMAREA  TO DP013-SECURITY-INFO             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH WILL DO A RETURN TRANSID WHEN THE            **        
      **  'NON DISCLOSURE' SCREEN IS DISPLAYED.                       **        
      ******************************************************************        
       2310-RETURN-TRANID.                                                      
           EXEC CICS                                                            
               RETURN                                                           
                   TRANSID  (PC-LOGON-TRANID)                                   
                   COMMAREA (CA-COMMAREA)                                       
                   LENGTH   (PC-TEMP-COMMAREA-LENGTH)                           
           END-EXEC.                                                            
                                                                                
       2320-SEND-MAP.                                                           
           EXEC CICS                                                            
               SEND                                                             
                   MAP     ('DP028B')                                           
                   MAPSET  (PC-CURRENT-MAPSET-NAME)                             
                   ERASE                                                        
                   FORMFEED                                                     
                   CURSOR                                                       
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND TO TRUE                                     
               MOVE '2320-SEND-MAP'  TO DP013-PARAGRAPH                         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  INITIAL ENTRY INTO THE CICS ARCHITECTURE OCCURS HERE.       **        
      **    -  INITIALIZE THE STANDARD COMMAREA.                      **        
      **    -  FIND THE USER'S INITIAL MNEMONIC AND TRAN-ID.          **        
      **    -  CHECK THE SECURITY FOR THE INITIAL MNEMONIC.           **        
      **    -  CHECK THE AVAILABILITY OF THE INITIAL MNEMONIC.        **        
      **  NOTE:  THERE IS NOWHERE TO RETURN; WE MUST START AN         **        
      **         INITIAL TRANSACTION OR ABEND.                        **        
      ******************************************************************        
       2500-INIT-COMMAREA-AND-DEST.                                             
           INITIALIZE DP020-STANDARD-COMMAREA                                   
           SET DP020-GOOD-COMMAREA        TO TRUE                               
           SET DP020-NEXT-ACT-INITIAL     TO TRUE.                              
           SET DP020-FK-DISPLAY-13-24     TO TRUE.                              
           SET DP020-SRC-RETURN-W-TRAN-ID TO TRUE.                              
           PERFORM 2505-PREP-COMMAREA.                                          
           PERFORM 2520-CHECK-INITIAL-SECURITY.                                 
           PERFORM 2530-CHECK-INITIAL-AVAILABLITY.                              
           PERFORM 2540-RESET-SRC-VARIABLES.                                    
           PERFORM 2550-LOAD-FKEY-COMMAREA.                                     
                                                                                
       2505-PREP-COMMAREA.                                                      
           MOVE CA-LOCATION-NBR         TO CA-LOCATION-PIC9.                    
           MOVE CA-LOCATION-CHAR (3:3)  TO DP020-LOCATION.                      
           MOVE CA-LOCATION-NBR         TO DP020-LOCATION-NBR.                  
           MOVE CA-LOCATION-TYPE        TO DP020-LOCATION-TYPE.                 
           MOVE CA-NETNAME              TO DP020-NETNAME.                       
           MOVE CA-FCLTY-ID             TO DP020-FCLTY-ID.                      
           MOVE CA-USERID               TO DP020-USERID.                        
           MOVE CA-OPERATING-COMPANY    TO DP020-OPERATING-COMPANY.             
           MOVE CA-SRC-MNEMONIC         TO DP020-SRC-MNEMONIC.                  
           MOVE CA-INITIAL-MNEMONIC     TO DP020-INITIAL-MNEMONIC.              
           MOVE CA-SRC-TRAN-ID          TO DP020-SRC-TRAN-ID.                   
           MOVE CA-LOCATION-ACRT-SW     TO DP020-LOCATION-ACRT-SW.              
                                                                                
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       2510-GET-INITIAL-VALUES.                                                 
           EXEC CICS                                                            
               ASSIGN                                                           
                   USERID  (CA-USERID)                                          
                   NETNAME (CA-NETNAME)                                         
           END-EXEC.                                                            
      *                                                                         
           INITIALIZE DCLTPHYUNT                                                
                      DCLXIT00000.                                              
      ****            DCLTWORKER.                                               
           SET CA-LOCATION-ACRT         TO TRUE.                                
      *                                                                         
      * SINCE OPER CO. IS ALWAYS 03 DON'T TRY TO FIGURE IT OUT.                 
      **** PERFORM 2511-GET-OPER-CO.                                            
      **** IF  WORKER-OPERCO-NBR = SPACE                                        
               MOVE PC-DEFAULT-OPERCO   TO CA-OPERATING-COMPANY                 
      **** ELSE                                                                 
      ****     MOVE WORKER-OPERCO-NBR   TO CA-OPERATING-COMPANY                 
      **** END-IF.                                                              
      *                                                                         
                                                                                
           PERFORM 2512-GET-LOCATION.                                           
           MOVE ZERO                     TO CA-LOCATION-NBR.                    
           MOVE SPACES                   TO CA-LOCATION-CHAR.                   
           IF PHYUNT-LOC-NBR = ZERO                                             
               MOVE PC-DEFAULT-LOCATION-NBR                                     
                                         TO CA-SYSTEM-LOCATION-NBR              
                                            CA-LOCATION-NBR                     
               MOVE PC-DEFAULT-LOCATION-TYPE                                    
                                         TO CA-LOCATION-TYPE                    
               MOVE PC-DEFAULT-FCLTY-ID  TO CA-FCLTY-ID                         
               SET CA-LOCATION-DFLT                                             
                   CA-DISPLAY-MSG        TO TRUE                                
           ELSE                                                                 
               MOVE PHYUNT-LOC-NBR       TO CA-SYSTEM-LOCATION-NBR              
                                            CA-LOCATION-NBR                     
               MOVE XIT00000-LOC-TYP-CDE TO CA-LOCATION-TYPE                    
               MOVE PHYUNT-FCLTY-ID      TO CA-FCLTY-ID                         
           END-IF.                                                              
      *                                                                         
           INITIALIZE DCLTUSER.                                                 
           PERFORM 2518-GET-INITIAL-MNEMONIC.                                   
           MOVE USER-DFLT-PRTR-ID       TO CA-DFLT-PRTR-ID.                     
           IF  USER-INITIAL-MNMNC-CODE = SPACE                                  
               MOVE PC-DEFAULT-MNEMONIC TO CA-SRC-MNEMONIC                      
                                           CA-INITIAL-MNEMONIC                  
           ELSE                                                                 
               MOVE USER-INITIAL-MNMNC-CODE                                     
                                        TO CA-SRC-MNEMONIC                      
                                           CA-INITIAL-MNEMONIC                  
           END-IF.                                                              
           PERFORM 2519-GET-INITIAL-TRAN-ID.                                    
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
      *2511-GET-OPER-CO.                                                        
      **** PERFORM                                                              
      ****     WITH TEST AFTER                                                  
      ****     VARYING PV-RETRY-COUNTER                                         
      ****     FROM 1 BY 1                                                      
      ****     UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)                       
      ****               OR (SQLCODE = ZERO)                                    
      ****               OR (SQLCODE = +100))                                   
      ****                                                                      
      ****     EXEC SQL                                                         
      ****         SELECT                                                       
      ****             OPERCO_NBR                                               
      ****         INTO                                                         
      ****             :WORKER-OPERCO-NBR                                       
      ****         FROM TWORKER                                                 
      ****         WHERE                                                        
      ****            UID_NBR = :CA-USERID                                      
      ****     END-EXEC                                                         
      ****                                                                      
      ****     EVALUATE TRUE                                                    
      ****         WHEN SQLCODE = ZERO                                          
      ****         WHEN SQLCODE = +100                                          
      ****         WHEN SQLCODE = -904                                          
      ****         WHEN SQLCODE = -913                                          
      ****             CONTINUE                                                 
      ****         WHEN SQLWARN0 NOT = SPACES                                   
      ****         WHEN SQLCODE < ZERO                                          
      ****         WHEN SQLCODE > ZERO                                          
      ****             MOVE '2511-GET-OPER-CO'                                  
      ****                                   TO DP013-PARAGRAPH                 
      ****             MOVE 'SELECT OPERATING COMPANY FOR'                      
      ****                                   TO DP013-MESSAGE-TEXT (1)          
      ****             STRING 'USERID: ' CA-USERID                              
      ****               DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (2)          
      ****             MOVE SQLCA            TO DP013-SQLCA                     
      ****             MOVE 'TWORKER'        TO DP013-DB2-TABLE-NAME (1)        
      ****             SET DP013-DB2-ABEND   TO TRUE                            
      ****             SET DP013-LINK-RETURN TO TRUE                            
      ****             PERFORM DP013-0000-PROCESS-ABEND                         
      ****     END-EVALUATE                                                     
      **** END-PERFORM.                                                         
      ****                                                                      
      **** IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                                
      ****     MOVE '2513-GET-OPER-CO'                                          
      ****        TO DP013-PARAGRAPH                                            
      ****     MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'             
      ****        TO DP013-MESSAGE-TEXT (1)                                     
      ****     MOVE 'OPERATING COMPANY FOR'                                     
      ****        TO DP013-MESSAGE-TEXT (2)                                     
      ****     STRING 'USERID: ' DELIMITED BY SIZE                              
      ****            CA-USERID  DELIMITED BY SIZE                              
      ****         INTO DP013-MESSAGE-TEXT (3)                                  
      ****     MOVE SQLCA          TO DP013-SQLCA                               
      ****     MOVE 'TWORKER'      TO DP013-DB2-TABLE-NAME (1)                  
      ****     SET DP013-DB2-ABEND TO TRUE                                      
      ****     SET DP013-LINK-RETURN TO TRUE                                    
      ****     PERFORM DP013-0000-PROCESS-ABEND                                 
      **** END-IF.                                                              
                                                                                
      ******************************************************************        
      ** DETERMINE WHETHER TO GET LOCATION USING IP -OR- VTAM.        **        
      ** IF THE USER IS COMING IN FROM TCP/IP, THEN THE NETNAME WILL  **        
      ** START WITH 'TCP0'.  IF THIS IS THE CASE, CALL DPKCS199       **        
      ** TO DETERMINE THE IP ADDRESS FOR THE NETNAME, THEN READ THE   **        
      ** PHYSICAL UNIT TABLE TO GET THE LOCATION FOR THE IP ADDRESS.  **        
      **                                                              **        
      ** IF THE USER IS COMING IN FROM VTAM, READ THE PHYSICAL UNIT   **        
      ** TABLE USING THE PHYSICAL UNIT AND LINE ADDRESS PARTS OF THE  **        
      ** NETNAME TO GET THE LOCATION.                                 **        
      ******************************************************************        
       2512-GET-LOCATION.                                                       
           EXEC CICS                                                            
               ASSIGN                                                           
                   USERID  (CA-USERID)                                          
                   NETNAME (CA-NETNAME)                                         
           END-EXEC.                                                            
      *                                                                         
           INITIALIZE PV-BLUEZONE-IP-CAPTURE                                    
                      PV-BLUEZONE-RETURN-CODE                                   
                      PV-MESSAGE-BUFFER.                                        
                                                                                
           PERFORM 9000-BLUEZONE-IP-EXIT.                                       
           MOVE RETURN-CODE TO PV-BLUEZONE-RETURN-CODE.                         
                                                                                
      * A CALL FROM AN EMULATOR OTHER THAN ROCKET TE WEB WILL GET A             
      * RETURN CODE 4 WITH AN IP OF NoQLoc                                      
           IF (PV-BLUEZONE-RETURN-CODE = ZERO)                                  
               INITIALIZE PV-MESSAGE-BUFFER                                     
               STRING ' '                     DELIMITED BY SIZE                 
                      PC-CURRENT-PROGRAM-NAME DELIMITED BY SIZE                 
                      ' - ID='                DELIMITED BY SIZE                 
                      CA-USERID                                                 
                      ' BZ IP='               DELIMITED BY SIZE                 
                      PV-BLUEZONE-IP-CAPTURE                                    
                      ' BZ RC='               DELIMITED BY SIZE                 
                      PV-BLUEZONE-RETURN-CODE DELIMITED BY SIZE                 
                   INTO PV-MESSAGE-BUFFER                                       
               PERFORM 2517-LOG-MESSAGE                                         
           END-IF.                                                              
      *                                                                         
           EVALUATE TRUE                                                        
               WHEN ((PV-BLUEZONE-RETURN-CODE = ZERO) AND                       
                       (PV-BLUEZONE-IP-CAPTURE NOT = SPACES))                   
                   SET PS-IP-FOUND TO TRUE                                      
                   MOVE PV-BLUEZONE-IP-CAPTURE TO DK-IP-ADDR                    
                   PERFORM 2513-GET-LOCATION-USING-IP                           
               WHEN ((CA-LINE-ADDR = 'TC') AND                                  
                       (CA-PHYSICAL-UNIT-ADDR = 'P0'))                          
                   PERFORM 2516-LOOKUP-IP-ADDRESS                               
                   PERFORM 2513-GET-LOCATION-USING-IP                           
               WHEN OTHER                                                       
                   PERFORM 2514-GET-LOCATION-USING-VTAM                         
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** GET LOCATION USING IP ADDRESS.                               **        
      ** (FOR USERS LOGGING IN FROM WORKSTATIONS.)                    **        
      ******************************************************************        
       2513-GET-LOCATION-USING-IP.                                              
           EXEC SQL                                                             
               SELECT                                                           
                   A.LOC_NBR,                                                   
                   A.FCLTY_ID,                                                  
                   B.LOC_TYP_CDE                                                
               INTO                                                             
                   :PHYUNT-LOC-NBR,                                             
                   :PHYUNT-FCLTY-ID,                                            
                   :XIT00000-LOC-TYP-CDE                                        
               FROM TPHYUNT A,                                                  
                    XIT_LOC B                                                   
               WHERE                                                            
                     ((:DK-IP-ADDR >= A.BEG_RNG_IP_ADDR                         
                 AND   :DK-IP-ADDR <= A.END_RNG_IP_ADDR)                        
                 AND (B.LOC_NBR = A.LOC_NBR))                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      * CAL    WHEN SQLCODE = -904                                              
      * CAL    WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE DK-IP-ADDR       TO PV-MSG-IP-ADDR                      
                   MOVE CA-NETNAME       TO PV-MSG-IP-NETNAME                   
                   MOVE PV-IP-MESSAGE    TO PV-MESSAGE-BUFFER                   
                   PERFORM 2517-LOG-MESSAGE                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2513-GET-LOCATION-USING-IP'                            
                                         TO DP013-PARAGRAPH                     
                   MOVE 'SELECT STORE LOCATION AND NUMBER FOR'                  
                                         TO DP013-MESSAGE-TEXT (1)              
                   STRING ' IP ADDR: ' DK-IP-ADDR                               
                     DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (2)              
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TPHYUNT'        TO DP013-DB2-TABLE-NAME (1)            
                   MOVE 'XIT_LOC'        TO DP013-DB2-TABLE-NAME (2)            
                   SET DP013-DB2-ABEND   TO TRUE                                
                   SET DP013-LINK-RETURN TO TRUE                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
           EJECT                                                                
      ******************************************************************        
      ** GET LOCATION USING VTAM ADDRESS (LINE & PHYSICAL UNIT).      **        
      ** (FOR USERS LOGGING IN FROM DUMB TERMINALS.)                  **        
      ******************************************************************        
       2514-GET-LOCATION-USING-VTAM.                                            
           EXEC SQL                                                             
               SELECT                                                           
                   A.LOC_NBR,                                                   
                   A.FCLTY_ID,                                                  
                   B.LOC_TYP_CDE                                                
               INTO                                                             
                   :PHYUNT-LOC-NBR,                                             
                   :PHYUNT-FCLTY-ID,                                            
                   :XIT00000-LOC-TYP-CDE                                        
               FROM TPHYUNT A,                                                  
                    XIT_LOC B                                                   
               WHERE ((A.LNE_ADDR_NBR = :CA-LINE-ADDR)                          
                 AND  (A.PHYUNT_ADDR_NBR = :CA-PHYSICAL-UNIT-ADDR)              
                 AND  (B.LOC_NBR = A.LOC_NBR))                                  
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
      * CAL    WHEN SQLCODE = -904                                              
      * CAL    WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   MOVE CA-NETNAME     TO PV-MSG-NETNAME                        
                   MOVE PV-NETNAME-MESSAGE                                      
                                       TO PV-MESSAGE-BUFFER                     
                   PERFORM 2517-LOG-MESSAGE                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2514-GET-LOCATION-USING-VTAM'                          
                                         TO DP013-PARAGRAPH                     
                   MOVE 'SELECT STORE LOCATION AND NUMBER FOR'                  
                                         TO DP013-MESSAGE-TEXT (1)              
                   STRING ' LINE: ' CA-LINE-ADDR                                
                          ' UNIT: ' CA-PHYSICAL-UNIT-ADDR                       
                     DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (2)              
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'TPHYUNT'        TO DP013-DB2-TABLE-NAME (1)            
                   MOVE 'XIT_LOC'        TO DP013-DB2-TABLE-NAME (2)            
                   SET DP013-DB2-ABEND   TO TRUE                                
                   SET DP013-LINK-RETURN TO TRUE                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      ******************************************************************        
      ** GET LOCATION USING VTAM ADDRESS (LINE & PHYSICAL UNIT).      **        
      ** (FOR USERS LOGGING IN FROM DUMB TERMINALS.)                  **        
      ******************************************************************        
       2515-GET-LOCATION-USING-STORE.                                           
           EXEC SQL                                                             
               SELECT                                                           
                   LOC_NBR,                                                     
                   LOC_TYP_CDE                                                  
               INTO                                                             
                   :XIT00000-LOC-NBR,                                           
                   :XIT00000-LOC-TYP-CDE                                        
               FROM XIT_LOC                                                     
               WHERE                                                            
                   LOC_NBR = :CA-LOCATION-NBR                                   
               AND LOC_TYP_CDE <> 'OP'                                          
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLCODE = +100                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2515-GET-LOCATION-USING-STORE'                         
                                         TO DP013-PARAGRAPH                     
                   MOVE 'SELECT STORE LOCATION AND NUMBER FOR'                  
                                         TO DP013-MESSAGE-TEXT (1)              
                   STRING 'STORE: ' CA-LOCATION-CHAR                            
                     DELIMITED BY SIZE INTO DP013-MESSAGE-TEXT (2)              
                   MOVE SQLCA            TO DP013-SQLCA                         
                   MOVE 'XIT_LOC'        TO DP013-DB2-TABLE-NAME (2)            
                   SET DP013-DB2-ABEND   TO TRUE                                
                   SET DP013-LINK-RETURN TO TRUE                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      **  PARAGRAPH TO GET THE IP ADDRESS.                            **        
      ******************************************************************        
       2516-LOOKUP-IP-ADDRESS.                                                  
           MOVE DK-IP-ADDR-INITIAL TO DK-IP-ADDR.                               
           SET PS-IP-NOT-FOUND     TO TRUE.                                     
           PERFORM                                                              
               VARYING PV-IP-RETRY-COUNT FROM 1 BY 1                            
               UNTIL PS-IP-FOUND                                                
                 OR PV-IP-RETRY-COUNT > PC-IP-MAX-RETRIES                       
               MOVE CA-NETNAME     TO DP199-VTAM-LU                             
               CALL DP199-DPKCS199 USING DP199-VTAM-LU                          
                                         DP199-IP-ADDR                          
                                         DP199-RETURN-CODE                      
               IF  DP199-RETURN-SUCCESS                                         
                   IF  (DP199-IP-ADDR = ZERO)                                   
                   OR  (DP199-IP-ADDR = SPACES)                                 
                       IF  PV-IP-RETRY-COUNT < PC-IP-MAX-RETRIES                
                           EXEC CICS DELAY                                      
                               FOR SECONDS(2)                                   
                           END-EXEC                                             
                       END-IF                                                   
                   ELSE                                                         
                       SET PS-IP-FOUND TO TRUE                                  
                       MOVE DP199-IP-OCTET-1  TO DK-IP-ADDR-OCTET-1             
                       MOVE DP199-IP-OCTET-2  TO DK-IP-ADDR-OCTET-2             
                       MOVE DP199-IP-OCTET-3  TO DK-IP-ADDR-OCTET-3             
                       MOVE DP199-IP-OCTET-4  TO DK-IP-ADDR-OCTET-4             
                       MOVE DK-IP-ADDR-FORMAT TO DK-IP-ADDR                     
                   END-IF                                                       
               ELSE                                                             
                   SET DP013-OTHER-ABEND TO TRUE                                
                   SET DP013-LINK-RETURN TO TRUE                                
                   MOVE '2516-LOOKUP-IP-ADDRESS'                                
                                          TO DP013-PARAGRAPH                    
                   MOVE 'ERROR GETTING IP ADDRESS.  RTN CDE ON THE NEXT         
      -                 'LINE'            TO DP013-MESSAGE-TEXT (1)             
                   MOVE DP199-RETURN-CODE TO DP013-MESSAGE-TEXT (2)             
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-PERFORM.                                                         
      ******************************************************************        
      ** IF THE ROW IS NOT FOUND IN THE PHYSICAL UNIT TABLE, LOG WRITE**        
      ** A LINE TO THE CICS LOG INDICATING THIS.                      **        
      ******************************************************************        
       2517-LOG-MESSAGE.                                                        
           EXEC CICS                                                            
               ENQ                                                              
                   RESOURCE (PC-LOG-QUEUE-RESOURCE)                             
           END-EXEC.                                                            
      *                                                                         
           PERFORM 2517-1-WRITEQ-TD                                             
      *                                                                         
           EXEC CICS                                                            
               DEQ                                                              
                   RESOURCE (PC-LOG-QUEUE-RESOURCE)                             
           END-EXEC.                                                            
      *                                                                         
      *                                                                         
       2517-1-WRITEQ-TD.                                                        
           EXEC CICS                                                            
               WRITEQ TD                                                        
                   QUEUE (PC-LOG-QUEUE)                                         
                   FROM  (PV-MESSAGE-BUFFER)                                    
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
               CONTINUE                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   ABEND                                                        
                       ABCODE (EIBTRMID)                                        
               END-EXEC                                                         
           END-IF.                                                              
                                                                                
           EJECT                                                                
      ******************************************************************        
      **  ATTEMPT TO GET THE USER'S INITIAL MNEMONIC FROM THE TUSER   **        
      **  TABLE.  IF FOUND, ATTEMPT TO GET THE CORRESPONDING          **        
      **  TRANSACTION FROM THE MNEMONIC TABLE.  IF THE USER IS NOT    **        
      **  FOUND ON THE TUSER TABLE, USE THE MAIN MENU AS THE INITIAL  **        
      **  MNEMONIC.                                                   **        
      ******************************************************************        
       2518-GET-INITIAL-MNEMONIC.                                               
           INITIALIZE DCLTUSER.                                                 
           EXEC SQL                                                             
               SELECT INITIAL_MNMNC_CODE,                                       
                      DFLT_PRTR_ID                                              
                   INTO :USER-INITIAL-MNMNC-CODE,                               
                        :USER-DFLT-PRTR-ID                                      
                   FROM TUSER                                                   
                   WHERE USER_CODE = :CA-USERID                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
               WHEN SQLCODE = +100                                              
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN SQLWARN0 NOT = SPACES                                       
               WHEN SQLCODE < ZERO                                              
               WHEN SQLCODE > ZERO                                              
                   MOVE '2518-GET-INITIAL-MNEMONIC'                             
                                       TO DP013-PARAGRAPH                       
                   MOVE 'SELECT INITIAL MNEMONIC FOR USER'                      
                                       TO DP013-MESSAGE-TEXT (1)                
                   MOVE CA-USERID      TO DP013-MESSAGE-TEXT (2)                
                   MOVE SQLCA          TO DP013-SQLCA                           
                   MOVE 'TUSER  '      TO DP013-DB2-TABLE-NAME (1)              
                   SET DP013-DB2-ABEND TO TRUE                                  
                   SET DP013-LINK-RETURN TO TRUE                                
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
      *                                                                         
                                                                                
           EJECT                                                                
      ******************************************************************        
      **  READ THE MNEMONIC FILE TO GET THE TRAN ID FOR THE USER'S    **        
      **  INITIAL MNEMONIC.                                           **        
      ******************************************************************        
       2519-GET-INITIAL-TRAN-ID.                                                
           PERFORM 2541-READ-MNEMONIC.                                          
           IF  PS-MNEMONIC-NOT-FOUND                                            
               SET DP013-LOGIC-ABEND TO TRUE                                    
               SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               MOVE '2519-GET-INITIAL-TRAN-ID'                                  
                                     TO DP013-PARAGRAPH                         
               MOVE 'THE USER''S INITIAL MNEMONIC WAS NOT FOUND:'               
                                     TO DP013-MESSAGE-TEXT (1)                  
               MOVE CA-SRC-MNEMONIC  TO DP013-MESSAGE-TEXT (2)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           ELSE                                                                 
               MOVE DP503-TRAN-ID    TO CA-SRC-TRAN-ID                          
           END-IF.                                                              
                                                                                
           EJECT                                                                
      ******************************************************************        
      **  THIS PARAGRAPH DOES A CALL TO MAKE A 'RACF' SECURITY CHECK  **        
      **  TO MAKE SURE THE USER IS AUTHORIZED TO USE THE TRANSACTION  **        
      **  OR MENU NAME THAT CORRESPONDS TO THE USER'S INITIAL         **        
      **  MNEMONIC.                                                   **        
      **  -  IF SECURITY FAILS, CALL ABEND AND FORCE A LOGOFF.  THE   **        
      **     USER'S SECURITY OR INITIAL MNEMONIC IS WRONG.            **        
      ******************************************************************        
       2520-CHECK-INITIAL-SECURITY.                                             
           IF  CA-SRC-TRAN-ID = PC-MENU-TRANID                                  
               SET DP008-RSRC-TYPE-MENU         TO TRUE                         
               MOVE CA-SRC-MNEMONIC             TO DP008-RESOURCE-NAME          
           ELSE                                                                 
               SET DP008-RSRC-TYPE-TRAN         TO TRUE                         
               MOVE CA-SRC-TRAN-ID              TO DP008-RESOURCE-NAME          
           END-IF.                                                              
           SET DP008-ACCESS-TYPE-USE            TO TRUE.                        
           EXEC CICS LINK                                                       
                     PROGRAM  (DP008-SECURITY-SUBROUTINE)                       
                     COMMAREA (DP008-SECURITY-COMMAREA)                         
                     LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)                  
           END-EXEC.                                                            
           IF  DP008-CALL-SUCCESSFUL                                            
               IF  DP008-ACCESS-ALLOWED                                         
                   CONTINUE                                                     
               ELSE                                                             
                   SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   SET DP013-OTHER-ABEND         TO TRUE                        
                   MOVE '2520-CHECK-INITIAL-SECURITY'                           
                                                TO DP013-PARAGRAPH              
                   MOVE 'YOU DO NOT HAVE SECURITY CLEARANCE FOR FOR YOUR        
      -                 ' INITIAL TRANSACTION.' TO DP013-MESSAGE-TEXT(1)        
                   MOVE 'CONTACT YOUR SECURITY ADMINISTRATOR.'                  
                                                TO DP013-MESSAGE-TEXT(2)        
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           ELSE                                                                 
               SET DP013-XCTL-DISPLAY-LOGOFF    TO TRUE                         
               SET DP013-SECURITY-ABEND         TO TRUE                         
               MOVE '2520-CHECK-INITIAL-SECURITY'                               
                                                TO DP013-PARAGRAPH              
               MOVE DP008-SECURITY-COMMAREA     TO DP013-SECURITY-INFO          
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
           EJECT                                                                
      ******************************************************************        
      **  VERIFY THAT THE INITIAL TRANSACTION IS AVAILABLE            **        
      **  (ENABLED TO CICS), IF IT IS NOT, TRY GOING TO THE           **        
      **  CORPORATE MENU.                                             **        
      ******************************************************************        
       2530-CHECK-INITIAL-AVAILABLITY.                                          
           SET PS-TRAN-NOT-AVAILABLE        TO TRUE                             
           PERFORM 2531-TRAN-AVAILABILITY-CHECK                                 
           IF  PS-TRAN-NOT-AVAILABLE                                            
               MOVE PC-MENU-TRANID          TO CA-SRC-TRAN-ID                   
               MOVE PC-DEFAULT-MNEMONIC     TO CA-SRC-MNEMONIC                  
               SET PS-AVAILABILITY-ERROR    TO TRUE                             
               PERFORM 2531-TRAN-AVAILABILITY-CHECK                             
               IF  PS-TRAN-NOT-AVAILABLE                                        
                   SET DP013-OTHER-ABEND    TO TRUE                             
                   SET DP013-XCTL-DISPLAY-LOGOFF  TO TRUE                       
                   MOVE '2530-CHECK-INITIAL-AVAILABLITY'                        
                                            TO DP013-PARAGRAPH                  
                   MOVE 'INITIAL TRANSACTION AND MENU ARE UNAVAILABLE'          
                                            TO DP013-MESSAGE-TEXT (1)           
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
           IF  PS-AVAILABILITY-ERROR                                            
      *        -- INITIAL TRANSACTION IS UNAVAILABLE --                         
               SET DP020-MSG-WARNING        TO TRUE                             
               MOVE PC-TSYMSG-99900         TO DP020-MSG-NUMBER                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  DO AN INQUIRE ON THE TRAN ID WE INTEND TO GO TO THIS WILL   **        
      **  TELL US IF THE TRANSACTION IS ENABLED.                      **        
      ******************************************************************        
       2531-TRAN-AVAILABILITY-CHECK.                                            
           EXEC CICS INQUIRE                                                    
                TRANSACTION (CA-SRC-TRAN-ID)                                    
                STATUS      (PV-INQUIRE-CODE)                                   
                RESP        (PV-CICS-RESPONSE)                                  
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
               IF  PV-INQUIRE-CODE = DFHVALUE(ENABLED)                          
                   IF  CA-SRC-MNEMONIC NOT = DP503-MNEMONIC                     
                       PERFORM 2541-READ-MNEMONIC                               
                   END-IF                                                       
                   IF  PS-MNEMONIC-FOUND                                        
                       IF  DP503-TRAN-AVAILABLE                                 
                           SET PS-TRAN-AVAILABLE TO TRUE                        
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
           EJECT                                                                
      ******************************************************************        
      **- SET THE SOURCE AND COMMAREA LENGTH VARIABLES -- ONLY READ   **        
      **  THE MNEMONIC FILE IF IT HASN'T ALREADY BEEN READ.           **        
      **- USE THE MNEMONIC FILE TO FILL THE FK DISPLAY FOR THE NEW    **        
      **  TRANSACTION.                                                **        
      ******************************************************************        
       2540-RESET-SRC-VARIABLES.                                                
           IF  CA-SRC-MNEMONIC = DP503-MNEMONIC                                 
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2541-READ-MNEMONIC                                       
               IF  PS-MNEMONIC-NOT-FOUND                                        
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   MOVE '2540-RESET-SRC-VARIABLES'                              
                                         TO DP013-PARAGRAPH                     
                   MOVE 'THE FOLLOWING MNEMONIC WAS NOT FOUND'                  
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE CA-SRC-MNEMONIC TO DP013-MESSAGE-TEXT (2)               
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
           MOVE DP503-PANEL-TITLE      TO DP020-PANEL-TITLE.                    
           MOVE DP503-DB2-PKG-IND      TO DP020-SRC-DB2-PKG-IND.                
           MOVE DP503-SAVE-COMMAREA-SW TO DP020-SRC-SAVE-COMMAREA-SW.           
      *                                                                         
           MOVE PC-GLOBAL-COMMAREA-LENGTH                                       
                                       TO DP020-GLOBAL-COMMAREA-LENGTH.         
           MOVE DP503-APPL-COMMAREA-LENGTH                                      
                                       TO DP020-APPL-COMMAREA-LENGTH.           
           COMPUTE DP020-COMMAREA-LENGTH =                                      
                     DP020-GLOBAL-COMMAREA-LENGTH                               
                     + DP020-APPL-COMMAREA-LENGTH.                              
      *                                                                         
      *                                                                         
       2541-READ-MNEMONIC.                                                      
           EXEC CICS                                                            
               READ                                                             
                   FILE   ('DPMNEMIC')                                          
                   INTO   (DP503-MNEMONIC-RECORD)                               
                   RIDFLD (CA-SRC-MNEMONIC)                                     
                   RESP   (SM-CICS-RESPONSE)                                    
           END-EXEC.                                                            
      *                                                                         
           IF  SM-CICS-RESPONSE = DFHRESP (NORMAL)                              
               SET PS-MNEMONIC-FOUND TO TRUE                                    
           ELSE                                                                 
               IF  SM-CICS-RESPONSE = DFHRESP (NOTFND)                          
                   SET PS-MNEMONIC-NOT-FOUND TO TRUE                            
               ELSE                                                             
                   SET DP013-CICS-ABEND      TO TRUE                            
                   SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '2541-READ-MNEMONIC' TO DP013-PARAGRAPH                 
                   MOVE 'BAD STATUS RETURNED READING DPMNEMIC'                  
                                             TO DP013-MESSAGE-TEXT (1)          
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      **  MOVE THE FK DEFINITIONS FOR THE DESTINATION TRANSACTION     **        
      **  FROM THE MNEMONIC FILE TO THE COMMAREA.  IF THE FK IS       **        
      **  RELATED TO A TRANID, CHECK THE SECURITY ON THE TRANSACTION  **        
      **  BEFORE MOVING THE FK AREA INTO THE COMMAREA.                **        
      ******************************************************************        
       2550-LOAD-FKEY-COMMAREA.                                                 
           INITIALIZE DP020-FUNCTION-KEY-ARRAY.                                 
                                                                                
           PERFORM                                                              
               VARYING SM-FK-SUB                                                
               FROM 1 BY 1                                                      
               UNTIL SM-FK-SUB > SM-AID-MAX                                     
               IF  DP503-FK-TRAN-ID (SM-FK-SUB) = SPACE                         
                   MOVE DP503-FK-LABEL (SM-FK-SUB)                              
                                       TO DP020-FK-MNEMONIC (SM-FK-SUB)         
                   MOVE DP503-FK-ALT-LABEL-IND (SM-FK-SUB)                      
                                       TO DP020-FK-ALT-LABEL-IND                
                                                           (SM-FK-SUB)          
                   MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                            
                                       TO DP020-FK-TRAN-ID (SM-FK-SUB)          
                   MOVE DP503-FK-ACTION (SM-FK-SUB)                             
                                       TO DP020-FK-ACTION (SM-FK-SUB)           
                   MOVE DP503-FK-STACK-IND (SM-FK-SUB)                          
                                       TO DP020-FK-STACK-IND (SM-FK-SUB)        
                   MOVE DP503-FK-INT-APPL-FORMAT-IND (SM-FK-SUB)                
                            TO DP020-FK-INT-APPL-FORMAT-IND (SM-FK-SUB)         
               ELSE                                                             
                   SET DP008-ACCESS-TYPE-USE    TO TRUE                         
                   IF  DP503-FK-TRAN-ID (SM-FK-SUB) = PC-MENU-TRANID            
                       SET DP008-RSRC-TYPE-MENU TO TRUE                         
                       MOVE DP503-FK-MNEMONIC (SM-FK-SUB)                       
                                       TO DP008-RESOURCE-NAME                   
                   ELSE                                                         
                       SET DP008-RSRC-TYPE-TRAN TO TRUE                         
                       MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                        
                                       TO DP008-RESOURCE-NAME                   
                   END-IF                                                       
                   EXEC CICS                                                    
                       LINK PROGRAM  (DP008-SECURITY-SUBROUTINE)                
                            COMMAREA (DP008-SECURITY-COMMAREA)                  
                            LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)           
                   END-EXEC                                                     
                   IF  DP008-CALL-SUCCESSFUL                                    
                       IF  DP008-ACCESS-ALLOWED                                 
                           MOVE DP503-FK-LABEL (SM-FK-SUB)                      
                                       TO DP020-FK-MNEMONIC (SM-FK-SUB)         
                           MOVE DP503-FK-ALT-LABEL-IND (SM-FK-SUB)              
                                       TO DP020-FK-ALT-LABEL-IND                
                                                             (SM-FK-SUB)        
                           MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                    
                                       TO DP020-FK-TRAN-ID (SM-FK-SUB)          
                           MOVE DP503-FK-ACTION (SM-FK-SUB)                     
                                       TO DP020-FK-ACTION (SM-FK-SUB)           
                           MOVE DP503-FK-STACK-IND (SM-FK-SUB)                  
                                       TO DP020-FK-STACK-IND (SM-FK-SUB)        
                           MOVE DP503-FK-INT-APPL-FORMAT-IND                    
                                                             (SM-FK-SUB)        
                                       TO DP020-FK-INT-APPL-FORMAT-IND          
                                                             (SM-FK-SUB)        
                       ELSE                                                     
                           INITIALIZE DP020-FUNCTION-KEY (SM-FK-SUB)            
                       END-IF                                                   
                   ELSE                                                         
                       SET DP013-SECURITY-ABEND      TO TRUE                    
                       SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                    
                       MOVE '2550-LOAD-FKEY-COMMAREA' TO DP013-PARAGRAPH        
                       MOVE DP008-SECURITY-COMMAREA                             
                                                 TO DP013-SECURITY-INFO         
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
           EJECT                                                                
      ******************************************************************        
      ** THIS ROUTINE WILL MAKE SURE THERE IS NOTHING LEFT FROM A               
      ** PREVIOUS SESSION.  DELETE SAVED COMMAREAS AND TSQUEUES FOR             
      ** THIS TERMINAL.                                                         
      ******************************************************************        
       2600-CLEANUP-TERMID.                                                     
           PERFORM 2610-DELETE-SAVED-COMMAREAS.                                 
           PERFORM 2620-DELETE-TSQUEUES.                                        
           IF PV-DEL-RECS-TERM > 0                                              
           OR PV-DEL-TSQS-TERM > 0                                              
               MOVE PV-DEL-RECS-TERM   TO PV-MSG-DEL-RECS                       
               MOVE PV-DEL-TSQS-TERM   TO PV-MSG-DEL-TSQS                       
               MOVE CA-USERID          TO PV-USERID                             
               MOVE PV-CLEANUP-MESSAGE TO PV-MESSAGE-BUFFER                     
               PERFORM 2517-LOG-MESSAGE                                         
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** DELETE SAVED COMMAREAS FROM A PREVIOUS SESSION.                        
      ******************************************************************        
       2610-DELETE-SAVED-COMMAREAS.                                             
           EXEC CICS DELETE                                                     
                FILE      (PC-SAVE-COMM-DD)                                     
                RIDFLD    (CA-NETNAME)                                          
                KEYLENGTH (PC-NETNAME-LENGTH)                                   
                GENERIC                                                         
                NUMREC    (PV-DEL-RECS-TERM)                                    
                RESP      (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
                                                                                
           IF (PV-CICS-RESPONSE EQUAL DFHRESP(NORMAL)                           
           OR  PV-CICS-RESPONSE EQUAL DFHRESP(NOTFND))                          
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND  TO TRUE                                    
               SET DP013-LINK-RETURN TO TRUE                                    
               SET DP013-NO-ROLLBACK TO TRUE                                    
               MOVE '2610-DELETE-SAVED-COMMAREAS'                               
                                     TO DP013-PARAGRAPH                         
               MOVE 'BAD RETURN CODE FROM SAVECOMM VSAM DELETE'                 
                                     TO DP013-MESSAGE-TEXT (1)                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** BROWSE FOR TSQUEUES FROM A PREVIOUS SESSION THAT EXIST FOR THIS        
      ** TERMID AND DELETE THEM.                                                
      ******************************************************************        
       2620-DELETE-TSQUEUES.                                                    
           MOVE LOW-VALUES TO PV-TS-KEY.                                        
           MOVE CA-TERM-ID TO PV-TS-KEY-TERM-ID.                                
           PERFORM 2621-TSQUEUE-BROWSE-START.                                   
                                                                                
           MOVE 0 TO PV-DEL-TSQS-TERM.                                          
           PERFORM 2622-TSQUEUE-BROWSE-NEXT.                                    
           PERFORM  UNTIL PV-TS-KEY-TERM-ID NOT = CA-TERM-ID                    
                       OR PV-CICS-RESPONSE      = DFHRESP(END)                  
               PERFORM 2624-DELETE-TSQUEUE                                      
               PERFORM 2622-TSQUEUE-BROWSE-NEXT                                 
           END-PERFORM.                                                         
           PERFORM 2623-TSQUEUE-BROWSE-END.                                     
                                                                                
      ******************************************************************        
      ** START BROWSING THE TSQUEUE NAMES.                            **        
      ******************************************************************        
       2621-TSQUEUE-BROWSE-START.                                               
           EXEC CICS                                                            
               INQUIRE TSQNAME                                                  
                   START AT (PV-TS-KEY)                                         
                   RESP     (PV-CICS-RESPONSE)                                  
           END-EXEC                                                             
           EVALUATE PV-CICS-RESPONSE                                            
               WHEN DFHRESP(NORMAL)                                             
               WHEN DFHRESP(END)                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET DP013-CICS-ABEND                                         
                       DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '2621-TSQUEUE-BROWSE-START' TO DP013-PARAGRAPH          
                   STRING 'ATTEMPTING TO START BROWSE ON TSQUEUE '              
                          PV-TS-KEY               DELIMITED BY SIZE             
                      INTO DP013-MESSAGE-TEXT (1)                               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** BROWSE FOR THE NEXT TSQUEUE NAME.                            **        
      ******************************************************************        
       2622-TSQUEUE-BROWSE-NEXT.                                                
           EXEC CICS                                                            
               INQUIRE TSQNAME (PV-TS-KEY)                                      
                   NEXT                                                         
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC                                                             
           EVALUATE PV-CICS-RESPONSE                                            
               WHEN DFHRESP(NORMAL)                                             
               WHEN DFHRESP(END)                                                
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   SET DP013-CICS-ABEND                                         
                       DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '2622-TSQUEUE-BROWSE-NEXT' TO DP013-PARAGRAPH           
                   STRING 'ATTEMPTING TO BROWSE NEXT ON TSQUEUE '               
                          PV-TS-KEY               DELIMITED BY SIZE             
                      INTO DP013-MESSAGE-TEXT (1)                               
                   PERFORM DP013-0000-PROCESS-ABEND                             
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      ** STOP BROWSING THE TSQUEUE NAMES.                             **        
      ******************************************************************        
       2623-TSQUEUE-BROWSE-END.                                                 
           EXEC CICS                                                            
               INQUIRE TSQNAME                                                  
                   END                                                          
                   RESP (PV-CICS-RESPONSE)                                      
           END-EXEC                                                             
           IF PV-CICS-RESPONSE NOT = DFHRESP(NORMAL)                            
               SET DP013-CICS-ABEND                                             
                   DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               MOVE '2623-TSQUEUE-BROWSE-END' TO DP013-PARAGRAPH                
               MOVE 'ATTEMPTING TO END BROWSE ON TSQUEUES'                      
                 TO DP013-MESSAGE-TEXT (1)                                      
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      ** DELETE THE TSQUEUE NAME.                                     **        
      ******************************************************************        
       2624-DELETE-TSQUEUE.                                                     
           EXEC CICS                                                            
               DELETEQ                                                          
                   TS QNAME (PV-TS-KEY)                                         
                   RESP     (PV-CICS-RESPONSE)                                  
           END-EXEC.                                                            
           IF PV-CICS-RESPONSE = DFHRESP(NORMAL)                                
               ADD 1 TO PV-DEL-TSQS-TERM                                        
           ELSE                                                                 
               SET DP013-CICS-ABEND                                             
                   DP013-XCTL-DISPLAY-LOGOFF TO TRUE                            
               MOVE '2624-DELETE-TSQUEUE'    TO DP013-PARAGRAPH                 
               STRING 'ATTEMPTING TO DELETEQ ON TSQUEUE '                       
                      PV-TS-KEY               DELIMITED BY SIZE                 
                  INTO DP013-MESSAGE-TEXT (1)                                   
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
                                                                                
           EJECT                                                                
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       3000-CREATE-INFO-TSQ.                                                    
           MOVE EIBTRMID                TO TS-INFO-QUEUE-TERMINAL.              
           MOVE CA-NETNAME              TO DP026-NETNAME.                       
           MOVE CA-LOCATION-CHAR (3:3)  TO DP026-LOCATION.                      
           MOVE CA-LOCATION-NBR         TO DP026-LOCATION-NBR.                  
           MOVE CA-FCLTY-ID             TO DP026-FCLTY-ID.                      
           MOVE CA-LOCATION-TYPE        TO DP026-LOCATION-TYPE.                 
           MOVE CA-USERID               TO DP026-USERID.                        
      *    MOVE CA-OPERATING-COMPANY    TO DP026-OPERATING-COMPANY.             
           MOVE CA-DFLT-PRTR-ID         TO DP026-DFLT-PRTR-ID.                  
      * WE DON'T NEED TO DO THE DELETE SINCE 2600- SHOULD HAVE DONE IT.         
      **** PERFORM 3200-DELETE-INFO-TSQ.                                        
           MOVE +1                      TO PV-TS-ITEM.                          
           PERFORM 3100-WRITE-INFO-TSQ.                                         
      ******************************************************************        
      ** CREATE THE 'INFO' TEMP STORAGE QUEUE.  THIS QUEUE WILL BE    **        
      ** USED BY NON-ARCHITECTURE PROGRAMS TO GET SIGN-ON INFORMATION.**        
      ******************************************************************        
       3100-WRITE-INFO-TSQ.                                                     
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE   (TS-INFO-QUEUE-NAME)                                 
                   ITEM    (PV-TS-ITEM)                                         
                   FROM    (DP026-SIGN-ON-INFO)                                 
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '3100-WRITE-INFO-TS-QUEUE'                                  
                                TO DP013-PARAGRAPH                              
               MOVE 'WRITEQ TS -- FAILED, QUEUE ID FOLLOWS'                     
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-INFO-QUEUE-NAME                                          
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       3200-DELETE-INFO-TSQ.                                                    
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE   (TS-INFO-QUEUE-NAME)                                 
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF   (PV-CICS-RESPONSE = DFHRESP (NORMAL))                           
             OR (PV-CICS-RESPONSE = DFHRESP (QIDERR))                           
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5220-DELETE-TS-QUEUE'                                      
                                TO DP013-PARAGRAPH                              
               MOVE 'DELETEQ TS -- FAILED, QUEUE ID FOLLOWS'                    
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-INFO-QUEUE-NAME                                          
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **  START THE APPROPRIATE TRANID.  IF LEAVING THE ARCHITECTURE, **        
      **  SAVE THE CURRENT COMMAREA TO TEMP STORAGE AND START THE     **        
      **  TRANSACTION WITHOUT A COMMAREA.                             **        
      ******************************************************************        
       5000-TRANSFER-CONTROL.                                                   
           IF  DP503-COMMAREA-STANDARD                                          
               PERFORM 5100-START-DEST-TRANID                                   
           ELSE                                                                 
               SET DP020-NEXT-ACT-RETURN TO TRUE                                
               MOVE EIBTRMID      TO TS-SCA-QUEUE-TERMINAL                      
               MOVE 'SCA'         TO TS-SCA-QUEUE-LITERAL                       
               MOVE +1            TO PV-TS-ITEM                                 
               PERFORM 5200-SAVE-COMMAREA-TO-TS                                 
               PERFORM 5300-START-TRANID-NO-COMM                                
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5100-START-DEST-TRANID.                                                  
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID  (DP020-SRC-TRAN-ID)                                 
                   COMMAREA (DP020-STANDARD-COMMAREA)                           
                   LENGTH   (DP020-COMMAREA-LENGTH)                             
                   RESP     (PV-CICS-RESPONSE)                                  
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5100-START-DEST-TRANID'                                    
                                      TO DP013-PARAGRAPH                        
               MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT LINE'        
                                      TO DP013-MESSAGE-TEXT (1)                 
               MOVE DP020-SRC-TRAN-ID TO DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **  SAVE THE STANDARD COMMAREA TO TEMP STORAGE.  IF, AFTER      **        
      **  WRITING THE ITEM NUMBER IS NOT 1, THE TS QUEUE HAD NOT BEEN **        
      **  CLEANED UP FROM A PREVIOUS SESSION, SO DELETE THE QUEUE     **        
      **  AND WRITE IT AGAIN.                                         **        
      ******************************************************************        
       5200-SAVE-COMMAREA-TO-TS.                                                
           PERFORM 5210-WRITE-TS-QUEUE.                                         
           IF  PV-TS-ITEM = PC-FIRST-ITEM                                       
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-DELETE-TS-QUEUE                                     
               PERFORM 5210-WRITE-TS-QUEUE                                      
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5210-WRITE-TS-QUEUE.                                                     
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE   (TS-SCA-QUEUE-NAME)                                  
                   ITEM    (PV-TS-ITEM)                                         
                   FROM    (DP020-STANDARD-COMMAREA)                            
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5210-WRITE-TS-QUEUE'                                       
                                TO DP013-PARAGRAPH                              
               MOVE 'WRITEQ TS -- FAILED, QUEUE ID FOLLOWS'                     
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-SCA-QUEUE-NAME                                           
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5220-DELETE-TS-QUEUE.                                                    
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE   (TS-SCA-QUEUE-NAME)                                  
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5220-DELETE-TS-QUEUE'                                      
                                TO DP013-PARAGRAPH                              
               MOVE 'DELETEQ TS -- FAILED, QUEUE ID FOLLOWS'                    
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-SCA-QUEUE-NAME                                           
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5300-START-TRANID-NO-COMM.                                               
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID (DP020-SRC-TRAN-ID)                                  
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5300-START-TRANID-NO-COMM'                                 
                                      TO DP013-PARAGRAPH                        
               MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT LINE'        
                                      TO DP013-MESSAGE-TEXT (1)                 
               MOVE DP020-SRC-TRAN-ID TO DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
           EJECT                                                                
      ******************************************************************        
      **  CALL THE ABEND ROUTINE.                                     **        
      ******************************************************************        
           COPY DPPD013.                                                        
                                                                                
       9000-BLUEZONE-IP-EXIT.                                                   
           CALL 'TN32READ' USING DFHEIBLK                                       
                                 DFHCOMMAREA                                    
                                 PV-BLUEZONE-IP-CAPTURE.                        
