/* REXX */                                                                      
/* RMKRX016 - Prepare Infopac recipient add transactions for manually */        
/*      added global recipients.  Match/merge request records with    */        
/*      extracts from the Infopac distribution file recipient records.*/        
/*      Create a recipient add record when the requested recipient    */        
/*      does not already have a recipient record on file.  If the     */        
/*      recipient is already on file, display detail information to   */        
/*      sysout.                                                       */        
/**********************************************************************/        
ARG IPACRGN                                                                     
INFOPAC_REGION = STRIP(IPACRGN)                                                 
UPPER INFOPAC_REGION                                                            
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' RMKRX016 - '                                                        
CURRENT_PROGRAM_NAME = 'RMKRX016'                                               
REQ_FILE_END_OF_FILE = 'Y'                                                      
REQ_FILE_NOT_END_OF_FILE = 'N'                                                  
RECIP_FILE_END_OF_FILE = 'Y'                                                    
RECIP_FILE_NOT_END_OF_FILE = 'N'                                                
EOF_RECIPIENT_ID = '9999999999'                                                 
ADD_TRANSACTION = 'A'                                                           
TRAN_RECIP_HEADER = '##RECIPIENT   '                                            
TRAN_RECIP_CONTINUATION = '              '                                      
DEFAULT_PRINT_JCL = 'ARCHPRNT  '                                                
INFOPAC_SECURED_REGION = 'IPACS'                                                
GLOBAL_ACCESS = 'G'                                                             
SECURED_REGION_REQUESTED = 'Y'                                                  
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
REQ_FILE_EOF = REQ_FILE_NOT_END_OF_FILE                                         
RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE                                     
TRAN_RECIPIENT_ID = '          '                                                
RECIP_RECIPIENT_ID = '          '                                               
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
SAY CURRPGM 'INFOPAC REGION =' INFOPAC_REGION                                   
CALL READ_REQ_RECORD                                                            
CALL READ_RECIP_RECORD                                                          
DO WHILE ((REQ_FILE_EOF = REQ_FILE_NOT_END_OF_FILE) |,                          
        (RECIP_FILE_EOF = RECIP_FILE_NOT_END_OF_FILE))                          
    SELECT                                                                      
        WHEN (REQ_RECIPIENT_ID < RECIP_RECIPIENT_ID) THEN DO                    
            IF ((INFOPAC_REGION ^= INFOPAC_SECURED_REGION) |,                   
                    (REQ_SECURED_REGION =,                                      
                                     SECURED_REGION_REQUESTED)) THEN DO         
                TRANSACTION_TYPE = ADD_TRANSACTION                              
                TRAN_RECIPIENT_ID = REQ_RECIPIENT_ID                            
                TRAN_RECIPIENT_NAME = REQ_RECIPIENT_NAME                        
                TRAN_RECIPIENT_ADDR_LINE_1 = ,                                  
                          REQ_RECIPIENT_ADDR_LINE_1                             
                TRAN_RECIPIENT_ID_SEQUENCE = REQ_RECIPIENT_ID_SEQUENCE          
                TRAN_RECIPIENT_TITLE = REQ_RECIPIENT_TITLE                      
                TRAN_RECIPIENT_PRINTER_ID = REQ_RECIPIENT_PRINTER_ID            
                IF (STRIP(TRAN_JCL_ID) ^= '') THEN                              
                    TRAN_JCL_ID = REQ_JCL_ID                                    
                ELSE                                                            
                    TRAN_JCL_ID = DEFAULT_PRINT_JCL                             
                TRAN_ACCESS_CODE = GLOBAL_ACCESS                                
                CALL CREATE_RECIP_TRANS                                         
            END                                                                 
            CALL READ_REQ_RECORD                                                
        END                                                                     
        WHEN (REQ_RECIPIENT_ID = RECIP_RECIPIENT_ID) THEN DO                    
            SAY CURRPGM '- REQUESTED RECIPIENT ADD IS ALREADY ON FILE'          
            SAY CURRPGM '-   RECIPIENT ID =' REQ_RECIPIENT_ID                   
            SAY CURRPGM '-   RECIPIENT NAME =' REQ_RECIPIENT_NAME               
            SAY CURRPGM '-   RECIPIENT TITLE =' REQ_RECIPIENT_TITLE             
            CALL READ_REQ_RECORD                                                
            CALL READ_RECIP_RECORD                                              
        END                                                                     
        OTHERWISE CALL READ_RECIP_RECORD                                        
    END                                                                         
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_REQ_RECORD                                                   */         
/*                                                                   */         
/* Read a recipient add request transaction record.                  */         
/* ----------------------------------------------------------------- */         
READ_REQ_RECORD:                                                                
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        REQ_FILE_EOF = REQ_FILE_END_OF_FILE                                     
        REQ_RECIPIENT_ID = EOF_RECIPIENT_ID                                     
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP01_RECORD                                                 
        REQ_RECIPIENT_ID = LEFT(SUBSTR(INP01_RECORD,1,8),10)                    
        REQ_RECIPIENT_NAME = LEFT(SUBSTR(INP01_RECORD,9,30),35)                 
        REQ_RECIPIENT_TITLE = SUBSTR(INP01_RECORD,39,35)                        
        REQ_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP01_RECORD,74,35)                  
        REQ_RECIPIENT_ID_SEQUENCE = SUBSTR(INP01_RECORD,109,4)                  
        REQ_RECIPIENT_PRINTER_ID = SUBSTR(INP01_RECORD,113,8)                   
        REQ_JCL_ID = SUBSTR(INP01_RECORD,121,10)                                
        REQ_SECURED_REGION = SUBSTR(INP01_RECORD,132,1)                         
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_RECIP_RECORD                                                 */         
/*                                                                   */         
/* Read an Infopac recipient extract record.                         */         
/* ----------------------------------------------------------------- */         
READ_RECIP_RECORD:                                                              
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        RECIP_FILE_EOF = RECIP_FILE_END_OF_FILE                                 
        RECIP_RECIPIENT_ID = EOF_RECIPIENT_ID                                   
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP02_RECORD                                                 
        RECIP_RECIPIENT_ID = SUBSTR(INP02_RECORD,1,10)                          
        RECIP_RECIPIENT_NAME = SUBSTR(INP02_RECORD,11,35)                       
        RECIP_RECIPIENT_TITLE = SUBSTR(INP02_RECORD,46,35)                      
        RECIP_RECIPIENT_ADDR_LINE_1 = SUBSTR(INP02_RECORD,81,35)                
        RECIP_RECIPIENT_ID_SEQUENCE = SUBSTR(INP02_RECORD,116,4)                
        RECIP_JCL_ID = SUBSTR(INP02_RECORD,130,10)                              
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* CREATE_RECIP_TRANS                                                */         
/*                                                                   */         
/* Create the records to support a single recipient transaction      */         
/* ----------------------------------------------------------------- */         
CREATE_RECIP_TRANS:                                                             
    INFOPAC_TRAN_RECORD = TRAN_RECIP_HEADER ||,                                 
              '01 ' || TRANSACTION_TYPE || ' ' ||,                              
              TRAN_RECIPIENT_ID || ' ' || TRAN_RECIPIENT_NAME ||,               
              ' ' || TRAN_RECIPIENT_ID_SEQUENCE                                 
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '02 ' || TRAN_RECIPIENT_TITLE || ' ' ||,                          
              TRAN_RECIPIENT_PRINTER_ID                                         
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '03 ' || TRAN_RECIPIENT_ADDR_LINE_1                               
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '07 N           ' || TRAN_ACCESS_CODE ||,                         
              '                       ' || TRAN_JCL_ID ||,                      
              '          Y'                                                     
    CALL WRITE_RECIP_TRAN_RECORD                                                
    INFOPAC_TRAN_RECORD = TRAN_RECIP_CONTINUATION ||,                           
              '08 ' || TRAN_RECIPIENT_PRINTER_ID                                
    CALL WRITE_RECIP_TRAN_RECORD                                                
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_RECIP_TRAN_RECORD                                           */         
/*                                                                   */         
/* Write an Infopac recipient add/change transaction record.         */         
/* ----------------------------------------------------------------- */         
WRITE_RECIP_TRAN_RECORD:                                                        
    OUT01_RECORD = LEFT(INFOPAC_TRAN_RECORD,80)                                 
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
