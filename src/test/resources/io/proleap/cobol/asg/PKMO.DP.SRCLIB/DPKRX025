/* REXX */                                                                      
/* DPKRX025 - Save employee history that has rolled off the extract   */        
/*      file.                                                         */        
/**********************************************************************/        
/* Set values for constants.                                          */        
/**********************************************************************/        
CURRPGM = ' DPKRX025 - '                                                        
CURRENT_PROGRAM_NAME = 'DPKRX025'                                               
LAST_FILE_END_OF_FILE = 'Y'                                                     
LAST_FILE_NOT_END_OF_FILE = 'N'                                                 
CURR_FILE_END_OF_FILE = 'Y'                                                     
CURR_FILE_NOT_END_OF_FILE = 'N'                                                 
EOF_SSN = '9999999999'                                                          
                                                                                
/**********************************************************************/        
/* Initialize local variables.                                        */        
/**********************************************************************/        
LAST_FILE_EOF = LAST_FILE_NOT_END_OF_FILE                                       
CURR_FILE_EOF = CURR_FILE_NOT_END_OF_FILE                                       
LAST_SSN = '          '                                                         
CURR_SSN = '          '                                                         
                                                                                
/**********************************************************************/        
/* Program mainline.                                                  */        
/**********************************************************************/        
CALL READ_LAST_EMPLOYEE_RECORD                                                  
CALL READ_CURR_EMPLOYEE_RECORD                                                  
DO WHILE ((LAST_FILE_EOF = LAST_FILE_NOT_END_OF_FILE) |,                        
        (CURR_FILE_EOF = CURR_FILE_NOT_END_OF_FILE))                            
    SELECT                                                                      
        WHEN (LAST_SSN < CURR_SSN) THEN DO                                      
            CALL WRITE_EMPLOYEE_HISTORY_RECORD                                  
            CALL READ_LAST_EMPLOYEE_RECORD                                      
        END                                                                     
        WHEN (LAST_SSN = CURR_SSN) THEN DO                                      
            CALL READ_LAST_EMPLOYEE_RECORD                                      
            CALL READ_CURR_EMPLOYEE_RECORD                                      
        END                                                                     
        OTHERWISE CALL READ_CURR_EMPLOYEE_RECORD                                
    END                                                                         
END                                                                             
                                                                                
"EXECIO 0 DISKR INP01 (FINIS"                                                   
"EXECIO 0 DISKR INP02 (FINIS"                                                   
"EXECIO 0 DISKR OUT01 (FINIS"                                                   
EXIT 0                                                                          
/* ----------------------------------------------------------------- */         
/* Subroutines                                                       */         
/* ----------------------------------------------------------------- */         
/* ----------------------------------------------------------------- */         
/* READ_LAST_EMPLOYEE_RECORD                                         */         
/*                                                                   */         
/* Read a record from the last cycle employee file.                  */         
/* ----------------------------------------------------------------- */         
READ_LAST_EMPLOYEE_RECORD:                                                      
    "EXECIO 1 DISKR INP01"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        LAST_FILE_EOF = LAST_FILE_END_OF_FILE                                   
        LAST_SSN = EOF_SSN                                                      
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP01_RECORD                                                 
        LAST_SSN = SUBSTR(INP01_RECORD,32,10)                                   
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* READ_CURR_EMPLOYEE_RECORD                                         */         
/*                                                                   */         
/* Read a record from the current cycle employee file.               */         
/* ----------------------------------------------------------------- */         
READ_CURR_EMPLOYEE_RECORD:                                                      
    "EXECIO 1 DISKR INP02"              /* read one record           */         
    IF RC = 2 THEN DO                                                           
        CURR_FILE_EOF = CURR_FILE_END_OF_FILE                                   
        CURR_SSN = EOF_SSN                                                      
    END                                                                         
    ELSE DO                                                                     
        PARSE PULL INP02_RECORD                                                 
        CURR_SSN = SUBSTR(INP02_RECORD,32,10)                                   
    END                                                                         
    RETURN                                                                      
/* ----------------------------------------------------------------- */         
/* WRITE_EMPLOYEE_HISTORY_RECORD                                     */         
/*                                                                   */         
/* Write an employee history record.                                 */         
/* ----------------------------------------------------------------- */         
WRITE_EMPLOYEE_HISTORY_RECORD:                                                  
    OUT01_RECORD = LEFT(INP01_RECORD,150)                                       
    PUSH OUT01_RECORD                                                           
    "EXECIO 1 DISKW OUT01"                                                      
    RETURN                                                                      
