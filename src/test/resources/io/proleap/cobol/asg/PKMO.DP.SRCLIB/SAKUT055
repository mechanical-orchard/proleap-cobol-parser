000100 IDENTIFICATION DIVISION.                                                 
000200                                                                          
000300 PROGRAM-ID.      SAKUT055.                                               
000400 AUTHOR.          LUKE SCHULTE.                                           
000500 INSTALLATION.    KOHLS DEPARTMENT STORES.                                
000600 DATE-WRITTEN.    02/04/2003.                                             
000700 DATE-COMPILED.                                                           
000800                                                                          
000900******************************************************************        
001000*  MODULE TYPE:   DB2 COBOL2 BATCH.                                       
001100*  PROJECT:       WR24707 - CORPORATE DATA WAREHOUSE                      
001200*                           COSA EXTRACT                                  
001300*                                                                         
001400*  DESCRIPTION:   EXTRACT DATA FROM COSA TABLES INTO                      
001500*                 SEVERAL DIFFERENT FILES FOR USE IN                      
001600*                 CORPORATE DATA WAREHOUSE.                               
001700*                                                                         
001800*                 FILE    DESCRIPTION           COPYBOOK                  
001900*                 -----   -------------------   ---------                 
002000*                 DB2     TEPATR  (READ)                                  
002100*                 DB2     TEPCHK  (READ)                                  
002100*                 DB2     TEPCMD  (READ)                                  
002100*                 DB2     TEPCRD  (READ)                                  
002200*                 DB2     TEPECX  (READ)                                  
002300*                 DB2     TEPEID  (READ)                                  
002600*                 DB2     TEPITM  (READ)                                  
002800*                 DB2     TEPORD  (READ)                                  
003300*                 DB2     TEPPART (READ)                                  
002900*                 DB2     TEPPAY  (READ)                                  
002900*                 DB2     TEPSVC  (READ)                                  
003000*                 DB2     TEPTLD  (READ)                                  
003100*                 DB2     TEPTND  (READ)                                  
N2I201*                 DB2     EPT_CR_MKTG_LID  (READ)                         
SP0901*                 DB2     EPT_CR_CRD_VRFN  (READ)                         
003110*                                                                         
003400*                 INP01   POS DATE CARD         HARDCODED                 
003400*                 INP02   STR PARM CARD         HARDCODED                 
C10091*                 INP03   TRANSACTIONS EXTRACT  SARD041                   
003400*                                                                         
004000*                 OUT01   ATR                   SARD020                   
003500*                 OUT02   CMD                   SARD021                   
003600*                 OUT03   EID                   SARD022                   
003800*                 OUT04   HDR                   SARD023                   
003700*                 OUT05   ITM                   SARD024                   
004220*                 OUT06   ORD                   SARD025                   
004210*                 OUT07   PAY                   SARD026                   
004200*                 OUT08   TND                   SARD027                   
004000*                 OUT09   TRN                   SARD028                   
N2I201*                 OUT11   TLD                   SARD030                   
N2I201*                 OUT12   LID                   SARD031                   
N2I201*                 OUT13   COUPON LID            SARD032                   
N2I201*                                                                         
004500*************************** CHANGE LOG ***************************        
004600* AUTH     BY       DATE      DESCRIPTION                                 
004700* -------  -------  --------  ------------------------------------        
004800* WR24707  TKMA543  02-04-03  CREATED.                                    
004800* WR29309  TKMAA69  09-26-05  KOHL DEBIT CARD.                            
004800* WR29385  TKMAA69  09-26-05  REMOVE EXTRACTS LID,TLD,TTD,SHP,IPS.        
SN0925******************************************************************        
SN0925*  CHANGED BY: STEVEN NOWAK            DATE: 09/25/2007                   
SN0925*                                      WR/IR PM00053316                   
SN0925*                                                                         
SN0925*  REMOVE CODE RELATED TO KOHLS DEBIT SINCE NO LONGER EXITS               
N2I201******************************************************************        
N2I201*  CHANGED BY: THOMAS HANSON           DATE: 02/01/2008                   
N2I201*                                      WR/IR WR33714                      
N2I201*  OFFERS CHANGES                                                         
N2I201*  ADD CODE FOR EXTRACTS TLD, LID, CMD, COUPON LID.                       
N2I201*  REMOVE CODE FOR THE IRT EXTRACT (COMMENTED OUT).                       
N2I201******************************************************************        
N2I210* DATE: 02/25/2008                                                        
N2I210* WHO: DAN PAYNTER  (REFER TO 'N2I210' FOR ALL CHANGES)                   
N2I210* WHY: ENHANCEMENT TO ALLOW FOR EMAIL CAPTURE                             
N2I201******************************************************************        
SP0906* DATE: 10/01/2008  WR/IR: WR34398                                        
SP0906* WHO: THOMAS HANSON (REFER TO 'SP0906' FOR ALL CHANGES)                  
SP0906* WHY: PULL EXTRA OFFER FIELDS FOR CMD AND CPN EXTRACTS.                  
SP0906******************************************************************        
SP0901* DATE: 10/29/2008  WR/IR: WR34398                                        
SP0901* WHO: THOMAS HANSON (REFER TO 'SP0901' FOR ALL CHANGES)                  
SP0901* WHY: AVS RESPONSE CODE WILL NO LONGER BE ON THE TEPCRD TABLE.           
SP0901*      ADDED A SELECT TO PULL THIS FIELD FROM EPT_CR_CRD_VRFN.            
SP0906******************************************************************        
090331* DATE: 03/31/2009  WR/IR: INC000000755913                                
090331* WHO: THOMAS HANSON (REFER TO '090331' FOR THE 1 LINE CHANGE)            
090331* WHY: WAS NOT SUCCESSFULLY WRITING OUT THE TEPCRD VALUES FROM            
090331*      PARAGRAPH D4100. SET THE TABLE FLAG TO TEPCRD IN S7450.            
090331******************************************************************        
FL0902* DATE: 04/07/2009  WR/IR: WR36454                                        
FL0902* WHO: JIM HUNTER    (REFER TO 'FL0902' FOR ALL CHANGES)                  
FL0902* WHY: UPDATING THE ITEM EXTRACT (COPYBOOK SARD024) TO HANDLE             
FL0902*      THE MDSE ID BEING EXPANDED FROM 15 TO 30 DIGITS.                   
PCIEDW******************************************************************        
PCIEDW* DATE: 09/21/2009  WR/IR: WR37364                                        
PCIEDW* WHO: THOMAS HANSON (REFER TO 'PCIEDW' FOR ALL CHANGES)                  
PCIEDW* WHY: CHANGE VISA DEBIT AND MC DEBIT TO PASS VISA OR MC TENDER           
PCIEDW*      TYPE CODE, ALL OTHER DEBIT TENDERS SHOULD STILL PASS '19'.         
PCIEDW*      THIS LIKELY APPLIES TO ONLY THE TND FILE.                          
PCIEDW*      PROGRAM WAS RENAMED FROM MKKUT027.                                 
072711******************************************************************        
072711* DATE: 07/27/2011  WR/IR: WR39649                                        
072711* WHO: JACKIE HELING                                                      
072711* WHY: REMOVE HARDCODING FOR LOCATION 873                                 
072711******************************************************************        
C10091* DATE: 08/16/2011  WR/IR: CR10091                                        
C10091* WHO: COGNIZANT                                                          
C10091* WHY: PERFORMANCE IMPROVEMENT. TRANSACTION CURSORS HAVE BEEN             
C10091*      REMOVED FROM THE PROGRAM AND ARE UNLOADED TO FILES BEFORE          
C10091*      THE PROGRAM IS EXECUTED. SAKUT055 RUNS 4 TIMES IN                  
C10091*      PARALLEL FOR OPTIMIZING PERFORMANCE.                               
C10091******************************************************************        
P10237*****************************************************************         
P10237* DATE: 03/27/2015  WR/IR: CHG0104429                           *         
P10237* WHO: COGNIZANT                                                *         
P10237* WHY: CHANGES WERE MADE TO MOVE SPACES TO KC ACCOUNT NUMBER,   *         
P10237*      TENDER-ID, DRIVER LICENSE NUMBER AND EXPIRATION DATE IN  *         
P10237*      PAYMENT EXTRACT AND TENDER EXTRACT FILES. THESE FIELDS   *         
P10237*      EXTRACT ARE ALSO REMOVED FROM SQL QUERY. CHANGES ARE MADE*         
P10237*      AS PART OF COSA DATA SECURITY PROJECT.                   *         
P10237*****************************************************************         
C25054* DATE: 08/24/2015  WR/IR: CHG0125054                           *         
C25054* WHO: COGNIZANT                                                *         
C25054* WHY: CHANGES WERE MADE TO REMOVE THE CREATION OF ATR, CMD, EID*         
C25054*      HDR, ORD, PAY, TND, LID AND CPN OUTPUT FILES AS THESE    *         
C25054*      FILES ARE NOT NEEDED FOR CORPORATE DATA WAREHOUSE (CDW)  *         
C25054*      AND ONLY ITM AND TRN OUTPUT FILE ARE RETAINED FOR CDW.   *         
C25054*      THE DD NAMES OF OUTPUT FILES ARE RE-NUMBERED AS PER PROC *         
C25054*      CHANGES. CHANGES WERE MADE AS PART OF COSA DATA SECURITY *         
C25054*      PROJECT - EMPLOYEE DISCOUNT FOR RELEASE 2.               *         
C25054*****************************************************************         
005000                                                                          
005100 ENVIRONMENT DIVISION.                                                    
005200 CONFIGURATION SECTION.                                                   
005300                                                                          
005400 SOURCE-COMPUTER.        IBM-3090.                                        
005500 OBJECT-COMPUTER.        IBM-3090.                                        
005600                                                                          
005700 INPUT-OUTPUT SECTION.                                                    
005800 FILE-CONTROL.                                                            
005900                                                                          
006000     SELECT POS-CNTL-CARD  ASSIGN TO INP01.                               
006000     SELECT STR-PARM-FILE  ASSIGN TO INP02.                               
C10091     SELECT TEP-TRANS-FILE ASSIGN TO INP03.                               
C25054     SELECT CW-EXTRACT-ITM ASSIGN TO OUT01.                               
C25054     SELECT CW-EXTRACT-TRN ASSIGN TO OUT02.                               
006900                                                                          
007000 DATA DIVISION.                                                           
007100 FILE SECTION.                                                            
007200                                                                          
007300 FD  POS-CNTL-CARD                                                        
007400     RECORDING MODE IS F                                                  
007500     BLOCK CONTAINS 0 RECORDS                                             
007600     LABEL RECORDS ARE STANDARD.                                          
007700 01  POS-CNTL-CARD-REC               PIC X(80).                           
007800                                                                          
007300 FD  STR-PARM-FILE                                                        
007400     RECORDING MODE IS F                                                  
007500     BLOCK CONTAINS 0 RECORDS                                             
007600     LABEL RECORDS ARE STANDARD.                                          
007700 01  STR-PARM-REC                    PIC X(80).                           
                                                                                
C10091 FD  TEP-TRANS-FILE                                                       
C10091     RECORDING MODE IS F                                                  
C10091     BLOCK CONTAINS 0 RECORDS                                             
C10091     LABEL RECORDS ARE STANDARD.                                          
C10091 01  TRANS-REC                       PIC X(143).                          
                                                                                
010300 FD  CW-EXTRACT-ITM                                                       
010400     RECORDING MODE IS F                                                  
010500     LABEL RECORDS ARE STANDARD                                           
010600     BLOCK CONTAINS 0 CHARACTERS.                                         
FL0902 01  CW-EXTRACT-ITM-REC              PIC X(294).                          
                                                                                
007900 FD  CW-EXTRACT-TRN                                                       
008000     RECORDING MODE IS F                                                  
008100     LABEL RECORDS ARE STANDARD                                           
008200     BLOCK CONTAINS 0 CHARACTERS.                                         
N2I210 01  CW-EXTRACT-TRN-REC              PIC X(200).                          
012660                                                                          
012700******************************************************************        
012800*                       WORKING STORAGE                                   
012900******************************************************************        
013000 WORKING-STORAGE SECTION.                                                 
013100                                                                          
013200 01  W-START                         PIC  X(40)                           
013300     VALUE 'SAKUT055 - WORKING STORAGE BEGINS HERE'.                      
013400                                                                          
013500                                                                          
013600******************************************************************        
013700*                         SWITCHES                                        
013800******************************************************************        
013900 01  PS-PROGRAM-SWITCHES.                                                 
014000                                                                          
014100     05  PS-END-OF-POS-FILE-SW       PIC  X(01)  VALUE 'N'.               
014200         88  PS-END-OF-POS-FILE                  VALUE 'Y'.               
014100     05  PS-END-OF-STR-FILE-SW       PIC  X(01)  VALUE 'N'.               
014200         88  PS-END-OF-STR-FILE                  VALUE 'Y'.               
014100     05  PS-STORE-FOUND-SW           PIC  X(01)  VALUE 'N'.               
014200         88  PS-STORE-FOUND                      VALUE 'Y'.               
014200         88  PS-STORE-NOT-FOUND                  VALUE 'N'.               
C10091     05  PS-END-OF-TRANS-SW          PIC  X(01)  VALUE 'N'.               
C10091         88  PS-END-OF-TRANS                     VALUE 'Y'.               
014700     05  PS-END-OF-TEPITM-SW         PIC  X(01)  VALUE 'N'.               
014800         88  PS-END-OF-TEPITM                    VALUE 'Y'.               
016440                                                                          
016600     05  PV-DB2-TABLE-NAME           PIC  X(12)  VALUE SPACES.            
016700         88  TEPTRN-TBL                          VALUE 'TEPTRN'.          
016700         88  TEPDKEY-TBL                         VALUE 'TEPDKEY'.         
017300         88  TEPITM-TBL                          VALUE 'TEPITM'.          
017300         88  TEPGFT-TBL                          VALUE 'TEPGFT'.          
017900                                                                          
018000                                                                          
018100     05  PV-DB2-OPERATION-MSG        PIC  X(60)  VALUE SPACES.            
018200         88  OPEN-CRSR                           VALUE                    
018300                                                 'CURSOR OPEN'.           
018400         88  FETCH-CRSR                          VALUE                    
018500                                                 'CURSOR FETCH'.          
018600         88  CLOSE-CRSR                          VALUE                    
018700                                                 'CURSOR CLOSE'.          
018800         88  SELECT-TBL                          VALUE                    
018900                                                 'SELECT'.                
019000                                                                          
019100     05  PV-PARAGRAPH-NAME           PIC  X(30)  VALUE SPACES.            
019800******************************************************************        
019900*                        ACCUMULATORS                                     
020000******************************************************************        
020100 01  PA-PROGRAM-ACCUMULATORS.                                             
020200                                                                          
           05  PA-PGM-READ-COUNT           PIC S9(9) VALUE ZEROES COMP.         
020300     05  PA-TRN-READ-COUNT           PIC S9(9) VALUE ZEROES COMP.         
020300     05  PA-DKEY-READ-COUNT          PIC S9(9) VALUE ZEROES COMP.         
020400     05  PA-ITM-READ-COUNT           PIC S9(9) VALUE ZEROES COMP.         
020900     05  PA-GFT-READ-COUNT           PIC S9(9) VALUE ZEROES COMP.         
021500                                                                          
021600     05  PA-TRN-WRITE-COUNT          PIC S9(9) VALUE ZEROES COMP.         
021600     05  PA-DKEY-WRITE-COUNT         PIC S9(9) VALUE ZEROES COMP.         
021700     05  PA-ITM-WRITE-COUNT          PIC S9(9) VALUE ZEROES COMP.         
022410     05  PA-GFT-WRITE-COUNT          PIC S9(9) VALUE ZEROES COMP.         
022800                                                                          
021600     05  PA-TRN-WRITE-AMT            PIC S9(9)V99                         
                                                     VALUE ZEROES COMP.         
021700     05  PA-ITM-WRITE-AMT            PIC S9(9)V99                         
                                                     VALUE ZEROES COMP.         
022800                                                                          
023100******************************************************************        
023200*                          CONSTANTS                                      
023300******************************************************************        
023400 01  PC-PROGRAM-CONSTANTS.                                                
023500     05  PC-PROGRAM-NAME             PIC  X(08)  VALUE 'SAKUT055'.        
023600                                                                          
023700     05  PC-SQL-SUCCESSFUL           PIC S9(04)  VALUE ZEROES.            
023800     05  PC-SQL-ROW-NOT-FOUND        PIC S9(04)  VALUE +100.              
023810     05  PC-OLD-GIFT-CERT-LEN        PIC S9(04)  VALUE 5.                 
072711*    05  PC-ECOM-STORE-VALUE         PIC S9(04)  VALUE 873.               
023810     05  PC-GENERIC-ST-CDE           PIC X(2)    VALUE 'ZZ'.              
023810     05  PC-NON-US-STATE             PIC S9(02)  VALUE ZEROES             
023900                                                 COMP-3.                  
024000******************************************************************        
024100*                          VARIABLES                                      
024200******************************************************************        
024300 01  PV-PROGRAM-VARIABLES.                                                
024400     05  PV-SQLCODE                  PIC +9(04)  VALUE ZEROES.            
024500                                                                          
024600******************************************************************        
024700*                        WORK AREA                                        
024800******************************************************************        
024900 01  PROGRAM-WORK-AREA.                                                   
           05  WS-COSA-SLS-DTE             PIC X(10)  VALUE SPACES.             
                                                                                
025100     05  WS-COSA-KEY-FIELDS.                                              
025200         10  WS-COSA-PARTN-NBR       PIC S9(4)  VALUE ZEROES COMP.        
025300         10  WS-COSA-STR-NBR         PIC S9(4)  VALUE ZEROES COMP.        
025400         10  WS-COSA-RGST-ID         PIC S9(4)  VALUE ZEROES COMP.        
025500         10  WS-COSA-TRN-NBR         PIC S9(4)  VALUE ZEROES COMP.        
025600         10  WS-COSA-STRT-TM         PIC X(8)   VALUE SPACES.             
025700         10  WS-COSA-SLS-CORR-SEQ-NBR                                     
025800                                     PIC S9(4)  VALUE ZEROES COMP.        
025900         10  WS-COSA-LNE-ITM-SEQ-NBR                                      
026000                                     PIC S9(4)  VALUE ZEROES COMP.        
026010         10  WS-COSA-TLD-SEQ-NBR                                          
026020                                     PIC S9(4)  VALUE ZEROES COMP.        
026010         10  WS-COSA-TNDR-SEQ-NBR    PIC S9(4)  VALUE ZEROES COMP.        
                                                                                
           05  WS-BEG-SLS-DTE              PIC X(10)  VALUE SPACES.             
           05  WS-END-SLS-DTE              PIC X(10)  VALUE SPACES.             
026600     05  WS-END-PRCG-DTE             PIC X(10)  VALUE SPACES.             
           05  WS-BEG-JULIAN-DTE           PIC 9(07)  VALUE ZEROES.             
026600     05  WS-END-JULIAN-DTE           PIC 9(07)  VALUE ZEROES.             
           05  WS-SAVE-PARTN-NBR           PIC S9(4)  VALUE ZEROES COMP.        
           05  WS-STR-SUB                  PIC S9(4)  VALUE ZEROES COMP.        
           05  WS-MAX-STRS                 PIC S9(4)  VALUE ZEROES.             
           05  WS-STR-LKUP-TABLE OCCURS 600 TIMES.                              
               10 WS-STR-LKUP              PIC S9(4)  VALUE ZEROES COMP.        
019200                                                                          
      *KEEP THIS.  IT'S USED FOR REPORTING BY STORE SO THAT A                   
      *POSSIBLE ERROR CAN BE FOUND.                                             
019300*01  WS-STORE-TABLE.                                                      
019400*    05  WS-STORE-GROUP OCCURS 458 TIMES.                                 
019500*        10 WS-STR-NBR               PIC S9(4) VALUE ZEROES COMP.         
019600*        10 WS-STR-COUNT             PIC S9(4) VALUE ZEROES COMP.         
019700*    05 WS-STR-INDX                  PIC S9(4) VALUE ZEROES COMP.         
022900*    05 WS-PREV-STR-NBR              PIC S9(4) VALUE ZEROES COMP.         
029500******************************************************************        
029600*               SYTEM TIME AND DATE VARIABLES                             
029700******************************************************************        
029800 01  SYS-DATE-TIME.                                                       
029900     05  SYS-DATE                    PIC  X(08)  VALUE SPACES.            
030000     05  SYS-TIME                    PIC  X(08)  VALUE SPACES.            
030100                                                                          
032200 01  SD-BEG-DATE.                                                         
032300     05  SD-BEG-CC                   PIC  9(02)  VALUE ZEROES.            
032400     05  SD-BEG-YY                   PIC  9(02)  VALUE ZEROES.            
032500     05  FILLER                      PIC  X(01)  VALUE '-'.               
032600     05  SD-BEG-MM                   PIC  9(02)  VALUE ZEROES.            
032700     05  FILLER                      PIC  X(01)  VALUE '-'.               
032800     05  SD-BEG-DD                   PIC  9(02)  VALUE ZEROES.            
                                                                                
032200 01  SD-END-DATE.                                                         
032300     05  SD-END-CC                   PIC  9(02)  VALUE ZEROES.            
032400     05  SD-END-YY                   PIC  9(02)  VALUE ZEROES.            
032500     05  FILLER                      PIC  X(01)  VALUE '-'.               
032600     05  SD-END-MM                   PIC  9(02)  VALUE ZEROES.            
032700     05  FILLER                      PIC  X(01)  VALUE '-'.               
032800     05  SD-END-DD                   PIC  9(02)  VALUE ZEROES.            
                                                                                
032200 01  SD-STAT-DATE.                                                        
032300     05  SD-STAT-CC                  PIC  9(02)  VALUE ZEROES.            
032400     05  SD-STAT-YY                  PIC  9(02)  VALUE ZEROES.            
032500     05  FILLER                      PIC  X(01)  VALUE '-'.               
032600     05  SD-STAT-MM                  PIC  9(02)  VALUE ZEROES.            
032700     05  FILLER                      PIC  X(01)  VALUE '-'.               
032800     05  SD-STAT-DD                  PIC  9(02)  VALUE ZEROES.            
                                                                                
030400 01  ST-BEG-TIME.                                                         
030500     05  ST-BEG-HR                   PIC  9(02)  VALUE ZEROES.            
030600     05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-BEG-MN                   PIC  9(02)  VALUE ZEROES.            
           05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-BEG-SEC                  PIC  9(02)  VALUE ZEROES.            
030800                                                                          
030900 01  ST-END-TIME.                                                         
031000     05  ST-END-HR                   PIC  9(02)  VALUE ZEROES.            
031100     05  FILLER                      PIC  X(01)  VALUE ':'.               
031200     05  ST-END-MN                   PIC  9(02)  VALUE ZEROES.            
           05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-END-SEC                  PIC  9(02)  VALUE ZEROES.            
031300                                                                          
030900 01  ST-STAT-TIME.                                                        
031000     05  ST-STAT-HR                  PIC  9(02)  VALUE ZEROES.            
031100     05  FILLER                      PIC  X(01)  VALUE ':'.               
031200     05  ST-STAT-MIN                 PIC  9(02)  VALUE ZEROES.            
           05  FILLER                      PIC  X(01)  VALUE ':'.               
030700     05  ST-STAT-SEC                 PIC  9(02)  VALUE ZEROES.            
031300                                                                          
031400 01  SD-PROC-DATE                    PIC  9(08)  VALUE ZEROES.            
032100                                                                          
033000******************************************************************        
033100*                        ERROR HANDLING                                   
033200******************************************************************        
033300 01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC.         
033400     88  ABEND-4001-WRITE-ERROR            VALUE +4001.                   
033500     88  ABEND-4002-READ-ERROR             VALUE +4002.                   
033600     88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.                   
033700     88  ABEND-4004-TABLE-ERROR            VALUE +4004.                   
033800     88  ABEND-4005-OPEN-ERROR             VALUE +4005.                   
033900     88  ABEND-4006-CLOSE-ERROR            VALUE +4006.                   
034000     88  ABEND-4007-SEQUENCE-ERROR         VALUE +4007.                   
034100     88  ABEND-4008-DATA-ERROR             VALUE +4008.                   
034200     88  ABEND-4009-CARD-ERROR             VALUE +4009.                   
034500     88  ABEND-4013-DB2-ERROR              VALUE +4013.                   
034600     88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.                   
034700     88  ABEND-4444-FORCED-ABEND           VALUE +4444.                   
034800                                                                          
034900                                                                          
035000******************************************************************        
035100*                       RECORD LAYOUTS                                    
035200******************************************************************        
C10091     COPY SARD041.                                                        
           COPY SARD024.                                                        
           COPY SARD028.                                                        
035400                                                                          
       01  POS-CONTROL-CARD.                                                    
           05  FILLER                   PIC X(9)     VALUE SPACES.              
           05  POS-CNTL-BEG-DATE-MMDDYY PIC 9(08)    VALUE ZEROES.              
           05  FILLER                   PIC X(01)    VALUE SPACES.              
           05  POS-CNTL-END-DATE-MMDDYY PIC 9(08)    VALUE ZEROES.              
           05  FILLER                   PIC X(12)    VALUE SPACES.              
           05  POS-CNTL-STR-MODE-FLAG   PIC X(01)    VALUE SPACES.              
               88 POS-CNTL-ALL-STORES                VALUE 'A'.                 
               88 POS-CNTL-LIST-STORES               VALUE 'L'.                 
               88 POS-CNTL-RANGE-STORES              VALUE 'R'.                 
           05  FILLER                   PIC X(09)    VALUE SPACES.              
           05  POS-CNTL-BEG-STR         PIC 9(04)    VALUE ZEROES.              
           05  FILLER                   PIC X(01)    VALUE SPACES.              
           05  POS-CNTL-END-STR         PIC 9(04)    VALUE ZEROES.              
           05  FILLER                   PIC X(01)    VALUE SPACES.              
           05  POS-STATUS-FLAG          PIC X(01)    VALUE SPACES.              
               88 POS-STATUS-ON                      VALUE 'Y'.                 
               88 POS-STATUS-OFF                     VALUE 'N'.                 
           05  FILLER                   PIC X(01)    VALUE SPACES.              
           05  POS-STATUS-COUNT         PIC 9(09)    VALUE ZEROES.              
           05  FILLER                   PIC X(11)    VALUE SPACES.              
036100                                                                          
035500 01  STR-CNTL-REC.                                                        
           05  STR-CNTL-NBR             PIC 9(4)    VALUE ZEROES.               
           05  FILLER                   PIC X(76)   VALUE SPACES.               
036200******************************************************************        
036300* DPWS004 VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004         
036400******************************************************************        
036500     COPY DPWS004.                                                        
036600                                                                          
036700******************************************************************        
036800* PARAMETER LIST FOR THE CALENDAR ROUTINE                                 
036900******************************************************************        
037000     COPY DPWS500.                                                        
037100                                                                          
037200******************************************************************        
037300* DB2 MESSAGE AREA                                                        
037400******************************************************************        
037500     COPY SQLCA2.                                                         
037600                                                                          
037700******************************************************************        
037800* STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                         
037900******************************************************************        
038000     EXEC SQL                                                             
038100         INCLUDE SQLCA                                                    
038200     END-EXEC.                                                            
039000******************************************************************        
039100* ITEM TABLE                                                              
039200******************************************************************        
040000     EXEC SQL                                                             
040100         INCLUDE TEPITM                                                   
040200     END-EXEC.                                                            
040300                                                                          
040400******************************************************************        
040500* GIFT REGISTRY TABLE                                                     
040600******************************************************************        
040700     EXEC SQL                                                             
040800         INCLUDE TEPGFT                                                   
040900     END-EXEC.                                                            
041000                                                                          
064100******************************************************************        
064200* ITEM CURSOR (ITM)                                                       
064300******************************************************************        
064400     EXEC SQL                                                             
064500        DECLARE TEPITM-CURSOR CURSOR FOR                                  
064600        SELECT                                                            
064700           LNE_ITM_SEQ_NBR                                                
064800          ,LIV_RSLU_CDE                                                   
                ,DEPT_NBR                                                       
                ,SUB_CL_NBR                                                     
                ,SKU_SEQ_NBR                                                    
064900          ,SLD_QTY                                                        
065000          ,NET_CHRGD_AMT                                                  
065100          ,UPC_NBR                                                        
065200          ,SAL_TYP_CDE                                                    
065300          ,SAL_RET_CDE                                                    
065400          ,TXBL_IND                                                       
065500          ,SRVR_ONLN_IND                                                  
065600          ,DEPT_CL_ERR_CDE                                                
065700          ,SKU_ERR_CDE                                                    
065800          ,RET_RSN_CDE                                                    
                ,MDSE_ID                                                        
                ,MDSE_ID_LEN_NBR                                                
                ,ITM_STAT_CDE                                                   
                ,SRC_TYP_CDE                                                    
                ,PRIC_SRC_CDE                                                   
066000          ,CUST_CHRGD_AMT                                                 
066100          ,REG_RTL_AMT                                                    
066200          ,PRIC_DSCRP_IND                                                 
                ,ADITEM_NBR                                                     
                ,PMMESL_NBR                                                     
                ,PROMO_CDE                                                      
                ,ITM_NBR                                                        
066300          ,UNT_COST_AMT                                                   
066400          ,OWND_UNT_RTL_AMT                                               
066500          ,OWNSHP_CDE                                                     
                ,REG_RTL_ORGN_CDE                                               
                ,ITM_ASG_TX_ID                                                  
                ,RCPT_TX_TOT_CDE                                                
                ,RET_QTY                                                        
066700          ,GFT_RCPT_CDE                                                   
066800          ,TX_RT_PCT                                                      
066900          ,TX_AMT                                                         
                ,MAJ_CL_NBR                                                     
                ,SKU_NBR                                                        
                ,PROMO_INTFC_ID                                                 
                ,PROMO_SCHM_CDE                                                 
067000        FROM TEPITM                                                       
067100        WHERE PARTN_NBR        = :WS-COSA-PARTN-NBR        AND            
067200              STR_NBR          = :WS-COSA-STR-NBR          AND            
067300              RGST_ID          = :WS-COSA-RGST-ID          AND            
067400              TRN_NBR          = :WS-COSA-TRN-NBR          AND            
067500              STRT_TM          = :WS-COSA-STRT-TM          AND            
067600              SLS_CORR_SEQ_NBR = :WS-COSA-SLS-CORR-SEQ-NBR                
067700        WITH UR                                                           
067800     END-EXEC.                                                            
067900                                                                          
N2I201******************************************************************        
N2I201                                                                          
070200 PROCEDURE DIVISION.                                                      
070300******************************************************************        
070400* MAIN DRIVER                                                             
070500******************************************************************        
070600 0000-MAINLINE.                                                           
070700                                                                          
070800     PERFORM A1000-INITIALIZE.                                            
                                                                                
C10091     PERFORM B1000-PROCESS-TRANS.                                         
                                                                                
085100     PERFORM F1000-CONTROLS.                                              
071100                                                                          
071200     PERFORM A3000-CLOSE.                                                 
071300                                                                          
071400     GOBACK.                                                              
071500                                                                          
071600******************************************************************        
071700* A1000 - PREFORMS TASK INITIATION PROCESSING                             
071800******************************************************************        
071900 A1000-INITIALIZE.                                                        
072000                                                                          
072100     OPEN INPUT  POS-CNTL-CARD                                            
072100                 STR-PARM-FILE                                            
C10091                 TEP-TRANS-FILE.                                          
C25054     OPEN OUTPUT CW-EXTRACT-ITM                                           
C25054                 CW-EXTRACT-TRN.                                          
073000                                                                          
073100     MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                         
073200                                                                          
073700     MOVE SYS-DATE (1:2) TO SD-BEG-CC.                                    
073800     MOVE SYS-DATE (3:2) TO SD-BEG-YY.                                    
073900     MOVE SYS-DATE (5:2) TO SD-BEG-MM.                                    
074000     MOVE SYS-DATE (7:2) TO SD-BEG-DD.                                    
073200                                                                          
073400     MOVE SYS-TIME (1:2) TO ST-BEG-HR.                                    
073500     MOVE SYS-TIME (3:2) TO ST-BEG-MN.                                    
073500     MOVE SYS-TIME (5:2) TO ST-BEG-SEC.                                   
074100                                                                          
074200     PERFORM A2000-PROCESS-CNTL-CARD.                                     
074300                                                                          
074800******************************************************************        
074900* A2000 -  READ CNTL CARD TO GET POS DATE IN MMDDYY FORMAT.               
075000*          CALL DATE ROUTINE TO CONVERT DATE TO A CCYY-MM-DD              
075100*          PROCESS DATE.                                                  
075200******************************************************************        
075300 A2000-PROCESS-CNTL-CARD.                                                 
075400                                                                          
075500     PERFORM A2100-READ-CNTL-CARDS.                                       
075600                                                                          
075800                                                                          
075900     IF PS-END-OF-POS-FILE                                                
076000        MOVE 'A2000-PROCESS-CNTL-CARD'     TO PV-PARAGRAPH-NAME           
076100        MOVE 'NO POS CNTL CARD TO PROCESS' TO PV-DB2-OPERATION-MSG        
076200                                                                          
076300        SET ABEND-4003-CTRL-CARD-ERROR     TO TRUE                        
076400        PERFORM Z9000-DB2-ABEND                                           
076        END-IF.                                                              
076600                                                                          
079300     MOVE POS-CNTL-BEG-DATE-MMDDYY         TO SD-PROC-DATE.               
076700     PERFORM A2200-CONVERT-INPUT-DATE.                                    
           PERFORM A2300-EVALUATE-BEG-DTE.                                      
077100                                                                          
079300     MOVE POS-CNTL-END-DATE-MMDDYY         TO SD-PROC-DATE.               
076700     PERFORM A2200-CONVERT-INPUT-DATE.                                    
           PERFORM A2400-EVALUATE-END-DTE.                                      
                                                                                
           IF WS-BEG-JULIAN-DTE > WS-END-JULIAN-DTE                             
081300        MOVE 'A2000-PROCESS-CNTL-CARD'  TO PV-PARAGRAPH-NAME              
081500        MOVE 'BEG DTE MUST BE <= END DTE'                                 
081600                                        TO PV-DB2-OPERATION-MSG           
081700        SET ABEND-4019-CAL-SUB-ERROR    TO TRUE                           
081800                                                                          
081900        PERFORM Z9000-DB2-ABEND                                           
077100     END-IF.                                                              
                                                                                
           IF NOT POS-CNTL-ALL-STORES AND NOT POS-CNTL-LIST-STORES AND          
              NOT POS-CNTL-RANGE-STORES                                         
081300        MOVE 'A2000-PROCESS-CNTL-CARD'  TO PV-PARAGRAPH-NAME              
081500        MOVE 'INVALID MODE - MUST BE A OR L'                              
081600                                        TO PV-DB2-OPERATION-MSG           
081800        SET ABEND-4003-CTRL-CARD-ERROR  TO TRUE                           
081900        PERFORM Z9000-DB2-ABEND                                           
077100     END-IF.                                                              
                                                                                
           IF POS-CNTL-LIST-STORES                                              
              PERFORM A2150-READ-STR-PARM-FILE                                  
                 UNTIL PS-END-OF-STR-FILE                                       
              MOVE WS-STR-SUB TO WS-MAX-STRS                                    
           END-IF.                                                              
                                                                                
075700     CLOSE POS-CNTL-CARD                                                  
075700           STR-PARM-FILE.                                                 
                                                                                
  7200******************************************************************        
077300* A2100 - READ THE POS DATE CONTROL CARD                                  
077400******************************************************************        
077500 A2100-READ-CNTL-CARDS.                                                   
077600                                                                          
077700     READ POS-CNTL-CARD INTO POS-CONTROL-CARD                             
077800        AT END                                                            
077900           SET PS-END-OF-POS-FILE TO TRUE.                                
                                                                                
077500 A2150-READ-STR-PARM-FILE.                                                
077600                                                                          
077700     READ STR-PARM-FILE INTO STR-CNTL-REC                                 
077800        AT END                                                            
077900           SET PS-END-OF-STR-FILE TO TRUE.                                
078000                                                                          
078100     IF NOT PS-END-OF-STR-FILE                                            
              ADD 1 TO WS-STR-SUB                                               
              MOVE STR-CNTL-NBR TO WS-STR-LKUP (WS-STR-SUB)                     
           END-IF.                                                              
                                                                                
078200******************************************************************        
078300* A2200 - CONVERT INPUT DATE TO CCYY-MM-DD FORMAT                         
078400******************************************************************        
078500 A2200-CONVERT-INPUT-DATE.                                                
078600                                                                          
078700     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
078800                                                                          
078900     SET  DPG51-ACTUAL-CALENDAR-ONLY   TO TRUE.                           
079000     SET  DPG51-DO-NOT-INCR-DECR-DATE  TO TRUE.                           
079100     SET  DPG52-LK-DTE-GREG-CC         TO TRUE.                           
                                                                                
079200     MOVE SD-PROC-DATE                 TO DPG52-LK-DATE-INPUT.            
079400                                                                          
079500     CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51 DPG52 DPG53            
079600                                             DPG54 DPG55 DPG56.           
079700                                                                          
078500 A2300-EVALUATE-BEG-DTE.                                                  
079800     EVALUATE TRUE                                                        
079900         WHEN DPG54-NO-ERROR                                              
C25054            MOVE DPG55-DB2-ISO-DATE-FMT TO WS-BEG-SLS-DTE                 
                  MOVE DPG55-JULIAN-DATE-CCYYDDD                                
                                              TO WS-BEG-JULIAN-DTE              
080600                                                                          
080700         WHEN DPG54-DATE-INVALID                                          
080800         WHEN DPG54-SEVERE-ERROR                                          
080900         WHEN DPG54-WARNING-ERROR                                         
081000            DISPLAY '            '                                        
081100            DISPLAY 'INPUT DATE: ' DPG52-LK-DATE-INPUT                    
081200                                                                          
081300            MOVE 'A2300-EVALUATE-BEG-DTE'                                 
081400                                          TO PV-PARAGRAPH-NAME            
081500            MOVE DPG54-ERROR-MESSAGE      TO PV-DB2-OPERATION-MSG         
081600                                                                          
081700            SET ABEND-4019-CAL-SUB-ERROR  TO TRUE                         
081800                                                                          
081900            PERFORM Z9000-DB2-ABEND                                       
082000     END-EVALUATE.                                                        
082100                                                                          
078500 A2400-EVALUATE-END-DTE.                                                  
079800     EVALUATE TRUE                                                        
079900         WHEN DPG54-NO-ERROR                                              
080000            MOVE DPG55-DB2-ISO-DATE-FMT TO WS-END-SLS-DTE                 
                                                 WS-END-PRCG-DTE                
                  MOVE DPG55-JULIAN-DATE-CCYYDDD                                
                                              TO WS-END-JULIAN-DTE              
080600                                                                          
080700         WHEN DPG54-DATE-INVALID                                          
080800         WHEN DPG54-SEVERE-ERROR                                          
080900         WHEN DPG54-WARNING-ERROR                                         
081000            DISPLAY '            '                                        
081100            DISPLAY 'INPUT DATE: ' DPG52-LK-DATE-INPUT                    
081200                                                                          
081300            MOVE 'A2400-EVALUATE-END-DTE'                                 
081400                                          TO PV-PARAGRAPH-NAME            
081500            MOVE DPG54-ERROR-MESSAGE      TO PV-DB2-OPERATION-MSG         
081600                                                                          
081700            SET ABEND-4019-CAL-SUB-ERROR TO TRUE                          
081800                                                                          
081900            PERFORM Z9000-DB2-ABEND                                       
082000     END-EVALUATE.                                                        
082100                                                                          
082200                                                                          
082300******************************************************************        
082400* A3000 - CLOSE FILES                                                     
082500******************************************************************        
082600 A3000-CLOSE.                                                             
082700                                                                          
C25054     CLOSE CW-EXTRACT-ITM                                                 
083520           CW-EXTRACT-TRN                                                 
C10091           TEP-TRANS-FILE.                                                
N2I201                                                                          
082500******************************************************************        
083700* B1000 - MAIN PROCESS                                                    
083800******************************************************************        
C10091 B1000-PROCESS-TRANS.                                                     
C10091                                                                          
C10091     PERFORM S1100-READ-TRANS.                                            
C10091                                                                          
C10091     PERFORM                                                              
C10091        UNTIL PS-END-OF-TRANS                                             
C10091           PERFORM B2000-PROCESS-COSA-EXTRACT                             
C10091           PERFORM S1100-READ-TRANS                                       
C10091     END-PERFORM.                                                         
C10091                                                                          
085300 B2000-PROCESS-COSA-EXTRACT.                                              
085400                                                                          
C10091     IF SA041-UNLOAD-TBL-NAME = 'TEPTRN '                                 
              IF SA041-EPTRN-PARTN-NBR NOT = WS-SAVE-PARTN-NBR                  
                 DISPLAY 'PARTN-NBR: ' SA041-EPTRN-PARTN-NBR                    
                 DISPLAY 'SLS-DTE  : ' SA041-EPTRN-SLS-DTE                      
                 DISPLAY ' '                                                    
                 MOVE SA041-EPTRN-PARTN-NBR TO WS-SAVE-PARTN-NBR                
              END-IF                                                            
C10091     END-IF.                                                              
                                                                                
           SET PS-STORE-NOT-FOUND TO TRUE.                                      
                                                                                
           IF POS-CNTL-LIST-STORES                                              
             PERFORM B2500-LOOKUP-STR                                           
           ELSE                                                                 
             IF POS-CNTL-RANGE-STORES                                           
               IF SA041-EPTRN-STR-NBR >= POS-CNTL-BEG-STR AND                   
                  SA041-EPTRN-STR-NBR <= POS-CNTL-END-STR                       
                 SET PS-STORE-FOUND TO TRUE                                     
               END-IF                                                           
             ELSE                                                               
               SET PS-STORE-FOUND TO TRUE                                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF PS-STORE-FOUND                                                    
085500        PERFORM C1000-PROCESS-TEPTRN                                      
088400     END-IF.                                                              
                                                                                
           IF POS-STATUS-ON AND                                                 
              PA-PGM-READ-COUNT = POS-STATUS-COUNT                              
              MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME                       
                                                                                
073700        MOVE SYS-DATE (1:2)        TO SD-STAT-CC                          
073800        MOVE SYS-DATE (3:2)        TO SD-STAT-YY                          
073900        MOVE SYS-DATE (5:2)        TO SD-STAT-MM                          
074000        MOVE SYS-DATE (7:2)        TO SD-STAT-DD                          
                                                                                
073400        MOVE SYS-TIME (1:2)        TO ST-STAT-HR                          
073500        MOVE SYS-TIME (3:2)        TO ST-STAT-MIN                         
073500        MOVE SYS-TIME (5:2)        TO ST-STAT-SEC                         
              DISPLAY 'DATE/TIME: ' SD-STAT-DATE ' ' ST-STAT-TIME ' COUN        
      -       'T: ' PA-TRN-READ-COUNT                                           
              MOVE ZEROES                TO PA-PGM-READ-COUNT                   
           END-IF.                                                              
                                                                                
       B2500-LOOKUP-STR.                                                        
                                                                                
           PERFORM                                                              
              VARYING WS-STR-SUB FROM 1 BY 1                                    
                 UNTIL PS-STORE-FOUND OR WS-STR-SUB > WS-MAX-STRS               
                    IF SA041-EPTRN-STR-NBR = WS-STR-LKUP (WS-STR-SUB)           
                      SET PS-STORE-FOUND TO TRUE                                
                    END-IF                                                      
           END-PERFORM.                                                         
                                                                                
088600                                                                          
092100 C1000-PROCESS-TEPTRN.                                                    
092200                                                                          
092300      MOVE SA041-EPTRN-PARTN-NBR  TO WS-COSA-PARTN-NBR                    
092400      MOVE SA041-EPTRN-STR-NBR    TO WS-COSA-STR-NBR                      
092500      MOVE SA041-EPTRN-RGST-ID    TO WS-COSA-RGST-ID                      
092600      MOVE SA041-EPTRN-TRN-NBR    TO WS-COSA-TRN-NBR                      
092700      MOVE SA041-EPTRN-STRT-TM    TO WS-COSA-STRT-TM                      
092800      MOVE SA041-EPTRN-SLS-CORR-SEQ-NBR                                   
              TO WS-COSA-SLS-CORR-SEQ-NBR                                       
093000      PERFORM D1000-WRITE-TEPTRN.                                         
093100                                                                          
087800      INITIALIZE PS-END-OF-TEPITM-SW.                                     
087900      PERFORM S5000-OPEN-TEPITM-CURSOR.                                   
088000      PERFORM S5100-FETCH-TEPITM-CURSOR.                                  
088100      PERFORM C5000-PROCESS-TEPITM                                        
088200         UNTIL PS-END-OF-TEPITM.                                          
088300      PERFORM S5200-CLOSE-TEPITM-CURSOR.                                  
                                                                                
096800 C5000-PROCESS-TEPITM.                                                    
096900                                                                          
097000      MOVE EPITM-LNE-ITM-SEQ-NBR TO WS-COSA-LNE-ITM-SEQ-NBR.              
097100                                                                          
097200      PERFORM D5000-WRITE-TEPITM.                                         
                                                                                
098700      PERFORM S5100-FETCH-TEPITM-CURSOR.                                  
098800                                                                          
099400 D1000-WRITE-TEPTRN.                                                      
099500                                                                          
099600      INITIALIZE SA028-CORP-WAREHOUSE-TRN-REC.                            
099700                                                                          
100800      MOVE SA041-EPTRN-SLS-DTE    TO SA028-TRN-SLS-DTE                    
                                           WS-COSA-SLS-DTE.                     
                                                                                
099800      MOVE WS-COSA-PARTN-NBR      TO SA028-TRN-PARTN-NBR.                 
099900      MOVE WS-COSA-STR-NBR        TO SA028-TRN-STR-NBR.                   
100000      MOVE WS-COSA-RGST-ID        TO SA028-TRN-RGST-ID.                   
100100      MOVE WS-COSA-TRN-NBR        TO SA028-TRN-TRN-NBR.                   
100200      MOVE WS-COSA-STRT-TM        TO SA028-TRN-STRT-TM.                   
100300      MOVE WS-COSA-SLS-CORR-SEQ-NBR                                       
100400                                  TO SA028-TRN-SLS-CORR-SEQ-NBR.          
100500      MOVE SA041-EPTRN-VOID-ABRT-CDE  TO SA028-TRN-VOID-ABRT-CDE.         
100600      MOVE SA041-EPTRN-TRN-STAT-CDE   TO SA028-TRN-TRN-STAT-CDE.          
100700      MOVE SA041-EPTRN-TRN-TYP-CDE    TO SA028-TRN-TYP-CDE.               
100900      MOVE SA041-EPTRN-PRCG-DTE       TO SA028-TRN-PRCG-DTE               
101000      MOVE SA041-EPTRN-TRN-DTE        TO SA028-TRN-TRN-DTE                
101100      MOVE SA041-EPTRN-TRN-TOT-AMT    TO SA028-TRN-TRN-TOT-AMT            
101200      MOVE SA041-EPTRN-SLS-TX-AMT     TO SA028-TRN-SLS-TX-AMT             
101300      MOVE SA041-EPTRN-SRVR-ONLN-IND  TO SA028-TRN-SRVR-ONLN-IND          
            MOVE SA041-EPTRN-QUAL-CDE       TO SA028-TRN-QUAL-CDE               
101400      MOVE SA041-EPTRN-ACCM-TOT-AMT   TO SA028-TRN-ACCM-TOT-AMT           
101500      MOVE SA041-EPTRN-END-TM         TO SA028-TRN-END-TM                 
            MOVE SA041-EPTRN-POS-CPBL-LVL-NBR                                   
              TO SA028-TRN-POS-CPBL-LVL-NBR                                     
101600      MOVE SA041-EPTRN-ASSOC-ID       TO SA028-TRN-ASSOC-ID               
101700      MOVE SA041-EPTRN-ORIG-OTR-AMT   TO SA028-TRN-ORIG-OTR-AMT           
            MOVE SA041-EPTRN-ACCM-RET-TOT-AMT                                   
              TO SA028-TRN-ACCM-RET-TOT-AMT                                     
101800      MOVE SA041-EPTRN-RGST-BNK-NBR   TO SA028-TRN-RGST-BANK-NBR          
101900      MOVE SA041-EPTRN-CRE-TMST       TO SA028-TRN-CRE-TMST               
101900      MOVE SA041-EPTRN-FIN-SEQ-STRT-TM                                    
              TO SA028-TRN-FIN-SEQ-STRT-TM                                      
101900      MOVE SA041-EPTRN-RCPT-TYP-CDE   TO SA028-TRN-RCPT-TYP-CDE           
101900      MOVE SA041-EPTRN-ASSOC-ID-SRC-CDE                                   
              TO SA028-TRN-ASSOC-ID-SRC-CDE                                     
N2I210      MOVE SA041-EPTRN-EMAIL-PRVDD-IND                                    
              TO SA028-TRN-EMAIL-CAPTURE-IND                                    
N2I210      MOVE SA041-EPTRN-EMAIL-PRMPT-IND                                    
102000        TO SA028-TRN-EMAIL-PROMPT-IND                                     
102100      WRITE CW-EXTRACT-TRN-REC FROM SA028-CORP-WAREHOUSE-TRN-REC          
102200      ADD 1 TO PA-TRN-WRITE-COUNT.                                        
102300                                                                          
115200 D5000-WRITE-TEPITM.                                                      
115300                                                                          
115400      INITIALIZE SA024-CORP-WAREHOUSE-ITM-REC.                            
115500                                                                          
            MOVE WS-COSA-SLS-DTE        TO SA024-ITM-SLS-DTE.                   
                                                                                
115600      MOVE WS-COSA-PARTN-NBR      TO SA024-ITM-PARTN-NBR.                 
115700      MOVE WS-COSA-STR-NBR        TO SA024-ITM-STR-NBR.                   
115800      MOVE WS-COSA-RGST-ID        TO SA024-ITM-RGST-ID.                   
115900      MOVE WS-COSA-TRN-NBR        TO SA024-ITM-TRN-NBR.                   
116000      MOVE WS-COSA-STRT-TM        TO SA024-ITM-STRT-TM.                   
116100      MOVE WS-COSA-SLS-CORR-SEQ-NBR                                       
116200                                  TO SA024-ITM-SLS-CORR-SEQ-NBR.          
145100      MOVE EPITM-LNE-ITM-SEQ-NBR  TO SA024-ITM-LNE-ITM-SEQ-NBR.           
145200      MOVE EPITM-LIV-RSLU-CDE     TO SA024-ITM-LIV-RSLU-CDE.              
            MOVE EPITM-DEPT-NBR         TO SA024-ITM-DEPT-NBR.                  
            MOVE EPITM-SUB-CL-NBR       TO SA024-ITM-SUB-CL-NBR.                
            MOVE EPITM-SKU-SEQ-NBR      TO SA024-ITM-SKU-SEQ-NBR.               
145300      MOVE EPITM-SLD-QTY          TO SA024-ITM-SLD-QTY.                   
145400      MOVE EPITM-NET-CHRGD-AMT    TO SA024-ITM-NET-CHRGD-AMT.             
145500      MOVE EPITM-UPC-NBR          TO SA024-ITM-UPC-NBR.                   
145600      MOVE EPITM-SAL-TYP-CDE      TO SA024-ITM-SAL-TYP-CDE.               
145700      MOVE EPITM-SAL-RET-CDE      TO SA024-ITM-SAL-RET-CDE.               
145800      MOVE EPITM-TXBL-IND         TO SA024-ITM-TXBL-IND.                  
145900      MOVE EPITM-SRVR-ONLN-IND    TO SA024-ITM-SRVR-ONLN-IND.             
146000      MOVE EPITM-DEPT-CL-ERR-CDE  TO SA024-ITM-DEPT-CL-ERR-CDE.           
146100      MOVE EPITM-SKU-ERR-CDE      TO SA024-ITM-SKU-ERR-CDE.               
146200      MOVE EPITM-RET-RSN-CDE      TO SA024-ITM-RET-RSN-CDE.               
            MOVE EPITM-MDSE-ID          TO SA024-ITM-MDSE-ID.                   
            MOVE EPITM-MDSE-ID-LEN-NBR  TO SA024-ITM-MDSE-ID-LEN-NBR.           
            MOVE EPITM-ITM-STAT-CDE     TO SA024-ITM-ITM-STAT-CDE.              
            MOVE EPITM-SRC-TYP-CDE      TO SA024-ITM-SRC-TYP-CDE.               
            MOVE EPITM-PRIC-SRC-CDE     TO SA024-ITM-PRIC-SRC-CDE.              
146400      MOVE EPITM-CUST-CHRGD-AMT   TO SA024-ITM-CUST-CHRGD-AMT.            
146500      MOVE EPITM-REG-RTL-AMT      TO SA024-ITM-REG-RTL-AMT.               
146600      MOVE EPITM-PRIC-DSCRP-IND   TO SA024-ITM-PRIC-DSCRP-IND.            
            MOVE EPITM-ADITEM-NBR       TO SA024-ITM-ADITEM-NBR                 
            MOVE EPITM-PMMESL-NBR       TO SA024-ITM-PMMESL-NBR                 
            MOVE EPITM-PROMO-CDE        TO SA024-ITM-PROMO-CDE.                 
            MOVE EPITM-ITM-NBR          TO SA024-ITM-ITM-NBR.                   
146700      MOVE EPITM-UNT-COST-AMT     TO SA024-ITM-UNT-COST-AMT.              
146800      MOVE EPITM-OWND-UNT-RTL-AMT TO SA024-ITM-OWND-UNT-RTL-AMT.          
146900      MOVE EPITM-OWNSHP-CDE       TO SA024-ITM-OWNSHP-CDE.                
            MOVE EPITM-REG-RTL-ORGN-CDE TO SA024-ITM-REG-RTL-ORGN-CDE.          
            MOVE EPITM-ITM-ASG-TX-ID    TO SA024-ITM-ITM-ASG-TX-ID.             
            MOVE EPITM-RCPT-TX-TOT-CDE  TO SA024-ITM-RCPT-TX-TOT-CDE.           
            MOVE EPITM-RET-QTY          TO SA024-ITM-RET-QTY.                   
147100      MOVE EPITM-GFT-RCPT-CDE     TO SA024-ITM-GFT-RCPT-CDE.              
147200      MOVE EPITM-TX-RT-PCT        TO SA024-ITM-TX-RT-PCT.                 
147300      MOVE EPITM-TX-AMT           TO SA024-ITM-TX-AMT.                    
            MOVE EPITM-MAJ-CL-NBR       TO SA024-ITM-MAJ-CL-NBR.                
            MOVE EPITM-SKU-NBR          TO SA024-ITM-SKU-NBR.                   
            MOVE EPITM-PROMO-INTFC-ID   TO SA024-ITM-PROMO-INTFC-ID.            
            MOVE EPITM-PROMO-SCHM-CDE   TO SA024-ITM-PROMO-SCHM-CDE.            
                                                                                
118700      MOVE ZEROES                 TO EPGFT-GFT-RGSTY-ID.                  
118800                                                                          
118900      PERFORM S7000-SELECT-TEPGFT.                                        
119000                                                                          
119100      MOVE EPGFT-GFT-RGSTY-ID     TO SA024-ITM-GFT-RGSTY-ID.              
119200                                                                          
119300      IF EPGFT-GFT-RGSTY-ID > ZEROES                                      
119400         ADD 1 TO PA-GFT-WRITE-COUNT                                      
119500      END-IF.                                                             
119600                                                                          
119700      WRITE CW-EXTRACT-ITM-REC FROM SA024-CORP-WAREHOUSE-ITM-REC.         
119800      ADD 1 TO PA-ITM-WRITE-COUNT.                                        
                                                                                
C10091******************************************************************        
C10091* S1100 - READ TRANSACTION FROM INPUT FILE                                
C10091******************************************************************        
C10091 S1100-READ-TRANS.                                                        
C10091                                                                          
C10091     READ TEP-TRANS-FILE INTO SA041-TEP-TRANS-REC                         
C10091        AT END                                                            
C10091           SET PS-END-OF-TRANS TO TRUE.                                   
C10091                                                                          
C10091     IF NOT PS-END-OF-TRANS                                               
C10091        EVALUATE SA041-UNLOAD-TBL-NAME                                    
C10091            WHEN 'TEPTRN '                                                
C10091              ADD 1                       TO PA-TRN-READ-COUNT            
C10091                                             PA-PGM-READ-COUNT            
C10091            WHEN 'TEPDKEY'                                                
C10091              ADD 1                       TO PA-DKEY-READ-COUNT           
C10091        END-EVALUATE                                                      
C10091     END-IF.                                                              
C10091                                                                          
143000******************************************************************        
143100* S5000 - OPEN COSA ITEM (TEPITM) CURSOR                                  
143200******************************************************************        
143300 S5000-OPEN-TEPITM-CURSOR.                                                
143400                                                                          
143500     EXEC SQL                                                             
143600         OPEN TEPITM-CURSOR                                               
143700     END-EXEC.                                                            
143800                                                                          
143900     SET OPEN-CRSR  TO TRUE.                                              
144000     SET TEPITM-TBL TO TRUE.                                              
144100     MOVE 'S5000-OPEN-TEPITM-CURSOR' TO PV-PARAGRAPH-NAME.                
144200     PERFORM Z1000-CHECK-SQLCODE.                                         
144300                                                                          
144400******************************************************************        
144500* S5100 - FETCH COSA ITEM (TEPITM) CURSOR                                 
144600******************************************************************        
144700 S5100-FETCH-TEPITM-CURSOR.                                               
144800                                                                          
144900     EXEC SQL                                                             
145000         FETCH TEPITM-CURSOR INTO                                         
145100           :EPITM-LNE-ITM-SEQ-NBR                                         
145200          ,:EPITM-LIV-RSLU-CDE                                            
                ,:EPITM-DEPT-NBR                                                
                ,:EPITM-SUB-CL-NBR                                              
                ,:EPITM-SKU-SEQ-NBR                                             
145300          ,:EPITM-SLD-QTY                                                 
145400          ,:EPITM-NET-CHRGD-AMT                                           
145500          ,:EPITM-UPC-NBR                                                 
145600          ,:EPITM-SAL-TYP-CDE                                             
145700          ,:EPITM-SAL-RET-CDE                                             
145800          ,:EPITM-TXBL-IND                                                
145900          ,:EPITM-SRVR-ONLN-IND                                           
146000          ,:EPITM-DEPT-CL-ERR-CDE                                         
146100          ,:EPITM-SKU-ERR-CDE                                             
146200          ,:EPITM-RET-RSN-CDE                                             
                ,:EPITM-MDSE-ID                                                 
                ,:EPITM-MDSE-ID-LEN-NBR                                         
                ,:EPITM-ITM-STAT-CDE                                            
                ,:EPITM-SRC-TYP-CDE                                             
                ,:EPITM-PRIC-SRC-CDE                                            
146400          ,:EPITM-CUST-CHRGD-AMT                                          
146500          ,:EPITM-REG-RTL-AMT                                             
146600          ,:EPITM-PRIC-DSCRP-IND                                          
                ,:EPITM-ADITEM-NBR                                              
                ,:EPITM-PMMESL-NBR                                              
                ,:EPITM-PROMO-CDE                                               
                ,:EPITM-ITM-NBR                                                 
146700          ,:EPITM-UNT-COST-AMT                                            
146800          ,:EPITM-OWND-UNT-RTL-AMT                                        
146900          ,:EPITM-OWNSHP-CDE                                              
                ,:EPITM-REG-RTL-ORGN-CDE                                        
                ,:EPITM-ITM-ASG-TX-ID                                           
                ,:EPITM-RCPT-TX-TOT-CDE                                         
                ,:EPITM-RET-QTY                                                 
147100          ,:EPITM-GFT-RCPT-CDE                                            
147200          ,:EPITM-TX-RT-PCT                                               
147300          ,:EPITM-TX-AMT                                                  
                ,:EPITM-MAJ-CL-NBR                                              
                ,:EPITM-SKU-NBR                                                 
                ,:EPITM-PROMO-INTFC-ID                                          
                ,:EPITM-PROMO-SCHM-CDE                                          
147400     END-EXEC.                                                            
147500                                                                          
147600     SET FETCH-CRSR TO TRUE.                                              
147700     SET TEPITM-TBL TO TRUE.                                              
147800     MOVE 'S5100-OPEN-TEPITM-CURSOR' TO PV-PARAGRAPH-NAME.                
147900     PERFORM Z1000-CHECK-SQLCODE.                                         
148000                                                                          
148100******************************************************************        
148200* S5200 - CLOSE COSA ITEM (TEPITM) CURSOR                                 
148300******************************************************************        
148400 S5200-CLOSE-TEPITM-CURSOR.                                               
148500                                                                          
148600     EXEC SQL                                                             
148700         CLOSE TEPITM-CURSOR                                              
148800     END-EXEC.                                                            
148900                                                                          
149000     SET CLOSE-CRSR TO TRUE.                                              
149100     SET TEPITM-TBL TO TRUE.                                              
149200     MOVE 'S5200-CLOSE-TEPITM-CURSOR' TO PV-PARAGRAPH-NAME.               
149300     PERFORM Z1000-CHECK-SQLCODE.                                         
149400                                                                          
149500                                                                          
154400******************************************************************        
154500* S7000 - SELECT GIFT REGISTRY (TEPGFT)                                   
154600******************************************************************        
154700 S7000-SELECT-TEPGFT.                                                     
154800                                                                          
154900     EXEC SQL                                                             
155000         SELECT                                                           
155100            GFT_RGSTY_ID                                                  
155200          INTO                                                            
155300            :EPGFT-GFT-RGSTY-ID                                           
155400          FROM TEPGFT                                                     
155500          WHERE PARTN_NBR        = :WS-COSA-PARTN-NBR        AND          
155600                STR_NBR          = :WS-COSA-STR-NBR          AND          
155700                RGST_ID          = :WS-COSA-RGST-ID          AND          
155800                TRN_NBR          = :WS-COSA-TRN-NBR          AND          
155900                STRT_TM          = :WS-COSA-STRT-TM          AND          
156000                SLS_CORR_SEQ_NBR = :WS-COSA-SLS-CORR-SEQ-NBR AND          
156100                LNE_ITM_SEQ_NBR  = :EPITM-LNE-ITM-SEQ-NBR                 
156200          WITH UR                                                         
156300     END-EXEC.                                                            
156400                                                                          
156500     SET SELECT-TBL TO TRUE.                                              
156600     SET TEPGFT-TBL TO TRUE.                                              
156700     MOVE 'S7000-SELECT-TEPGFT' TO PV-PARAGRAPH-NAME.                     
156800     PERFORM Z1000-CHECK-SQLCODE.                                         
156900                                                                          
174150 Z1000-CHECK-SQLCODE.                                                     
174200                                                                          
174300     EVALUATE TRUE                                                        
174400         WHEN OPEN-CRSR OR CLOSE-CRSR                                     
174500             IF SQLCODE = PC-SQL-SUCCESSFUL                               
174600                CONTINUE                                                  
174700             ELSE                                                         
174800                PERFORM Z9000-DB2-ABEND                                   
174900             END-IF                                                       
175000         WHEN FETCH-CRSR OR SELECT-TBL                                    
175100             PERFORM Z1100-EVALUATE-FETCH                                 
175200         WHEN OTHER                                                       
175300            DISPLAY PC-PROGRAM-NAME 'SQL ACTION NOT SET!!'                
175400     END-EVALUATE.                                                        
175500                                                                          
175600 Z1100-EVALUATE-FETCH.                                                    
175700                                                                          
175800     IF SQLCODE = PC-SQL-SUCCESSFUL                                       
175900        EVALUATE TRUE                                                     
176800           WHEN TEPITM-TBL                                                
176900               ADD 1                   TO PA-ITM-READ-COUNT               
                     ADD EPITM-NET-CHRGD-AMT TO PA-ITM-WRITE-AMT                
177200           WHEN TEPGFT-TBL                                                
177300               ADD 1                   TO PA-GFT-READ-COUNT               
178400           WHEN OTHER                                                     
178500               DISPLAY 'SAKUT055 - CURSOR UNKNOWN'                        
178600        END-EVALUATE                                                      
178700     ELSE                                                                 
178800        IF SQLCODE = PC-SQL-ROW-NOT-FOUND                                 
178900           EVALUATE TRUE                                                  
179800              WHEN TEPITM-TBL                                             
179900                  SET PS-END-OF-TEPITM TO TRUE                            
180200              WHEN TEPGFT-TBL                                             
180300                  CONTINUE                                                
181400              WHEN OTHER                                                  
181500                  DISPLAY 'SAKUT055 - CURSOR UNKNOWN'                     
181600           END-EVALUATE                                                   
181700        ELSE                                                              
181800           PERFORM Z9000-DB2-ABEND                                        
181900        END-IF                                                            
182000     END-IF.                                                              
182100                                                                          
182200******************************************************************        
182300* F1000 - PROGRAM CONTROLS                                                
182400******************************************************************        
182500 F1000-CONTROLS.                                                          
182600                                                                          
182700     MOVE FUNCTION CURRENT-DATE TO SYS-DATE-TIME.                         
182800                                                                          
073700     MOVE SYS-DATE (1:2) TO SD-END-CC.                                    
073800     MOVE SYS-DATE (3:2) TO SD-END-YY.                                    
073900     MOVE SYS-DATE (5:2) TO SD-END-MM.                                    
074000     MOVE SYS-DATE (7:2) TO SD-END-DD.                                    
073200                                                                          
182900     MOVE SYS-TIME (1:2) TO ST-END-HR.                                    
183000     MOVE SYS-TIME (3:2) TO ST-END-MN.                                    
183000     MOVE SYS-TIME (5:2) TO ST-END-SEC.                                   
183100                                                                          
183200     DISPLAY '                    '.                                      
183300     DISPLAY '**********************'.                                    
183400     DISPLAY '   SAKUT055 CONTROLS  '.                                    
183500     DISPLAY '**********************'.                                    
183600     DISPLAY 'BEG DATE/TIME = ' SD-BEG-DATE ' ' ST-BEG-TIME               
183600     DISPLAY 'END DATE/TIME = ' SD-END-DATE ' ' ST-END-TIME               
183900     DISPLAY '======================'.                                    
184000     DISPLAY 'BEG CNTL DATE      = ' WS-BEG-SLS-DTE.                      
184000     DISPLAY 'END CNTL DATE      = ' WS-END-SLS-DTE.                      
184100                                                                          
           EVALUATE TRUE                                                        
              WHEN POS-CNTL-ALL-STORES                                          
                 DISPLAY 'STR CNTL MODE      = ALL'                             
              WHEN POS-CNTL-RANGE-STORES                                        
                 DISPLAY 'STR CNTL MODE      = RANGE'                           
              WHEN POS-CNTL-LIST-STORES                                         
                 DISPLAY 'STR CNTL MODE      = LIST'                            
           END-EVALUATE.                                                        
           DISPLAY '                     '.                                     
                                                                                
           IF POS-CNTL-RANGE-STORES                                             
              DISPLAY 'BEG CNTL STR       = ' POS-CNTL-BEG-STR                  
              DISPLAY 'END CNTL STR       = ' POS-CNTL-END-STR                  
           END-IF.                                                              
184100                                                                          
           IF POS-CNTL-LIST-STORES                                              
              DISPLAY 'CNTL STR LIST'                                           
              DISPLAY '-------------'                                           
              PERFORM                                                           
                 VARYING WS-STR-SUB FROM 1 BY 1                                 
                    UNTIL WS-STR-SUB > WS-MAX-STRS OR                           
                          WS-STR-LKUP (WS-STR-SUB) = ZEROES                     
                       DISPLAY 'CNTL STR NBR       = ' WS-STR-LKUP              
                                                       (WS-STR-SUB)             
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
184800     DISPLAY 'TEPGFT READ : ' PA-GFT-READ-COUNT.                          
184900     DISPLAY '      WRITE : ' PA-GFT-WRITE-COUNT.                         
187760     DISPLAY ' '.                                                         
184500     DISPLAY 'TEPITM READ : ' PA-ITM-READ-COUNT.                          
184600     DISPLAY '      WRITE : ' PA-ITM-WRITE-COUNT.                         
185300     DISPLAY ' '.                                                         
184200     DISPLAY 'TEPTRN READ : ' PA-TRN-READ-COUNT.                          
184200     DISPLAY 'TEPDKEY READ: ' PA-DKEY-READ-COUNT.                         
184300     DISPLAY '      WRITE : ' PA-TRN-WRITE-COUNT.                         
187800                                                                          
189600******************************************************************        
189700* Z9000 DB2 ABEND                                                         
189800******************************************************************        
189900 Z9000-DB2-ABEND.                                                         
190000                                                                          
190100     MOVE SQLCODE TO SQLCODE2.                                            
190200     MOVE SQLERRD(3) TO SQLERRD3.                                         
190300                                                                          
190400     DISPLAY 'Z9000-DB2-ABEND'.                                           
190500     DISPLAY '** ABEND           ** = SQLABEND'.                          
190600     DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
190700     DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
190800     DISPLAY '** TABLES AFFECTED ** = ' PV-DB2-TABLE-NAME.                
190900     DISPLAY '** OPERATION       ** = ' PV-DB2-OPERATION-MSG.             
191000     DISPLAY '** SQLCODE         ** = ' SQLCODE2.                         
191100     DISPLAY '** ROWS PROCESSED  ** = ' SQLERRD3.                         
191200     DISPLAY '** SQLERRM         ** = ' SQLERRM.                          
191300     DISPLAY '** SQLERRMC        ** = ' SQLERRMC.                         
191400                                                                          
191500******************************************************************        
191600*  THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-           
191700*  MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                
191800* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
191900*  FORMAT.                                                                
192000******************************************************************        
192100     COPY DPPD004.                                                        
192200                                                                          
192300     SET ABEND-4013-DB2-ERROR TO TRUE.                                    
192400                                                                          
192500     CALL 'ILBOABN0' USING ABEND-CODE.                                    
192400                                                                          
