       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    TFKUP003.                                                 
       AUTHOR.        D. WELLER.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  11/28/2008.                                               
       DATE-COMPILED.                                                           
                                                                                
      *****************************************************************         
      * TFKUP003 - PURGE ROWS FROM TFT_PHY_INV_ITM WHEN THE ASSOCIATED          
      *            TTFMSTR AND TTFITEM ROWS HAVE ALREADY BEEN PURGED.           
      *****************************************************************         
      * THIS PROGRAM WILL READ A FILE CONTAINING XFER-DKEY-ID AND               
      * XFER_LNE_NBR TO BE DELETED FROM TFT_PHY_INV_ITM TABLE.                  
      *****************************************************************         
      *                                                                         
      *  TABLES UPDATED ARE:                                                    
      *      TFT_PHY_INV_ITM  - TF PHYSICAL INVENTORY ITEM TABLE                
      *  FILES READ ARE:                                                        
      *      TFRD063                                                            
      *****************************************************************         
      * 11/28/08 - D WELLER         -                                           
      *    - NEW PROGRAM.                                                       
      *****************************************************************         
      * 02/10/09 - D WELLER         -                                           
      *    - STANDARDIZE COBOL                                                  
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT TF-PURGE-TFT-PHY-INV-ITM   ASSIGN TO INP01.                   
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  TF-PURGE-TFT-PHY-INV-ITM                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS TF-PURGE-TFT-PHY-INV-ITM-REC.                         
       01  TF-PURGE-TFT-PHY-INV-ITM-REC     PIC X(100).                         
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      * PC PROGRAM CONSTANTS                                           *        
      ******************************************************************        
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE                    
                                                       'TFKUP003'.              
           05  PC-JOB-NAME                  PIC X(08)  VALUE 'TFK035  '.        
           05  PC-MAXIMUM-RETRY-COUNT       PIC S9(04) VALUE +2                 
                                                        COMP SYNC.              
           05  PC-DEFAULT-TMST              PIC X(26)  VALUE                    
               '2008-01-01-00.00.00.000000'.                                    
                                                                                
      ******************************************************************        
      * PV PROGRAM VARIABLES                                           *        
      ******************************************************************        
       01  PV-WORK-VARIABLES.                                                   
           05  PV-RETRIES                   PIC S9(04) VALUE +0                 
                                                           COMP.                
           05  PV-PARAGRAPH-NAME            PIC  X(30) VALUE SPACES.            
           05  PV-DB2-OPERATION-ATTEMPTED   PIC  X(30) VALUE SPACES.            
           05  PV-SQLCODE-DISPLAY           PIC +(8)9  VALUE ZEROES.            
           05  PV-COMMITS-TAKEN             PIC S9(09) COMP-3                   
                                                       VALUE ZERO.              
           05  PV-COMMIT-INTERVAL           PIC S9(09) COMP-3                   
                                                       VALUE +5000.             
           05  PV-COMMIT-CNTR               PIC S9(04) VALUE +1                 
                                                       COMP-3.                  
           05  PV-CURRENT-DATE              PIC X(10).                          
           05  PV-CURRENT-TMST              PIC X(26).                          
                                                                                
      ******************************************************************        
      * PS PROGRAM SWITCHES                                            *        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-INPUT-SW           PIC  X(01) VALUE 'N'.               
               88  PS-END-OF-INPUT                     VALUE 'Y'.               
           05  PS-PROCESS-STOP-SW           PIC  X(01) VALUE 'N'.               
               88  PS-PROCESS-STOP                     VALUE 'Y'.               
           05  PS-FIRST-COMMIT-SW           PIC  X(01) VALUE 'Y'.               
               88  PS-FIRST-COMMIT                     VALUE 'Y'.               
           05  PS-INPUT-ROW-FOUND-SW        PIC  X(01) VALUE 'Y'.               
               88  PS-INPUT-ROW-FOUND                  VALUE 'Y'.               
               88  PS-INPUT-ROW-NOT-FOUND              VALUE 'N'.               
                                                                                
       01  ABEND-CODE                       PIC S9(04) VALUE +0                 
                                                       COMP SYNC.               
       01  CR-CHECKPOINT-RESTART-AREA.                                          
           05  CR-AREA-LEN                  PIC S9(04) VALUE +5  COMP.          
                                                                                
           05  CR-CHECKPOINT-RESTART-VAR.                                       
               10  CR-MNT-READ-CNT          PIC 9(09)  VALUE 0 COMP-3.          
                                                                                
       01  DISPLAY-EDITS.                                                       
           05  DI-MNT-CNTR                  PIC  9(09) VALUE ZEROS.             
           05  DI-COMMIT-CNTR               PIC  9(09) VALUE ZEROS.             
      *****************************************************************         
      *                                                                         
      *****************************************************************         
      *    INPUT/OUTPUT COPYBOOKS                                               
      *****************************************************************         
           COPY TFRD063.                                                        
                                                                                
      *****************************************************************         
      *    DB2 COMMUNICATION AREA                                               
      *****************************************************************         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART CONTROL TABLE                            
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTCN                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DCLGEN FOR CHECKPOINT RESTART INFORMATION TABLE                        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TCKRSTIN                                                 
           END-EXEC.                                                            
      *****************************************************************         
      *  TFT_PHY_INV_ITM TABLE                                                  
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TFT00000                                                
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           COPY SQLCA2.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      * 0000-MAIN-MODULE.                                              *        
      *  ==> PROCESSES:                                                *        
      *    - CONTROLS HIGHEST LEVEL FLOW OF PROGRAM                    *        
      ******************************************************************        
       0000-MAIN-MODULE.                                                        
           MOVE '0000-MAIN-MODULE'     TO PV-PARAGRAPH-NAME.                    
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 7000-READ-INPUT.                                             
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
            DISPLAY 'STARTING RECORD - '                                        
              TF063-PURGE-XFER-RECORD                                           
           END-IF                                                               
           PERFORM 2000-PROCESS-INPUT                                           
             UNTIL PS-END-OF-INPUT.                                             
           PERFORM 8000-SQL-COMMIT.                                             
           DISPLAY 'INPUT PROCESSING DONE'.                                     
                                                                                
           PERFORM 3000-EOJ-PROCESSING.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * 1000-INITIALIZATION.                                           *        
      ******************************************************************        
       1000-INITIALIZATION.                                                     
           MOVE '1000-INITIALIZATION ' TO PV-PARAGRAPH-NAME.                    
                                                                                
           PERFORM 8100-INIT-CHECKPOINT-SELECT.                                 
           MOVE TCKRSTCNTL-CKPT-FRQNCY-QTY                                      
                                       TO PV-COMMIT-INTERVAL.                   
      ******************************************************************        
      *  SELECT SAVED DATA TO SEE THE PROGRAM IS IN A RESTART STATUS            
      *  FROM THE RESTART INFO TABLE AND THEN ONLY PROCESS NON-COMPLETED        
      *  RECORDS AS OF THE LAST COMMIT. IF NOT, THEN THE VARIABLES WILL         
      *  CONTAIN THEIR INITIAL VALUES AND THE PROGRAM WILL PROCESS ALL          
      *  RECORDS ON THE INPUT FILE.                                             
      ******************************************************************        
           IF  TCKRSTCNTL-CKPT-STAT-CODE = '9'                                  
               PERFORM 8150-SELECT-RESTART-INFO                                 
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT                               
                                       TO CR-CHECKPOINT-RESTART-VAR             
               MOVE CR-MNT-READ-CNT    TO DI-MNT-CNTR                           
               SUBTRACT 1 FROM CR-MNT-READ-CNT                                  
               DISPLAY SPACES                                                   
               DISPLAY '** TFKUP003 IS BEING RESTARTED FROM RECORD# '           
                 CR-MNT-READ-CNT.                                               
                                                                                
           OPEN INPUT  TF-PURGE-TFT-PHY-INV-ITM.                                
           PERFORM 7000-READ-INPUT CR-MNT-READ-CNT TIMES.                       
           IF CR-MNT-READ-CNT > 0                                               
             DISPLAY 'RESTART ON RECORD KEY - ' TF063-XFER-DKEY-ID-X            
                                          ' - ' TF063-XFER-LNE-NBR-X.           
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-DATE = CURRENT DATE                              
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               SET :PV-CURRENT-TMST = CURRENT TIMESTAMP                         
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * 2000-PROCESS-INPUT.                                            *        
      *  ==> INSERT  ROWS FOR EACH ACTIVE COUNT/LOC/SKU RECORD         *        
      ******************************************************************        
       2000-PROCESS-INPUT.                                                      
           MOVE '2000-PROCESS-INPUT'   TO PV-PARAGRAPH-NAME.                    
           MOVE  TF063-XFER-DKEY-ID    TO  TFT00000-XFER-DKEY-ID.               
           MOVE  TF063-XFER-LNE-NBR    TO  TFT00000-XFER-LNE-NBR.               
           PERFORM 8300-DEL-PHY-INV-ITM.                                        
           IF PS-INPUT-ROW-FOUND THEN                                           
               ADD 1                   TO PV-COMMIT-CNTR                        
               IF PV-COMMIT-CNTR > PV-COMMIT-INTERVAL                           
                   ADD PV-COMMIT-INTERVAL                                       
                                       TO CR-MNT-READ-CNT                       
                   PERFORM 8000-SQL-COMMIT                                      
               END-IF                                                           
           END-IF.                                                              
           PERFORM 7000-READ-INPUT.                                             
                                                                                
      ******************************************************************        
      * 3000-EOJ-PROCESSING.                                           *        
      *  ==> PROCESSES:                                                *        
      *    - CLOSE FILE THAT WAS OPENED IN PARA 1000.                  *        
      *    - DISPLAYS PROCESSING TOTALS                                *        
      *    - SETS CHEKPOINT STATUS CODE TO 0 (PROGRAM NOT IN PROCESS OR*        
      *      IN RESTART STATUS.                                        *        
      ******************************************************************        
       3000-EOJ-PROCESSING.                                                     
           MOVE '3000-EOJ-PROCESSING'  TO PV-PARAGRAPH-NAME.                    
                                                                                
           CLOSE TF-PURGE-TFT-PHY-INV-ITM.                                      
                                                                                
           MOVE '0'                    TO TCKRSTCNTL-CKPT-STAT-CODE.            
           PERFORM 8160-UPDATE-CHECKPOINT-STATUS.                               
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
      *----------------------------------------------------------               
      * 7000-READ-INPUT.                                                        
      *----------------------------------------------------------               
       7000-READ-INPUT.                                                         
           MOVE '7000-READ-INPUT'      TO PV-PARAGRAPH-NAME.                    
           READ TF-PURGE-TFT-PHY-INV-ITM                                        
             INTO TF063-PURGE-XFER-RECORD                                       
           AT END SET PS-END-OF-INPUT  TO TRUE.                                 
           ADD +1                      TO DI-MNT-CNTR.                          
                                                                                
      *----------------------------------------------------------               
      * 8000-SQL-COMMIT.                                                        
      *----------------------------------------------------------               
       8000-SQL-COMMIT.                                                         
           MOVE '8000-SQL-COMMIT'      TO PV-PARAGRAPH-NAME.                    
           MOVE 'COMMIT'               TO PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
           MOVE DI-MNT-CNTR            TO CR-MNT-READ-CNT.                      
           MOVE CR-CHECKPOINT-RESTART-AREA                                      
                                       TO TCKRSTINF-WORK-STRG-DESC.             
                                                                                
           IF PS-FIRST-COMMIT                                                   
               MOVE '9'                TO TCKRSTCNTL-CKPT-STAT-CODE             
               PERFORM 8160-UPDATE-CHECKPOINT-STATUS                            
               MOVE 'N'                TO PS-FIRST-COMMIT-SW                    
           END-IF.                                                              
           EXEC SQL                                                             
             UPDATE TCKRSTINF                                                   
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC                  
              WHERE JOB_NAME  = :PC-JOB-NAME                                    
                AND PLAN_NAME = :PC-PROGRAM-NAME                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN SQLCODE = 100                                                
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP     TO TRUE                                  
               DISPLAY '** TFKUP003 CHECKPOINT'                                 
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
           EXEC SQL                                                             
               COMMIT WORK                                                      
           END-EXEC.                                                            
           DISPLAY 'COMMIT COMPLETED'.                                          
           ADD +1                      TO DI-COMMIT-CNTR.                       
           MOVE 1                      TO PV-COMMIT-CNTR.                       
                                                                                
      *----------------------------------------------------------               
      *  SELECT FROM THE CHECKPOINT RESTART CONTROL TABLE TO SEE IF             
      *  THE PROGRAM IS IN A RESTART CONDITION.                                 
      *----------------------------------------------------------               
       8100-INIT-CHECKPOINT-SELECT.                                             
           MOVE '8100-INIT-CHECKPOINT-SELECT'                                   
                                       TO  PV-PARAGRAPH-NAME.                   
           MOVE 'SELECT'               TO PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
           EXEC SQL                                                             
             SELECT                                                             
                  CKPT_STAT_CODE                                                
                 ,CKPT_FRQNCY_QTY                                               
               INTO                                                             
                 :TCKRSTCNTL-CKPT-STAT-CODE                                     
                ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                                    
               FROM  TCKRSTCNTL                                                 
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP     TO TRUE                                  
               DISPLAY '** TFKUP003 SELECT CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  SELECT FROM THE CHECKPOINT RESTART INFORMATION TABLE TO                
      *  OBTAIN THE PROGRAMS SAVED VARIABLES.                                   
      ******************************************************************        
       8150-SELECT-RESTART-INFO.                                                
           MOVE '8150-SELECT-RESTART-INFO '                                     
                                       TO  PV-PARAGRAPH-NAME.                   
           MOVE 'SELECT'               TO PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
           EXEC SQL                                                             
               SELECT                                                           
                  WORK_STRG_DESC                                                
               INTO                                                             
                 :TCKRSTINF-WORK-STRG-DESC                                      
               FROM  TCKRSTINF                                                  
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN SQLCODE = 100                                                
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP     TO TRUE                                  
               DISPLAY '** TFKUP003 SELECT XRST'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *  UPDATE THE STATUS ON THE CHECKPOINT CONTROL TABLE                      
      ******************************************************************        
       8160-UPDATE-CHECKPOINT-STATUS.                                           
           MOVE '8160-UPDATE-CHECKPOINT-STATUS'                                 
                                       TO PV-PARAGRAPH-NAME.                    
           MOVE 'UPDATE'               TO PV-DB2-OPERATION-ATTEMPTED.           
                                                                                
           EXEC SQL                                                             
             UPDATE  TCKRSTCNTL                                                 
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE                
              WHERE  JOB_NAME  = :PC-JOB-NAME                                   
                AND  PLAN_NAME = :PC-PROGRAM-NAME                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                  CONTINUE                                                      
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP     TO TRUE                                  
               DISPLAY '** TFKUP003 UPDATE CKPT'                                
                       ' HAD SQLCODE OF ' SQLCODE                               
               DISPLAY ' KEY - '   PC-JOB-NAME                                  
                                   PC-PROGRAM-NAME                              
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      *-------------------------------------------------------------            
      * 8300-DEL-PHY-INV-ITM.                                                   
      *      DELETE TFT_PHY_INV_ITM ROW.                                        
      *-------------------------------------------------------------            
       8300-DEL-PHY-INV-ITM.                                                    
           MOVE '8300-DEL-PHY-INV-ITM' TO PV-PARAGRAPH-NAME.                    
           MOVE 'DELETE'               TO PV-DB2-OPERATION-ATTEMPTED.           
           EXEC SQL                                                             
            DELETE                                                              
              FROM TFT_PHY_INV_ITM  A                                           
            WHERE A.XFER_DKEY_ID  = :TFT00000-XFER-DKEY-ID                      
              AND A.XFER_LNE_NBR  = :TFT00000-XFER-LNE-NBR                      
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
               SET PS-INPUT-ROW-FOUND  TO TRUE                                  
              WHEN SQLCODE = +100                                               
               SET PS-INPUT-ROW-NOT-FOUND                                       
                                       TO TRUE                                  
               DISPLAY 'KEY NOT FOUND IS - '   TFT00000-XFER-DKEY-ID            
                       '        LINE  IS - '   TFT00000-XFER-LNE-NBR            
              WHEN OTHER                                                        
               SET PS-PROCESS-STOP     TO TRUE                                  
               DISPLAY '** TFKUP003 DELETE TFT_PHY_INV_ITM'                     
                       ' HAD SQLCODE OF ' SQLCODE                               
               PERFORM 9999-SQL-ABEND                                           
           END-EVALUATE.                                                        
TEST****   IF DI-MNT-CNTR > 6000 THEN                                           
TEST****     DISPLAY 'INTENTIONAL ABEND - ' DI-MNT-CNTR                         
TEST****     DISPLAY TF063-PURGE-XFER-RECORD                                    
TEST****       PERFORM 9999-SQL-ABEND                                           
TEST****   END-IF.                                                              
                                                                                
      ******************************************************************        
      * 9990-DISPLAY-CONTROL-INFO.                                     *        
      *  ==> PROCESSES:                                                *        
      *    - DISPLAYS CONTROL INFORMATION AT END OF JOB                *        
      ******************************************************************        
       9990-DISPLAY-CONTROL-INFO.                                               
                                                                                
           SUBTRACT 1 FROM DI-MNT-CNTR.                                         
           DISPLAY '********************************************'.              
           DISPLAY '**** RUN STATISTICS FOR PROGRAM TFKUP003 ***'.              
           DISPLAY '****'.                                                      
           DISPLAY 'DELETE RECORDS PROCESSED:' DI-MNT-CNTR.                     
           DISPLAY 'COMMITS TAKEN           :' DI-COMMIT-CNTR.                  
           DISPLAY '****'.                                                      
           DISPLAY '**** RUN STATS END FOR PROGRAM TFKUP003  ***'.              
           DISPLAY '********************************************'.              
                                                                                
      ******************************************************************        
      * 9999-SQL-ABEND.                                                *        
      *  ==> PROCESSES:                                                *        
      *    - PERFORMS ABENDS AFTER UNEXPECTED SQLCODE                  *        
      ******************************************************************        
       9999-SQL-ABEND.                                                          
                                                                                
           MOVE SQLCODE                TO PV-SQLCODE-DISPLAY.                   
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'TFKUP003 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARAGRAPH-NAME.                            
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           PERFORM 9990-DISPLAY-CONTROL-INFO.                                   
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013                  TO ABEND-CODE.                           
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
