       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    INKUT316.                                         00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  08-22-2004.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *     INKUT316 - THIS WAS CLONED FROM INKUT289.                   00080000
      *                THIS PROGRAM HAS BEEN RE-WRITTEN TO PROCESS      00081000
      *                THE MERGED FILE BETWEEN THE PHYSICAL (TSUMINV)   00090000
      *                EXTRACT AND THE CTOFF DATABASE (INT_INV_CTOFF)   00100000
      *                EXTRACT IN THE INRD024 FORMAT AND STRIP OFF THE  00110000
      *                TOP 00100 CANDIDATES. CHANGE INCORPORATES        00120000
      *                CHANGES FOR STORE NUMBER EXPANSION (WR26600)     00130000
      *                PREVIOUS 'CHANGE HISTORY' HAS BEEN DELETED       00140000
      *                                                                 00150000
      *                CONTROL CARD (INP02) DETERMINES NUMBER OF        00160000
      *                EXCEPTIONS PER STORE WILL BE ON OUTPUT FILE      00170000
      *                                                                 00180000
      *CHANGE HISTORY:                                                  00190000
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                     00200000
      * -------  ----------  ----------------------------------------   00210000
W26600* W26600   08-22-2004  PROCESS RE-WRITE/INVENTORY STORE EXPANSION 00220000
W26600*                      - SCOTT JACKSON                            00230000
SR3403* WR03403  04-06-2012  RECOMPILE-LARGE RETAIL PROJECT             00231000
SR3403*                     - ANUJ GUPTA                                00232000
RONNA *          08-10-2021  FIX LOGIC WHEN READING A MERGE FILE        00233000
RONNA *                      AND IT IS THE END OF FILE AND THE MAX      00234000
RONNA *                      NUMBER OF EXCEPTIONS HAS NOT BEEN REACHED  00235000
      ******************************************************************00240000
                                                                        00250000
       ENVIRONMENT DIVISION.                                            00260000
       CONFIGURATION SECTION.                                           00270000
       SOURCE-COMPUTER.        IBM-3090.                                00280000
       OBJECT-COMPUTER.        IBM-3090.                                00290000
       INPUT-OUTPUT SECTION.                                            00300000
       FILE-CONTROL.                                                    00310000
                                                                        00320000
           SELECT STORE-MERGE-FILE-IN                                   00330000
               ASSIGN TO INP01.                                         00340000
                                                                        00350000
           SELECT CONTROL-FILE-IN                                       00360000
               ASSIGN TO INP02.                                         00370000
                                                                        00380000
           SELECT STORE-REPORT-FILE-OUT                                 00390000
               ASSIGN TO OUT01.                                         00400000
                                                                        00410000
       DATA DIVISION.                                                   00420000
       FILE SECTION.                                                    00430000
                                                                        00440000
       FD  STORE-MERGE-FILE-IN                                          00450000
           LABEL RECORDS ARE OMITTED                                    00460000
           RECORDING MODE IS F                                          00470000
           BLOCK CONTAINS 0 RECORDS.                                    00480000
       01  STORE-MERGE-REC-IN    PIC X(100).                            00490000
                                                                        00500000
       FD  CONTROL-FILE-IN                                              00510000
           LABEL RECORDS ARE OMITTED                                    00520000
           RECORDING MODE IS F                                          00530000
           BLOCK CONTAINS 0 RECORDS.                                    00540000
       01  CONTROL-REC-IN        PIC X(80).                             00550000
                                                                        00560000
       FD  STORE-REPORT-FILE-OUT                                        00570000
           LABEL RECORDS ARE OMITTED                                    00580000
           RECORDING MODE IS F                                          00590000
           BLOCK CONTAINS 0 RECORDS.                                    00600000
       01  STORE-REPORT-REC-OUT   PIC X(100).                           00610000
                                                                        00620000
      ******************************************************************00630000
       WORKING-STORAGE SECTION.                                         00640000
      ******************************************************************00650000
                                                                        00660000
       01  PC-PROGRAM-CONSTANTS.                                        00670000
           05  PC-PROGRAM-NAME         PIC X(20)  VALUE 'INKUT316'.     00680000
                                                                        00690000
       01  CC-CONTROL-REC.                                              00700000
           05  CC-CONTROL-COUNT        PIC 9(05)  VALUE ZERO.           00710000
           05  FILLER                  PIC X(75)  VALUE SPACES.         00720000
                                                                        00730000
       01  PV-PROGRAM-VARIABLES.                                        00740000
W26600     05  PV-MAX-PER-STORE-COUNT  PIC  9(05) VALUE ZERO.           00750000
           05  PV-STORE-INV-READ       PIC S9(09) VALUE +0  COMP.       00760000
           05  PV-LOCATION-COUNT       PIC S9(09) VALUE +0  COMP.       00770000
           05  PV-TOTAL-EXTR-WRITTEN   PIC S9(09) VALUE +0  COMP.       00780000
           05  PV-STORE-NUMBER         PIC  9(04) VALUE ZEROS.          00790000
           05  PV-HOLD-LOCATION        PIC  9(04) VALUE ZEROS.          00800000
                                                                        00810000
       01  PS-PROGRAM-SWITCHES.                                         00820000
           05  PS-STORE-INV-DONE-SW    PIC  X(01)      VALUE 'N'.       00830000
               88  PS-STORE-INV-IS-DONE                VALUE 'Y'.       00840000
               88  PS-STORE-INV-IS-NOT-DONE            VALUE 'N'.       00850000
           05  PS-FIRST-TIME-SW        PIC  X(01)      VALUE 'N'.       00860000
               88  FIRST-TIME-THRU                     VALUE 'N'.       00870000
               88  CONTINUED-PROCESSING                VALUE 'Y'.       00880000
           05  PS-CONTROL-FILE-SW      PIC  X(01)      VALUE 'N'.       00890000
               88  PS-CONTROL-FILE-EMPTY               VALUE 'Y'.       00900000
           05  PS-END-OF-LOCS-SW       PIC  X(01)      VALUE 'N'.       00910000
               88  PS-END-OF-LOCATION-RECS             VALUE 'Y'.       00920000
               88  PS-MORE-LOCATION-RECS               VALUE 'N'.       00930000
           05  PS-LOCATION-SW          PIC  X(01)      VALUE 'N'.       00940000
               88  PS-LOCATION-RECS-PROCESSED          VALUE 'Y'.       00950000
               88  PS-CONT-LOCATION                    VALUE 'N'.       00960000
                                                                        00970000
      /**************************************************************** 00980000
      * COPYBOOK - INVENTORY EXCEPTIONS BY STORE EXTRACT                00990000
      ***************************************************************** 01000000
           COPY INRD024.                                                01010000
                                                                        01020000
      ***************************************************************** 01030000
      *  INVENTORY EXCEPTIONS BY STORE EXTRACT FILE LAYOUT - WORK AREA  01040000
      ***TO BE USED AGAIN AS THE OUTPUT RECORD LAYOUT AS WELL AS INPUT  01050000
      ***************************************************************** 01060000
           COPY INRD024 REPLACING IN024 BY INWRK.                       01070000
                                                                        01080000
      /*****************************************************************01090000
      ******************************************************************01100000
       PROCEDURE DIVISION.                                              01110000
      ******************************************************************01120000
                                                                        01130000
       0100-MAINLINE-INKUT316.                                          01140000
                                                                        01150000
           PERFORM 1000-INITIALIZATION.                                 01160000
                                                                        01170000
           PERFORM 2000-PROCESS-STORE-MERGE-RECS                        01180000
               UNTIL PS-STORE-INV-IS-DONE                               01190000
                  OR PS-CONTROL-FILE-EMPTY.                             01200000
                                                                        01210000
           PERFORM 8000-EOJ.                                            01220000
                                                                        01230000
       9999-EXIT-PROGRAM.                                               01240000
           STOP RUN.                                                    01250000
                                                                        01260000
      /*****************************************************************01270000
      * 1000-INITIALIZATION.                                            01280000
      ******************************************************************01290000
       1000-INITIALIZATION.                                             01300000
                                                                        01310000
           DISPLAY '******************************************'.        01320000
           DISPLAY '*  START OF INKUT316                     *'.        01330000
           DISPLAY '******************************************'.        01340000
                                                                        01350000
           OPEN  INPUT STORE-MERGE-FILE-IN                              01360000
                 INPUT CONTROL-FILE-IN                                  01370000
                OUTPUT STORE-REPORT-FILE-OUT.                           01380000
                                                                        01390000
           PERFORM 4000-READ-MERGE-FILE.                                01400000
           IF PS-STORE-INV-IS-DONE                                      01410000
               DISPLAY '*************************************'          01420000
               DISPLAY '**  PROGRAM = INKUT316'                         01430000
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             01440000
               DISPLAY '**'                                             01450000
               DISPLAY '**  NO EXCEPT RECORD TO PROCESS'                01460000
               DISPLAY '**  INP01'                                      01470000
               DISPLAY '*************************************'          01480000
           END-IF.                                                      01490000
                                                                        01500000
           PERFORM 4100-READ-CONTROL-FILE.                              01510000
           IF PS-CONTROL-FILE-EMPTY                                     01520000
               DISPLAY '*************************************'          01530000
               DISPLAY '**  PROGRAM = INKUT316'                         01540000
               DISPLAY '**  PARAGRAPH = 1000-INITIALIZTION'             01550000
               DISPLAY '**'                                             01560000
               DISPLAY '**  NO CONTROL RECORD COUNT WAS FOUND'          01570000
               DISPLAY '**  INP02'                                      01580000
               DISPLAY '*************************************'          01590000
           END-IF.                                                      01600000
                                                                        01610000
      /*****************************************************************01620000
      * 2000-PROCESS-STORE-MERGE-RECS.                                  01630000
      *   EACH LOCATION IS PROCESSED UNTIL ##### RECORDS ARE WRITTEN    01640000
      ******************************************************************01650000
       2000-PROCESS-STORE-MERGE-RECS.                                   01660000
                                                                        01670000
           MOVE IN024-STORE-NUMBER   TO PV-STORE-NUMBER                 01680000
                                        PV-HOLD-LOCATION                01690000
           MOVE +0                   TO PV-LOCATION-COUNT               01700000
                                                                        01710000
           SET PS-CONT-LOCATION      TO TRUE.                           01720000
                                                                        01730000
           PERFORM 2100-PROCESS-LOCATION                                01740000
RONNA          UNTIL PS-LOCATION-RECS-PROCESSED                         01750000
RONNA             OR PS-STORE-INV-IS-DONE.                              01750100
      *                                                                 01760000
      /*****************************************************************01770000
      * 2100-PROCESS-LOCATION.                                          01780000
      *   PROCESS EACH STORE FROM THE STORE-SORTED INPUT FILE.          01790000
      *   BASED ON THE INPUT CONTROL CARD, THE STORE WILL WRITE THE     01800000
      *    SPECIFIC NUMBER OF RECORDS PER STORE (UNLESS THE NUMBER OF   01810000
      *    RECORDS READ IS LESS THAN THE MAX - THEN END THERE)          01820000
RTL0  *   BYPASS RECORDS WHERE THE SYSTEM PRICE = $ 0.00                01830000
      ******************************************************************01840000
       2100-PROCESS-LOCATION.                                           01850000
                                                                        01860000
           IF PV-LOCATION-COUNT = PV-MAX-PER-STORE-COUNT                01870000
                                                                        01880000
               PERFORM 2300-COMPLETE-LOC-PROCESSING                     01890000
                                                                        01900000
               SET PS-LOCATION-RECS-PROCESSED TO TRUE                   01910000
                                                                        01920000
           ELSE                                                         01930000
                IF PV-STORE-NUMBER > PV-HOLD-LOCATION                   01940000
                                                                        01950000
                   MOVE 'Y' TO PS-LOCATION-SW                           01960000
               ELSE                                                     01970000
RTL0  *            IF IN024-SYSTEM-PRICE > 0                            01980000
                       MOVE IN024-INV-EXCEPTS-STR-REC  TO               01990000
                            INWRK-INV-EXCEPTS-STR-REC                   02000000
                                                                        02010000
                       PERFORM 5000-WRITE-EXTRACT-REC                   02020000
                                                                        02030000
                       PERFORM 4000-READ-MERGE-FILE                     02040000
RTL0  *            ELSE                                                 02050000
RTL0  *                PERFORM 4000-READ-MERGE-FILE                     02060000
RTL0  *            END-IF                                               02070000
               END-IF                                                   02080000
           END-IF.                                                      02090000
                                                                        02100000
      /**************************************************************** 02110000
      * 2300-COMPLETE-LOC-PROCESSING                                    02120000
      *   PROCESS INPUT FILE UNTIL END-OF-FILE OR NEXT LOCATION FOUND   02130000
      ***************************************************************** 02140000
       2300-COMPLETE-LOC-PROCESSING.                                    02150000
                                                                        02160000
           IF PV-STORE-NUMBER > PV-HOLD-LOCATION                        02170000
               SET PS-LOCATION-RECS-PROCESSED TO TRUE                   02180000
           ELSE                                                         02190000
               PERFORM 4000-READ-MERGE-FILE                             02200000
                   UNTIL PV-STORE-NUMBER > PV-HOLD-LOCATION             02210000
                      OR PS-STORE-INV-IS-DONE                           02220000
           END-IF.                                                      02230000
                                                                        02240000
                                                                        02250000
                                                                        02260000
      /**************************************************************** 02270000
      * 4000-READ-MERGE-FILE.                                           02280000
      * INPUT FILE IS SORTED IN CORRECT ORDER.                          02290000
      ***************************************************************** 02300000
       4000-READ-MERGE-FILE.                                            02310000
                                                                        02320000
           READ STORE-MERGE-FILE-IN NEXT                                02330000
               INTO IN024-INV-EXCEPTS-STR-REC                           02340000
             AT END                                                     02350000
                 MOVE 'Y' TO PS-STORE-INV-DONE-SW                       02360000
             NOT AT END                                                 02370000
                 MOVE IN024-STORE-NUMBER TO PV-STORE-NUMBER             02380000
                 ADD +1   TO PV-STORE-INV-READ                          02390000
           END-READ.                                                    02400000
                                                                        02410000
      /**************************************************************** 02440000
      * 4100-READ THE CONTROL FILE DENOTING MAX RECORDS/LOC TO WRITE    02450000
      ***************************************************************** 02460000
       4100-READ-CONTROL-FILE.                                          02470000
                                                                        02480000
           READ CONTROL-FILE-IN                                         02490000
               INTO CC-CONTROL-REC                                      02500000
             AT END  MOVE 'Y' TO PS-CONTROL-FILE-SW                     02510000
             NOT AT END                                                 02520000
                 MOVE CC-CONTROL-COUNT TO PV-MAX-PER-STORE-COUNT        02530000
           END-READ.                                                    02540000
      *                                                                 02550000
      /*****************************************************************02560000
      * 5000-WRITE-EXTRACT-REC.                                         02570000
      * WRITE THE TOP EXCEPTION ITEMS. MAX RECORDS/LOC DEFINED BY INP02 02580000
      ******************************************************************02590000
       5000-WRITE-EXTRACT-REC.                                          02600000
                                                                        02610000
           WRITE STORE-REPORT-REC-OUT                                   02620000
               FROM INWRK-INV-EXCEPTS-STR-REC                           02630000
                                                                        02640000
           ADD +1 TO PV-TOTAL-EXTR-WRITTEN.                             02650000
           ADD +1 TO PV-LOCATION-COUNT.                                 02660000
                                                                        02670000
      /*****************************************************************02680000
      * 8000-EOJ.  FINAL PROCESSING FOR THE PROGRAM.                    02690000
      ******************************************************************02700000
       8000-EOJ.                                                        02710000
                                                                        02720000
           DISPLAY ' '.                                                 02730000
           DISPLAY '*** RECORD COUNT SUMMARY'.                          02740000
           DISPLAY 'MAXIMUM RECORDS CREATED PER LOC = '                 02750000
                 PV-MAX-PER-STORE-COUNT.                                02760000
           DISPLAY 'INVENTORY EXTRACT RECORDS READ  = '                 02770000
                 PV-STORE-INV-READ.                                     02780000
           DISPLAY 'INVENTORY EXTRACT RECORDS WRITTEN  = '              02790000
                 PV-TOTAL-EXTR-WRITTEN.                                 02800000
                                                                        02810000
           DISPLAY '******************************************'.        02820000
           DISPLAY '*  END OF INKUT316                       *'.        02830000
           DISPLAY '******************************************'.        02840000
                                                                        02850000
           CLOSE STORE-MERGE-FILE-IN                                    02860000
                 CONTROL-FILE-IN                                        02870000
                 STORE-REPORT-FILE-OUT.                                 02880000
                                                                        02890000
