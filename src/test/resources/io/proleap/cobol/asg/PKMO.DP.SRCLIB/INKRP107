       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.              INKRP107.                                       
       AUTHOR.                  MARC BLAZICH - GREENBRIER & RUSSEL.             
       INSTALLATION.            KOHLS DEPARTMENT STORES.                        
       DATE-WRITTEN.            10/22/93.                                       
       DATE-COMPILED.                                                           
      ******************************************************************        
      *  DETERMINE WHICH STORES CAN BOOK INVENTORY                              
      ******************************************************************        
      ******************************************************************        
      *  CHANGED: 04/23/98     JEFF HARTIG     WR# 6041                *        
      *                                                                *        
      *  PGM CHANGED FOR Y2K COMPLIANCE. EXPAND CONTROL CARD DATE TO   *        
      *  ADD CENTURY (MMDDYYYY)                                        *        
      ******************************************************************        
      *  CHANGED: 03/15/99     MARIA KLAMIK    WR# 9999                *        
      *                                                                *        
      *  CHANGED PROGRAM TO CHECK FOR SALES ADJ COMPLETED INDICATOR    *        
      *  ON TINVPAR TABLE - WHEN THE INDICATOR IS 'N' WRITE OUT ERROR  *        
      *  MESSAGE.                                                      *        
      ******************************************************************        
      *  CHANGED: 10/25/99     ROLLIN KRING    WR# 20245               *        
      *                                                                *        
      *  CHANGED PROGRAM TO NOT SELECT DC'S OR FAKE-OUT STORES AS UNIT *        
      *  BOOKING CANDIDATES.  DC'S IDENTIFIED BY 'N' IN SCAN-INV-IND   *        
W33304*  AND 'NS' SCHD-PHY-CNT-CDE.                                    *        
      *  FAKE-OUT STORES IDENTIFIED BY 'Y' IN SCAN-INV-IND AND         *        
W33304*  'EI' IN SCHD-PHY-CNT-CDE. BOTH FIELDS ARE IN TINVPAR.         *        
      ******************************************************************        
20246 *  CHANGED: 11/01/99     ROLLIN KRING    WR# 20246               *        
20246 *                                                                *        
20246 *  REVISED TO USE THE TINVPAR TABLE FOR UPDATING THE UNIT BOOKING*        
20246 *  PROCESS STATUS CODE.REMOVED THE BOOKINGS VSAM FILE. A VALUE OF*        
20246 *  'S' WILL BE UPDATED IN THE UNIT BOOKINGS PROCESS STAT CODE TO *        
20246 *  INDICATE THE STORE WAS SELECTED AS A CANDIDATE TO BOOK. VALID *        
20246 *  VALUES FOR THE UNIT BOOKINGS PROCESS STATUS CODE ARE:         *        
W33304*  SL - SELECTED AS CANDIDATE                                    *        
W33304*  AP - SELECTED BY USER TO BOOK(APPROVED)                       *        
W33304*  IP - BOOKING IN PROCESS                                       *        
W33304*  TC - TRANSACTION CREATED                                      *        
20246 *  REMOVED BOOKED-STORES INPUT FILE SINCE TINVPAR WILL BE USED TO*        
20246 *  DETERMINE WHICH STORES ARE BOOKED.                            *        
      ******************************************************************        
20246 *  CHANGED: 03/23/00     LINDA MOYER     WR# 20925               *        
20246 *                                                                *        
20246 *  FOR MAY INVENTORY CHANGE THE CRITEIA FOR POTENTIAL CANDIDATES *        
20246 *  1) BEFORE A SCANNING STORE CAN BE A CANDIDATE FOR BOOKING,    *        
20246 *  DETERMINE WHETHER IT HAS ON-LINE SHEETS AND IF THEY HAVE BEEN *        
20246 *  PROCESSED.                                                    *        
20246 *  2) CENTRAL STOCK LOCATIONS WILL BE CONSIDERED FOR BOOKINGS    *        
20246 *  3) CLOSED STORES WILL BE CANDIDATES 3 DAYS AFTER THE INVENTORY*        
20246 *  HAS BEEN DONE.  THIS WILL ALLOW A  CLEAR OUT OF THE UNIT AND  *        
20246 *  RETAIL VALUES FOR THE CLOSED STORE.                           *        
20246 *  4) ONLY ALLOW STORES WHERE THE UNT-BKG-STAT IS NOT AN 'S',    *        
20246 *     'I' OR 'A', THIS WILL ALLOW FOR THE CONTINUED PROCESSING   *        
20246 *     PRIOR TO THE COMPLETION OF INK365/045 AND 380.             *        
      ******************************************************************        
21015 *  CHANGED: 02/20/2001  ROD JANSSEN      WR# 21015               *        
  |   *           CHANGE THE DERIVED BOOK DATE # OF DAYS AND DATE TO   *        
  |   *           REFLECT THE ACTL_INV_DTE + 3 DAY.......              *        
21015 *                                                                *        
      *                                                                *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      * 22000    02-28-2001  CHANGE TO ALLOW A STORE TO BECOME BOOKING *        
      *                      CANDIDACY IF THE STOCKROOM WITHDRAWAL IND *        
      *                      HAS BEEN SET TO 'N'.  THIS CHANGES ALSO   *        
      *                      REMOVES THE WAIT FOR PLAN BOOK DATE BEFORE*        
      *                      BECOMING ELIGIBLE.      -V. LEE YANG      *        
      *                                                                *        
      ******************************************************************        
SJ0301*  CHANGED: 03/20/2001   S. JACKSON      CHG ID: SJ0301          *        
      *                                                                *        
      *  ADDED FUNCTIONALITY TO ALLOW CENTRAL STOCK/RETURN-TO-VENDOR   *        
      *  STORE TYPES TO HAVE ACTL_INV_RECD_DTE SET PROGRAMATICALLY.    *        
      *  3 DAYS AFTER THE MAX(PLND_INV_DTE) HAVE PASSED, ALL 'CS'/'RV' *        
      *  STORE TYPES WILL HAVE ACTL_RECD DATE SET (IF NOT SET ALREADY).*        
      *                                                                *        
      *  CS/RV WILL ALREADY HAVE THIS DATE SET IF A FILE OR 'SHEETS'   *        
      *  HAVE BEEN RUN THROUGH INK300 EDITS/UPDATES TO TSUMINV/TINVDTL.*        
      *  CENTRAL STOCK LOCATIONS CANNOT BECOME BOOKING CANDIDATES UNTIL*        
      *  THE ACTL_INV_RECD_DTE HAS BEEN SET. RETURN-TO-VENDOR SITES    *        
      *  WILL NOT BOOK UNITS AND WILL NEVER BE A CANDIDATE.            *        
      ******************************************************************        
W05241*  CHANGED: 05/24/2001  ROD JANSSEN      WR# 99999               *        
W05241*           CHANGE THE DERIVED BOOK DATE # OF DAYS AND DATE TO   *        
W05241*           REFLECT THE ACTL_INV_DTE + 0 DAYS......              *        
      ******************************************************************        
RB0103*  CHANGED: 01/22/2003  RONNA BUTZ       WR# 99999               *        
RB0103*           LP WANTS TO SEE CANDIDATES FOR UB WHEN THE ACTUAL    *        
RB0103*           RECEIVED INVENTORY DATE IS EQUAL TO OR LESS THAN              
RB0103*           THE PROCESSING DATE.  THIS IS TO CATCH ANY STORES             
RB0103*           THAT PROCESSED INK300 AT THE 2:00 AM CYCLE.                   
RM0103******************************************************************        
RM0103*  CHANGED: 01/27/2003  RONNA BUTZ       WR# 99999               *        
RM0103*           NEEDED TO MODIFY CHANGES FROM RB0102 SO THAT THE     *        
RM0103*           CLOSED STORES DO NOT SHOW UP ON THE REPORT.          *        
RM0103*                                                                         
RM0103*           ALSO, ADDED THE ACTUAL INVENTORY DATE TO THE REPORT.          
W21732******************************************************************        
W21732*  CHANGED: 03/21/2003  RONNA BUTZ       WR# 21732               *        
W21732*  IF THE LOCATION IS A CENTRAL STOCK LOCATION AND SHEETS ARE             
W21732*  NOT REQUIRED, THEN ALLOW THE LOCATION TO BE A UNIT BOOKING             
W21732*  CANDIDATE                                                              
W21732*                                                                         
W21732*  ALSO FIXED A BUG FOR CHECKING SHEET PROCESSING FOR A DS                
W21732*  LOCATION SO THAT IF A STORE IS A POSSIBLE CANDIDATE ON THE DAY         
W21732*  THE INVENTORY RECIEVED DATE IS SET, IT WILL CHECK FOR SHEET            
W21732*  PROCESSING.                                                            
W21732*                                                                         
R21732*  ALSO ALLOW E-COMMERCE LOCATION 873 COME UP AS A CANDIDATE.             
R21732*  THIS LOCATION IS SET UP LIKE A CENTRAL STOCK, AND THE RECEIVING        
R21732*  DATE WILL GET SET WHEN THE LOCATION 873 IS PROCESSED THROUGH           
R21732*  THE CENTRAL STOCK PROCESSING.                                          
R21732*                                                                         
X21732*  ALSO, ALLOW E-COMMERCE LOCATION 873 TO CHECK SHEETS COMPLETED          
X21732*  INDICATOR                                                              
W26600*                                                                         
W26600*  W26600  07-  -2004  PHYSICAL INVENTORY STORE EXPANSION                 
W26600*                      M JOHNSON                                          
W26600*                                                                         
      ******************************************************************        
W26006*  CHANGED: 02/24/2005  ERIN SEARL       WR# 26006               *        
W26006*           REMOVE REFERENCES TO OBSOLETE STORE TABLE.           *        
W28545* W28545   08-22-2005 ROD JANSSEN THE NEW TRANSFER PROCESS                
844762*P00844762 01-30-2006 ROD JANSSEN SQL -305 ON XMT_LOC FOR LOC 400         
      ******************************************************************        
28545 *  CHANGED: 06/28/2006  J SELWITSCHKA    WR# 28545               *        
28545 *           DEPARTMENT LEVEL INVENTORY PROJECT                   *        
W33304******************************************************************        
W33304*  CHANGED: 06-20-2008  JILL LIN         WR# 33304               *        
W33304*           HOLDING INVENTORY LEDGER OPEN ACROSS PERIODS         *        
W33304*           INVENTORY SHORTAGE REPORTING                         *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD-FILE                                             
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT STORES-TO-BOOK-REPORT                                         
               ASSIGN TO RPT01.                                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD-FILE                                                    
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  CONTROL-CARD-REC        PIC X(080).                                  
                                                                                
       FD  STORES-TO-BOOK-REPORT                                                
           LABEL RECORDS ARE OMITTED                                            
           RECORD CONTAINS 133 CHARACTERS                                       
           RECORDING  MODE IS F                                                 
           DATA RECORD IS REPORT-LINE.                                          
       01  REPORT-LINE.                                                         
           05  REPORT-LINE-CC    PIC X(001).                                    
           05  REPORT-LINE-DATA  PIC X(132).                                    
           05  FILLER REDEFINES  REPORT-LINE-DATA.                              
               10  FILLER        PIC X(131).                                    
               10  REPORT-HEADER-FONT-INDICATOR                                 
                                 PIC X(001).                                    
                                                                                
       EJECT                                                                    
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CC-CONTROL-CARD.                                                     
           05  CC-DATE.                                                         
               10  CC-MM               PIC X(02).                               
                   88  CC-MM-VALID         VALUE '01' THRU '12'.                
               10  CC-DD               PIC X(02).                               
                   88  CC-DD-VALID         VALUE '01' THRU '31'.                
               10  CC-YY               PIC X(04).                               
           05  FILLER                  PIC X(01).                               
           05  CC-STORE-CTR            PIC 99.                                  
           05  FILLER                  PIC X(69).                               
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-TINVPAR-ROWS-UPDATED                                          
                                     PIC S9(09)    COMP-3 VALUE +0.             
                                                                                
                                                                                
      *****************************************************************         
      *  SQL ABEND ROUTINE WORKING STORAGE                                      
      *****************************************************************         
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  HEADER LINE                                                            
      ******************************************************************        
           COPY DPWS133O.                                                       
                                                                                
       01  PS-SWITCHES.                                                         
           05  END-OF-TINVPAR-SW           PIC X       VALUE 'N'.               
               88  END-OF-TINVPAR                      VALUE 'Y'.               
           05  PS-BOOKED-STORE-SW          PIC X       VALUE 'N'.               
               88  PS-END-OF-BOOKED-STORES             VALUE 'Y'.               
JS0500     05  PS-LOCATION-IS-CAND-SW      PIC X       VALUE ' '.               
JS0500         88  PS-LOCATION-IS-CAND                 VALUE 'Y'.               
JS0500         88  PS-LOCATION-IS-NOT-CAND             VALUE 'N'.               
JS0500     05  PS-PS-BY-PASS-LOCATION-SW   PIC X       VALUE ' '.               
JS0500         88  PS-PROCESS-LOCATION                 VALUE 'Y'.               
JS0500         88  PS-BY-PASS-LOCATION                 VALUE 'N'.               
SJ0101     05  PS-CENTRAL-STOCK-SW         PIC X       VALUE 'N'.               
SJ0101         88  PS-NOT-CS-LOCATION                  VALUE 'N'.               
SJ0101         88  PS-CS-LOCATION                      VALUE 'Y'.               
R21732     05  PS-ECOM-SW                  PIC X       VALUE 'N'.               
R21732         88  PS-NOT-ECOM-LOCATION                VALUE 'N'.               
R21732         88  PS-ECOM-LOCATION                    VALUE 'Y'.               
SJ0101     05  PS-RETURN-TO-VENDOR-SW      PIC X       VALUE 'N'.               
SJ0101         88  PS-NOT-RV-LOCATION                  VALUE 'N'.               
SJ0101         88  PS-RV-LOCATION                      VALUE 'Y'.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX           PIC S9(03)   VALUE +57.                    
20246      05  PC-PROGRAM-ID         PIC X(08)    VALUE 'INKRP107'.             
20246      05  PC-EMPTY-DATE         PIC X(10)    VALUE '9999-09-09'.           
W33304     05  PC-UNIT-BK-STAT-CDE   PIC X(02)    VALUE 'SL'.                   
           05  PC-REPORT-CC            PIC X(01)  VALUE SPACES.                 
           05  PC-NO-STORE-READY-MSG.                                           
RM0103*        10  FILLER                  PIC X(48)   VALUE                    
RM0103         10  FILLER                  PIC X(70)   VALUE                    
               'NO STORES ARE READY TO BE BOOKED'.                              
           05  PC-INVALID-DETAIL-MSG.                                           
RM0103*        10  FILLER                  PIC X(97)   VALUE                    
RM0103         10  FILLER                  PIC X(70)   VALUE                    
               'OUTSTANDING INVALID DETAIL INVENTORY ITEMS EXIST.'.             
           05  PC-STKRMW-MSG.                                                   
RM0103*        10  FILLER                  PIC X(97)   VALUE                    
RM0103         10  FILLER                  PIC X(70)   VALUE                    
               'STOCKROOM WITHDRAWAL DATE NOT REACHED YET.       '.             
           05  PC-PLND-BOOK-DTE-MSG.                                            
               10  FILLER                  PIC X(49)   VALUE                    
               'PLANNED UNIT BOOK DATE S/B MOVED OUT - INVENTORY '.             
RM0103*        10  FILLER                  PIC X(48)   VALUE                    
RM0103         10  FILLER                  PIC X(21)   VALUE                    
               'NOT RECEIEVED AS YET.'.                                         
           05  PC-MAX-STORES-MSG.                                               
RM0103*        10  FILLER                  PIC X(97)   VALUE                    
RM0103         10  FILLER                  PIC X(70)   VALUE                    
               'MAX STORES ALREADY BEING BOOKED IN THIS RUN.     '.             
           05  PC-SALE-ADJ-MSG.                                                 
RM0103*        10  FILLER                  PIC X(97)   VALUE                    
RM0103         10  FILLER                  PIC X(70)   VALUE                    
               'SALES ADJUSTMENTS HAVE NOT OCCURRED.             '.             
JS0500     05  PC-CLOSED-STR-MSG.                                               
RM0103*        10  FILLER                  PIC X(38) VALUE                      
RM0103         10  FILLER                  PIC X(37) VALUE                      
                   'CLOSED STORE NOT READY TO BOOK UNTIL '.                     
               10  PC-CLOSED-TARGET        PIC X(10) VALUE SPACE.               
RM0103*        10  FILLER                  PIC X(18) VALUE SPACE.               
JS0500     05 PC-SHEET-NOT-PROC.                                                
               10  FILLER                  PIC X(97)   VALUE                    
               'ON-LINE SHEETS HAVE NOT BEEN PROCESSED YET       '.             
W21732     05 PC-SHEET-NOT-ENTRD.                                               
W21732         10  FILLER                  PIC X(97)   VALUE                    
W21732         'ON-LINE SHEETS HAVE NOT BEEN ENTERED YET         '.             
W21732     05 PC-CS-SHEET-NOT-PROC.                                             
W21732         10  FILLER                  PIC X(97)   VALUE                    
W21732         'CENTRAL STOCK SHEETS HAVE NOT BEEN PROCESSED YET '.             
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-YES                        PIC X       VALUE 'Y'.             
           05  PV-PROCESSING-DATE-DAYS       PIC S9(9)   COMP.                  
           05  PV-ACTL-PLUS-STKRM-DAYS       PIC S9(9)   COMP.                  
           05  PV-STKRM-DAY-QTY              PIC  9(5).                         
           05  PV-CHECK                      PIC S9(4)   COMP.                  
JS0500     05  PV-CHECK2                     PIC S9(4)   COMP.                  
           05  PV-STORE-CTR                  PIC  9(4)   VALUE 0.               
           05  PV-OPER-ATTEMPTED             PIC X(25).                         
           05  PV-PAGE-COUNT                 PIC 9(5)    VALUE 0.               
           05  PV-LINE-COUNT                 PIC 9(2)    VALUE 0.               
           05  PV-NEW-CANDIDATES             PIC 9(5)    VALUE 0.               
           05  PV-PROCESSING-DATE            PIC X(10).                         
           05  PV-PROCESSING-DATE-RDF REDEFINES PV-PROCESSING-DATE.             
               10  PV-PROCESSING-YY          PIC X(04).                         
               10  PV-PROC-SL1               PIC X(01).                         
               10  PV-PROCESSING-MM          PIC X(02).                         
               10  PV-PROC-SL2               PIC X(01).                         
               10  PV-PROCESSING-DD          PIC X(02).                         
           05  PV-TARGET-DATE                PIC X(10).                         
           05  PV-TARGET-DATE-RDF REDEFINES PV-TARGET-DATE.                     
               10  PV-TARGET-YY              PIC X(04).                         
               10  FILLER                    PIC X(01).                         
               10  PV-TARGET-MM              PIC X(02).                         
               10  FILLER                    PIC X(01).                         
               10  PV-TARGET-DD              PIC X(02).                         
           05  PV-TODAYS-DATE.                                                  
               10  PV-YY                     PIC 9(02)   VALUE ZERO.            
               10  PV-MM                     PIC 9(02)   VALUE ZERO.            
               10  PV-DD                     PIC 9(02)   VALUE ZERO.            
           05  PV-TODAYS-TIME.                                                  
               10  PV-HR                     PIC 9(02)   VALUE ZERO.            
               10  PV-MIN                    PIC 9(02)   VALUE ZERO.            
               10  PV-SEC                    PIC 9(02)   VALUE ZERO.            
               10  FILLER                    PIC 9(02)   VALUE ZERO.            
           05  PV-MAX-RETRIES                PIC S9(05)  VALUE +3               
                                                         COMP-3.                
           05  PV-PARAGRAPH-NAME             PIC X(30)   VALUE SPACES.          
           05  PV-SQL-SUB                    PIC S9(05)  VALUE +0               
                                                         COMP-3.                
           05  PV-DB2-OPER-ATTEMPTED         PIC X(30)   VALUE SPACES.          
           05 PV-INVALID-SW                  PIC X       VALUE SPACES.          
              88  PV-VALID                               VALUE ' '.             
              88  PV-INVALID                             VALUE 'Y'.             
                                                                                
JS0500*    05 PV-INENTY-STR-NBR       COMP   PIC S9(4)   VALUE +0.              
W26600*    05 PV-INVPAR-STR-NBR-NUM          PIC 9(4)    VALUE 0.               
JS0500*    05 PV-INVPAR-STR-NBR-CHAR REDEFINES                                  
W26600*       PV-INVPAR-STR-NBR-NUM          PIC X(4).                          
                                                                                
SJ0301     05 PV-MAX-PLND-DTE-PLUS           PIC X(10)   VALUE SPACES.          
                                                                                
RM0103     05  PV-REFORMAT-DATE.                                        MLKRP030
RM0103         10  PV-REFORMAT-MM            PIC X(02)    VALUE '  '.   MLKRP030
RM0103         10  FILLER                    PIC X        VALUE '/'.    MLKRP030
RM0103         10  PV-REFORMAT-DD            PIC X(02)    VALUE '  '.   MLKRP030
RM0103         10  FILLER                    PIC X        VALUE '/'.    MLKRP030
RM0103         10  PV-REFORMAT-CCYY          PIC X(04)    VALUE '  '.   MLKRP030
                                                                                
RM0103     05  PV-HLD-DATE.                                             MLKRP030
RM0103         10  PV-HLD-CCYY               PIC X(04)    VALUE '  '.   MLKRP030
RM0103         10  PV-FILLER                 PIC X(01)    VALUE ' '.    MLKRP030
RM0103         10  PV-HLD-MM                 PIC X(02)    VALUE '  '.   MLKRP030
RM0103         10  PV-FILLER                 PIC X(01)    VALUE ' '.    MLKRP030
RM0103         10  PV-HLD-DD                 PIC X(02)    VALUE '  '.   MLKRP030
                                                                                
       01  RH-REPORT-HEADING-ONE.                                               
           05  FILLER                      PIC X(50) VALUE SPACES.              
           05  FILLER                      PIC X(83) VALUE                      
               'STORES WHICH COULD NOT BOOK UNITS'.                             
                                                                                
       01  RH-REPORT-HEADING-TWO.                                               
           05  FILLER                      PIC X(05)  VALUE '0'.                
W26600     05  FILLER                      PIC X(33)  VALUE 'STORE'.            
RM0103     05  FILLER                      PIC X(10)  VALUE                     
RM0103        'INV DTE '.                                                       
RM0103     05  FILLER                      PIC X(03)  VALUE SPACE.              
           05  FILLER                      PIC X(70)  VALUE                     
              'REASON NOT BOOKED'.                                              
RM0103     05  FILLER                      PIC X(12)  VALUE SPACES.             
                                                                                
       01  DL-DETAIL-LINE.                                                      
           05  FILLER                      PIC X(05)  VALUE ' '.                
           05  DL-DATA.                                                         
W26600         10  DL-STORE-NBR            PIC 9(04).                           
               10  FILLER                  PIC X(03)  VALUE ' - '.              
               10  DL-STORE-NAME           PIC X(25).                           
               10  FILLER                  PIC X(01)  VALUE SPACE.              
RM0103         10  DL-ACTL-INV-DTE         PIC X(10).                           
RM0103         10  FILLER                  PIC X(03)  VALUE SPACES.             
RM0103*        10  DL-REASON               PIC X(97).                           
RM0103         10  DL-REASON               PIC X(70).                           
RM0103         10  FILLER                  PIC X(12)  VALUE SPACES.             
                                                                                
       01  DL-DETAIL-LINE2.                                                     
           05  FILLER                      PIC X(01)  VALUE '0'.                
           05  FILLER                  PIC X(03)  VALUE SPACES.                 
           05  FILLER                  PIC X(03)  VALUE SPACES.                 
           05  FILLER                  PIC X(25)  VALUE SPACES.                 
           05  FILLER                  PIC X(01)  VALUE SPACES.                 
           05  DL-NEW-CANDIDATES       PIC ZZ,Z99 VALUE ZERO.                   
           05  FILLER                  PIC X(90) VALUE                          
                   '  STORES WERE SELECTED AS NEW CANDIDATES'.                  
                                                                                
       01  RE-END-OF-REPORT-MARKER.                                             
           05  FILLER                      PIC X     VALUE '0'.                 
           05  FILLER                      PIC X(51) VALUE ALL '*'.             
           05  FILLER                      PIC X(29) VALUE                      
               '  E N D   O F   R E P O R T  '.                                 
           05  FILLER                      PIC X(51) VALUE ALL '*'.             
                                                                                
                                                                                
       01  ABEND-CODE                          PIC S9(04)  VALUE +0             
                                                           COMP SYNC.           
           88  ABEND-4003                                  VALUE +4003.         
       EJECT                                                                    
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
W26600         INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVDTL                                                  
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
               INCLUDE TINENTY                                                  
           END-EXEC.                                                            
                                                                                
28545 *    EXEC SQL                                                             
28545 *        INCLUDE TMLCNTL                                                  
28545 *    END-EXEC.                                                            
28545 *                                                                         
28545 *    EXEC SQL                                                             
28545 *        INCLUDE TDTATTR                                                  
28545 *    END-EXEC.                                                            
28545      EXEC SQL                                                             
28545          INCLUDE TINCNTL                                                  
28545      END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL   DECLARE TINVPAR_CURSOR CURSOR FOR                         
                                                                                
28545 *        SELECT LOC_NBR                                                   
28545          SELECT A.INV_ID                                                  
28545                ,LOC_NBR                                                   
                     ,ACTL_INV_DTE                                              
                     ,PLND_UNIT_BK_DTE                                          
                     ,ACTL_INV_RECD_DTE                                         
28545 *              ,STKRM_DAY_QTY                                             
28545                ,A.STKRM_DAY_QTY                                           
                     ,SCAN_INV_IND                                              
      *-->            ** DERIVED BOOK DATE (IN #DAYS)                           
W05241               ,DAYS(ACTL_INV_DTE)                                        
W05241**********     ,DAYS(ACTL_INV_DTE) + 3                                    
21015 **********     ,DAYS(ACTL_INV_DTE) + STKRM_DAY_QTY                        
      *-->            ** DERIVED BOOK DATE (AS A DATE)                          
W05241               ,DATE(DAYS(ACTL_INV_DTE))                                  
W05241**********     ,DATE(DAYS(ACTL_INV_DTE) + 3)                              
21015 **********     ,DATE(DAYS(ACTL_INV_DTE) + STKRM_DAY_QTY)                  
      *-->            ** PROCESSING DATE (IN #DAYS)                             
                     ,DAYS(:PV-PROCESSING-DATE)                                 
                     ,SLS_ADJ_CPLD_IND                                          
20245                ,SCAN_INV_IND                                              
W33304               ,SCHD_PHY_CNT_CDE                                          
20245                ,SHEET_INV_CPLD_IND                                        
20245                ,UNT_BKG_STAT_CDE                                          
22000                ,STKRM_WDRWL_IND                                           
W21732               ,SHEET_INV_REQR_IND                                        
                                                                                
28545            FROM TINVPAR A                                                 
28545                ,TINCNTL B                                                 
28545           WHERE B.ACTV_IND          = 'Y'                                 
28545             AND A.INV_ID            = B.INV_ID                            
28545             AND (ACTL_INV_RECD_DTE <= :PV-PROCESSING-DATE                 
22000                 OR PLND_UNIT_BK_DTE <= :PV-PROCESSING-DATE)               
W21732            AND ACTL_INV_DTE        < :PV-PROCESSING-DATE                 
                  AND ACTL_UNIT_BK_DTE = '9999-09-09'                           
W33304            AND LOC_INV_STAT_CDE = 'IN'                                   
W26600       ORDER BY LOC_NBR                                                   
                                                                                
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
      ******************************************************************        
      *  MAIN PROCESSING.                                                       
      ******************************************************************        
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
           IF END-OF-TINVPAR                                                    
               MOVE PC-NO-STORE-READY-MSG TO DL-DATA                            
               MOVE DL-DETAIL-LINE       TO REPORT-LINE                         
               PERFORM Z800-WRITE-REPORT-LINE                                   
           ELSE                                                                 
               PERFORM B200-PROCESS-TINVPAR                                     
                 UNTIL END-OF-TINVPAR                                           
           END-IF.                                                              
                                                                                
           MOVE PV-NEW-CANDIDATES TO DL-NEW-CANDIDATES.                         
           MOVE DL-DETAIL-LINE2 TO REPORT-LINE.                                 
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
           PERFORM B300-EOJ.                                                    
                                                                                
           STOP RUN.                                                            
      *                                                                         
       B100-HOUSEKEEPING.                                                       
      ******************************************************************        
      * GET THE SYSTEM TIME AND MOVE IT TO THE REPORT HEADING. SET              
      * THE REPORT NAME AND NUMBER.                                             
      ******************************************************************        
                                                                                
           ACCEPT PV-TODAYS-DATE FROM DATE.                                     
           ACCEPT PV-TODAYS-TIME FROM TIME.                                     
           MOVE PV-MM           TO DP133O-RUN-MONTH.                            
           MOVE PV-DD           TO DP133O-RUN-DAY.                              
           MOVE PV-YY           TO DP133O-RUN-YEAR.                             
           MOVE PV-HR           TO DP133O-RUN-HOUR.                             
           MOVE PV-MIN          TO DP133O-RUN-MINUTE.                           
           MOVE 'INKRP107'      TO DP133O-PROGRAM-NAME.                         
                                                                                
           OPEN  INPUT CONTROL-CARD-FILE                                        
                OUTPUT STORES-TO-BOOK-REPORT.                                   
                                                                                
           READ CONTROL-CARD-FILE INTO CC-CONTROL-CARD                          
              AT END                                                            
                 DISPLAY 'INKRP107 - NO CONTROL CARD PRESENT'                   
                 MOVE +4019                  TO ABEND-CODE                      
                 CALL 'ILBOABN0'  USING ABEND-CODE.                             
                                                                                
           DISPLAY 'CONTROL RECORD ==>'                                         
                   CC-CONTROL-CARD.                                             
                                                                                
           IF CC-MM-VALID   AND                                                 
              CC-DD-VALID   AND                                                 
              CC-YY NUMERIC                                                     
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '** INKRP107 ABEND **'                                   
               DISPLAY '** CARDIN DATE INVALID ' CC-DATE                        
               MOVE +4019                  TO ABEND-CODE                        
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF  CC-STORE-CTR NOT NUMERIC                                         
               DISPLAY '** INKRP107 ABEND **'                                   
               DISPLAY '** STORE "COUNTER" NOT NUMERIC==>'                      
                       CC-STORE-CTR                                             
                       '<=='                                                    
               MOVE +4019                  TO ABEND-CODE                        
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           MOVE CC-MM    TO PV-PROCESSING-MM                                    
           MOVE CC-DD    TO PV-PROCESSING-DD                                    
           MOVE CC-YY    TO PV-PROCESSING-YY                                    
                                                                                
           MOVE '-'      TO PV-PROC-SL1                                         
                            PV-PROC-SL2.                                        
                                                                                
           DISPLAY '*****************************************'                  
           DISPLAY 'INKRP107 - PROCESSING DATE = ' PV-PROCESSING-DATE.          
           DISPLAY ' '                                                          
                                                                                
           EXEC SQL                                                             
               OPEN TINVPAR_CURSOR                                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = +0                                                  
               MOVE 'OPEN TINVPAR_CURSOR' TO PV-OPER-ATTEMPTED                  
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
W28545*    PERFORM B500-READ-TINVPAR-MAX.                                       
                                                                                
           PERFORM Z700-PRINT-HEADINGS.                                         
                                                                                
       B200-PROCESS-TINVPAR.                                                    
                                                                                
W28545     PERFORM B500-READ-TINVPAR-MAX.                                       
W28545                                                                          
           PERFORM B205-IF-BOOKING-LOCATION.                                    
                                                                                
           IF PS-PROCESS-LOCATION                                               
20246          PERFORM B210-CHECK-IF-READY-FOR-BKING                            
           END-IF.                                                              
                                                                                
           PERFORM Z100-FETCH-TINVPAR.                                          
                                                                                
      *************************************************************             
      * THE FOLLOWING SITUATIONS WILL BE CHECKED TO SEE IF THE STORE            
      * IS A CANDIDATE FOR UNIT BOOKING:                                        
      * -FAKE OUT STORES, NOT VALID                                             
      * -STORES THAT ARE 'S'ELECTED, 'I'NPROCESS OR 'A'PPROVED, INVALID         
      * -CENTRAL STOCK LOCATIONS WILL BE PROCESSED, DC'S WILL NOT               
      * -STORES WITH SHEET NOT PROCESSED, INVALID                               
      * -CLOSED STORES THREE DAYS AFTER INVENTORY, VALID                        
      *************************************************************             
       B205-IF-BOOKING-LOCATION.                                                
                                                                                
           SET PS-PROCESS-LOCATION TO TRUE.                                     
           SET PS-LOCATION-IS-CAND TO TRUE.                                     
           SET PS-NOT-CS-LOCATION                                               
R21732         PS-NOT-ECOM-LOCATION                                             
               PS-NOT-RV-LOCATION  TO TRUE.                                     
                                                                                
           PERFORM Z200-SELECT-XMTLOC.                                          
                                                                                
      **** FAKE OUT STORE, DO NOT PROCESS                                       
           IF   (INVPAR-SCAN-INV-IND = 'Y' AND                                  
W33304           INVPAR-SCHD-PHY-CNT-CDE = 'EI')                                
                 SET PS-BY-PASS-LOCATION TO TRUE                                
           END-IF.                                                              
                                                                                
      **** UNIT BOOKING ALREADY - INPROCESS, SELECTED OR APPROVED               
W33304     IF   INVPAR-UNT-BKG-STAT-CDE = 'IP'                                  
W33304      OR  INVPAR-UNT-BKG-STAT-CDE = 'SL'                                  
W33304      OR  INVPAR-UNT-BKG-STAT-CDE = 'AP'                                  
               SET PS-BY-PASS-LOCATION TO TRUE                                  
W26600         DISPLAY INVPAR-LOC-NBR  ':  BKNG IND SET TO '                    
                       INVPAR-UNT-BKG-STAT-CDE                                  
           END-IF.                                                              
                                                                                
      **** DC - DO NOT PROCESS, CS - PROCESS, RV - SPECIAL PROCESSING           
R21732**** ECOM - PROCESS                                                       
           IF PS-BY-PASS-LOCATION                                               
               CONTINUE                                                         
           ELSE                                                                 
              IF INVPAR-SCAN-INV-IND = 'N'                                      
W33304          AND INVPAR-SCHD-PHY-CNT-CDE = 'NS'                              
SJ0301*           IF XMT00001-LOC-TYP-CDE NOT EQUAL 'CS'                        
SJ0301*               SET PS-BY-PASS-LOCATION TO TRUE                           
W26600            IF XMT00001-LOC-TYP-CDE EQUAL 'CS'                            
SJ0301                SET PS-CS-LOCATION                                        
SJ0301                    PS-PROCESS-LOCATION TO TRUE                           
SJ0301            ELSE                                                          
W26600                IF XMT00001-LOC-TYP-CDE EQUAL 'RV'                        
SJ0301                    SET PS-RV-LOCATION                                    
SJ0301                        PS-PROCESS-LOCATION TO TRUE                       
SJ0301                ELSE                                                      
W26600                    IF XMT00001-E-BUS-IND = 'Y'                           
W26600                    AND XMT00001-LOC-TYP-CDE = 'DS'                       
R21732                        SET PS-ECOM-LOCATION                              
R21732                            PS-PROCESS-LOCATION TO TRUE                   
R21732                    ELSE                                                  
SJ0301                        SET PS-BY-PASS-LOCATION TO TRUE                   
R21732                    END-IF                                                
SJ0301                END-IF                                                    
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       B210-CHECK-IF-READY-FOR-BKING.                                           
                                                                                
      *    CHECK IF ACTUAL INVENTORY RECD DATE HAS BEEN SET                     
W21732*    IF THE LOCATION IS A CS AND SHEETS ARE NOT REQUIRED                  
W21732*    THEN THE ACTUAL INV RECVD DATE IS NOT REUIRED TO BE                  
W21732*    TO BE FILLED IN IN ORDER FOR THE LOCATION TO BE A                    
W21732*    UNIT BOOKING CANDIDATE                                               
           IF  INVPAR-ACTL-INV-RECD-DTE > PV-PROCESSING-DATE                    
W33304         IF INVPAR-SCHD-PHY-CNT-CDE = 'CL'                                
JS0500             IF PV-PROCESSING-DATE < PV-TARGET-DATE                       
                       MOVE PV-TARGET-DATE TO PC-CLOSED-TARGET                  
JS0500                 MOVE PC-CLOSED-STR-MSG TO DL-REASON                      
JS0500                 PERFORM B250-WRITE-REJECT-REPORT                         
JS0500                 SET PS-LOCATION-IS-NOT-CAND TO TRUE                      
W21732             ELSE                                                         
W21732                 IF PV-PROCESSING-DATE >= PV-TARGET-DATE                  
W21732                     CONTINUE                                             
W21732                 END-IF                                                   
JS0500             END-IF                                                       
               ELSE                                                             
      *        SET ACTL_INV_RECD_DTE FOR CS / RV IF NOT SET                     
      *        THIS WILL OCCUR 3 DAYS AFTER MAX(PLND_INV_DTE)                   
SJ0101             IF PS-RV-LOCATION                                            
SJ0101                 SET PS-LOCATION-IS-NOT-CAND TO TRUE                      
SJ0101                 IF PV-PROCESSING-DATE > PV-MAX-PLND-DTE-PLUS             
SJ0101                     PERFORM B600-UPDATE-ACTL-RECD-DTE                    
SJ0101                 END-IF                                                   
SJ0101             ELSE                                                         
W21732                 EVALUATE TRUE                                            
W21732*                    CENTRAL STOCK LOCATION AFTER LAST DAYS OF INV        
SJ0101                     WHEN (PS-CS-LOCATION AND                             
SJ0101                       PV-PROCESSING-DATE > PV-MAX-PLND-DTE-PLUS )        
SJ0101                         PERFORM B600-UPDATE-ACTL-RECD-DTE                
W21732*                    CENTRAL STOCK LOCATION THAT REQUIRE SHEETS           
W21732                     WHEN (PS-CS-LOCATION AND                             
W21732                       INVPAR-SHEET-INV-REQR-IND = 'Y')                   
W21732                         MOVE PC-CS-SHEET-NOT-PROC TO DL-REASON           
W21732                         PERFORM B250-WRITE-REJECT-REPORT                 
W21732                         SET PS-LOCATION-IS-NOT-CAND TO TRUE              
W21732*                    CENTRAL STOCK LOCATION DON'T REQUIRE SHEETS          
W21732                     WHEN (PS-CS-LOCATION AND                             
W21732                       INVPAR-SHEET-INV-REQR-IND = 'N')                   
W21732                         CONTINUE                                         
W21732*                    ALL OTHER LOCATIONS                                  
W21732                     WHEN OTHER                                           
SJ0101*                 ELSE                                                    
                               MOVE PC-PLND-BOOK-DTE-MSG TO DL-REASON           
                               PERFORM B250-WRITE-REJECT-REPORT                 
JS0500                         SET PS-LOCATION-IS-NOT-CAND TO TRUE              
SJ0101*                END-IF                                                   
W21732                 END-EVALUATE                                             
SJ0101             END-IF                                                       
JS0500         END-IF                                                           
JS0500     END-IF.                                                              
                                                                                
      *    CHECK IF UNPROCESSED SHEETS EXIST FOR SCANNING STRS                  
W21732     IF  INVPAR-ACTL-INV-RECD-DTE <= PV-PROCESSING-DATE                   
               IF (INVPAR-SCAN-INV-IND = 'Y'                                    
W33304           AND INVPAR-SCHD-PHY-CNT-CDE = 'PI')                            
X21732           OR PS-ECOM-LOCATION                                            
                   PERFORM Z210-CHECK-SHEETS                                    
W21732             EVALUATE TRUE                                                
W21732                 WHEN SQLCODE = +000 OR -811                              
                           IF INVPAR-SHEET-INV-CPLD-IND NOT EQUAL 'Y'           
                               MOVE PC-SHEET-NOT-PROC TO DL-REASON              
                               PERFORM B250-WRITE-REJECT-REPORT                 
                               SET PS-LOCATION-IS-NOT-CAND TO TRUE              
                           END-IF                                               
W21732                 WHEN SQLCODE = +100                                      
W21732                     IF INVPAR-SHEET-INV-REQR-IND = 'Y'                   
W21732                         MOVE PC-SHEET-NOT-ENTRD TO DL-REASON             
W21732                         PERFORM B250-WRITE-REJECT-REPORT                 
W21732                         SET PS-LOCATION-IS-NOT-CAND TO TRUE              
W21732                     END-IF                                               
W21732             END-EVALUATE                                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *    CHECK IF LAPSE TIME FOR ENTRY OF STCKRM WITH SHEETS (NON RV-)        
22000 *    OR IF THE USER HAS INDICATED NO STOCKROOM WITHDRAWALS                
W21732     EVALUATE TRUE                                                        
W21732         WHEN PS-RV-LOCATION                                              
SJ0301             SET PS-LOCATION-IS-NOT-CAND TO TRUE                          
W33304         WHEN INVPAR-SCHD-PHY-CNT-CDE = 'CL'                              
W21732             CONTINUE                                                     
W21732         WHEN (INVPAR-ACTL-INV-RECD-DTE <= PV-PROCESSING-DATE) OR         
W21732              (INVPAR-ACTL-INV-RECD-DTE  = PC-EMPTY-DATE AND              
W21732               INVPAR-SHEET-INV-REQR-IND = 'N')                           
                   IF PV-ACTL-PLUS-STKRM-DAYS > PV-PROCESSING-DATE-DAYS         
22000                  AND INVPAR-STKRM-WDRWL-IND = 'Y'                         
JS0500                 SET PS-LOCATION-IS-NOT-CAND TO TRUE                      
                       MOVE PC-STKRMW-MSG TO DL-REASON                          
                       PERFORM B250-WRITE-REJECT-REPORT                         
                   ELSE                                                         
                       PERFORM B220-CHK-INVALID-IND                             
                       IF SQLCODE = +000 OR -811                                
                            MOVE PC-INVALID-DETAIL-MSG TO DL-REASON             
                            PERFORM B250-WRITE-REJECT-REPORT                    
                            SET PS-LOCATION-IS-NOT-CAND TO TRUE                 
                       ELSE                                                     
                          IF INVPAR-SLS-ADJ-CPLD-IND = 'N'                      
W26600                     AND XMT00001-LOC-TYP-CDE EQUAL 'DS'                  
R21732                     AND PS-NOT-ECOM-LOCATION                             
                             MOVE PC-SALE-ADJ-MSG TO DL-REASON                  
                             PERFORM B250-WRITE-REJECT-REPORT                   
                             SET PS-LOCATION-IS-NOT-CAND TO TRUE                
                          END-IF                                                
                       END-IF                                                   
                   END-IF                                                       
W21732         WHEN OTHER                                                       
W21732             CONTINUE                                                     
W21732     END-EVALUATE.                                                        
                                                                                
           IF PS-LOCATION-IS-CAND                                               
               PERFORM Z900-UPDATE-BOOKING-IND                                  
               ADD +1 TO PV-NEW-CANDIDATES                                      
           ELSE                                                                 
SJ0301         IF PS-RV-LOCATION                                                
SJ0301             CONTINUE                                                     
SJ0301         ELSE                                                             
                   MOVE SPACES TO REPORT-LINE                                   
                   MOVE ' ' TO REPORT-LINE-CC                                   
                   PERFORM Z800-WRITE-REPORT-LINE                               
               END-IF                                                           
           END-IF.                                                              
                                                                                
       B220-CHK-INVALID-IND.                                                    
                                                                                
           EXEC SQL                                                             
               SELECT 1                                                         
                 INTO :PV-CHECK                                                 
                 FROM TINVDTL                                                   
W26600          WHERE LOC_NBR       = :INVPAR-LOC-NBR                           
                  AND INVALID_REC_IND = 'Y'                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN +100                                                        
               WHEN -811                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVDTL SELECT'                                  
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       B250-WRITE-REJECT-REPORT.                                                
                                                                                
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
               PERFORM Z700-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
W26600     MOVE INVPAR-LOC-NBR       TO DL-STORE-NBR                            
RM0103     MOVE INVPAR-ACTL-INV-DTE  TO PV-HLD-DATE.                            
RM0103     MOVE PV-HLD-MM            TO PV-REFORMAT-MM.                 MLKRP030
RM0103     MOVE PV-HLD-DD            TO PV-REFORMAT-DD.                 MLKRP030
RM0103     MOVE PV-HLD-CCYY          TO PV-REFORMAT-CCYY.               MLKRP030
RM0103     MOVE PV-REFORMAT-DATE     TO DL-ACTL-INV-DTE.                MLKRP030
                                                                                
           MOVE XMT00001-LOC-NM      TO DL-STORE-NAME                           
                                                                                
           MOVE DL-DETAIL-LINE       TO REPORT-LINE.                            
                                                                                
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
           INITIALIZE DL-DETAIL-LINE.                                           
      *                                                                         
       B300-EOJ.                                                                
      ******************************************************************        
      *    CLOSE THE FILES.                                                     
      ******************************************************************        
                                                                                
           IF PV-LINE-COUNT > PC-LINE-MAX                                       
               PERFORM Z700-PRINT-HEADINGS                                      
           END-IF.                                                              
                                                                                
           MOVE RE-END-OF-REPORT-MARKER TO REPORT-LINE.                         
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
20246      EXEC SQL                                                             
20246          CLOSE TINVPAR_CURSOR                                             
20246      END-EXEC.                                                            
20246                                                                           
20246      IF SQLCODE NOT = +0                                                  
20246          MOVE 'CLOSE TINVPAR_CURSOR' TO PV-OPER-ATTEMPTED                 
20246          PERFORM Z999-SQL-ABEND                                           
20246      END-IF.                                                              
           CLOSE   STORES-TO-BOOK-REPORT                                        
                   CONTROL-CARD-FILE.                                           
           DISPLAY '*****************************************'.                 
                                                                                
      ******************************************************************        
      *    RETRIEVE MAX PLND-INV-DTE                                            
      ******************************************************************        
SJ0301 B500-READ-TINVPAR-MAX.                                                   
                                                                                
           EXEC SQL                                                             
           SELECT DATE(MAX(PLND_INV_DTE) + 3 DAYS)                              
             INTO :PV-MAX-PLND-DTE-PLUS                                         
             FROM TINVPAR A                                                     
28545            ,TINCNTL B                                                     
28545      WHERE B.ACTV_IND      = 'Y'                                          
28545        AND A.INV_ID        = :INVPAR-INV-ID                               
28545        AND A.INV_ID        = B.INV_ID                                     
28545        AND A.PLND_INV_DTE <= B.OPN_FSCL_MN_DTE                            
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'INVALID SELECT AGAINST TINVPAR'                     
                   MOVE 'MAX PLND DATE '                                        
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      *    UPDATE ACTL_INV_RECD_DTE FOR CS/RV LOCATIONS IF NOT YET SET          
      ******************************************************************        
SJ0301 B600-UPDATE-ACTL-RECD-DTE.                                               
                                                                                
      *    EXEC SQL                                                             
      *        UPDATE TINVPAR                                                   
      *            SET ACTL_INV_RECD_DTE = :PV-PROCESSING-DATE                  
W26600*            WHERE LOC_NBR       = :INVPAR-LOC-NBR                        
      *    END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   MOVE PV-MAX-PLND-DTE-PLUS TO INVPAR-ACTL-INV-RECD-DTE        
      *****        'STORE: ' INVPAR-STORE-NBR, ' ACTL-RECD-DTE WAS SET'         
               WHEN OTHER                                                       
                   DISPLAY 'ERROR ATTEMPTING TO UPDATE TINVPAR'                 
                   MOVE 'ACTL_INV_RECD_DTE UPDATE'                              
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      *    READ THE TINVPAR TABLE - DB2 FETCH                                   
      ******************************************************************        
       Z100-FETCH-TINVPAR.                                                      
      *                                                                         
           EXEC SQL FETCH TINVPAR_CURSOR                                        
28545 *       INTO :INVPAR-LOC-NBR                                              
28545         INTO :INVPAR-INV-ID                                               
28545             ,:INVPAR-LOC-NBR                                              
                  ,:INVPAR-ACTL-INV-DTE                                         
                  ,:INVPAR-PLND-UNIT-BK-DTE                                     
                  ,:INVPAR-ACTL-INV-RECD-DTE                                    
                  ,:INVPAR-STKRM-DAY-QTY                                        
                  ,:INVPAR-SCAN-INV-IND                                         
                  ,:PV-ACTL-PLUS-STKRM-DAYS                                     
                  ,:PV-TARGET-DATE                                              
                  ,:PV-PROCESSING-DATE-DAYS                                     
                  ,:INVPAR-SLS-ADJ-CPLD-IND                                     
                  ,:INVPAR-SCAN-INV-IND                                         
W33304            ,:INVPAR-SCHD-PHY-CNT-CDE                                     
                  ,:INVPAR-SHEET-INV-CPLD-IND                                   
                  ,:INVPAR-UNT-BKG-STAT-CDE                                     
22000             ,:INVPAR-STKRM-WDRWL-IND                                      
W21732            ,:INVPAR-SHEET-INV-REQR-IND                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN +100                                                        
                   SET END-OF-TINVPAR TO TRUE                                   
               WHEN OTHER                                                       
                   MOVE 'FETCH TINVPAR_CURSOR'                                  
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
       Z200-SELECT-XMTLOC.                                                      
                                                                                
W26600     EXEC SQL                                                             
W26600       SELECT   LOC_NM                                                    
W26600               ,LOC_TYP_CDE                                               
W26600               ,LOC_NBR                                                   
800762               ,VALUE(E_BUS_IND,'N')                                      
W26600         INTO  :XMT00001-LOC-NM                                           
W26600              ,:XMT00001-LOC-TYP-CDE                                      
W26600              ,:XMT00001-LOC-NBR                                          
W26600              ,:XMT00001-E-BUS-IND                                        
W26600         FROM   XMT_LOC                                                   
W26600        WHERE   LOC_NBR = :INVPAR-LOC-NBR                                 
W26600     END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
                   CONTINUE                                                     
               WHEN OTHER                                                       
W26600             DISPLAY 'STORE NOT FOUND ON XMT_LOC'                         
W26600             DISPLAY 'STORE = ' INVPAR-LOC-NBR                            
W26600             MOVE 'SELECT XMT_LOC '                                       
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ************************************************                          
      * CHECK IF THE STORE HAS SHEETS TO BE PROCESSED                           
      ************************************************                          
      *                                                                         
       Z210-CHECK-SHEETS.                                                       
W26600*    MOVE INVPAR-LOC-NBR TO PV-INVPAR-STR-NBR-CHAR.                       
                                                                                
W26600*    MOVE PV-INVPAR-STR-NBR-NUM TO PV-INENTY-STR-NBR.                     
                                                                                
                                                                                
           EXEC SQL                                                             
               SELECT 1                                                         
                 INTO :PV-CHECK2                                                
                 FROM TINENTY                                                   
W26600          WHERE STR_NBR       = :INVPAR-LOC-NBR                           
W28545            AND SHEET_TYP_CDE IN('K','T','M')                             
W28545*****       AND SHEET_TYP_CDE = 'K'                                       
           END-EXEC.                                                            
                                                                                
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN +0                                                          
               WHEN +100                                                        
               WHEN -811                                                        
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE 'FETCH TINENTY_CURSOR'                                  
                                      TO  PV-OPER-ATTEMPTED                     
                   PERFORM Z999-SQL-ABEND                                       
           END-EVALUATE.                                                        
      *                                                                         
       Z700-PRINT-HEADINGS.                                                     
      ******************************************************************        
      *    PRINT THE REPORT HEADINGS                                            
      ******************************************************************        
                                                                                
           ADD 1 TO PV-PAGE-COUNT.                                              
           MOVE PV-PAGE-COUNT TO DP133O-PAGE-NUMBER.                            
           MOVE 1             TO DP133O-REPORT-NUMBER.                          
           MOVE DP133O-STANDRD-HEADER-133-COLS TO REPORT-LINE.                  
           MOVE '1'           TO REPORT-LINE-CC                                 
           MOVE ' '           TO REPORT-HEADER-FONT-INDICATOR                   
                                                                                
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE RH-REPORT-HEADING-ONE TO REPORT-LINE.                           
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE RH-REPORT-HEADING-TWO TO REPORT-LINE.                           
           PERFORM Z800-WRITE-REPORT-LINE.                                      
                                                                                
           MOVE 4 TO PV-LINE-COUNT.                                             
                                                                                
       Z800-WRITE-REPORT-LINE.                                                  
                                                                                
           MOVE REPORT-LINE-CC TO PC-REPORT-CC.                                 
                                                                                
           WRITE REPORT-LINE.                                                   
                                                                                
           EVALUATE  PC-REPORT-CC                                               
               WHEN ' '                                                         
                  ADD 1 TO PV-LINE-COUNT                                        
               WHEN '0'                                                         
                  ADD 2 TO PV-LINE-COUNT                                        
               WHEN '-'                                                         
                  ADD 3 TO PV-LINE-COUNT                                        
           END-EVALUATE.                                                        
                                                                                
           EJECT                                                                
                                                                                
20246 ******************************************************************        
20246 *    UPDATE THE TINVPAR UNIT BOOKING STAT CODE WITH A VALUE OF 'S'        
20246 *    TO INDICATE THIS STORE WAS SELECTED AS A CANDIDATE TO BOOK.          
20246 ******************************************************************        
20246  Z900-UPDATE-BOOKING-IND.                                                 
20246                                                                           
20246      PERFORM                                                              
20246          WITH TEST AFTER                                                  
20246          VARYING PV-SQL-SUB                                               
20246          FROM 1 BY 1                                                      
20246          UNTIL (PV-SQL-SUB > PV-MAX-RETRIES) OR                           
20246              (SQLCODE = 0)                                                
20246                                                                           
20246                  EXEC SQL                                                 
20246                      UPDATE TINVPAR                                       
20246                      SET UNT_BKG_STAT_CDE = :PC-UNIT-BK-STAT-CDE,         
20246                          CHG_ID_NBR       = :PC-PROGRAM-ID,               
20246                          CHG_TMST         = CURRENT TIMESTAMP             
20246                      WHERE LOC_NBR        = :INVPAR-LOC-NBR               
W33304                     AND INV_ID           = :INVPAR-INV-ID                
20246                      AND ACTL_UNIT_BK_DTE = :PC-EMPTY-DATE                
20246                  END-EXEC                                                 
20246                                                                           
20246                  EVALUATE TRUE                                            
20246                      WHEN SQLCODE = ZERO                                  
20246                          ADD +1 TO PA-TINVPAR-ROWS-UPDATED                
W26600                         DISPLAY INVPAR-LOC-NBR                           
JS0500                                 ':  STORE SELECTED TO BOOK'              
20246                      WHEN SQLWARN0 NOT = SPACES                           
20246                      WHEN SQLCODE LESS ZERO                               
20246                      WHEN SQLCODE GREATER ZERO                            
W26600                         DISPLAY 'STORE = ' INVPAR-LOC-NBR                
20246                          MOVE 'Z900-UPDATE-BOOKING-IND'                   
20246                              TO PV-PARAGRAPH-NAME                         
20246                          MOVE 'UPDATE TINVPAR ROW'                        
20246                              TO PV-DB2-OPER-ATTEMPTED                     
20246                          PERFORM Z999-SQL-ABEND                           
20246                  END-EVALUATE                                             
20246      END-PERFORM.                                                         
20246                                                                           
20246      IF PV-SQL-SUB GREATER PV-MAX-RETRIES                                 
20246          MOVE 'Z900-UPDATE-BOOKING-IND' TO PV-PARAGRAPH-NAME              
20246          MOVE 'EXCEEDED MAX RETRIES' TO PV-DB2-OPER-ATTEMPTED             
20246          PERFORM Z999-SQL-ABEND                                           
20246      END-IF.                                                              
20246                                                                           
       Z999-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                 TO ABEND-CODE.                            
           DISPLAY '**  ABEND     **'.                                          
           DISPLAY '**  PROGRAM   **  =  INKRP107'.                             
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED                    
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           CALL 'ILBOABN0'  USING ABEND-CODE.                                   
