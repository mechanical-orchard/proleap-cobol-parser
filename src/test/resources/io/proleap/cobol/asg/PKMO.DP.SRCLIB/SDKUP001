       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    SDKUP001.                                                 
       AUTHOR.        KOHLS DEPARTMENT STORES.                                  
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  2002-03-19.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      **  LOAD SDT_TSK_LBR_STD TABLE.  THIS TABLE HOLDS THE MONTH-END           
      **  DATE, TASK-ID AND LABOR STANDARD AMOUNT.                              
      ******************************************************************        
      **03/19/2002 CHANGED BY:JACKIE HELING     INITIAL WRITE                   
      ******************************************************************        
012705**01/27/2005 CHANGED BY:JACKIE HELING    ADD MINIMUM SALES AMT            
      ******************************************************************        
063005**06/30/2005 CHANGED BY:JACKIE HELING    EFFECTIVE DATE FILE              
      ******************************************************************        
071205**07/12/2005 CHANGED BY:JACKIE HELING    CHANGE DATE HEADER FORMAT        
      ******************************************************************        
082905**08/29/2005 CHANGED BY:JACKIE HELING   REMOVE MINIMUM SALES AMT          
082905**                                      MOVED TO ANOTHER TABLE            
      ******************************************************************        
I65165**04/08/2013 CHANGED BY:COGNIZANT       ADDED A CONDITION IN              
I65165**                                      UNSTRING TO BYPASS BAD            
I65165**                                      DATA X'0D' IN INPUT FILE          
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT TSK-LBR-STD                                                   
                  ASSIGN TO INP01.                                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  TSK-LBR-STD                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 132 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS TSK-LBR-STD-RECORD.                                   
       01  TSK-LBR-STD-RECORD              PIC X(132).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      **PROGRAM SWITCHES                                                        
      ******************************************************************        
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-LBR-STD-FILE-SW   PIC X(01) VALUE 'N'.                 
               88 PS-END-OF-LBR-STD-FILE             VALUE 'Y'.                 
               88 PS-NOT-END-OF-LBR-STD-FILE         VALUE 'N'.                 
      ******************************************************************        
      **PROGRAM UNSTRING VARIABLES - TRUCK FILE LAYOUT                          
      ******************************************************************        
       01  PU-PROGRAM-UNSTRING.                                                 
           05  PU-TSK-ID                   PIC 9(10)  VALUE ZEROES.             
072302     05  PU-LBR-STD                  PIC S9(05) VALUE ZEROES.             
           05  PU-TSK-DESC                 PIC X(10)  VALUE SPACES.             
      ******************************************************************        
      ** FILE HEADER CHECK LAYOUT                                               
      ******************************************************************        
       01  OSA-FILE-LAYOUT.                                                     
           05  OSA-DATA-CHECK              PIC X(01) VALUE SPACES.              
           05  FILLER                      PIC X(79) VALUE SPACES.              
      ******************************************************************        
      ** FILE DATE RECORD LAYOUT                                                
      ******************************************************************        
063005 01  FILE-DATE-LAYOUT.                                                    
063005     05  FILE-DATE.                                                       
063005         10  FILE-DATE-MONTH         PIC 9(02) VALUE ZEROES.              
063005         10  FILLER                  PIC X(01) VALUE '/'.                 
071205         10  FILE-DATE-DAY           PIC 9(02) VALUE ZEROES.              
071205         10  FILLER                  PIC X(01) VALUE '/'.                 
063005         10  FILE-DATE-YEAR          PIC 9(04) VALUE ZEROES.              
063005     05  FILLER                      PIC X(70) VALUE SPACES.              
                                                                                
063005 01  PV-DATE.                                                             
063005     05  PV-YEAR                     PIC 9(04) VALUE ZEROES.              
063005     05  FILLER                      PIC X(01) VALUE '-'.                 
063005     05  PV-MONTH                    PIC 9(02) VALUE ZEROES.              
063005     05  FILLER                      PIC X(01) VALUE '-'.                 
063005     05  PV-DAY                      PIC 9(02) VALUE 15.                  
                                                                                
      ******************************************************************        
      **TIME FIELDS                                                             
      ******************************************************************        
       01  SD-TIME-STAMP                   PIC  X(26)  VALUE SPACES.            
       01  FILLER REDEFINES SD-TIME-STAMP.                                      
           05  SD-CURR-DTE                 PIC  X(10).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-HOUR                     PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MINUTE                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-SECOND                   PIC  X(02).                          
           05  FILLER                      PIC  X(01).                          
           05  SD-MICROSEC                 PIC  X(06).                          
       01  PD-PROGRAM-DATE-VARIABLES.                                           
           05  PD-MONTH-END                PIC  X(10) VALUE SPACES.             
      ******************************************************************        
      **PROGRAM ACCUMULATORS                                                    
      ******************************************************************        
       01  PA-PROGRAM-ACCUM.                                                    
           05  PA-RECORDS-READ             PIC  9(02) VALUE ZEROS.              
           05  PA-ROWS-INSERTED            PIC  9(02) VALUE ZEROS.              
           05  PA-ROWS-UPDATED             PIC  9(02) VALUE ZEROS.              
      ******************************************************************        
      **ABEND INFORMATION                                                       
      ******************************************************************        
       01  ABEND-CODE                  PIC S9(04)  VALUE +0 COMP SYNC.          
           88  AC-OPEN-FILE-ERROR                  VALUE +4005.                 
           88  AC-DB2-ERROR                        VALUE +4013.                 
      ******************************************************************        
      **PROGRAM VARIABLES                                                       
      ******************************************************************        
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
           05  PV-PROGRAM-NAME             PIC X(08) VALUE 'SDKUP001'.          
      ******************************************************************        
      **COPYBOOKS                                                               
      ******************************************************************        
      ***CALENDAR ROUTINE COPYBOOK                                              
           COPY DPWS500.                                                        
      ***SQLCA ROUTINE                                                          
           COPY DPWS004.                                                        
      ***DB2 MESSAGE AREA                                                       
           COPY SQLCA2.                                                         
      ******************************************************************        
      **TABLE INCLUSIONS                                                        
      ******************************************************************        
      ***SQL COMM AREA                                                          
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
      ***SDT_TSK_LBR_STD TABLE                                                  
           EXEC SQL                                                             
               INCLUDE SDT00016                                                 
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN-LINE.                                                          
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
063005     PERFORM 4000-READ-INPUT-FILE.                                        
063005     PERFORM 4000-READ-INPUT-FILE UNTIL                                   
063005        OSA-DATA-CHECK NUMERIC.                                           
           PERFORM 2000-PROCESS-INPUT-FILE                                      
                   UNTIL PS-END-OF-LBR-STD-FILE.                                
           PERFORM 6000-WRAPUP.                                                 
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      **INITIALIZATION                                                          
      ******************************************************************        
       1000-INITIALIZATION.                                                     
                                                                                
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.                     
                                                                                
           OPEN INPUT TSK-LBR-STD.                                              
                                                                                
           PERFORM 1100-GET-TIMESTAMP.                                          
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
063005     PERFORM 1200-RETRIEVE-EOM.                                           
                                                                                
      **********************************************************                
      **RETRIEVE DB2 TIMESTAMP                                                  
      **********************************************************                
       1100-GET-TIMESTAMP.                                                      
                                                                                
           MOVE '1100-GET-TIMESTAMP' TO PV-PARAGRAPH-NAME.                      
                                                                                
           EXEC SQL                                                             
                SET :SD-TIME-STAMP = CURRENT TIMESTAMP                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT EQUAL ZERO                                            
               DISPLAY 'UNSUCCESSFUL TIME RETRIEVAL'                            
               PERFORM 9000-ABEND                                               
           END-IF.                                                              
                                                                                
      **********************************************************                
      **RETRIEVE END OF MONTH                                                   
      **********************************************************                
       1200-RETRIEVE-EOM.                                                       
                                                                                
           MOVE '1200-RETRIEVE-EOM' TO PV-PARAGRAPH-NAME.                       
                                                                                
063005     MOVE OSA-FILE-LAYOUT TO FILE-DATE-LAYOUT.                            
063005     MOVE FILE-DATE-MONTH TO PV-MONTH.                                    
063005     MOVE FILE-DATE-YEAR  TO PV-YEAR.                                     
                                                                                
063005***RETRIEVE END OF FISCAL MONTH BEING LOADED                              
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
           SET DPG51-VALID-FISCAL-CALENDAR TO TRUE.                             
           SET DPG51-DO-NOT-INCR-DECR-DATE TO TRUE.                             
           SET DPG52-LK-DTE-ISO-FMT        TO TRUE.                             
063005     MOVE PV-DATE                    TO DPG52-LK-DATE-INPUT.              
           PERFORM 1250-CALL-DATE-ROUTINE.                                      
063005     MOVE DPG56-LAST-FP-DB2-ISO-FMT  TO PD-MONTH-END.                     
                                                                                
063005     DISPLAY 'LOADING DATA WITH MONTH END DATE OF ' PD-MONTH-END.         
                                                                                
      ******************************************************************        
      **CALLS DATE ROUTINE                                                      
      ******************************************************************        
       1250-CALL-DATE-ROUTINE.                                                  
                                                                                
           MOVE '1250-CALL-DATE-ROUTINE' TO PV-PARAGRAPH-NAME                   
                                                                                
           CALL DP500-KOHLS-CALENDAR-ROUTINE USING DPG51                        
                                                   DPG52                        
                                                   DPG53                        
                                                   DPG54                        
                                                   DPG55                        
                                                   DPG56.                       
                                                                                
           EVALUATE TRUE                                                        
              WHEN DPG54-WARNING-ERROR                                          
                   DISPLAY 'DATE ROUTINE WARNING: ' DPG54-ERROR-MESSAGE         
                   DISPLAY DPG52-DB2-ISO-DATE                                   
                   PERFORM 9000-ABEND                                           
              WHEN DPG54-SEVERE-ERROR                                           
                   DISPLAY 'DATE ROUTINE ERROR: ' DPG54-ERROR-MESSAGE           
                   DISPLAY DPG52-DB2-ISO-DATE                                   
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      **********************************************************                
      **PROCESS INPUT FILE                                                      
      **********************************************************                
       2000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE '2000-PROCESS-INPUT-FILE' TO PV-PARAGRAPH-NAME                  
                                                                                
           UNSTRING OSA-FILE-LAYOUT                                             
I65165        DELIMITED BY ',' OR ' ' OR X'0D' INTO  PU-TSK-ID                  
                                                  ,  PU-LBR-STD                 
                                                  ,  PU-TSK-DESC.               
                                                                                
           MOVE PD-MONTH-END          TO SDT00016-MN-END-DTE.                   
           MOVE PU-TSK-ID             TO SDT00016-LS-TSK-NM-ID.                 
           MOVE PU-LBR-STD            TO SDT00016-SLS-LBR-FCTR-NBR.             
                                                                                
           PERFORM 2100-INSERT-RECORD.                                          
                                                                                
           PERFORM 4000-READ-INPUT-FILE.                                        
                                                                                
      **********************************************************                
      **INSERT DB RECORD INTO TABLE SDT_TSK-LBR-STD                             
      **********************************************************                
       2100-INSERT-RECORD.                                                      
                                                                                
           MOVE '2100-INSERT-RECORD' TO PV-PARAGRAPH-NAME                       
                                                                                
           EXEC SQL                                                             
                INSERT INTO SDT_TSK_LBR_STD                                     
                       (MN_END_DTE                                              
                      , LS_TSK_NM_ID                                            
                      , SLS_LBR_FCTR_NBR)                                       
                VALUES(:SDT00016-MN-END-DTE                                     
                     , :SDT00016-LS-TSK-NM-ID                                   
082905               , :SDT00016-SLS-LBR-FCTR-NBR)                              
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = 0                                                  
                   ADD +1             TO PA-ROWS-INSERTED                       
              WHEN SQLCODE = -803                                               
                   PERFORM 2200-UPDATE-RECORD                                   
              WHEN OTHER                                                        
012705             DISPLAY '**************************************'             
012705             DISPLAY 'ERROR ON INSERT!'                                   
012705             DISPLAY 'TASK ID = ' SDT00016-LS-TSK-NM-ID                   
012705             DISPLAY 'DATE    = ' SDT00016-MN-END-DTE                     
012705             DISPLAY 'STD     = ' SDT00016-SLS-LBR-FCTR-NBR               
012705             DISPLAY '**************************************'             
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      **UPDATE RECORD ON TABLE SDT_TSK_LBR_STD                                  
      ******************************************************************        
       2200-UPDATE-RECORD.                                                      
                                                                                
           MOVE '2200-UPDATE-RECORD' TO PV-PARAGRAPH-NAME                       
                                                                                
           EXEC SQL                                                             
                UPDATE SDT_TSK_LBR_STD                                          
                SET SLS_LBR_FCTR_NBR =  :SDT00016-SLS-LBR-FCTR-NBR              
                WHERE MN_END_DTE   = :SDT00016-MN-END-DTE                       
                  AND LS_TSK_NM_ID = :SDT00016-LS-TSK-NM-ID                     
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                   ADD +1                   TO PA-ROWS-UPDATED                  
              WHEN OTHER                                                        
012705             DISPLAY '**************************************'             
                   DISPLAY 'ERROR ON UPDATE OF DATA'                            
012705             DISPLAY 'TASK ID = ' SDT00016-LS-TSK-NM-ID                   
012705             DISPLAY 'DATE    = ' SDT00016-MN-END-DTE                     
012705             DISPLAY 'STD     = ' SDT00016-SLS-LBR-FCTR-NBR               
012705             DISPLAY '**************************************'             
                   PERFORM 9000-ABEND                                           
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      **READ INPUT FILE                                                         
      ******************************************************************        
       4000-READ-INPUT-FILE.                                                    
                                                                                
           MOVE '4000-READ-INPUT-FILE' TO PV-PARAGRAPH-NAME.                    
                                                                                
           READ TSK-LBR-STD INTO OSA-FILE-LAYOUT                                
                AT END                                                          
                   SET PS-END-OF-LBR-STD-FILE TO TRUE                           
           END-READ.                                                            
                                                                                
           ADD +1                           TO PA-RECORDS-READ.                 
                                                                                
      ******************************************************************        
      **END OF JOB                                                              
      ******************************************************************        
       6000-WRAPUP.                                                             
                                                                                
           MOVE '6000-WRAPUP' TO PV-PARAGRAPH-NAME.                             
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE NOT ZERO                                                 
                   DISPLAY 'ERROR UPON COMMIT OF DATA'                          
                   PERFORM 9000-ABEND                                           
           END-IF.                                                              
                                                                                
           CLOSE TSK-LBR-STD.                                                   
                                                                                
           DISPLAY '*************************************************'.         
           DISPLAY '* PROGRAM.................: ' PV-PROGRAM-NAME.              
           DISPLAY '* RUN DATE/TIME...........: ' SD-TIME-STAMP.                
           DISPLAY '*************************************************'.         
           DISPLAY '* RECORDS READ.......: '                                    
                                          PA-RECORDS-READ.                      
           DISPLAY '* ROWS INSERTED......: '                                    
                                          PA-ROWS-INSERTED.                     
           DISPLAY '* ROWS UPDATED.......: '                                    
                                          PA-ROWS-UPDATED.                      
           DISPLAY '*************************************************'.         
                                                                                
      **********************************************************                
      **ABEND ROUTINE                                                           
      **********************************************************                
       9000-ABEND.                                                              
                                                                                
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = ' PV-PROGRAM-NAME.                        
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
      ******************************************************************        
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED             
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.              
      ******************************************************************        
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
