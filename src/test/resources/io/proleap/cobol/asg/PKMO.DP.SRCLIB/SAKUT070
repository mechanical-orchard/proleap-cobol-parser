      ****************************************************************  00010000
       IDENTIFICATION DIVISION.                                         00020000
      ****************************************************************  00030000
                                                                        00040000
       PROGRAM-ID.             SAKUT070.                                00050001
       AUTHOR.                 COGNIZANT.                               00060001
       INSTALLATION.           KOHLS DEPARTMENT STORES.                 00070000
       DATE-WRITTEN            MAY 2012.                                00080001
       DATE-COMPILED.                                                   00090000
                                                                        00100000
      ****************************************************************  00110000
      *  THIS PROGRAM EXTRACTS PREVIOUS SALES DAY ECOM TRANSACTIONS     00130201
      *  EXTRACTED FILE USE TO FIND MISSING TRNSACTIONS                 00130325
      ****************************************************************  00133000
      * CHANGE LOG:                                                     00139000
      * DATE:  2012-05-29                                               00140001
      * PRGMR: COGNIZANT                                                00150001
      * WHY:   NEW PROGRAM TO VERIFY ECOM TRANSACTIONS.                 00160000
      ******************************************************************00173000
C53632****************************************************************  00174040
C53632* CHANGE LOG:                                                     00175040
C53632* CHG#:  CHG0053632                                               00176040
C53632* DATE:  2013-06-28                                               00177040
C53632* PRGMR: COGNIZANT                                                00178040
C53632* WHY:   TO INCLUDE SHIP FROM STORE TRANSACTIONS(SFS) AS A PART   00179041
C53632*        OF OCF COMPANION CHANGE.                                 00179141
      ****************************************************************  00180000
C12677****************************************************************  00181053
C12677* CHANGE LOG:                                                     00182053
C12677* CHG#:  CHG0112677                                               00183053
C12677* DATE:  2015-05-20                                               00184053
C12677* PRGMR: SARVESWARAN N.S.K.                                       00185053
C12677* WHY:   ADDED CONDITION IN THE CURSOR TO EXTRACT TRANSACTIONS    00186053
C12677*        WHICH IS GREATER THAN REGISTER NO 15.                    00187053
C12677*        INITIALIZED ORD NO VARIABLE TO PULL THE CORRECT          00187153
C12677*        ORDER NUMBER WHICH HAS 9 DIGITS.                         00187253
      ****************************************************************  00188053
       ENVIRONMENT DIVISION.                                            00190000
      ****************************************************************  00200000
                                                                        00210000
       CONFIGURATION SECTION.                                           00220000
                                                                        00230000
       SOURCE-COMPUTER.        IBM-3090.                                00240000
                                                                        00250000
       OBJECT-COMPUTER.        IBM-3090.                                00260000
                                                                        00270000
       INPUT-OUTPUT SECTION.                                            00280000
                                                                        00290000
       FILE-CONTROL.                                                    00300000
                                                                        00310001
           SELECT ECOM-EXT-FILE ASSIGN TO OUT01.                        00331001
                                                                        00350000
      ***************************************************************** 00360000
       DATA DIVISION.                                                   00370000
      ***************************************************************** 00380000
                                                                        00390000
       FILE SECTION.                                                    00400000
                                                                        00410000
                                                                        00480000
       FD  ECOM-EXT-FILE                                                00490001
           RECORDING MODE IS F                                          00491000
           LABEL RECORDS ARE STANDARD                                   00492000
           BLOCK CONTAINS 0 RECORDS                                     00493000
           RECORD CONTAINS 86 CHARACTERS                                00494021
           DATA RECORD IS ECOM-EXT-RECORD.                              00495001
                                                                        00496000
       01  ECOM-EXT-RECORD                PIC X(86).                    00497020
                                                                        00498000
      ***************************************************************** 00500000
       WORKING-STORAGE SECTION.                                         00510000
      ***************************************************************** 00520000
       01  PV-ECOM-RECORD.                                              00530026
           05  PV-ECOM-SLS-DATE.                                        00540026
               10  PV-ECOM-SLS-CCYY        PIC X(4).                    00550026
               10  FILLER                  PIC X(1) VALUE '-'.          00560026
               10  PV-ECOM-SLS-MM          PIC X(2).                    00570026
               10  FILLER                  PIC X(1) VALUE '-'.          00580026
               10  PV-ECOM-SLS-DD          PIC X(2).                    00590026
           05  FILLER                      PIC X(1) VALUE SPACE.        00600026
           05  PV-ECOM-ORD-DATE.                                        00610026
                10  PV-ECOM-ORD-CCYY       PIC X(4).                    00620026
                10  FILLER                 PIC X(1) VALUE '-'.          00630026
                10  PV-ECOM-ORD-MM         PIC X(2).                    00640026
                10  FILLER                 PIC X(1) VALUE '-'.          00650026
                10  PV-ECOM-ORD-DD         PIC X(2).                    00660026
           05  FILLER                      PIC X(1) VALUE SPACE.        00670026
           05  PV-ECOM-STR-NBR             PIC 9(05).                   00680026
           05  FILLER                      PIC X(1) VALUE SPACE.        00690026
           05  PV-ECOM-RGST-ID             PIC 9(02).                   00700026
           05  FILLER                      PIC X(1) VALUE SPACE.        00710026
           05  PV-ECOM-TRN-NBR             PIC 9(4).                    00720026
           05  FILLER                      PIC X(1) VALUE SPACE.        00730026
           05  PV-ECOM-STRT-TM.                                         00740026
               10  PV-ECOM-STRT-TM-HH      PIC X(2).                    00750026
               10  FILLER                  PIC X(1) VALUE ':'.          00760026
               10  PV-ECOM-STRT-TM-MM      PIC X(2).                    00770026
               10  FILLER                  PIC X(1) VALUE ':'.          00780026
               10  PV-ECOM-STRT-TM-SS      PIC X(2).                    00790026
           05  FILLER                      PIC X(1) VALUE SPACE.        00790126
           05  PV-ECOM-ORDERNO             PIC X(40).                   00790226
           05  FILLER                      PIC X(1) VALUE SPACE.        00790326
       01  PS-PROGRAM-SWITCHES.                                         00790800
           05  PS-END-OF-CURSOR-SW           PIC  X(01) VALUE 'N'.      00800004
               88  PS-END-OF-CURSOR-YES                 VALUE 'Y'.      00810004
               88  PS-END-OF-CURSOR-NO                  VALUE 'N'.      00820004
                                                                        00980000
       01  PC-CONSTANTS.                                                00990000
           05  PC-CURRENT-PROGRAM-NAME       PIC  X(08) VALUE           01000000
                                                        'SAKUT070'.     01010001
           05  PC-ROW-NOT-FOUND              PIC S9(04) VALUE +100      01020000
                                                        COMP SYNC.      01030000
                                                                        01050037
       01  PROGRAM-VARIABLES.                                           01070000
           05  PV-READ-CNT                   PIC S9(07)    VALUE ZERO.  01080001
                                                                        01140000
       01  PV-MISC-FIELDS.                                              01270000
           05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACE.   01280000
           05  PV-OPERATION-MSG-1          PIC  X(40)    VALUE SPACE.   01290000
           05  PV-OPERATION-MSG-2          PIC  X(40)    VALUE SPACE.   01300000
           05  PV-DB2-OPERATION            PIC  X(30)    VALUE SPACE.   01310000
           05  PV-WRITE-CNT                PIC  9(07)    VALUE ZERO.    01311030
       01  AC-ABEND-CODE                   PIC S9(4) VALUE ZEROS        01320030
                                                      COMP SYNC.        01330018
           88  AC-DB2-ERROR                           VALUE +4013.      01430018
                                                                        01480018
                                                                        01480100
      ***************************************************************** 01510000
      *  WS FIELDS FOR SQLCA MESSAGE ROUTINE                            01520000
      ***************************************************************** 01530000
            COPY DPWS004.                                               01540000
      ***************************************************************** 01560000
      *  DB2 COMMUNICATION AREA.                                        01570000
      ***************************************************************** 01580000
           EXEC SQL                                                     01590000
                INCLUDE SQLCA                                           01600000
           END-EXEC.                                                    01610000
                                                                        01620000
      ***************************************************************** 01633000
      *  DB2 TABLE TEPORD - ORDER NUMBER TABLE                          01640000
      ***************************************************************** 01650000
           EXEC SQL                                                     01660000
                INCLUDE TEPORD                                          01670000
           END-EXEC.                                                    01680000
                                                                        01771600
      ***************************************************************** 01772000
      *  DB2 TABLE TEPPART - PARTITION TABLE                            01780000
      ***************************************************************** 01790000
           EXEC SQL                                                     01800000
                INCLUDE TEPPART                                         01810000
           END-EXEC.                                                    01820000
      ***************************************************************** 01821004
      *  DB2 TABLE TEPTRN  - TRANSACTION TABLE                          01822004
      ***************************************************************** 01823004
           EXEC SQL                                                     01824004
                INCLUDE TEPTRN                                          01825004
           END-EXEC.                                                    01826004
                                                                        01830000
      ***************************************************************** 01840007
      *  DB2 TABLE TEPTRN  - STORE LOACTION TABLE                       01850007
      ***************************************************************** 01860007
           EXEC SQL                                                     01891008
               INCLUDE XST00001                                         01892008
           END-EXEC.                                                    01893008
                                                                        01900007
      ***************************************************************** 01900109
      *  EXTRACT E-COM PREVIOUS DAY TRANSACTION CURSOR                  01900209
      ***************************************************************** 01901009
           EXEC SQL                                                     01910009
           DECLARE ECOM-CSR CURSOR FOR                                  01911011
               SELECT P.SLS_DTE                                         01920010
                     ,T.TRN_DTE                                         01930010
                     ,T.STR_NBR                                         01940010
                     ,T.RGST_ID                                         01950010
                     ,T.TRN_NBR                                         01951019
                     ,T.STRT_TM                                         01960010
                     ,O.ORD_NBR                                         01970024
               FROM  TEPPART P                                          01980010
                    ,TEPTRN  T                                          01990026
                    ,TEPORD  O                                          02000026
                    ,XST_LOC L                                          02010026
               WHERE P.SLS_DTE = CURRENT_DATE - 1 DAYS                  02020051
                 AND P.STR_GP_CDE    = 'A'                              02030026
                 AND P.DB_STRUC_CDE  = 'O'                              02040026
                 AND P.PARTN_NBR = T.PARTN_NBR                          02050026
                 AND T.PARTN_NBR = O.PARTN_NBR                          02060026
                 AND T.STR_NBR = O.STR_NBR                              02070026
                 AND T.RGST_ID = O.RGST_ID                              02080049
C12677           AND T.RGST_ID > 15                                     02081053
                 AND T.TRN_NBR = O.TRN_NBR                              02090036
                 AND T.STRT_TM = O.STRT_TM                              02100026
                 AND T.SLS_CORR_SEQ_NBR = O.SLS_CORR_SEQ_NBR            02110026
C53632           AND (L.E_BUS_IND ='Y'                                  02121038
C53632            OR T.OMNI_CHNL_FFLMT_TYP_CDE IS NOT NULL)             02122039
                 AND L.LOC_NBR = T.STR_NBR                              02130026
               WITH UR                                                  02140026
           END-EXEC.                                                    02349309
                                                                        02349409
      ***************************************************************** 02350000
      * PROCEDURE DIVISION.                                           * 02360000
      ***************************************************************** 02370000
       PROCEDURE DIVISION.                                              02380000
                                                                        02390000
      ***************************************************************** 02400000
      * 0000-MAINLINE-PARAGRAPH                                         02410000
      ***************************************************************** 02420000
       0000-MAINLINE-PARAGRAPH.                                         02430000
                                                                        02440000
           PERFORM 1000-INITIALIZE-PROGRAM.                             02450000
           PERFORM 2000-PROCESS-PARA                                    02451004
           UNTIL PS-END-OF-CURSOR-YES.                                  02452004
           PERFORM 5000-CLOSE-ECOM-CURSOR.                              02452127
           PERFORM 9999-END-OF-ROUTINE.                                 02452228
           GOBACK.                                                      02453015
                                                                        02454015
           EJECT                                                        02455015
      ***************************************************************** 02860000
      * 1000-INITIALIZE-PROGRAM                                         02870000
      *     PROCESS CONTROL CARD                                        02880000
      ***************************************************************** 02890000
       1000-INITIALIZE-PROGRAM.                                         02900000
                                                                        02910000
           OPEN OUTPUT  ECOM-EXT-FILE.                                  02920004
           PERFORM 1100-OPEN-CURSOR.                                    03090004
      *1000-INITIALIZE-PROGRAM-EXIT.                                    03100000
      ***************************************************************** 03110005
      * 1100-OPEN-CURSOR.                                               03120005
      ***************************************************************** 08571700
       1100-OPEN-CURSOR.                                                08571805
            EXEC SQL                                                    08571912
              OPEN ECOM-CSR                                             08572012
            END-EXEC.                                                   08572112
                                                                        08574112
           EVALUATE TRUE                                                08574212
               WHEN SQLCODE = +0                                        08574312
                    SET PS-END-OF-CURSOR-NO TO TRUE                     08574412
               WHEN SQLWARN0 NOT EQUAL SPACE                            08574712
               WHEN SQLCODE NOT EQUAL ZERO                              08574812
                    MOVE '1100-OPEN-CSR' TO PV-PARAGRAPH-NAME           08574912
                    MOVE 'OPEN DB2 CURSOR    ' TO PV-DB2-OPERATION      08575012
                    PERFORM 9900-SQL-ABEND-ROUTINE                      08575112
           END-EVALUATE.                                                08575212
      * 1100-OPEN-CURSOR-EXIT.                                          08575412
      ***************************************************************** 08575512
      * 2000-PROCESS-PARA                                               08575612
      ***************************************************************** 08575712
       2000-PROCESS-PARA.                                               08575812
            PERFORM  3000-FETCH-CURSOR.                                 08575916
            IF PS-END-OF-CURSOR-NO                                      08576012
               PERFORM  4000-WRTITE-ECOM-EXTRACT                        08576117
            END-IF.                                                     08576212
      * 2000-PROCESS-PARA-EXIT.                                         08576312
      ***************************************************************** 08576412
      * 3000-FETCH-CURSOR                                               08576512
      ***************************************************************** 08576612
       3000-FETCH-CURSOR.                                               08576716
            EXEC SQL                                                    08576812
              FETCH ECOM-CSR INTO                                       08576912
                    :EPPART-SLS-DTE                                     08577012
                   ,:EPTRN-TRN-DTE                                      08577112
                   ,:EPTRN-STR-NBR                                      08577212
                   ,:EPTRN-RGST-ID                                      08577312
                   ,:EPTRN-TRN-NBR                                      08577412
                   ,:EPTRN-STRT-TM                                      08577512
                   ,:EPORD-ORD-NBR                                      08577613
            END-EXEC.                                                   08577713
                                                                        08577813
           EVALUATE TRUE                                                08577913
               WHEN SQLCODE = +0                                        08578013
                    SET PS-END-OF-CURSOR-NO TO TRUE                     08578113
               WHEN SQLCODE = +100                                      08578213
                    SET PS-END-OF-CURSOR-YES TO TRUE                    08578313
               WHEN SQLWARN0 NOT EQUAL SPACE                            08578413
               WHEN SQLCODE NOT EQUAL ZERO                              08578513
                    MOVE '3000-FETCH-CURSOR' TO  PV-PARAGRAPH-NAME      08578613
                    MOVE 'ERROR ON FETCHING' TO   PV-DB2-OPERATION      08578713
                    PERFORM 9900-SQL-ABEND-ROUTINE                      08578813
           END-EVALUATE.                                                08578913
      * 3000-FETCH-CURSOR-EXIT.                                         08579013
      ***************************************************************** 08579113
      *  4000-WRTITE-ECOM-EXTRACT                                       08579214
      ***************************************************************** 08579413
       4000-WRTITE-ECOM-EXTRACT.                                        08579514
            MOVE  EPPART-SLS-DTE TO  PV-ECOM-SLS-DATE.                  08579626
            MOVE  EPTRN-TRN-DTE  TO  PV-ECOM-ORD-DATE.                  08579726
            MOVE  EPTRN-STR-NBR  TO  PV-ECOM-STR-NBR.                   08579826
            MOVE  EPTRN-RGST-ID  TO  PV-ECOM-RGST-ID.                   08579926
            MOVE  EPTRN-TRN-NBR  TO  PV-ECOM-TRN-NBR.                   08580026
            MOVE  EPTRN-STRT-TM  TO  PV-ECOM-STRT-TM.                   08580126
            MOVE  EPORD-ORD-NBR-TEXT TO  PV-ECOM-ORDERNO.               08580226
            WRITE ECOM-EXT-RECORD FROM PV-ECOM-RECORD.                  08580526
            ADD 1 TO PV-WRITE-CNT.                                      08580630
C12677      INITIALIZE EPORD-ORD-NBR-TEXT.                              08580753
      *4000-WRTITE-ECOM-EXTRACT-EXIT.                                   08580814
      ***************************************************************** 08580927
      * 5000-CLOSE-ECOM-CURSOR                                          08581027
      ***************************************************************** 08581135
       5000-CLOSE-ECOM-CURSOR.                                          08581227
                                                                        08581327
           EXEC SQL                                                     08581427
                CLOSE ECOM-CSR                                          08581527
           END-EXEC.                                                    08581627
                                                                        08581727
           EVALUATE TRUE                                                08581827
               WHEN SQLCODE = ZERO                                      08581927
                    CONTINUE                                            08582027
                                                                        08582127
               WHEN SQLWARN0 NOT EQUAL SPACE                            08582227
               WHEN SQLCODE NOT EQUAL ZERO                              08582327
                   MOVE '5000-CLOSE-ECOM-CURSOR'                        08582427
                                         TO PV-PARAGRAPH-NAME           08582527
                   MOVE 'ERROR ON CLOSE OF ECOM CURSOR'                 08582627
                                         TO PV-DB2-OPERATION            08582732
                   PERFORM 9900-SQL-ABEND-ROUTINE                       08582834
           END-EVALUATE.                                                08582927
      *5000-CLOSE-ECOM-CURSOR-EXIT.                                     08583028
      ***************************************************************** 08583114
      * 9900-SQL-ABEND-ROUTINE                                          08583214
      *     ABEND PROGRAM WHEN AN SQL ERROR CODE OR WARNING CODE OCCURS.08583314
      ***************************************************************** 08583414
       9900-SQL-ABEND-ROUTINE.                                          08584014
           DISPLAY '** ABEND     **'.                                   08590000
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        08600000
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              08610000
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               08620000
      ***************************************************************** 08680000
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     08690000
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      08700000
      ***************************************************************** 08710000
            COPY DPPD004.                                               08720000
                                                                        08730000
           SET AC-DB2-ERROR TO TRUE.                                    08740000
           CALL 'ILBOABN0' USING AC-ABEND-CODE.                         08750000
                                                                        08760000
      ***************************************************************** 08770028
      * 9999-END-OF-ROUTINE                                             08780028
      ***************************************************************** 08810028
        9999-END-OF-ROUTINE.                                            08810131
           CLOSE ECOM-EXT-FILE.                                         08811029
           DISPLAY 'NUMBER OF RECORDS WRITTEN:' PV-WRITE-CNT.           08811130
           DISPLAY 'END OF SAKUT070 PROGRAM'.                           08812030
      *9999-END-OF-ROUTINE-EXIT                                         08820028
