000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.   TRKUT212.                                                  
000600 AUTHOR.       MICHAEL FRIESS.                                            
000700 INSTALLATION. KOHLS DEPARTMENT STORES.                                   
000800 DATE-WRITTEN. SEPTEMBER, 2009.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001002******************************************************************        
001005*  JOB TRK212 - AIR AND LANDED ASN PROCESS.  THIS PROGRAM WILL   *        
001006*  READ THE 400 BYTE FORMATTED AIR OR LANDED ASN FILE, ADD SORT  *        
001007*  DATA TO THE END OF EACH RECORD AND WRITE IT BACK TO A 500     *        
001008*  BYTE RECORD LENGTH FILE.                                      *        
001009*                                                                *        
001010*  THE PURPOSE OF THE SORT FILE IS TO PLACE CARTONS (UPCS) UNDER *        
001011*  THEIR APPROPRIATE PO HIERARCHICAL RECORDS.  THIS HAS TO BE    *        
001012*  DONE BECAUSE NIKE OFTEN SENDS THE SAME PO MULTIPLE TIMES ON   *        
001013*  AN ASN, E.G. SEND ONE PO WITH A SET OF CARTONS FOLLOWED BY A  *        
001014*  DIFFERENT PO, THEN REPEAT THE FIRST PO WITH A SECOND SET OF   *        
001015*  CARTONS.  TRKUP212 DOES NOT HANDLE THIS AND CONTAINER LOAD    *        
001016*  REJECTS THE SECOND PO/CARTON SET WITH A "DUPLICATE PO" ERROR. *        
001017*                                                                *        
001018*  INPUT/OUTPUT                                                  *        
001019*                                                                *        
001020*   TRRD035 - ASN INPUT RECORDS                                  *        
001021*                                                                *        
001022*   PV-OUT-SORT-REC - ASN RECORDS WITH ADDED SORT FIELDS         *        
001023*                                                                *        
002401******************************************************************        
002402 ENVIRONMENT DIVISION.                                                    
002403******************************************************************        
002404                                                                          
002405 CONFIGURATION SECTION.                                                   
002406                                                                          
002407 SOURCE-COMPUTER.        IBM-3090.                                        
002408 OBJECT-COMPUTER.        IBM-3090.                                        
002409                                                                          
002410 INPUT-OUTPUT SECTION.                                                    
002411                                                                          
002412 FILE-CONTROL.                                                            
002500                                                                          
002600     SELECT TR063-ASN-FILE                                                
002700         ASSIGN TO INP01.                                                 
002800                                                                          
002900     SELECT PV-OUT-SORT-FILE                                              
003000         ASSIGN TO OUT01.                                                 
003100                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
004100 FD  TR063-ASN-FILE                                                       
004200     RECORDING MODE IS F                                                  
004300     LABEL RECORDS ARE STANDARD                                           
004400     BLOCK CONTAINS 0 RECORDS.                                            
004500                                                                          
004600 01  TR063-ASN-RECORD                PIC X(400).                          
004700                                                                          
004800 FD  PV-OUT-SORT-FILE                                                     
004900     RECORDING MODE IS F                                                  
005000     LABEL RECORDS ARE STANDARD                                           
005100     BLOCK CONTAINS 0 RECORDS.                                            
005200                                                                          
005300 01  PV-OUT-SORT-RECORD              PIC X(500).                          
005400                                                                          
006107                                                                          
006300 WORKING-STORAGE SECTION.                                                 
006500                                                                          
006509*****************************************************************         
006510*    ASN DETAIL INPUT RECORD LAYOUT                                       
006511*****************************************************************         
006512     COPY TRRD063.                                                        
127313                                                                          
127314*****************************************************************         
127315*    OUTPUT RECORD WITH ADDED SORT FIELDS LAYOUT.                         
127316*****************************************************************         
127317 01  PV-OUT-SORT-REC-LAYOUT.                                              
127319     05  FILLER                             PIC X(350).                   
127320     05  PV-OS-ISA-CONTROL.                                               
127321         10  PV-OS-ISA-CNTL-NBR-SEQ         PIC 9(9).                     
127322         10  PV-OS-SHIPT-ID-SEQ             PIC X(30).                    
127323         10  PV-OS-DAT-XFER-CNTL            PIC 9(9).                     
127324     05  FILLER                             PIC X(02).                    
127325     05  PV-OS-CONTAINER-SORT.                                            
127326         10  PV-OS-TD301-EQUIP-CDE          PIC X(2).                     
127327         10  PV-OS-TD302-EQUIP-INIT-CDE     PIC X(4).                     
127328         10  PV-OS-TD303-EQUIP-NBR          PIC X(10).                    
127329     05  FILLER                             PIC X(01).                    
127330     05  PV-OS-PO-NBR-SORT                  PIC X(22).                    
127331     05  FILLER                             PIC X(01).                    
127332     05  PV-OS-ORIGINAL-REC-SEQ             PIC 9(9).                     
127333     05  FILLER                             PIC X(01).                    
127334     05  PS-OS-PO-GROUP-REC-ID              PIC X(02).                    
127335     05  FILLER                             PIC X(48).                    
127336                                                                          
127338 01  PA-PROGRAM-ACCUMULATORS.                                             
127339     05  PA-TOTALS.                                                       
127340         10 PA-INPUT-COUNT           PIC  9(13) VALUE 0.                  
127342         10 PA-ERROR-COUNT           PIC  9(13) VALUE 0.                  
127346         10 PA-OUTPUT-COUNT          PIC  9(13) VALUE 0.                  
127364                                                                          
127365 01  PS-PROGRAM-SWITCHES.                                                 
127366     05  PS-END-OF-ASN-SW            PIC  X(01)    VALUE 'N'.             
127367         88  PS-END-OF-ASN                         VALUE 'Y'.             
127368         88  PS-END-OF-ASN-NO                      VALUE 'N'.             
127451     05  PS-THIS-IS-PO-GROUP-SW      PIC  X(01)    VALUE 'N'.             
127452         88  PS-THIS-IS-PO-GROUP                   VALUE 'Y'.             
127453         88  PS-THIS-IS-PO-GROUP-NO                VALUE 'N'.             
127457                                                                          
127458 01  PC-PROGRAM-CONSTANTS.                                                
127459     05  PC-CURRENT-PROGRAM-NAME     PIC X(08)     VALUE                  
127460         'TRKUT212'.                                                      
127486                                                                          
127487 01 PV-PROGRAM-VARIABLES.                                                 
127488     05  PV-HOLD-CONTAINER-SORT.                                          
127489         10  PV-HOLD-EQUIP-CDE       PIC X(2)  VALUE LOW-VALUES.          
127490         10  PV-HOLD-EQUIP-INIT-CDE  PIC X(4)  VALUE LOW-VALUES.          
127491         10  PV-HOLD-EQUIP-NBR       PIC X(10) VALUE LOW-VALUES.          
127492     05  PV-HOLD-PO-NBR-SORT         PIC X(22) VALUE LOW-VALUES.          
127493     05  PS-HOLD-PO-GROUP-REC-ID     PIC X(02) VALUE SPACES.              
127494                                                                          
127495     05  PV-HOLD-TR063-HL-ORD-REC           PIC X(400).                   
127496                                                                          
127500                                                                          
127501     05  PV-PARAGRAPH-NAME           PIC X(30)     VALUE SPACES.          
127502     05  PV-ABEND-INFO               PIC X(80)     VALUE SPACES.          
127511                                                                          
127900     05  PV-PREV-RECORD-LEVEL-CDE    PIC X(2).                            
128000         88  PV-PREV-ID-LEVEL                      VALUE 'ID'.            
128100         88  PV-PREV-SHIPMENT-LEVEL                VALUE 'S '.            
128200         88  PV-PREV-ORDER-LEVEL                   VALUE 'O '.            
128300         88  PV-PREV-PACK-LEVEL                    VALUE 'P '.            
128400         88  PV-PREV-ITEM-LEVEL                    VALUE 'I '.            
128410                                                                          
128420 01  ABEND-CODE       PIC S9(4) COMP SYNC VALUE ZERO.                     
128430       88  ABEND-4001-INPUT-OUTPUT-UNEQL  VALUE +4001.                    
247465                                                                          
247466 LINKAGE SECTION.                                                         
247467                                                                          
247468 PROCEDURE DIVISION.                                                      
247469                                                                          
247470******************************************************************        
247471*  0000-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE PROGRAM.  *        
247472******************************************************************        
247473                                                                          
247480 0000-PROGRAM-MAINLINE.                                                   
247490                                                                          
494665***  DISPLAY '0000-PROGRAM-MAINLINE'.                                     
494666                                                                          
494667     PERFORM 1000-INITIALIZE.                                             
494668                                                                          
494669     IF PS-END-OF-ASN                                                     
494670        DISPLAY 'EMTY FILE'                                               
494671     ELSE                                                                 
494672        PERFORM 2000-MAIN-LINE-PROCESS                                    
494673           UNTIL PS-END-OF-ASN.                                           
494674                                                                          
494675     PERFORM 8000-EOJ-ROUTINE.                                            
494676     STOP RUN.                                                            
494677                                                                          
494678******************************************************************        
494679*  OPENS INPUT AND OUTPUT FILES AND PERFORMS OTHER BEGINNING OF  *        
494680*  JOB PROCESSES.                                                *        
494681******************************************************************        
494682                                                                          
494683 1000-INITIALIZE.                                                         
494684                                                                          
494685***  DISPLAY '1000-INITIALIZE'.                                           
494686                                                                          
494687     OPEN INPUT TR063-ASN-FILE                                            
494688          OUTPUT PV-OUT-SORT-FILE.                                        
494693                                                                          
494694*    PERFORM INITIAL READ                                                 
494695     PERFORM 6000-READ-ASN-FILE.                                          
494696                                                                          
494697 EJECT                                                                    
494764                                                                          
494765******************************************************************        
494766* MAIN PROCESSING PARAGRAPH.                                     *        
494767******************************************************************        
494768 2000-MAIN-LINE-PROCESS.                                                  
494769                                                                          
494770***  DISPLAY '2000-MAIN-LINE-PROCESS'.                                    
494771                                                                          
494772     EVALUATE TRUE                                                        
494773                                                                          
494775      WHEN ID-IDENTIFICATION-REC                                          
494776*     **** ID REC (ASN HEADER)                                            
494777        INITIALIZE PV-HOLD-CONTAINER-SORT                                 
494778                   PV-HOLD-PO-NBR-SORT                                    
494779                   PS-HOLD-PO-GROUP-REC-ID                                
494780        PERFORM 2500-PREP-FOR-WRITE                                       
494781        PERFORM 6100-WRITE-OUT-REC                                        
494782                                                                          
494783      WHEN TD3-CARRIER-EQUIPMENT-REC                                      
494784*     **** CONTAINER REC                                                  
494785        INITIALIZE PS-HOLD-PO-GROUP-REC-ID                                
494786        MOVE TR063-TD301-EQUIP-CDE      TO PV-HOLD-EQUIP-CDE              
494787        MOVE TR063-TD302-EQUIP-INIT-CDE TO PV-HOLD-EQUIP-INIT-CDE         
494788        MOVE TR063-TD303-EQUIP-NBR      TO PV-HOLD-EQUIP-NBR              
494789        PERFORM 2500-PREP-FOR-WRITE                                       
494790        PERFORM 6100-WRITE-OUT-REC                                        
494791                                                                          
494792      WHEN HL-HIERARCHICAL-REC                                            
494793        INITIALIZE PS-HOLD-PO-GROUP-REC-ID                                
495319        IF ORDER-LEVEL                                                    
495320*         *** THIS IS THE HL REC JUST BEFORE THE PO REC.                  
495321*         *** JUST SAVE IT BECAUSE YOU CAN NOT WRITE IT UNTIL             
495322*         *** YOU GET PO # FROM PO (PRF) REC WHICH S/B NEXT.              
495323          MOVE TR063-HL-SEGMENT TO PV-HOLD-TR063-HL-ORD-REC               
495324*         *** MARK FROM THIS REC UNTIL NEXT HL AS PO HEADERS              
495325          MOVE 'PO' TO PS-HOLD-PO-GROUP-REC-ID                            
495326        ELSE                                                              
495327          PERFORM 2500-PREP-FOR-WRITE                                     
495328          PERFORM 6100-WRITE-OUT-REC                                      
495329        END-IF                                                            
495331                                                                          
495332      WHEN PRF-PURCHASE-ORDER-REF-REC                                     
495333        MOVE TR063-PRF01-PO-NBR TO PV-HOLD-PO-NBR-SORT                    
495334        PERFORM 2600-PREP-SAVED-HL-REC                                    
495335        PERFORM 6100-WRITE-OUT-REC                                        
495336        PERFORM 2500-PREP-FOR-WRITE                                       
495337        PERFORM 6100-WRITE-OUT-REC                                        
495338                                                                          
495339      WHEN OTHER                                                          
495340        PERFORM  2500-PREP-FOR-WRITE                                      
495341        PERFORM  6100-WRITE-OUT-REC                                       
495342                                                                          
495343     END-EVALUATE.                                                        
495344                                                                          
495345     PERFORM 6000-READ-ASN-FILE.                                          
495346                                                                          
495347******************************************************************        
495348* MOVE ORIGINAL RECORD AND AND NEW SORT FIELDS TO OUTPUT RECORD.          
495349******************************************************************        
495350                                                                          
495351 2500-PREP-FOR-WRITE.                                                     
495352                                                                          
495353***  DISPLAY '2500-PREP-FOR-WRITE'.                                       
495354     MOVE TR063-SHIP-NOTICE-DTL-RECORD                                    
495355                                  TO PV-OUT-SORT-REC-LAYOUT.              
495356                                                                          
495357*    *** ADD THE SORT FIELDS TO THE ORIGINAL INPUT REC                    
495358     MOVE PV-HOLD-CONTAINER-SORT  TO PV-OS-CONTAINER-SORT.                
495359     MOVE PV-HOLD-PO-NBR-SORT     TO PV-OS-PO-NBR-SORT.                   
495360     MOVE PS-HOLD-PO-GROUP-REC-ID TO PS-OS-PO-GROUP-REC-ID.               
495361     MOVE PA-INPUT-COUNT          TO PV-OS-ORIGINAL-REC-SEQ.              
495362                                                                          
495363******************************************************************        
495365* MOVE SAVED HL REC AND NEW SORT FIELDS TO OUTPUT RECORD.                 
495366******************************************************************        
495367                                                                          
495368 2600-PREP-SAVED-HL-REC.                                                  
495369                                                                          
495370***  DISPLAY '2600-PREP-SAVED-HL-REC'.                                    
495372     MOVE PV-HOLD-TR063-HL-ORD-REC TO PV-OUT-SORT-REC-LAYOUT.             
495373                                                                          
495374*    *** ADD THE SORT FIELDS TO THE ORIGINAL INPUT REC                    
495375     MOVE PV-HOLD-CONTAINER-SORT  TO PV-OS-CONTAINER-SORT.                
495376     MOVE PV-HOLD-PO-NBR-SORT     TO PV-OS-PO-NBR-SORT.                   
495377     MOVE PS-HOLD-PO-GROUP-REC-ID TO PS-OS-PO-GROUP-REC-ID.               
495378*    *** WE ARE WRITING THE PREV REC SO WE                                
495379*    *** NEED SEQ FROM CURRENT REC MINUS 1.                               
495381     COMPUTE PV-OS-ORIGINAL-REC-SEQ = (PA-INPUT-COUNT - 1).               
495390                                                                          
809217******************************************************************        
809218* READ A RECORD FROM THE ASN FILE.                                        
809219******************************************************************        
809220 6000-READ-ASN-FILE.                                                      
809221                                                                          
809222***  DISPLAY '6000-READ-ASN-FILE'.                                        
809230                                                                          
809233     READ TR063-ASN-FILE                                                  
809234         INTO TR063-ADV-SHIP-NOTICE-DTL-REC                               
809235        AT END                                                            
809236           SET PS-END-OF-ASN TO TRUE.                                     
809237                                                                          
809238     IF PS-END-OF-ASN-NO                                                  
809301       ADD +1 TO PA-INPUT-COUNT                                           
809303     END-IF.                                                              
810409                                                                          
810410*****************************************************************         
810411* WRITE UPDATE RECORD                                                     
810412*****************************************************************         
810413 6100-WRITE-OUT-REC.                                                      
810414                                                                          
810415***  DISPLAY '6100-WRITE-OUT-REC'.                                        
810416                                                                          
811502     WRITE PV-OUT-SORT-RECORD FROM PV-OUT-SORT-REC-LAYOUT.                
811503     ADD +1 TO PA-OUTPUT-COUNT.                                           
811504                                                                          
836191                                                                          
836192******************************************************************        
836193* CLOSE THE INPUT AND OUTPUT FILES AND DISPLAY MISCELLANEOUS     *        
836194* JOB SUMMARY INFORMATION.                                       *        
836200******************************************************************        
836300 8000-EOJ-ROUTINE.                                                        
836301                                                                          
836310***  DISPLAY '8000-EOJ-ROUTINE'.                                          
836400                                                                          
842300     CLOSE TR063-ASN-FILE                                                 
842400           PV-OUT-SORT-FILE.                                              
843002                                                                          
843003     DISPLAY '*************************************************'.         
843004     DISPLAY '** TRKUT212 SUMMARY                            **'.         
843005     DISPLAY '*************************************************'.         
843006     DISPLAY 'TOTAL RECORDS READ     = ' PA-INPUT-COUNT.                  
843007***  DISPLAY '*************************************************'.         
843008***  DISPLAY 'TOTAL ERROR COUNT      = ' PA-ERROR-COUNT.                  
843009     DISPLAY '*************************************************'.         
843010     DISPLAY 'TOTAL RECORDS WRITTEN  = ' PA-OUTPUT-COUNT.                 
843016     DISPLAY '*************************************************'.         
843017                                                                          
843018     IF PA-INPUT-COUNT = PA-OUTPUT-COUNT                                  
843019        CONTINUE                                                          
843020     ELSE                                                                 
843021        SET ABEND-4001-INPUT-OUTPUT-UNEQL TO TRUE                         
843023        MOVE '8000-EOJ-ROUTINE' TO PV-PARAGRAPH-NAME                      
843024        MOVE 'INPUT RECORD COUNT NOT EQUAL TO OUTPUT COUNT'               
843025                                TO PV-ABEND-INFO                          
843026        PERFORM 9000-USER-ABEND                                           
843027     END-IF.                                                              
843033                                                                          
843035******************************************************************        
843037* USER ABEND                                                     *        
843038******************************************************************        
843039 9000-USER-ABEND.                                                         
843040                                                                          
843041***  DISPLAY '9000-USER-ABEND'.                                           
843042                                                                          
843043     DISPLAY '+++++++++++++++++++++++++++++++++++++++++++++'.             
843044     DISPLAY 'THIS IS A USER ABEND GENERATED BY PARAGRAPH -'.             
843045     DISPLAY ' ' PV-PARAGRAPH-NAME,                                       
843046     DISPLAY ' ' PV-ABEND-INFO.                                           
843047     CALL 'ILBOABN0' USING ABEND-CODE.                                    
843050                                                                          
