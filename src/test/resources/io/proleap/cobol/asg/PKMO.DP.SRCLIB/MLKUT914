       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    MLKUT914.                                                 
       AUTHOR.        COGNIZANT.                                                
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  08-24-2012.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKUT914 - DESCRIPTION.............                                     
      *                                                                         
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * PKE11775 09/14/2012 COGNIZANT  CONVERTED THE UNLOAD UTILITY             
      *                                INTO A COBOL PROGRAM                     
      *                                RETURN CODE IS SET TO 4090 IF            
      *                                THERE ARE NO RECORDS FETCHED             
      *                                FOR THE CORRESPONDING DATE.              
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT DATE-IN                                                       
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT UNLOAD-MNL-SUM-TSL                                            
               ASSIGN TO OUT01.                                                 
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
       FILE SECTION.                                                            
                                                                                
       FD  DATE-IN                                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE  F                                                    
           BLOCK CONTAINS 0 RECORDS.                                            
       01  DATE-IN-REC                      PIC X(80).                          
                                                                                
       FD  UNLOAD-MNL-SUM-TSL                                                   
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE  F                                                    
           BLOCK CONTAINS 0 RECORDS.                                            
       01  UNLOAD-MNL-SUM-TSL-REC           PIC X(129).                         
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05 PV-TAB-LOC-READ               PIC S9(09) VALUE +0  COMP.          
           05 PV-TOTAL-RECS-WRITTEN         PIC S9(09) VALUE +0  COMP.          
           05 PV-RETURN-CODE-4090           PIC 9(04) VALUE 4090.               
       01  PV-BDI-DATE-FILE.                                                    
           05 PV-BDI-DATES                  PIC X(10).                          
       01  PV-OUTPUT-REC-LAYOUT.                                                
           05 PV-DEPT-NBR                   PIC  S9(4)V COMP-3.                 
           05 PV-ML-LO-MDSE-LVL-ID          PIC  S9(9) COMP.                    
           05 PV-FSCL-MN-END-DTE            PIC  X(10).                         
           05 PV-MKDN-BYR-RTL-AMT           PIC  S9(11)V9(2) COMP-3.            
           05 PV-MKDN-RTL-AMT               PIC  S9(11)V9(2) COMP-3.            
           05 PV-MKUP-RTL-AMT               PIC  S9(11)V9(2) COMP-3.            
           05 PV-RCPT-RTL-AMT               PIC  S9(11)V9(2) COMP-3.            
           05 PV-RCPT-NET-COST-AMT          PIC  S9(11)V9(2) COMP-3.            
           05 PV-RCPT-GRO-COST-AMT          PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-RTL-AMT               PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-FRGT-COST-AMT         PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-DISC-COST-AMT         PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-RTV-COST-AMT          PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-RTV-RTL-AMT           PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-DA-COST-AMT           PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-DA-RTL-AMT            PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-NET-COST-AMT          PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-OTH-COST-AMT          PIC  S9(11)V9(2) COMP-3.            
           05 PV-PADJ-OTH-RTL-AMT           PIC  S9(11)V9(2) COMP-3.            
                                                                                
       01  PV-DATE-IN-REC.                                                      
           05  FILER                        PIC X(01).                          
           05  PV-BDI-DATE                  PIC X(10).                          
           05  FILER                        PIC X(69).                          
      *** VALID DATE FORMAT FOR CONTROL CARD 'YYYY-MM-DD' ***                   
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-TAB-ULD-DONE              PIC  X(01)      VALUE 'N'.          
               88  PS-TAB-ULD-IS-DONE                       VALUE 'Y'.          
                                                                                
       EJECT                                                                    
                                                                                
      *-----------------------------------------------------------------        
      *  ABEND AREA                                                             
      *-----------------------------------------------------------------        
       01 ABEND-AREAS.                                                          
           05 ABEND-CODE                    PIC S9(08)  VALUE ZERO.             
               88  AC-DB2-ERROR                          VALUE +4013.           
           05  AA-ABEND-LIT                 PIC X(40)   VALUE                   
                   '***** ABEND'.                                               
           05  AA-PROGRAM-LIT               PIC X(40)   VALUE                   
                   '***** PROGRAM: MLKUT914'.                                   
           05  AA-PARAGRAPH-LIT.                                                
               10  FILLER                   PIC X(17)   VALUE                   
                   '***** PARAGRAPH: '.                                         
               10  AA-PARAGRAPH-NAME        PIC X(35)   VALUE SPACES.           
           05  AA-MESSAGE-LINE.                                                 
               10  FILLER                   PIC X(06)   VALUE '*****'.          
               10  AA-MESSAGE               PIC X(127)  VALUE SPACES.           
           05  AA-DB2-ERROR-LIT             PIC X(40)   VALUE                   
                   '*****    DB2 ERROR'.                                        
           05  AA-DB2-OPERATION-LIT.                                            
               10  FILLER                   PIC X(17)   VALUE                   
                   '***** OPERATION: '.                                         
               10  AA-DB2-OPERATION.                                            
                   15  AA-DB2-OP-1          PIC X(25)   VALUE SPACES.           
                   15  AA-DB2-OP-2          PIC X(25)   VALUE SPACES.           
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *  DB2 ERROR MESSAGES FORMAT AREA.                                        
      ******************************************************************        
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  USED FOR DB2 ERRORS                                                    
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  LAYOUT FOR DATE TABLE                                                  
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
      ******************************************************************        
      *    DCLGEN FOR MLT_MNLY_SUM                                     *        
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE MLT00004                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      ******************************************************************        
      *    DCLGEN FOR MLT_PARTN_CNTL                                   *        
      ******************************************************************        
      *                                                                         
           EXEC SQL                                                             
               INCLUDE MLT00003                                                 
           END-EXEC.                                                            
      *                                                                         
      ******************************************************************        
      *  DB2 TABLE CURSOR                                                       
      ******************************************************************        
           EXEC SQL                                                             
                DECLARE TABLE_CURSOR CURSOR FOR                                 
               SELECT  DEPT_NBR                                                 
               ,ML_LO_MDSE_LVL_ID                                               
               ,FSCL_MN_END_DTE                                                 
               ,MKDN_BYR_RTL_AMT                                                
               ,MKDN_RTL_AMT                                                    
               ,MKUP_RTL_AMT                                                    
               ,RCPT_RTL_AMT                                                    
               ,RCPT_NET_COST_AMT                                               
               ,RCPT_GRO_COST_AMT                                               
               ,PADJ_RTL_AMT                                                    
               ,PADJ_FRGT_COST_AMT                                              
               ,PADJ_DISC_COST_AMT                                              
               ,PADJ_RTV_COST_AMT                                               
               ,PADJ_RTV_RTL_AMT                                                
               ,PADJ_DA_COST_AMT                                                
               ,PADJ_DA_RTL_AMT                                                 
               ,PADJ_NET_COST_AMT                                               
               ,PADJ_OTH_COST_AMT                                               
               ,PADJ_OTH_RTL_AMT                                                
           FROM  MLT_MNLY_SUM                                                   
               WHERE PARTN_NBR IN                                               
                     (SELECT DISTINCT(PARTN_NBR)                                
                         FROM MLT_PARTN_CNTL                                    
                         WHERE PARTN_NBR IN                                     
                               (SELECT PARTN_NBR                                
                                    FROM MLT_PARTN_CNTL                         
                                      WHERE TBL_TM_LVL_CDE = 'M'                
                                      AND FSCL_DTE =                            
                                        (SELECT FSCL_MN_END_DTE                 
                                             FROM TDTATTR                       
                                         WHERE KY_DTE = :PV-BDI-DATES           
                                        )                                       
                               )                                                
                     )                                                          
               AND ML_LO_MDSE_LVL_ID IN                                         
                  (SELECT DEPT_BRND_VND_ID                                      
                      FROM IMT_DEPT_BRND_VND)                                   
           END-EXEC.                                                            
                                                                                
       EJECT                                                                    
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0100-MAINLINE-MLKUT914.                                                  
           DISPLAY '*************************************************'.         
           DISPLAY '**         MLKUT914 MESSAGES FOLLOW            **'.         
           DISPLAY '*************************************************'.         
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM 3000-MAIN-PROCESS-DATES.                                     
                                                                                
           PERFORM 9000-END-OF-JOB.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
       1000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN INPUT  DATE-IN.                                                 
           OPEN OUTPUT UNLOAD-MNL-SUM-TSL.                                      
                                                                                
           PERFORM 2100-READ-DATE-FILE.                                         
       EJECT                                                                    
      ******************************************************************        
       2100-READ-DATE-FILE.                                                     
      ***THE CONTROL CARD IS CHECKED FOR THE DATE AND THE PROG IS MADE          
      ***TO ABEND IF IT IS EMPTY.                                               
      ******************************************************************        
                                                                                
           READ DATE-IN INTO PV-DATE-IN-REC                                     
               AT END                                                           
                   DISPLAY '** NO DATE IN CONTROL CARD **'                      
                   DISPLAY '************-X-**************'                      
                   MOVE '2100-READ-DATE-FILE' TO AA-PARAGRAPH-LIT               
                   PERFORM 9900-COMMON-ABEND-ROUTINE                            
               NOT AT END                                                       
                   DISPLAY '                             '                      
                   DISPLAY '************-X-**************'                      
                   DISPLAY 'INPUT DATE IS : ' PV-BDI-DATE.                      
                   DISPLAY 'VALID FORMAT: YYYY-MM-DD'                           
                   DISPLAY '************-X-**************'                      
                   DISPLAY '                             '                      
           MOVE PV-BDI-DATE TO PV-BDI-DATES.                                    
      ******************************************************************        
       3000-MAIN-PROCESS-DATES.                                                 
      ***THE DATE FROM THE CC CARD IS USED IN THE CURSOR TO FETCH ROWS**        
      ******************************************************************        
           PERFORM 7000-OPEN-CURSOR-MLT-MNLY-SUM.                               
                                                                                
           PERFORM 7100-FETCH-MLT-MNLY-SUM                                      
             UNTIL PS-TAB-ULD-IS-DONE.                                          
                                                                                
           PERFORM 7200-CLOSE-CURSOR-MLT-MNLY-SUM.                              
                                                                                
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7000-OPEN-CURSOR-MLT-MNLY-SUM.                                           
      ****THE DECLARED CURSOR IS OPENED HERE *************************          
      ******************************************************************        
      *                                                                         
                                                                                
           EXEC SQL                                                             
               OPEN TABLE_CURSOR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = 0                                                 
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7000-OPEN-CURSOR-MLT-MNLY-SUM'                         
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'MLT_MNLY_SUM TABLE ERROR'                              
                                       TO AA-MESSAGE                            
                   MOVE 'OPEN CURSOR'  TO AA-DB2-OPERATION                      
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
      ******************************************************************        
      ******************************************************************        
       7100-FETCH-MLT-MNLY-SUM.                                                 
      ***THE RECORDS ARE FETCHED FOR THE GIVEN INPUT DATE                       
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               FETCH TABLE_CURSOR                                               
               INTO                                                             
               :PV-DEPT-NBR                                                     
               ,:PV-ML-LO-MDSE-LVL-ID                                           
               ,:PV-FSCL-MN-END-DTE                                             
               ,:PV-MKDN-BYR-RTL-AMT                                            
               ,:PV-MKDN-RTL-AMT                                                
               ,:PV-MKUP-RTL-AMT                                                
               ,:PV-RCPT-RTL-AMT                                                
               ,:PV-RCPT-NET-COST-AMT                                           
               ,:PV-RCPT-GRO-COST-AMT                                           
               ,:PV-PADJ-RTL-AMT                                                
               ,:PV-PADJ-FRGT-COST-AMT                                          
               ,:PV-PADJ-DISC-COST-AMT                                          
               ,:PV-PADJ-RTV-COST-AMT                                           
               ,:PV-PADJ-RTV-RTL-AMT                                            
               ,:PV-PADJ-DA-COST-AMT                                            
               ,:PV-PADJ-DA-RTL-AMT                                             
               ,:PV-PADJ-NET-COST-AMT                                           
               ,:PV-PADJ-OTH-COST-AMT                                           
               ,:PV-PADJ-OTH-RTL-AMT                                            
                                                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                MOVE PV-OUTPUT-REC-LAYOUT TO UNLOAD-MNL-SUM-TSL-REC             
                                                                                
                PERFORM 8000-WRITE-EXTRACT-RECORD                               
                   ADD +1                 TO PV-TAB-LOC-READ                    
               WHEN SQLCODE = +100                                              
                   MOVE 'Y'               TO PS-TAB-ULD-DONE                    
               WHEN OTHER                                                       
                   MOVE '7100-FETCH-MLT-MNLY-SUM'                               
                                          TO AA-PARAGRAPH-NAME                  
                   MOVE 'MLT_MNLY_SUM TABLE ERROR'                              
                                          TO AA-MESSAGE                         
                   MOVE 'FETCH'           TO AA-DB2-OPERATION                   
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       7200-CLOSE-CURSOR-MLT-MNLY-SUM.                                          
      ***THE CURSOR IS CLOSED AND THE PROCESS IS COMPLETED**************        
      ******************************************************************        
                                                                                
                                                                                
           EXEC SQL                                                             
               CLOSE TABLE_CURSOR                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   MOVE '7200-CLOSE-CURSOR-MLT-MNLY-SUM'                        
                                       TO AA-PARAGRAPH-NAME                     
                   MOVE 'MLT-MNLY-SUM TABLE ERROR'                              
                                       TO AA-MESSAGE                            
                   MOVE 'CLOSE CURSOR' TO AA-DB2-OPERATION                      
                   PERFORM 9900-SQL-ERROR                                       
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       8000-WRITE-EXTRACT-RECORD.                                               
      ***THE FETCHED REOCRDS ARE WRITTEN INTO THE OUTPUT FILE **********        
      ***AND THE COUNTER IS UPDATED (NO OF RECS READ/WRITTEN)**********         
      ******************************************************************        
      *                                                                         
           WRITE UNLOAD-MNL-SUM-TSL-REC                                         
                                                                                
           ADD 1 TO PV-TOTAL-RECS-WRITTEN.                                      
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
       9000-END-OF-JOB.                                                         
      ****RECORDS READ/WRITTEN ARE DISPLAYED HERE AND FILES ARE CLOSED          
      ******************************************************************        
            DISPLAY '*************************'.                                
            DISPLAY '***TOTAL RECORDS READ ***' PV-TAB-LOC-READ.                
            DISPLAY '*                       *'.                                
            DISPLAY '**TOTAL RECORDS WRITTEN**' PV-TOTAL-RECS-WRITTEN.          
            DISPLAY '*************************'.                                
            CLOSE DATE-IN.                                                      
            CLOSE UNLOAD-MNL-SUM-TSL.                                           
                                                                                
            IF PV-TAB-LOC-READ = 0                                              
                    MOVE PV-RETURN-CODE-4090 TO RETURN-CODE             04100099
                    DISPLAY '                                     '             
                    DISPLAY 'NO RECORDS FETCHED FOR THE GIVEN DATE'             
                    DISPLAY '                                     '             
            END-IF.                                                             
                                                                                
      ******************************************************************        
       9900-COMMON-ABEND-ROUTINE.                                               
      ******************************************************************        
                                                                                
           DISPLAY AA-ABEND-LIT.                                                
           DISPLAY AA-PROGRAM-LIT.                                              
           DISPLAY AA-PARAGRAPH-LIT.                                            
           DISPLAY AA-MESSAGE-LINE.                                             
           DISPLAY ABEND-CODE.                                                  
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
           EJECT                                                                
                                                                                
      ******************************************************************        
       9900-SQL-ERROR.                                                          
      ******************************************************************        
           DISPLAY '*****************************'.                             
           DISPLAY  AA-ABEND-LIT.                                               
           DISPLAY  AA-DB2-ERROR-LIT.                                           
           DISPLAY  AA-PROGRAM-LIT.                                             
           DISPLAY  AA-PARAGRAPH-LIT.                                           
           DISPLAY  AA-MESSAGE-LINE.                                            
           DISPLAY  AA-DB2-OPERATION-LIT.                                       
           SET AC-DB2-ERROR TO TRUE.                                            
           DISPLAY '*****************************'.                             
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
