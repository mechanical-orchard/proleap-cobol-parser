       ID DIVISION.                                                             
W34517 PROGRAM-ID.      VAKUT192.                                               
       AUTHOR.          RONNA BUTZ.                                             
       DATE-WRITTEN     6/19/00                                                 
       DATE-COMPILED.                                                           
W34517****IMPORTANT*****************************************************        
W34517* THIS IS A CLONE OF THE INKUT192 CREATED FOR THE VA PROJECT     *        
W34517* THE MAJOR CHANGES ARE .....                                    *        
W34517******************************************************************        
      ******************************************************************        
W34517*    VAKUT192                                                    *        
      *  - CALCULATE THE SHORTAGE AND CREATE A SHORTAGE EXTRACT FILE   *        
      *    FOR STORES THAT ARE DOING AN ESTIMATED INVENTORY.           *        
      *  - NO BOOK RETAIL OR PHYSICAL WILL BE CALCULATED FOR THESE     *        
      *    STORES BECAUSE THE SHORTAGE CALCULATION FOR STORES DOING    *        
      *    AN ESTIMATED INVENTORY DOES NOT USE THEM.                   *        
      ******************************************************************        
      *    CHANGE ID: ESTPCT    S. JACKSON      DATE : 02/01/2001      *        
      *    FINANCE NEEDS DEPT ADJUSTMENT PERCENTAGE TO BE CALCULATED   *        
      *    OUT TO TWO DECIMAL POSITIONS. THESE % ARE BASED ON PHYSICAL *        
      *    STORE'S ACTUAL CALCULATED SHORTAGE AND APPLIED TO EST STORES*        
      *    COPYBOOK INRD134 WAS EXPANDED TO ACCOMODATE AN ADDITIONAL   *        
      *    POSITION OF PRECISION TO IN134-SHORTAGE-PCT.                *        
      ******************************************************************        
      *                                                                *        
W26600* WR/PROJ DATE       PROGAMMER    DESCRIPTION OF CHANGES                  
W26600* ------- ---------- ------------ --------------------------              
W26600* W26600  04/01/2004 M.JOHNSON    STORE EXPANSION                         
W20445* W20445  09-01-2004 ROD JANSSEN  DETAILED INVNENTORY ADJUSTMENT          
W26006* W26006  02-24-2005 ERIN SEARL   REMOVE REFERENCES TO OBSOLETE           
W26006*                                 STORE TABLE.                            
W31767* W31767  02-02-2007 JILL LIN     PULL IN OVERAGE FOR ESTIMATE            
W31767*                                 STORE PROCESS.                          
W28582* W28582  12-25-2005 CLARK SHWENN VENDOR BRAND CHANGES                    
W34517* W34517  10-14-2008 IN-VA INVENTORY STREAM SPLIT                         
W33304* W33304  12-03-2008 SATHISH/     HOLDING INVENTORY LEDGER OPEN.          
W33304*                    INFOSYS                                              
W26006******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  IN-SHORTAGE-PCT-FILE  ASSIGN TO INP01.                       
                                                                                
           SELECT  IN-SLS-BASE-CNTL-FILE ASSIGN TO INP02.                       
                                                                                
           SELECT  IN-SLS-BASE-CUM-FILE  ASSIGN TO INP03.                       
                                                                                
           SELECT  OUT-SHORTAGE-EXT-FILE ASSIGN TO OUT01.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  IN-SHORTAGE-PCT-FILE                                                 
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS  0 RECORDS.                                           
                                                                                
       01  IN-SHRTAGE-PCT-RECORD          PIC X(20).                            
                                                                                
       FD  IN-SLS-BASE-CNTL-FILE                                                
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS  0 RECORDS.                                           
                                                                                
       01  IN-SALES-BASE-CNTL-REC         PIC X(50).                            
                                                                                
       FD  IN-SLS-BASE-CUM-FILE                                                 
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS  0 RECORDS.                                           
                                                                                
W20445 01  SALES-BASE-CUM-REC          PIC X(80).                               
                                                                                
       FD  OUT-SHORTAGE-EXT-FILE                                                
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS  0 RECORDS.                                           
                                                                                
W33304 01  OUT-SHORTAGE-EXT-RECORD   PIC X(170).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-SHORTAGE-PCT-RECS            PIC S9(09) COMP-3                
                                                          VALUE +0.             
           05  PA-SLS-BASE-CNTL-RECS           PIC S9(09) COMP-3                
                                                          VALUE +0.             
           05  PA-SLS-BASE-CUM-RECS            PIC S9(09) COMP-3                
                                                          VALUE +0.             
           05  PA-STORE-SHORT-EXT-RECS         PIC S9(09) COMP-3                
                                                          VALUE +0.             
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-MAX-RETRIES                  PIC S9(04) COMP SYNC             
                                                          VALUE +2.             
           05  PC-PROGRAM-ID                   PIC  X(08) VALUE                 
W34517                                                    'VAKUT192'.           
           05  PC-OPERCO-NBR                   PIC  X(02) VALUE '03'.           
           05  PC-APLSYS-CDE                   PIC  X(02) VALUE 'ML'.           
           05  PC-MDSELV-CDE                   PIC  X(03) VALUE 'DPT'.          
           05  PC-RSPLVL-CDE-BYR               PIC  X(03) VALUE 'BYR'.          
           05  PC-ONE-HUNDRED                  PIC S9(04) COMP SYNC             
                                                          VALUE +100.           
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES.         
           05  PV-DB2-OPER-ATTEMPTED           PIC  X(30) VALUE SPACES.         
           05  PV-SQL-SUB                      PIC S9(05) COMP-3                
                                                          VALUE +0.             
           05  PV-HOLD-DEPT                    PIC  X(03) VALUE SPACES.         
           05  PV-HOLD-SUB-CL                  PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-MAJ-CL.                                                  
               10 PV-HOLD-MAJ-CL-1             PIC  X(01) VALUE SPACES.         
               10 PV-HOLD-MAJ-CL-2             PIC  X(01) VALUE SPACES.         
           05  PV-HOLD-MDSEAR-GMA-NBR          PIC  X(02) VALUE SPACES.         
           05  PV-HOLD-BYR-NBR                 PIC  X(03) VALUE SPACES.         
           05  PV-HOLD-BYR-NAME                PIC  X(20) VALUE SPACES.         
           05  PV-HOLD-DEPT-DESC               PIC  X(20) VALUE SPACES.         
W26600     05  PV-AMT-RNDED                    PIC S9(13)V99 COMP-3             
                                                          VALUE +0.             
           05  PV-MAJ-CL-NBR.                                                   
               10 PV-MAJ-CL-1                  PIC  X(01) VALUE SPACES.         
               10 PV-MAJ-CL-2                  PIC  X(01) VALUE SPACES.         
                                                                                
       01  SUBSCRIPTS.                                                          
           05  SS-STORE                        PIC S9(04) COMP-3                
                                                          VALUE +0.             
           05  SS-DEPT                         PIC S9(04) COMP-3                
                                                          VALUE +0.             
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-SHORTAGE-PCT-SW          PIC  X(01) VALUE 'N'.            
               88  PS-NOT-EOF-SHORTAGE-PCT                VALUE 'N'.            
               88  PS-EOF-SHORTAGE-PCT                    VALUE 'Y'.            
           05  PS-EOF-SALES-BASE-CNTL-SW       PIC  X(01) VALUE 'N'.            
               88  PS-NOT-EOF-SALES-BASE-CNTL             VALUE 'N'.            
               88  PS-EOF-SALES-BASE-CNTL                 VALUE 'Y'.            
           05  PS-EOF-SALES-BASE-CUM-SW        PIC  X(01) VALUE 'N'.            
               88  PS-NOT-EOF-SALES-BASE-CUM              VALUE 'N'.            
               88  PS-EOF-SALES-BASE-CUM                  VALUE 'Y'.            
           05  PS-XMTLOC-FOUND-SW              PIC  X(01) VALUE 'N'.            
               88  PS-XMTLOC-NOT-FOUND                    VALUE 'N'.            
               88  PS-XMTLOC-FOUND                        VALUE 'Y'.            
           05  PS-GMA-BYR-FOUND-SW             PIC  X(01) VALUE 'N'.            
               88  PS-GMA-BYR-NOT-FOUND                   VALUE 'N'.            
               88  PS-GMA-BYR-FOUND                       VALUE 'Y'.            
           05  PS-BYR-NAME-FOUND-SW            PIC  X(01) VALUE 'N'.            
               88  PS-BYR-NAME-NOT-FOUND                  VALUE 'N'.            
               88  PS-BYR-NAME-FOUND                      VALUE 'Y'.            
           05  PS-DEPT-DESC-FOUND-SW           PIC  X(01) VALUE 'N'.            
               88  PS-DEPT-DESC-NOT-FOUND                 VALUE 'N'.            
               88  PS-DEPT-DESC-FOUND                     VALUE 'Y'.            
                                                                                
       01  ST-TABLE-AREA.                                                       
W26600     05  ST-STORE-TAB OCCURS 9999 TIMES.                                  
               10  ST-STORE-TABLE.                                              
W26600*            15  ST-STORE-NBR            PIC  X(03).                      
W26600             15  ST-STORE-NBR            PIC  X(04).                      
                   15  ST-STORE-NAME           PIC  X(10).                      
                   15  ST-UNBKD-INV-IND        PIC  X(01).                      
W33304             15  ST-SCHED-INV-TYPE-CDE   PIC  X(02).                      
W33304             15  ST-INV-ID               PIC  S9(09) COMP.                
W33304             15  ST-SLS-BSE-MN-END-DTE   PIC  X(10).                      
W33304             15  ST-FINCL-INV-STAT-CDE   PIC  X(02).                      
                                                                                
       01  DT-TABLE-AREA.                                                       
           05  DT-DEPT-TAB OCCURS 1000 TIMES.                                   
               10  DT-DEPT-TABLE.                                               
                   15  DT-DEPT-NBR             PIC  X(03).                      
      *            15  DT-SHORTAGE-PCT         PIC S9(03)V9.                    
ESTPCT             15  DT-SHORTAGE-PCT         PIC S9(03)V99.                   
                                                                                
       01  ABEND-CODE                          PIC S9(04) VALUE +0              
                                                          COMP SYNC.            
           88  ABEND-4013-DB2-ERROR                       VALUE +4013.          
                                                                                
      ***************************************************************           
      *    WORKING STORAGE AREA FOR THE NAME REFORMATTER                        
      *    ROUTINE DPPD024.                                                     
      ***************************************************************           
                                                                                
           COPY DPWS024.                                                        
                                                                                
      *************************************************************             
      *    DEPARTMENT SHORTAGE PERCENTS FILE                                    
      *************************************************************             
           COPY INRD134.                                                        
                                                                                
      *************************************************************             
      *    SALES BASE CONTROL FILE                                              
      *************************************************************             
           COPY INRD022.                                                        
                                                                                
      *************************************************************             
      *    SALES BASE CUM FILE                                                  
      *************************************************************             
W02445     COPY INRD023.                                                        
                                                                                
      *************************************************************             
      *    COPYBOOK FOR INVENTORY EXTRACT FILE                                  
      *************************************************************             
W20445     COPY INRD014.                                                        
                                                                                
      *************************************************************             
      *    DCLGEN FOR STORE TABLE                                 *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE XMT00001                                                 
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE                            *        
      *  MERCHANDISE AREA TABLE (TMDSEAR)                              *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TMDSEAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE                            *        
      *  RESPONSIBILITY AREA TABLE (TRSPBAR)                           *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TRSPBAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE                            *        
      *  APPLICATION AVAILABILITY TABLE (TAPLAVL)                      *        
      ******************************************************************        
           EXEC SQL                                                             
               INCLUDE TAPLAVL                                                  
           END-EXEC.                                                            
                                                                                
      *************************************************************             
      *    DB2 ERROR MESSAGE AREA                                 *             
      *************************************************************             
           COPY DPWS004.                                                        
                                                                                
      *************************************************************             
      *    DB2 COMMUNICATION AREA                                 *             
      *************************************************************             
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *************************************************************             
      * MAIN PROCESSING PARAGRAPH                                 *             
      *************************************************************             
       0000-MAINLINE.                                                           
                                                                                
            PERFORM 1000-INITIALIZATION.                                        
                                                                                
            PERFORM 7000-READ-SALES-BASE-CUM.                                   
            IF PS-EOF-SALES-BASE-CUM                                            
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 0000-MAINLINE            '                
                DISPLAY '** SALES BASE CUM FILE IS EMPTY.           '           
                MOVE +4008 TO ABEND-CODE                                        
                PERFORM 9999-ABEND                                              
            END-IF.                                                             
                                                                                
            PERFORM 2000-PROCESS-SALES-BASE-CUM                                 
               UNTIL PS-EOF-SALES-BASE-CUM.                                     
                                                                                
            PERFORM 3000-END-PROCESSING.                                        
                                                                                
            STOP RUN.                                                           
                                                                                
      ***************************************************************           
      * OPEN FILES AND LOAD WS-STORE TABLE WITH STORE NAMES                     
      ***************************************************************           
       1000-INITIALIZATION.                                                     
                                                                                
            OPEN INPUT IN-SHORTAGE-PCT-FILE                                     
                       IN-SLS-BASE-CNTL-FILE                                    
                       IN-SLS-BASE-CUM-FILE                                     
                OUTPUT OUT-SHORTAGE-EXT-FILE.                                   
                                                                                
            MOVE +20  TO DP024-FLNAME-LENGTH.                                   
                                                                                
            INITIALIZE DT-TABLE-AREA.                                           
            PERFORM 7100-READ-SHORTAGE-PCT-FILE.                                
            IF PS-EOF-SHORTAGE-PCT                                              
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 1000-INITIALIZATION      '                
                DISPLAY '** SHORTAGE PERCENT FILE IS EMPTY.         '           
                MOVE +4008 TO ABEND-CODE                                        
                PERFORM 9999-ABEND                                              
            END-IF.                                                             
                                                                                
            PERFORM UNTIL PS-EOF-SHORTAGE-PCT                                   
                PERFORM 1100-LOAD-DEPT-TABLE                                    
                PERFORM 7100-READ-SHORTAGE-PCT-FILE                             
            END-PERFORM.                                                        
                                                                                
            INITIALIZE ST-TABLE-AREA.                                           
            PERFORM 7200-READ-SLS-BASE-CNTL-FILE.                               
            IF PS-EOF-SALES-BASE-CNTL                                           
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 1000-INITIALIZATION      '                
                DISPLAY '** SALES BASE CONTROL FILE IS EMPTY.       '           
                MOVE +4008 TO ABEND-CODE                                        
                PERFORM 9999-ABEND                                              
            END-IF.                                                             
                                                                                
            PERFORM UNTIL PS-EOF-SALES-BASE-CNTL                                
                PERFORM 1200-LOAD-STORE-TABLE                                   
                PERFORM 7200-READ-SLS-BASE-CNTL-FILE                            
            END-PERFORM.                                                        
                                                                                
                                                                                
      ***************************************************************           
      * LOAD THE DEPARTMENT TABLE                                               
      ***************************************************************           
       1100-LOAD-DEPT-TABLE.                                                    
                                                                                
            MOVE IN134-DEPT-NBR             TO SS-DEPT.                         
            IF SS-DEPT >= 0 AND                                                 
               SS-DEPT < 1000                                                   
                CONTINUE                                                        
            ELSE                                                                
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 1100-LOAD-DEPT-TABLE     '                
                DISPLAY '** DEPARTMENT NUMBER IS OUT OF TABLE RANGE.'           
                DISPLAY '** IN134-DEPT-NBR:  ' IN134-DEPT-NBR                   
                MOVE +4004 TO ABEND-CODE                                        
                PERFORM 9999-ABEND.                                             
                                                                                
                                                                                
            MOVE IN134-DEPT-NBR         TO DT-DEPT-NBR(SS-DEPT).                
            MOVE IN134-SHORTAGE-PCT     TO DT-SHORTAGE-PCT(SS-DEPT).            
                                                                                
                                                                                
      ***************************************************************           
      * LOAD THE STORE TABLE                                                    
      ***************************************************************           
       1200-LOAD-STORE-TABLE.                                                   
                                                                                
            IF IN022-STR-NBR >= 0 AND                                           
               IN022-STR-NBR <  10000                                           
                MOVE IN022-STR-NBR TO XMT00001-LOC-NBR                          
                                               SS-STORE                         
            ELSE                                                                
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 1200-LOAD-STORE-TABLE    '                
                DISPLAY '** STORE NUMBER IS OUT OF TABLE RANGE.'                
                DISPLAY '** IN022-STR-NBR:  ' IN022-STR-NBR                     
                MOVE +4008 TO ABEND-CODE                                        
                PERFORM 9999-ABEND.                                             
                                                                                
            MOVE 'N'               TO PS-XMTLOC-FOUND-SW.                       
W26600      PERFORM 8000-SELECT-XMT-LOC.                                        
                                                                                
            IF PS-XMTLOC-FOUND                                                  
                MOVE IN022-STR-NBR TO ST-STORE-NBR(SS-STORE)                    
W26600          MOVE XMT00001-LOC-10-ABBR-NM                                    
                                   TO ST-STORE-NAME(SS-STORE)                   
                MOVE IN022-UNBKD-INV-IND                                        
                                   TO ST-UNBKD-INV-IND(SS-STORE)                
                MOVE IN022-SCHEDULED-INV-TYPE-CDE                               
                                   TO ST-SCHED-INV-TYPE-CDE(SS-STORE)           
W33304          MOVE IN022-INV-ID  TO ST-INV-ID(SS-STORE)                       
W33304          MOVE IN022-SLS-BSE-MN-END-DTE                                   
W33304                             TO ST-SLS-BSE-MN-END-DTE(SS-STORE)           
W33304          MOVE IN022-FINCL-INV-STAT-CDE                                   
W33304                             TO ST-FINCL-INV-STAT-CDE(SS-STORE)           
            ELSE                                                                
W26600          MOVE ZEROS         TO ST-STORE-NBR(SS-STORE)                    
W26600          MOVE SPACE         TO ST-STORE-NAME(SS-STORE)                   
            END-IF.                                                             
                                                                                
                                                                                
      ***************************************************************           
      *     WRITE AN UPDATED STORE SHORTAGE EXTRACT RECORD.         *           
      ***************************************************************           
       2000-PROCESS-SALES-BASE-CUM.                                             
                                                                                
            MOVE IN023-STR-NBR            TO SS-STORE.                          
            IF (ST-UNBKD-INV-IND(SS-STORE)      = 'Y' AND                       
W33304          ST-SCHED-INV-TYPE-CDE(SS-STORE) = 'EI')                         
                                                                                
                IF    IN023-DEPT-NBR = PV-HOLD-DEPT                             
                  AND IN023-SUB-CL-NBR = PV-HOLD-SUB-CL                         
                    CONTINUE                                                    
                ELSE                                                            
                    MOVE IN023-DEPT-NBR       TO PV-HOLD-DEPT                   
                    MOVE IN023-SUB-CL-NBR     TO PV-HOLD-SUB-CL                 
                                                                                
                    MOVE 'N'                  TO PS-GMA-BYR-FOUND-SW            
                    PERFORM 8100-SELECT-GMA-BYR                                 
                    IF PS-GMA-BYR-FOUND                                         
                        MOVE MDSEAR-GMA-NBR   TO PV-HOLD-MDSEAR-GMA-NBR         
                        MOVE MDSEAR-BYR-NBR   TO PV-HOLD-BYR-NBR                
                    ELSE                                                        
                        MOVE SPACES           TO PV-HOLD-MDSEAR-GMA-NBR         
                                                 PV-HOLD-BYR-NBR                
                    END-IF                                                      
                                                                                
                    MOVE 'N'              TO PS-BYR-NAME-FOUND-SW               
                    PERFORM 8200-SELECT-BUYER-NAME                              
                    IF PS-BYR-NAME-FOUND                                        
                        MOVE RSPBAR-WORKER-FIR-NAME   TO DP024-FNAME            
                        MOVE RSPBAR-WORKER-LAST-NAME  TO DP024-LNAME            
                    ELSE                                                        
                        MOVE 'NOT FOUND'              TO DP024-FNAME            
                        MOVE SPACES                   TO DP024-LNAME            
                    END-IF                                                      
                                                                                
                    PERFORM DP024-0000-FORMAT-NAME                              
                    MOVE DP024-FLNAME     TO PV-HOLD-BYR-NAME                   
                                                                                
                    MOVE 'N'              TO PS-DEPT-DESC-FOUND-SW              
                    MOVE IN023-DEPT-NBR   TO MDSEAR-DEPT-NBR                    
                    PERFORM 8300-SELECT-DEPT-DESC                               
                                                                                
                    IF PS-DEPT-DESC-FOUND                                       
                        MOVE MDSEAR-DESC      TO PV-HOLD-DEPT-DESC              
                    ELSE                                                        
                        MOVE 'NOT FOUND'      TO PV-HOLD-DEPT-DESC              
                    END-IF                                                      
                END-IF                                                          
                                                                                
                PERFORM 2100-FORMAT-EXT-RECORDS                                 
                                                                                
                MOVE IN023-STR-NBR        TO SS-STORE                           
                                                                                
            END-IF.                                                             
                                                                                
            PERFORM 7000-READ-SALES-BASE-CUM.                                   
                                                                                
                                                                                
      ***************************************************************           
      *     FORMAT AND WRITE AN UPDATED SHORTAGE EXTRACT AND A     *            
      *     STORE SHORTAGE EXTRACT RECORD.                          *           
      ***************************************************************           
       2100-FORMAT-EXT-RECORDS.                                                 
            INITIALIZE IN014-SHORTAGE-EXTR-REC.                                 
                                                                                
            MOVE PV-HOLD-MDSEAR-GMA-NBR   TO IN014-GMA-NBR.                     
            MOVE PV-HOLD-BYR-NBR          TO IN014-BYR-NBR.                     
            MOVE PV-HOLD-BYR-NAME         TO IN014-BYR-NAME                     
            MOVE IN023-DEPT-NBR           TO IN014-DEPT-NBR.                    
            MOVE PV-HOLD-DEPT-DESC        TO IN014-DEPT-MDSEAR-DESC.            
            MOVE IN023-SUB-CL-NBR         TO PV-MAJ-CL-NBR.                     
            MOVE ZERO                     TO PV-MAJ-CL-2.                       
            MOVE PV-MAJ-CL-NBR            TO IN014-MAJ-CL-NBR.                  
            MOVE IN023-SUB-CL-NBR         TO IN014-SUB-CL-NBR.                  
            MOVE IN023-STR-NBR            TO IN014-STR-NBR                      
                                             SS-STORE.                          
W28582      MOVE IN023-ML-LWST-ID         TO IN014-ML-LWST-ID.                  
                                                                                
            MOVE ST-STORE-NAME(SS-STORE)                                        
                                          TO IN014-STR-NAME.                    
            MOVE IN023-CUM-SLS-RTL-AMT    TO IN014-SLS-BSE-RTL-AMT.             
            MOVE IN023-CUM-SHNK-RTL-AMT   TO IN014-SHNK-RTL-AMT.                
W33304      MOVE ST-INV-ID(SS-STORE)      TO IN014-INV-ID.                      
W33304      MOVE ST-SLS-BSE-MN-END-DTE(SS-STORE)                                
W33304                                    TO IN014-OPN-FSCL-MN-DTE.             
W33304      MOVE ST-FINCL-INV-STAT-CDE(SS-STORE)                                
W33304                                    TO IN014-FINCL-INV-STAT-CDE.          
            MOVE IN023-DEPT-NBR           TO SS-DEPT.                           
            IF SS-DEPT >= 0 AND                                                 
               SS-DEPT < 1000                                                   
                MOVE IN023-DEPT-NBR       TO SS-DEPT                            
W31767*         IF DT-SHORTAGE-PCT(SS-DEPT) >= ZERO                             
W31767*             MOVE ZERO             TO IN014-SHORTAGE-RTL-AMT             
W31767*         ELSE                                                            
W31767          COMPUTE PV-AMT-RNDED ROUNDED                                    
W31767                = (IN014-SLS-BSE-RTL-AMT                                  
W31767                *  DT-SHORTAGE-PCT(SS-DEPT))                              
W31767                / PC-ONE-HUNDRED                                          
W31767          MOVE PV-AMT-RNDED     TO IN014-SHORTAGE-RTL-AMT                 
W31767*         END-IF                                                          
            ELSE                                                                
                DISPLAY '** ABEND **'                                           
W34517          DISPLAY '** PROGRAM VAKUT192 **'                                
                DISPLAY '** PARAGRAPH 2100-FORMAT-EXT-RECORDS  '                
                DISPLAY '** DEPARTMENT NUMBER IS OUT OF TABLE RANGE.'           
                DISPLAY '** IN023-DEPT-NBR:  ' IN023-DEPT-NBR                   
                MOVE +4008 TO ABEND-CODE                                        
                PERFORM 9999-ABEND                                              
            END-IF.                                                             
                                                                                
            PERFORM 7300-WRITE-SHORTAGE-RECORD.                                 
                                                                                
                                                                                
      ***************************************************************           
      *                                                             *           
      ***************************************************************           
       3000-END-PROCESSING.                                                     
                                                                                
            DISPLAY '*** TOTAL NBR OF STORE SHORT RECS WRITTEN    = '           
                PA-STORE-SHORT-EXT-RECS.                                        
            DISPLAY ' '.                                                        
                                                                                
            CLOSE IN-SHORTAGE-PCT-FILE                                          
                  IN-SLS-BASE-CNTL-FILE                                         
                  IN-SLS-BASE-CUM-FILE                                          
                  OUT-SHORTAGE-EXT-FILE.                                        
                                                                                
W26600*                                                                         
      *************************************************************             
      *    SELECT XMT_LOC DATA FOR ALL STORES                                   
      *************************************************************             
       8000-SELECT-XMT-LOC.                                                     
                                                                                
            EXEC SQL                                                            
              SELECT  LOC_10_ABBR_NM                                            
                INTO :XMT00001-LOC-10-ABBR-NM                                   
                FROM   XMT_LOC                                                  
               WHERE LOC_NBR = :XMT00001-LOC-NBR                                
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
                    SET PS-XMTLOC-FOUND TO TRUE                                 
                WHEN SQLCODE        = +100                                      
                    CONTINUE                                                    
                WHEN OTHER                                                      
                    DISPLAY 'STORE' IN022-STR-NBR                               
                    MOVE '8000-SELECT-XMT-LOC '                                 
                                    TO PV-PARAGRAPH-NAME                        
                    MOVE 'SELECT ROW FROM XMT_LOC  '                            
                                    TO PV-DB2-OPER-ATTEMPTED                    
                    PERFORM         9998-SQL-ABEND                              
            END-EVALUATE.                                                       
      *                                                                         
      ******************************************************************        
      *    SELECT GMA AND BUYER.                                                
      ******************************************************************        
       8100-SELECT-GMA-BYR.                                                     
                                                                                
            EXEC SQL                                                            
                SELECT                                                          
                    GMA_NBR                                                     
                   ,BYR_NBR                                                     
                INTO                                                            
                    :MDSEAR-GMA-NBR                                             
                   ,:MDSEAR-BYR-NBR                                             
                FROM                                                            
                    TMDSEAR                                                     
      *         WHERE DEPT_NBR  = :IN014-DEPT-NBR                               
                WHERE DEPT_NBR  = :PV-HOLD-DEPT                                 
                  AND MDSELV_CDE  = :PC-MDSELV-CDE                              
                  AND CURRENT DATE BETWEEN STRT_DTE AND END_DTE                 
            END-EXEC.                                                           
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
                   MOVE 'Y'        TO PS-GMA-BYR-FOUND-SW                       
                WHEN SQLCODE        = +100                                      
                   CONTINUE                                                     
                WHEN OTHER                                                      
                    DISPLAY 'DEPT ' IN014-DEPT-NBR                              
                    MOVE '8100-SELECT GMA-BYR '                                 
                                    TO PV-PARAGRAPH-NAME                        
                    MOVE 'SELECT ROW FROM TMDSEAR  '                            
                                    TO PV-DB2-OPER-ATTEMPTED                    
                    PERFORM         9998-SQL-ABEND                              
            END-EVALUATE.                                                       
                                                                                
                                                                                
      ******************************************************************        
      *    SELECT BUYER FROM THE MBS.                                           
      ******************************************************************        
       8200-SELECT-BUYER-NAME.                                                  
                                                                                
            EXEC SQL                                                            
                SELECT      DISTINCT                                            
                            A.BYR_NBR                                           
                     ,      B.WORKER_LAST_NAME                                  
                     ,      B.WORKER_FIR_NAME                                   
                  INTO     :MDSEAR-BYR-NBR                                      
                     ,     :RSPBAR-WORKER-LAST-NAME                             
                     ,     :RSPBAR-WORKER-FIR-NAME                              
                  FROM      TMDSEAR A                                           
                     ,      TRSPBAR B                                           
                     ,      TAPLAVL C                                           
                 WHERE     (A.BYR_NBR     = :PV-HOLD-BYR-NBR)                   
                   AND     (A.DEPT_NBR    = :PV-HOLD-DEPT)                      
                   AND     (B.RSPLVL_CDE  = :PC-RSPLVL-CDE-BYR)                 
                   AND     (A.OPERCO_NBR  = :PC-OPERCO-NBR)                     
                   AND     (C.APLSYS_CDE  = :PC-APLSYS-CDE)                     
                   AND     (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                     
                   AND     (A.MDSEAR_DKEY =  C.MDSEAR_DKEY)                     
                   AND     (CURRENT DATE                                        
                            BETWEEN CHAR(B.WORKER_STRT_DTE,ISO)                 
                            AND     CHAR(B.WORKER_END_DTE,ISO))                 
                   AND     (CURRENT DATE                                        
                            BETWEEN CHAR(C.MDSEAR_STRT_DTE,ISO)                 
                            AND     CHAR(C.MDSEAR_END_DTE,ISO))                 
            END-EXEC                                                            
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
                    MOVE 'Y'        TO PS-BYR-NAME-FOUND-SW                     
                WHEN SQLCODE        = +100                                      
                    CONTINUE                                                    
                WHEN OTHER                                                      
                    MOVE           '8200-SELECT-BUYER-NAME'                     
                                    TO PV-PARAGRAPH-NAME                        
                    MOVE 'SELECT ROW FROM TRSPBAR  '                            
                                    TO PV-DB2-OPER-ATTEMPTED                    
                    PERFORM         9998-SQL-ABEND                              
            END-EVALUATE.                                                       
                                                                                
                                                                                
       EJECT                                                                    
                                                                                
      ******************************************************************        
      *  SELECT DEPARTMENT DESCRIPTION FROM TMDSEAR                    *        
      ******************************************************************        
       8300-SELECT-DEPT-DESC.                                                   
                                                                                
            EXEC SQL                                                            
                SELECT     A.DEPT_NBR                                           
                     ,     A.MDSEAR_DESC                                        
                  INTO    :MDSEAR-DEPT-NBR                                      
                     ,    :MDSEAR-DESC                                          
                  FROM     TMDSEAR A                                            
                     ,     TAPLAVL B                                            
                 WHERE    (A.DEPT_NBR    = :MDSEAR-DEPT-NBR)                    
                   AND    (A.MDSELV_CDE  = :PC-MDSELV-CDE)                      
                   AND    (A.OPERCO_NBR  = :PC-OPERCO-NBR)                      
                   AND    (B.APLSYS_CDE  = :PC-APLSYS-CDE)                      
                   AND    (A.MDSEAR_DKEY =  B.MDSEAR_DKEY)                      
                   AND    (CURRENT DATE     BETWEEN                             
                           CHAR(B.MDSEAR_STRT_DTE,ISO)  AND                     
                           CHAR(B.MDSEAR_END_DTE,ISO))                          
                   AND    (CURRENT DATE     BETWEEN                             
                           CHAR(A.STRT_DTE,ISO)         AND                     
                           CHAR(A.END_DTE,ISO))                                 
            END-EXEC                                                            
                                                                                
            EVALUATE TRUE                                                       
                WHEN SQLCODE        = +000                                      
                    MOVE 'Y'             TO PS-DEPT-DESC-FOUND-SW               
                WHEN SQLCODE        = +100                                      
                    CONTINUE                                                    
                WHEN OTHER                                                      
                    MOVE           '8300-SELECT-DEPT'                           
                                    TO PV-PARAGRAPH-NAME                        
                    MOVE 'SELECT DEPARTMENT ROW ON TMDSEAR'                     
                                    TO PV-DB2-OPER-ATTEMPTED                    
                    DISPLAY 'DEPT-NBR = ' IN014-DEPT-NBR                        
                    PERFORM         9998-SQL-ABEND                              
            END-EVALUATE.                                                       
                                                                                
                                                                                
      ***************************************************************           
      *   READ A RECORD FOR PROCESSING                              *           
      ***************************************************************           
       7000-READ-SALES-BASE-CUM.                                                
                                                                                
            READ IN-SLS-BASE-CUM-FILE                                           
                INTO IN023-SALES-BASE-CUM-REC                                   
                AT END                                                          
                   SET PS-EOF-SALES-BASE-CUM TO TRUE.                           
                                                                                
            IF PS-NOT-EOF-SALES-BASE-CUM                                        
                ADD +1 TO PA-SLS-BASE-CUM-RECS.                                 
                                                                                
                                                                                
      ***************************************************************           
      *    READ SALES BASE CONTROL FILE                                         
      ***************************************************************           
       7100-READ-SHORTAGE-PCT-FILE.                                             
                                                                                
            READ IN-SHORTAGE-PCT-FILE  INTO IN134-DEPT-SHORTAGE-PCT-REC         
                AT END                                                          
                   SET PS-EOF-SHORTAGE-PCT    TO TRUE.                          
                                                                                
            IF PS-NOT-EOF-SHORTAGE-PCT                                          
                ADD +1 TO PA-SHORTAGE-PCT-RECS.                                 
                                                                                
                                                                                
      ***************************************************************           
      *    READ SALES BASE CONTROL FILE                                         
      ***************************************************************           
       7200-READ-SLS-BASE-CNTL-FILE.                                            
                                                                                
            READ IN-SLS-BASE-CNTL-FILE INTO IN022-SALES-BASE-CNTL-REC           
                AT END                                                          
                   SET PS-EOF-SALES-BASE-CNTL TO TRUE.                          
                                                                                
            IF PS-NOT-EOF-SALES-BASE-CNTL                                       
                ADD +1 TO PA-SLS-BASE-CNTL-RECS.                                
                                                                                
                                                                                
      ***************************************************************           
      *   READ A RECORD FOR PROCESSING                              *           
      ***************************************************************           
                                                                                
       7300-WRITE-SHORTAGE-RECORD.                                              
                                                                                
            WRITE OUT-SHORTAGE-EXT-RECORD FROM IN014-SHORTAGE-EXTR-REC.         
            ADD +1 TO PA-STORE-SHORT-EXT-RECS.                                  
                                                                                
                                                                                
      ***************************************************************           
      *    SQL ABEND PROCESSING                                     *           
      ***************************************************************           
       9998-SQL-ABEND.                                                          
                                                                                
            DISPLAY '** ABEND     **'.                                          
            DISPLAY '** PROGRAM   = ' PC-PROGRAM-ID.                            
            DISPLAY '** PARAGRAPH = ' PV-PARAGRAPH-NAME.                        
            DISPLAY '** OPERATION = ' PV-DB2-OPER-ATTEMPTED.                    
                                                                                
           COPY DPPD004.                                                        
                                                                                
            PERFORM 9999-ABEND.                                                 
                                                                                
                                                                                
       9999-ABEND.                                                              
            CALL  'ILBOABN0' USING ABEND-CODE.                                  
                                                                                
      ******************************************************************        
      *    NAME REFORMATTER ROUTINE.                                            
      ******************************************************************        
           COPY DPPD024.                                                        
                                                                                
                                                                                
