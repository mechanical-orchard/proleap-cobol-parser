       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    DPKCS008.                                                 
                                                                                
       AUTHOR.        CALVIN BUSH.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  JULY 2, 1990.                                             
       DATE-COMPILED.                                                           
      ******************************************************************        
      **                        NAVIGATOR                             **        
      **                                                              **        
      **                                                              **        
      **                                                              **        
      **    CHANGE   PROG-                                            **        
      **     DATE    RAMMER   DESCRIPTION OF CHANGE                   **        
      **   --------  -------  -------------------------------------   **        
      **   11/21/90  TKMA051  ADDED LOGIC TO HANDLE INITIAL SIGNON    **        
      **   11/11/91  TKMA006  ADDED LOGIC TO SUPPORT RF DEVICES       **        
      **   02/02/93  TKMA025  CORRECTED LOGIC IN PARAGRAPH 2200-      **        
      **                      ONLY CONVERT COMMANDS FOR GO-TO AND     **        
      **                      RETURN FKEY ACTIONS.                    **        
      **   02/19/96  TKMA235  ADD LOGIC FOR CICS24X7 PROCESSING.      **        
      **                      ARCHITECTURE CHANGES TO ALLOW CICS      **        
      **                      TO BE UP 24HRS/DAY, 7DAYS/WEEK:         **        
      **                      1) CHGD DPSAVCOM ESDS VSAM DATASET      **        
      **                         TO:  DPSAVCMK KSDS VSAM DATASET.     **        
      **                         SEE NEW COPYBOOK DPRD515).           **        
      **                      2) DELETE APPROPRIATE RECORD FROM       **        
      **                         DPSAVCMK KSDS VSAM DATASET, WHEN:    **        
      **                         A) ITEM IS ROLLED OFF OF STACK (DUE  **        
      **                            TO OVERFLOW SITUATION).           **        
      **                         B) ITEM IS "POPPED" OFF OF STACK     **        
      **                            (DUE TO A RETURN <PF3> SITUATION).**        
      **   04/29/96  TKMA025  REMOVED 'MOVE EIBTASKN' IN PARAGRAPH    **        
      **                      6000- BEFORE 'ADD-ENTRY' PARAGRAPH      **        
      **                      IS PERFORMED.                           **        
      **                                                              **        
      **   08/24/98  TKMA464  CONVERTED "START" TO "RETURN IMMEDIATE".**        
      **   10/08/98  TKMA464  CHECK IF OTHER REGIONS ARE AVAILABLE.   **        
-=-   **   10/21/99  TKMA464  TEMPORARILY DISPLAY DUPLICATE RECORDS   **        
-=-   **                      IN THE CICS LOG.  LINES MARKED WITH -=- **        
      *    10/20/99  TKMA464  IMPLEMENT ACCURATE LOCATION REQUIRED     *        
      *                       OPTION.                                  *        
      *    08/01/02  TKMA464  INDICATE THAT SESSION HAS TIMED OUT IF   *        
      *                       DPSAVCMK RECORDS ARE MISSING.            *        
      *    03/05/03  TKMA464  LOG ONLY ONE LINE WHEN DPSAVCMK RECORD   *        
      *                       IS MISSING ON DELETE INSTEAD OF LOGGING  *        
      *                       A STANDARD ABEND (22 LINES).             *        
      *    12/22/03  TKMA464  MADE CHANGES FOR EXPANDING LOCATION FROM *        
      *                       3 TO 5 DIGITS.                           *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FD-FILE-DDNAME-AREA.                                                 
           05  FD-MNEMONIC-FILE        PIC X(8)     VALUE 'DPMNEMIC'.           
           05  FD-SAVED-COMMAREA-FILE  PIC X(8)     VALUE 'DPSAVCMK'.           
           05  FD-MENU-ITEM-FILE       PIC X(8)     VALUE 'DPMNUITM'.           
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-GLOBAL-COMMAREA-LENGTH                                        
                                        PIC S9(04)  VALUE +1207                 
                                                    COMP.                       
           05  PC-MAX-COMMAREA-LENGTH   PIC S9(04)  VALUE +4072                 
                                                    COMP.                       
           05  PC-MAX-DPSAVCMK-LENGTH   PIC S9(04)  VALUE +3000                 
                                                    COMP.                       
           05  PC-STACK-KEY-LENGTH      PIC S9(04)  VALUE +15                   
                                                    COMP.                       
           05  PC-EIBDATE-EIBTIME-8-LENGTH                                      
                                        PIC S9(04)  VALUE +15                   
                                                    COMP.                       
           05  PC-FIRST-ITEM            PIC S9(04)  VALUE +0001                 
                                                    COMP.                       
           05  PC-MAX-APPL-QUEUE-SEQ    PIC S9(04)  VALUE +5                    
                                                    COMP.                       
           05  PC-MAIN-MENU-COMMAND     PIC  X(08)  VALUE 'MAINMENU'.           
           05  PC-CORP-MENU-MNEMONIC    PIC  X(08)  VALUE 'CORPMENU'.           
           05  PC-LOGOFF-MNEMONIC       PIC  X(08)  VALUE 'LOGOFF  '.           
           05  PC-OBJLOK-BASE-DD        PIC  X(08)  VALUE 'DPOBJLOK'.           
           05  PC-OBJLOK-AIX-TASK-DD    PIC  X(08)  VALUE 'DPOBJLK1'.           
           05  PC-MENU-TRANID           PIC  X(04)  VALUE 'Z007'.               
           05  PC-LOGOFF-TRANID         PIC  X(04)  VALUE 'Z011'.               
           05  PC-LOGON-TRANID          PIC  X(04)  VALUE 'Z028'.               
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE 'DPKCS008'.           
           05  PC-CICSARCH-FUNC         PIC X(08)   VALUE 'NEWARCH '.           
           05  PC-SYSTEM-MESSAGE-NUMBER.                                        
               10  PC-TSYMSG-00045      PIC  9(05)  VALUE 00045.                
               10  PC-TSYMSG-99901      PIC  9(05)  VALUE 99901.                
               10  PC-TSYMSG-99902      PIC  9(05)  VALUE 99902.                
               10  PC-TSYMSG-99903      PIC  9(05)  VALUE 99903.                
               10  PC-TSYMSG-99904      PIC  9(05)  VALUE 99904.                
               10  PC-TSYMSG-99911      PIC  9(05)  VALUE 99911.                
-=-        05  PC-LOG-QUEUE-RESOURCE    PIC  X(04)   VALUE 'CSSL'.              
-=-        05  PC-LOG-QUEUE             PIC  X(04)   VALUE 'CSSL'.              
           05  PC-TIMEOUT-COMMAREA         PIC  X(07)  VALUE 'TIMEOUT'.         
           05  PC-TIMEOUT-COMMAREA-LENGTH  PIC S9(04) COMP                      
                                                       VALUE +7.                
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-AVAILABILITY-ERROR-SW PIC  X(01)  VALUE 'N'.                  
               88  PS-AVAILABILITY-ERROR            VALUE 'Y'.                  
               88  PS-AVAILABILITY-GOOD             VALUE 'N'.                  
           05  PS-AVAILABILITY-REASON   PIC  X(01)  VALUE 'N'.                  
               88  PS-AVAIL-NA                      VALUE 'N'.                  
               88  PS-AVAIL-LOC                     VALUE 'L'.                  
           05  PS-BAD-COMMAREA-SW       PIC  X(01)  VALUE 'N'.                  
               88  PS-BAD-COMMAREA                  VALUE 'Y'.                  
               88  PS-GOOD-COMMAREA                 VALUE 'N'.                  
           05  PS-MNEMONIC-FOUND-SW     PIC  X(01)  VALUE 'Y'.                  
               88  PS-MNEMONIC-FOUND                VALUE 'Y'.                  
               88  PS-MNEMONIC-NOT-FOUND            VALUE 'N'.                  
           05  PS-MAIN-MENU-SW          PIC  X(01)  VALUE 'N'.                  
               88  PS-MAIN-MENU-CMMD-ENTERED        VALUE 'Y'.                  
               88  PS-MAIN-MENU-CMMD-NOT-ENTERED    VALUE 'N'.                  
           05  PS-RETURN-FROM-OUTSIDE-ARCH-SW                                   
                                        PIC  X(01)  VALUE 'N'.                  
               88  PS-RETURN-FROM-OUTSIDE-ARCH  VALUE 'Y'.                      
           05  PS-TRAN-AVAILABLE-SW     PIC  X(01)  VALUE 'Y'.                  
               88  PS-TRAN-AVAILABLE                VALUE 'Y'.                  
               88  PS-TRAN-NOT-AVAILABLE            VALUE 'N'.                  
           05  PS-SRC-STACK-IND         PIC  X(01)  VALUE 'N'.                  
               88  PS-SRC-STACK                     VALUE 'Y'.                  
               88  PS-SRC-DONT-STACK                VALUE 'N'.                  
           05  PS-CICS-START-IND        PIC  X(01)  VALUE 'Y'.                  
               88  PS-CICS-START-SUCCESS            VALUE 'Y'.                  
               88  PS-CICS-START-FAILED             VALUE 'N'.                  
      *                                                                         
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-INQUIRE-CODE          PIC S9(08)  VALUE +0                    
                                                        COMP.                   
           05  PV-CICS-RESPONSE         PIC S9(08)  VALUE ZERO                  
                                                        COMP SYNC.              
           05  PV-CICS-LENGTH           PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-CICS-KEYLENGTH        PIC S9(04)  VALUE +0                    
                                                    COMP SYNC.                  
           05  PV-TS-ITEM               PIC S9(04)  VALUE ZERO                  
                                                        COMP SYNC.              
           05  PV-HOLD-INDEX            PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
           05  PV-ENTRIES-TO-POP        PIC S9(04)  VALUE ZERO                  
                                                        COMP.                   
           05  PV-PRT-TASK-NBR          PIC  9(07)  VALUE ZERO.                 
           05  PV-DELETEQ-TRAN-ID       PIC  X(04)  VALUE SPACE.                
           05  PV-DEST-MNEMONIC         PIC  X(08)  VALUE SPACE.                
           05  PV-DEST-TRAN-ID          PIC  X(04)  VALUE SPACE.                
           05  PV-INT-APPL-FORMAT-IND   PIC  X(01)  VALUE SPACE.                
           05  PV-SYSID                 PIC  X(04)  VALUE SPACE.                
           05  PV-REMOTE-SYSTEM         PIC  X(04)  VALUE SPACE.                
           05  PV-REMOTE-STATUS         PIC S9(09)  COMP SYNC                   
                                                    VALUE ZERO.                 
               88  PV-REMOTE-ACQUIRED               VALUE +69.                  
               88  PV-REMOTE-RELEASED               VALUE +70.                  
               88  PV-REMOTE-FREEING                VALUE +94.                  
               88  PV-REMOTE-AVAILABLE              VALUE +95.                  
               88  PV-REMOTE-OBTAINING              VALUE +96.                  
           05  PV-ACT-DPSAVCMK-LENGTH   PIC S9(04)  VALUE ZERO                  
                                                    COMP.                       
           05  PV-UNPACK-NUMERIC        PIC 9(4)    VALUE ZERO.                 
           05  PV-DISPLAY-LINE-2.                                               
               10  FILLER.                                                      
                   15  FILLER           PIC  X(04)  VALUE 'KEY='.               
                   15  PV-DISPLAY-KEY   PIC  X(15)  VALUE SPACE.                
                   15  FILLER           PIC  X(01)  VALUE ','.                  
               10  FILLER.                                                      
                   15  PV-DISPLAY-FIELD2-HDR                                    
                                        PIC  X(07)  VALUE SPACE.                
                   15  PV-DISPLAY-FIELD2                                        
                                        PIC  X(04)  VALUE SPACE.                
                   15  FILLER           PIC  X(01)  VALUE ','.                  
               10  FILLER.                                                      
                   15  PV-DISPLAY-FIELD3-HDR                                    
                                        PIC  X(07)  VALUE SPACE.                
                   15  PV-DISPLAY-FIELD3                                        
                                        PIC  X(04)  VALUE SPACE.                
-=-        05  PV-LOG-SUB               PIC  9(02)  VALUE ZERO.                 
-=-        05  PV-LOG-LINE              PIC  X(117) VALUE SPACE.                
-=-        05  PV-LOG-LINES-TABLE.                                              
-=-            10  FILLER               PIC  X(17)   VALUE                      
-=-                                           ' DPKCS008 (OLD): '.              
-=-            10  PV-OLD-REC           PIC  X(100) VALUE SPACE.                
-=-            10  FILLER               PIC  X(17)   VALUE                      
-=-                                           ' DPKCS008 (NEW): '.              
-=-            10  PV-NEW-REC           PIC  X(100) VALUE SPACE.                
-=-        05  PV-LOG-LINES  REDEFINES  PV-LOG-LINES-TABLE                      
-=-            OCCURS 2 TIMES           PIC  X(117).                            
-=-        05  PV-HOLD-EIBLK            PIC  X(85)  VALUE SPACES.               
           05  PV-LOG-TIMEOUT.                                                  
               10  FILLER               PIC  X(28)   VALUE                      
                                       ' DPKCS008 (TIMEOUT NOTICE): '.          
               10  FILLER               PIC  X(08)   VALUE                      
                                              'NETNAME='.                       
               10  PV-NETNAME           PIC  X(08)   VALUE SPACE.               
               10  FILLER               PIC  X(05)   VALUE                      
                                              ' LOC='.                          
               10  PV-LOCATION          PIC  9(05)   VALUE ZERO.                
               10  FILLER               PIC  X(06)   VALUE                      
                                              ' USER='.                         
               10  PV-USERID            PIC  X(08)   VALUE SPACE.               
               10  FILLER               PIC  X(02)   VALUE                      
                                              '- '.                             
               10  PV-USERNAME          PIC  X(20)   VALUE SPACE.               
           05  PV-LOG-NOTFND.                                                   
               10  FILLER               PIC  X(32)   VALUE                      
                                ' DPKCS008: DPSAVCMK NOTFND (DEL)'.             
               10  FILLER               PIC  X(05)   VALUE                      
                                              ' KEY='.                          
               10  PV-NF-KEY            PIC  X(15)   VALUE SPACE.               
               10  FILLER               PIC  X(05)   VALUE                      
                                              ' AID='.                          
               10  PV-NF-AID            PIC  X(05)   VALUE SPACE.               
               10  FILLER               PIC  X(09)   VALUE                      
                                              ' NETNAME='.                      
               10  PV-NF-NETNAME        PIC  X(08)   VALUE SPACE.               
               10  FILLER               PIC  X(05)   VALUE                      
                                              ' LOC='.                          
               10  PV-NF-LOCATION       PIC  9(05)   VALUE ZERO.                
               10  FILLER               PIC  X(06)   VALUE                      
                                              ' USER='.                         
               10  PV-NF-USERID         PIC  X(08)   VALUE SPACE.               
               10  FILLER               PIC  X(02)   VALUE                      
                                              '- '.                             
               10  PV-NF-USERNAME       PIC  X(25)   VALUE SPACE.               
      *                                                                         
      ** ATTENTION IDENTIFIER DESCRIPTION TABLE                                 
      *                                                                         
       01  AC-AID-CONVERSION-TABLE.                                             
           05  AC-ATTENTION-ID-ENTRIES.                                         
               10  FILLER              PIC  X(06)  VALUE ' NULL '.              
               10  FILLER              PIC  X(06)  VALUE '''ENTER'.             
               10  FILLER              PIC  X(06)  VALUE '_CLEAR'.              
               10  FILLER              PIC  X(06)  VALUE 'ºCLRP '.              
               10  FILLER              PIC  X(06)  VALUE '=PEN  '.              
               10  FILLER              PIC  X(06)  VALUE 'WOPID '.              
               10  FILLER              PIC  X(06)  VALUE 'XMSRE '.              
               10  FILLER              PIC  X(06)  VALUE 'HSTRF '.              
               10  FILLER              PIC  X(06)  VALUE '"TRIG '.              
               10  FILLER              PIC  X(06)  VALUE '%PA1  '.              
               10  FILLER              PIC  X(06)  VALUE '>PA2  '.              
               10  FILLER              PIC  X(06)  VALUE ',PA3  '.              
               10  FILLER              PIC  X(06)  VALUE '1PF1  '.              
               10  FILLER              PIC  X(06)  VALUE '2PF2  '.              
               10  FILLER              PIC  X(06)  VALUE '3PF3  '.              
               10  FILLER              PIC  X(06)  VALUE '4PF4  '.              
               10  FILLER              PIC  X(06)  VALUE '5PF5  '.              
               10  FILLER              PIC  X(06)  VALUE '6PF6  '.              
               10  FILLER              PIC  X(06)  VALUE '7PF7  '.              
               10  FILLER              PIC  X(06)  VALUE '8PF8  '.              
               10  FILLER              PIC  X(06)  VALUE '9PF9  '.              
               10  FILLER              PIC  X(06)  VALUE ':PF10 '.              
               10  FILLER              PIC  X(06)  VALUE '#PF11 '.              
               10  FILLER              PIC  X(06)  VALUE '@PF12 '.              
               10  FILLER              PIC  X(06)  VALUE 'APF13 '.              
               10  FILLER              PIC  X(06)  VALUE 'BPF14 '.              
               10  FILLER              PIC  X(06)  VALUE 'CPF15 '.              
               10  FILLER              PIC  X(06)  VALUE 'DPF16 '.              
               10  FILLER              PIC  X(06)  VALUE 'EPF17 '.              
               10  FILLER              PIC  X(06)  VALUE 'FPF18 '.              
               10  FILLER              PIC  X(06)  VALUE 'GPF19 '.              
               10  FILLER              PIC  X(06)  VALUE 'HPF20 '.              
               10  FILLER              PIC  X(06)  VALUE 'IPF21 '.              
               10  FILLER              PIC  X(06)  VALUE '›PF22 '.              
               10  FILLER              PIC  X(06)  VALUE '.PF23 '.              
               10  FILLER              PIC  X(06)  VALUE '<PF24 '.              
           05  AC-ATTENTION-ID-ENTRY   REDEFINES                                
                                       AC-ATTENTION-ID-ENTRIES                  
                                       OCCURS 36 TIMES                          
                                       INDEXED BY AC-AID-IDX.                   
               10  AC-EIBAID           PIC  X(01).                              
               10  AC-AID-NAME         PIC  X(05).                              
                                                                                
      *                                                                         
      **  FIELDS FOR STACK MANIPULATION                                         
      *                                                                         
       01  SM-STACK-MANIPULATION-FIELDS.                                        
           05  SM-STACK-MAX             PIC S9(04)  VALUE +10                   
                                                    COMP SYNC.                  
           05  SM-AID-MAX               PIC S9(04)  VALUE +29                   
                                                    COMP SYNC.                  
           05  SM-FK-SUB                PIC S9(04)  VALUE ZERO                  
                                                    COMP SYNC.                  
           05  SM-STACK-KEY.                                                    
               10  SM-STACK-KEY-NETNAME.                                        
                   15 SM-STACK-KEY-LINE PIC  X(02)  VALUE SPACE.                
                   15 SM-STACK-KEY-UNIT PIC  X(02)  VALUE SPACE.                
                   15 SM-STACK-KEY-TERM PIC  X(04)  VALUE SPACE.                
               10  SM-STACK-KEY-TASK    PIC  9(07)  VALUE ZERO.                 
      *                                                                         
       01  TS-TEMP-STORAGE-QUEUE-INFO.                                          
           05  TS-SCA-QUEUE-INFO.                                               
               10  TS-SCA-QUEUE-NAME.                                           
                   15  TS-SCA-QUEUE-TERMINAL                                    
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-SCA-QUEUE-LITERAL                                     
                                        PIC  X(04)  VALUE 'SCA '.               
               10  TS-SCA-QUEUE-ITEM-NUMBER                                     
                                        PIC S9(04)  VALUE ZERO                  
                                        COMP SYNC.                              
           05  TS-APPL-QUEUE-INFO.                                              
               10  TS-APPL-QUEUE-NAME.                                          
                   15  TS-APPL-QUEUE-TERMINAL                                   
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-APPL-QUEUE-TASK                                       
                                        PIC S9(07)  VALUE ZERO                  
                                        COMP-3.                                 
               10  TS-APPL-MULT-QUEUE-ID.                                       
                   15  TS-APPL-MULT-QUEUE-TERM                                  
                                        PIC  X(04)  VALUE SPACES.               
                   15  TS-APPL-MULT-QUEUE-TASK                                  
                                        PIC S9(05)  VALUE ZERO                  
                                        COMP-3.                                 
                   15  TS-APPL-MULT-QUEUE-SEQ                                   
                                        PIC  9(01)  VALUE ZERO.                 
      *                                                                         
      **  STANDARD PFKEY TRANSLATIONS                                           
      *                                                                         
           COPY DPWS016.                                                        
      *                                                                         
      **  SECURITY SUBROUTINE PARAMETERS                                        
      *                                                                         
           COPY DPWS008.                                                        
      *                                                                         
      **  STANDARD COMMAREA LAYOUT                                              
      *                                                                         
           COPY DPWS020.                                                        
      *                                                                         
      **  KSDS VSAM FILE FOR SAVING EACH TRAN'S STANDARD COMMAREA LAYOUT        
      *                                                                         
           COPY DPRD515.                                                        
      *                                                                         
      **  INPUT RECORD DESCRIPTION FOR MNEMONIC FILE                            
      *                                                                         
           COPY DPRD503.                                                        
      *                                                                         
      **  RECORD DESCRIPTION FOR THE OBJECT LOCKING FILE                        
      *                                                                         
           COPY DPRD509.                                                        
      *                                                                         
      **  LAYOUT FOR ABEND-MODULE COMMAREA                                      
      *                                                                         
           COPY DPWS013.                                                        
                                                                                
                                                                                
       LINKAGE SECTION.                                                         
       01  DFHCOMMAREA                    PIC  X(4072).                         
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN-LINE.                                                          
           PERFORM 1000-CHECK-FOR-VALID-COMMAREA.                               
           SET PS-CICS-START-SUCCESS TO TRUE.                                   
           IF  PS-GOOD-COMMAREA                                                 
               PERFORM 2000-IDENTIFY-DEST-TRAN-ID                               
               PERFORM 3000-VALIDATE-SECURITY                                   
               PERFORM 4000-CHECK-AVAILABILITY                                  
               PERFORM 6000-PREPARE-CA-FOR-DEST                                 
               PERFORM 5000-TRANSFER-CONTROL                                    
               IF  PS-CICS-START-FAILED                                         
                   SET DP020-NEXT-ACT-APPL-ERROR TO TRUE                        
                   SET DP020-MSG-FATAL           TO TRUE                        
      *            -- TRANSACTION UNAVAILABLE --                                
                   MOVE PC-TSYMSG-99904          TO DP020-MSG-NUMBER            
                   PERFORM 2000-IDENTIFY-DEST-TRAN-ID                           
                   PERFORM 3000-VALIDATE-SECURITY                               
                   PERFORM 4000-CHECK-AVAILABILITY                              
                   PERFORM 6000-PREPARE-CA-FOR-DEST                             
                   PERFORM 5000-TRANSFER-CONTROL                                
               END-IF                                                           
           ELSE                                                                 
               PERFORM 7000-START-LOGON                                         
           END-IF.                                                              
      *                                                                         
           EXEC CICS                                                            
                RETURN                                                          
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      **  ATTEMPT TO RETRIEVE A COMMAREA.  IF THE COMMAREA RETRIEVED  **        
      **  IS NOT A STANDARD COMMAREA OR IF THE INCOMING COMMAREA      **        
      **  LENGTH IS GREATER THAN ZERO (THE NAVIGATOR WAS RETURNED OR  **        
      **  XCTL'D TO), SET THE BAD COMMAREA SWITCH.                    **        
      ******************************************************************        
       1000-CHECK-FOR-VALID-COMMAREA.                                           
           IF  EIBCALEN = ZERO                                                  
               EXEC CICS                                                        
                   RETRIEVE                                                     
                       INTO   (DP020-STANDARD-COMMAREA)                         
                       LENGTH (PC-MAX-COMMAREA-LENGTH)                          
                       RESP   (PV-CICS-RESPONSE)                                
               END-EXEC                                                         
               IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                          
                   IF  DP020-GOOD-COMMAREA                                      
                       CONTINUE                                                 
                   ELSE                                                         
                       SET PS-BAD-COMMAREA TO TRUE                              
                   END-IF                                                       
               ELSE                                                             
                   IF  PV-CICS-RESPONSE = DFHRESP (ENDDATA)                     
                       PERFORM 1100-RETRIEVE-COMMAREA-FROM-TS                   
                   ELSE                                                         
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       MOVE '1000-CHECK-FOR-VALID-COMMAREA'                     
                                           TO DP013-PARAGRAPH                   
                       SET DP013-LOGIC-ABEND TO TRUE                            
                       MOVE 'UNABLE TO FIND A COMMAREA FOR CALLING TRANS        
      -                     'ACTION'       TO DP013-MESSAGE-TEXT (1)            
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           ELSE                                                                 
             MOVE DFHCOMMAREA TO DP020-STANDARD-COMMAREA                        
             IF  DP020-GOOD-COMMAREA                                            
               SET PS-GOOD-COMMAREA TO TRUE                                     
             ELSE                                                               
               SET PS-BAD-COMMAREA TO TRUE                                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      **- TRY TO GET A COMMAREA FROM TEMP STORAGE -- THE QUEUE ID IS  **        
      **  TERMINAL ID + 'SCA'                                         **        
      **- THIS IS THE STANDARD COMMAREA WHICH IS SAVED WHEN NAVIGATING**        
      **  OUTSIDE OF THE ARCHITECTURE.  IF READING THIS WE ARE        **        
      **  RE-ENTERING THE ARCHITECTURE.                               **        
      **- BECAUSE THE NAVIGATOR WROTE THE QUEUE, NO CHECK FOR REMOTE  **        
      **  SYSTEM IS REQUIRED.                                         **        
      **                                                              **        
      ******************************************************************        
       1100-RETRIEVE-COMMAREA-FROM-TS.                                          
           MOVE EIBTRMID          TO TS-SCA-QUEUE-TERMINAL.                     
      *                                                                         
           EXEC CICS                                                            
               READQ TS                                                         
                   QUEUE (TS-SCA-QUEUE-NAME)                                    
                   ITEM  (1)                                                    
                   INTO  (DP020-STANDARD-COMMAREA)                              
                   RESP  (PV-CICS-RESPONSE)                                     
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-SCA-QUEUE-NAME)                                
                       RESP  (PV-CICS-RESPONSE)                                 
               END-EXEC                                                         
               IF  DP020-GOOD-COMMAREA                                          
                   SET PS-RETURN-FROM-OUTSIDE-ARCH TO TRUE                      
               ELSE                                                             
                   SET PS-BAD-COMMAREA TO TRUE                                  
               END-IF                                                           
           ELSE                                                                 
               SET PS-BAD-COMMAREA TO TRUE                                      
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      ** THE FOLLOWING MODULE WILL DETERMINE TO WHICH TRANSACTION     **        
      ** CONTROL SHOULD BE PASSED:                                    **        
      ** --> START THE LAST STACKED TRAN IF COMING BACK INTO THE      **        
      **     ARCHITECTURE FROM A NON-ARCHITECTURE PROGRAM.            **        
      ** --> LOGOFF IF THE RETURNING FROM A NON-ARCHITECTURE INITIAL  **        
      **     TRANSACTION.                                             **        
      ** --> START THE INITIAL TRANSACTION AND RESET THE STACK IF THE **        
      **     MAINMENU COMMAND WAS ENTERED OR IF THE MAINMENU FKEY WAS **        
      **     USED.                                                    **        
      ** --> GO TO THE ENTERED COMMAND IF ONE WAS KEYED IN THE COMMAND**        
      **     FIELD -- CHECK AGAINST THE FKEY TABLE FIRST TO SEE IF AN **        
      **     ALTERNATE LABEL WAS ENTERED.                             **        
      ** --> IF THE RETURN FKEY (F3) WAS PRESSED, START THE LAST      **        
      **     LOGICALLY STACKED TRANSACTION.                           **        
      ** --> IF AN APPLICATION-ERROR OCCURRED, START THE LAST PHYSI-  **        
      **     CALLY STACKED TRANSACTION.                               **        
      ** --> START THE TRANSACTION ASSOCIATED WITH THE 'GO-TO' TYPE   **        
      **     FUNCTION KEY PRESSED.                                    **        
      ******************************************************************        
       2000-IDENTIFY-DEST-TRAN-ID.                                              
           EVALUATE TRUE ALSO TRUE                                              
               WHEN PS-RETURN-FROM-OUTSIDE-ARCH                                 
               ALSO DP020-ENTRIES-IN-STACK > ZERO                               
                   MOVE DP020-STACK-TRAN-ID (DP020-ENTRIES-IN-STACK)            
                                              TO PV-DEST-TRAN-ID                
                   MOVE DP020-STACK-MNEMONIC (DP020-ENTRIES-IN-STACK)           
                                              TO PV-DEST-MNEMONIC               
               WHEN PS-RETURN-FROM-OUTSIDE-ARCH                                 
               ALSO ANY                                                         
      *              THIS INDICATES THAT THE INITIAL MNEMONIC MUST HAVE         
      *              BEEN OUTSIDE OF THE ARCHITECTURE, AND THIS RETURN          
      *              MUST BE FOLLOWING AN ABNORMAL TERMINATION, OR THE          
      *              DEPRESSION OF AN "EXIT" FUNCTION KEY.  THIS WILL BE        
      *              TREATED AS A REQUEST TO LOGOFF.                            
                   SET DP020-NEXT-ACT-INITIAL TO TRUE                           
                   MOVE PC-LOGOFF-TRANID     TO PV-DEST-TRAN-ID                 
                   MOVE PC-LOGOFF-MNEMONIC   TO PV-DEST-MNEMONIC                
                   SET PS-SRC-DONT-STACK  TO TRUE                               
               WHEN DP020-NEXT-ACT-APPL-ERROR                                   
               ALSO ANY                                                         
                   MOVE DP020-STACK-TRAN-ID (DP020-ENTRIES-IN-STACK)            
                                              TO PV-DEST-TRAN-ID                
                   MOVE DP020-STACK-MNEMONIC (DP020-ENTRIES-IN-STACK)           
                                              TO PV-DEST-MNEMONIC               
                   MOVE +0                    TO PV-ENTRIES-TO-POP              
               WHEN DP020-SRC-AID = DP016-ENTER                                 
               ALSO DP020-NAV-MNEMONIC = PC-MAIN-MENU-COMMAND                   
                   MOVE DP020-INITIAL-MNEMONIC    TO PV-DEST-MNEMONIC           
                   PERFORM 6110-READ-MNEMONIC                                   
                   IF  PS-MNEMONIC-FOUND                                        
                       SET PS-MAIN-MENU-CMMD-ENTERED  TO TRUE                   
                       SET DP020-NEXT-ACT-INITIAL     TO TRUE                   
                       SET PS-SRC-DONT-STACK          TO TRUE                   
                       MOVE DP503-TRAN-ID     TO PV-DEST-TRAN-ID                
                   ELSE                                                         
                       SET DP013-LOGIC-ABEND         TO TRUE                    
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       MOVE '2000-IDENTIFY-DEST-TRAN-ID'                        
                                                  TO DP013-PARAGRAPH            
                       STRING 'UNABLE TO FIND INITIAL MNEMONIC ('               
                                                      DELIMITED BY SIZE         
                              PV-DEST-MNEMONIC        DELIMITED BY SIZE         
                              ')'                     DELIMITED BY SIZE         
                           INTO DP013-MESSAGE-TEXT (1)                          
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               WHEN DP020-SRC-AID = DP016-ENTER                                 
               ALSO ANY                                                         
                   PERFORM 2200-INTERPRET-COMMAND                               
               WHEN OTHER                                                       
                   PERFORM 2100-INTERPRET-FKEY                                  
           END-EVALUATE.                                                        
      ******************************************************************        
      **  THIS PARAGRAPH WILL TAKE APPRORIATE ACTION BASED ON THE     **        
      **  FUNCTION KEY TYPE THAT IS ASSOCIATED WITH THE FUNCTION KEY  **        
      **  THAT WAS PRESSED BY THE TERMINAL OPERATOR.                  **        
      **                                                              **        
      ******************************************************************        
       2100-INTERPRET-FKEY.                                                     
           EVALUATE TRUE ALSO TRUE                                              
               WHEN (DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                        
                  OR DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                        
                  OR DP020-FK-DISPN-GO   (DP020-SRC-AID))                       
               ALSO (DP020-FK-MNEMONIC (DP020-SRC-AID) =                        
                                                  PC-MAIN-MENU-COMMAND)         
                   MOVE DP020-INITIAL-MNEMONIC    TO PV-DEST-MNEMONIC           
                   PERFORM 2220-VALIDATE-MNEMONIC                               
                   SET PS-MAIN-MENU-CMMD-ENTERED  TO TRUE                       
                   SET DP020-NEXT-ACT-INITIAL     TO TRUE                       
                   SET PS-SRC-DONT-STACK          TO TRUE                       
               WHEN (DP020-FK-GO-TO-TRAN (DP020-SRC-AID)                        
                  OR DP020-FK-LOCAL-GOTO (DP020-SRC-AID)                        
                  OR DP020-FK-DISPN-GO   (DP020-SRC-AID))                       
               ALSO  ANY                                                        
                   SET DP020-NEXT-ACT-INITIAL TO TRUE                           
                   IF  DP020-FK-ALT-LABEL (DP020-SRC-AID)                       
                       MOVE DP020-SRC-MNEMONIC TO PV-DEST-MNEMONIC              
                       PERFORM 6110-READ-MNEMONIC                               
                       MOVE DP503-FK-MNEMONIC (DP020-SRC-AID)                   
                                             TO PV-DEST-MNEMONIC                
                       MOVE DP020-FK-TRAN-ID (DP020-SRC-AID)                    
                                             TO PV-DEST-TRAN-ID                 
                       MOVE DP020-FK-STACK-IND (DP020-SRC-AID)                  
                                             TO PS-SRC-STACK-IND                
                       MOVE DP020-FK-INT-APPL-FORMAT-IND (DP020-SRC-AID)        
                                             TO PV-INT-APPL-FORMAT-IND          
                   ELSE                                                         
                       MOVE DP020-FK-MNEMONIC (DP020-SRC-AID)                   
                                             TO PV-DEST-MNEMONIC                
                       MOVE DP020-FK-TRAN-ID (DP020-SRC-AID)                    
                                             TO PV-DEST-TRAN-ID                 
                       MOVE DP020-FK-STACK-IND (DP020-SRC-AID)                  
                                             TO PS-SRC-STACK-IND                
                       MOVE DP020-FK-INT-APPL-FORMAT-IND (DP020-SRC-AID)        
                                             TO PV-INT-APPL-FORMAT-IND          
                   END-IF                                                       
               WHEN (DP020-FK-RETURN (DP020-SRC-AID)                            
                  OR DP020-FK-LOCAL-RETURN (DP020-SRC-AID)                      
                  OR DP020-FK-DISPN-RETURN (DP020-SRC-AID))                     
               ALSO (DP020-ENTRIES-IN-STACK > ZERO)                             
                   PERFORM 2110-FIND-LAST-STACKED-TRAN                          
                   MOVE DP020-FK-INT-APPL-FORMAT-IND (DP020-SRC-AID)            
                                             TO PV-INT-APPL-FORMAT-IND          
               WHEN (DP020-FK-RETURN (DP020-SRC-AID)                            
                  OR DP020-FK-LOCAL-RETURN (DP020-SRC-AID)                      
                  OR DP020-FK-DISPN-RETURN (DP020-SRC-AID))                     
               ALSO ANY                                                         
      *            -- NO PREVIOUS SCREEN --                                     
                   MOVE DP020-SRC-TRAN-ID    TO PV-DEST-TRAN-ID                 
                   MOVE DP020-SRC-MNEMONIC   TO PV-DEST-MNEMONIC                
                   SET DP020-NEXT-ACT-NAV-ERROR                                 
                                             TO TRUE                            
                   MOVE PC-TSYMSG-99902      TO DP020-MSG-NUMBER                
                   SET DP020-MSG-FATAL       TO TRUE                            
               WHEN OTHER                                                       
      *              -- INVALID FUNCTION KEY --                                 
                   SET DP020-NEXT-ACT-NAV-ERROR                                 
                                             TO TRUE                            
                   MOVE DP020-SRC-TRAN-ID    TO PV-DEST-TRAN-ID                 
                   MOVE DP020-SRC-MNEMONIC   TO PV-DEST-MNEMONIC                
                   MOVE PC-TSYMSG-00045      TO DP020-MSG-NUMBER                
                   SET DP020-MSG-FATAL       TO TRUE                            
           END-EVALUATE.                                                        
      ******************************************************************        
      **  THIS PARAGRAPH WILL SEARCH BACKWARDS THROUGH THE STACK TO   **        
      **  FIND THE LAST LOGICALLY STACKED TRANSACTION (STACK-TRAN-ON).**        
      **  IF THE END OF THE STACK IS REACHED FIRST, SEND AN ERROR     **        
      **  MESSAGE TO THE CURRENT SCREEN.  IF A STACKED TRANSACTION IS **        
      **  FOUND, SET UP THE COMMAREA TO START IT.                     **        
      ******************************************************************        
       2110-FIND-LAST-STACKED-TRAN.                                             
           PERFORM                                                              
               VARYING DP020-STACK-INDEX                                        
               FROM DP020-ENTRIES-IN-STACK BY -1                                
               UNTIL ((DP020-STACK-INDEX < 1)                                   
                   OR (DP020-STACK-TRAN-ON (DP020-STACK-INDEX)))                
                       CONTINUE                                                 
           END-PERFORM.                                                         
      *                                                                         
           IF  DP020-STACK-INDEX < 1                                            
      *        -- NO PREVIOUS SCREEN --                                         
               MOVE DP020-SRC-TRAN-ID    TO PV-DEST-TRAN-ID                     
               MOVE DP020-SRC-MNEMONIC   TO PV-DEST-MNEMONIC                    
               SET DP020-NEXT-ACT-NAV-ERROR                                     
                                         TO TRUE                                
               MOVE PC-TSYMSG-99902      TO DP020-MSG-NUMBER                    
               SET DP020-MSG-FATAL       TO TRUE                                
           ELSE                                                                 
               SET PV-HOLD-INDEX         TO DP020-STACK-INDEX                   
               SUBTRACT PV-HOLD-INDEX FROM DP020-ENTRIES-IN-STACK               
                   GIVING PV-ENTRIES-TO-POP                                     
               SET DP020-NEXT-ACT-RETURN TO TRUE                                
               MOVE DP020-STACK-TRAN-ID (DP020-STACK-INDEX)                     
                                         TO PV-DEST-TRAN-ID                     
               MOVE DP020-STACK-MNEMONIC (DP020-STACK-INDEX)                    
                                         TO PV-DEST-MNEMONIC                    
           END-IF.                                                              
      ******************************************************************        
      ** THIS PARAGRAPH TRIES TO CONVERT THE COMMAND ENTERED IN THE   **        
      ** COMMAND-FIELD INTO A FUNCTION KEY, AND IF FOUND, WILL TAKE   **        
      ** THE ACTION ASSIGNED TO THE FUNCTION KEY.  NOTE: BECAUSE WE   **        
      ** HAVE ALREADY LEFT THE 'SOURCE' PROGRAM, THE ONLY FUNCTIONS   **        
      ** THAT ARE VALID ARE 'GO-TO-TRAN' AND 'RETURN'.  ANYTHING THAT **        
      ** REQUIRES ANY KIND OF LOCAL PROCESSING CANNOT BE HANDLED HERE.**        
      ******************************************************************        
       2200-INTERPRET-COMMAND.                                                  
           SET DP020-FK-INDEX TO 1.                                             
           SEARCH DP020-FUNCTION-KEY                                            
               AT END                                                           
                   MOVE DP020-NAV-MNEMONIC     TO PV-DEST-MNEMONIC              
                   PERFORM 2220-VALIDATE-MNEMONIC                               
               WHEN DP020-NAV-MNEMONIC =                                        
                                   DP020-FK-MNEMONIC (DP020-FK-INDEX)           
                   IF  (DP020-FK-GO-TO-TRAN (DP020-FK-INDEX)                    
                     OR DP020-FK-RETURN (DP020-FK-INDEX))                       
                       IF  DP020-FK-ALT-LABEL (DP020-FK-INDEX)                  
                           PERFORM 2210-CONVERT-ALT-LABEL                       
                       ELSE                                                     
                          MOVE DP020-NAV-MNEMONIC    TO PV-DEST-MNEMONIC        
                       END-IF                                                   
                       PERFORM 2220-VALIDATE-MNEMONIC                           
                   ELSE                                                         
      *                -- INVALID COMMAND ENTERED --                            
                       SET DP020-NEXT-ACT-NAV-ERROR TO TRUE                     
                       MOVE DP020-SRC-TRAN-ID     TO PV-DEST-TRAN-ID            
                       MOVE DP020-SRC-MNEMONIC    TO PV-DEST-MNEMONIC           
                       MOVE PC-TSYMSG-99901       TO DP020-MSG-NUMBER           
                       SET DP020-MSG-FATAL        TO TRUE                       
                   END-IF                                                       
               END-SEARCH.                                                      
      *                                                                         
      *                                                                         
       2210-CONVERT-ALT-LABEL.                                                  
           MOVE DP020-SRC-MNEMONIC TO PV-DEST-MNEMONIC.                         
           PERFORM 6110-READ-MNEMONIC.                                          
           IF  PS-MNEMONIC-FOUND                                                
               SET DP503-FK-INDEX TO 1                                          
               SEARCH DP503-FUNCTION-KEY                                        
                  AT END                                                        
                      SET DP013-XCTL-DISPLAY-RESTART TO TRUE                    
                      SET DP013-LOGIC-ABEND          TO TRUE                    
                      MOVE '2210-CONVERT-ALT-LABEL'                             
                                         TO DP013-PARAGRAPH                     
                      STRING 'UNABLE TO FIND ALTERNATE LABEL ('                 
                                              DELIMITED BY SIZE                 
                             DP020-NAV-MNEMONIC                                 
                                              DELIMITED BY SIZE                 
                             ') ON SRC MNEMONIC RECORD'                         
                                              DELIMITED BY SIZE                 
                          INTO DP013-MESSAGE-TEXT (1)                           
                      PERFORM DP013-0000-PROCESS-ABEND                          
                  WHEN DP020-NAV-MNEMONIC =                                     
                           DP503-FK-LABEL (DP503-FK-INDEX)                      
                      MOVE DP503-FK-MNEMONIC (DP503-FK-INDEX)                   
                                          TO PV-DEST-MNEMONIC                   
               END-SEARCH                                                       
           ELSE                                                                 
               SET DP013-LOGIC-ABEND         TO TRUE                            
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '2210-CONVERT-ALT-LABEL' TO DP013-PARAGRAPH                 
               STRING 'UNABLE TO FIND SOURCE MNEMONIC ('                        
                                              DELIMITED BY SIZE                 
                      DP020-SRC-MNEMONIC      DELIMITED BY SIZE                 
                      ')'                     DELIMITED BY SIZE                 
                   INTO DP013-MESSAGE-TEXT (1)                                  
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       2220-VALIDATE-MNEMONIC.                                                  
           IF  DP503-MNEMONIC = PV-DEST-MNEMONIC                                
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 6110-READ-MNEMONIC                                       
           END-IF.                                                              
      *                                                                         
           IF  DP503-GLOBAL-ACCESS                                              
               SET DP020-NEXT-ACT-INITIAL TO TRUE                               
               MOVE DP503-TRAN-ID TO PV-DEST-TRAN-ID                            
               SET PS-SRC-STACK TO TRUE                                         
           ELSE                                                                 
      *        -- INVALID COMMAND ENTERED --                                    
               SET DP020-NEXT-ACT-NAV-ERROR TO TRUE                             
               MOVE DP020-SRC-TRAN-ID     TO PV-DEST-TRAN-ID                    
               MOVE DP020-SRC-MNEMONIC    TO PV-DEST-MNEMONIC                   
               MOVE PC-TSYMSG-99901       TO DP020-MSG-NUMBER                   
               SET DP020-MSG-FATAL        TO TRUE                               
           END-IF.                                                              
      *                                                                         
      *                                                                         
                                                                                
      ******************************************************************        
      **  THIS PARAGRAPH DOES A CALL TO MAKE A 'RACF' SECURITY CHECK  **        
      **  TO MAKE SURE THE USER IS AUTHORIZED TO USE THE TRANSACTION  **        
      **  WE ARE GOING TO.                                            **        
      ** - DON'T CHECK SECURITY OF A TRANSACTION BEING RETURNED TO,   **        
      **   BECAUSE IT WAS ALREADY CHECKED ON THE FIRST TIME TO IT.    **        
      ** - DON'T CHECK SECURITY ON AN ERROR RETURN BECAUSE CONTROL    **        
      **   IS BEING PASSED BACK TO THE TRAN THAT STARTED THE NAVIGATOR**        
      ******************************************************************        
       3000-VALIDATE-SECURITY.                                                  
           IF  (DP020-NEXT-ACT-RETURN                                           
             OR DP020-NEXT-ACT-ERROR)                                           
               CONTINUE                                                         
           ELSE                                                                 
               IF  PV-DEST-TRAN-ID = PC-MENU-TRANID                             
                   SET DP008-RSRC-TYPE-MENU    TO TRUE                          
                   MOVE PV-DEST-MNEMONIC       TO DP008-RESOURCE-NAME           
               ELSE                                                             
                   SET DP008-RSRC-TYPE-TRAN    TO TRUE                          
                   MOVE PV-DEST-TRAN-ID        TO DP008-RESOURCE-NAME           
               END-IF                                                           
               SET DP008-ACCESS-TYPE-USE       TO TRUE                          
               EXEC CICS LINK                                                   
                         PROGRAM  (DP008-SECURITY-SUBROUTINE)                   
                         COMMAREA (DP008-SECURITY-COMMAREA)                     
                         LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)              
               END-EXEC                                                         
               IF  DP008-CALL-SUCCESSFUL                                        
                   IF  DP008-ACCESS-ALLOWED                                     
                       CONTINUE                                                 
                   ELSE                                                         
      *                -- NO SECURITY CLEARANCE FOR TRANSACTION --              
                       MOVE DP020-SRC-TRAN-ID  TO PV-DEST-TRAN-ID               
                       MOVE DP020-SRC-MNEMONIC TO PV-DEST-MNEMONIC              
                       SET DP020-NEXT-ACT-NAV-ERROR TO TRUE                     
                       SET DP020-MSG-FATAL TO TRUE                              
                       MOVE PC-TSYMSG-99903    TO DP020-MSG-NUMBER              
                   END-IF                                                       
               ELSE                                                             
                   SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   SET DP013-SECURITY-ABEND      TO TRUE                        
                   MOVE '3000-VALIDATE-SECURITY' TO DP013-PARAGRAPH             
                   MOVE DP008-SECURITY-COMMAREA  TO DP013-SECURITY-INFO         
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  VERIFY THAT THE TRAN WE ARE GOING TO IS AVAILABLE           **        
      **  (ENABLED TO CICS), IF IT IS NOT, TRY GOING TO THE SOURCE.   **        
      ******************************************************************        
       4000-CHECK-AVAILABILITY.                                                 
           IF   (PV-DEST-TRAN-ID  = DP020-SRC-TRAN-ID                           
             AND PV-DEST-MNEMONIC = DP020-SRC-MNEMONIC)                         
               CONTINUE                                                         
           ELSE                                                                 
               SET PS-TRAN-NOT-AVAILABLE TO TRUE                                
               PERFORM 4100-TRAN-AVAILABILITY-CHECK                             
               IF  PS-TRAN-NOT-AVAILABLE                                        
                   MOVE DP020-SRC-TRAN-ID  TO PV-DEST-TRAN-ID                   
                   MOVE DP020-SRC-MNEMONIC TO PV-DEST-MNEMONIC                  
                   SET PS-AVAILABILITY-ERROR    TO TRUE                         
                   PERFORM 4100-TRAN-AVAILABILITY-CHECK                         
                   IF  PS-TRAN-NOT-AVAILABLE                                    
                       SET DP013-OTHER-ABEND TO TRUE                            
                       SET DP013-XCTL-DISPLAY-RESTART TO TRUE                   
                       MOVE '4000-CHECK-AVAILABILITY'                           
                                           TO DP013-PARAGRAPH                   
                       MOVE 'NO AVAILABLE TRANSACTION TO GO TO'                 
                                           TO DP013-MESSAGE-TEXT (1)            
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
           IF  PS-AVAILABILITY-ERROR                                            
      *        -- REQUESTED TRANSACTION IS UNAVAILABLE --                       
               SET DP020-NEXT-ACT-NAV-ERROR TO TRUE                             
               SET DP020-MSG-FATAL TO TRUE                                      
               IF PS-AVAIL-LOC                                                  
                   MOVE PC-TSYMSG-99911 TO DP020-MSG-NUMBER                     
               ELSE                                                             
                   MOVE PC-TSYMSG-99904 TO DP020-MSG-NUMBER                     
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      **  DO AN INQUIRE ON THE TRAN ID WE INTEND TO GO TO THIS WILL   **        
      **  TELL US IF THE TRANSACTION IS ENABLED.                      **        
      ******************************************************************        
       4100-TRAN-AVAILABILITY-CHECK.                                            
           EXEC CICS INQUIRE                                                    
                TRANSACTION  (PV-DEST-TRAN-ID)                                  
                STATUS       (PV-INQUIRE-CODE)                                  
                REMOTESYSTEM (PV-REMOTE-SYSTEM)                                 
                RESP         (PV-CICS-RESPONSE)                                 
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
               IF  PV-INQUIRE-CODE = DFHVALUE(ENABLED)                          
                   IF  PV-DEST-MNEMONIC NOT = DP503-MNEMONIC                    
                       PERFORM 6110-READ-MNEMONIC                               
                   END-IF                                                       
                   IF  PS-MNEMONIC-FOUND                                        
                       IF  DP503-TRAN-AVAILABLE                                 
                           SET PS-TRAN-AVAILABLE TO TRUE                        
                       END-IF                                                   
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  (PV-REMOTE-SYSTEM NOT = SPACES)                                  
           AND (PS-TRAN-AVAILABLE)                                              
               EXEC CICS INQUIRE                                                
                   CONNECTION  (PV-REMOTE-SYSTEM)                               
                   CONNSTATUS  (PV-REMOTE-STATUS)                               
                   RESP        (PV-CICS-RESPONSE)                               
               END-EXEC                                                         
               IF (PV-CICS-RESPONSE NOT = DFHRESP(NORMAL))                      
               OR (NOT PV-REMOTE-ACQUIRED)                                      
                   SET PS-TRAN-NOT-AVAILABLE TO TRUE.                           
                                                                                
           IF PS-TRAN-AVAILABLE                                                 
               IF (DP020-LOCATION-ACRT                                          
               OR  DP503-ACRT-LOC-NOT-REQR)                                     
                   CONTINUE                                                     
               ELSE                                                             
                   SET PS-TRAN-NOT-AVAILABLE TO TRUE                            
                   SET PS-AVAIL-LOC          TO TRUE                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  START THE APPROPRIATE TRANID.  IF LEAVING THE ARCHITECTURE, **        
      **  SAVE THE CURRENT COMMAREA TO TEMP STORAGE AND START THE     **        
      **  TRANSACTION WITHOUT A COMMAREA.                             **        
      ******************************************************************        
       5000-TRANSFER-CONTROL.                                                   
           IF  DP020-NEXT-ACT-ERROR                                             
               PERFORM 5100-START-DEST-TRANID                                   
           ELSE                                                                 
               IF  DP503-COMMAREA-STANDARD                                      
                   PERFORM 5100-START-DEST-TRANID                               
               ELSE                                                             
                   SET DP020-NEXT-ACT-RETURN TO TRUE                            
                   MOVE EIBTRMID      TO TS-SCA-QUEUE-TERMINAL                  
                   MOVE 'SCA'         TO TS-SCA-QUEUE-LITERAL                   
                   MOVE +1            TO PV-TS-ITEM                             
                   PERFORM 5200-SAVE-COMMAREA-TO-TS                             
                   PERFORM 5300-START-TRANID-NO-COMM                            
               END-IF                                                           
           END-IF.                                                              
      *****************************************************************         
      **                                                             **         
      ** START THE DESTINATION TRANSACTION.  IF THE START FAILS,     **         
      ** SET AN ERROR CONDITION SO A MESSAGE WILL BE DISPLAYED AND   **         
      ** CONTROL WILL BE RETURNED TO THE PREVIOUS TRANSACTION.  IF   **         
      ** THE ERROR HAS ALREADY BEEN SET, ABEND.                      **         
      **                                                             **         
      *****************************************************************         
       5100-START-DEST-TRANID.                                                  
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID   (PV-DEST-TRAN-ID)                                  
                   COMMAREA  (DP020-STANDARD-COMMAREA)                          
                   LENGTH    (DP020-COMMAREA-LENGTH)                            
                   RESP      (PV-CICS-RESPONSE)                                 
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               IF  PS-CICS-START-FAILED                                         
                   SET DP013-CICS-ABEND TO TRUE                                 
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   MOVE '5100-START-DEST-TRANID'                                
                                          TO DP013-PARAGRAPH                    
                   MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT L        
      -                 'INE'             TO DP013-MESSAGE-TEXT (1)             
                   MOVE PV-DEST-TRAN-ID   TO DP013-MESSAGE-TEXT (2)             
                   PERFORM DP013-0000-PROCESS-ABEND                             
               ELSE                                                             
                   SET PS-CICS-START-FAILED TO TRUE                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      **  SAVE THE STANDARD COMMAREA TO TEMP STORAGE.  IF, AFTER      **        
      **  WRITING THE ITEM NUMBER IS NOT 1, THE TS QUEUE HAD NOT BEEN **        
      **  CLEANED UP FROM A PREVIOUS SESSION, SO DELETE THE QUEUE     **        
      **  AND WRITE IT AGAIN.                                         **        
      ******************************************************************        
       5200-SAVE-COMMAREA-TO-TS.                                                
           PERFORM 5210-WRITE-TS-QUEUE.                                         
           IF  PV-TS-ITEM = PC-FIRST-ITEM                                       
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 5220-DELETE-TS-QUEUE                                     
               PERFORM 5210-WRITE-TS-QUEUE                                      
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5210-WRITE-TS-QUEUE.                                                     
           EXEC CICS                                                            
               WRITEQ TS                                                        
                   QUEUE   (TS-SCA-QUEUE-NAME)                                  
                   ITEM    (PV-TS-ITEM)                                         
                   FROM    (DP020-STANDARD-COMMAREA)                            
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5210-WRITE-TS-QUEUE'                                       
                                TO DP013-PARAGRAPH                              
               MOVE 'WRITEQ TS -- FAILED, QUEUE ID FOLLOWS'                     
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-SCA-QUEUE-NAME                                           
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5220-DELETE-TS-QUEUE.                                                    
           EXEC CICS                                                            
               DELETEQ TS                                                       
                   QUEUE   (TS-SCA-QUEUE-NAME)                                  
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5220-DELETE-TS-QUEUE'                                      
                                TO DP013-PARAGRAPH                              
               MOVE 'DELETEQ TS -- FAILED, QUEUE ID FOLLOWS'                    
                                TO DP013-MESSAGE-TEXT (1)                       
               MOVE TS-SCA-QUEUE-NAME                                           
                                TO DP013-MESSAGE-TEXT (2)                       
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      *                                                                         
       5300-START-TRANID-NO-COMM.                                               
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID (PV-DEST-TRAN-ID)                                    
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '5300-START-TRANID-NO-COMM'                                 
                                      TO DP013-PARAGRAPH                        
               MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT LINE'        
                                      TO DP013-MESSAGE-TEXT (1)                 
               MOVE PV-DEST-TRAN-ID   TO DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  IF WE ARE GOING TO A NEW TRANSACTION, STACK THE TRANSACTION **        
      **  WE ARE LEAVING IF SO INDICATED.  RESET THE SOURCE VARIABLES **        
      **  SO THEY INDICATE THE TRANSACTION WE ARE GOING TO AS THE     **        
      **  NEW SOURCE.                                                 **        
      ******************************************************************        
       6000-PREPARE-CA-FOR-DEST.                                                
           IF  DP020-NEXT-ACT-INITIAL                                           
               IF PS-MAIN-MENU-CMMD-ENTERED                                     
                   PERFORM 6420-DELETE-STACK-ENTRY                              
                       UNTIL DP020-ENTRIES-IN-STACK = ZERO                      
               END-IF                                                           
               PERFORM 6200-ADD-ENTRY-TO-STACK                                  
               SET DP020-FK-DISPLAY-13-24 TO TRUE                               
               PERFORM 6100-RESET-SRC-VARIABLES                                 
               PERFORM 6300-FORMAT-COMM-AREA                                    
               MOVE EIBTASKN              TO DP020-SRC-TASK-NUMBER              
               INITIALIZE DP020-APPL-CURSOR-POSITION                            
               SET DP020-SRC-NO-LOCK-HELD TO TRUE                               
           ELSE                                                                 
               IF  (DP020-NEXT-ACT-RETURN                                       
                 OR DP020-NEXT-ACT-APPL-ERROR)                                  
                   SET DP020-FK-DISPLAY-13-24 TO TRUE                           
                   PERFORM 6400-POP-OFF-STACK                                   
                   PERFORM 6100-RESET-SRC-VARIABLES                             
                   PERFORM 6300-FORMAT-COMM-AREA                                
               END-IF                                                           
           END-IF.                                                              
           SET DP020-SRC-RETURN-W-TRAN-ID TO TRUE.                              
           MOVE PV-DEST-TRAN-ID     TO DP020-SRC-TRAN-ID.                       
           MOVE PV-DEST-MNEMONIC    TO DP020-SRC-MNEMONIC.                      
           MOVE 0                   TO DP020-SRC-AID.                           
           MOVE PV-INT-APPL-FORMAT-IND                                          
                                    TO DP020-INT-APPL-FORMAT-IND.               
           IF  NOT DP020-NEXT-ACT-NAV-ERROR                                     
               MOVE SPACE           TO DP020-NAV-MNEMONIC                       
           END-IF.                                                              
      ******************************************************************        
      **- SET THE SOURCE AND COMMAREA LENGTH VARIABLES -- ONLY READ   **        
      **  THE MNEMONIC FILE IF IT HASN'T ALREADY BEEN READ.           **        
      **- USE THE MNEMONIC FILE TO FILL THE FK DISPLAY FOR THE NEW    **        
      **  TRANSACTION.                                                **        
      ******************************************************************        
       6100-RESET-SRC-VARIABLES.                                                
           IF  PV-DEST-MNEMONIC = DP503-MNEMONIC                                
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 6110-READ-MNEMONIC                                       
               IF  PS-MNEMONIC-NOT-FOUND                                        
                   SET DP013-LOGIC-ABEND TO TRUE                                
                   SET DP013-XCTL-DISPLAY-RESTART TO TRUE                       
                   MOVE '6100-RESET-SRC-VARIABLES'                              
                                         TO DP013-PARAGRAPH                     
                   MOVE 'THE FOLLOWING MNEMONIC WAS NOT FOUND'                  
                                         TO DP013-MESSAGE-TEXT (1)              
                   MOVE PV-DEST-MNEMONIC TO DP013-MESSAGE-TEXT (2)              
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
           MOVE DP503-SAVE-COMMAREA-SW   TO DP020-SRC-SAVE-COMMAREA-SW.         
           SET PS-SRC-STACK              TO TRUE.                               
      *                                                                         
           MOVE PC-GLOBAL-COMMAREA-LENGTH                                       
                                       TO DP020-GLOBAL-COMMAREA-LENGTH.         
           MOVE DP503-APPL-COMMAREA-LENGTH                                      
                                       TO DP020-APPL-COMMAREA-LENGTH.           
           COMPUTE DP020-COMMAREA-LENGTH =                                      
                     DP020-GLOBAL-COMMAREA-LENGTH                               
                     + DP020-APPL-COMMAREA-LENGTH.                              
      *                                                                         
           COMPUTE PV-ACT-DPSAVCMK-LENGTH =                                     
                     DP020-APPL-COMMAREA-LENGTH                                 
                     + PC-STACK-KEY-LENGTH                                      
                     + PC-EIBDATE-EIBTIME-8-LENGTH.                             
      *                                                                         
      *                                                                         
       6110-READ-MNEMONIC.                                                      
           EXEC CICS                                                            
               READ                                                             
                   FILE   (FD-MNEMONIC-FILE)                                    
                   INTO   (DP503-MNEMONIC-RECORD)                               
                   RIDFLD (PV-DEST-MNEMONIC)                                    
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               SET PS-MNEMONIC-FOUND TO TRUE                                    
           ELSE                                                                 
               IF  PV-CICS-RESPONSE = DFHRESP (NOTFND)                          
                   SET PS-MNEMONIC-NOT-FOUND TO TRUE                            
               ELSE                                                             
                   SET DP013-CICS-ABEND      TO TRUE                            
                   SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                        
                   MOVE '6110-READ-MNEMONIC' TO DP013-PARAGRAPH                 
                   MOVE 'BAD STATUS RETURNED READING DPMNEMIC'                  
                                             TO DP013-MESSAGE-TEXT (1)          
                   PERFORM DP013-0000-PROCESS-ABEND                             
               END-IF                                                           
           END-IF.                                                              
      ******************************************************************        
      **  SAVE THE 'SOURCE' VARIABLES TO THE STACK VARIABLES.  IF THE **        
      **  STACK ALREADY CONTAINS THE MAXIMUM NUMBER OF ENTRIES,       **        
      **  REMOVE THE FIRST ENTRY IN THE STACK AND DELETE THE          **        
      **  APPLICATION TS QUEUES, IF ANY IS ASSOCIATED WITH THE        **        
      **  TRANSACTION.  BUMP ALL CURRENT STACK CONTENTS DOWN ONE TO   **        
      **  FREE UP THE HIGHEST STACK ENTRY.  IF THE TRANSACTION BEING  **        
      **  STACKED HAS AN APPLICATION-SPECIFIC COMMAREA THAT NEEDS TO  **        
      **  BE SAVED, THAT PORTION OF THE COMMAREA IS WRITTEN TO A VSAM **        
      **  FILE.                                                       **        
      ******************************************************************        
       6200-ADD-ENTRY-TO-STACK.                                                 
           IF  DP020-ENTRIES-IN-STACK = SM-STACK-MAX                            
               MOVE EIBTRMID            TO TS-APPL-QUEUE-TERMINAL               
                                           TS-APPL-MULT-QUEUE-TERM              
               MOVE DP020-STACK-TASK-NUMBER (1)                                 
                                        TO TS-APPL-QUEUE-TASK                   
                                           TS-APPL-MULT-QUEUE-TASK              
               MOVE DP020-STACK-TRAN-ID (1)                                     
                                        TO PV-DELETEQ-TRAN-ID                   
               PERFORM 6430-DELETE-APPL-TS-QUEUE                                
               IF DP020-STACK-LOCK-HELD (1)                                     
                   MOVE DP020-STACK-TASK-NUMBER (1)                             
                                        TO DP509-TASK-NBR                       
                   PERFORM 6440-DELETE-TASK-RCD                                 
                   SET DP020-STACK-NO-LOCK-HELD (1) TO TRUE                     
               END-IF                                                           
               MOVE DP020-STACK-TASK-NUMBER (1)                                 
                                         TO SM-STACK-KEY-TASK                   
               PERFORM 6450-DELETE-FROM-DPSAVCMK                                
               PERFORM                                                          
                   VARYING DP020-STACK-INDEX                                    
                   FROM 2 BY 1                                                  
                   UNTIL DP020-STACK-INDEX > SM-STACK-MAX                       
                   MOVE DP020-STACK-TRANSACTION (DP020-STACK-INDEX)             
                      TO DP020-STACK-TRANSACTION (DP020-STACK-INDEX - 1)        
               END-PERFORM                                                      
           ELSE                                                                 
               ADD 1 TO DP020-ENTRIES-IN-STACK                                  
           END-IF.                                                              
           SET DP020-STACK-INDEX        TO DP020-ENTRIES-IN-STACK.              
           MOVE DP020-SRC-TRAN-ID       TO DP020-STACK-TRAN-ID                  
                                           (DP020-STACK-INDEX).                 
           MOVE DP020-SRC-MNEMONIC      TO DP020-STACK-MNEMONIC                 
                                           (DP020-STACK-INDEX).                 
           MOVE DP020-SRC-SAVE-COMMAREA-SW                                      
                                        TO DP020-STACK-COMMAREA-SAVED-SW        
                                           (DP020-STACK-INDEX).                 
           MOVE DP020-SRC-TASK-NUMBER   TO DP020-STACK-TASK-NUMBER              
                                           (DP020-STACK-INDEX).                 
           MOVE DP020-SRC-LOCK-IND      TO DP020-STACK-LOCK-IND                 
                                           (DP020-STACK-INDEX).                 
      *                                                                         
           IF  PS-SRC-STACK                                                     
               SET DP020-STACK-TRAN-ON (DP020-STACK-INDEX)                      
                                        TO TRUE                                 
           ELSE                                                                 
               SET DP020-STACK-TRAN-OFF (DP020-STACK-INDEX)                     
                                        TO TRUE                                 
           END-IF.                                                              
      *                                                                         
           IF  DP020-SRC-SAVE-COMMAREA                                          
               MOVE DP020-NETNAME       TO SM-STACK-KEY-NETNAME                 
               MOVE DP020-STACK-TASK-NUMBER                                     
                                    (DP020-STACK-INDEX)                         
                                        TO SM-STACK-KEY-TASK                    
               MOVE SM-STACK-KEY        TO DP515-KEY                            
               MOVE EIBDATE             TO DP515-CREATE-EIBDATE                 
               MOVE EIBTIME             TO DP515-CREATE-EIBTIME                 
               MOVE '8'                 TO DP515-CREATE-PGM                     
               MOVE DP020-APPL-COMM-AREA                                        
                                        TO DP515-APPL-COMMAREA                  
               COMPUTE PV-ACT-DPSAVCMK-LENGTH =                                 
                       DP020-APPL-COMMAREA-LENGTH                               
                     + PC-STACK-KEY-LENGTH                                      
                     + PC-EIBDATE-EIBTIME-8-LENGTH                              
               PERFORM 6210-WRITE-DPSAVCMK                                      
           END-IF.                                                              
      ******************************************************************        
      **  WRITE THE APPLICATION SPECIFIC COMMAREA TO THE KSDS VSAM    **        
      **  FILE AND SAVE THE KEY SO THE RECORD CAN BE RETRIEVED IF     **        
      **  THE TRANSACTION IS 'POPPED' OFF THE STACK.                  **        
      ******************************************************************        
       6210-WRITE-DPSAVCMK.                                                     
      *                                                                         
           EXEC CICS                                                            
               WRITE                                                            
                   FILE      (FD-SAVED-COMMAREA-FILE)                           
                   FROM      (DP515-SAVED-COMMAREA-RECORD)                      
                   LENGTH    (PV-ACT-DPSAVCMK-LENGTH)                           
                   RESP      (PV-CICS-RESPONSE)                                 
                   RIDFLD    (DP515-KEY)                                        
                   KEYLENGTH (PC-STACK-KEY-LENGTH)                              
           END-EXEC.                                                            
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND       TO TRUE                               
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '6210-WRITE-DPSAVCMK' TO DP013-PARAGRAPH                    
               MOVE 'BAD RESPONSE WRITING APPL COMMAREA TO VSAM'                
                                              TO DP013-MESSAGE-TEXT (1)         
               MOVE DP515-KEY                 TO PV-DISPLAY-KEY                 
               MOVE 'KEYLEN='                 TO PV-DISPLAY-FIELD2-HDR          
               MOVE PC-STACK-KEY-LENGTH       TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD2              
               MOVE 'RECLEN='                 TO PV-DISPLAY-FIELD3-HDR          
               MOVE PV-ACT-DPSAVCMK-LENGTH    TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD3              
               MOVE PV-DISPLAY-LINE-2         TO DP013-MESSAGE-TEXT (2)         
-=-            IF PV-CICS-RESPONSE = DFHRESP (DUPREC)                           
-=-                MOVE DFHEIBLK TO PV-HOLD-EIBLK                               
-=-                PERFORM 6211-LOG-DUPREC                                      
-=-                MOVE PV-HOLD-EIBLK TO DFHEIBLK                               
-=-            END-IF                                                           
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
-=-                                                                             
-=-                                                                             
-=-    6211-LOG-DUPREC.                                                         
-=-                                                                             
-=-        MOVE DP515-SAVED-COMMAREA-RECORD TO PV-NEW-REC.                      
-=-        PERFORM 6410-READ-DPSAVCMK.                                          
-=-        MOVE DP515-SAVED-COMMAREA-RECORD TO PV-OLD-REC.                      
-=-                                                                             
-=-        PERFORM 6212-ENQUEUE.                                                
-=-        PERFORM  VARYING PV-LOG-SUB FROM 1 BY 1                              
-=-                   UNTIL PV-LOG-SUB > 2                                      
-=-            MOVE PV-LOG-LINES (PV-LOG-SUB) TO PV-LOG-LINE                    
-=-            PERFORM 6213-WRITEQ-TD                                           
-=-        END-PERFORM.                                                         
-=-        PERFORM 6214-DEQUEUE.                                                
-=-                                                                             
-=-                                                                             
-=-    6212-ENQUEUE.                                                            
-=-        EXEC CICS ENQ                                                        
-=-            RESOURCE (PC-LOG-QUEUE-RESOURCE)                                 
-=-        END-EXEC.                                                            
-=-                                                                             
-=-                                                                             
-=-    6213-WRITEQ-TD.                                                          
-=-        EXEC CICS WRITEQ TD                                                  
-=-            QUEUE (PC-LOG-QUEUE)                                             
-=-            FROM  (PV-LOG-LINE)                                              
-=-            RESP  (PV-CICS-RESPONSE)                                         
-=-        END-EXEC.                                                            
-=-   *                                                                         
-=-        IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
-=-            CONTINUE                                                         
-=-        ELSE                                                                 
-=-            INITIALIZE                         DP013-MESSAGE-TABLE           
-=-            SET DP013-CICS-ABEND           TO  TRUE                          
-=-            SET DP013-LINK-RETURN          TO  TRUE                          
-=-            SET DP013-NO-ROLLBACK          TO  TRUE                          
-=-            MOVE '6213-WRITEQ-TD'          TO  DP013-PARAGRAPH               
-=-            MOVE 'BAD RETURN CODE FROM WRITE TO CSSL'                        
-=-                                           TO  DP013-MESSAGE-TEXT (1)        
-=-            PERFORM DP013-0000-PROCESS-ABEND                                 
-=-        END-IF.                                                              
-=-                                                                             
-=-                                                                             
-=-    6214-DEQUEUE.                                                            
-=-        EXEC CICS DEQ                                                        
-=-            RESOURCE (PC-LOG-QUEUE-RESOURCE)                                 
-=-        END-EXEC.                                                            
                                                                                
      ******************************************************************        
      **  MOVE THE FK DEFINITIONS FOR THE DESTINATION TRANSACTION     **        
      **  FROM THE MNEMONIC FILE TO THE COMMAREA.  IF THE FK IS       **        
      **  RELATED TO A TRANID, CHECK THE SECURITY ON THE TRANSACTION  **        
      **  BEFORE MOVING THE FK AREA INTO THE COMMAREA.                **        
      ******************************************************************        
       6300-FORMAT-COMM-AREA.                                                   
           INITIALIZE DP020-FUNCTION-KEY-ARRAY.                                 
           MOVE DP503-PANEL-TITLE      TO DP020-PANEL-TITLE.                    
           MOVE DP503-DB2-PKG-IND      TO DP020-SRC-DB2-PKG-IND.                
                                                                                
           PERFORM                                                              
               VARYING SM-FK-SUB                                                
               FROM 1 BY 1                                                      
               UNTIL SM-FK-SUB > SM-AID-MAX                                     
               IF  DP503-FK-TRAN-ID (SM-FK-SUB) = SPACE                         
                   MOVE DP503-FK-LABEL (SM-FK-SUB)                              
                            TO DP020-FK-MNEMONIC (SM-FK-SUB)                    
                   MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                            
                            TO DP020-FK-TRAN-ID (SM-FK-SUB)                     
                   MOVE DP503-FK-ACTION (SM-FK-SUB)                             
                            TO DP020-FK-ACTION (SM-FK-SUB)                      
                   MOVE DP503-FK-STACK-IND (SM-FK-SUB)                          
                            TO DP020-FK-STACK-IND (SM-FK-SUB)                   
                   MOVE DP503-FK-ALT-LABEL-IND (SM-FK-SUB)                      
                            TO DP020-FK-ALT-LABEL-IND (SM-FK-SUB)               
                   MOVE DP503-FK-INT-APPL-FORMAT-IND (SM-FK-SUB)                
                            TO DP020-FK-INT-APPL-FORMAT-IND (SM-FK-SUB)         
                   MOVE DP503-FK-DISPN-IND (SM-FK-SUB)                          
                            TO DP020-FK-DISPN-IND (SM-FK-SUB)                   
               ELSE                                                             
                   SET DP008-ACCESS-TYPE-USE    TO TRUE                         
                   IF  DP503-FK-TRAN-ID (SM-FK-SUB) = PC-MENU-TRANID            
                       SET DP008-RSRC-TYPE-MENU TO TRUE                         
                       MOVE DP503-FK-MNEMONIC (SM-FK-SUB)                       
                                       TO DP008-RESOURCE-NAME                   
                   ELSE                                                         
                       SET DP008-RSRC-TYPE-TRAN TO TRUE                         
                       MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                        
                                       TO DP008-RESOURCE-NAME                   
                   END-IF                                                       
                   EXEC CICS                                                    
                       LINK PROGRAM  (DP008-SECURITY-SUBROUTINE)                
                            COMMAREA (DP008-SECURITY-COMMAREA)                  
                            LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)           
                   END-EXEC                                                     
                   IF  DP008-CALL-SUCCESSFUL                                    
                       IF  DP008-ACCESS-ALLOWED                                 
                           MOVE DP503-FK-LABEL (SM-FK-SUB)                      
                                       TO DP020-FK-MNEMONIC (SM-FK-SUB)         
                           MOVE DP503-FK-TRAN-ID (SM-FK-SUB)                    
                                       TO DP020-FK-TRAN-ID (SM-FK-SUB)          
                           MOVE DP503-FK-ACTION (SM-FK-SUB)                     
                                       TO DP020-FK-ACTION (SM-FK-SUB)           
                           MOVE DP503-FK-STACK-IND (SM-FK-SUB)                  
                                       TO DP020-FK-STACK-IND (SM-FK-SUB)        
                           MOVE DP503-FK-ALT-LABEL-IND (SM-FK-SUB)              
                                       TO DP020-FK-ALT-LABEL-IND                
                                                            (SM-FK-SUB)         
                           MOVE DP503-FK-INT-APPL-FORMAT-IND                    
                                                            (SM-FK-SUB)         
                                        TO DP020-FK-INT-APPL-FORMAT-IND         
                                                            (SM-FK-SUB)         
                           MOVE DP503-FK-DISPN-IND (SM-FK-SUB)                  
                                        TO DP020-FK-DISPN-IND                   
                                                            (SM-FK-SUB)         
                       ELSE                                                     
                           INITIALIZE DP020-FUNCTION-KEY (SM-FK-SUB)            
                       END-IF                                                   
                   ELSE                                                         
                       SET DP013-SECURITY-ABEND      TO TRUE                    
                       SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                    
                       MOVE '6300-FORMAT-COMM-AREA'  TO DP013-PARAGRAPH         
                       MOVE DP008-SECURITY-COMMAREA                             
                                                 TO DP013-SECURITY-INFO         
                       PERFORM DP013-0000-PROCESS-ABEND                         
                   END-IF                                                       
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
      ******************************************************************        
      **  RETURNING THROUGH THE STACK.  RESET THE SOURCE VARIABLES    **        
      **  FROM THE STACK VARIABLES.  RESTORE THE APPLICATION SPECIFIC **        
      **  COMMAREA, IF ANY.                                           **        
      ******************************************************************        
       6400-POP-OFF-STACK.                                                      
           IF  PV-ENTRIES-TO-POP > ZERO                                         
               PERFORM 6420-DELETE-STACK-ENTRY                                  
                   PV-ENTRIES-TO-POP TIMES                                      
           END-IF.                                                              
      *                                                                         
           IF  DP020-STACK-COMMAREA-SAVED (DP020-ENTRIES-IN-STACK)              
               MOVE DP020-NETNAME         TO SM-STACK-KEY-NETNAME               
               MOVE DP020-STACK-TASK-NUMBER                                     
                                    (DP020-ENTRIES-IN-STACK)                    
                                          TO SM-STACK-KEY-TASK                  
               MOVE SM-STACK-KEY          TO DP515-KEY                          
               PERFORM 6410-READ-DPSAVCMK                                       
           ELSE                                                                 
               INITIALIZE DP020-APPL-CURSOR-POSITION                            
           END-IF.                                                              
      *                                                                         
           IF  DP020-SRC-TASK-NUMBER > ZERO                                     
               MOVE EIBTRMID              TO TS-APPL-QUEUE-TERMINAL             
                                             TS-APPL-MULT-QUEUE-TERM            
               MOVE DP020-SRC-TASK-NUMBER TO TS-APPL-QUEUE-TASK                 
                                             TS-APPL-MULT-QUEUE-TASK            
               MOVE DP020-SRC-TRAN-ID     TO PV-DELETEQ-TRAN-ID                 
               PERFORM 6430-DELETE-APPL-TS-QUEUE                                
               IF DP020-SRC-LOCK-HELD                                           
                   MOVE DP020-SRC-TASK-NUMBER                                   
                                          TO DP509-TASK-NBR                     
                   PERFORM 6440-DELETE-TASK-RCD                                 
                   SET DP020-SRC-NO-LOCK-HELD TO TRUE                           
               END-IF                                                           
           END-IF.                                                              
      *                                                                         
           MOVE DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK)                
                                          TO SM-STACK-KEY-TASK                  
           PERFORM 6450-DELETE-FROM-DPSAVCMK                                    
           MOVE DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK)                
                                          TO DP020-SRC-TASK-NUMBER.             
           MOVE DP020-STACK-COMMAREA-SAVED-SW (DP020-ENTRIES-IN-STACK)          
                                          TO DP020-SRC-SAVE-COMMAREA-SW.        
           MOVE DP020-STACK-LOCK-IND (DP020-ENTRIES-IN-STACK)                   
                                          TO DP020-SRC-LOCK-IND.                
           INITIALIZE DP020-STACK-TRANSACTION (DP020-ENTRIES-IN-STACK).         
           SUBTRACT +1 FROM DP020-ENTRIES-IN-STACK.                             
      ******************************************************************        
      **  READ A SAVED APPLICATION-SPECIFIC COMMAREA.                 **        
      ******************************************************************        
       6410-READ-DPSAVCMK.                                                      
           EXEC CICS                                                            
               READ                                                             
                   FILE      (FD-SAVED-COMMAREA-FILE)                           
                   INTO      (DP515-SAVED-COMMAREA-RECORD)                      
                   RIDFLD    (DP515-KEY)                                        
                   LENGTH    (PC-MAX-DPSAVCMK-LENGTH)                           
                   RESP      (PV-CICS-RESPONSE)                                 
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               MOVE DP515-APPL-COMMAREA      TO DP020-APPL-COMM-AREA            
           ELSE                                                                 
           IF  PV-CICS-RESPONSE = DFHRESP (NOTFND)                              
               PERFORM 6212-ENQUEUE                                             
               MOVE DP020-NETNAME      TO PV-NETNAME                            
               MOVE DP020-LOCATION-NBR TO PV-LOCATION                           
               MOVE DP020-USERID       TO PV-USERID                             
               EXEC CICS ASSIGN                                                 
                   USERNAME(PV-USERNAME)                                        
               END-EXEC                                                         
               MOVE PV-LOG-TIMEOUT     TO PV-LOG-LINE                           
               PERFORM 6213-WRITEQ-TD                                           
               PERFORM 6214-DEQUEUE                                             
               PERFORM 7100-START-LOGON-TIMEOUT                                 
           ELSE                                                                 
               SET DP013-CICS-ABEND          TO TRUE                            
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '6410-READ-DPSAVCMK'     TO DP013-PARAGRAPH                 
               MOVE 'BAD STATUS RETURNED READING DPSAVCMK'                      
                                             TO DP013-MESSAGE-TEXT (1)          
               MOVE DP515-KEY                 TO PV-DISPLAY-KEY                 
               MOVE SPACES                    TO PV-DISPLAY-FIELD2-HDR          
               MOVE SPACES                    TO PV-DISPLAY-FIELD2              
               MOVE 'RECLEN='                 TO PV-DISPLAY-FIELD3-HDR          
               MOVE PC-MAX-DPSAVCMK-LENGTH    TO PV-UNPACK-NUMERIC              
               MOVE PV-UNPACK-NUMERIC         TO PV-DISPLAY-FIELD3              
               MOVE PV-DISPLAY-LINE-2         TO DP013-MESSAGE-TEXT (2)         
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **- DELETE A STACK ENTRY, BY DELETING ALL TEMPORARY STORAGE     **        
      **  QUEUES CREATED BY THE STACKED TRANSACTION, INITIALIZING     **        
      **  THE STACK AREA FOR THE ENTRY, AND DECREMENTING THE "ENTRIES **        
      **  IN STACK" COUNTER.  THIS IS FOR USE IN DELETING THE ENTIRE  **        
      **  STACK WHEN REQUESTING THE "MAINMENU" COMMAND AND WHEN       **        
      **  RETURNING TO A LOGICALLY STACKED TRANSACTION AFTER SEVERAL  **        
      **  NON-LOGICALLY STACKED TRANSACTIONS HAVE BEEN EXECUTED.      **        
      ******************************************************************        
       6420-DELETE-STACK-ENTRY.                                                 
           IF DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK) > ZERO           
               MOVE EIBTRMID              TO TS-APPL-QUEUE-TERMINAL             
                                             TS-APPL-MULT-QUEUE-TERM            
               MOVE DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK)            
                                          TO TS-APPL-QUEUE-TASK                 
                                             TS-APPL-MULT-QUEUE-TASK            
               MOVE DP020-STACK-TRAN-ID (DP020-ENTRIES-IN-STACK)                
                                          TO PV-DELETEQ-TRAN-ID                 
               PERFORM 6430-DELETE-APPL-TS-QUEUE                                
               IF DP020-STACK-LOCK-HELD (DP020-ENTRIES-IN-STACK)                
                   MOVE DP020-STACK-TASK-NUMBER                                 
                                        (DP020-ENTRIES-IN-STACK)                
                                          TO DP509-TASK-NBR                     
                   PERFORM 6440-DELETE-TASK-RCD                                 
                   SET DP020-STACK-NO-LOCK-HELD                                 
                                    (DP020-ENTRIES-IN-STACK) TO TRUE            
               END-IF                                                           
               MOVE DP020-STACK-TASK-NUMBER (DP020-ENTRIES-IN-STACK)            
                                         TO SM-STACK-KEY-TASK                   
               PERFORM 6450-DELETE-FROM-DPSAVCMK                                
           END-IF.                                                              
                                                                                
           INITIALIZE DP020-STACK-TRANSACTION (DP020-ENTRIES-IN-STACK).         
           SUBTRACT +1 FROM DP020-ENTRIES-IN-STACK.                             
      ******************************************************************        
      **  DELETE THE TEMP STORAGE QUEUE AN APPLICATION TRANSACTION    **        
      **  MAY HAVE BUILT.                                             **        
      ******************************************************************        
       6430-DELETE-APPL-TS-QUEUE.                                               
           EXEC CICS                                                            
               INQUIRE                                                          
                   TRANSACTION  (PV-DELETEQ-TRAN-ID)                            
                   REMOTESYSTEM (PV-SYSID)                                      
           END-EXEC.                                                            
      *                                                                         
           IF PV-SYSID = SPACES                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-QUEUE-NAME)                               
                       SYSID (PV-SYSID)                                         
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
           PERFORM 6431-DELETE-APPL-MULT-TS-QUEUE                               
               VARYING TS-APPL-MULT-QUEUE-SEQ                                   
               FROM 1 BY 1                                                      
               UNTIL TS-APPL-MULT-QUEUE-SEQ > PC-MAX-APPL-QUEUE-SEQ.            
      ******************************************************************        
      **  DELETE THE 'MULTIPLE' TEMP STORAGE QUEUES AN APPLICATION    **        
      **  MAY HAVE BUILT.                                             **        
      ******************************************************************        
       6431-DELETE-APPL-MULT-TS-QUEUE.                                          
           IF PV-SYSID = SPACES                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-MULT-QUEUE-ID)                            
                       NOHANDLE                                                 
               END-EXEC                                                         
           ELSE                                                                 
               EXEC CICS                                                        
                   DELETEQ TS                                                   
                       QUEUE (TS-APPL-MULT-QUEUE-ID)                            
                       SYSID (PV-SYSID)                                         
                       NOHANDLE                                                 
               END-EXEC                                                         
           END-IF.                                                              
      ******************************************************************        
      *  REMOVE ANY OBJECT LOCKS THAT MAY HAVE BEEN SET, BY            *        
      *  DELETING ANY ROWS WITH THAT TASK NUMBER FROM THE LOCK TABLE.  *        
      *  IF THERE IS A DB2 ERROR, THE ABEND MODULE IS CALLED TO PRINT  *        
      *  THE ERROR ON THE LOG.  THE PROGRAM WILL NOT ACTUALLY ABEND.   *        
      ******************************************************************        
                                                                                
       6440-DELETE-TASK-RCD.                                                    
                                                                                
           EXEC CICS                                                            
               DELETE                                                           
                   FILE   (PC-OBJLOK-AIX-TASK-DD)                               
                   RIDFLD (DP509-TASK-NBR)                                      
                   RESP   (PV-CICS-RESPONSE)                                    
           END-EXEC.                                                            
      *                                                                         
      *                                                                         
           IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                               
                           OR DFHRESP(NOTFND)                                   
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND       TO TRUE                               
               SET DP013-LINK-RETURN      TO TRUE                               
               MOVE '6440-DELETE-TASK-RCD'                                      
                                          TO DP013-PARAGRAPH                    
               MOVE 'BAD RESPONSE DELETING FROM OBJ LOCK VSAM'                  
                                          TO DP013-MESSAGE-TEXT (1)             
               MOVE DP509-TASK-NBR        TO PV-PRT-TASK-NBR                    
               MOVE PV-PRT-TASK-NBR       TO DP013-MESSAGE-TEXT (2)             
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  DELETE THE SAVED APPLICATION COMMAREA;                      **        
      **  DELETE A RECORD FROM THE KSDS VSAM FILE.                    **        
      ******************************************************************        
       6450-DELETE-FROM-DPSAVCMK.                                               
           MOVE DP020-NETNAME            TO SM-STACK-KEY-NETNAME.               
           MOVE SM-STACK-KEY             TO DP515-KEY.                          
      *                                                                         
           EXEC CICS                                                            
               DELETE                                                           
                   FILE      (FD-SAVED-COMMAREA-FILE)                           
                   RIDFLD    (DP515-KEY)                                        
                   RESP      (PV-CICS-RESPONSE)                                 
                   KEYLENGTH (PC-STACK-KEY-LENGTH)                              
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
           IF  PV-CICS-RESPONSE = DFHRESP (NOTFND)                              
               PERFORM 6451-LOG-DELETE-NOTFND                                   
           ELSE                                                                 
               SET DP013-CICS-ABEND          TO TRUE                            
               SET DP013-LINK-RETURN         TO TRUE                            
               SET DP013-NO-ROLLBACK         TO TRUE                            
               MOVE '6450-DELETE-FROM-DPSAVCMK' TO DP013-PARAGRAPH              
               MOVE 'BAD STATUS ON DELETE FROM DPSAVCMK'                        
                                             TO DP013-MESSAGE-TEXT (1)          
               MOVE DP515-KEY                TO PV-DISPLAY-KEY                  
               MOVE 'KEYLEN='                TO PV-DISPLAY-FIELD2-HDR           
               MOVE PC-STACK-KEY-LENGTH      TO PV-UNPACK-NUMERIC               
               MOVE PV-UNPACK-NUMERIC        TO PV-DISPLAY-FIELD2               
               MOVE SPACES                   TO PV-DISPLAY-FIELD3-HDR           
               MOVE SPACES                   TO PV-DISPLAY-FIELD3               
               MOVE PV-DISPLAY-LINE-2        TO DP013-MESSAGE-TEXT (2)          
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
                                                                                
      ******************************************************************        
      **  LOG ONE LINE IN THE CSSL LOG WHEN A DPSAVCMK RECORD WAS NOT **        
      **  FOUND DOING A DELETE.                                       **        
      ******************************************************************        
       6451-LOG-DELETE-NOTFND.                                                  
           PERFORM 6212-ENQUEUE.                                                
           MOVE DP515-KEY          TO PV-NF-KEY.                                
 *****     MOVE EIBAID             TO PV-NF-AID.                                
           SET AC-AID-IDX TO 1.                                                 
           SEARCH AC-ATTENTION-ID-ENTRY                                         
             AT END                                                             
               MOVE ALL '?'                  TO PV-NF-AID                       
             WHEN AC-EIBAID (AC-AID-IDX) = EIBAID                               
               MOVE AC-AID-NAME (AC-AID-IDX) TO PV-NF-AID                       
           END-SEARCH.                                                          
           MOVE DP020-NETNAME      TO PV-NF-NETNAME.                            
           MOVE DP020-LOCATION-NBR TO PV-NF-LOCATION.                           
           MOVE DP020-USERID       TO PV-NF-USERID.                             
           EXEC CICS ASSIGN                                                     
               USERNAME(PV-NF-USERNAME)                                         
           END-EXEC.                                                            
           MOVE PV-LOG-NOTFND      TO PV-LOG-LINE.                              
           PERFORM 6213-WRITEQ-TD.                                              
           PERFORM 6214-DEQUEUE.                                                
                                                                                
      *                                                                         
      ******************************************************************        
      **  START THE LOGON PROGRAM TO BUILD THE STANDARD COMMAREA IF   **        
      **  ONE CANNOT BE RETRIEVED OR RETURNED FROM TEMP STORAGE.      **        
      ******************************************************************        
       7000-START-LOGON.                                                        
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID (PC-LOGON-TRANID)                                    
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '7000-START-LOGON'                                          
                                      TO DP013-PARAGRAPH                        
               MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT LINE'        
                                      TO DP013-MESSAGE-TEXT (1)                 
               MOVE PC-LOGON-TRANID   TO DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      *                                                                         
      ******************************************************************        
      **  START THE LOGON PROGRAM TO BUILD THE STANDARD COMMAREA IF   **        
      **  ONE CANNOT BE RETRIEVED OR RETURNED FROM TEMP STORAGE.      **        
      ******************************************************************        
       7100-START-LOGON-TIMEOUT.                                                
           EXEC CICS                                                            
               RETURN IMMEDIATE                                                 
                   TRANSID (PC-LOGON-TRANID)                                    
                   COMMAREA(PC-TIMEOUT-COMMAREA)                                
                   LENGTH  (PC-TIMEOUT-COMMAREA-LENGTH)                         
                   RESP    (PV-CICS-RESPONSE)                                   
           END-EXEC.                                                            
           IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
               CONTINUE                                                         
           ELSE                                                                 
               SET DP013-CICS-ABEND TO TRUE                                     
               SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
               MOVE '7100-START-LOGON-TIMEOUT'                                  
                                      TO DP013-PARAGRAPH                        
               MOVE 'UNABLE TO "START" THE TRANSACTION ON THE NEXT LINE'        
                                      TO DP013-MESSAGE-TEXT (1)                 
               MOVE PC-LOGON-TRANID   TO DP013-MESSAGE-TEXT (2)                 
               PERFORM DP013-0000-PROCESS-ABEND                                 
           END-IF.                                                              
      ******************************************************************        
      **  CALL THE ABEND ROUTINE.                                     **        
      ******************************************************************        
           COPY DPPD013.                                                        
