       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    MLKRP011                                                  
       AUTHOR.        SCOTT JACKSON                                             
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  04-25-2005.                                               
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      * MLKRP011 - CREATE THE PRICE CHANGE BY HIERARCHY SUMMARY RPT FILE        
      ******************************************************************        
      *  THIS PROGRAMS IN A FILE CONTAINING POSITIVE AND NEGATIVE UNITS         
      *  INFORMATION (DMM, GMM, BYR) INFORMATION ATTACHED FOR REPORTING*        
      *                                                                         
      *CHANGE HISTORY:                                                          
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
      * ------- ---------- ----------- --------------------------------         
      * W27813  04-25-2005 S. JACKSON  INITIAL INSTALL                          
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT CONTROL-CARD                                                  
               ASSIGN TO INP01.                                                 
                                                                                
           SELECT PRICE-CHANGE-FILE                                             
               ASSIGN TO INP02.                                                 
                                                                                
           SELECT REPORT-FILE                                                   
               ASSIGN TO RPT01.                                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  CONTROL-CARD                                                         
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS  80 CHARACTERS                                       
           DATA RECORD IS CONTROL-REC.                                          
                                                                                
       01  CONTROL-REC                     PIC X(80).                           
                                                                                
       FD  PRICE-CHANGE-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS  80 CHARACTERS                                       
           DATA RECORD IS PRICE-CHANGE-REC.                                     
                                                                                
       01  PRICE-CHANGE-REC                PIC X(80).                           
                                                                                
       FD  REPORT-FILE                                                          
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           DATA RECORD IS REPORT-REC.                                           
                                                                                
       01  REPORT-REC                     PIC X(132).                           
                                                                                
       EJECT                                                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILLER                          PIC X(40)       VALUE                
           '*** WORKING STORAGE AREA BEGIN ***'.                                
                                                                                
      *****************************************************************         
      *   CONTROL INPUT RECORD                                                  
      *****************************************************************         
       01  CD-CONTROL-DATA.                                                     
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  REPORT-LEVEL                  PIC X(03).                         
               88  CD-DPT-LEVEL                          VALUE 'DPT'.           
               88  CD-GMM-LEVEL                          VALUE 'GMM'.           
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  PRICE-CHANGE-TYPE             PIC X(03).                         
               88  PCT-MARKDOWN                          VALUE '411'.           
               88  PCT-MARKUP                            VALUE '533'.           
               88  PCT-CLEARANCE-TO-ACTIVE               VALUE '422'.           
           05  FILLER                        PIC X(72)   VALUE SPACES.          
                                                                                
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-LINE-MAX                   PIC S9(4)   VALUE +55              
                                                         COMP SYNC.             
           05  PC-PROGRAM-NAME               PIC X(08) VALUE 'MLKRP011'.        
           05  PC-BLANK-LINE                 PIC X(132)  VALUE SPACES.          
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-CC-EOF-SW                  PIC X(1)     VALUE 'N'.            
               88  PS-CC-EOF                              VALUE 'Y'.            
           05  PS-INPUT-EOF-SW               PIC X(1)     VALUE 'N'.            
               88  PS-INPUT-EOF                           VALUE 'Y'.            
           05  PS-FIRST-TIME-SW              PIC X(1)     VALUE 'Y'.            
               88  PS-FIRST-TIME-THRU                     VALUE 'Y'.            
               88  PS-NOT-FIRST-TIME                      VALUE 'N'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PAGE-COUNTER               PIC S9(4)   VALUE ZEROES           
                                                         COMP SYNC.             
           05  PV-LINE-COUNTER               PIC S9(4)   VALUE 99               
                                                         COMP SYNC.             
           05  PV-IN-COUNTER                 PIC S9(4)   VALUE 99               
                                                         COMP SYNC.             
           05  PV-BEG-MONTH-DATE             PIC X(10)   VALUE SPACES.          
           05  PV-END-MONTH-DATE             PIC X(10)   VALUE SPACES.          
           05  PV-PARAGRAPH-NAME             PIC X(30)   VALUE SPACE.           
           05  PV-DB2-OPER-ATTEMPTED         PIC X(30)   VALUE SPACE.           
           05  PV-ABEND-CODE                 PIC S9(04)  COMP                   
                                                         VALUE ZERO.            
           05  PV-MARKDOWN-REPORT            PIC X(19)   VALUE                  
                                                  '      MARKDOWNS    '.        
           05  PV-MARKUP-REPORT              PIC X(19)   VALUE                  
                                                  '       MARKUPS     '.        
           05  PV-CTA-REPORT                 PIC X(19)   VALUE                  
                                                  'CLEARANCE-TO-ACTIVE'.        
           05  PV-HOLD-GMM                   PIC X(02)   VALUE SPACES.          
           05  PV-HOLD-DMM                   PIC X(02)   VALUE SPACES.          
           05  PV-HOLD-DPT                   PIC X(04)   VALUE SPACES.          
                                                                                
           05  PV-LEVEL-TOTAL.                                                  
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  PV-LVL-GMM                PIC X(03)   VALUE SPACES.          
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  PV-LVL-DMM                PIC X(03)   VALUE SPACES.          
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  PV-CORP-LVL               PIC X(04)   VALUE SPACES.          
                                                                                
           05  PV-TEMP-TOTALS.                                                  
               10  PV-TEMP-PRICE-CHG-ONHAND  PIC S9(9)   VALUE ZERO             
                                                         COMP.                  
               10  PV-TEMP-VAR-ONHAND        PIC S9(9)   VALUE ZERO             
                                                         COMP.                  
               10  PV-TEMP-NET-ONHAND        PIC S9(9)   VALUE ZERO             
                                                         COMP.                  
               10  PV-TEMP-PRICE-CHG-DOLLARS PIC S9(12)V99 VALUE 0              
                                                         COMP-3.                
               10  PV-TEMP-VAR-DOLLARS       PIC S9(12)V99 VALUE 0              
                                                         COMP-3.                
               10  PV-TEMP-NET-DOLLARS       PIC S9(12)V99 VALUE 0              
                                                         COMP-3.                
               10  PV-TEMP-VARIANCE-OH-PCT   PIC S9(3)V999999 VALUE 0           
                                                         COMP-3.                
               10  PV-TEMP-VARIANCE-RTL-PCT  PIC S9(3)V999999 VALUE 0           
                                                         COMP-3.                
               10  PV-VARIANCE-PCT           PIC S9(3)V999999 VALUE 0           
                                                         COMP-3.                
       01  DEPT-TOTALS.                                                         
           05  DEPT-NET-TOTALS               PIC S9(09)   VALUE ZEROES          
                                                          COMP-3.               
           05  DEPT-PRICE-CHG-OH             PIC S9(09)   VALUE ZEROES          
                                                          COMP-3.               
           05  DEPT-VARIANCE-TOTALS          PIC S9(09)   VALUE ZEROES          
                                                          COMP-3.               
           05  DEPT-NET-DOLLARS              PIC S9(10)V99 VALUE ZEROES         
                                                           COMP-3.              
           05  DEPT-PRICE-CHG-DOLLARS        PIC S9(10)V99 VALUE ZEROES         
                                                           COMP-3.              
           05  DEPT-VARIANCE-DOLLARS         PIC S9(10)V99 VALUE ZEROES         
                                                           COMP-3.              
                                                                                
       01  DMM-TOTALS.                                                          
           05  DMM-NET-TOTALS               PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  DMM-PRICE-CHG-OH             PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  DMM-VARIANCE-TOTALS          PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  DMM-NET-DOLLARS              PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
           05  DMM-PRICE-CHG-TOTALS         PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
           05  DMM-VARIANCE-DOLLARS         PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
                                                                                
       01  GMM-TOTALS.                                                          
           05  GMM-NET-TOTALS               PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  GMM-PRICE-CHG-OH             PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  GMM-VARIANCE-TOTALS          PIC S9(09)    VALUE ZEROES          
                                                          COMP-3.               
           05  GMM-NET-DOLLARS              PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
           05  GMM-PRICE-CHG-TOTALS         PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
           05  GMM-VARIANCE-DOLLARS         PIC S9(10)V99 VALUE ZEROES          
                                                          COMP-3.               
                                                                                
       01  FINAL-TOTALS.                                                        
           05  FINAL-NET-TOTALS               PIC S9(09)  VALUE ZEROES          
                                                          COMP-3.               
           05  FINAL-PRICE-CHG-OH             PIC S9(09)  VALUE ZEROES          
                                                          COMP-3.               
           05  FINAL-VARIANCE-TOTALS          PIC S9(09)  VALUE ZEROES          
                                                          COMP-3.               
           05  FINAL-NET-DOLLARS              PIC S9(10)V99 VALUE ZEROES        
                                                            COMP-3.             
           05  FINAL-PRICE-CHG-TOTALS         PIC S9(10)V99 VALUE ZEROES        
                                                            COMP-3.             
           05  FINAL-VARIANCE-DOLLARS         PIC S9(10)V99 VALUE ZEROES        
                                                            COMP-3.             
      *                                                                         
      *****************************************************************         
      * KOHL'S STANDARD HEADER                                                  
      *****************************************************************         
           COPY DPWS132O.                                                       
                                                                                
       01  HL1-HEADING-LINE1.                                                   
           05  FILLER                        PIC X(32)   VALUE SPACES.          
           05  FILLER                        PIC X(23)   VALUE                  
                                             '     PERM PRICE CHANGE '.         
           05  FILLER                        PIC X(20)   VALUE                  
                                             'NET VARIANCE REPORT '.            
           05  FILLER                        PIC X(19)   VALUE                  
                                             '(NEGATIVE ON-HANDS)'.             
           05  FILLER                        PIC X(38)   VALUE SPACES.          
                                                                                
       01  HL2-HEADING-LINE2.                                                   
           05  FILLER                        PIC X(37)   VALUE SPACES.          
           05  HL2-REPORT-TYPE               PIC X(19)   VALUE SPACES.          
           05  FILLER                        PIC X(12)   VALUE                  
                                             ' FOR PERIOD '.                    
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  HL2-MONTH-FIRST-DATE          PIC X(10)   VALUE SPACES.          
           05  FILLER                        PIC X(06)   VALUE                  
                                             ' THRU '.                          
           05  HL2-MONTH-LAST-DATE           PIC X(10)   VALUE SPACES.          
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(36)   VALUE SPACES.          
                                                                                
       01  HL3-HEADING-LINE3.                                                   
           05  FILLER                        PIC X(21)   VALUE SPACES.          
           05  FILLER                        PIC X(21)   VALUE                  
                                             '  NET     PRICE CHG  '.           
           05  FILLER                        PIC X(19)   VALUE                  
                                             '|---- VARIANCE ---|'.             
           05  FILLER                        PIC X(14)   VALUE                  
                                             '           NET'.                  
           05  FILLER                        PIC X(24)   VALUE                  
                                             '           PRICE CHG    '.        
           05  FILLER                        PIC X(20)   VALUE                  
                                             '  |------- VARIANCE '.            
           05  FILLER                        PIC X(08)   VALUE                  
                                             '-------|'.                        
           05  FILLER                        PIC X(05)   VALUE SPACES.          
                                                                                
       01  HL4-HEADING-LINE4.                                                   
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  HL4-GMM-PRINTABLE.                                               
               10  HL4-GMM-NBR               PIC X(03)   VALUE 'GMM'.           
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  HL4-DMM-NBR               PIC X(03)   VALUE 'DMM'.           
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  FILLER                        PIC X(04)   VALUE 'DEPT'.          
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  FILLER                        PIC X(18)   VALUE                  
                                             '     ON-HAND      '.              
           05  FILLER                        PIC X(21)   VALUE                  
                                             'ON-HAND    ON-HAND   '.           
           05  FILLER                        PIC X(23)   VALUE                  
                                             'PERCENT         DOLLARS'.         
           05  FILLER                        PIC X(24)   VALUE                  
                                             '           DOLLARS      '.        
           05  FILLER                        PIC X(19)   VALUE                  
                                             '     DOLLARS       '.             
           05  FILLER                        PIC X(07)   VALUE                  
                                             'PERCENT'.                         
           05  FILLER                        PIC X(03)   VALUE SPACES.          
                                                                                
                                                                                
       01  HL5-HEADING-DASH.                                                    
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  HL5-GMM-PRINTABLE.                                               
               10  HL5-GMM-NBR               PIC X(03)   VALUE ALL '-'.         
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  HL5-DMM-NBR               PIC X(03)   VALUE ALL '-'.         
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  HL5-DEPT-NBR                  PIC X(04)   VALUE ALL '-'.         
           05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  FILLER                        PIC X(13)   VALUE                  
                                             '------------ '.                   
           05  FILLER                        PIC X(13)   VALUE                  
                                             '------------ '.                   
           05  FILLER                        PIC X(11)   VALUE                  
                                             '---------- '.                     
           05  FILLER                        PIC X(10)   VALUE                  
                                             '--------  '.                      
           05  FILLER                        PIC X(19)   VALUE                  
                                             '------------------ '.             
           05  FILLER                        PIC X(19)   VALUE                  
                                             '------------------ '.             
           05  FILLER                        PIC X(19)   VALUE                  
                                             '------------------ '.             
           05  FILLER                        PIC X(08)   VALUE                  
                                             '-------'.                         
           05  FILLER                        PIC X(03)   VALUE SPACES.          
                                                                                
       01  DL-DETAIL.                                                           
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  DL-GMM-PRINTABLE.                                                
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  DL-GMM-NBR                PIC X(02)   VALUE SPACES.          
               10  FILLER                    PIC X(02)   VALUE SPACES.          
               10  DL-DMM-NBR                PIC X(02)   VALUE SPACES.          
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  DL-DEPT-NBR                   PIC ZZZ9    VALUE ZEROES.          
           05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  DL-NET-ONHAND                 PIC ----,---,--9.                  
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  DL-PRICE-CHG-ONHAND           PIC ----,---,--9.                  
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  DL-VARIANCE-ONHAND            PIC --,---,--9.                    
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  DL-VARIANCE-OH-PCT            PIC ----.99.                       
           05  FILLER                        PIC X(03)   VALUE '%  '.           
           05  DL-NET-DOLLARS                PIC --,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  DL-PRICE-CHG-DOLLARS          PIC --,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  DL-VARIANCE-DOLLARS           PIC --,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  DL-VARIANCE-RTL-PCT           PIC ----.99.                       
           05  FILLER                        PIC X(01)   VALUE '%'.             
                                                                                
      *SET PRINT-MSG TO THE APPROPRIATE SUB-TOTAL LEVEL (GMM/DMM/TOTAL)         
       01  ST-SUBTOTAL-LINE.                                                    
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  ST-LVL-PRINT-MSG.                                                
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  ST-LVL-BREAK              PIC X(03)   VALUE SPACES.          
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  ST-LVL-NBR                PIC X(02)   VALUE SPACES.          
               10  FILLER                    PIC X(01)   VALUE SPACES.          
               10  FILLER                    PIC X(04)   VALUE 'TOT:'.          
           05  FILLER                        PIC X(03)   VALUE SPACES.          
           05  ST-NET-ONHAND                 PIC ----,---,--9.                  
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  ST-PRICE-CHG-ONHAND           PIC ----,---,--9.                  
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  ST-VARIANCE-ONHAND            PIC --,---,--9.                    
           05  FILLER                        PIC X(01)   VALUE SPACES.          
           05  ST-VARIANCE-OH-PCT            PIC ----.99.                       
           05  FILLER                        PIC X(03)   VALUE '%  '.           
           05  ST-NET-DOLLARS                PIC $-,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  ST-PRICE-CHG-DOLLARS          PIC $-,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  ST-VARIANCE-DOLLARS           PIC $-,---,---,---.99.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
           05  ST-VARIANCE-RTL-PCT           PIC ----.99.                       
           05  FILLER                        PIC X(01)   VALUE '%'.             
           05  FILLER                        PIC X(02)   VALUE SPACES.          
      ****************************************************************          
      * DATE ATTRIBUTE TABLE                                                    
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      * MERCHANDISE LEDGER FISCAL MONTH CONTROL TABLE                           
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE TMLCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
      *   SQL COMMUNICATION AREA                                                
      ****************************************************************          
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
      *****************************************************************         
      *   PRICE CHANGE SUMMARY RECORD LAYOUT                                    
      *****************************************************************         
           COPY MLRD011.                                                        
                                                                                
                                                                                
                                                                                
       01  RUN-TIME.                                                            
           05  R-HR                          PIC 99.                            
           05  R-MIN                         PIC 99.                            
           05  R-SEC                         PIC 99.                            
           05  R-TH                          PIC 99.                            
                                                                                
       01  DATE-IN.                                                             
           05 DATE-IN-YY                     PIC XX.                            
           05 DATE-IN-MM                     PIC XX.                            
           05 DATE-IN-DD                     PIC XX.                            
                                                                                
       EJECT                                                                    
      *                                                                         
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MLKRP011.                                                           
                                                                                
      ****************************************************************          
      *    OPEN ALL FILES.                                                      
      *    INITIALIZE PAGE HEADINGS                                             
      *    PROCESS AND PRINT RECORDS ON PURCHASE JOURNAL FILE                   
      *    PRINT THE VENDOR TOTALS ON CHANGE IN VENDOR NUMBER                   
      *    PRINT THE BATCH TOTALS ON CHANGE IN BATCH NUMBER                     
      *    PRINT THE GRAND TOTALS WHICH INCLUDE ALL BATCH TOTALS                
      *    AT END-OF-JOB.                                                       
      *    CLOSE ALL FILES.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT CONTROL-CARD                                              
                      PRICE-CHANGE-FILE                                         
               OUTPUT REPORT-FILE.                                              
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           IF PS-CC-EOF OR PS-INPUT-EOF                                         
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 2000-PROCESS-PRICE-CHANGES                               
           END-IF.                                                              
                                                                                
           CLOSE CONTROL-CARD                                                   
                 PRICE-CHANGE-FILE                                              
                 REPORT-FILE.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-INITIALIZATION.                                                     
                                                                                
      ****************************************************************          
      *    SET UP THE STANDARD PAGE HEADER.                                     
      *    ACCEPT THE RUN DATE AND RUN TIME FROM THE SYSTEM.                    
      *    PERFORMED BY 0000-MLKRP011.                                          
      ****************************************************************          
           ACCEPT DATE-IN  FROM DATE.                                           
           ACCEPT RUN-TIME FROM TIME.                                           
           MOVE DATE-IN-MM TO DP132O-RUN-MONTH.                                 
           MOVE DATE-IN-DD TO DP132O-RUN-DAY.                                   
           MOVE DATE-IN-YY TO DP132O-RUN-YEAR.                                  
           MOVE R-HR       TO DP132O-RUN-HOUR.                                  
           MOVE R-MIN      TO DP132O-RUN-MINUTE.                                
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
           MOVE 1               TO DP132O-REPORT-NUMBER.                        
                                                                                
           PERFORM 4100-READ-CONTROL-CARD.                                      
           PERFORM 4000-READ-PRICE-CHANGE-FILE.                                 
                                                                                
           PERFORM 5000-GET-OPEN-MONTH-DATES.                                   
           PERFORM 6000-PRINT-HEADINGS.                                         
                                                                                
      ****************************************************************          
      *2000-PROCESS-PRICE-CHANGES.                                              
      *   DETERMINE THE REPORTING LEVEL.                                        
      ****************************************************************          
       2000-PROCESS-PRICE-CHANGES.                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN CD-DPT-LEVEL                                                
                    PERFORM 2100-PROCESS-DEPT-RECORDS                           
                       UNTIL PS-INPUT-EOF                                       
                    IF PV-PAGE-COUNTER > +0                                     
                       PERFORM 6400-PRINT-FINAL-TOTAL                           
                    END-IF                                                      
                                                                                
               WHEN CD-GMM-LEVEL                                                
                    MOVE ML011-GMM-NBR TO PV-HOLD-GMM                           
                    MOVE ML011-DMM-NBR TO PV-HOLD-DMM                           
                    PERFORM 2200-PROCESS-GMM-RECORDS                            
                        UNTIL PS-INPUT-EOF                                      
                    IF PV-PAGE-COUNTER > +0                                     
                       PERFORM 6300-PRINT-SUBTOTAL                              
                       PERFORM 6400-PRINT-FINAL-TOTAL                           
                    END-IF                                                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
      ****************************************************************          
      *2100-PROCESS-DEPT-RECORDS.                                               
      *   REPORT WILL BE AT A DEPARTMENT LEVEL. SUBTOT-N. CORP TOT-Y            
      *   VALUES: 'ML011-POS-ON-HAND-QTY' (PRICE CHANGE TOTALS)                 
      *           'ML011-NEG-ON-HAND-QTY' (VARIANCE AMT)                        
      *            NET TOTALS       = POSITIVE (-) NEGATIVE                     
      *            NET VARIANCE     = POS (-) NEG                               
      *            NET VARIANCE PCT = VARIANCE / NET                            
      ****************************************************************          
       2100-PROCESS-DEPT-RECORDS.                                               
                                                                                
           MOVE SPACES TO DL-GMM-PRINTABLE.                                     
           MOVE ML011-DEPT-NBR           TO  DL-DEPT-NBR.                       
      **   PROCESS UNITS                                                        
           MOVE ML011-POS-ON-HAND-QTY    TO  DL-PRICE-CHG-ONHAND                
                                             PV-TEMP-PRICE-CHG-ONHAND.          
           MOVE ML011-NEG-ON-HAND-QTY    TO  DL-VARIANCE-ONHAND                 
                                             PV-TEMP-VAR-ONHAND.                
           COMPUTE PV-TEMP-NET-ONHAND        = (ML011-POS-ON-HAND-QTY           
                                             +  ML011-NEG-ON-HAND-QTY).         
           MOVE PV-TEMP-NET-ONHAND       TO  DL-NET-ONHAND.                     
                                                                                
      **   PROCESS DOLLARS                                                      
           MOVE ML011-POS-EXTD-RTL-AMT   TO  DL-PRICE-CHG-DOLLARS               
                                             PV-TEMP-PRICE-CHG-DOLLARS.         
           MOVE ML011-NEG-EXTD-RTL-AMT   TO  DL-VARIANCE-DOLLARS                
                                             PV-TEMP-VAR-DOLLARS.               
           COMPUTE PV-TEMP-NET-DOLLARS       = (ML011-POS-EXTD-RTL-AMT          
                                             +  ML011-NEG-EXTD-RTL-AMT)         
           MOVE PV-TEMP-NET-DOLLARS      TO  DL-NET-DOLLARS.                    
                                                                                
      *                                                                         
           PERFORM 2900-CALC-PERCENT.                                           
           MOVE PV-TEMP-VARIANCE-OH-PCT  TO  DL-VARIANCE-OH-PCT.                
           MOVE PV-TEMP-VARIANCE-RTL-PCT TO  DL-VARIANCE-RTL-PCT.               
                                                                                
           PERFORM 6100-PRINT-REPORT-RECORD.                                    
           PERFORM 2400-CALC-FINAL-TOTALS.                                      
           PERFORM 4000-READ-PRICE-CHANGE-FILE.                                 
                                                                                
                                                                                
      ****************************************************************          
      *2200-PROCESS-GMM-RECORDS.                                                
      *   REPORT WILL BE AT A GMM/DMM/DPT LEVEL. SUBTOT-Y. CORP TOT-Y           
      *   VALUES: 'ML011-POS-ON-HAND-QTY' (AUTO.COUNT - NET AMT)                
      *           'ML011-NEG-ON-HAND-QTY' (NEG.AUTO - VARIANCE AMT)             
      *            PRICE CHANGE TOTAL = POS (-) NEG                             
      *            PRICE CHG VARIANCE = (POS (-) TOTAL) / POS                   
      ****************************************************************          
       2200-PROCESS-GMM-RECORDS.                                                
                                                                                
           IF (PV-HOLD-GMM NOT EQUAL ML011-GMM-NBR OR                           
               PV-HOLD-DMM NOT EQUAL ML011-DMM-NBR)                             
               PERFORM 6300-PRINT-SUBTOTAL                                      
           END-IF.                                                              
           MOVE ML011-GMM-NBR            TO  DL-GMM-NBR.                        
           MOVE ML011-DMM-NBR            TO  DL-DMM-NBR.                        
                                                                                
      **   PROCESS UNITS                                                        
           MOVE ML011-DEPT-NBR           TO  DL-DEPT-NBR.                       
           MOVE ML011-POS-ON-HAND-QTY    TO  DL-PRICE-CHG-ONHAND                
                                             PV-TEMP-PRICE-CHG-ONHAND.          
           MOVE ML011-NEG-ON-HAND-QTY    TO  DL-VARIANCE-ONHAND                 
                                             PV-TEMP-VAR-ONHAND.                
           COMPUTE PV-TEMP-NET-ONHAND        = (ML011-POS-ON-HAND-QTY           
                                             +  ML011-NEG-ON-HAND-QTY).         
           MOVE PV-TEMP-NET-ONHAND       TO  DL-NET-ONHAND.                     
                                                                                
      **   PROCESS DOLLARS                                                      
           MOVE ML011-POS-EXTD-RTL-AMT   TO  DL-PRICE-CHG-DOLLARS               
                                             PV-TEMP-PRICE-CHG-DOLLARS.         
           MOVE ML011-NEG-EXTD-RTL-AMT   TO  DL-VARIANCE-DOLLARS                
                                             PV-TEMP-VAR-DOLLARS.               
           COMPUTE PV-TEMP-NET-DOLLARS       = (ML011-POS-EXTD-RTL-AMT          
                                             +  ML011-NEG-EXTD-RTL-AMT).        
           MOVE PV-TEMP-NET-DOLLARS      TO  DL-NET-DOLLARS.                    
                                                                                
      *                                                                         
           PERFORM 2900-CALC-PERCENT.                                           
           MOVE PV-TEMP-VARIANCE-OH-PCT  TO  DL-VARIANCE-OH-PCT.                
           MOVE PV-TEMP-VARIANCE-RTL-PCT TO  DL-VARIANCE-RTL-PCT.               
                                                                                
           PERFORM 6100-PRINT-REPORT-RECORD.                                    
           PERFORM 2300-CALC-SUBTOTALS.                                         
           PERFORM 2400-CALC-FINAL-TOTALS.                                      
                                                                                
           MOVE ML011-GMM-NBR TO PV-HOLD-GMM.                                   
           MOVE ML011-DMM-NBR TO PV-HOLD-DMM.                                   
                                                                                
           PERFORM 4000-READ-PRICE-CHANGE-FILE.                                 
                                                                                
      ****************************************************************          
      *2300-CALC-SUBTOTALS                                                      
      *   FOR GMM / DMM REPORT LEVELS                                           
      ****************************************************************          
       2300-CALC-SUBTOTALS.                                                     
                                                                                
           ADD ML011-POS-ON-HAND-QTY     TO DMM-PRICE-CHG-OH                    
                                            GMM-PRICE-CHG-OH.                   
                                                                                
           ADD ML011-NEG-ON-HAND-QTY     TO DMM-VARIANCE-TOTALS                 
                                            GMM-VARIANCE-TOTALS.                
                                                                                
           ADD ML011-POS-ON-HAND-QTY     TO DMM-NET-TOTALS                      
                                            GMM-NET-TOTALS.                     
                                                                                
           ADD ML011-NEG-ON-HAND-QTY     TO DMM-NET-TOTALS                      
                                            GMM-NET-TOTALS.                     
                                                                                
           ADD ML011-POS-EXTD-RTL-AMT    TO DMM-PRICE-CHG-TOTALS                
                                            GMM-PRICE-CHG-TOTALS.               
                                                                                
           ADD ML011-NEG-EXTD-RTL-AMT    TO DMM-VARIANCE-DOLLARS                
                                            GMM-VARIANCE-DOLLARS.               
                                                                                
           ADD ML011-POS-EXTD-RTL-AMT    TO DMM-NET-DOLLARS                     
                                            GMM-NET-DOLLARS.                    
                                                                                
           ADD ML011-NEG-EXTD-RTL-AMT    TO DMM-NET-DOLLARS                     
                                            GMM-NET-DOLLARS.                    
                                                                                
      ****************************************************************          
      *2400-CALC-FINAL-TOTALS                                                   
      *    FOR CORPORATE TOTALS                                                 
      ****************************************************************          
       2400-CALC-FINAL-TOTALS.                                                  
      *                                                                         
           COMPUTE FINAL-PRICE-CHG-OH     = FINAL-PRICE-CHG-OH                  
                                          + ML011-POS-ON-HAND-QTY.              
                                                                                
           COMPUTE FINAL-VARIANCE-TOTALS  = FINAL-VARIANCE-TOTALS               
                                          + ML011-NEG-ON-HAND-QTY.              
                                                                                
           COMPUTE FINAL-NET-TOTALS       = FINAL-NET-TOTALS                    
                                          + ML011-POS-ON-HAND-QTY               
                                          + ML011-NEG-ON-HAND-QTY.              
                                                                                
           COMPUTE FINAL-PRICE-CHG-TOTALS = FINAL-PRICE-CHG-TOTALS              
                                          + ML011-POS-EXTD-RTL-AMT.             
                                                                                
           COMPUTE FINAL-VARIANCE-DOLLARS = FINAL-VARIANCE-DOLLARS              
                                          + ML011-NEG-EXTD-RTL-AMT.             
                                                                                
           COMPUTE FINAL-NET-DOLLARS      = FINAL-NET-DOLLARS                   
                                          + ML011-POS-EXTD-RTL-AMT              
                                          + ML011-NEG-EXTD-RTL-AMT.             
                                                                                
      ****************************************************************          
      *2500-PREP-DMM-TOTALS                                                     
      *    FOR DMM TOTALS LINE                                                  
      ****************************************************************          
       2500-PREP-DMM-TOTALS.                                                    
           MOVE 'DMM'       TO ST-LVL-BREAK.                                    
           MOVE PV-HOLD-DMM TO ST-LVL-NBR.                                      
                                                                                
           MOVE DMM-NET-TOTALS       TO ST-NET-ONHAND                           
                                        PV-TEMP-NET-ONHAND.                     
           MOVE DMM-VARIANCE-TOTALS  TO ST-VARIANCE-ONHAND                      
                                        PV-TEMP-VAR-ONHAND.                     
           MOVE DMM-PRICE-CHG-OH     TO ST-PRICE-CHG-ONHAND                     
                                        PV-TEMP-PRICE-CHG-ONHAND.               
           MOVE DMM-NET-DOLLARS      TO ST-NET-DOLLARS                          
                                        PV-TEMP-NET-DOLLARS.                    
           MOVE DMM-VARIANCE-DOLLARS TO ST-VARIANCE-DOLLARS                     
                                        PV-TEMP-VAR-DOLLARS.                    
           MOVE DMM-PRICE-CHG-TOTALS TO ST-PRICE-CHG-DOLLARS                    
                                        PV-TEMP-PRICE-CHG-DOLLARS.              
      *                                                                         
           PERFORM 2900-CALC-PERCENT.                                           
           MOVE PV-TEMP-VARIANCE-OH-PCT  TO  ST-VARIANCE-OH-PCT.                
           MOVE PV-TEMP-VARIANCE-RTL-PCT TO  ST-VARIANCE-RTL-PCT.               
                                                                                
           MOVE ZEROES TO DMM-NET-TOTALS                                        
                          DMM-VARIANCE-TOTALS                                   
                          DMM-PRICE-CHG-OH                                      
                          DMM-NET-DOLLARS                                       
                          DMM-VARIANCE-DOLLARS                                  
                          DMM-PRICE-CHG-TOTALS.                                 
                                                                                
      ****************************************************************          
      *2600-PREP-GMM-TOTALS                                                     
      *    FOR DMM TOTALS LINE                                                  
      ****************************************************************          
       2600-PREP-GMM-TOTALS.                                                    
           MOVE 'GMM'       TO ST-LVL-BREAK.                                    
           MOVE PV-HOLD-GMM TO ST-LVL-NBR.                                      
                                                                                
           MOVE GMM-NET-TOTALS       TO ST-NET-ONHAND                           
                                        PV-TEMP-NET-ONHAND                      
           MOVE GMM-VARIANCE-TOTALS  TO ST-VARIANCE-ONHAND                      
                                        PV-TEMP-VAR-ONHAND                      
           MOVE GMM-PRICE-CHG-OH     TO ST-PRICE-CHG-ONHAND                     
                                        PV-TEMP-PRICE-CHG-ONHAND                
           MOVE GMM-NET-DOLLARS      TO ST-NET-DOLLARS                          
                                        PV-TEMP-NET-DOLLARS                     
           MOVE GMM-VARIANCE-DOLLARS TO ST-VARIANCE-DOLLARS                     
                                        PV-TEMP-VAR-DOLLARS                     
           MOVE GMM-PRICE-CHG-TOTALS TO ST-PRICE-CHG-DOLLARS                    
                                        PV-TEMP-PRICE-CHG-DOLLARS               
                                                                                
      *                                                                         
           PERFORM 2900-CALC-PERCENT.                                           
           MOVE PV-TEMP-VARIANCE-OH-PCT  TO  ST-VARIANCE-OH-PCT.                
           MOVE PV-TEMP-VARIANCE-RTL-PCT TO  ST-VARIANCE-RTL-PCT.               
                                                                                
           MOVE ZEROES TO GMM-NET-TOTALS                                        
                          GMM-VARIANCE-TOTALS                                   
                          GMM-PRICE-CHG-OH                                      
                          GMM-NET-DOLLARS                                       
                          GMM-VARIANCE-DOLLARS                                  
                          GMM-PRICE-CHG-TOTALS.                                 
                                                                                
      *****************************************************************         
      *2900-CALC-PERCENT.                                                       
      *                                                                         
      *  CALCULATE UNIT AND DOLLARS VARIANCE PERCENTAGES                        
      *****************************************************************         
       2900-CALC-PERCENT.                                                       
      * CALCULATE THE UNIT VARIANCE                                             
           IF PV-TEMP-NET-ONHAND = 0                                            
               MOVE -100 TO PV-VARIANCE-PCT                                     
           ELSE                                                                 
               IF PV-TEMP-PRICE-CHG-ONHAND = 0                                  
                  MOVE +100 TO PV-VARIANCE-PCT                                  
               ELSE                                                             
                   IF PV-TEMP-VAR-ONHAND = 0                                    
                       MOVE 0 TO PV-VARIANCE-PCT                                
                   ELSE                                                         
                       COMPUTE PV-VARIANCE-PCT =                                
                               ( PV-TEMP-VAR-ONHAND                             
                               / PV-TEMP-NET-ONHAND)                            
                       COMPUTE PV-VARIANCE-PCT = PV-VARIANCE-PCT * 100          
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-VARIANCE-PCT          TO  PV-TEMP-VARIANCE-OH-PCT.           
                                                                                
      **   PROCESS DOLLARS                                                      
           IF PV-TEMP-NET-DOLLARS = 0                                           
               MOVE -100 TO PV-VARIANCE-PCT                                     
           ELSE                                                                 
               IF PV-TEMP-PRICE-CHG-DOLLARS = 0                                 
                   MOVE +100 TO PV-VARIANCE-PCT                                 
               ELSE                                                             
                   IF PV-TEMP-VAR-DOLLARS = 0                                   
                       MOVE 0 TO PV-VARIANCE-PCT                                
                   ELSE                                                         
                       COMPUTE PV-VARIANCE-PCT =                                
                           ( PV-TEMP-VAR-DOLLARS                                
                           / PV-TEMP-NET-DOLLARS)                               
                       COMPUTE PV-VARIANCE-PCT = PV-VARIANCE-PCT * 100          
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           MOVE PV-VARIANCE-PCT          TO  PV-TEMP-VARIANCE-RTL-PCT.          
      *                                                                         
      *****************************************************************         
      *4000-READ-PRICE-CHANGE-FILE.                                             
      *                                                                         
      *  READ THE VALID MARKDOWN RECORDS (THESE ARE THE 'NET' RECORDS)          
      *****************************************************************         
       4000-READ-PRICE-CHANGE-FILE.                                             
                                                                                
           READ PRICE-CHANGE-FILE INTO ML011-PRICE-CHANGE-RECORD                
               AT END                                                           
                   SET PS-INPUT-EOF TO TRUE.                                    
                                                                                
      *****************************************************************         
      *4100-READ-CONTROL-CARD.                                                  
      *                                                                         
      *  READ THE MARKDOWN RECORDS THAT CONTAIN THE NEGATIVE ON-HANDS           
      *****************************************************************         
       4100-READ-CONTROL-CARD.                                                  
                                                                                
           READ CONTROL-CARD INTO CD-CONTROL-DATA                               
               AT END                                                           
                   SET PS-CC-EOF TO TRUE                                        
               NOT AT END                                                       
                   EVALUATE TRUE                                                
                       WHEN PCT-MARKDOWN                                        
                           MOVE PV-MARKDOWN-REPORT   TO HL2-REPORT-TYPE         
                       WHEN PCT-MARKUP                                          
                           MOVE PV-MARKUP-REPORT     TO HL2-REPORT-TYPE         
                       WHEN PCT-CLEARANCE-TO-ACTIVE                             
                           MOVE PV-CTA-REPORT        TO HL2-REPORT-TYPE         
                   END-EVALUATE.                                                
                                                                                
      *****************************************************************         
      *5000-GET-OPEN-MONTH-DATES.                                               
      *                                                                         
      *  READ THE MARKDOWN RECORDS THAT CONTAIN THE NEGATIVE ON-HANDS           
      *****************************************************************         
       5000-GET-OPEN-MONTH-DATES.                                               
                                                                                
            EXEC SQL                                                            
               SELECT KY_DTE                                                    
                     ,FSCL_MN_BEG_DTE                                           
                     ,FSCL_WK_END_DTE                                           
                 INTO :DTATTR-KY-DTE                                            
                     ,:DTATTR-FSCL-MN-BEG-DTE                                   
                     ,:DTATTR-FSCL-WK-END-DTE                                   
                 FROM TMLCNTL                                                   
                     ,TDTATTR                                                   
               WHERE OPN_FSCL_MN_DTE = KY_DTE                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE DTATTR-FSCL-MN-BEG-DTE TO HL2-MONTH-FIRST-DATE         
                    MOVE DTATTR-FSCL-WK-END-DTE TO HL2-MONTH-LAST-DATE          
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' DTATTR-KY-DTE                      
                   MOVE    '5000-GET-OPEN-MONTH-DATES'                          
                                                TO PV-PARAGRAPH-NAME            
                   MOVE    'SELECT ROW FROM TDTATTR'                            
                                                TO PV-DB2-OPER-ATTEMPTED        
                   PERFORM 9000-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ****************************************************************          
      *6000-PRINT-HEADINGS                                                      
      *                                                                         
      ****************************************************************          
       6000-PRINT-HEADINGS.                                                     
                                                                                
           ADD  1               TO PV-PAGE-COUNTER.                             
           MOVE PV-PAGE-COUNTER TO DP132O-PAGE-NUMBER.                          
           MOVE PC-PROGRAM-NAME TO DP132O-PROGRAM-NAME.                         
                                                                                
           WRITE REPORT-REC                                                     
               FROM DP132O-STANDRD-HEADER-132-COLS                              
               AFTER ADVANCING PAGE.                                            
                                                                                
           WRITE REPORT-REC                                                     
               FROM HL1-HEADING-LINE1                                           
               AFTER ADVANCING 1 LINE.                                          
                                                                                
     ***   MOVE PS-INPUT-REPORT-TYPE TO HL2-REPORT-TYPE.                        
                                                                                
           WRITE REPORT-REC                                                     
               FROM HL2-HEADING-LINE2                                           
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           WRITE REPORT-REC                                                     
               FROM HL3-HEADING-LINE3                                           
               AFTER ADVANCING 2 LINE.                                          
                                                                                
           EVALUATE TRUE                                                        
               WHEN CD-DPT-LEVEL                                                
                    MOVE SPACES TO HL4-GMM-NBR                                  
                                   HL5-GMM-NBR                                  
                                   HL4-DMM-NBR                                  
                                   HL5-DMM-NBR                                  
      *             MOVE SPACES TO HL4-GMM-PRINTABLE                            
      *             MOVE SPACES TO HL5-GMM-PRINTABLE                            
               WHEN CD-GMM-LEVEL                                                
                    MOVE ML011-GMM-NBR  TO DL-GMM-NBR                           
                    MOVE ML011-DMM-NBR  TO DL-DMM-NBR                           
                    MOVE ML011-DEPT-NBR TO DL-DEPT-NBR                          
           END-EVALUATE.                                                        
                                                                                
           WRITE REPORT-REC                                                     
               FROM HL4-HEADING-LINE4                                           
               AFTER ADVANCING 1 LINE.                                          
                                                                                
      *    IF REPORT-LEVEL = CD-DPT-LEVEL                                       
                                                                                
           WRITE REPORT-REC                                                     
               FROM HL5-HEADING-DASH                                            
               AFTER ADVANCING 1 LINE.                                          
                                                                                
           MOVE 07 TO PV-LINE-COUNTER.                                          
                                                                                
                                                                                
      ****************************************************************          
      *6100-PRINT-REPORT-RECORD.                                                
      *                                                                         
      ****************************************************************          
       6100-PRINT-REPORT-RECORD.                                                
                                                                                
           IF PV-LINE-COUNTER >= PC-LINE-MAX                                    
              PERFORM 6000-PRINT-HEADINGS                                       
           END-IF.                                                              
           WRITE REPORT-REC                                                     
               FROM DL-DETAIL                                                   
               AFTER ADVANCING 1 LINE.                                          
           ADD 1 TO PV-LINE-COUNTER.                                            
                                                                                
      ****************************************************************          
      *6300-PRINT-SUBTOTAL                                                      
      ****************************************************************          
       6300-PRINT-SUBTOTAL.                                                     
                                                                                
           MOVE SPACES TO HL4-GMM-PRINTABLE                                     
                          HL5-GMM-PRINTABLE                                     
                          HL5-DEPT-NBR.                                         
                                                                                
      *    IF PS-FIRST-TIME-THRU                                                
      *        SET PS-NOT-FIRST-TIME TO TRUE                                    
      *    ELSE                                                                 
               IF PV-HOLD-GMM NOT EQUAL ML011-GMM-NBR                           
                   OR PS-INPUT-EOF                                              
      *        WRITE DMM SUBTOTALS                                              
                   PERFORM 2500-PREP-DMM-TOTALS                                 
                                                                                
                   WRITE REPORT-REC                                             
                       FROM HL5-HEADING-DASH                                    
                           AFTER ADVANCING 1 LINE                               
                   WRITE REPORT-REC                                             
                       FROM ST-SUBTOTAL-LINE                                    
                           AFTER ADVANCING 1 LINE                               
                                                                                
                   WRITE REPORT-REC FROM PC-BLANK-LINE                          
                       AFTER ADVANCING 1 LINE                                   
                                                                                
                   ADD 3 TO PV-LINE-COUNTER                                     
                   MOVE ML011-DMM-NBR TO PV-HOLD-DMM                            
      *                                                                         
      *        WRITE GMM SUBTOTALS                                              
                   PERFORM 2600-PREP-GMM-TOTALS                                 
                                                                                
                   WRITE REPORT-REC                                             
                       FROM HL5-HEADING-DASH                                    
                           AFTER ADVANCING 1 LINE                               
                   WRITE REPORT-REC                                             
                       FROM ST-SUBTOTAL-LINE                                    
                           AFTER ADVANCING 1 LINE                               
                                                                                
                   WRITE REPORT-REC FROM PC-BLANK-LINE                          
                       AFTER ADVANCING 1 LINE                                   
                                                                                
                   ADD 3 TO PV-LINE-COUNTER                                     
                   MOVE ML011-GMM-NBR TO PV-HOLD-GMM                            
               ELSE                                                             
                   IF PV-HOLD-DMM NOT EQUAL ML011-DMM-NBR                       
      *             WRITE DMM SUBTOTALS                                         
                                                                                
                        PERFORM 2500-PREP-DMM-TOTALS                            
                                                                                
                        WRITE REPORT-REC                                        
                            FROM HL5-HEADING-DASH                               
                                AFTER ADVANCING 1 LINE                          
                        WRITE REPORT-REC                                        
                            FROM ST-SUBTOTAL-LINE                               
                                AFTER ADVANCING 1 LINE                          
                                                                                
                        WRITE REPORT-REC FROM PC-BLANK-LINE                     
                            AFTER ADVANCING 1 LINE                              
                                                                                
                        ADD 3 TO PV-LINE-COUNTER                                
                        MOVE ML011-DMM-NBR TO PV-HOLD-DMM                       
                   END-IF                                                       
      *        END-IF                                                           
           END-IF.                                                              
                                                                                
      ****************************************************************          
      *6400-PRINT-FINAL-TOTAL.                                                  
      ****************************************************************          
       6400-PRINT-FINAL-TOTAL.                                                  
           WRITE REPORT-REC                                                     
               FROM HL5-HEADING-DASH                                            
                   AFTER ADVANCING 1 LINE.                                      
                                                                                
           ADD 1 TO PV-LINE-COUNTER.                                            
                                                                                
           MOVE 'COMPANY TOT:'         TO ST-LVL-PRINT-MSG                      
           MOVE FINAL-NET-TOTALS       TO ST-NET-ONHAND                         
                                          PV-TEMP-NET-ONHAND.                   
           MOVE FINAL-PRICE-CHG-OH     TO ST-PRICE-CHG-ONHAND                   
                                          PV-TEMP-PRICE-CHG-ONHAND.             
           MOVE FINAL-VARIANCE-TOTALS  TO ST-VARIANCE-ONHAND                    
                                          PV-TEMP-VAR-ONHAND.                   
           MOVE FINAL-NET-DOLLARS      TO ST-NET-DOLLARS                        
                                          PV-TEMP-NET-DOLLARS.                  
           MOVE FINAL-PRICE-CHG-TOTALS TO ST-PRICE-CHG-DOLLARS                  
                                          PV-TEMP-PRICE-CHG-DOLLARS.            
           MOVE FINAL-VARIANCE-DOLLARS TO ST-VARIANCE-DOLLARS                   
                                          PV-TEMP-VAR-DOLLARS.                  
      *                                                                         
           PERFORM 2900-CALC-PERCENT.                                           
           MOVE PV-TEMP-VARIANCE-OH-PCT  TO  ST-VARIANCE-OH-PCT.                
           MOVE PV-TEMP-VARIANCE-RTL-PCT TO  ST-VARIANCE-RTL-PCT.               
                                                                                
                                                                                
           WRITE REPORT-REC                                                     
               FROM ST-SUBTOTAL-LINE                                            
                   AFTER ADVANCING 1 LINE.                                      
                                                                                
           ADD 1 TO PV-LINE-COUNTER.                                            
                                                                                
      ****************************************************************          
      *9000-SQL-ABEND                                                           
      *                                                                         
      ****************************************************************          
       9000-SQL-ABEND.                                                          
                                                                                
           MOVE +4013 TO PV-ABEND-CODE.                                         
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = MLKRP011'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE                 
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.                
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
                                                                                
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'  USING PV-ABEND-CODE.                                
