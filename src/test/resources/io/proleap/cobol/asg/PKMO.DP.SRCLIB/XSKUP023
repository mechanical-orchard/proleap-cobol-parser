       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    XSKUP023.                                         00020018
       AUTHOR.        KOHLS DEPARTMENT STORES.                          00030000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040000
       DATE-WRITTEN.  09/29/2002.                                       00050014
       DATE-COMPILED.                                                   00060000
      ******************************************************************00070019
      **       VSM ITEM MESSAGE - NUMBER 025 - VPN NUMBER CHANGE        00080019
      ******************************************************************00090019
      ***THIS PROGRAM IS CALLED FROM XSKUT012 TO UPDATE THE FOLLOWING   00100025
      ***     ATTRIBUTES ON XST_VND_STYL:                               00110025
      ***     1. VS_NBR                                                 00120025
      ******************************************************************00130019
      **09/29/2002 CHANGED BY:JULIE SMITH          PROGRAM WRITE        00140019
121402**12/14/2002 CHANGED BY:JULIE SMITH  ADDITIONAL TIMESTAMP         00150024
      ******************************************************************00160019
       ENVIRONMENT DIVISION.                                            00170000
       CONFIGURATION SECTION.                                           00180000
       SOURCE-COMPUTER.  IBM-3090.                                      00190000
       OBJECT-COMPUTER.  IBM-3090.                                      00200000
       DATA DIVISION.                                                   00210000
                                                                        00220005
       WORKING-STORAGE SECTION.                                         00230000
      ******************************************************************00240005
      **PROGRAM SWITCHES                                                00250005
      ******************************************************************00260005
       01  PS-PROGRAM-SWITCHES.                                         00270005
           05  PS-PROCESS-CONTINUE-SW      PIC X(01)  VALUE 'Y'.        00280005
               88  PS-PROCESS-CONTINUE                VALUE 'Y'.        00290005
               88  PS-PROCESS-COMPLETE                VALUE 'N'.        00300005
           05  PS-ACTION-IND               PIC 9(02)  VALUE 00.         00310021
               88  PS-ACTION-REVERSE                  VALUE 01.         00320021
      ***DUE TO A ROW ALREADY EXISTING ON TABLE                         00330021
               88  PS-ACTION-NOTHING                  VALUE 02.         00340021
      ******************************************************************00350000
      **PROGRAM VARIABLES                                               00360000
      ******************************************************************00370000
       01  PC-PROGRAM-COUNTERS.                                         00380007
           05  PC-RETRY-COUNTER            PIC 9(01)  VALUE ZEROS.      00390007
           05  PC-RETRY-MAX                PIC 9(01)  VALUE 5.          00400007
      ******************************************************************00410000
      **PROGRAM VARIABLES                                               00420020
      ******************************************************************00430000
       01  PV-PROGRAM-VARIABLES.                                        00440020
           05  PV-PARAGRAPH-NAME           PIC X(30)  VALUE SPACE.      00450020
      ******************************************************************00460000
      **COPYBOOKS                                                       00470000
      ******************************************************************00480000
      ***VSM ITEM COPYBOOK                                              00490005
           COPY IMRD008.                                                00500026
      ***ABEND COPYBOOK                                                 00510005
           COPY DPWS004.                                                00520026
      ***DB2 MESSAGE AREA                                               00530000
           COPY SQLCA2.                                                 00540026
      ******************************************************************00550000
      **TABLE INCLUSIONS                                                00560000
      ******************************************************************00570000
      ***DB2 COMMUNICATIONS AREA                                        00580000
           EXEC SQL                                                     00590000
                INCLUDE SQLCA                                           00600000
           END-EXEC.                                                    00610000
      ***XST_VND_STYL TABLE                                             00620000
           EXEC SQL                                                     00630000
               INCLUDE XST00013                                         00640000
           END-EXEC.                                                    00650000
                                                                        00660000
       LINKAGE SECTION.                                                 00670000
      ******************************************************************00680000
      **LINKAGE VARIABLES                                               00690000
      ******************************************************************00700000
       01  LV-IMRD008-RECORD               PIC X(450).                  00710005
       01  LV-RETURN-CODE                  PIC S9(04).                  00720005
       01  LV-ACTION-IND                   PIC 9(02).                   00730021
       01  LV-CURRENT-TIMESTAMP            PIC X(26).                   00740021
                                                                        00750000
       PROCEDURE DIVISION USING LV-IMRD008-RECORD,                      00760005
                                LV-RETURN-CODE,                         00770021
                                LV-ACTION-IND,                          00780021
                                LV-CURRENT-TIMESTAMP.                   00790021
      ******************************************************************00800000
       0000-MAINLINE.                                                   00810000
                                                                        00820000
            PERFORM 1000-INITIALIZATION.                                00830000
            PERFORM 2000-PROCESS-MESSAGE                                00840014
                    UNTIL PS-PROCESS-COMPLETE.                          00850005
            PERFORM 9999-WRAPUP.                                        00860000
                                                                        00870000
       EJECT.                                                           00880005
      ***************************************************************** 00890000
      **MOVES ALL COPYBOOK FIELDS TO THE DCLGEN FIELDS                  00900007
      ***************************************************************** 00910000
       1000-INITIALIZATION.                                             00920000
                                                                        00930000
           MOVE '1000-INITIALIZATION'          TO PV-PARAGRAPH-NAME.    00940020
                                                                        00950000
           INITIALIZE PS-PROCESS-CONTINUE-SW.                           00960008
           INITIALIZE PC-RETRY-COUNTER.                                 00970008
                                                                        00980008
           MOVE LV-IMRD008-RECORD             TO IMRD008-RECORD.        00990014
           MOVE IM008-ITEM-KEY-RMS-STYL-ID    TO XST00013-STYL-ID.      01000014
           MOVE IM008-VPN-CHG-VS-NBR-NEW      TO XST00013-VS-NBR.       01010018
121402     MOVE LV-CURRENT-TIMESTAMP          TO XST00013-LAST-UPD-TMST.01020024
121402     MOVE IM008-CRE-TMST            TO XST00013-SOR-LAST-MSG-TMST.01030024
                                                                        01040017
       EJECT.                                                           01050005
      ***************************************************************** 01060000
      **UPDATES THE VPN NUMBER ON XST_VND_STYL                          01070025
      ***************************************************************** 01080000
       2000-PROCESS-MESSAGE.                                            01090014
                                                                        01100000
           MOVE '2000-PROCESS-MESSAGE' TO PV-PARAGRAPH-NAME.            01110020
                                                                        01120005
           EXEC SQL                                                     01130005
              UPDATE XST_VND_STYL                                       01140005
                  SET   VS_NBR        = :XST00013-VS-NBR                01150018
                      , LAST_UPD_TMST = :XST00013-LAST-UPD-TMST         01160016
121402                , SOR_LAST_MSG_TMST = :XST00013-SOR-LAST-MSG-TMST 01170024
                  WHERE STYL_ID       = :XST00013-STYL-ID               01180007
           END-EXEC.                                                    01190005
                                                                        01200005
           EVALUATE TRUE                                                01210005
      ***SUCCESSFUL PROCESSING                                          01220025
              WHEN SQLCODE = ZERO                                       01230005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01240008
      ***ROW/TABLE LOCK                                                 01250025
              WHEN SQLCODE = -911                                       01260005
              WHEN SQLCODE = -913                                       01270007
                   ADD +1                      TO PC-RETRY-COUNTER      01280007
                   IF  PC-RETRY-COUNTER = PC-RETRY-MAX                  01290007
                       SET PS-PROCESS-COMPLETE TO TRUE                  01300007
                   ELSE                                                 01310007
                       ADD +1                  TO PC-RETRY-COUNTER      01320007
                   END-IF                                               01330007
              WHEN OTHER                                                01340005
                   SET PS-PROCESS-COMPLETE     TO TRUE                  01350005
           END-EVALUATE.                                                01360005
                                                                        01370000
       EJECT.                                                           01380005
      ******************************************************************01390000
      *MOVE RETURN CODE TO LINKAGE VARIABLE AND EXIT MODULE             01400000
      ******************************************************************01410000
       9999-WRAPUP.                                                     01420000
                                                                        01430000
           MOVE '9999-WRAPUP'         TO PV-PARAGRAPH-NAME              01440023
                                                                        01450000
           MOVE SQLCODE               TO LV-RETURN-CODE.                01460023
           MOVE PS-ACTION-IND         TO LV-ACTION-IND.                 01470021
                                                                        01480021
           EXIT PROGRAM.                                                01490000
                                                                        01500000
       EJECT.                                                           01510005
