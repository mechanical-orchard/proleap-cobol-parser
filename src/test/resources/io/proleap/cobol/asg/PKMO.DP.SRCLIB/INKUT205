       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    INKUT205                                          00020000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030000
       DATE-WRITTEN.  01-26-2001.                                       00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:  INKUT205 - EXTRACT/FORMAT FOR INVENTORY LEDGER         00080000
      *                                                                 00090000
      * THIS PROGRAM READS FOUR TRANSACTION EXTRACT FILES (PURCHASE,    00100000
      * SALE, PRICE CHANGE, AND ADJUSTMENT), FORMATS THE TRANSACTIONS   00110000
      * INTO THE LAYOUTS USED BY THE INVENTORY LEDGER UPDATE, AND       00120000
      * WRITES THEM TO A COMMON OUTPUT FILE.                            00130000
      *                                                                 00140000
      *CHANGE HISTORY:                                                  00191000
      * WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES           00192000
      * ------- ---------- ----------- -------------------------------- 00193000
RB0401*  02/28/2001 WR# 21591 - RONNA BUTZ (LOOK FOR RB0401 TO IDENTIFY 00200000
RB0401*     CHANGES ARE TO HANDLE STORES THAT ARE DOING MANIFESTING.    00220000
RB0401*     IF A STORE IS DOING MANIFESTING, THEN USE THE STORE'S       00230000
RB0401*     PLANNED INVENTORY DATE INSTEAD OF THE DC CUTOFF DATE.       00240000
      *  03/08/2002 WR# 23511 - CAROL WOJCIECHOWSKI                     00280000
      *     RECOMPILED PROGRAM FOR EXPANDED FIELDS                      00300000
24029 *  10/22/2002 WR# 24029 - ROD JANSSEN                             00301000
24029 *     INVENTORY LEDGER TO INTERFACE WITH ML NI-SKU                00302000
W26600* W26600  04-05-2004 ROD JANSSEN INV LEDGER STORE EXPANSION       00307000
W20445* W20445  09-01-2004 ROD JANSSEN DETAILED INVNENTORY ADJUSTMENT   00308000
W26006* W26006  02-24-2005 ERIN SEARL  REMOVE REFERENCES TO OBSOLETE    00309000
W26006*                                STORE TABLE.                     00310000
W26606* W26606  01-24-2006    - JOHN SELWITSCHKA                        00320000
W26606*                      PRICE CHANGE COPYBOOKS EXPANSION.          00321000
W26606*                      MODIFIED PRICE-CHANGE-FILE LRECL.          00322000
W33304* W33304  10-21-2008    - NIJESH/INFOSYS                                  
W33304*                      HOLDING INVENTORY LEDGER OPEN (HILO).              
207210* CHG0207210 08-22-2018 JIM HUNTER  RENAME KDM2809 TO MLWS050             
207210*                                   RENAME KDU1804 TO DPWS035             
207210*                                   RENAME KDU1805 TO DPPD035             
      ******************************************************************00330000
                                                                        00340000
       ENVIRONMENT DIVISION.                                            00350000
       CONFIGURATION SECTION.                                           00360000
       SOURCE-COMPUTER.        IBM-3090.                                00370000
       OBJECT-COMPUTER.        IBM-3090.                                00380000
       INPUT-OUTPUT SECTION.                                            00390000
       FILE-CONTROL.                                                    00400000
                                                                        00410000
           SELECT PURCHASE-EXTRACT-FILE                                 00420000
               ASSIGN TO INP01.                                         00430000
           SELECT SALE-EXTRACT-FILE                                     00440000
               ASSIGN TO INP02.                                         00450000
           SELECT PRICE-CHANGE-EXTRACT-FILE                             00460000
               ASSIGN TO INP03.                                         00470000
           SELECT ADJUSTMENT-EXTRACT-FILE                               00480000
               ASSIGN TO INP04.                                         00490000
           SELECT CONTROL-CARD-FILE                                     00500000
               ASSIGN TO INP05.                                         00510000
           SELECT ML-FORMATTED-TRANS-FILE                               00520000
               ASSIGN TO OUT01.                                         00530000
                                                                        00540000
       DATA DIVISION.                                                   00550000
       FILE SECTION.                                                    00560000
                                                                        00570000
       FD  PURCHASE-EXTRACT-FILE                                        00580000
           LABEL RECORDS ARE STANDARD                                   00590000
           RECORDING MODE IS F                                          00600000
           BLOCK CONTAINS 0 RECORDS.                                    00610000
       01  PURCHASE-EXTRACT-RECORD     PIC X(150).                      00620000
                                                                        00630000
       FD  SALE-EXTRACT-FILE                                            00640000
           LABEL RECORDS ARE STANDARD                                   00650000
           RECORDING MODE IS F                                          00660000
           BLOCK CONTAINS 0 RECORDS.                                    00670000
       01  SALE-EXTRACT-RECORD         PIC X(80).                       00680000
                                                                        00690000
       FD  PRICE-CHANGE-EXTRACT-FILE                                    00700000
           LABEL RECORDS ARE STANDARD                                   00710000
           RECORDING MODE IS F                                          00720000
           BLOCK CONTAINS 0 RECORDS.                                    00730000
W26606 01  PRICE-CHANGE-EXTRACT-RECORD PIC X(130).                      00740000
                                                                        00750000
       FD  ADJUSTMENT-EXTRACT-FILE                                      00760000
           LABEL RECORDS ARE STANDARD                                   00770000
           RECORDING MODE IS F                                          00780000
           BLOCK CONTAINS 0 RECORDS.                                    00790000
       01  ADJUSTMENT-EXTRACT-RECORD PIC X(90).                         00800000
                                                                        00810000
       FD  CONTROL-CARD-FILE                                            00820000
           LABEL RECORDS ARE STANDARD                                   00830000
           RECORDING MODE IS F                                          00840000
           BLOCK CONTAINS 0 RECORDS.                                    00850000
       01  CONTROL-CARD-RECORD    PIC X(80).                            00860000
                                                                        00870000
       FD  ML-FORMATTED-TRANS-FILE                                      00880000
           LABEL RECORDS ARE STANDARD                                   00890000
           RECORDING MODE IS F                                          00900000
           BLOCK CONTAINS 0 RECORDS.                                    00910000
23511  01  ML-FORMATTED-TRANS-RECORD PIC X(125).                        00920000
                                                                        00930000
      /*****************************************************************00940000
       WORKING-STORAGE SECTION.                                         00950000
      ******************************************************************00960000
                                                                        00970000
       01  PC-PROGRAM-CONSTANTS.                                        00980000
           05  PC-CALENDAR-ROUTINE          PIC  X(08) VALUE 'DPKUT500'.00990000
                                                                        01000000
       01  PS-PROGRAM-SWITCHES.                                         01010000
           05  PS-EOF-PURCHASE-EXTRACT-SW   PIC  X        VALUE 'N'.    01020000
               88  PS-EOF-PURCHASE-EXTRACT                VALUE 'Y'.    01030000
           05  PS-EOF-SALE-EXTRACT-SW       PIC  X        VALUE 'N'.    01040000
               88  PS-EOF-SALE-EXTRACT                    VALUE 'Y'.    01050000
           05  PS-EOF-PRICE-CHANGE-EXTRACT-SW PIC X       VALUE 'N'.    01060000
               88  PS-EOF-PRICE-CHANGE-EXTRACT            VALUE 'Y'.    01070000
           05  PS-EOF-ADJUSTMENT-EXTRACT-SW PIC  X        VALUE 'N'.    01080000
               88  PS-EOF-ADJUSTMENT-EXTRACT              VALUE 'Y'.    01090000
           05  PS-PROCESS-TRANS-SW          PIC  X        VALUE 'N'.    01100000
               88  PROCESS-THE-TRANS                      VALUE 'Y'.    01110000
           05  PS-CONTROL-CARD-EOF-SW PIC  X              VALUE 'N'.    01120000
               88  PS-END-OF-CONTROL-CARD-FILE            VALUE 'Y'.    01130000
                                                                        01140000
       01  PV-PROGRAM-VARIABLES.                                        01150000
W26600     05  PV-STORE-NUMBER        PIC X(05)  VALUE SPACES.          01160000
W26600     05  PV-STORE-NUMBER-NUM REDEFINES                            01161000
W26600         PV-STORE-NUMBER        PIC 9(05).                        01162000
           05  PV-DEFAULT-DTE         PIC X(10)  VALUE '9999-09-09'.    01170000
W26600*    05  PV-5-DIGIT-STORE.                                        01180000
W26600*        10  FILLER                   PIC  X        VALUE '0'.    01190000
W26600*        10  PV-5-DIGIT-STORE-2-4     PIC  X(3).                  01200000
W26600*        10  FILLER                   PIC  X.                     01210000
           05  PV-ACCT-NO.                                              01220000
W26600*        10  FILLER                   PIC  X(2)     VALUE '00'.   01230000
W26600         10  PV-ACCT-NO-STORE-NBR     PIC  X(5).                  01240000
           05  PV-ISO-FMT-DATE.                                         01250000
               10  PV-ISO-FMT-CC            PIC  9(02).                 01260000
               10  PV-ISO-FMT-YY            PIC  9(02).                 01270000
               10  FILLER                   PIC  X.                     01280000
               10  PV-ISO-FMT-MM            PIC  9(02).                 01290000
               10  FILLER                   PIC  X.                     01300000
               10  PV-ISO-FMT-DD            PIC  9(02).                 01310000
           05  PV-FORMATTED-DATE.                                       01320000
               10  PV-FMT-DTE-CCYY          PIC X(04).                  01330000
               10  FILLER                   PIC X(01).                  01340000
               10  PV-FMT-DTE-MM            PIC X(02).                  01350000
               10  FILLER                   PIC X(01).                  01360000
               10  PV-FMT-DTE-DD            PIC X(02).                  01370000
           05  PV-UNFMT-DATE.                                           01380000
               10  PV-UNFMT-DTE-CCYY        PIC X(04).                  01390000
               10  PV-UNFMT-DTE-MM          PIC X(02).                  01400000
               10  PV-UNFMT-DTE-DD          PIC X(02).                  01410000
                                                                        01420000
       01  RC-RECORD-COUNTS.                                            01430000
           05  RC-XT-EXTRACT-COUNTS.                                    01440000
               10  RC-XT-PURCHASE-EXTRACT-CNT     PIC S9(7) COMP-3      01450000
                                                            VALUE ZERO. 01460000
               10  RC-XT-SALE-EXTRACT-CNT         PIC S9(7) COMP-3      01470000
                                                            VALUE ZERO. 01480000
               10  RC-XT-PRICE-CHANGE-EXTRACT-CNT PIC S9(7) COMP-3      01490000
                                                            VALUE ZERO. 01500000
               10  RC-XT-ADJUSTMENT-EXTRACT-CNT   PIC S9(7) COMP-3      01510000
                                                            VALUE ZERO. 01520000
           05  RC-ML-FORMATTED-TRANS-COUNTS.                            01530000
               10  RC-ML-PURCHASE-TRANS-CNT       PIC S9(7) COMP-3      01540000
                                                            VALUE ZERO. 01550000
               10  RC-ML-TRANSFER-FROM-TRANS-CNT  PIC S9(7) COMP-3      01560000
                                                            VALUE ZERO. 01570000
               10  RC-ML-TRANSFER-TO-TRANS-CNT    PIC S9(7) COMP-3      01580000
                                                            VALUE ZERO. 01590000
               10  RC-ML-SALE-CNT                 PIC S9(7) COMP-3      01600000
                                                            VALUE ZERO. 01610000
               10  RC-ML-MARKDOWN-CNT             PIC S9(7) COMP-3      01620000
                                                            VALUE ZERO. 01630000
               10  RC-ML-MARKUP-CNT               PIC S9(7) COMP-3      01640000
                                                            VALUE ZERO. 01650000
               10  RC-ML-ADJUSTMENT-CNT           PIC S9(7) COMP-3      01660000
                                                            VALUE ZERO. 01670000
                                                                        01680000
           05  RC-BP-FORMATTED-TRANS-COUNTS.                            01690000
               10  RC-BP-PURCHASE-TRANS-CNT       PIC S9(7) COMP-3      01700000
                                                            VALUE ZERO. 01710000
               10  RC-BP-TRANSFER-FROM-TRANS-CNT  PIC S9(7) COMP-3      01720000
                                                            VALUE ZERO. 01730000
               10  RC-BP-TRANSFER-TO-TRANS-CNT    PIC S9(7) COMP-3      01740000
                                                            VALUE ZERO. 01750000
               10  RC-BP-SALE-CNT                 PIC S9(7) COMP-3      01760000
                                                            VALUE ZERO. 01770000
               10  RC-BP-MARKDOWN-CNT             PIC S9(7) COMP-3      01780000
                                                            VALUE ZERO. 01790000
               10  RC-BP-MARKUP-CNT               PIC S9(7) COMP-3      01800000
                                                            VALUE ZERO. 01810000
               10  RC-BP-ADJUSTMENT-CNT           PIC S9(7) COMP-3      01820000
                                                            VALUE ZERO. 01830000
                                                                        01840000
       01  DF-DISPLAY-FIELDS.                                           01850000
           05  DF-XT-COUNTS-TITLE-1         PIC  X(32)    VALUE         01860000
                        'NUMBER OF EXTRACT RECORDS READ'.               01870000
           05  DF-XT-COUNTS-TITLE-2         PIC  X(32)    VALUE ALL '-'.01880000
           05  DF-XT-COUNT-DISPLAY.                                     01890000
               10  FILLER                   PIC  X(4)     VALUE SPACE.  01900000
               10  DF-XT-TRANS-TYPE         PIC  X(20).                 01910000
               10  DF-XT-RECORD-CNT         PIC  Z(6)9.                 01920000
           05  DF-ML-COUNTS-TITLE-1         PIC  X(43)    VALUE         01930000
                        'NUMBER OF FORMATTED ML TRANSACTIONS WRITTEN'.  01940000
           05  DF-ML-COUNTS-TITLE-2         PIC  X(43)    VALUE ALL '-'.01950000
           05  DF-ML-TRANS-HEADING.                                     01960000
               10  FILLER                   PIC  X(4)     VALUE SPACE.  01970000
               10  DF-ML-TRANS-TYPE-HEAD    PIC  X(18).                 01980000
           05  DF-ML-COUNT-DISPLAY.                                     01990000
               10  FILLER                   PIC  X(6)     VALUE SPACE.  02000000
               10  DF-ML-TRANS-TYPE         PIC  X(18).                 02010000
               10  DF-ML-RECORD-CNT         PIC  Z(6)9.                 02020000
                                                                        02030000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.02040000
                                                                        02050000
      ******************************************************************02060000
      * INPUT CONTROL CARD LAYOUT.                                      02070000
      ******************************************************************02080000
       01  CC-CONTROL-CARD.                                             02090000
           05  CC-CALENDAR-TYPE        PIC  X(02)    VALUE SPACE.       02100000
           05  FILLER                  PIC  X        VALUE SPACE.       02110000
           05  CC-ISO-FMT-DATE.                                         02120000
               10  CC-ISO-FMT-CC       PIC  X(02)    VALUE SPACE.       02130000
               10  CC-ISO-FMT-YY       PIC  X(02)    VALUE SPACE.       02140000
               10  CC-ISO-FMT-DASH1    PIC  X        VALUE SPACE.       02150000
               10  CC-ISO-FMT-MM       PIC  X(02)    VALUE SPACE.       02160000
               10  CC-ISO-FMT-DASH2    PIC  X        VALUE SPACE.       02170000
               10  CC-ISO-FMT-DD       PIC  X(02)    VALUE SPACE.       02180000
           05  FILLER                  PIC  X(67)    VALUE SPACE.       02190000
                                                                        02200000
      ******************************************************************02210000
      * OUTPUT CONTROL RECORD LAYOUT. THE CONTROL RECORD IS THE FIRST   02220000
      * RECORD WRITTEN TO THE FORMATTED TRANSACTION FILE.               02230000
      ******************************************************************02240000
       01  OC-OUTPUT-CONTROL-RECORD.                                    02250000
           05  FILLER                  PIC  X(02)    VALUE SPACE.       02260000
           05  FILLER                  PIC  X(18)    VALUE SPACE.       02270000
           05  FILLER                  PIC  X(08)    VALUE 'DSAIC40U'.  02280000
           05  FILLER                  PIC  X(01)    VALUE SPACE.       02290000
           05  OC-WEEK-END-DATE        PIC  X(06)    VALUE SPACE.       02300000
           05  FILLER                  PIC  X(01)    VALUE SPACE.       02310000
           05  OC-NEW-MONTH            PIC  X(01)    VALUE SPACE.       02320000
           05  FILLER                  PIC  X(01)    VALUE SPACE.       02330000
           05  FILLER                  PIC  X(01)    VALUE SPACE.       02340000
           05  FILLER                  PIC  X(61)    VALUE SPACE.       02350000
                                                                        02360000
      ***************************************************************** 02370000
      * STORE CHECK DIGIT CALCULATION WORK AREA                         02380000
      ***************************************************************** 02390000
                                                                        02400000
207210     COPY DPWS035.                                                02410000
                                                                        02420000
      ******************************************************************02430000
      * CALENDAR ROUTINE WORK AREA.                                     02440000
      ******************************************************************02450000
                                                                        02460000
           COPY DPWS500.                                                02470000
                                                                        02480000
      ***************************************************************** 02490000
      * PURCHASE EXTRACT RECORD LAYOUT                                  02500000
      ***************************************************************** 02510000
                                                                        02520000
           COPY MLRD021.                                                02530000
                                                                        02540000
      ***************************************************************** 02550000
      * SALE EXTRACT RECORD LAYOUT                                      02560000
      ***************************************************************** 02570000
                                                                        02580000
           COPY MLRD022.                                                02590000
                                                                        02600000
      ***************************************************************** 02610000
      * PRICE CHANGE EXTRACT RECORD LAYOUT                              02620000
      ***************************************************************** 02630000
                                                                        02640000
W26606     COPY MLRD023.                                                02650000
                                                                        02660000
      ***************************************************************** 02670000
      * ADJUSTMENT EXTRACT RECORD LAYOUT                                02680000
      ***************************************************************** 02690000
                                                                        02700000
           COPY MLRD024.                                                02710000
                                                                        02720000
      ***************************************************************** 02730000
      * MERCHANDISE LEDGER FORMATTED TRANSACTION LAYOUT                 02740000
      ***************************************************************** 02750000
                                                                        02760000
207210     COPY MLWS050.                                                02770000
                                                                        02780000
      ******************************************************************02790000
      * DB2 ERROR HANDLING ARGUMENTS....                                02800000
      ******************************************************************02810000
           EXEC SQL                                                     02820000
               INCLUDE SQLCA                                            02830000
           END-EXEC                                                     02840000
                                                                        02850000
      ***************************************************************** 02940000
      * KOHLS INVENTORY PARAMETERS TABLE SQL DEFINITION.                02950000
      ***************************************************************** 02960000
                                                                        02970000
           EXEC SQL                                                     02980000
               INCLUDE TINVPAR                                          02990000
           END-EXEC                                                     03000000
                                                                        03010000
W33304*****************************************************************         
W33304* KOHLS INVENTORY CONTROLS TABLE SQL DEFINITION.                          
W33304*****************************************************************         
W33304                                                                          
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC                                                             
W33304                                                                          
      ******************************************************************03020000
      * SQL ERROR HANDLING ARGUMENTS.......                             03030000
      ******************************************************************03040000
           COPY SQLCA2.                                                 03050000
                                                                        03060000
      /**************************************************************** 03070000
      ***************************************************************** 03080000
       PROCEDURE DIVISION.                                              03090000
      ***************************************************************** 03100000
                                                                        03110000
       A100-MAIN-MODULE.                                                03120000
                                                                        03130000
           OPEN INPUT  CONTROL-CARD-FILE                                03140000
                OUTPUT ML-FORMATTED-TRANS-FILE.                         03150000
                                                                        03160000
           PERFORM B050-PROCESS-CONTROL-CARD.                           03170000
                                                                        03180000
           DISPLAY DF-XT-COUNTS-TITLE-1.                                03190000
           DISPLAY DF-XT-COUNTS-TITLE-2.                                03200000
                                                                        03210000
      *    *------------------------------------------------------------03220000
      *    * PROCESS THE PURCHASE EXTRACT FILE AND DISPLAY THE          03230000
      *    * NUMBER OF PURCHASE EXTRACT RECORDS READ.                   03240000
      *    *------------------------------------------------------------03250000
           OPEN INPUT PURCHASE-EXTRACT-FILE.                            03260000
           PERFORM Z100-READ-PURCHASE-EXTRACT.                          03270000
           PERFORM B100-FORMAT-PURCHASE-TRANS                           03280000
               UNTIL PS-EOF-PURCHASE-EXTRACT.                           03290000
           CLOSE PURCHASE-EXTRACT-FILE.                                 03300000
           MOVE 'PURCHASE:'                TO DF-XT-TRANS-TYPE.         03310000
           MOVE RC-XT-PURCHASE-EXTRACT-CNT TO DF-XT-RECORD-CNT.         03320000
           DISPLAY DF-XT-COUNT-DISPLAY.                                 03330000
                                                                        03340000
      *    *------------------------------------------------------------03350000
      *    * PROCESS THE SALE EXTRACT FILE AND DISPLAY THE              03360000
      *    * NUMBER OF SALE EXTRACT RECORDS READ.                       03370000
      *    *------------------------------------------------------------03380000
           OPEN INPUT SALE-EXTRACT-FILE.                                03390000
           PERFORM Z200-READ-SALE-EXTRACT.                              03400000
           PERFORM B200-FORMAT-SALE-TRANS                               03410000
               UNTIL PS-EOF-SALE-EXTRACT.                               03420000
           CLOSE SALE-EXTRACT-FILE.                                     03430000
           MOVE 'SALE:'                TO DF-XT-TRANS-TYPE.             03440000
           MOVE RC-XT-SALE-EXTRACT-CNT TO DF-XT-RECORD-CNT.             03450000
           DISPLAY DF-XT-COUNT-DISPLAY.                                 03460000
                                                                        03470000
      *    *------------------------------------------------------------03480000
      *    * PROCESS THE PRICE CHANGE EXTRACT FILE AND DISPLAY THE      03490000
      *    * NUMBER OF PRICE CHANGE EXTRACT RECORDS READ.               03500000
      *    *------------------------------------------------------------03510000
           OPEN INPUT PRICE-CHANGE-EXTRACT-FILE.                        03520000
           PERFORM Z300-READ-PRICE-CHANGE-EXTRACT.                      03530000
           PERFORM B300-FORMAT-PRICE-CHANGE-TRANS                       03540000
               UNTIL PS-EOF-PRICE-CHANGE-EXTRACT.                       03550000
           CLOSE PRICE-CHANGE-EXTRACT-FILE.                             03560000
           MOVE 'PRICE CHANGE:'                TO DF-XT-TRANS-TYPE.     03570000
           MOVE RC-XT-PRICE-CHANGE-EXTRACT-CNT TO DF-XT-RECORD-CNT.     03580000
           DISPLAY DF-XT-COUNT-DISPLAY.                                 03590000
                                                                        03600000
      *    *------------------------------------------------------------03610000
      *    * PROCESS THE ADJUSTMENT EXTRACT FILE AND DISPLAY THE        03620000
      *    * NUMBER OF ADJUSTMENT EXTRACT RECORDS READ.                 03630000
      *    *------------------------------------------------------------03640000
           OPEN INPUT ADJUSTMENT-EXTRACT-FILE.                          03650000
           PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                        03660000
           PERFORM B400-FORMAT-ADJUSTMENT-TRANS                         03670000
               UNTIL PS-EOF-ADJUSTMENT-EXTRACT.                         03680000
           CLOSE ADJUSTMENT-EXTRACT-FILE.                               03690000
           MOVE 'ADJUSTMENT:'                TO DF-XT-TRANS-TYPE.       03700000
           MOVE RC-XT-ADJUSTMENT-EXTRACT-CNT TO DF-XT-RECORD-CNT.       03710000
           DISPLAY DF-XT-COUNT-DISPLAY.                                 03720000
           DISPLAY ' '.                                                 03730000
                                                                        03740000
           CLOSE CONTROL-CARD-FILE                                      03750000
                 ML-FORMATTED-TRANS-FILE.                               03760000
                                                                        03770000
           DISPLAY DF-ML-COUNTS-TITLE-1.                                03780000
           DISPLAY DF-ML-COUNTS-TITLE-2.                                03790000
      *    *------------------------------------------------------------03800000
      *    * DISPLAY THE NUMBER OF PURCHASE TRANSACTIONS WRITTEN.       03810000
      *    *------------------------------------------------------------03820000
           MOVE 'PURCHASE'                    TO DF-ML-TRANS-TYPE-HEAD. 03830000
           DISPLAY DF-ML-TRANS-HEADING.                                 03840000
           MOVE 'PURCHASE:'                   TO DF-ML-TRANS-TYPE.      03850000
           MOVE RC-ML-PURCHASE-TRANS-CNT      TO DF-ML-RECORD-CNT.      03860000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 03870000
           MOVE 'TRANSFER FROM:'              TO DF-ML-TRANS-TYPE.      03880000
           MOVE RC-ML-TRANSFER-FROM-TRANS-CNT TO DF-ML-RECORD-CNT.      03890000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 03900000
           MOVE 'TRANSFER TO:'                TO DF-ML-TRANS-TYPE.      03910000
           MOVE RC-ML-TRANSFER-TO-TRANS-CNT   TO DF-ML-RECORD-CNT.      03920000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 03930000
           DISPLAY ' '.                                                 03940000
      *    *------------------------------------------------------------03950000
      *    * DISPLAY THE NUMBER OF SALE TRANSACTIONS WRITTEN.           03960000
      *    *------------------------------------------------------------03970000
           MOVE 'SALE'                        TO DF-ML-TRANS-TYPE-HEAD. 03980000
           DISPLAY DF-ML-TRANS-HEADING.                                 03990000
           MOVE 'SALE:'                       TO DF-ML-TRANS-TYPE.      04000000
           MOVE RC-ML-SALE-CNT                TO DF-ML-RECORD-CNT.      04010000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04020000
           DISPLAY ' '.                                                 04030000
      *    *------------------------------------------------------------04040000
      *    * DISPLAY THE NUMBER OF PRICE CHANGE TRANSACTIONS WRITTEN.   04050000
      *    *------------------------------------------------------------04060000
           MOVE 'PRICE CHANGE'                TO DF-ML-TRANS-TYPE-HEAD. 04070000
           DISPLAY DF-ML-TRANS-HEADING.                                 04080000
           MOVE 'MARKDOWN:'                   TO DF-ML-TRANS-TYPE.      04090000
           MOVE RC-ML-MARKDOWN-CNT            TO DF-ML-RECORD-CNT.      04100000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04110000
           MOVE 'MARKUP:'                     TO DF-ML-TRANS-TYPE.      04120000
           MOVE RC-ML-MARKUP-CNT              TO DF-ML-RECORD-CNT.      04130000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04140000
           DISPLAY ' '.                                                 04150000
      *    *------------------------------------------------------------04160000
      *    * DISPLAY THE NUMBER OF ADJUSTMENT TRANSACTIONS WRITTEN.     04170000
      *    *------------------------------------------------------------04180000
           MOVE 'ADJUSTMENT'                  TO DF-ML-TRANS-TYPE-HEAD. 04190000
           DISPLAY DF-ML-TRANS-HEADING.                                 04200000
           MOVE 'ADJUSTMENT:'                 TO DF-ML-TRANS-TYPE.      04210000
           MOVE RC-ML-ADJUSTMENT-CNT          TO DF-ML-RECORD-CNT.      04220000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04230000
                                                                        04240000
      ****** BYPASSED TRANSACTION SUMMARY...................            04250000
                                                                        04260000
           DISPLAY 'NUMBER OF BYPASSED INV TRANSACTIONS'.               04270000
           DISPLAY DF-ML-COUNTS-TITLE-2.                                04280000
      *    *------------------------------------------------------------04290000
      *    * DISPLAY THE NUMBER OF PURCHASE TRANSACTIONS BYPASSED.      04300000
      *    *------------------------------------------------------------04310000
           MOVE 'PURCHASE'                    TO DF-ML-TRANS-TYPE-HEAD. 04320000
           DISPLAY DF-ML-TRANS-HEADING.                                 04330000
           MOVE 'PURCHASE:'                   TO DF-ML-TRANS-TYPE.      04340000
           MOVE RC-BP-PURCHASE-TRANS-CNT      TO DF-ML-RECORD-CNT.      04350000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04360000
           MOVE 'TRANSFER FROM:'              TO DF-ML-TRANS-TYPE.      04370000
           MOVE RC-BP-TRANSFER-FROM-TRANS-CNT TO DF-ML-RECORD-CNT.      04380000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04390000
           MOVE 'TRANSFER TO:'                TO DF-ML-TRANS-TYPE.      04400000
           MOVE RC-BP-TRANSFER-TO-TRANS-CNT   TO DF-ML-RECORD-CNT.      04410000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04420000
           DISPLAY ' '.                                                 04430000
      *    *------------------------------------------------------------04440000
      *    * DISPLAY THE NUMBER OF SALE TRANSACTIONS BYPASSED.          04450000
      *    *------------------------------------------------------------04460000
           MOVE 'SALE'                        TO DF-ML-TRANS-TYPE-HEAD. 04470000
           DISPLAY DF-ML-TRANS-HEADING.                                 04480000
           MOVE 'SALE:'                       TO DF-ML-TRANS-TYPE.      04490000
           MOVE RC-BP-SALE-CNT                TO DF-ML-RECORD-CNT.      04500000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04510000
           DISPLAY ' '.                                                 04520000
      *    *------------------------------------------------------------04530000
      *    * DISPLAY THE NUMBER OF PRICE CHANGE TRANSACTIONS BYPASSED.  04540000
      *    *------------------------------------------------------------04550000
           MOVE 'PRICE CHANGE'                TO DF-ML-TRANS-TYPE-HEAD. 04560000
           DISPLAY DF-ML-TRANS-HEADING.                                 04570000
           MOVE 'MARKDOWN:'                   TO DF-ML-TRANS-TYPE.      04580000
           MOVE RC-BP-MARKDOWN-CNT            TO DF-ML-RECORD-CNT.      04590000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04600000
           MOVE 'MARKUP:'                     TO DF-ML-TRANS-TYPE.      04610000
           MOVE RC-BP-MARKUP-CNT              TO DF-ML-RECORD-CNT.      04620000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04630000
           DISPLAY ' '.                                                 04640000
      *    *------------------------------------------------------------04650000
      *    * DISPLAY THE NUMBER OF ADJUSTMENT TRANSACTIONS BYPASSED.    04660000
      *    *------------------------------------------------------------04670000
           MOVE 'ADJUSTMENT'                  TO DF-ML-TRANS-TYPE-HEAD. 04680000
           DISPLAY DF-ML-TRANS-HEADING.                                 04690000
           MOVE 'ADJUSTMENT:'                 TO DF-ML-TRANS-TYPE.      04700000
           MOVE RC-BP-ADJUSTMENT-CNT          TO DF-ML-RECORD-CNT.      04710000
           DISPLAY DF-ML-COUNT-DISPLAY.                                 04720000
                                                                        04730000
           STOP RUN.                                                    04740000
                                                                        04750000
                                                                        04760000
      ******************************************************************04770000
      * ACCEPT THE INPUT CONTROL CARD, VALIDATE ITS DATE, AND USE IT TO 04780000
      * BUILD AND WRITE THE CONTROL RECORD FOR THE ML FORMATTED         04790000
      * TRANSACTION FILE.                                               04800000
      ******************************************************************04810000
       B050-PROCESS-CONTROL-CARD.                                       04820000
                                                                        04830000
           PERFORM Z700-READ-CONTROL-CARD-FILE.                         04840000
           DISPLAY 'CONTROL CARD: ' CC-CONTROL-CARD.                    04850000
                                                                        04860000
           IF CC-CONTROL-CARD = SPACE    OR                             04870000
              PS-END-OF-CONTROL-CARD-FILE                               04880000
               DISPLAY '** ABEND **'                                    04890000
               DISPLAY '** PGM = INKUT205'                              04900000
               DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'         04910000
               MOVE +4003 TO ABEND-CODE                                 04920000
               CALL 'ILBOABN0' USING ABEND-CODE                         04930000
           END-IF.                                                      04940000
                                                                        04950000
           DISPLAY '** INKUT205 INPUT CONTROL CARD =' CC-CONTROL-CARD.  04960000
                                                                        04970000
           INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.              04980000
           ACCEPT DPG51-SYSTEM-DATE FROM DATE.                          04990000
           MOVE CC-CALENDAR-TYPE     TO DPG51-CALENDAR-TYPE.            05000000
           SET  DPG52-LK-DTE-ISO-FMT TO TRUE.                           05010000
           MOVE CC-ISO-FMT-DATE      TO DPG52-LK-DATE-INPUT.            05020000
                                                                        05030000
           CALL PC-CALENDAR-ROUTINE                                     05040000
               USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.               05050000
                                                                        05060000
           EVALUATE TRUE                                                05070000
               WHEN DPG54-SEVERE-ERROR                                  05080000
                   DISPLAY '** ABEND **'                                05090000
                   DISPLAY '** PGM = INKUT205'                          05100000
                   DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE. '  05110000
                   DISPLAY '** ERROR MESSAGE = '                        05120000
                           DPG54-ERROR-MESSAGE                          05130000
                   DISPLAY '** DPG51 = ' DPG51                          05140000
                   DISPLAY '** DPG52 = ' DPG52                          05150000
                   DISPLAY '** DPG53 = ' DPG53                          05160000
                   DISPLAY '** DPG54 = ' DPG54                          05170000
                   MOVE +4003 TO ABEND-CODE                             05180000
                   CALL 'ILBOABN0' USING ABEND-CODE                     05190000
               WHEN OTHER                                               05200000
                   CONTINUE                                             05210000
           END-EVALUATE.                                                05220000
                                                                        05230000
           IF  DPG55-NUMERIC-WEEK-DAY-SAT                               05240000
               CONTINUE                                                 05250000
           ELSE                                                         05260000
               DISPLAY '** ABEND **'                                    05270000
               DISPLAY '** PGM = INKUT205'                              05280000
               DISPLAY '** CONTROL CARD DATE NOT A SATURDAY DATE.'      05290000
               MOVE +4003 TO ABEND-CODE                                 05300000
               CALL 'ILBOABN0' USING ABEND-CODE                         05310000
           END-IF.                                                      05320000
                                                                        05330000
           MOVE DPG55-GREG-DATE TO OC-WEEK-END-DATE.                    05340000
                                                                        05350000
           IF  DPG56-FISCAL-PERIOD-WEEK-X = '1'                         05360000
               MOVE 'Y'         TO OC-NEW-MONTH                         05370000
           END-IF.                                                      05380000
                                                                        05390000
           MOVE OC-OUTPUT-CONTROL-RECORD TO ML-FORMATTED-TRANS-RECORD.  05400000
           PERFORM Z500-WRITE-ML-FORMATTED-TRANS.                       05410000
                                                                        05420000
                                                                        05430000
      /*****************************************************************05440000
      * B100-FORMAT AND WRITE THE PURCHASE TRANSACTIONS.                05450000
      *     THE ROUTINE FORMATS AND WRITES, IF APPLICABLE, PURCHASE     05460000
      * TRANSACTIONS FOR ESTIMATED STORES. DETERMINE THE TYPE OF STORE  05470000
      * WE ARE PROCESSING. IF WE DO NOT FIND AN ESTIMATED STORE, DO NOT 05480000
      * PROCESS THE TRANSACTION FOR IT HAS BEEN INCLUDED IN THE         05490000
      * INVENTORIED NUMBERS OR THE NORMAL MERCHANDISE LEDGER NUMBERS.   05500000
      ******************************************************************05510000
       B100-FORMAT-PURCHASE-TRANS.                                      05520000
                                                                        05530000
           ADD +1 TO RC-XT-PURCHASE-EXTRACT-CNT.                        05540000
                                                                        05550000
           IF ML021-PURCH-STORE-NBR NOT EQUAL PV-STORE-NUMBER           05560000
               MOVE ML021-PURCH-STORE-NBR TO PV-STORE-NUMBER            05570000
               MOVE 'N'                   TO PS-PROCESS-TRANS-SW        05580000
               PERFORM Z600-SELECT-ESTIMATED-STORE                      05590000
           END-IF.                                                      05600000
                                                                        05610000
           IF PROCESS-THE-TRANS                                         05620000
               PERFORM B125-DETER-A-BEFORE-TRANS                        05630000
           ELSE                                                         05640000
               PERFORM B175-PROCESS-BYPASSED-TRANS                      05650000
           END-IF.                                                      05660000
                                                                        05670000
           PERFORM Z100-READ-PURCHASE-EXTRACT.                          05680000
                                                                        05690000
      ******************************************************************05700000
      * B125-DETERMINE IF THE TRANSACTION QUALIFIES TO BE COUNTED.      05710000
      ******************************************************************05720000
       B125-DETER-A-BEFORE-TRANS.                                       05730000
                                                                        05740000
           IF (ML021-PURCH-PURCHASE AND                                 05750000
RB0401         ML021-PURCH-TYP-CDE = '1') AND                           05760000
RB0401         INVPAR-MFSTNG-IND = 'N'                                  05770000
             IF ML021-PURCH-FIN-LIAB-DTE NOT > INVPAR-ACTL-DC-CTOFF-DTE 05780000
               PERFORM B150-GENERATE-PURCHASE-TRANS                     05790000
             ELSE                                                       05800000
               PERFORM B175-PROCESS-BYPASSED-TRANS                      05810000
             END-IF                                                     05820000
           ELSE                                                         05830000
             IF ML021-PURCH-FIN-LIAB-DTE NOT > INVPAR-PLND-INV-DTE      05840000
               PERFORM B150-GENERATE-PURCHASE-TRANS                     05850000
             ELSE                                                       05860000
               PERFORM B175-PROCESS-BYPASSED-TRANS                      05870000
             END-IF                                                     05880000
           END-IF.                                                      05890000
                                                                        05900000
                                                                        05910000
      ******************************************************************05920000
      * B150-GENERATE PURCHASE TRANSACTIONS.                            05930000
      ******************************************************************05940000
       B150-GENERATE-PURCHASE-TRANS.                                    05950000
                                                                        05960000
           EVALUATE TRUE                                                05970000
               WHEN ML021-PURCH-PURCHASE                                05980000
                   MOVE SPACE                  TO D115-PURCH-JOURNAL-REC05990000
                   MOVE ML021-PURCH-TRANS-CDE  TO D115-CODE-10          06000000
                   MOVE ML021-PURCH-BATCH-NBR  TO D115-BATCH-NO         06010000
W26600                                                                  06011000
W26600*            MOVE ML021-PURCH-STORE-NBR  TO PV-5-DIGIT-STORE-2-4  06020000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           06030000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        06040000
W26600*               THRU CHECK-DIGIT-EXIT                             06050000
W26600*            MOVE CD-STORE-NO            TO D115-STORE-10         06060000
W26600             MOVE ML021-PURCH-STORE-NBR  TO D115-STORE-10         06060100
                                                                        06061000
                   MOVE ML021-PURCH-DEPT-NBR   TO D115-DEPT-10X         06070000
                   MOVE ML021-PURCH-CLASS-NBR  TO D115-CLASS-10X        06080000
                   MOVE ML021-PURCH-DOC-5-NBR  TO D115-DOC-NO-10        06090000
                   MOVE ML021-PURCH-DOC-DTE    TO PV-ISO-FMT-DATE       06100000
                   MOVE PV-ISO-FMT-MM          TO D115-DOC-MM           06110000
                   MOVE PV-ISO-FMT-DD          TO D115-DOC-DD           06120000
                   MOVE ML021-PURCH-VEND-NBR   TO D115-VEND-NO-10       06130000
                   MOVE ML021-PURCH-PO-NBR     TO D115-PO-NO            06140000
W26600                                                                  06150000
W26600*            MOVE ML021-PURCH-STORE-NBR  TO PV-ACCT-NO-STORE-NBR  06160000
W26600*            MOVE PV-ACCT-NO             TO D115-ACCT-NO          06161000
W26600             MOVE ML021-PURCH-STORE-NBR  TO D115-ACCT-NO          06162000
W26600                                                                  06163000
                   MOVE ML021-PURCH-KOHLS-MM   TO D115-KOHLS-MM         06170000
                   MOVE ML021-PURCH-EXTD-GRO-COST-AMT                   06180000
                                               TO D115-MERCH-AMT        06190000
                   MOVE ML021-PURCH-DISC-AMT   TO D115-DISCT-AMT        06200000
                   MOVE ML021-PURCH-FRGT-AMT   TO D115-FRGHT-AMT        06210000
                   MOVE ML021-PURCH-EXTD-NET-COST-AMT                   06220000
                                               TO D115-NET-COST         06230000
                   MOVE ML021-PURCH-EXTD-RTL-AMT                        06240000
                                               TO D115-RETAIL-AMT       06250000
                   MOVE ML021-PURCH-DR-CR-CDE  TO D115-TRANS-CD         06260000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    06270000
                        D115-PJ-FISCAL-PERIOD                           06280000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   06290000
                        D115-PJ-FISCAL-WK                               06300000
                   MOVE ML021-PURCH-FIN-LIAB-DTE TO PV-FORMATTED-DATE   06310000
                   MOVE PV-FMT-DTE-CCYY          TO PV-UNFMT-DTE-CCYY   06320000
                   MOVE PV-FMT-DTE-MM            TO PV-UNFMT-DTE-MM     06330000
                   MOVE PV-FMT-DTE-DD            TO PV-UNFMT-DTE-DD     06340000
                   MOVE PV-UNFMT-DATE            TO D115-FIN-LIAB-DTE   06350000
                   MOVE ML021-PURCH-AP-TRANS-CDE TO D115-AP-TRANS-CDE   06360000
                   MOVE D115-PURCH-JOURNAL-REC                          06370000
                                            TO ML-FORMATTED-TRANS-RECORD06380000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                06390000
                   ADD +1 TO RC-ML-PURCHASE-TRANS-CNT                   06400000
               WHEN ML021-PURCH-TRANSFER-FRM-STORE                      06410000
                   MOVE SPACE                  TO D115-TRANSFER-FROM-REC06420000
                   MOVE ML021-PURCH-TRANS-CDE  TO D115-CODE-30          06430000
W26600                                                                  06440000
W26600*            MOVE ML021-PURCH-STORE-NBR  TO PV-5-DIGIT-STORE-2-4  06450000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           06460000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        06470000
W26600*            MOVE CD-STORE-NO            TO D115-TRANSFER-FROM    06471000
W26600             MOVE ML021-PURCH-STORE-NBR  TO D115-TRANSFER-FROM    06472000
W26600                                                                  06473000
                   MOVE ML021-PURCH-DEPT-NBR   TO D115-DEPT-30X         06480000
                   MOVE ML021-PURCH-CLASS-NBR  TO D115-CLASS-30X        06490000
24029 *******      MOVE ML021-PURCH-SEQ-NBR    TO D115-SEQ-30X          06500000
24029              MOVE ML021-PURCH-SKU        TO D115-SKU-30           06502000
                   MOVE ML021-PURCH-DOC-5-NBR  TO D115-DOC-NO-30        06510000
                   MOVE ML021-PURCH-DOC-DTE    TO PV-ISO-FMT-DATE       06520000
                   MOVE PV-ISO-FMT-MM          TO D115-TRANS-MM         06530000
                   MOVE PV-ISO-FMT-DD          TO D115-TRANS-DD         06540000
W26600                                                                  06550000
W26600*            MOVE ML021-PURCH-REL-STORE-NBR                       06560000
W26600*                                        TO PV-5-DIGIT-STORE-2-4  06570000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           06580000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        06590000
W26600*            MOVE CD-STORE-NO            TO D115-TRANSFER-TO      06591000
W26600             MOVE ML021-PURCH-REL-STORE-NBR TO D115-TRANSFER-TO   06592000
W26600                                                                  06593000
                   MOVE ML021-PURCH-KOHLS-MM   TO D115-KOHLS-MM-30      06600000
                   MOVE ML021-PURCH-QTY        TO D115-TRANS-UNITS      06610000
                   MOVE ML021-PURCH-PER-UNT-RTL-AMT                     06620000
                                               TO D115-RETAIL-PER-UN    06630000
                   MOVE ML021-PURCH-BATCH-NBR  TO D115-TRANS-BATCHX     06640000
                   MOVE ZERO                   TO D115-TRANS-SCAN-NO    06650000
                   MOVE ML021-PURCH-EXTD-NET-COST-AMT                   06660000
                                            TO D115-TOTAL-TRNSFR-FM-COST06670000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    06680000
                        D115-TFR-FISCAL-PERIOD                          06690000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   06700000
                        D115-TFR-FISCAL-WK                              06710000
                   MOVE D115-TRANSFER-FROM-REC                          06720000
                                            TO ML-FORMATTED-TRANS-RECORD06730000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                06740000
                   ADD +1 TO RC-ML-TRANSFER-FROM-TRANS-CNT              06750000
               WHEN ML021-PURCH-TRANSFER-TO-STORE                       06760000
                   MOVE SPACE                  TO D115-TRANSFER-TO-REC  06770000
                   MOVE ML021-PURCH-TRANS-CDE  TO D115-CODE-31          06780000
W26600                                                                  06790000
W26600*            MOVE ML021-PURCH-STORE-NBR  TO PV-5-DIGIT-STORE-2-4  06800000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           06810000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        06820000
W26600*            MOVE CD-STORE-NO            TO D115-TRANSFER-TO31    06821000
W26600             MOVE ML021-PURCH-STORE-NBR  TO D115-TRANSFER-TO31    06822000
W26600                                                                  06823000
                   MOVE ML021-PURCH-DEPT-NBR   TO D115-DEPT-31          06830000
                   MOVE ML021-PURCH-CLASS-NBR  TO D115-CLASS-31         06840000
24029 ******       MOVE ML021-PURCH-SEQ-NBR    TO D115-SEQ-31           06850000
24029              MOVE ML021-PURCH-SKU        TO D115-SKU-31           06852000
                   MOVE ML021-PURCH-DOC-5-NBR  TO D115-DOC-NO-31        06860000
                   MOVE ML021-PURCH-DOC-DTE    TO PV-ISO-FMT-DATE       06870000
                   MOVE PV-ISO-FMT-MM          TO D115-MM-31            06880000
                   MOVE PV-ISO-FMT-DD          TO D115-DD-31            06890000
W26600                                                                  06900000
W26600*            MOVE ML021-PURCH-REL-STORE-NBR                       06910000
W26600*                                        TO PV-5-DIGIT-STORE-2-4  06920000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           06930000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        06940000
W26600*            MOVE CD-STORE-NO            TO D115-TRANSFER-FROM-31 06941000
W26600             MOVE ML021-PURCH-REL-STORE-NBR                       06942000
W26600                                         TO D115-TRANSFER-FROM-31 06943000
                   MOVE ML021-PURCH-KOHLS-MM   TO D115-KOHLS-MM-31      06950000
                   MOVE ML021-PURCH-QTY        TO D115-UNITS-TRANSFER   06960000
                   MOVE ML021-PURCH-PER-UNT-RTL-AMT                     06970000
                                               TO D115-RETAIL-PER       06980000
                   MOVE ML021-PURCH-BATCH-NBR  TO D115-TRANS-BATCHX-31  06990000
                   MOVE ML021-PURCH-EXTD-NET-COST-AMT                   07000000
                                            TO D115-TOTAL-TRNSFR-TO-COST07010000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    07020000
                        D115-TTR-FISCAL-PERIOD                          07030000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   07040000
                        D115-TTR-FISCAL-WK                              07050000
                   MOVE D115-TRANSFER-TO-REC                            07060000
                                            TO ML-FORMATTED-TRANS-RECORD07070000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                07080000
                   ADD +1 TO RC-ML-TRANSFER-TO-TRANS-CNT                07090000
               WHEN OTHER                                               07100000
                   MOVE +4008 TO ABEND-CODE                             07110000
                   DISPLAY '** ABEND     **'                            07120000
                   DISPLAY '** PROGRAM = INKUT205'                      07130000
                   DISPLAY                                              07140000
                         '** PARAGRAPH = B150-GENERATE-PURCHASE-TRA'    07150000
                   DISPLAY '** TRANS-CDE ' ML021-PURCH-TRANS-CDE        07160000
                             ' ON PURCHASE-EXTRACT-RECORD IS NOT VALID.'07170000
                   DISPLAY '** PURCHASE-EXTRACT-RECORD: '               07180000
                           PURCHASE-EXTRACT-RECORD                      07190000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    07200000
           END-EVALUATE.                                                07210000
                                                                        07220000
                                                                        07230000
      ******************************************************************07240000
      * B175-COUNT BYPASSED TRANSACTIONS                                07250000
      ******************************************************************07260000
       B175-PROCESS-BYPASSED-TRANS.                                     07270000
                                                                        07280000
           EVALUATE TRUE                                                07290000
               WHEN ML021-PURCH-PURCHASE                                07300000
                   ADD +1 TO RC-BP-PURCHASE-TRANS-CNT                   07310000
               WHEN ML021-PURCH-TRANSFER-FRM-STORE                      07320000
                   ADD +1 TO RC-BP-TRANSFER-FROM-TRANS-CNT              07330000
               WHEN ML021-PURCH-TRANSFER-TO-STORE                       07340000
                   ADD +1 TO RC-BP-TRANSFER-TO-TRANS-CNT                07350000
               WHEN OTHER                                               07360000
                   MOVE +4008 TO ABEND-CODE                             07370000
                   DISPLAY '** ABEND     **'                            07380000
                   DISPLAY '** PROGRAM = INKUT205'                      07390000
                   DISPLAY '** PARAGRAPH = B175-PROCESS-BYPASS-TRANS'   07400000
                   DISPLAY '** TRANS-CDE ' ML021-PURCH-TRANS-CDE        07410000
                             ' ON PURCHASE-EXTRACT-RECORD IS NOT VALID.'07420000
                   DISPLAY '** PURCHASE-EXTRACT-RECORD: '               07430000
                           PURCHASE-EXTRACT-RECORD                      07440000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    07450000
           END-EVALUATE.                                                07460000
                                                                        07470000
      /*****************************************************************07480000
      * FORMAT AND WRITE THE SALE TRANSACTIONS.                         07490000
      *     THE ROUTINE FORMATS AND WRITES, IF APPLICABLE, SALES        07500000
      * TRANSACTIONS FOR ESTIMATED STORES. DETERMINE THE TYPE OF STORE  07510000
      * WE ARE PROCESSING. IF WE DO NOT FIND AN ESTIMATED STORE, DO NOT 07520000
      * PROCESS THE TRANSACTION FOR IT HAS BEEN INCLUDED IN THE         07530000
      * INVENTORIED NUMBERS OR THE NORMAL MERCHANDISE LEDGER NUMBERS.   07540000
      ******************************************************************07550000
       B200-FORMAT-SALE-TRANS.                                          07560000
                                                                        07570000
           ADD +1 TO RC-XT-SALE-EXTRACT-CNT.                            07580000
                                                                        07590000
           IF ML022-SALE-STORE-NBR NOT EQUAL PV-STORE-NUMBER            07600000
               MOVE ML022-SALE-STORE-NBR TO PV-STORE-NUMBER             07610000
               MOVE 'N'                  TO PS-PROCESS-TRANS-SW         07620000
               PERFORM Z600-SELECT-ESTIMATED-STORE                      07630000
           END-IF.                                                      07640000
                                                                        07650000
           IF PROCESS-THE-TRANS                                         07660000
               PERFORM B225-DETER-A-BEFORE-TRANS                        07670000
           ELSE                                                         07680000
               PERFORM B275-PROCESS-BYPASSED-TRANS                      07690000
           END-IF.                                                      07700000
                                                                        07710000
           PERFORM Z200-READ-SALE-EXTRACT.                              07720000
                                                                        07730000
      ******************************************************************07740000
      * B225-DETERMINE IF THE TRANSACTION QUALIFIES TO BE COUNTED.      07750000
      ******************************************************************07760000
       B225-DETER-A-BEFORE-TRANS.                                       07770000
                                                                        07780000
           IF ML022-SALE-FIN-LIAB-DTE NOT > INVPAR-PLND-INV-DTE         07790000
               PERFORM B250-GENERATE-SALES-TRANS                        07800000
           ELSE                                                         07810000
               PERFORM B275-PROCESS-BYPASSED-TRANS                      07820000
           END-IF.                                                      07830000
                                                                        07840000
                                                                        07850000
      ******************************************************************07860000
      * FORMAT AND WRITE THE SALE TRANSACTIONS.                         07870000
      ******************************************************************07880000
                                                                        07890000
       B250-GENERATE-SALES-TRANS.                                       07900000
                                                                        07910000
           EVALUATE TRUE                                                07920000
               WHEN ML022-SALE                                          07930000
                   MOVE SPACE                  TO D115-SALES-REC        07940000
                   MOVE ML022-SALE-TRANS-CDE   TO D115-CODE-20          07950000
W26600                                                                  07960000
W26600*            MOVE ML022-SALE-STORE-NBR   TO PV-5-DIGIT-STORE-2-4  07970000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           07980000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        07990000
W26600*            MOVE CD-STORE-NO            TO D115-STORE-20         07991000
W26600             MOVE ML022-SALE-STORE-NBR   TO D115-STORE-20         07992000
W26600                                                                  07993000
                   MOVE ML022-SALE-DEPT-NBR    TO D115-DEPT-20X         08000000
                   MOVE ML022-SALE-CLASS-NBR   TO D115-CLASS-20X        08010000
                   MOVE ML022-SALE-FIN-LIAB-DTE TO PV-ISO-FMT-DATE      08020000
                   MOVE PV-ISO-FMT-MM          TO D115-SALES-MM         08030000
                   MOVE PV-ISO-FMT-DD          TO D115-SALES-DD         08040000
                   MOVE PV-ISO-FMT-YY          TO D115-SALES-YY         08050000
                   MOVE ML022-SALE-EXTD-RTL-AMT                         08060000
                                               TO D115-SALE-AMT         08070000
                   MOVE ML022-SALE-FSCL-WK-NBR TO D115-SALE-WEEK        08080000
                   MOVE ML022-SALE-FSCL-DAY    TO D115-SALE-DAY         08090000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    08100000
                        D115-SR-FISCAL-PERIOD                           08110000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   08120000
                        D115-SR-FISCAL-WK                               08130000
                   MOVE ML022-SALE-TYPE-2 TO                            08140000
                        D115-SALE-TYP-CDE                               08150000
                   MOVE D115-SALES-REC TO ML-FORMATTED-TRANS-RECORD     08160000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                08170000
                   ADD +1 TO RC-ML-SALE-CNT                             08180000
               WHEN OTHER                                               08190000
                   MOVE +4008 TO ABEND-CODE                             08200000
                   DISPLAY '** ABEND     **'                            08210000
                   DISPLAY '** PROGRAM = INKUT205'                      08220000
                   DISPLAY '** PARAGRAPH = B250-GENERATE-SALES-TRA'     08230000
                   DISPLAY '** TRANS-CDE ' ML022-SALE-TRANS-CDE         08240000
                         ' ON SALE-EXTRACT-RECORD IS NOT VALID.'        08250000
                   DISPLAY '** SALE-EXTRACT-RECORD: '                   08260000
                           SALE-EXTRACT-RECORD                          08270000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    08280000
           END-EVALUATE.                                                08290000
                                                                        08300000
                                                                        08310000
      ******************************************************************08320000
      * B275-COUNT BYPASSED TRANSACTIONS                                08330000
      ******************************************************************08340000
       B275-PROCESS-BYPASSED-TRANS.                                     08350000
                                                                        08360000
           EVALUATE TRUE                                                08370000
               WHEN ML022-SALE                                          08380000
                   ADD +1 TO RC-BP-SALE-CNT                             08390000
               WHEN OTHER                                               08400000
                   MOVE +4008 TO ABEND-CODE                             08410000
                   DISPLAY '** ABEND     **'                            08420000
                   DISPLAY '** PROGRAM = INKUT205'                      08430000
                   DISPLAY '** PARAGRAPH = B275-PROCESS-BYPASS-TRA'     08440000
                   DISPLAY '** TRANS-CDE ' ML022-SALE-TRANS-CDE         08450000
                         ' ON SALE-EXTRACT-RECORD IS NOT VALID.'        08460000
                   DISPLAY '** SALE-EXTRACT-RECORD: '                   08470000
                           SALE-EXTRACT-RECORD                          08480000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    08490000
           END-EVALUATE.                                                08500000
                                                                        08510000
      /*****************************************************************08520000
      * FORMAT AND WRITE THE PRICE CHANGE TRANSACTIONS.                 08530000
      *     THE ROUTINE FORMATS AND WRITES, IF APPLICABLE, PRICE CHANGE 08540000
      * TRANSACTIONS FOR ESTIMATED STORES. DETERMINE THE TYPE OF STORE  08550000
      * WE ARE PROCESSING. IF WE DO NOT FIND AN ESTIMATED STORE, DO NOT 08560000
      * PROCESS THE TRANSACTION FOR IT HAS BEEN INCLUDED IN THE         08570000
      * INVENTORIED NUMBERS OR THE NORMAL MERCHANDISE LEDGER NUMBERS.   08580000
      ******************************************************************08590000
       B300-FORMAT-PRICE-CHANGE-TRANS.                                  08600000
                                                                        08610000
           ADD +1 TO RC-XT-PRICE-CHANGE-EXTRACT-CNT.                    08620000
                                                                        08630000
           IF ML023-PC-STORE-NBR NOT EQUAL PV-STORE-NUMBER              08640000
               MOVE ML023-PC-STORE-NBR TO PV-STORE-NUMBER               08650000
               MOVE 'N'                TO PS-PROCESS-TRANS-SW           08660000
               PERFORM Z600-SELECT-ESTIMATED-STORE                      08670000
           END-IF.                                                      08680000
                                                                        08690000
           IF PROCESS-THE-TRANS                                         08700000
               PERFORM B325-DETER-A-BEFORE-TRANS                        08710000
           ELSE                                                         08720000
               PERFORM B375-PROCESS-BYPASSED-TRANS                      08730000
           END-IF.                                                      08740000
                                                                        08750000
           PERFORM Z300-READ-PRICE-CHANGE-EXTRACT.                      08760000
                                                                        08770000
      ******************************************************************08780000
      * B325-DETERMINE IF THE TRANSACTION QUALIFIES TO BE COUNTED.      08790000
      ******************************************************************08800000
       B325-DETER-A-BEFORE-TRANS.                                       08810000
                                                                        08820000
           IF ML023-PC-FIN-LIAB-DTE NOT > INVPAR-PLND-INV-DTE           08830000
               PERFORM B350-GEN-PRICE-CHANGE-TRANS                      08840000
           ELSE                                                         08850000
               PERFORM B375-PROCESS-BYPASSED-TRANS                      08860000
           END-IF.                                                      08870000
                                                                        08880000
                                                                        08890000
      ******************************************************************08900000
      * B350-GEN-PRICE-CHANGE-TRANS.                                    08910000
      ******************************************************************08920000
       B350-GEN-PRICE-CHANGE-TRANS.                                     08930000
                                                                        08940000
           EVALUATE TRUE                                                08950000
               WHEN ML023-PC-MARKDOWN                                   08960000
                   MOVE SPACE                  TO D115-MKDWN-REC        08970000
                   MOVE ML023-PC-TRANS-CDE     TO D115-CODE-40          08980000
                   MOVE ML023-PC-BATCH-NBR     TO D115-BATCH-NO-40      08990000
W26600                                                                  09000000
W26600*            MOVE ML023-PC-STORE-NBR     TO PV-5-DIGIT-STORE-2-4  09010000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           09020000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        09030000
W26600*            MOVE CD-STORE-NO            TO D115-STORE-40         09031000
W26600             MOVE ML023-PC-STORE-NBR     TO D115-STORE-40         09032000
W26600                                                                  09033000
                   MOVE ML023-PC-DEPT-NBR      TO D115-DEPT-40X         09040000
                   MOVE ML023-PC-CLASS-NBR     TO D115-CLASS-40X        09050000
                   MOVE ML023-PC-DOC-5-NBR     TO D115-DOC-NO-40        09060000
                   MOVE ML023-PC-FIN-LIAB-DTE  TO PV-ISO-FMT-DATE       09070000
                   MOVE PV-ISO-FMT-MM          TO D115-MKDWN-MM         09080000
                   MOVE PV-ISO-FMT-DD          TO D115-MKDWN-DD         09090000
                   MOVE ML023-PC-KOHLS-MM      TO D115-KOHLS-MM-40      09100000
                   MOVE ML023-PC-OLD-RSN-CDE   TO D115-MKDWN-REASON     09110000
                   MOVE ML023-PC-OLD-PR-AMT    TO D115-MKDWN-OPRICE     09120000
                   MOVE ML023-PC-NEW-PR-AMT    TO D115-MKDWN-NPRICE     09130000
                   MOVE ML023-PC-QTY           TO D115-MKDWN-QTY        09140000
                   MOVE ML023-PC-CATEGORY      TO D115-CATEGORY-40      09150000
                   MOVE ML023-PC-DOC-7-NBR                              09160000
                                       TO D115-7-DIGIT-DOCUMENT-NMBR-40 09170000
                   MOVE ML023-PC-MKDN-MKUP-RSN-CDE                      09180000
                                       TO D115-MKDWN-MKDN-MKUP-RSN-CDE  09190000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    09200000
                        D115-MKDWN-FISCAL-PERIOD                        09210000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   09220000
                        D115-MKDWN-FISCAL-WK                            09230000
                   MOVE D115-MKDWN-REC TO ML-FORMATTED-TRANS-RECORD     09240000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                09250000
                   ADD +1 TO RC-ML-MARKDOWN-CNT                         09260000
               WHEN ML023-PC-MARKUP                                     09270000
                   MOVE SPACE                  TO D115-MARKON-REC       09280000
                   MOVE ML023-PC-TRANS-CDE     TO D115-CODE-50          09290000
                   MOVE ML023-PC-BATCH-NBR     TO D115-BATCH-NO-50      09300000
W26600                                                                  09310000
W26600*            MOVE ML023-PC-STORE-NBR     TO PV-5-DIGIT-STORE-2-4  09320000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           09330000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        09340000
W26600*            MOVE CD-STORE-NO            TO D115-STORE-50         09341000
W26600             MOVE ML023-PC-STORE-NBR     TO D115-STORE-50         09342000
W26600                                                                  09343000
                   MOVE ML023-PC-DEPT-NBR      TO D115-DEPT-50X         09350000
                   MOVE ML023-PC-CLASS-NBR     TO D115-CLASS-50X        09360000
                   MOVE ML023-PC-DOC-5-NBR     TO D115-DOC-NO-50        09370000
                   MOVE ML023-PC-FIN-LIAB-DTE  TO PV-ISO-FMT-DATE       09380000
                   MOVE PV-ISO-FMT-MM          TO D115-MARKON-MM        09390000
                   MOVE PV-ISO-FMT-DD          TO D115-MARKON-DD        09400000
                   MOVE ML023-PC-KOHLS-MM      TO D115-KOHLS-MM-50      09410000
                   MOVE ML023-PC-OLD-RSN-CDE   TO D115-MARKON-REASON    09420000
                   MOVE ML023-PC-OLD-PR-AMT    TO D115-MARKON-OPRICE    09430000
                   MOVE ML023-PC-NEW-PR-AMT    TO D115-MARKON-NPRICE    09440000
                   MOVE ML023-PC-QTY           TO D115-MARKON-QTY       09450000
                   MOVE ML023-PC-CATEGORY      TO D115-CATEGORY-50      09460000
                   MOVE ML023-PC-DOC-7-NBR                              09470000
                                       TO D115-7-DIGIT-DOCUMENT-NMBR-50 09480000
                   MOVE ML023-PC-MKDN-MKUP-RSN-CDE                      09490000
                                       TO D115-MARKON-MKDN-MKUP-RSN-CDE 09500000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    09510000
                        D115-MARKON-FISCAL-PERIOD                       09520000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   09530000
                        D115-MARKON-FISCAL-WK                           09540000
                   MOVE D115-MARKON-REC TO ML-FORMATTED-TRANS-RECORD    09550000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                09560000
                   ADD +1 TO RC-ML-MARKUP-CNT                           09570000
               WHEN OTHER                                               09580000
                   MOVE +4008 TO ABEND-CODE                             09590000
                   DISPLAY '** ABEND     **'                            09600000
                   DISPLAY '** PROGRAM = INKUT205'                      09610000
                   DISPLAY                                              09620000
                         '** PARAGRAPH = B350-GEN-PRICE-CHANGE-TRA'     09630000
                   DISPLAY '** TRANS-CDE ' ML023-PC-TRANS-CDE           09640000
                         ' ON PRICE-CHANGE-EXTRACT-RECORD IS NOT VALID.'09650000
                   DISPLAY '** PRICE-CHANGE-EXTRACT-RECORD: '           09660000
                           PRICE-CHANGE-EXTRACT-RECORD                  09670000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    09680000
           END-EVALUATE.                                                09690000
                                                                        09700000
                                                                        09710000
      ******************************************************************09720000
      * B375-COUNT BYPASSED TRANSACTIONS                                09730000
      ******************************************************************09740000
       B375-PROCESS-BYPASSED-TRANS.                                     09750000
                                                                        09760000
           EVALUATE TRUE                                                09770000
               WHEN ML023-PC-MARKDOWN                                   09780000
                   ADD +1 TO RC-BP-MARKDOWN-CNT                         09790000
               WHEN ML023-PC-MARKUP                                     09800000
                   ADD +1 TO RC-BP-MARKUP-CNT                           09810000
               WHEN OTHER                                               09820000
                   MOVE +4008 TO ABEND-CODE                             09830000
                   DISPLAY '** ABEND     **'                            09840000
                   DISPLAY '** PROGRAM = INKUT205'                      09850000
                   DISPLAY                                              09860000
                         '** PARAGRAPH = B375-PROCESS-BYPASSED-TRANS'   09870000
                   DISPLAY '** TRANS-CDE ' ML023-PC-TRANS-CDE           09880000
                         ' ON PRICE-CHANGE-EXTRACT-RECORD IS NOT VALID.'09890000
                   DISPLAY '** PRICE-CHANGE-EXTRACT-RECORD: '           09900000
                           PRICE-CHANGE-EXTRACT-RECORD                  09910000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    09920000
           END-EVALUATE.                                                09930000
                                                                        09940000
                                                                        09950000
      /*****************************************************************09960000
      * FORMAT AND WRITE THE ADJUSTMENT TRANSACTIONS.                   09970000
      *     THE ROUTINE FORMATS AND WRITES, IF APPLICABLE, ADJUSTMENT   09980000
      * TRANSACTIONS FOR ESTIMATED STORES. DETERMINE THE TYPE OF STORE  09990000
      * WE ARE PROCESSING. IF WE DO NOT FIND AN ESTIMATED STORE, DO NOT 10000000
      * PROCESS THE TRANSACTION FOR IT HAS BEEN INCLUDED IN THE         10010000
      * INVENTORIED NUMBERS OR THE NORMAL MERCHANDISE LEDGER NUMBERS.   10020000
      ******************************************************************10030000
       B400-FORMAT-ADJUSTMENT-TRANS.                                    10040000
                                                                        10050000
           ADD +1 TO RC-XT-ADJUSTMENT-EXTRACT-CNT.                      10060000
                                                                        10070000
           IF ML024-ADJ-STORE-NBR NOT EQUAL PV-STORE-NUMBER             10080000
               MOVE ML024-ADJ-STORE-NBR TO PV-STORE-NUMBER              10090000
               MOVE 'N'                 TO PS-PROCESS-TRANS-SW          10100000
               PERFORM Z600-SELECT-ESTIMATED-STORE                      10110000
           END-IF.                                                      10120000
                                                                        10130000
           IF PROCESS-THE-TRANS                                         10140000
               PERFORM B425-DETER-A-BEFORE-TRANS                        10150000
           ELSE                                                         10160000
               PERFORM B475-PROCESS-BYPASSED-TRANS                      10170000
           END-IF.                                                      10180000
                                                                        10190000
           PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                        10200000
                                                                        10210000
      ******************************************************************10220000
      * B425-DETERMINE IF THE TRANSACTION QUALIFIES TO BE COUNTED.      10230000
      ******************************************************************10240000
       B425-DETER-A-BEFORE-TRANS.                                       10250000
                                                                        10260000
           IF ML024-ADJ-FIN-LIAB-DTE NOT > INVPAR-PLND-INV-DTE          10270000
               PERFORM B450-GENERATE-ADJUSTMENT-TRANS                   10280000
           ELSE                                                         10290000
               PERFORM B475-PROCESS-BYPASSED-TRANS                      10300000
           END-IF.                                                      10310000
                                                                        10320000
                                                                        10330000
      ******************************************************************10340000
      * B450-GENERATE-ADJUSTMENT-TRANS.                                 10350000
      ******************************************************************10360000
       B450-GENERATE-ADJUSTMENT-TRANS.                                  10370000
                                                                        10380000
           EVALUATE TRUE                                                10390000
               WHEN ML024-ADJ-ADJUSTMENT                                10400000
                   MOVE SPACE                  TO D115-ADJ-REC          10410000
                   MOVE ML024-ADJ-TRANS-CDE    TO D115-CODE-80          10420000
W26600                                                                  10430000
W26600*            MOVE ML024-ADJ-STORE-NBR    TO PV-5-DIGIT-STORE-2-4  10440000
W26600*            MOVE PV-5-DIGIT-STORE       TO CD-STORE-NO           10450000
W26600*            PERFORM CALCULATE-CHECK-DIGIT                        10460000
W26600*            MOVE CD-STORE-NO            TO D115-STORE-80         10461000
W26600             MOVE ML024-ADJ-STORE-NBR    TO D115-STORE-80         10462000
W26600                                                                  10463000
                   MOVE ML024-ADJ-DEPT-NBR     TO D115-DEPT-80X         10470000
                   MOVE ML024-ADJ-CLASS-NBR    TO D115-CLASS-80X        10480000
                   MOVE ML024-ADJ-FIN-LIAB-DTE TO PV-ISO-FMT-DATE       10490000
                   MOVE PV-ISO-FMT-MM          TO D115-ADJ-MM           10500000
                   MOVE PV-ISO-FMT-DD          TO D115-ADJ-DD           10510000
                   MOVE PV-ISO-FMT-YY          TO D115-ADJ-YY           10520000
                   MOVE ML024-ADJ-KOHLS-MM     TO D115-KOHLS-MM-80      10530000
                   MOVE ML024-ADJ-AT-RTL-AMT   TO D115-AT-RETAIL        10540000
                   MOVE ML024-ADJ-AT-COST-AMT  TO D115-AT-COST          10550000
                   MOVE DPG56-FISCAL-PERIOD-NBR-X TO                    10560000
                        D115-AR-FISCAL-PERIOD                           10570000
                   MOVE DPG56-FISCAL-PERIOD-WEEK-X TO                   10580000
                        D115-AR-FISCAL-WK                               10590000
                   MOVE D115-ADJ-REC   TO ML-FORMATTED-TRANS-RECORD     10600000
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                10610000
                   ADD +1 TO RC-ML-ADJUSTMENT-CNT                       10620000
               WHEN OTHER                                               10630000
                   MOVE +4008 TO ABEND-CODE                             10640000
                   DISPLAY '** ABEND     **'                            10650000
                   DISPLAY '** PROGRAM = INKUT205'                      10660000
                   DISPLAY                                              10670000
                         '** PARAGRAPH = B450-GENERATE-ADJUSTMENT-TRA'  10680000
                   DISPLAY '** TRANS-CDE ' ML024-ADJ-TRANS-CDE          10690000
                         ' ON ADJUSTMENT-EXTRACT-RECORD IS NOT VALID.'  10700000
                   DISPLAY '** ADJUSTMENT-EXTRACT-RECORD: '             10710000
                           ADJUSTMENT-EXTRACT-RECORD                    10720000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    10730000
           END-EVALUATE.                                                10740000
                                                                        10750000
                                                                        10760000
      ******************************************************************10770000
      * B475-COUNT BYPASSED TRANSACTIONS                                10780000
      ******************************************************************10790000
       B475-PROCESS-BYPASSED-TRANS.                                     10800000
                                                                        10810000
           EVALUATE TRUE                                                10820000
               WHEN ML024-ADJ-ADJUSTMENT                                10830000
                   ADD +1 TO RC-BP-ADJUSTMENT-CNT                       10840000
               WHEN OTHER                                               10850000
                   MOVE +4008 TO ABEND-CODE                             10860000
                   DISPLAY '** ABEND     **'                            10870000
                   DISPLAY '** PROGRAM = INKUT205'                      10880000
                   DISPLAY                                              10890000
                         '** PARAGRAPH = B475-PROCESS-BYPASSED-TRANS'   10900000
                   DISPLAY '** TRANS-CDE ' ML024-ADJ-TRANS-CDE          10910000
                         ' ON ADJUSTMENT-EXTRACT-RECORD IS NOT VALID.'  10920000
                   DISPLAY '** ADJUSTMENT-EXTRACT-RECORD: '             10930000
                           ADJUSTMENT-EXTRACT-RECORD                    10940000
                   CALL 'ILBOABN0'  USING ABEND-CODE                    10950000
           END-EVALUATE.                                                10960000
                                                                        10970000
                                                                        10980000
      /*****************************************************************10990000
      * Z100-READ-PURCHASE-EXTRACT.                                     11000000
      ******************************************************************11010000
       Z100-READ-PURCHASE-EXTRACT.                                      11020000
                                                                        11030000
           READ PURCHASE-EXTRACT-FILE                                   11040000
               INTO ML021-PURCH-RECORD                                  11050000
                   AT END SET PS-EOF-PURCHASE-EXTRACT TO TRUE.          11060000
                                                                        11070000
                                                                        11080000
      ******************************************************************11090000
      * Z200-READ-SALE-EXTRACT.                                         11100000
      ******************************************************************11110000
       Z200-READ-SALE-EXTRACT.                                          11120000
                                                                        11130000
           READ SALE-EXTRACT-FILE                                       11140000
               INTO ML022-SALE-RECORD                                   11150000
                   AT END SET PS-EOF-SALE-EXTRACT TO TRUE.              11160000
                                                                        11170000
                                                                        11180000
      ******************************************************************11190000
      * Z300-READ-PRICE-CHANGE-EXTRACT.                                 11200000
      ******************************************************************11210000
       Z300-READ-PRICE-CHANGE-EXTRACT.                                  11220000
                                                                        11230000
           READ PRICE-CHANGE-EXTRACT-FILE                               11240000
               INTO ML023-PC-RECORD                                     11250000
                   AT END SET PS-EOF-PRICE-CHANGE-EXTRACT TO TRUE.      11260000
                                                                        11270000
                                                                        11280000
      ******************************************************************11290000
      * Z400-READ-ADJUSTMENT-EXTRACT.                                   11300000
      ******************************************************************11310000
       Z400-READ-ADJUSTMENT-EXTRACT.                                    11320000
                                                                        11330000
           READ ADJUSTMENT-EXTRACT-FILE                                 11340000
               INTO ML024-ADJ-RECORD                                    11350000
                   AT END SET PS-EOF-ADJUSTMENT-EXTRACT TO TRUE.        11360000
                                                                        11370000
                                                                        11380000
      ******************************************************************11390000
      * Z500-WRITE-ML-FORMATTED-TRANS.                                  11400000
      ******************************************************************11410000
       Z500-WRITE-ML-FORMATTED-TRANS.                                   11420000
                                                                        11430000
           WRITE ML-FORMATTED-TRANS-RECORD.                             11440000
                                                                        11450000
                                                                        11460000
      ******************************************************************11470000
      * Z600-SELECT-ESTIMATED-STORE.                                    11480000
      ******************************************************************11490000
       Z600-SELECT-ESTIMATED-STORE.                                     11500000
                                                                        11510000
W26600     MOVE PV-STORE-NUMBER-NUM TO INVPAR-LOC-NBR.                  11511000
W33304     MOVE 'EI'                TO INVPAR-SCHD-PHY-CNT-CDE.                 
W26600                                                                  11512000
           EXEC SQL                                                     11520000
W33304         SELECT A.LOC_NBR                                                 
W33304               ,A.PLND_INV_DTE                                            
W33304               ,A.ACTL_DC_CTOFF_DTE                                       
W33304               ,A.MFSTNG_IND                                              
W20445         INTO :INVPAR-LOC-NBR                                     11570000
                   ,:INVPAR-PLND-INV-DTE                                11580000
                   ,:INVPAR-ACTL-DC-CTOFF-DTE                           11590000
RB0401             ,:INVPAR-MFSTNG-IND                                  11600000
W33304         FROM TINVPAR  A,                                                 
W33304              TINCNTL  B                                                  
W33304         WHERE A.LOC_NBR           = :INVPAR-LOC-NBR                      
W33304           AND A.ACTL_FIN_BK_DTE   = '9999-09-09'                         
W33304           AND A.SCHD_PHY_CNT_CDE  = :INVPAR-SCHD-PHY-CNT-CDE             
W33304           AND A.LOC_INV_STAT_CDE  = 'IN'                                 
W33304           AND B.ACTV_IND          = 'Y'                                  
W33304           AND B.INV_ID            = A.INV_ID                             
W26600         WITH UR                                                  11641000
           END-EXEC.                                                    11650000
                                                                        11660000
           EVALUATE TRUE                                                11670000
               WHEN SQLCODE IS EQUAL TO ZERO                            11680000
                   MOVE 'Y' TO PS-PROCESS-TRANS-SW                      11690000
               WHEN SQLCODE IS EQUAL TO +100                            11700000
                   MOVE 'N' TO PS-PROCESS-TRANS-SW                      11710000
W33304         WHEN OTHER                                                       
                   DISPLAY SPACES                                       11740000
                   DISPLAY '** ABEND IN PROGRAM - INKUT205'             11750000
                   DISPLAY '** Z600-SELECT-ESTIMATED-STORE.'            11760000
                   DISPLAY 'STORE_NBR        ', PV-STORE-NUMBER         11770000
                   DISPLAY 'SQLCODE ', SQLCODE                          11780000
                   MOVE +4003 TO ABEND-CODE                             11790000
                   CALL 'ILBOABN0' USING ABEND-CODE                     11800000
           END-EVALUATE.                                                11810000
                                                                        11820000
                                                                        11830000
      ******************************************************************11840000
      * READ FILE BACKPOSTING CONTROL CARD.....                         11850000
      ******************************************************************11860000
       Z700-READ-CONTROL-CARD-FILE.                                     11870000
                                                                        11880000
            READ CONTROL-CARD-FILE NEXT RECORD                          11890000
              INTO CC-CONTROL-CARD                                      11900000
                AT END SET PS-END-OF-CONTROL-CARD-FILE TO TRUE.         11910000
                                                                        11920000
                                                                        11930000
      /*****************************************************************11940000
      * CODE FOR CALCULATION OF CHECK-DIGIT FOR STORE NUMBER.           11950000
      ******************************************************************11960000
                                                                        11970000
207210     COPY DPPD035.                                                11980000
