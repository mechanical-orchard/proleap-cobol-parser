       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.           PXKUT098.                                  00020004
       AUTHOR.               DAVE WELLER.                               00030004
       INSTALLATION.         KOHLS DEPARTMENT STORES.                   00040004
       DATE-WRITTEN          05/2010.                                   00050004
       DATE-COMPILED.                                                   00060004
                                                                        00070004
      ******************************************************************00080004
      *   PXKUT098 - ADJUST STR-EXPTED-QTY ON EXTRACT FILE FROM DATA    00090004
      *              FROM THE ORACLE PXT_STR_MDSE_OH_TRKG TABLE.        00100004
      ******************** HISTORY OF CHANGES **************************00110004
      * CHG ID/                                                        *00120004
      * WR/PROJ          DATE        DESCRIPTION OF CHANGES            *00130004
      * ---------------  ----------  --------------------------------  *00140004
      * WR36056          05/07/2010  INITIAL IMPLEMENTATION             00150004
      *                                                                 00160004
      * WR36056          10/01/2010  ADD 3 NEW FILES.                   00170008
      *                              THE FIRST CONTAINS ADJUSTMENTS     00180008
      *                              THAT DID NOT MATCH THOSE ON THE    00190008
      *                              YLLW-TCK-EXTR-FILE AND HAD <0      00200008
      *                              ADJ QTYS.                          00210008
      *                              THE SECOND CONTAINS TRACKING TABLE 00220008
      *                              ADJ RECS. ANY TIME A REC IS WRITTEN00230008
      *                              TO THE OH-QTY-ADJ-FILE OR THE      00240008
      *                              TRKG-UPD-FILE A RECORD IS WRITTEN. 00250008
      *                              THE THIRD FILE CONTAINS A COUNT OF 00260008
      *                              THE NUMBER OF RECS WRITTEN TO THE  00270008
      *                              SECOND FILE DESCRIBED ABOVE.       00280008
      * PKE000000006059  01/09/2011  CHANGE PROGRAM TO SET              00290030
      *                              PXRD126-BEG-OH-QTY TO ZERO FOR     00300026
      *                              SKUS THAT HAVE A NEGATIVE ADJ      00310026
      *                              AND NO MATCH TO THE EXTRACT FILE.  00320026
      ******************************************************************00330008
                                                                        00340004
       ENVIRONMENT DIVISION.                                            00350004
                                                                        00360004
       CONFIGURATION SECTION.                                           00370004
                                                                        00380004
       SOURCE-COMPUTER. IBM-3090.                                       00390004
       OBJECT-COMPUTER. IBM-3090.                                       00400004
                                                                        00410004
       INPUT-OUTPUT SECTION.                                            00420004
       FILE-CONTROL.                                                    00430004
           SELECT YLLW-TCK-EXTR-FILE                                    00440004
               ASSIGN TO INP01.                                         00450004
                                                                        00460004
           SELECT YLLW-TCK-ADJ-FILE                                     00470004
               ASSIGN TO INP02.                                         00480004
                                                                        00490004
           SELECT OH-QTY-ADJ-FILE                                       00500004
               ASSIGN TO OUT01.                                         00510004
                                                                        00520004
           SELECT ADJ-NOT-FND-FILE                                      00530004
               ASSIGN TO OUT02.                                         00540004
                                                                        00550004
           SELECT TRKG-UPD-FILE                                         00560004
               ASSIGN TO OUT03.                                         00570004
                                                                        00580004
           SELECT TRKG-CNT-FILE                                         00590004
               ASSIGN TO OUT04.                                         00600004
                                                                        00610004
       DATA DIVISION.                                                   00620004
                                                                        00630004
       FILE SECTION.                                                    00640004
       FD  YLLW-TCK-EXTR-FILE                                           00650004
           LABEL RECORDS ARE STANDARD                                   00660004
           RECORDING MODE IS F                                          00670004
           BLOCK CONTAINS 0 RECORDS.                                    00680004
       01  YLLW-TCK-EXTR-REC               PIC  X(080).                 00690004
                                                                        00700004
       FD  YLLW-TCK-ADJ-FILE                                            00710004
           LABEL RECORDS ARE STANDARD                                   00720004
           RECORDING MODE IS F                                          00730004
           BLOCK CONTAINS 0 RECORDS.                                    00740004
       01  YLLW-TCK-ADJ-REC                PIC  X(026).                 00750004
                                                                        00760004
       FD  OH-QTY-ADJ-FILE                                              00770004
           LABEL RECORDS ARE STANDARD                                   00780004
           RECORDING MODE IS F                                          00790004
           BLOCK CONTAINS 0 RECORDS.                                    00800004
       01  OH-QTY-ADJ-REC                  PIC X(080).                  00810004
                                                                        00820004
       FD  ADJ-NOT-FND-FILE                                             00830004
           LABEL RECORDS ARE STANDARD                                   00840004
           RECORDING MODE IS F                                          00850004
           BLOCK CONTAINS 0 RECORDS.                                    00860004
       01  ADJ-NOT-FND-REC                 PIC  X(026).                 00870004
                                                                        00880004
       FD  TRKG-UPD-FILE                                                00890004
           LABEL RECORDS ARE STANDARD                                   00900004
           RECORDING MODE IS F                                          00910004
           BLOCK CONTAINS 0 RECORDS.                                    00920004
       01  TRKG-UPD-RECORD                 PIC X(34).                   00930004
                                                                        00940004
       FD  TRKG-CNT-FILE                                                00950004
           LABEL RECORDS ARE STANDARD                                   00960004
           RECORDING MODE IS F                                          00970004
           BLOCK CONTAINS 0 RECORDS.                                    00980004
       01  TRKG-CNT-RECORD                 PIC X(80).                   00990004
                                                                        01000004
       WORKING-STORAGE SECTION.                                         01010004
                                                                        01020004
                                                                        01030004
                                                                        01040004
      ***************************************************************** 01050004
      *  PROGRAM'S ACCUMULATORS                                       * 01060004
      ***************************************************************** 01070004
       01  PA-PROGRAM-ACCUMULATORS.                                     01080004
           05  PA-RECORDS-READ-LTONE       PIC 9(09)       VALUE ZEROES.01090006
           05  PA-RECORDS-READ-GTZERO      PIC 9(09)       VALUE ZEROES.01100006
           05  PA-RECORDS-UPDATED          PIC 9(09)       VALUE ZEROES.01110012
           05  PA-RECORDS-WRITTEN          PIC 9(09)       VALUE ZEROES.01120006
           05  PA-ADJ-NF-RECORDS-WRITTEN   PIC 9(09)       VALUE ZEROES.01130006
                                                                        01140004
      ***************************************************************** 01150004
      *  PROGRAM'S CONSTANTS                                          * 01160004
      ***************************************************************** 01170004
       01  PC-PROGRAM-CONSTANTS.                                        01180004
           05  PC-PROGRAM-NAME             PIC X(08)       VALUE        01190004
               'PXKUT098'.                                              01200004
                                                                        01210004
      ***************************************************************** 01220004
      *  PROGRAM'S SWITCHES                                           * 01230004
      ***************************************************************** 01240004
       01  PS-PROGRAM-SWITCHES.                                         01250004
           05  PS-END-OF-EXTR-INP-FILE-SW PIC X(01)        VALUE 'N'.   01260004
               88  PS-END-OF-EXTR-INP-FILE                 VALUE 'Y'.   01270004
               88  PS-NOT-END-OF-EXTR-INP-FILE             VALUE 'N'.   01280004
           05  PS-END-OF-ADJ-INP-FILE-SW PIC X(01)         VALUE 'N'.   01290004
               88  PS-END-OF-ADJ-INP-FILE                  VALUE 'Y'.   01300004
               88  PS-NOT-END-OF-ADJ-INP-FILE              VALUE 'N'.   01310004
           05  PS-ADDITIONAL-MARK-SW     PIC X(01)         VALUE 'N'.   01320017
               88  PS-ADDITIONAL-MARK                      VALUE 'Y'.   01330017
               88  PS-NOT-ADDITIONAL-MARK                  VALUE 'N'.   01340017
           05  PS-OPERATION-ATTEMPTED      PIC X(40)       VALUE SPACES.01350004
               88  PS-DEADLOCK-MSG                         VALUE        01360004
                   'MAXIMUM DEADLOCK COUNT EXCEEDED         '.          01370004
                                                                        01380004
      ***************************************************************** 01390004
      *  PROGRAM'S VARIABLES                                          * 01400004
      ***************************************************************** 01410004
       01  PV-PROGRAM-VARIABLES.                                        01420004
           05  PV-PARAGRAPH-NAME           PIC X(40)   VALUE SPACES.    01430004
           05  PV-OPERATION-MSG            PIC  X(60)  VALUE SPACES.    01440004
           05  PV-OPERATION-MSG-1          PIC  X(40)  VALUE SPACES.    01450004
           05  PV-OPERATION-MSG-2          PIC  X(40)  VALUE SPACES.    01460004
           05  PV-ADJ-QTY                  PIC S9(6)   VALUE ZEROES.    01470004
           05  PV-HOLD-ADJ-QTY             PIC S9(6)   VALUE ZEROES.    01480004
           05  PV-TRKG-RECORDS-WRITTEN     PIC  9(9)   VALUE ZEROES.    01490004
                                                                        01500004
                                                                        01510004
      ***************************************************************** 01520004
      *  ABEND CODE AREA                                              * 01530004
      ***************************************************************** 01540004
       01  ABEND-CODE                      PIC S9(04)  VALUE +0  COMP.  01550004
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.     01560004
           88  ABEND-4002-READ-ERROR                   VALUE +4002.     01570004
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.     01580004
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.     01590004
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.     01600004
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.     01610004
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.     01620004
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.     01630004
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.     01640004
           88  ABEND-4013-DB-ERROR                     VALUE +4013.     01650004
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.     01660004
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.     01670004
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.     01680004
                                                                        01690004
       01  WS-MSG-TEXT                     PIC X(512)      VALUE SPACES.01700004
       01  WS-MAX-SIZE                     PIC S9(09)      VALUE +512   01710004
                               COMP.                                    01720004
       01  WS-MSG-LENGTH                   PIC S9(09)      VALUE ZEROES 01730004
                               COMP.                                    01740004
                                                                        01750004
      ******************************************************************01760004
      **COPYBOOKS                                                       01770009
      ******************************************************************01780004
      ****ON HAND QTY EXTRACT INPUT RECORD LAYOUT                       01790004
      *    COPY PXRD098.                                                01800004
                                                                        01810004
      ****ON HAND QTY ADJUSTMENT INPUT RECORD LAYOUT                    01820004
           COPY PXRD124.                                                01830004
                                                                        01840004
      ****YELLOW TICKET EVENT EXTRACT OUTPUT RECORD LAYOUT              01850004
           COPY PXRD050.                                                01860004
                                                                        01870004
      ****TRACKING TABLE UPDATE OUTPUT RECORD LAYOUT                    01880004
           COPY PXRD126.                                                01890004
                                                                        01900004
      ****COUNT FILE OUTPUT RECORD LAYOUT                               01910004
           COPY PXRD045.                                                01920004
                                                                        01930004
                                                                        01940004
       PROCEDURE DIVISION.                                              01950004
                                                                        01960004
      ******************************************************************01970004
      *    MAINLINE LOGIC                                              *01980004
      ******************************************************************01990004
                                                                        02000004
       0000-MAINLINE.                                                   02010004
                                                                        02020004
           PERFORM 1000-INITIALIZATION.                                 02030004
                                                                        02040004
           PERFORM 2000-PRCSS-YLLW-TCK-EXTR-FL                          02050004
               UNTIL PS-END-OF-EXTR-INP-FILE                            02060015
                 AND PS-END-OF-ADJ-INP-FILE.                            02070015
                                                                        02080004
           PERFORM 9900-EOJ-LOGIC.                                      02090004
                                                                        02100004
           GOBACK.                                                      02110004
                                                                        02120004
      ******************************************************************02130004
      *    OPEN FILES AND READ FIRST RECORDS FROM INPUT FILES.         *02140004
      ******************************************************************02150004
       1000-INITIALIZATION.                                             02160004
                                                                        02170004
           OPEN INPUT  YLLW-TCK-EXTR-FILE                               02180004
                       YLLW-TCK-ADJ-FILE                                02190004
               OUTPUT  OH-QTY-ADJ-FILE                                  02200004
                       ADJ-NOT-FND-FILE                                 02210004
                       TRKG-UPD-FILE                                    02220004
                       TRKG-CNT-FILE.                                   02230004
                                                                        02240004
           PERFORM 6100-READ-YLLW-TCK-EXTR-FL.                          02250004
           PERFORM 6200-READ-YLLW-TCK-ADJ-FL.                           02260004
      * THIS IS NEEDED TO PREVENT A PROBLEM WHEN ADJ FILE IS EMPTY.     02270011
      * AS INITIALIZING THIS FIELD TO ZERO DOES NOT INITIALIZE THE SIGN 02280011
      * POSITION OF THE FIELD.                                          02290011
           IF PS-END-OF-ADJ-INP-FILE                                    02300011
             MOVE +0 TO PXRD124-OH-ADJ-QTY.                             02310011
           IF PS-END-OF-EXTR-INP-FILE                                   02320004
               DISPLAY ' *** NOTHING TO PROCESS ***'                    02330004
               DISPLAY ' '                                              02340004
           END-IF.                                                      02350004
                                                                        02360004
      ******************************************************************02370004
      *    2000-PRCSS-YLLW-TCK-EXTR-FL                                 *02380004
      *    THE INPUT FILES ARE IN SKU, STORE SEQUENCE.                 *02390004
      ******************************************************************02400004
       2000-PRCSS-YLLW-TCK-EXTR-FL.                                     02410004
                                                                        02420004
           MOVE PXRD124-OH-ADJ-QTY     TO PV-HOLD-ADJ-QTY.              02430011
           MOVE PXRD050-SKU-NBR        TO PXRD126-SKU-NBR.              02440011
           MOVE PXRD050-STR-NBR        TO PXRD126-STR-NBR.              02450011
           MOVE PXRD050-STR-EXPTED-QTY TO PXRD126-BEG-OH-QTY.           02460011
           MOVE 0                      TO PXRD126-END-OH-QTY            02470011
                                          PXRD126-END-SCND-QTY.         02480011
           IF PXRD050-PRCHG-TYP-CDE = 'MK2' OR 'FNL'                    02490017
             SET PS-ADDITIONAL-MARK TO TRUE                             02500017
           ELSE                                                         02510017
             SET PS-NOT-ADDITIONAL-MARK TO TRUE.                        02520017
           IF PXRD124-SKU-NBR = PXRD050-SKU-NBR                         02530004
             IF PXRD124-STR-NBR = PXRD050-STR-NBR                       02540004
               COMPUTE PV-ADJ-QTY = PXRD050-STR-EXPTED-QTY -            02550004
                 PV-HOLD-ADJ-QTY                                        02560004
               MOVE PV-ADJ-QTY TO PXRD050-STR-EXPTED-QTY                02570004
               ADD 1 TO PA-RECORDS-UPDATED                              02580004
      * WRITE ADJ FILE UNCONDITIONALLY ON THE MATCH, NO MATTER WHAT     02590010
               PERFORM 6300-WRITE-OH-QTY-ADJ-FL                         02600004
               PERFORM 6200-READ-YLLW-TCK-ADJ-FL                        02610004
               PERFORM 6100-READ-YLLW-TCK-EXTR-FL                       02620004
             ELSE                                                       02630004
      * NEVER WRITE ADJ FILE WHEN ONLY THE ADJUSTMENT ROW EXISTS.       02640014
      * WE CAN WRITE TO THE ADJ NOT FOUND FILE FOR <0. THESE RECS       02650014
      * WILL BE  PICKED UP BY PGM PXKUT107 IN JOB PXK007 AND PRCG       02660014
      * RECS WILL BE CREATED FOR THEM THERE.                            02670014
               IF PXRD124-STR-NBR < PXRD050-STR-NBR                     02680004
                 IF PV-HOLD-ADJ-QTY < 0                                 02690006
                   PERFORM 6400-WRITE-ADJ-NOT-FND-FILE                  02700004
                   MOVE PXRD124-SKU-NBR        TO PXRD126-SKU-NBR       02710006
                   MOVE PXRD124-STR-NBR        TO PXRD126-STR-NBR       02720006
                   MOVE 0                      TO PXRD126-END-OH-QTY    02730006
                                                  PXRD126-END-SCND-QTY  02740006
                                                  PXRD126-BEG-OH-QTY    02750020
                   PERFORM 6500-WRITE-TRKG-UPD-FILE                     02760018
                 END-IF                                                 02770004
                 PERFORM 6200-READ-YLLW-TCK-ADJ-FL                      02780004
               ELSE                                                     02790004
      * WRITE ADJ FILE WHEN QUANTITIES > 0 WHEN ONLY THE INPUT ROW      02800010
      * EXISTS                                                          02810010
                 IF PXRD050-STR-EXPTED-QTY > 0                          02820004
                   PERFORM 6300-WRITE-OH-QTY-ADJ-FL                     02830004
                 END-IF                                                 02840004
                 PERFORM 6100-READ-YLLW-TCK-EXTR-FL                     02850004
           ELSE                                                         02860004
      * NEVER WRITE ADJ FILE WHEN ONLY THE ADJUSTMENT ROW EXISTS        02870010
      * WE CAN WRITE TO THE ADJ NOT FOUND FILE FOR <0. THESE RECS       02880014
      * WILL BE  PICKED UP BY PGM PXKUT107 IN JOB PXK007 AND PRCG       02890014
      * RECS WILL BE CREATED FOR THEM THERE.                            02900014
           IF PXRD124-SKU-NBR < PXRD050-SKU-NBR                         02910004
             IF PV-HOLD-ADJ-QTY < 0                                     02920007
               PERFORM 6400-WRITE-ADJ-NOT-FND-FILE                      02930011
               MOVE PXRD124-SKU-NBR        TO PXRD126-SKU-NBR           02940006
               MOVE PXRD124-STR-NBR        TO PXRD126-STR-NBR           02950006
               MOVE 0                      TO PXRD126-END-OH-QTY        02960006
                                              PXRD126-END-SCND-QTY      02970006
                                              PXRD126-BEG-OH-QTY        02980020
               PERFORM 6500-WRITE-TRKG-UPD-FILE                         02990006
             END-IF                                                     03000004
             PERFORM 6200-READ-YLLW-TCK-ADJ-FL                          03010004
           ELSE                                                         03020004
      * WRITE ADJ FILE WHEN QUANTITIES > 0 WHEN ONLY THE INPUT ROW      03030010
      * EXISTS                                                          03040010
             IF PXRD050-STR-EXPTED-QTY > 0                              03050010
               PERFORM 6300-WRITE-OH-QTY-ADJ-FL                         03060004
             END-IF                                                     03070004
             PERFORM 6100-READ-YLLW-TCK-EXTR-FL.                        03080004
                                                                        03090004
      ******************************************************************03100004
      *  READ EXTRACT INPUT FILE                                        03110004
      ******************************************************************03120004
       6100-READ-YLLW-TCK-EXTR-FL.                                      03130004
                                                                        03140004
           IF PS-NOT-END-OF-EXTR-INP-FILE                               03150015
             READ YLLW-TCK-EXTR-FILE INTO PXRD050-DATA-RECORD           03160015
                 AT END SET PS-END-OF-EXTR-INP-FILE TO TRUE.            03170015
                                                                        03180004
           IF PS-END-OF-EXTR-INP-FILE                                   03190015
               MOVE ALL '9' TO PXRD050-SKU-NBR                          03200015
                               PXRD050-STR-NBR                          03210015
           ELSE                                                         03220015
             IF PS-NOT-END-OF-EXTR-INP-FILE                             03230015
                 IF PXRD050-STR-EXPTED-QTY > 0                          03240015
                     ADD 1 TO PA-RECORDS-READ-GTZERO                    03250015
                 ELSE                                                   03260015
                     ADD 1 TO PA-RECORDS-READ-LTONE                     03270015
                 END-IF                                                 03280015
             END-IF                                                     03290015
           END-IF.                                                      03300004
                                                                        03310004
      ******************************************************************03320004
      *  READ ADJUSTMENT INPUT FILE                                     03330004
      ******************************************************************03340004
       6200-READ-YLLW-TCK-ADJ-FL.                                       03350004
           IF PS-NOT-END-OF-ADJ-INP-FILE                                03360015
             READ YLLW-TCK-ADJ-FILE INTO PXRD124-OH-ADJ-RECORD          03370015
                 AT END SET PS-END-OF-ADJ-INP-FILE TO TRUE.             03380015
                                                                        03390004
           IF PS-END-OF-ADJ-INP-FILE                                    03400004
               MOVE ALL '9' TO PXRD124-SKU-NBR                          03410004
                               PXRD124-STR-NBR                          03420004
           END-IF.                                                      03430004
                                                                        03440004
      ******************************************************************03450004
      *  WRITE OH QTY ADJ FILE                                          03460004
      ******************************************************************03470004
       6300-WRITE-OH-QTY-ADJ-FL.                                        03480004
           PERFORM 6500-WRITE-TRKG-UPD-FILE.                            03490031
           WRITE OH-QTY-ADJ-REC                                         03500004
             FROM PXRD050-DATA-RECORD.                                  03510004
           ADD 1 TO PA-RECORDS-WRITTEN.                                 03520004
                                                                        03530004
      ******************************************************************03540004
      *  WRITE ADJUSTMENTS NOT FOUND FILE                               03550004
      *  THIS FILE IS WRITTEN TO ONLY FOR ADDITIONAL MARKS              03560017
      ******************************************************************03570004
       6400-WRITE-ADJ-NOT-FND-FILE.                                     03580004
           IF PS-ADDITIONAL-MARK                                        03590017
             WRITE ADJ-NOT-FND-REC                                      03600017
               FROM PXRD124-OH-ADJ-RECORD                               03610017
             ADD 1 TO PA-ADJ-NF-RECORDS-WRITTEN.                        03620017
                                                                        03630004
      ******************************************************************03640004
      *  WRITE OUT TRACKING UPDATE RECORD.                              03650004
      *  THIS FILE IS WRITTEN TO ONLY FOR ADDITIONAL MARKS              03660017
      ******************************************************************03670004
       6500-WRITE-TRKG-UPD-FILE.                                        03680004
                                                                        03690004
           IF PS-ADDITIONAL-MARK                                        03700017
             WRITE TRKG-UPD-RECORD FROM PXRD126-TRKG-UPD-RECORD         03710017
             ADD 1 TO PV-TRKG-RECORDS-WRITTEN.                          03720017
                                                                        03730004
      ******************************************************************03740004
      *  WRITE OUT TRACKING UPDATE COUNT RECORD                         03750004
      ******************************************************************03760004
       6600-WRITE-TRKG-COUNT-FILE.                                      03770004
                                                                        03780004
           MOVE PV-TRKG-RECORDS-WRITTEN     TO PXRD045-COUNT.           03790004
           WRITE TRKG-CNT-RECORD FROM PXRD045-COUNT-RECORD.             03800004
                                                                        03810004
      ******************************************************************03820004
      *    9900-EOJ-LOGIC.                                              03830004
      *      THIS IS PERFORMED AT END OF INPUT FILE.                    03840004
      *      PURPOSE: CLOSE INPUT FILE,                                 03850004
      *      DISPLAY RECORD COUNTS.                                     03860004
      ******************************************************************03870004
       9900-EOJ-LOGIC.                                                  03880004
                                                                        03890004
           PERFORM 6600-WRITE-TRKG-COUNT-FILE.                          03900004
           CLOSE YLLW-TCK-EXTR-FILE                                     03910004
                 YLLW-TCK-ADJ-FILE                                      03920004
                 OH-QTY-ADJ-FILE                                        03930004
                 ADJ-NOT-FND-FILE                                       03940004
                 TRKG-UPD-FILE                                          03950004
                 TRKG-CNT-FILE.                                         03960004
                                                                        03970004
           DISPLAY ' '.                                                 03980004
           DISPLAY 'RECORDS READ > 0   ' PA-RECORDS-READ-GTZERO.        03990004
           DISPLAY 'RECORDS READ < 1   ' PA-RECORDS-READ-LTONE.         04000004
           DISPLAY 'RECORDS UPDATED    ' PA-RECORDS-UPDATED.            04010004
           DISPLAY 'RECORDS WRITTEN    ' PA-RECORDS-WRITTEN.            04020004
           DISPLAY 'RECORDS ADJ NF     ' PA-ADJ-NF-RECORDS-WRITTEN.     04030006
           DISPLAY 'RECORDS ADJ TRKG   ' PV-TRKG-RECORDS-WRITTEN.       04040006
           DISPLAY '**------ END ' PC-PROGRAM-NAME ' ------**'.         04050004
                                                                        04060004
      ******************************************************************04070004
      *    END OF SOURCE CODE FOR PXKUT098                             *04080004
      ******************************************************************04090004
