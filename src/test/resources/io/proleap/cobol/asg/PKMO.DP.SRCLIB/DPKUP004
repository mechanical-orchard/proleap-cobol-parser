       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    DPKUP004.                                                 
       AUTHOR.        JEFF ROUBIK.                                              
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  03/31/2000.                                               
       DATE-COMPILED.                                                           
      ******************************************************************        
      ** THIS PROGRAM WILL LOAD MNEMONIC-RELATED INFORMATION INTO THE **        
      ** ARCHITECTURE TABLES.  THIS IS PART OF THE PROCESS USED TO    **        
      ** BACKOUT AN ARCHITECTURE INFORMATION MOVE FROM ONE REGION TO  *         
      ** ANOTHER.                                                     **        
      **                                                              **        
      **  CHANGES:                                                    **        
      **    03-31-2000  (JEFF ROUBIK)  --  WR# 21021                  **        
      **    -  CREATED PROGRAM.                                       **        
      **                                                              **        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BACKOUT-FILE                 ASSIGN TO INP01.                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BACKOUT-FILE                                                         
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
                                                                                
       01  BACKOUT-RECORD                  PIC X(200).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  IV-INDICATOR-VARIABLES.                                              
           05  IV-DESC                 PIC S9(04)  VALUE +0                     
                                                   COMP.                        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARAGRAPH-NAME       PIC  X(30)  VALUE SPACE.                 
           05  PV-DB2-OPERATION-ATTEMPTED                                       
                                       PIC  X(30)  VALUE SPACE.                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-BACKOUT-FILE-SW                                        
                                       PIC X(01)   VALUE 'N'.                   
               88  PS-END-OF-BACKOUT-FILE          VALUE 'Y'.                   
           05  PS-SET-RC-8-SW          PIC  X(01)  VALUE 'N'.                   
               88  PS-SET-RC-8                     VALUE 'Y'.                   
                                                                                
       01  ABEND-CODE                  PIC S9(04)  VALUE +0                     
                                                   COMP SYNC.                   
      *                                                                         
      *  BACKOUT FILE RECORD DESCRIPTION                                        
      *                                                                         
           COPY DPRD502.                                                        
                                                                                
      *                                                                         
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004                
      *                                                                         
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE MNEMONIC TABLE                      
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNMNC                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE MENUITEM TABLE                      
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TMNUITM                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  SQL AND COBOL DECLARATIONS FOR THE CICSROUTE TABLE                     
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TCICSRTE                                                 
           END-EXEC.                                                            
                                                                                
      *                                                                         
      ** STANDARD LAYOUT FOR THE SQL COMMUNICATIONS AREA                        
      *                                                                         
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZE.                                             
                                                                                
           PERFORM 2000-PROCESS-BACKOUT-FILE                                    
               UNTIL PS-END-OF-BACKOUT-FILE.                                    
                                                                                
           PERFORM 9000-EOJ-ROUTINE.                                            
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ******************************************************************        
      ** OPEN INPUT FILES AND DO PRIMING READS ON THEM.               **        
      ******************************************************************        
       1000-INITIALIZE.                                                         
           DISPLAY ' **** DPKUP004 - ARCHITECTURE BACKOUT ****'.                
           OPEN INPUT BACKOUT-FILE.                                             
                                                                                
           PERFORM 2010-READ-BACKOUT-INPUT.                                     
                                                                                
                                                                                
      ******************************************************************        
      ** UPDATE ARCHITECTURE WITH BACKOUT RECORDS.                    **        
      ******************************************************************        
       2000-PROCESS-BACKOUT-FILE.                                               
           EVALUATE TRUE                                                        
             WHEN DP502-MNEMONIC                                                
                 PERFORM 2100-BACKOUT-MNEMONIC                                  
             WHEN DP502-MENUITEM                                                
                 PERFORM 2200-BACKOUT-MENUITEM                                  
             WHEN DP502-CICSROUTE                                               
                 PERFORM 2300-BACKOUT-CICSROUTE                                 
             WHEN OTHER                                                         
                 MOVE '2000-PROCESS-BACKOUT-FILE'                               
                                         TO PV-PARAGRAPH-NAME                   
                 MOVE 'UNKNOWN DP502-TABLE VALUE'                               
                                         TO PV-DB2-OPERATION-ATTEMPTED          
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
           PERFORM 2010-READ-BACKOUT-INPUT.                                     
                                                                                
                                                                                
       2010-READ-BACKOUT-INPUT.                                                 
           READ BACKOUT-FILE INTO DP502-BACKOUT-RECORD                          
              AT END SET PS-END-OF-BACKOUT-FILE TO TRUE.                        
                                                                                
                                                                                
                                                                                
       2100-BACKOUT-MNEMONIC.                                                   
           MOVE DP502-BACKOUT-DATA TO DCLTMNEMONIC.                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN DP502-CHANGE                                                  
                 PERFORM 2110-CHANGE-MNEMONIC                                   
             WHEN DP502-DELETE                                                  
                 PERFORM 2120-DELETE-MNEMONIC                                   
             WHEN DP502-ADD                                                     
                 PERFORM 2130-ADD-MNEMONIC                                      
             WHEN OTHER                                                         
                 MOVE '2100-BACKOUT-MNEMONIC'                                   
                                   TO PV-PARAGRAPH-NAME                         
                 MOVE 'UNKNOWN DP502-BACKOUT-ACTION VALUE'                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2110-CHANGE-MNEMONIC.                                                    
           EXEC SQL                                                             
               UPDATE TMNEMONIC                                                 
                  SET TRANS_ID_CODE      = :TMNEMONIC-TRANS-ID-CODE             
                    , AVAIL_INDCT        = :TMNEMONIC-AVAIL-INDCT               
                    , CMAREA_FRMT_CODE   = :TMNEMONIC-CMAREA-FRMT-CODE          
                    , SAVE_CMAREA_INDCT  = :TMNEMONIC-SAVE-CMAREA-INDCT         
                    , CMAREA_LNGTH_MSRMT = :TMNEMONIC-CMAREA-LNGTH-MSRMT        
                    , PANEL_TITLE_NAME   = :TMNEMONIC-PANEL-TITLE-NAME          
                    , LAST_UPDATE_TMESTP = :TMNEMONIC-LAST-UPDATE-TMESTP        
                    , GLBL_ACCS_IND      = :TMNEMONIC-GLBL-ACCS-IND             
                    , PGM_NAME           = :TMNEMONIC-PGM-NAME                  
                    , SCRN_TYP_CDE       = :TMNEMONIC-SCRN-TYP-CDE              
                    , UPD_VSAM_IND       = :TMNEMONIC-UPD-VSAM-IND              
                    , DB2_PKG_CDE        = :TMNEMONIC-DB2-PKG-CDE               
                    , CMNT_TXT           = :TMNEMONIC-CMNT-TXT                  
                    , ACRT_LOC_REQR_IND  = :TMNEMONIC-ACRT-LOC-REQR-IND         
                WHERE MNEMONIC_CODE      = :TMNEMONIC-MNEMONIC-CODE             
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2110-CHANGE-MNEMONIC'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD UPDATE INTO TMNEMONIC TABLE'                           
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2120-DELETE-MNEMONIC.                                                    
           EXEC SQL                                                             
               DELETE FROM TMNEMONIC                                            
               WHERE MNEMONIC_CODE = :TMNEMONIC-MNEMONIC-CODE                   
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2120-DELETE-MNEMONIC'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD DELETE FROM TMNEMONIC TABLE'                           
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2130-ADD-MNEMONIC.                                                       
           EXEC SQL                                                             
               INSERT                                                           
                 INTO TMNEMONIC                                                 
                     (MNEMONIC_CODE,                                            
                      TRANS_ID_CODE,                                            
                      AVAIL_INDCT,                                              
                      CMAREA_FRMT_CODE,                                         
                      SAVE_CMAREA_INDCT,                                        
                      CMAREA_LNGTH_MSRMT,                                       
                      PANEL_TITLE_NAME,                                         
                      LAST_UPDATE_TMESTP,                                       
                      GLBL_ACCS_IND,                                            
                      PGM_NAME,                                                 
                      SCRN_TYP_CDE,                                             
                      UPD_VSAM_IND,                                             
                      DB2_PKG_CDE,                                              
                      CMNT_TXT,                                                 
                      ACRT_LOC_REQR_IND)                                        
               VALUES                                                           
                    (:TMNEMONIC-MNEMONIC-CODE,                                  
                     :TMNEMONIC-TRANS-ID-CODE,                                  
                     :TMNEMONIC-AVAIL-INDCT,                                    
                     :TMNEMONIC-CMAREA-FRMT-CODE,                               
                     :TMNEMONIC-SAVE-CMAREA-INDCT,                              
                     :TMNEMONIC-CMAREA-LNGTH-MSRMT,                             
                     :TMNEMONIC-PANEL-TITLE-NAME,                               
                     :TMNEMONIC-LAST-UPDATE-TMESTP,                             
                     :TMNEMONIC-GLBL-ACCS-IND,                                  
                     :TMNEMONIC-PGM-NAME,                                       
                     :TMNEMONIC-SCRN-TYP-CDE,                                   
                     :TMNEMONIC-UPD-VSAM-IND,                                   
                     :TMNEMONIC-DB2-PKG-CDE,                                    
                     :TMNEMONIC-CMNT-TXT,                                       
                     :TMNEMONIC-ACRT-LOC-REQR-IND)                              
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2130-ADD-MNEMONIC'                                         
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TMNEMONIC TABLE'                           
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       2200-BACKOUT-MENUITEM.                                                   
           MOVE DP502-BACKOUT-DATA TO DCLTMENUITEM.                             
                                                                                
           EVALUATE TRUE                                                        
             WHEN DP502-CHANGE                                                  
                 PERFORM 2210-CHANGE-MENUITEM                                   
             WHEN DP502-DELETE                                                  
                 PERFORM 2220-DELETE-MENUITEM                                   
             WHEN DP502-ADD                                                     
                 PERFORM 2230-ADD-MENUITEM                                      
             WHEN OTHER                                                         
                 MOVE '2200-BACKOUT-MENUITEM'                                   
                                   TO PV-PARAGRAPH-NAME                         
                 MOVE 'UNKNOWN DP502-BACKOUT-ACTION VALUE'                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2210-CHANGE-MENUITEM.                                                    
           IF TMENUITEM-DESC = SPACE                                            
               MOVE -1    TO IV-DESC                                            
           ELSE                                                                 
               MOVE +1    TO IV-DESC                                            
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               UPDATE TMENUITEM                                                 
                  SET DESC               = :TMENUITEM-DESC:IV-DESC              
                    , SEQUENCE_NMBR      = :TMENUITEM-SEQUENCE-NMBR             
                    , LAST_UPDATE_TMESTP = :TMENUITEM-LAST-UPDATE-TMESTP        
                    , IN_APLCTN_COMM_CDE = :TMENUITEM-IN-APLCTN-COMM-CDE        
                WHERE MENU_NAME          = :TMENUITEM-MENU-NAME                 
                  AND ITEM_MNEMONIC_CODE = :TMENUITEM-ITEM-MNEMONIC-CODE        
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2210-CHANGE-MENUITEM'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD UPDATE ON TMENUITEM TABLE'                             
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2220-DELETE-MENUITEM.                                                    
           EXEC SQL                                                             
               DELETE FROM TMENUITEM                                            
                WHERE MENU_NAME          = :TMENUITEM-MENU-NAME                 
                  AND ITEM_MNEMONIC_CODE = :TMENUITEM-ITEM-MNEMONIC-CODE        
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2220-DELETE-MENUITEM'                                      
                                       TO PV-PARAGRAPH-NAME                     
               MOVE 'BAD DELETE FROM TMENUITEM TABLE'                           
                                       TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2230-ADD-MENUITEM.                                                       
           IF TMENUITEM-DESC = SPACE                                            
               MOVE -1    TO IV-DESC                                            
           ELSE                                                                 
               MOVE +1    TO IV-DESC                                            
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
               INSERT                                                           
                 INTO TMENUITEM                                                 
                     (MENU_NAME,                                                
                      ITEM_MNEMONIC_CODE,                                       
                      DESC,                                                     
                      SEQUENCE_NMBR,                                            
                      LAST_UPDATE_TMESTP,                                       
                      IN_APLCTN_COMM_CDE)                                       
               VALUES                                                           
                    (:TMENUITEM-MENU-NAME,                                      
                     :TMENUITEM-ITEM-MNEMONIC-CODE,                             
                     :TMENUITEM-DESC:IV-DESC,                                   
                     :TMENUITEM-SEQUENCE-NMBR,                                  
                     :TMENUITEM-LAST-UPDATE-TMESTP,                             
                     :TMENUITEM-IN-APLCTN-COMM-CDE)                             
           END-EXEC.                                                            
                                                                                
           IF  SQLCODE = ZERO                                                   
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2230-ADD-MENUITEM'                                         
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TMENUITEM TABLE'                           
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       2300-BACKOUT-CICSROUTE.                                                  
           MOVE DP502-BACKOUT-DATA TO DCLTCICSROUTE.                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN DP502-CHANGE                                                  
                 PERFORM 2310-CHANGE-CICSROUTE                                  
             WHEN DP502-DELETE                                                  
                 PERFORM 2320-DELETE-CICSROUTE                                  
             WHEN DP502-ADD                                                     
                 PERFORM 2330-ADD-CICSROUTE                                     
             WHEN OTHER                                                         
                 MOVE '2300-BACKOUT-CICSROUTE'                                  
                                   TO PV-PARAGRAPH-NAME                         
                 MOVE 'UNKNOWN DP502-BACKOUT-ACTION VALUE'                      
                                   TO PV-DB2-OPERATION-ATTEMPTED                
                 PERFORM 9100-SQL-ABEND                                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2310-CHANGE-CICSROUTE.                                                   
           EXEC SQL                                                             
              UPDATE TCICSROUTE                                                 
                 SET DEST_MNEMONIC_CODE = :TCICSROUTE-DEST-MNEMONIC-CODE        
                   , ACTION_CODE        = :TCICSROUTE-ACTION-CODE               
                   , LAST_UPDATE_TMESTP = :TCICSROUTE-LAST-UPDATE-TMESTP        
                   , ROUTE_STACK_INDCT  = :TCICSROUTE-ROUTE-STACK-INDCT         
                   , FUNC_KEY_LBL_NAME  = :TCICSROUTE-FUNC-KEY-LBL-NAME         
                   , IN_APLCTN_COMM_CDE = :TCICSROUTE-IN-APLCTN-COMM-CDE        
                   , FUNC_KY_DISPN_IND  = :TCICSROUTE-FUNC-KY-DISPN-IND         
               WHERE MNEMONIC_CODE      = :TCICSROUTE-MNEMONIC-CODE             
                 AND AID_SEQUENCE_NMBR  = :TCICSROUTE-AID-SEQUENCE-NMBR         
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2310-CHANGE-CICSROUTE'                                     
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD UPDATE ON TCICSROUTE TABLE'                            
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2320-DELETE-CICSROUTE.                                                   
           EXEC SQL                                                             
               DELETE FROM TCICSROUTE                                           
                WHERE MNEMONIC_CODE = :TCICSROUTE-MNEMONIC-CODE                 
           END-EXEC.                                                            
                                                                                
           IF ((SQLCODE = ZERO)                                                 
           OR  (SQLCODE = +100))                                                
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2320-DELETE-CICSROUTE'                                     
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD DELETE FROM TCICSROUTE TABLE'                          
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
       2330-ADD-CICSROUTE.                                                      
           EXEC SQL                                                             
               INSERT                                                           
                 INTO TCICSROUTE                                                
                     (MNEMONIC_CODE,                                            
                      AID_SEQUENCE_NMBR,                                        
                      DEST_MNEMONIC_CODE,                                       
                      ACTION_CODE,                                              
                      LAST_UPDATE_TMESTP,                                       
                      ROUTE_STACK_INDCT,                                        
                      FUNC_KEY_LBL_NAME,                                        
                      IN_APLCTN_COMM_CDE,                                       
                      FUNC_KY_DISPN_IND)                                        
               VALUES                                                           
                    (:TCICSROUTE-MNEMONIC-CODE,                                 
                     :TCICSROUTE-AID-SEQUENCE-NMBR,                             
                     :TCICSROUTE-DEST-MNEMONIC-CODE,                            
                     :TCICSROUTE-ACTION-CODE,                                   
                     :TCICSROUTE-LAST-UPDATE-TMESTP,                            
                     :TCICSROUTE-ROUTE-STACK-INDCT,                             
                     :TCICSROUTE-FUNC-KEY-LBL-NAME,                             
                     :TCICSROUTE-IN-APLCTN-COMM-CDE,                            
                     :TCICSROUTE-FUNC-KY-DISPN-IND)                             
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
               CONTINUE                                                         
           ELSE                                                                 
               MOVE '2330-ADD-CICSROUTE'                                        
                       TO PV-PARAGRAPH-NAME                                     
               MOVE 'BAD INSERT INTO TCICSROUTE TABLE'                          
                       TO PV-DB2-OPERATION-ATTEMPTED                            
               PERFORM 9100-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       9000-EOJ-ROUTINE.                                                        
           IF  PS-SET-RC-8                                                      
               MOVE +0008    TO RETURN-CODE                                     
               DISPLAY '****************************************'               
               DISPLAY '** AN ERROR OCCURRED WHILE UPDATING   **'               
               DISPLAY '** MNEMONIC(S) TO THE TARGET TABLES.  **'               
               DISPLAY '** SEE PREVIOUS MESSAGES FOR DETAILS. **'               
               DISPLAY '****************************************'               
           ELSE                                                                 
               MOVE +0000    TO RETURN-CODE                                     
               DISPLAY '****************************************'               
               DISPLAY '** ALL MNEMONICS WERE SUCCESSFULLY    **'               
               DISPLAY '** BACKED OUT OF THE TARGET TABLES.   **'               
               DISPLAY '****************************************'               
           END-IF.                                                              
                                                                                
           CLOSE BACKOUT-FILE.                                                  
                                                                                
                                                                                
      ******************************************************************        
      **                                                              **        
      ******************************************************************        
       9100-SQL-ABEND.                                                          
           DISPLAY '** ABEND     **'.                                           
           DISPLAY '** PROGRAM   ** = DPKUP004'.                                
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
                                                                                
      ******************************************************************        
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
      * MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
      * 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
      * FORMAT.                                                                 
      ******************************************************************        
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
