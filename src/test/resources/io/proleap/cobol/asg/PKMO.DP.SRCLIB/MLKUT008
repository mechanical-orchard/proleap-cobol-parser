       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.     MLKUT008.                                        00020026
       INSTALLATION.   KOHLS DEPARTMENT STORES.                         00030000
       DATE-WRITTEN.   02/23/2006.                                      00040000
       DATE-COMPILED.                                                   00050000
                                                                        00060000
      ******************************************************************00070000
      *PURPOSE:  THE MODULE TAKES IN THE CUTOFF WEEK FAST TRANSFERS TO  00080027
      *          DETERMINE IF THE DOLLARS SHOULD BOOKED TO MERCH        00090027
      *          LEDGER.                                                00100027
      *                 (SEE THE FLOW SECTION FOR MORE DETAILS)         00110000
      *                                                                 00120000
      *CHG ID/                                                          00130000
      *WR/PROJ DATE       PGMR        DESCRIPTION                       00140000
      *------- ---------- ----------- ----------------------------------00150000
W29908*W29908  02-23-2006 ROD JANSSEN THE NEW TRANSFER PROCESS          00160000
W29908*                               (CREATION)                        00170000
W33304*W33304  11-17-2008 NIJESH/     HOLDING INVENTORY LEDGER OPEN             
W33304*                     INFOSYS   ACROSS PERIODS (HILO).                    
PM3403*PM3403  04-20-2012 S.JACKSON   LG RETAIL RECOMPILE ONLY(TFRD054)    CL**6
PM3403*                                                                    CL**6
      ******************************************************************00180000
                                                                        00190000
       ENVIRONMENT DIVISION.                                            00200000
       CONFIGURATION SECTION.                                           00210000
       INPUT-OUTPUT SECTION.                                            00220000
       FILE-CONTROL.                                                    00230000
                                                                        00240000
           SELECT TRANSFER-DATA-FILE                                    00250000
               ASSIGN TO INP01.                                         00260000
                                                                        00270000
           SELECT TRANSFER-PROCESS-FILE                                 00280000
               ASSIGN TO OUT01.                                         00290000
                                                                        00300000
           SELECT TRANSFER-RECYCLE-FILE                                 00310000
               ASSIGN TO OUT02.                                         00320000
                                                                        00330000
       DATA DIVISION.                                                   00340000
       FILE SECTION.                                                    00350000
                                                                        00360000
       FD  TRANSFER-DATA-FILE                                           00370000
           RECORDING MODE IS F                                          00380000
           LABEL RECORDS ARE STANDARD                                   00390000
           BLOCK CONTAINS 0 RECORDS.                                    00400000
       01  TRANSFER-DATA-RECORD     PIC X(120).                         00410003
                                                                        00420000
       FD  TRANSFER-PROCESS-FILE                                        00430000
           LABEL RECORDS ARE OMITTED                                    00440000
           RECORDING MODE IS F                                          00450000
           BLOCK CONTAINS 0 RECORDS.                                    00460000
       01  TFR-PROCESS-RECORD       PIC X(120).                         00470003
                                                                        00480000
       FD  TRANSFER-RECYCLE-FILE                                        00490000
           LABEL RECORDS ARE OMITTED                                    00500000
           RECORDING MODE IS F                                          00510000
           BLOCK CONTAINS 0 RECORDS.                                    00520000
       01  TFR-RECYCLE-RECORD       PIC X(120).                         00530003
                                                                        00540000
      /*****************************************************************00550000
       WORKING-STORAGE SECTION.                                         00560000
      ******************************************************************00570000
                                                                        00580000
       01  PA-PROGRAM-ACCUMULATORS.                                     00590000
           05  PA-EXTRACT-RECS-READ    PIC 9(09) VALUE ZEROES.          00600000
                                                                        00610000
       01  PC-PROGRAM-CONSTANTS-AREA.                                   00620000
           05  PC-PROGRAM-NAME         PIC X(08)  VALUE 'MLKUT008'.     00630026
           05  PC-JOB-NAME             PIC X(08)  VALUE 'MLK124  '.     00640001
           05  PC-MAX-RETRIES          PIC S9(04) VALUE +3 COMP SYNC.   00650000
                                                                        00660000
       01  PV-PROGRAM-VARIABLES-AREA.                                   00670000
           05  PV-STR-NDX              PIC S9(05) COMP-3 VALUE ZEROS.   00680001
           05  PV-EDIT-LOC-NBR         PIC S9(04) COMP VALUE ZEROS.     00681006
           05  PV-PARAGRAPH-NAME       PIC X(30) VALUE SPACES.          00690001
           05  PV-ABEND-MESSAGE        PIC X(75) VALUE SPACES.          00700001
           05  PV-OPERATION-ATTEMPTED  PIC X(30) VALUE SPACES.          00710001
           05  PV-RETRY-COUNT          PIC S9(04) COMP VALUE ZEROS.     00720001
           05  PV-OPER-ATTEMPTED       PIC X(25)  VALUE SPACES.         00730001
           05  PV-RECORDS-READ         PIC S9(15) COMP-3 VALUE ZEROS.   00740001
           05  PV-RECORDS-ACCEPTED     PIC S9(15) COMP-3 VALUE ZEROS.   00750001
           05  PV-RECORDS-RECYCLED     PIC S9(15) COMP-3 VALUE ZEROS.   00751006
           05  PV-TINVPAR-ROW-COUNT    PIC S9(15) COMP-3 VALUE ZEROS.   00752008
                                                                        00760000
       01  PS-SWITCHES-AREA.                                            00770000
           05  PS-END-OF-TRANSFER-DATA   PIC X(01)  VALUE 'N'.          00780001
               88  END-OF-TRANSFER-DATA             VALUE 'Y'.          00790001
           05  PS-TINVPAR-DONE-SW        PIC X(01)  VALUE 'N'.          00800001
               88  TINVPAR-PROCESS-COMPLETE         VALUE 'Y'.          00810001
           05  PS-TINVPAR-FOUND-SW       PIC X(01)  VALUE 'N'.          00820001
               88  INVENTORY-STORE-FOUND            VALUE 'Y'.          00830001
           05  PS-RECYCLE-TFR-SW         PIC X(01)  VALUE 'N'.          00831004
               88  PS-RECYCLE-FAST-TRANSFER         VALUE 'Y'.          00832004
           05  PS-PROCESS-TFR-SW         PIC X(01)  VALUE 'N'.          00833004
               88  PS-PROCESS-FAST-TRANSFER         VALUE 'Y'.          00834004
                                                                        00840000
       01  ABEND-CODE                    PIC S9(04) COMP VALUE ZEROS.   00850001
           88  ABEND-4013                                VALUE +4013.   00870001
                                                                        00880000
       01  WS-STORE-TABLE.                                              00890001
           05  WS-STORE-TABLE-ELEMENTS   OCCURS 9999 TIMES.             00900001
               10  WS-LOC-NBR            PIC S9(04) COMP VALUE ZEROS.   00910001
W33304         10  WS-INV-ID             PIC S9(09) COMP VALUE ZEROS.           
               10  WS-ACTL-INV-DTE       PIC X(10) VALUE SPACES.        00911029
               10  WS-ACTL-LDG-CTOF-TMST PIC X(26) VALUE SPACES.        00912008
               10  WS-OPN-FSCL-WK-DTE    PIC X(10) VALUE SPACES.        00913029
                                                                        00920001
      ******************************************************************00930000
      * COPYBOOK - INVENTORY SNAPSHOT TO ML FILE FORMAT                 00940000
      ******************************************************************00950000
           COPY TFRD054.                                                00960003
                                                                        00970000
      ******************************************************************00980000
      * COPYBOOK -                                                      00990000
      *    DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA      01000000
      * MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS.     01010000
      ******************************************************************01020000
           COPY DPWS004.                                                01030000
                                                                        01040000
      ******************************************************************01050000
           EXEC SQL                                                     01060000
               INCLUDE SQLCA                                            01070000
           END-EXEC.                                                    01080000
                                                                        01090000
      ******************************************************************01100000
      *    TINVPAR - INVENTORY PARAMETERS TABLE DCLGEN                  01110000
      ******************************************************************01120000
           EXEC SQL                                                     01130000
               INCLUDE TINVPAR                                          01140000
           END-EXEC.                                                    01150000
                                                                        01160000
      ******************************************************************01170000
      *    TMLCNTL - MERCH LEDGER CONTROL TABLE DCLGEN                  01180000
      ******************************************************************01190000
           EXEC SQL                                                     01200000
               INCLUDE TMLCNTL                                          01210000
           END-EXEC.                                                    01220000
                                                                        01230000
W33304******************************************************************        
W33304*    TINCNTL - INVENTORY CONTROL TABLE DCLGEN                             
W33304******************************************************************        
W33304     EXEC SQL                                                             
W33304         INCLUDE TINCNTL                                                  
W33304     END-EXEC.                                                            
W33304                                                                          
      ******************************************************************01310001
      *    TINVPAR_CSR - TO DEFINE STORE PERFORMING INVENTORY           01320001
      ***************************************************************** 01330001
                                                                        01340001
           EXEC SQL                                                     01350001
               DECLARE TINVPAR_CSR CURSOR FOR                           01360001
W33304           SELECT DISTINCT(LOC_NBR),                                      
W33304                  B.INV_ID,                                               
                        ACTL_INV_DTE,                                   01370129
                        ACTL_LDG_CTOF_TMST,                             01370208
                        C.OPN_FSCL_WK_DTE                               01371029
                 FROM TMLCNTL C,                                        01380001
                      TINVPAR B                                         01400001
W33304               ,TINCNTL A                                                 
W33304           WHERE ACTV_IND = 'Y'                                           
W33304             AND A.INV_ID = B.INV_ID                                      
W33304             AND LOC_INV_STAT_CDE = 'IN'                                  
                   AND ACTL_FIN_BK_DTE = '9999-09-09'                   01420001
                   AND ACTL_LDG_CTOF_TMST < '9999-12-31-23.59.59.999999'01441005
                 WITH UR                                                01450001
           END-EXEC.                                                    01460001
                                                                        01470001
      ******************************************************************01480000
      *FLOW:    THE MODULE SEQUENTIALLY READS THE FAST TRANSFER RECORDS 01490027
      *      TO DETERMINE IF THE TRANSACTIONS SHOULD BE PASSED TO       01491027
      *      MERCH LEDGER. THE MODULE IS RUN AS PART OF FAST TRANSFER   01492028
      *      CUTOFF WEEK PROCESSING IN THE JOB MLK124.                  01493028
      *                                                                 01500000
      *         PARAMETERS TO PASS A CUTOFF WEEK FAST TRANSFER TO       01501028
      *      MERCH LEDGER. (WRITTEN TO THE PROCESS FILE)                01502028
      *      1.  THE FROM/TO LOCATION IS SETUP FOR PHYSICAL INVENTORY.  01503028
W33304*      2.  THE FROM/TO LOCATION HAS ITS FINANCIALS CUTOVER TO     01504028
      *          INVENTORY LEDGER.                                      01504128
      *                                                                 01505028
      *         IF ONE OF THE ABOVE CONDITIONS DOES NOT APPLY, THE      01506028
      *      FAST TRANSFER TRANSACTION IS WRITTEN TO THE RECYCLE FILE.  01507028
      *                                                                 01508028
      *         IF THE FROM/TO LOCATION IS NOT SCHEDULED FOR INVENTORY, 01509028
      *      THE FAST TRANSFER IS WRITTEN TO THE RECYCLE FILE.          01509128
      *                                                                 01509228
W33304*         THE ACTUAL INVENTORY DATE MAY BE IN AN ACTIVE PRIOR             
W33304*      INVENTORY CYCLE, AND MUST STILL BE CONSIDERED.                     
W33304*                                                                         
W33304*         FUTURE INVENTORY CYCLES AND LOCATIONS MAY BE DEFINED            
W33304*      BUT IF THE CYCLE IS INACTIVE, THESE DATES SHOULD NOT BE            
W33304*      CONSIDERED IN CURRENT MONTH PROCESSING.                            
W33304*                                                                         
      *      OUTPUT:                                                    01510000
      *         INP01 - FAST TRANSFER INPUT FILE                        01520000
      *         OUT01 - FAST TRANSFER PROCESS FILE                      01530000
      *         OUT02 - FAST TRANSFER RECYCLE FILE                      01540000
      *                                                                 01550000
      *      TABLES USED:                                               01560000
      *         TINVPAR    - INVENTORY PARMS                            01570000
      *         TMLCNTL    - INVENTORY SHEET ENTRY                      01580000
W33304*         TINCNTL    - INVENTORY CONTROL                                  
      *                                                                 01600000
      ******************************************************************01610000
      ******************************************************************01620001
       PROCEDURE DIVISION.                                              01630000
      ******************************************************************01640000
       0000-MAINLINE-DRIVER-SECTION.                                    01650000
                                                                        01660000
           PERFORM 1000-INITIALIZATION.                                 01670000
                                                                        01680000
           PERFORM 2000-PROCESS-TRANSFER-STORE                          01690000
               UNTIL END-OF-TRANSFER-DATA.                              01700000
                                                                        01710000
           PERFORM 9000-END-OF-JOB-TASKS.                               01720000
                                                                        01730000
       9999-EXIT-PROGRAM.                                               01740000
           STOP RUN.                                                    01750000
                                                                        01760000
      /*****************************************************************01770000
      * 1000-INITIALIZATION.                                            01780000
      ******************************************************************01790000
       1000-INITIALIZATION.                                             01800000
                                                                        01810000
           OPEN  INPUT TRANSFER-DATA-FILE                               01820000
                OUTPUT TRANSFER-PROCESS-FILE                            01830000
                OUTPUT TRANSFER-RECYCLE-FILE.                           01840000
                                                                        01850000
           MOVE ZEROS          TO PV-RECORDS-READ                       01860000
                                  PV-RECORDS-ACCEPTED                   01870006
                                  PV-RECORDS-RECYCLED.                  01871006
                                                                        01880000
      *** LOAD THE INVENTORYING STORE TABLE.                            01890001
           INITIALIZE WS-STORE-TABLE.                                   01900001
           MOVE ZEROS          TO PV-STR-NDX.                           01910001
           MOVE 'N'            TO PS-TINVPAR-DONE-SW.                   01920001
           PERFORM 8200-OPEN-TINVPAR.                                   01930007
           PERFORM 8300-LOAD-STORE-TABLE                                01940001
               UNTIL TINVPAR-PROCESS-COMPLETE.                          01950001
           PERFORM 8400-CLOSE-TINVPAR.                                  01960007
                                                                        01970001
      *** READ FAST TRANSFER RECORDS.                                   01980001
           PERFORM 8100-READ-TRANSFER-DATA.                             01990001
                                                                        02000000
      /*****************************************************************02010000
      * 2000-PROCESS-TRANSFER-STORE.                                    02020000
      ******************************************************************02030000
       2000-PROCESS-TRANSFER-STORE.                                     02040000
                                                                        02050000
           ADD 1               TO PV-RECORDS-READ.                      02060004
           MOVE 'N'            TO PS-PROCESS-TFR-SW.                    02070015
           MOVE 'N'            TO PS-RECYCLE-TFR-SW.                    02071015
           MOVE TFRD054-TO-LOC-NBR   TO PV-EDIT-LOC-NBR.                02081415
           PERFORM 3000-TEST-TRANSFER-LOC.                              02082015
                                                                        02089615
           MOVE TFRD054-FROM-LOC-NBR TO PV-EDIT-LOC-NBR.                02089725
           PERFORM 3000-TEST-TRANSFER-LOC.                              02089825
                                                                        02089925
           IF PS-PROCESS-FAST-TRANSFER                                  02090015
               PERFORM 3100-PROCESS-FAST-TRANSFER                       02090126
           ELSE                                                         02090215
           IF PS-RECYCLE-FAST-TRANSFER                                  02090315
               PERFORM 3200-RECYCLE-FAST-TRANSFER                       02090425
           ELSE                                                         02090515
               PERFORM 3200-RECYCLE-FAST-TRANSFER                       02126025
           END-IF.                                                      02461015
                                                                        02470015
      *** READ FAST TRANSFER RECORDS.                                   02471008
           PERFORM 8100-READ-TRANSFER-DATA.                             02480000
                                                                        02490000
      /*****************************************************************02491004
      * 3000-TEST-TRANSFER-LOC.                                         02492004
      ******************************************************************02493004
       3000-TEST-TRANSFER-LOC.                                          02494004
                                                                        02495004
           MOVE PV-EDIT-LOC-NBR     TO PV-STR-NDX.                      02495124
                                                                        02495324
           IF (WS-LOC-NBR (PV-STR-NDX) EQUAL ZEROS             AND      02495424
               WS-ACTL-INV-DTE (PV-STR-NDX) EQUAL SPACES       AND      02495529
               WS-ACTL-LDG-CTOF-TMST (PV-STR-NDX) EQUAL SPACES AND      02495624
               WS-OPN-FSCL-WK-DTE (PV-STR-NDX) EQUAL SPACES)            02495729
                NEXT SENTENCE                                           02495824
           ELSE                                                         02495924
           IF TFRD054-CREATE-DATE > WS-OPN-FSCL-WK-DTE (PV-STR-NDX)     02496530
                MOVE 'Y'             TO PS-RECYCLE-TFR-SW               02496830
           ELSE                                                         02496930
           IF TFRD054-CREATE-DATE > WS-ACTL-INV-DTE (PV-STR-NDX)        02497030
                MOVE 'Y'             TO PS-RECYCLE-TFR-SW               02497130
           ELSE                                                         02497230
           IF PV-EDIT-LOC-NBR EQUAL WS-LOC-NBR (PV-STR-NDX)             02497330
                MOVE 'Y'             TO PS-PROCESS-TFR-SW               02497430
           END-IF.                                                      02497615
                                                                        02497715
      ******************************************************************02497804
      * 3100-PROCESS-FAST-TRANSFER.                                     02497926
      ******************************************************************02498004
       3100-PROCESS-FAST-TRANSFER.                                      02499026
                                                                        02499104
           WRITE TFR-PROCESS-RECORD FROM TFRD054-ML-DAILY-TRANSFER-FILE.02499206
           ADD 1                    TO PV-RECORDS-ACCEPTED.             02499406
                                                                        02499506
      ******************************************************************02499604
      * 3200-RECYCLE-FAST-TRANSFER.                                     02499725
      ******************************************************************02499804
       3200-RECYCLE-FAST-TRANSFER.                                      02499925
                                                                        02500004
           WRITE TFR-RECYCLE-RECORD FROM TFRD054-ML-DAILY-TRANSFER-FILE.02500406
           ADD 1                    TO PV-RECORDS-RECYCLED.             02500506
                                                                        02500606
      ******************************************************************02501000
      * 8100-READ-TRANSFER-DATA.                                        02510000
      *     READ THE INTRANSIT FILE PASSED FROM THE RECEIVING TEAM.     02520000
      ******************************************************************02530000
       8100-READ-TRANSFER-DATA.                                         02540000
                                                                        02550000
           READ TRANSFER-DATA-FILE NEXT                                 02560000
               INTO TFRD054-ML-DAILY-TRANSFER-FILE                      02570006
               AT END  SET END-OF-TRANSFER-DATA TO TRUE.                02580000
                                                                        02590000
      ***************************************************************** 02600001
      * 8200-OPEN-TINVPAR.                                              02610001
      ***************************************************************** 02620001
       8200-OPEN-TINVPAR.                                               02630001
                                                                        02640001
           EXEC SQL                                                     02650001
               OPEN TINVPAR_CSR                                         02660001
           END-EXEC.                                                    02670001
                                                                        02680001
           EVALUATE TRUE                                                02690001
             WHEN SQLCODE IS EQUAL TO ZERO                              02700001
               CONTINUE                                                 02710001
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        02720001
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          02730001
              MOVE '8200-OPEN-TINVPAR' TO PV-PARAGRAPH-NAME             02740001
              MOVE 'OPEN CURSOR'       TO PV-OPERATION-ATTEMPTED        02750001
              MOVE 'Y'                 TO PS-TINVPAR-DONE-SW            02760001
              PERFORM 9900-SQL-ERROR                                    02770007
           END-EVALUATE.                                                02780001
                                                                        02790001
      ***************************************************************** 02800001
      * 8300-LOAD-STORE-TABLE.                                          02810001
      ***************************************************************** 02820001
       8300-LOAD-STORE-TABLE.                                           02830001
                                                                        02840001
           EXEC SQL                                                     02850001
               FETCH TINVPAR_CSR                                        02860001
               INTO :INVPAR-LOC-NBR                                     02870001
W33304             ,:INVPAR-INV-ID                                              
                   ,:INVPAR-ACTL-INV-DTE                                02870129
                   ,:INVPAR-ACTL-LDG-CTOF-TMST                          02870208
                   ,:MLCNTL-OPN-FSCL-WK-DTE                             02871029
           END-EXEC.                                                    02880001
                                                                        02890001
           EVALUATE TRUE                                                02900001
             WHEN SQLCODE IS EQUAL TO ZERO                              02910001
               MOVE INVPAR-LOC-NBR  TO PV-STR-NDX                       02920001
               MOVE INVPAR-LOC-NBR  TO WS-LOC-NBR (PV-STR-NDX)          02930001
W33304         MOVE INVPAR-INV-ID   TO WS-INV-ID (PV-STR-NDX)                   
               MOVE INVPAR-ACTL-INV-DTE                                 02930129
                               TO WS-ACTL-INV-DTE (PV-STR-NDX)          02930229
               MOVE INVPAR-ACTL-LDG-CTOF-TMST                           02930308
                               TO WS-ACTL-LDG-CTOF-TMST (PV-STR-NDX)    02930508
               MOVE MLCNTL-OPN-FSCL-WK-DTE                              02931029
                               TO WS-OPN-FSCL-WK-DTE (PV-STR-NDX)       02932029
               MOVE 'Y'        TO PS-TINVPAR-FOUND-SW                   02940008
               ADD 1           TO PV-TINVPAR-ROW-COUNT                  02941008
             WHEN SQLCODE IS EQUAL TO +100                              02950001
               MOVE 'Y'        TO PS-TINVPAR-DONE-SW                    02960009
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        02970001
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          02980001
               MOVE '8300-LOAD-STORE-TABLE' TO PV-PARAGRAPH-NAME        02990001
               MOVE 'LOAD TABLE'  TO PV-OPERATION-ATTEMPTED             03000009
               MOVE 'Y'        TO PS-TINVPAR-DONE-SW                    03010009
               PERFORM 9900-SQL-ERROR                                   03020007
           END-EVALUATE.                                                03030001
                                                                        03040001
      ***************************************************************** 03050001
      * 8400-CLOSE-TINVPAR.                                             03060001
      ***************************************************************** 03070001
        8400-CLOSE-TINVPAR.                                             03080001
                                                                        03090001
           EXEC SQL                                                     03100001
               CLOSE TINVPAR_CSR                                        03110001
           END-EXEC.                                                    03120001
                                                                        03130001
           EVALUATE TRUE                                                03140001
             WHEN SQLCODE IS EQUAL TO ZERO                              03150001
               CONTINUE                                                 03160001
             WHEN SQLWARN IS NOT EQUAL TO SPACES                        03170001
             WHEN SQLCODE IS NOT EQUAL TO ZERO                          03180001
              MOVE '8400-CLOSE-TINVPAR' TO PV-PARAGRAPH-NAME            03190001
              MOVE 'CLOSE CURSOR'       TO PV-OPERATION-ATTEMPTED       03200001
              MOVE 'Y'                  TO PS-TINVPAR-DONE-SW           03210001
              PERFORM 9900-SQL-ERROR                                    03220007
           END-EVALUATE.                                                03230001
                                                                        03240001
      /*****************************************************************03250000
      * 9000-END-OF-JOB-TASKS.                                          03260000
      ******************************************************************03270000
       9000-END-OF-JOB-TASKS.                                           03280000
                                                                        03290000
      *** FINAL TOTALS.....                                             03300000
           DISPLAY '***********************'.                           03310000
           DISPLAY '* ' PC-PROGRAM-NAME ' SUMMARY    *'.                03320000
           DISPLAY '***********************'.                           03330000
           DISPLAY ' '.                                                 03340000
           DISPLAY ' TINVPAR LOCS LOAD:  ' PV-TINVPAR-ROW-COUNT.        03350008
           DISPLAY ' '.                                                 03351008
           DISPLAY ' RECORDS READ:       ' PV-RECORDS-READ.             03352008
           DISPLAY ' RECORDS PROCESSED:  ' PV-RECORDS-ACCEPTED.         03360000
           DISPLAY ' RECORDS RECYCLED:   ' PV-RECORDS-RECYCLED.         03370006
           DISPLAY ' '.                                                 03380000
           DISPLAY '***********************'.                           03390000
                                                                        03400000
           CLOSE TRANSFER-DATA-FILE                                     03410000
                 TRANSFER-PROCESS-FILE                                  03420000
                 TRANSFER-RECYCLE-FILE.                                 03430000
                                                                        03440000
      ******************************************************************03450000
      * 9900-SQL-ERROR.                                                 03460000
      ******************************************************************03470000
       9900-SQL-ERROR.                                                  03480000
                                                                        03490000
           SET ABEND-4013            TO TRUE.                           03500000
           DISPLAY '**  ABEND     **'.                                  03510000
           DISPLAY '**  PROGRAM   **  =  ' PC-PROGRAM-NAME              03520000
           DISPLAY '**  OPERATION **  = '  PV-OPER-ATTEMPTED            03530000
                                                                        03540000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         03550000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        03560000
                                                                        03570000
           COPY DPPD004.                                                03580000
                                                                        03590000
           PERFORM 9990-ABEND.                                          03600000
                                                                        03610000
       9990-ABEND.                                                      03620000
                                                                        03630000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              03640000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            03650000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           03660000
                                                                        03670000
