000100******************************************************************06/20/94
000200 IDENTIFICATION DIVISION.                                         DPKCS011
000300******************************************************************   LV001
000400                                                                  DPKCS011
000500 PROGRAM-ID.                DPKCS011.                             DPKCS011
000600 AUTHOR.                    RICK TULLOCH.                         DPKCS011
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.              DPKCS011
000800 DATE-WRITTEN.              9-24-90.                              DPKCS011
000900 DATE-COMPILED.                                                   DPKCS011
001000                                                                  DPKCS011
001100******************************************************************DPKCS011
001200*  CICS PROGRAM - DPKCS011 - Z011                                *DPKCS011
001300*                                                                *DPKCS011
001400*       CICS ARCHITECTURE - LOGOFF                               *DPKCS011
001500*                                                                *DPKCS011
001524*   03/06/96  -  ADD LOGIC FOR CICS24X7 PROCESSING.              *DPKCS013
001525*                CLEAN-UP ALL RECORDS ASSOCIATED WITH A TERMINAL *DPKCS013
001526*                (IN THE KSDS SAVED COMMAREA VSAM FILE) WHEN THE *DPKCS013
001527*                USER LOGGED-IN TO THAT TERMINAL LOGS OFF.       *DPKCS013
001530*                MADE BY: CHERI MASTEL                           *DPKCS013
980824*   08/24/98  -  HANDLE CORRECTLY IF PROGRAM INITIATED BY A      *        
980824*                "RETURN IMMEDIATE" INSTEAD OF "START".          *        
001600******************************************************************DPKCS011
001700                                                                  DPKCS011
001800******************************************************************DPKCS011
001900 ENVIRONMENT DIVISION.                                            DPKCS011
002000******************************************************************DPKCS011
002100                                                                  DPKCS011
002200******************************************************************DPKCS011
002300 DATA DIVISION.                                                   DPKCS011
002400******************************************************************DPKCS011
002500                                                                  DPKCS011
002600 WORKING-STORAGE SECTION.                                         DPKCS011
002700*----------------------------------------------------------------*DPKCS011
002800*    WORKING STORAGE INCLUDE MEMBER USED WITH INCLUDE DPPD005    *DPKCS011
002900*----------------------------------------------------------------*DPKCS011
003000*                                                                         
003100 01  FD-FILE-DDNAME-AREA.                                                 
003200     05  FD-SAVED-COMMAREA-FILE  PIC X(8)     VALUE 'DPSAVCMK'.           
003300*                                                                         
003400     COPY DPWS006.                                                      11
003500 EJECT                                                            DPKCS011
003600                                                                  DPKCS011
003700*                                                                 DPKCS011
003800*  CICS ARCHITECTURE STANDARD COMMAREA                            DPKCS011
003900     COPY DPWS020.                                                DPKCS011
004000                                                                  DPKCS011
004100*                                                                         
004200*   KSDS VSAM FILE FOR SAVING EACH TRAN'S STANDARD COMMAREA LAYOUT        
004300     COPY DPRD515.                                                        
004400                                                                          
004500*                                                                 DPKCS011
004600*  RECORD LAYOUT FOR THE OBJECT LOCKING VSAM FILE.                DPKCS011
004700     COPY DPRD509.                                                      11
004800                                                                  DPKCS011
004900*                                                                 DPKCS011
005000*  WORKING-STORAGE VARIABLES REQUIRED FOR THE PROCESS ABEND       DPKCS011
005100*  ROUTINE (DPPD013).                                             DPKCS011
005200     COPY DPWS013.                                                      11
005300                                                                  DPKCS011
005400*                                                                 DPKCS011
005500*  WORKING-STORAGE VARIABLES REQUIRED FOR THE SECURITY            DPKCS011
005600*  SUBROUTINE (CICSAUTH)                                          DPKCS011
005700     COPY DPWS008.                                                      11
005800 01  PS-PROGRAM-SWITCHES.                                         DPKCS011
005900     05  PS-CLEAN-UP-TEMP-STORAGE-IND                             DPKCS011
006000                                 PIC X(1)     VALUE 'N'.          DPKCS011
006100         88  PS-CLEAN-UP-TEMP-STORAGE         VALUE 'Y'.          DPKCS011
006200         88  PS-DONT-CLEAN-UP-TEMP-STORAGE    VALUE 'N'.          DPKCS011
006300                                                                  DPKCS011
006400 01  PC-PROGRAM-CONSTANTS.                                        DPKCS011
006500     05  PC-CURRENT-PROGRAM-NAME PIC X(8)     VALUE 'DPKCS011'.   DPKCS011
006600     05  PC-CURRENT-TRAN-ID      PIC X(4)     VALUE 'Z011'.       DPKCS011
006700     05  PC-OBJLOK-BASE-DD       PIC  X(08)   VALUE 'DPOBJLOK'.   DPKCS011
006800     05  PC-OBJLOK-AIX-TASK-DD   PIC  X(08)   VALUE 'DPOBJLK1'.   DPKCS011
006900     05  PC-NAVIGATOR-TRAN-ID    PIC X(4)     VALUE 'Z008'.       DPKCS011
007000     05  PC-LOGON-TRAN-ID        PIC X(4)     VALUE 'Z028'.       DPKCS011
007100     05  PC-MAX-APPL-QUEUE-SEQ   PIC S9(04)   VALUE +5            DPKCS011
007200                                              COMP SYNC.          DPKCS011
007300     05  PC-MAXIMUM-COMMAREA-LENGTH                               DPKCS011
007400                                 PIC S9(4)    VALUE +4072         DPKCS011
007500                                 COMP SYNC.                       DPKCS011
007600     05  PC-LEAVE-SYSTEM-MESSAGE PIC X(28)    VALUE               DPKCS011
007700         'CICS ARCHITECTURE TERMINATED'.                          DPKCS011
007800     05  PC-LEAVE-SYSTEM-LENGTH  PIC S9(4)    VALUE +28           DPKCS011
007900                                 COMP SYNC.                       DPKCS011
008000     05  PC-NETNAME-LENGTH       PIC S9(04)   VALUE +8                    
008100                                              COMP.                       
008200                                                                  DPKCS011
008300 01  PV-PROGRAM-VARIABLES.                                        DPKCS011
008400     05  PV-CICS-LENGTH          PIC S9(04)  VALUE +0             DPKCS011
008500                                             COMP SYNC.           DPKCS011
008600     05  PV-CICS-KEYLENGTH       PIC S9(04)  VALUE +0             DPKCS011
008700                                             COMP SYNC.           DPKCS011
008800     05  PV-CICS-RESPONSE        PIC S9(9)    VALUE ZERO          DPKCS011
008900                                 COMP SYNC.                       DPKCS011
009000     05  PV-SYSID-SPECIFIED-LITERAL                               DPKCS011
009100                                 PIC X(10)    VALUE SPACES.       DPKCS011
009200         88  PV-SYSID-SPECIFIED               VALUE 'WITH SYSID'. DPKCS011
009300     05  PV-SYSID                PIC X(4)     VALUE SPACES.       DPKCS011
009400     05  PV-NAVIGATOR-SYSID      PIC X(4)     VALUE SPACES.       DPKCS011
009500     05  PV-LOGON-SYSID          PIC X(4)     VALUE SPACES.       DPKCS011
009600     05  PV-PRT-TASK-NBR         PIC  9(07)   VALUE ZERO.         DPKCS011
009700     05  PV-ESCAPE-FUNC          PIC X(8)     VALUE 'ESCAPE  '.   DPKCS011
009800     05  PV-NAVIGATION-KEY       PIC X(25)    VALUE SPACES.       DPKCS011
009900         88  PV-RETURN-TO-NATIVE-CICS         VALUE 'CICS'.       DPKCS011
010000     05  PV-NETNAME-DELETE-NUMREC                                         
010100                                 PIC S9(04)   VALUE +0                  11
010200                                              COMP SYNC.                11
010300*                                PIC  9(07)   VALUE ZERO.         DPKCS011
010400     05  PV-UNPACK-NUMERIC        PIC 9(4)    VALUE ZERO.                 
010500     05  PV-DISPLAY-LINE-2.                                               
010600         10  FILLER.                                                      
010700             15  FILLER           PIC  X(04)  VALUE 'KEY='.               
010800             15  PV-DISPLAY-KEY   PIC  X(15)  VALUE SPACE.                
010900             15  FILLER           PIC  X(01)  VALUE ','.                  
011000         10  FILLER.                                                      
011100             15  PV-DISPLAY-FIELD2-HDR                                    
011200                                  PIC  X(07)  VALUE SPACE.                
011300             15  PV-DISPLAY-FIELD2                                        
011400                                  PIC  X(04)  VALUE SPACE.                
011500             15  FILLER           PIC  X(01)  VALUE ','.                  
011600         10  FILLER.                                                      
011700             15  PV-DISPLAY-FIELD3-HDR                                    
011800                                  PIC  X(07)  VALUE SPACE.                
011900             15  PV-DISPLAY-FIELD3                                        
012000                                  PIC  X(04)  VALUE SPACE.                
012100*                                                                         
012200                                                                  DPKCS011
012300 01  TS-TEMP-STORAGE-QUEUE-INFO.                                  DPKCS011
012400     05  TS-INFO-QUEUE-NAME.                                      DPKCS011
012500         10  TS-INFO-QUEUE-TERMINAL                               DPKCS011
012600                                  PIC  X(04)  VALUE SPACE.        DPKCS011
012700         10  FILLER               PIC  X(04)  VALUE 'INFO'.       DPKCS011
012800     05  TS-SCA-QUEUE-INFO.                                       DPKCS011
012900         10  TS-SCA-QUEUE-NAME.                                   DPKCS011
013000             15  TS-SCA-QUEUE-TERMINAL                            DPKCS011
013100                                 PIC X(4)     VALUE SPACES.       DPKCS011
013200             15  TS-SCA-QUEUE-LITERAL                             DPKCS011
013300                                 PIC X(4)     VALUE 'SCA '.       DPKCS011
013400         10  TS-SCA-QUEUE-ITEM-NUMBER                             DPKCS011
013500                                 PIC S9(4)    VALUE ZERO          DPKCS011
013600                                 COMP SYNC.                       DPKCS011
013700     05  TS-APPL-QUEUE-INFO.                                      DPKCS011
013800         10  TS-APPL-QUEUE-NAME.                                  DPKCS011
013900             15  TS-APPL-QUEUE-TERMINAL                           DPKCS011
014000                                 PIC X(4)     VALUE SPACES.       DPKCS011
014100             15  TS-APPL-QUEUE-TASK                               DPKCS011
014200                                 PIC S9(7)    VALUE ZERO          DPKCS011
014300                                 COMP-3.                          DPKCS011
014400         10  TS-APPL-MULT-QUEUE-ID.                               DPKCS011
014500             15  TS-APPL-MULT-QUEUE-TERM                          DPKCS011
014600                                 PIC X(4)     VALUE SPACES.       DPKCS011
014700             15  TS-APPL-MULT-QUEUE-TASK                          DPKCS011
014800                                 PIC S9(5)    VALUE ZERO          DPKCS011
014900                                 COMP-3.                          DPKCS011
015000             15  TS-APPL-MULT-QUEUE-SEQ                           DPKCS011
015100                                 PIC  9(01)   VALUE ZERO.         DPKCS011
015200                                                                  DPKCS011
015300******************************************************************DPKCS011
015400 LINKAGE SECTION.                                                 DPKCS011
015500******************************************************************DPKCS011
015600                                                                  DPKCS011
015700 01  DFHCOMMAREA.                                                 DPKCS011
015800     05  FILLER                         OCCURS  1 TO 4072 TIMES   DPKCS011
015900                                        DEPENDING ON EIBCALEN.    DPKCS011
016000         10  FILLER                     PIC X.                    DPKCS011
016100                                                                  DPKCS011
016200******************************************************************DPKCS011
016300 PROCEDURE DIVISION.                                              DPKCS011
016400******************************************************************DPKCS011
016500                                                                  DPKCS011
016600 0000-MAINLINE.                                                   DPKCS011
016700     SET DP013-LINK-RETURN                                        DPKCS011
016800         DP013-NO-ROLLBACK TO TRUE.                               DPKCS011
016900                                                                  DPKCS011
017000     PERFORM 1000-RETRIEVE-COMMAREA.                              DPKCS011
017100                                                                  DPKCS011
017200     PERFORM 1500-DELETE-FROM-DPSAVCMK.                           DPKCS011
017300                                                                  DPKCS011
017400     IF PS-CLEAN-UP-TEMP-STORAGE                                  DPKCS011
017500         PERFORM 2000-CLEAN-UP-TEMP-STORAGE                       DPKCS011
017600     END-IF.                                                      DPKCS011
017700                                                                  DPKCS011
017800     IF PV-RETURN-TO-NATIVE-CICS                                  DPKCS011
017900         SET DP008-RSRC-TYPE-FUNC TO TRUE                         DPKCS011
018000         SET DP008-ACCESS-TYPE-USE TO TRUE                        DPKCS011
018100         MOVE PV-ESCAPE-FUNC TO DP008-FUNCTION-NAME               DPKCS011
018200         EXEC CICS LINK                                           DPKCS011
018300                   PROGRAM  (DP008-SECURITY-SUBROUTINE)           DPKCS011
018400                   COMMAREA (DP008-SECURITY-COMMAREA)             DPKCS011
018500                   LENGTH   (DP008-SECURITY-COMMAREA-LENGTH)      DPKCS011
018600         END-EXEC                                                 DPKCS011
018700         IF DP008-CALL-SUCCESSFUL                                 DPKCS011
018800             IF DP008-ACCESS-ALLOWED                              DPKCS011
018900                 CONTINUE                                         DPKCS011
019000             ELSE                                                 DPKCS011
019100                 PERFORM 9000-DISCONNECT-FROM-CICS                DPKCS011
019200             END-IF                                               DPKCS011
019300         ELSE                                                     DPKCS011
019400             SET DP013-XCTL-DISPLAY-LOGOFF TO TRUE                DPKCS011
019500             SET DP013-SECURITY-ABEND      TO TRUE                DPKCS011
019600             MOVE '0000-MAINLINE'          TO DP013-PARAGRAPH     DPKCS011
019700             MOVE DP008-SECURITY-COMMAREA  TO DP013-SECURITY-INFO DPKCS011
019800             PERFORM DP013-0000-PROCESS-ABEND                     DPKCS011
019900         END-IF                                                   DPKCS011
020000         EXEC CICS                                                DPKCS011
020100             SEND                                                 DPKCS011
020200                 FROM   (PC-LEAVE-SYSTEM-MESSAGE)                 DPKCS011
020300                 LENGTH (PC-LEAVE-SYSTEM-LENGTH)                  DPKCS011
020400                 ERASE                                            DPKCS011
020500         END-EXEC                                                 DPKCS011
020600     ELSE                                                         DPKCS011
020700         PERFORM 9000-DISCONNECT-FROM-CICS                        DPKCS011
020800     END-IF.                                                      DPKCS011
020900                                                                  DPKCS011
021000     SET DP020-SRC-RETURN-W-O-TRAN-ID TO TRUE.                    DPKCS011
021100     PERFORM DP003-0000-RETURN-TO-CICS.                           DPKCS011
021200                                                                  DPKCS011
021300     GOBACK.                                                      DPKCS011
021400                                                                  DPKCS011
021500******************************************************************DPKCS011
021600*  LOGIC NECESSARY TO COMMAREA PRIOR TO INITIATING THE LOGOFF    *DPKCS011
021700*  PROCESS.                                                      *DPKCS011
021800******************************************************************DPKCS011
021900                                                                  DPKCS011
022000 1000-RETRIEVE-COMMAREA.                                          DPKCS011
022100     IF EIBCALEN = ZERO                                           DPKCS011
022200         EXEC CICS                                                DPKCS011
022300             RETRIEVE                                             DPKCS011
022400                 INTO   (DP020-STANDARD-COMMAREA)                 DPKCS011
022500                 LENGTH (PC-MAXIMUM-COMMAREA-LENGTH)              DPKCS011
022600                 RESP   (PV-CICS-RESPONSE)                        DPKCS011
022700         END-EXEC                                                 DPKCS011
022800         EVALUATE PV-CICS-RESPONSE  ALSO  TRUE                    DPKCS011
022900             WHEN DFHRESP(ENDDATA)  ALSO  ANY                     DPKCS011
023000             WHEN DFHRESP(NORMAL)   ALSO                          DPKCS011
023100                                    (NOT DP020-GOOD-COMMAREA)     DPKCS011
023200                 MOVE EIBTRMID TO TS-SCA-QUEUE-TERMINAL           DPKCS011
023300                 MOVE 1 TO TS-SCA-QUEUE-ITEM-NUMBER               DPKCS011
023400                 PERFORM 7000-READ-SCA-TS-QUEUE                   DPKCS011
023500             WHEN DFHRESP(NORMAL)   ALSO                          DPKCS011
023600                                    (DP020-GOOD-COMMAREA)         DPKCS011
023700                 SET PS-CLEAN-UP-TEMP-STORAGE TO TRUE             DPKCS011
023800                 MOVE DP020-NAV-KEY-DATA TO PV-NAVIGATION-KEY     DPKCS011
023900             WHEN OTHER                                           DPKCS011
024000                 SET DP013-CICS-ABEND TO TRUE                     DPKCS011
024100                 MOVE '1000-RETRIEVE-COMMAREA' TO DP013-PARAGRAPH DPKCS011
024200                 MOVE 'ATTEMPTING TO RETRIEVE THE COMMAREA.' TO   DPKCS011
024300                           DP013-MESSAGE-TEXT (1)                 DPKCS011
024400                 PERFORM DP013-0000-PROCESS-ABEND                 DPKCS011
024500         END-EVALUATE                                             DPKCS011
024600     ELSE                                                         DPKCS011
980824       MOVE DFHCOMMAREA TO DP020-STANDARD-COMMAREA                        
980824       IF DP020-GOOD-COMMAREA                                             
980824         SET PS-CLEAN-UP-TEMP-STORAGE TO TRUE                     DPKCS011
980824         MOVE DP020-NAV-KEY-DATA TO PV-NAVIGATION-KEY             DPKCS011
980824       ELSE                                                               
024700         SET DP013-LOGIC-ABEND TO TRUE                            DPKCS011
024800         MOVE '1000-RETRIEVE-COMMAREA' TO DP013-PARAGRAPH         DPKCS011
024900         MOVE 'ATTEMPTING TO RETRIEVE THE COMMAREA.  A COMMAREA WADPKCS011
025000-                  'S DETECTED, INDICATING ' TO                   DPKCS011
025100                   DP013-MESSAGE-TEXT (1)                         DPKCS011
025200         MOVE 'AN ATTEMPT TO XCTL OR LINK TO THIS TRANSACTION.  THDPKCS011
025300-                  'IS TRANSACTION MUST BE' TO                    DPKCS011
025400                   DP013-MESSAGE-TEXT (2)                         DPKCS011
025500         MOVE 'STARTED, OR INVOKED FROM NATIVE CICS' TO           DPKCS011
025600                   DP013-MESSAGE-TEXT (3)                         DPKCS011
025700         PERFORM DP013-0000-PROCESS-ABEND                         DPKCS011
025800     END-IF.                                                      DPKCS011
025900*1000-EXIT.                                                       DPKCS011
026000                                                                  DPKCS011
026100******************************************************************        
026200**  DELETE ALL RECORDS FROM DPSAVCMK ASSOCIATED WITH THE CURRENT**   CL**5
026300**  NETNAME.                                                    **        
026400******************************************************************        
026500 1500-DELETE-FROM-DPSAVCMK.                                             11
026600     INITIALIZE                       DP515-KEY.                          
026601     EXEC CICS                                                            
026602         ASSIGN                                                           
026604             NETNAME (DP515-NETNAME)                                      
026610     END-EXEC.                                                            
026800*                                                                         
026900     EXEC CICS                                                            
027000         DELETE                                                           
027100             FILE      (FD-SAVED-COMMAREA-FILE)                           
027200             RIDFLD    (DP515-KEY)                                        
027300             RESP      (PV-CICS-RESPONSE)                                 
027400             KEYLENGTH (PC-NETNAME-LENGTH)                                
027500             GENERIC                                                      
027600             NUMREC    (PV-NETNAME-DELETE-NUMREC)                         
027700     END-EXEC.                                                            
027800     IF  PV-CICS-RESPONSE = DFHRESP (NORMAL)                              
027810       OR PV-CICS-RESPONSE = DFHRESP (NOTFND)                             
027900         CONTINUE                                                         
028000     ELSE                                                                 
028100         SET DP013-CICS-ABEND          TO TRUE                            
028200         SET DP013-XCTL-DISPLAY-RESTART TO TRUE                           
028300         MOVE '1500-DELETE-FROM-DPSAVCMK' TO DP013-PARAGRAPH              
028400         MOVE 'BAD STATUS ON DELETE FROM DPSAVCMK'                        
028500                                       TO DP013-MESSAGE-TEXT (1)          
028600         MOVE DP515-KEY                TO PV-DISPLAY-KEY                  
028700         MOVE 'KEYLEN='                TO PV-DISPLAY-FIELD2-HDR           
028800         MOVE PC-NETNAME-LENGTH        TO PV-UNPACK-NUMERIC               
028900         MOVE PV-UNPACK-NUMERIC        TO PV-DISPLAY-FIELD2               
029000         MOVE 'NUMREC='                TO PV-DISPLAY-FIELD3-HDR           
029100         MOVE PV-NETNAME-DELETE-NUMREC TO PV-UNPACK-NUMERIC               
029200         MOVE PV-UNPACK-NUMERIC        TO PV-DISPLAY-FIELD3               
029300         MOVE PV-DISPLAY-LINE-2        TO DP013-MESSAGE-TEXT (2)          
029400         PERFORM DP013-0000-PROCESS-ABEND                                 
029500     END-IF.                                                              
029600*                                                                         
029700                                                                  DPKCS011
029800******************************************************************DPKCS011
029900*  LOGIC NECESSARY TO CLEAN-UP THE 'SCA' TS QUEUE AS WELL AS THE *DPKCS011
030000*  INDIVIDUAL APPLICATION QUEUES (TERM ID + TASK #) FOR ITEM IN  *DPKCS011
030100*  THE STACK.                                                    *DPKCS011
030200******************************************************************DPKCS011
030300                                                                  DPKCS011
030400 2000-CLEAN-UP-TEMP-STORAGE.                                      DPKCS011
030500     MOVE EIBTRMID              TO TS-SCA-QUEUE-TERMINAL          DPKCS011
030600                                   TS-INFO-QUEUE-TERMINAL         DPKCS011
030700                                   TS-APPL-QUEUE-TERMINAL         DPKCS011
030800                                   TS-APPL-MULT-QUEUE-TERM.       DPKCS011
030900                                                                  DPKCS011
031000     PERFORM                                                      DPKCS011
031100             VARYING DP020-STACK-INDEX                            DPKCS011
031200             FROM 1 BY 1                                          DPKCS011
031300             UNTIL DP020-STACK-INDEX > DP020-ENTRIES-IN-STACK     DPKCS011
031400         MOVE DP020-STACK-TASK-NUMBER (DP020-STACK-INDEX)         DPKCS011
031500                                TO TS-APPL-QUEUE-TASK             DPKCS011
031600                                   TS-APPL-MULT-QUEUE-TASK        DPKCS011
031700                                   DP509-TASK-NBR                 DPKCS011
031800         PERFORM 8400-DELETE-TASK-RCD                             DPKCS011
031900         PERFORM 8000-DELETE-APPL-TS-QUEUE                        DPKCS011
032000         PERFORM 8100-DELETE-APPL-MULT-TS-QUEUE                   DPKCS011
032100             VARYING TS-APPL-MULT-QUEUE-SEQ                       DPKCS011
032200             FROM 1 BY 1                                          DPKCS011
032300             UNTIL TS-APPL-MULT-QUEUE-SEQ > PC-MAX-APPL-QUEUE-SEQ DPKCS011
032400     END-PERFORM.                                                 DPKCS011
032500                                                                  DPKCS011
032600     PERFORM 8200-DELETE-SCA-TS-QUEUE.                            DPKCS011
032700     PERFORM 8300-DELETE-INFO-TS-QUEUE.                           DPKCS011
032800*2000-EXIT.                                                       DPKCS011
032900                                                                  DPKCS011
033000******************************************************************DPKCS011
033100*  IF A NON ARCHITECTURE TRANSACTION IS REQUESTING LOGOFF, CHECK *DPKCS011
033200*  TO SEE IF AN 'SCA' TS QUEUE EXISTS. IF IT DOES,LOAD IT INTO   *DPKCS011
033300*  THE STANDARD COMMAREA SO THAT OTHER ROUTINES MAY DELETE ANY   *DPKCS011
033400*  TEMP STORAGE QUEUES ASSOCIATED WITH THE STACK.                *DPKCS011
033500******************************************************************DPKCS011
033600                                                                  DPKCS011
033700 7000-READ-SCA-TS-QUEUE.                                          DPKCS011
033800     EXEC CICS                                                    DPKCS011
033900         READQ TS                                                 DPKCS011
034000             QUEUE  (TS-SCA-QUEUE-NAME)                           DPKCS011
034100             INTO   (DP020-STANDARD-COMMAREA)                     DPKCS011
034200             LENGTH (PC-MAXIMUM-COMMAREA-LENGTH)                  DPKCS011
034300             ITEM   (TS-SCA-QUEUE-ITEM-NUMBER)                    DPKCS011
034400             RESP   (PV-CICS-RESPONSE)                            DPKCS011
034500     END-EXEC.                                                    DPKCS011
034600                                                                  DPKCS011
034700     EVALUATE PV-CICS-RESPONSE  ALSO  TRUE                        DPKCS011
034800         WHEN DFHRESP(QIDERR)  ALSO  ANY                          DPKCS011
034900         WHEN DFHRESP(NORMAL)  ALSO  (NOT DP020-GOOD-COMMAREA)    DPKCS011
035000             CONTINUE                                             DPKCS011
035100         WHEN DFHRESP(NORMAL)  ALSO  (DP020-GOOD-COMMAREA)        DPKCS011
035200             SET PS-CLEAN-UP-TEMP-STORAGE TO TRUE                 DPKCS011
035300             MOVE DP020-NAV-KEY-DATA TO PV-NAVIGATION-KEY         DPKCS011
035400         WHEN OTHER                                               DPKCS011
035500             SET DP013-CICS-ABEND TO TRUE                         DPKCS011
035600             MOVE '7000-READ-SCA-TS-QUEUE' TO DP013-PARAGRAPH     DPKCS011
035700             MOVE 'ATTEMPTING TO READ A RECORD FROM THE SAVED COMMDPKCS011
035800-                      'AREA TEMP STORAGE QUEUE' TO               DPKCS011
035900                       DP013-MESSAGE-TEXT (1)                     DPKCS011
036000             STRING 'QUEUE NAME = ('            DELIMITED BY SIZE DPKCS011
036100                    TS-SCA-QUEUE-NAME           DELIMITED BY SIZE DPKCS011
036200                    ') '                        DELIMITED BY SIZE DPKCS011
036300                    PV-SYSID-SPECIFIED-LITERAL  DELIMITED BY SIZE DPKCS011
036400                 INTO DP013-MESSAGE-TEXT (2)                      DPKCS011
036500             PERFORM DP013-0000-PROCESS-ABEND                     DPKCS011
036600     END-EVALUATE.                                                DPKCS011
036700*7000-EXIT.                                                       DPKCS011
036800                                                                  DPKCS011
036900******************************************************************DPKCS011
037000*  UTILITY LOGIC TO DELETE A TS QUEUE.                           *DPKCS011
037100******************************************************************DPKCS011
037200                                                                  DPKCS011
037300 8000-DELETE-APPL-TS-QUEUE.                                       DPKCS011
037400     EXEC CICS                                                    DPKCS011
037500         DELETEQ TS                                               DPKCS011
037600             QUEUE (TS-APPL-QUEUE-NAME)                           DPKCS011
037700             NOHANDLE                                             DPKCS011
037800     END-EXEC.                                                    DPKCS011
037900*8000-EXIT.                                                       DPKCS011
038000                                                                  DPKCS011
038100******************************************************************DPKCS011
038200*  DELETE MULTIPLE APPLICATION TEMP STORAGE QUEUES.              *DPKCS011
038300******************************************************************DPKCS011
038400                                                                  DPKCS011
038500 8100-DELETE-APPL-MULT-TS-QUEUE.                                  DPKCS011
038600     EXEC CICS                                                    DPKCS011
038700         DELETEQ TS                                               DPKCS011
038800             QUEUE (TS-APPL-MULT-QUEUE-ID)                        DPKCS011
038900             NOHANDLE                                             DPKCS011
039000     END-EXEC.                                                    DPKCS011
039100******************************************************************DPKCS011
039200*  LOGIC NECESSARY TO DELETE SCA TS QUEUE.                       *DPKCS011
039300******************************************************************DPKCS011
039400                                                                  DPKCS011
039500 8200-DELETE-SCA-TS-QUEUE.                                        DPKCS011
039600         EXEC CICS                                                DPKCS011
039700             DELETEQ TS                                           DPKCS011
039800                 QUEUE (TS-SCA-QUEUE-NAME)                        DPKCS011
039900                 NOHANDLE                                         DPKCS011
040000         END-EXEC.                                                DPKCS011
040100*8200-EXIT.                                                       DPKCS011
040200                                                                  DPKCS011
040300******************************************************************DPKCS011
040400** LOGIC NECESSARY TO DELETE THE 'INFO' TS QUEUE                **DPKCS011
040500******************************************************************DPKCS011
040600 8300-DELETE-INFO-TS-QUEUE.                                       DPKCS011
040700     EXEC CICS                                                    DPKCS011
040800         DELETEQ TS                                               DPKCS011
040900             QUEUE (TS-INFO-QUEUE-NAME)                           DPKCS011
041000             NOHANDLE                                             DPKCS011
041100     END-EXEC.                                                    DPKCS011
041200******************************************************************DPKCS011
041300*  REMOVE ANY OBJECT LOCKS THAT MAY HAVE BEEN SET, BY            *DPKCS011
041400*  DELETING ANY ROWS WITH THAT TASK NUMBER FROM THE LOCK TABLE.  *DPKCS011
041500******************************************************************DPKCS011
041600                                                                  DPKCS011
041700 8400-DELETE-TASK-RCD.                                            DPKCS011
041800     EXEC CICS                                                    DPKCS011
041900         INQUIRE                                                  DPKCS011
042000             TRANSACTION  (PC-NAVIGATOR-TRAN-ID)                  DPKCS011
042100             REMOTESYSTEM (PV-NAVIGATOR-SYSID)                    DPKCS011
042200     END-EXEC.                                                    DPKCS011
042300                                                                  DPKCS011
042400     IF PV-NAVIGATOR-SYSID = SPACES                               DPKCS011
042500         EXEC CICS                                                DPKCS011
042600             DELETE                                               DPKCS011
042700                 FILE   (PC-OBJLOK-AIX-TASK-DD)                   DPKCS011
042800                 RIDFLD (DP509-TASK-NBR)                          DPKCS011
042900                 RESP   (PV-CICS-RESPONSE)                        DPKCS011
043000         END-EXEC                                                 DPKCS011
043100     ELSE                                                         DPKCS011
043200         MOVE LENGTH OF DP509-OBJLOK-KEY                          DPKCS011
043300                               TO PV-CICS-KEYLENGTH               DPKCS011
043400         EXEC CICS                                                DPKCS011
043500             DELETE                                               DPKCS011
043600                 FILE      (PC-OBJLOK-AIX-TASK-DD)                DPKCS011
043700                 RIDFLD    (DP509-TASK-NBR)                       DPKCS011
043800                 KEYLENGTH (PV-CICS-KEYLENGTH)                    DPKCS011
043900                 SYSID     (PV-NAVIGATOR-SYSID)                   DPKCS011
044000                 RESP      (PV-CICS-RESPONSE)                     DPKCS011
044100         END-EXEC                                                 DPKCS011
044200*                                                                 DPKCS011
044300*                                                                 DPKCS011
044400     IF  PV-CICS-RESPONSE = DFHRESP(NORMAL)                       DPKCS011
044500                     OR DFHRESP(NOTFND)                           DPKCS011
044600         CONTINUE                                                 DPKCS011
044700     ELSE                                                         DPKCS011
044800         SET DP013-CICS-ABEND       TO TRUE                       DPKCS011
044900         SET DP013-LINK-RETURN      TO TRUE                       DPKCS011
045000         MOVE '8400-DELETE-TASK-RCD'                              DPKCS011
045100                                    TO DP013-PARAGRAPH            DPKCS011
045200         MOVE 'BAD RESPONSE DELETING FROM OBJ LOCK VSAM'          DPKCS011
045300                                    TO DP013-MESSAGE-TEXT (1)     DPKCS011
045400         MOVE DP509-TASK-NBR        TO PV-PRT-TASK-NBR            DPKCS011
045500         MOVE PV-PRT-TASK-NBR       TO DP013-MESSAGE-TEXT (2)     DPKCS011
045600         PERFORM DP013-0000-PROCESS-ABEND                         DPKCS011
045700     END-IF.                                                      DPKCS011
045800                                                                  DPKCS011
045900******************************************************************DPKCS011
046000*  ISSUE A DISCONNECT FROM CICS TO RETURN USER TO VTAM.          *DPKCS011
046100******************************************************************DPKCS011
046200                                                                  DPKCS011
046300 9000-DISCONNECT-FROM-CICS.                                       DPKCS011
046400     EXEC CICS                                                    DPKCS011
046500         ISSUE                                                    DPKCS011
046600             DISCONNECT                                           DPKCS011
046700             NOHANDLE                                             DPKCS011
046800     END-EXEC.                                                    DPKCS011
046900                                                                  DPKCS011
047000 EJECT                                                            DPKCS011
047100*                                                                 DPKCS011
047200*  RETURN TO CICS ROUTINE.                                        DPKCS011
047300     COPY DPPD003.                                                      11
047400                                                                  DPKCS011
047500*                                                                 DPKCS011
047600*  PROCESS ABEND ROUTINE.                                         DPKCS011
047700     COPY DPPD013.                                                      11
