      *----------------------------------------------------------               
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------               
       PROGRAM-ID.      MOKUT632.                                               
       AUTHOR.          JOHN BOLDT, STRATAGEM.                                  
       INSTALLATION.    KOHLS DEPARTMENT STORES.                                
       DATE-WRITTEN.    APRIL 2006.                                             
       DATE-COMPILED.                                                           
      *----------------------------------------------------------               
      *     MOKUT632 - GEO PROD EXTRACT FOR SAS.                                
      *                ONLY EXTRACT WHERE PRC_CDE = 'N' AND                     
      *                PRICG_FWK_EFF_BEG_DTE <= SYSDATE.                        
      *                (DON'T SEND FUTURE-DATED '40' RECORDS).                  
      * THIS IS A COPIED AND MODIFIED VERSION OF PROGRAM PEKUT631.              
      *----------------------------------------------------------               
      * HISTORY LOG:                                                            
      *----------------------------------------------------------               
      * DATE:         04/20/2006                                                
      * WR/IR#:       WR29858                                                   
      * PROGRAMMER:   JOHN BOLDT, STRATAGEM.                                    
      * DESCRIPTION:  INITIAL CREATION.                                         
      *----------------------------------------------------------               
      * DATE:         05/05/2006                                                
      * WR/IR#:       P000877326                                                
      * PROGRAMMER:   DARYL WOELFEL                                             
      * DESCRIPTION:  FIXED INCORRECT TABLE NAMES AND REMOVED                   
      *               JOIN TO VND_STYL THAT IS NO LONGER NEEDED.                
      *----------------------------------------------------------               
      * DATE:         05/08/2006                                                
      * WR/IR#:       PM00878226                                                
      * PROGRAMMER:   DARYL WOELFEL                                             
      * DESCRIPTION:  CHANGED STORE FROM LEVEL 3 TO LEVEL 5 IN                  
      *               THE HIERARCHY (LEADING DIGIT ON STR-NBR).                 
      *----------------------------------------------------------               
      * DATE:         06/14/2006                                                
      * WR/IR#:       PM00890369                                                
      * PROGRAMMER:   DARYL WOELFEL                                             
      * DESCRIPTION:  REMOVED OLD "ALTER SESSION COST=5" STATEMENT, AS          
      *               IT WAS CAUSING THE SQL TO USE A BAD PATH.                 
      *----------------------------------------------------------               
      * DATE:         06/21/2006                                                
      * WR/IR#:       WR29858                                                   
      * PROGRAMMER:   DARYL WOELFEL                                             
      * DESCRIPTION:  CHANGED TO UPDATE ONCE PER DEPARTMENT INSTEAD             
      *               OF ONCE PER ROW RETRIEVED.                                
      *----------------------------------------------------------               
      * DATE:         07/10/2006                                                
      * WR/IR#:       PM000898667                                               
      * PROGRAMMER:   DARYL WOELFEL                                             
      * DESCRIPTION:  HANDLE NO ROWS FOUND ON DELETE.                           
      *----------------------------------------------------------               
      * DATE:         10/25/2007                                       *        
      * WR/IR#:       WR32984                                          *        
      * PROGRAMMER:   LINDA FRIESS                                              
      * DESCRIPTION:  CHANGE VALID PROCESS INSTANCE TO BE 1 THRU 8.    *        
      *----------------------------------------------------------               
      * DATE:         08/27/2008                                       *        
      * WR/IR#:       INC000000497852                                  *        
      * PROGRAMMER:   DAVE METZGER                                              
      * DESCRIPTION:  CHANGE VALID PROCESS INSTANCE TO BE 1 THRU 12.   *        
      *----------------------------------------------------------               
      * DATE:         07/30/2018                                       *        
      * WR/IR#:       CHG0204126                                       *        
      * PROGRAMMER:   COGNIZANT                                        *        
      * DESCRIPTION:  ORACLE LOGON STANDARDS PROGRAM CHANGE            *        
      *----------------------------------------------------------               
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------               
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PROCESS-INSTANCE-FILE                                         
                  ASSIGN                    TO INP01.                           
                                                                                
           SELECT DATE-RANGE-FILE                                               
                  ASSIGN                    TO INP02.                           
                                                                                
           SELECT ORACLE-LOGON-FILE                                             
               ASSIGN                       TO ORALOGON.                        
                                                                                
           SELECT SAS-GEO-PROD-FILE                                             
                  ASSIGN                    TO OUT01.                           
                                                                                
      *----------------------------------------------------------               
       DATA DIVISION.                                                           
      *----------------------------------------------------------               
       FILE SECTION.                                                            
                                                                                
      *    PROCESS INSTANCE WILL CONTAIN WHICH PARALLEL JOBFLOW IS              
      *    RUNNING THAT DETERMINES THE DEPARTMENTS TO PROCESS                   
       FD  PROCESS-INSTANCE-FILE                                                
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS PROCESS-INSTANCE-RECORD.                              
       01  PROCESS-INSTANCE-RECORD         PIC  X(80).                          
                                                                                
      *    DATE RANGE FILE WILL CONTAIN THE DATE RANGE TO PROCESS               
      *    DATES MUST BE FISCAL WEEK BEGIN/END DATES.                           
       FD  DATE-RANGE-FILE                                                      
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS DATE-RANGE-RECORD.                                    
       01  DATE-RANGE-RECORD               PIC  X(80).                          
                                                                                
       FD  ORACLE-LOGON-FILE                                                    
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           DATA RECORDS IS OL-ORACLE-LOGON-RECORD.                              
       01  OL-ORACLE-LOGON-RECORD      PIC X(80).                               
                                                                                
       FD  SAS-GEO-PROD-FILE                                                    
           RECORDING MODE F                                                     
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS SAS-GEO-PROD-REC.                                     
       01  SAS-GEO-PROD-REC                 PIC X(86).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------               
      * ABEND- ABEND VARIABLES                                                  
      *----------------------------------------------------------               
       01  ABEND-CODE                       PIC S9(04) COMP VALUE +0.           
           88  ABEND-4001-WRITE-ERROR                  VALUE +4001.             
           88  ABEND-4002-READ-ERROR                   VALUE +4002.             
           88  ABEND-4003-CTRL-CARD-ERROR              VALUE +4003.             
           88  ABEND-4004-TABLE-ERROR                  VALUE +4004.             
           88  ABEND-4005-OPEN-ERROR                   VALUE +4005.             
           88  ABEND-4006-CLOSE-ERROR                  VALUE +4006.             
           88  ABEND-4007-SEQUENCE-ERROR               VALUE +4007.             
           88  ABEND-4008-DATA-ERROR                   VALUE +4008.             
           88  ABEND-4009-KEY-DATA-ERROR               VALUE +4009.             
           88  ABEND-4013-SQL-ERROR                    VALUE +4013.             
           88  ABEND-4014-ORACLE-LOGON-ERROR           VALUE +4014.             
           88  ABEND-4019-DATE-ROUTINE-ERROR           VALUE +4019.             
           88  ABEND-4444-FORCED-ABEND                 VALUE +4444.             
                                                                                
      *----------------------------------------------------------               
      * CC- PROCESS INSTANCE CONTROL CARD                                       
      *----------------------------------------------------------               
       01  CC-PROCESS-INSTANCE-REC.                                             
           05  CC-PROCESS-INSTANCE         PIC  9(02)  VALUE 0.                 
           05  FILLER                      PIC  X(78)  VALUE SPACES.            
                                                                                
      *----------------------------------------------------------               
      * CC- DATE RANGE CONTROL CARD                                             
      *----------------------------------------------------------               
       01  CC-DATE-RANGE-REC.                                                   
           05  CC-STRT-DTE                    PIC X(08) VALUE SPACES.           
           05  FILLER                         PIC X(01) VALUE SPACES.           
           05  CC-END-DTE                     PIC X(08) VALUE SPACES.           
           05  FILLER                         PIC X(63) VALUE SPACES.           
                                                                                
      *----------------------------------------------------------               
      * PS- PROGRAM SWITCHES                                                    
      *----------------------------------------------------------               
       01  PS-PROGRAM-SWITCHES.                                                 
006510     05  PS-END-OF-LOGON-FILE-SW     PIC X(01)   VALUE 'N'.               
006530         88  PS-END-OF-LOGON-FILE                VALUE 'Y'.               
           05  PS-ORA-SRC-CTL-DONE-SW  PIC  X(01)      VALUE 'N'.               
               88  PS-ORA-SRC-EMPTY                    VALUE 'Y'.               
           05  PS-END-OF-PROC-INST-FILE-SW PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-PROC-INST-FILE            VALUE 'Y'.               
               88  PS-NOT-END-OF-PROC-INST-FILE        VALUE 'N'.               
           05  PS-END-OF-DATE-RANGE-FILE-SW PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-DATE-RANGE-FILE           VALUE 'Y'.               
               88  PS-NOT-END-OF-DATE-RANGE-FILE       VALUE 'N'.               
           05  PS-PROCESS-INSTANCE-SW      PIC  9(02)  VALUE 0.                 
               88  PS-PROCESS-INSTANCE-VALID           VALUE 1 THRU 12.         
           05  PS-END-OF-DEPTS-CSR-SW      PIC  X(01)  VALUE 'N'.               
               88  PS-END-OF-DEPTS-CSR                 VALUE 'Y'.               
               88  PS-NOT-END-OF-DEPTS-CSR             VALUE 'N'.               
           05  PS-END-OF-GEO-PROD-CSR-SW    PIC X(01)  VALUE 'N'.               
               88  PS-END-OF-GEO-PROD-CSR              VALUE 'Y'.               
               88  PS-NOT-END-OF-GEO-PROD-CSR          VALUE 'N'.               
                                                                                
       01  CC-ORACLE-CONNECTION.                                                
           05  CC-ORACLE-USERID            PIC X(40).                           
               88 CC-AUTO-LOGON                        VALUE '/'.               
           05  CC-ORACLE-PASSWORD          PIC X(40).                           
                                                                                
      *----------------------------------------------------------               
      * PC- PROGRAM CONSTANTS                                                   
      *----------------------------------------------------------               
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-AUTO-LOGON               PIC X(1)    VALUE '/'.               
           05  PC-PROGRAM-NAME              PIC X(08)  VALUE                    
                                                      'MOKUT632'.               
                                                                                
      *----------------------------------------------------------               
      * PV- PROGRAM VARIABLES                                                   
      *----------------------------------------------------------               
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ORACLE-USERID-LEN        PIC S9(4)   VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-ORACLE-PASSWORD-LEN      PIC S9(4)   VALUE ZEROS              
                                                       COMP SYNC.               
           05  PV-TABLE-NAME                PIC X(30)  VALUE SPACES.            
           05  PV-SQL-CODE                  PIC 9(09)  VALUE 0.                 
           05  PV-OPERATION-MSG             PIC X(60)  VALUE SPACES.            
           05  PV-OPERATION-MSG-1           PIC X(40)  VALUE SPACES.            
           05  PV-PARAGRAPH-NAME            PIC X(30)  VALUE SPACES.            
           05  PV-SYS-DATE                  PIC X(21)  VALUE SPACES.            
                                                                                
       01  PV-SELECT-VARIABLES.                                                 
           05  PV-STYL-STAT-CDE             PIC X(03)  VALUE SPACES.            
           05  PV-DEPT-NBR                  PIC S9(04) COMP-3 VALUE +0.         
           05  PV-STR-NBR                   PIC S9(04) COMP-3 VALUE +0.         
           05  PV-STYL-ID                   PIC S9(08) COMP-3 VALUE +0.         
           05  PV-LAST-ACTV-UNT-RTL-AMT     PIC S9(07)V9(03) COMP-3             
                                            VALUE +0.                           
           05  PV-STRT-TMST-FORMAT.                                             
               10 PV-STRT-DTE-FORMAT        PIC X(08)  VALUE SPACES.            
               10 FILLER                    PIC X(01)  VALUE '-'.               
               10 FILLER                    PIC X(08)  VALUE '00.00.00'.        
           05  PV-END-TMST-FORMAT.                                              
               10 PV-END-DTE-FORMAT         PIC X(08)  VALUE SPACES.            
               10 FILLER                    PIC X(01)  VALUE '-'.               
               10 FILLER                    PIC X(08)  VALUE '23.59.00'.        
           05  PV-STRT-TMST                 PIC X(17)  VALUE SPACES.            
           05  PV-END-TMST                  PIC X(17)  VALUE SPACES.            
           05  PV-SAS-STRT-TMST             PIC X(19)  VALUE SPACES.            
           05  PV-SAS-STRT-TMST-REDEF REDEFINES PV-SAS-STRT-TMST.               
               10  PV-STRT-MM               PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-STRT-DD               PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-STRT-YYYY             PIC X(04).                          
               10  FILLER                   PIC X(01).                          
               10  PV-STRT-TIME-HH24        PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-STRT-TIME-MI          PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-STRT-TIME-SS          PIC X(02).                          
           05  PV-SAS-SLS-INTRD-DTE         PIC X(19)  VALUE SPACES.            
           05  PV-SAS-SLS-INTRD-DTE-REDEF  REDEFINES                            
               PV-SAS-SLS-INTRD-DTE.                                            
               10  PV-END-MM                PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-END-DD                PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-END-YYYY              PIC X(04).                          
               10  FILLER                   PIC X(01).                          
               10  PV-END-TIME-HH24         PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-END-TIME-MI           PIC X(02).                          
               10  FILLER                   PIC X(01).                          
               10  PV-END-TIME-SS           PIC X(02).                          
                                                                                
      *----------------------------------------------------------               
      * PA- PROGRAM ACCUMULATORS                                                
      *----------------------------------------------------------               
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECS-WRITTEN              PIC 9(09) VALUE ZEROES.             
           05  PA-KEEP-TRACK                PIC 9(09) VALUE ZEROES.             
           05  PA-INSERT-CNT                PIC 9(09) VALUE ZEROES.             
           05  PA-CNT-DEPTS                 PIC 9(09) VALUE ZEROES.             
           05  PA-CNT-STRS                  PIC 9(09) VALUE ZEROES.             
           05  PA-UPDATE-PROCESSED          PIC 9(09) VALUE ZEROES.             
           05  PA-NBR-OF-COMMITS            PIC 9(09) VALUE ZEROES.             
                                                                                
      *----------------------------------------------------------               
      * COPYBOOK FOR THE GEO PROD RECORD FOR SAS.                               
      *----------------------------------------------------------               
           COPY MORD118.                                                        
                                                                                
      *----------------------------------------------------------               
      *                      O  R  A  C  L  E                                   
      *==========================================================               
      *                                                                         
      * ORACLE DECLARE SECTION                                                  
      * ORACLE USED AND RETURNED VARIABLES                                      
      *                                                                         
      *----------------------------------------------------------               
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
      *----------------------------------------------------------               
      * ORACLE LOGON WORKING STORAGE COPYBOOK                                   
      *----------------------------------------------------------               
      *    COPY MOWS700.                                                        
                                                                                
       01  PC-ENT01-TYPE-CDE                PIC X(02)  VALUE '01'.              
       01  PV-ORACLE-ID                     PIC X(1)   VALUE '/'.               
                                                                                
       01  USERNAME                         PIC X(10)  VARYING.                 
       01  PASSWD                           PIC X(10)  VARYING.                 
                                                                                
       01  ORACLE-RETURN                    PIC X(200) VALUE SPACES.            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
           EXEC SQL INCLUDE SQLCACOB    END-EXEC.                               
                                                                                
       EJECT                                                                    
                                                                                
      *----------------------------------------------------------               
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAINLINE.                                                           
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
                                                                                
           PERFORM C200-OPEN-DEPTS-CSR.                                         
           PERFORM C300-FETCH-DEPTS-CSR.                                        
                                                                                
           PERFORM C100-CONTROL                                                 
             UNTIL PS-END-OF-DEPTS-CSR.                                         
                                                                                
           PERFORM 9100-COMMIT.                                                 
                                                                                
           PERFORM C400-CLOSE-DEPTS-CSR.                                        
           PERFORM 9999-WRAPUP.                                                 
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      *0000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 1000-INITIALIZATION.                                                    
      *     INITIALIZATION AREA - INITIALIZES ALL VARIABLES, OPENS              
      *     FILES, READS CONTROL CARD, AND PROCESSES ANY CONTROL                
      *     CARD PARAMETERS IT MAY NEED TO USE TO FETCH PROMO ITEMS             
      *     ITEMS FROM THE CURSOR.                                              
      *----------------------------------------------------------               
       1000-INITIALIZATION.                                                     
                                                                                
           INITIALIZE MO118-SAS-GEO-PROD                                        
                      PV-SELECT-VARIABLES.                                      
                                                                                
           OPEN INPUT  PROCESS-INSTANCE-FILE                                    
                       DATE-RANGE-FILE                                          
                OUTPUT SAS-GEO-PROD-FILE.                                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*********************************************'.             
           DISPLAY 'PROGRAM NAME: ' PC-PROGRAM-NAME.                            
           DISPLAY '*********************************************'.             
                                                                                
           PERFORM B300-READ-PROC-INST-FILE.                                    
           PERFORM B400-READ-DATE-RANGE-FILE.                                   
                                                                                
           IF PS-END-OF-DATE-RANGE-FILE                                         
              DISPLAY 'NO RUN DATES TO PROCESS'                                 
           ELSE                                                                 
              PERFORM B200-ORACLE-LOGON                                         
              PERFORM B800-DECLARE-DEPTS-CSR                                    
              PERFORM 1210-DECLARE-GEO-PROD-CSR                                 
           END-IF.                                                              
                                                                                
      *1000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      ******************************************************************        
      * READS THE PROCESS INSTANCE THAT ACQUIRES THE DEPTS NEEDED.              
      ******************************************************************        
       B200-ORACLE-LOGON.                                                       
                                                                                
           OPEN INPUT ORACLE-LOGON-FILE.                                        
                                                                                
           PERFORM 7000-READ-ORACLE-LOGON.                                      
                                                                                
           CLOSE ORACLE-LOGON-FILE.                                             
                                                                                
           IF ((PS-END-OF-LOGON-FILE)                                           
                   OR (CC-ORACLE-CONNECTION = SPACES))                          
               DISPLAY '** ABEND **'                                            
               DISPLAY PC-PROGRAM-NAME ' - ORACLE LOGON CONTROL CARD '          
                         'NOT PROVIDED'                                         
               SET PS-ORA-SRC-EMPTY TO TRUE                                     
               CALL 'ILBOABN0' USING ABEND-CODE                                 
           END-IF.                                                              
                                                                                
           IF CC-AUTO-LOGON                                                     
               DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                
               EXEC SQL                                                         
                    CONNECT :USERNAME                                           
               END-EXEC                                                         
           ELSE                                                                 
               INITIALIZE PV-ORACLE-USERID-LEN                                  
                          PV-ORACLE-PASSWORD-LEN                                
               INSPECT CC-ORACLE-USERID                                         
                   TALLYING PV-ORACLE-USERID-LEN                                
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            INSPECT CC-ORACLE-PASSWORD                                          
                TALLYING PV-ORACLE-PASSWORD-LEN                                 
                FOR CHARACTERS BEFORE INITIAL SPACE                             
            MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                   
                      USERNAME-ARR                                              
            MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                           
            MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO               
                      PASSWD-ARR                                                
            MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                           
            DISPLAY PC-PROGRAM-NAME ' - LOGON TO ORACLE AS USER '               
                      USERNAME-ARR                                              
            EXEC SQL                                                            
              CONNECT :USERNAME IDENTIFIED BY :PASSWD                           
            END-EXEC                                                            
            INITIALIZE CC-ORACLE-PASSWORD                                       
                       PASSWD                                                   
            END-IF.                                                             
                                                                                
020500     IF (SQLCODE NOT = 0)                                                 
020900         DISPLAY PV-PARAGRAPH-NAME                                        
020910         DISPLAY PC-PROGRAM-NAME ' - ERROR LOGGING ON TO ORACLE'          
021000         PERFORM 9900-ORACLE-ABEND                                        
021200     END-IF.                                                              
                                                                                
      ******************************************************************        
      * NEEDS TO BE IN FOR NOW TIL KURT FIXES CURSOR SHARING BUG                
      * OTHERWISE I GET A ORA-3113 ERROR                                        
      ******************************************************************        
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET CURSOR_SHARING = 'EXACT'                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      ******************************************************************        
      * READS THE PROCESS INSTANCE THAT ACQUIRES THE DEPTS NEEDED.              
      ******************************************************************        
       B300-READ-PROC-INST-FILE.                                                
                                                                                
           READ PROCESS-INSTANCE-FILE INTO CC-PROCESS-INSTANCE-REC              
                AT END SET PS-END-OF-PROC-INST-FILE TO TRUE.                    
                                                                                
           IF PS-NOT-END-OF-PROC-INST-FILE                                      
              DISPLAY 'PROC INST ' CC-PROCESS-INSTANCE                          
              MOVE CC-PROCESS-INSTANCE TO PS-PROCESS-INSTANCE-SW                
              IF PS-PROCESS-INSTANCE-VALID                                      
                  NEXT SENTENCE                                                 
              ELSE                                                              
                 MOVE 4003                       TO ABEND-CODE                  
                 MOVE 'B300-READ-PROC-INST-FILE' TO PV-PARAGRAPH-NAME           
                 MOVE 'INVALID PROCESS INSTANCE' TO PV-OPERATION-MSG            
                 PERFORM 9990-APPLICATION-ABEND                                 
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
      * READ IN THE DATE RANGE FILE                                             
      ******************************************************************        
       B400-READ-DATE-RANGE-FILE.                                               
                                                                                
           READ DATE-RANGE-FILE                                                 
               INTO CC-DATE-RANGE-REC                                           
               AT END                                                           
                   SET PS-END-OF-DATE-RANGE-FILE TO TRUE.                       
                                                                                
           IF PS-NOT-END-OF-DATE-RANGE-FILE                                     
               MOVE CC-STRT-DTE         TO PV-STRT-DTE-FORMAT                   
               MOVE CC-END-DTE          TO PV-END-DTE-FORMAT                    
               MOVE PV-STRT-TMST-FORMAT TO PV-STRT-TMST                         
               MOVE PV-END-TMST-FORMAT  TO PV-END-TMST                          
               DISPLAY 'CC-STRT-DTE : ' CC-STRT-DTE                             
               DISPLAY 'CC-END-DTE  : ' CC-END-DTE                              
               DISPLAY 'PV-STRT-TMST: ' PV-STRT-TMST                            
               DISPLAY 'PV-END-TMST : ' PV-END-TMST                             
           ELSE                                                                 
               MOVE 4003                        TO ABEND-CODE                   
               MOVE 'B400-READ-DATE-RANGE-FILE' TO PV-PARAGRAPH-NAME            
               MOVE 'EMPTY DATE RANGE FILE'     TO PV-OPERATION-MSG             
               PERFORM 9990-APPLICATION-ABEND                                   
           END-IF.                                                              
                                                                                
                                                                                
      *--------------------------------------------------------------           
      * THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION THAT              
      * LOGS ON TO ORACLE.                                                      
      *---------------------------------------------------------------          
      *    COPY MOPD700.                                                        
                                                                                
                                                                                
      *---------------------------------------------------------------          
      * DECLARE THE DEPTS CURSOR.                                               
      *---------------------------------------------------------------          
       B800-DECLARE-DEPTS-CSR.                                                  
                                                                                
           EXEC SQL                                                             
             DECLARE DEPTSCSR CURSOR FOR                                        
               SELECT PRC_CNTL_VAL_NBR                                          
                 FROM MOTW_PRC_CNTL                                             
                WHERE PRC_CNTL_INSTN_NBR = :CC-PROCESS-INSTANCE                 
                  AND ACTV_IND           = 'Y'                                  
                  AND PRC_CNTL_STAT_CDE  = 'P'                                  
           END-EXEC.                                                            
                                                                                
                                                                                
      *---------------------------------------------------------------          
      * 1210-DECLARE-GEO-PROD-CSR                                               
      *     DECLARE THE GEO PROD CURSOR.                                        
      *---------------------------------------------------------------          
       1210-DECLARE-GEO-PROD-CSR.                                               
                                                                                
           EXEC SQL                                                             
                DECLARE GEOPRODCSR CURSOR FOR                                   
                  SELECT /*+ FIRST_ROWS */                                      
                         A.STYL_ID                                              
                        ,A.STR_NBR                                              
                        ,TO_CHAR(A.PRICG_FWK_EFF_BEG_DTE,                       
                                 'MM-DD-YYYY-HH24.MI.SS')                       
                        ,A.STR_STYL_STAT_CDE                                    
                        ,TO_CHAR(A.SLS_INTRD_DTE,                               
                                 'MM-DD-YYYY-HH24.MI.SS')                       
                        ,A.LAST_ACTV_UNT_RTL_AMT                                
                  FROM MOT_STGG_STR_STYL_PRICG_LCYC A                           
                  WHERE A.PRC_CDE = 'N'                                         
                    AND A.DEPT_NBR = :PV-DEPT-NBR                               
                    AND A.PRICG_FWK_EFF_BEG_DTE <= SYSDATE                      
           END-EXEC.                                                            
                                                                                
      *1210-EXIT.                                                               
       EJECT.                                                                   
                                                                                
                                                                                
      ******************************************************************        
      * CONTROL PROCESSING BASED ON DEPARTMENT NUMBERS.                         
      ******************************************************************        
       C100-CONTROL.                                                            
                                                                                
           PERFORM 2000-PROCESS-CURSORS.                                        
           PERFORM 4000-UPDATE-PROCESSED.                                       
           PERFORM D500-UPDATE-DEPT-COMPLETE.                                   
           PERFORM C300-FETCH-DEPTS-CSR.                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * OPENS THE PROCESS CONTROL TABLE CURSOR.                                 
      *----------------------------------------------------------               
       C200-OPEN-DEPTS-CSR.                                                     
                                                                                
           EXEC SQL                                                             
               OPEN DEPTSCSR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-DEPTS-CSR TO TRUE                         
               WHEN OTHER                                                       
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE 'C200-OPEN-DEPTS-CSR'                                  
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON DEPTSCSR'                               
                                      TO PV-OPERATION-MSG                       
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * FETCHES THE PROCESS CONTROL TABLE CURSOR.                               
      *----------------------------------------------------------               
       C300-FETCH-DEPTS-CSR.                                                    
                                                                                
           EXEC SQL                                                             
             FETCH DEPTSCSR                                                     
               INTO :PV-DEPT-NBR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    ADD 1 TO PA-CNT-DEPTS                                       
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-DEPTS-CSR TO TRUE                             
               WHEN OTHER                                                       
                    MOVE SQLCODE      TO PV-SQL-CODE                            
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE 'C300-FETCH-DEPTS-CSR'                                 
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON DEPTSCSR'                              
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * CLOSE THE PROCESS CONTROL TABLE CURSOR.                                 
      *----------------------------------------------------------               
       C400-CLOSE-DEPTS-CSR.                                                    
                                                                                
           EXEC SQL                                                             
               CLOSE DEPTSCSR                                                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE 'C400-CLOSE-DEPTS-CSR'                                 
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON DEPTSCSR'                              
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
      ******************************************************************        
      * MARK PROCESS CONTROL TABLE COMPLETE FOR A DEPARTMENT.                   
      ******************************************************************        
       D500-UPDATE-DEPT-COMPLETE.                                               
                                                                                
           EXEC SQL EXECUTE                                                     
             BEGIN                                                              
               K_MO01.K_MOPG_SAS_UTILITIES.K_MOSP_CNTL_DEPT_COMPLETE(           
               :PV-DEPT-NBR);                                                   
             END;                                                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROES                                              
                 DISPLAY 'DEPARTMENT PROCESSING COMPLETE: ' PV-DEPT-NBR         
             WHEN OTHER                                                         
                 MOVE SQLCODE                     TO PV-SQL-CODE                
                 MOVE 'MOTW_PRC_CNTL'             TO PV-TABLE-NAME              
                 MOVE 'D500-UPDATE-DEPT-COMPLETE' TO PV-PARAGRAPH-NAME          
                 PERFORM 9900-ORACLE-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 2000-PROCESS-CURSORS.                                                   
      * PROCESS EACH DEPARTMENT                                                 
      *----------------------------------------------------------               
       2000-PROCESS-CURSORS.                                                    
                                                                                
           PERFORM 3010-OPEN-GEO-PROD-CSR.                                      
           PERFORM 3020-FETCH-GEO-PROD-CSR                                      
             UNTIL PS-END-OF-GEO-PROD-CSR.                                      
           PERFORM 3030-CLOSE-GEO-PROD-CSR.                                     
                                                                                
      *2000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3010-OPEN-GEO-PROD-CSR.                                                 
      *     OPENS THE ADVERTISED PRICING CURSOR.                                
      *----------------------------------------------------------               
       3010-OPEN-GEO-PROD-CSR.                                                  
                                                                                
           EXEC SQL                                                             
                 OPEN GEOPRODCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZEROES                                            
                    SET PS-NOT-END-OF-GEO-PROD-CSR TO TRUE                      
               WHEN OTHER                                                       
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE SQLCODE             TO PV-SQL-CODE                     
                    MOVE '3010-OPEN-GEO-PROD-CSR'                               
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'OPEN ERROR ON GEOPRODCSR'                             
                                      TO PV-OPERATION-MSG                       
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      *3010-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3020-FETCH-GEO-PROD-CSR.                                                
      *     FETCHES THE ADVERTISED PRICING CURSOR.                              
      *----------------------------------------------------------               
       3020-FETCH-GEO-PROD-CSR.                                                 
                                                                                
           EXEC SQL                                                             
               FETCH GEOPRODCSR                                                 
                 INTO :PV-STYL-ID                                               
                     ,:PV-STR-NBR                                               
                     ,:PV-SAS-STRT-TMST                                         
                     ,:PV-STYL-STAT-CDE                                         
                     ,:PV-SAS-SLS-INTRD-DTE                                     
                     ,:PV-LAST-ACTV-UNT-RTL-AMT                                 
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    MOVE PV-STYL-ID TO MO118-STYL-ID                            
      *             STORE IS LEVEL 5 IN THE HIERARCHY                           
                    MOVE '5'        TO MO118-STR-LVL-CDE                        
                    MOVE PV-STR-NBR TO MO118-STR-NBR                            
                    MOVE PV-SAS-STRT-TMST-REDEF TO                              
                         MO118-STRT-TMST                                        
                    MOVE PV-SAS-SLS-INTRD-DTE-REDEF TO                          
                         MO118-SLS-INTRD-DTE                                    
                    MOVE PV-LAST-ACTV-UNT-RTL-AMT TO                            
                         MO118-LAST-ACTV-UNT-RTL-AMT                            
                    MOVE PV-STYL-STAT-CDE TO MO118-STYL-STAT-CDE                
                    MOVE 'I' TO MO118-CHANGE-FLG                                
                    PERFORM 9000-WRITE-SAS-GEO-PROD-REC                         
               WHEN SQLCODE = 1403                                              
                    SET PS-END-OF-GEO-PROD-CSR TO TRUE                          
               WHEN OTHER                                                       
                    MOVE SQLCODE      TO PV-SQL-CODE                            
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '3020-FETCH-GEO-PROD-CSR'                              
                                      TO PV-PARAGRAPH-NAME                      
                    MOVE 'FETCH ERROR ON GEOPRODCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      *3020-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 3030-CLOSE-GEO-PROD-CSR.                                                
      *     CLOSE THE ADVERTISED PRICING CURSOR.                                
      *----------------------------------------------------------               
       3030-CLOSE-GEO-PROD-CSR.                                                 
                                                                                
           EXEC SQL                                                             
                CLOSE GEOPRODCSR                                                
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE     TO PV-SQL-CODE                             
                    SET ABEND-4013-SQL-ERROR TO TRUE                            
                    MOVE '3030-CLOSE-GEO-PROD-CSR'                              
                                     TO PV-PARAGRAPH-NAME                       
                    MOVE 'CLOSE ERROR ON GEOPRODCSR'                            
                                     TO PV-OPERATION-MSG                        
                    PERFORM 9900-ORACLE-ABEND                                   
           END-EVALUATE.                                                        
                                                                                
      *3030-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *****************************************************************         
      * 4000-UPDATE-PROCESSED.                                        *         
      *****************************************************************         
       4000-UPDATE-PROCESSED.                                                   
                                                                                
           EXEC SQL                                                             
               UPDATE                                                           
                   MOT_STGG_STR_STYL_PRICG_LCYC                                 
               SET                                                              
                   PRC_CDE = 'Y'                                                
                  ,PRC_CHG_DTE = SYSDATE                                        
                  WHERE PRC_CDE = 'N'                                           
                    AND DEPT_NBR = :PV-DEPT-NBR                                 
                    AND PRICG_FWK_EFF_BEG_DTE <= SYSDATE                        
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZEROS                                               
             WHEN SQLCODE = 1403                                                
             WHEN SQLCODE = -1403                                               
               ADD +1  TO PA-UPDATE-PROCESSED                                   
                                                                                
             WHEN OTHER                                                         
               MOVE 'MOT_STGG_STR_STYL_PRICG_LCYC' TO PV-TABLE-NAME             
               MOVE '4000-UPDATE-PROCESSED'   TO PV-PARAGRAPH-NAME              
               DISPLAY 'PV-DEPT-NBR = ' PV-DEPT-NBR                             
               MOVE SQLCODE             TO PV-SQL-CODE                          
               SET ABEND-4013-SQL-ERROR TO TRUE                                 
               MOVE 'UPDATE PRC_CDE'    TO PV-OPERATION-MSG                     
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *4000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
034503****************************************************************          
034504** READ THE ORACLE LOGON CONTROL CARD.                        **          
034505****************************************************************          
034506 7000-READ-ORACLE-LOGON.                                                  
034510     READ ORACLE-LOGON-FILE                                               
034520         INTO CC-ORACLE-CONNECTION                                        
034530         AT END                                                           
034540             SET PS-END-OF-LOGON-FILE TO TRUE                             
034550     END-READ.                                                            
034600*                                                                         
                                                                                
      *----------------------------------------------------------               
      * 9000-WRITE-SAS-GEO-PROD-REC.                                            
      *     WRITES THE GEO PROD RECORD TO THE SAS FILE.                         
      *----------------------------------------------------------               
       9000-WRITE-SAS-GEO-PROD-REC.                                             
                                                                                
           WRITE SAS-GEO-PROD-REC                                               
               FROM MO118-SAS-GEO-PROD.                                         
                                                                                
           ADD 1 TO PA-RECS-WRITTEN                                             
                    PA-KEEP-TRACK.                                              
           IF PA-KEEP-TRACK = 10000                                             
              MOVE 0 TO PA-KEEP-TRACK                                           
              DISPLAY 'RECS WRITTEN: ' PA-RECS-WRITTEN                          
              PERFORM 9400-FIND-SYSDATE                                         
              DISPLAY 'TIMESTAMP: ' PV-SYS-DATE                                 
           END-IF.                                                              
                                                                                
      *9000-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *****************************************************************         
      * 9100-COMMIT                                                   *         
      * ==> PROCESSES:                                                *         
      *  - SAVE WHAT HAS BEEN DONE TO THE DATABASE SO FAR             *         
      * ==> SQLCODE:                                                  *         
      *  - 0 = COMMIT WAS SUCCESSFUL                                  *         
      *****************************************************************         
       9100-COMMIT.                                                             
                                                                                
           EXEC SQL                                                             
               COMMIT                                                           
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZEROES                                                  
               ADD +1 TO PA-NBR-OF-COMMITS                                      
           ELSE                                                                 
               MOVE 'PGM ABEND DURING A COMMIT' TO PV-OPERATION-MSG             
               MOVE '9100-COMMIT'               TO PV-PARAGRAPH-NAME            
               PERFORM 9900-ORACLE-ABEND                                        
           END-IF.                                                              
                                                                                
      *-----------------------------------------------------------              
      * 9400-FIND-SYSDATE:                                                      
      * ==> PROCESSES:                                                          
      *  - SINCE NO TABLE IS ACTUALLY USED WE USE THE DUAL TABLE                
      *  - FIND THE CURRENT DATE AND TIME                                       
      * ==> SQLCODE:                                                            
      *  - 0 = A ROW WAS FOUND.                                                 
      * ==> PERFORMED FROM: A100-MAINLINE                                       
      *-----------------------------------------------------------              
       9400-FIND-SYSDATE.                                                       
           EXEC SQL                                                             
               SELECT                                                           
                   SYSDATE                                                      
               INTO :PV-SYS-DATE                                                
               FROM                                                             
                   DUAL                                                         
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
             WHEN SQLCODE = ZERO                                                
                 CONTINUE                                                       
             WHEN OTHER                                                         
               MOVE SQLCODE               TO PV-SQL-CODE                        
               MOVE 'DUAL'                TO PV-TABLE-NAME                      
               MOVE 'C100-FIND-SYSDATE'   TO PV-PARAGRAPH-NAME                  
               PERFORM 9900-ORACLE-ABEND                                        
           END-EVALUATE.                                                        
                                                                                
      *9400-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 9800-ABEND.                                                             
      *     PERFORMS A NON-SQL-ABEND.                                           
      *----------------------------------------------------------               
      *9800-ABEND.                                                              
      *                                                                         
      *    DISPLAY '** ABEND           **'.                                     
      *    DISPLAY '** PROGRAM         ** = ' PC-PROGRAM-NAME.                  
      *    DISPLAY '** PARAGRAPH       ** = ' PV-PARAGRAPH-NAME.                
      *    DISPLAY '** OPERATION       ** = ' PV-OPERATION-MSG.                 
      *    DISPLAY '**                 ** = ' PV-OPERATION-MSG-1.               
      *                                                                         
      *    CALL 'ILBOABN0' USING ABEND-CODE.                                    
      *                                                                         
      *9800-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 9900-ORACLE-ABEND.                                                      
      *     PERFORM AN ABEND THAT OCCURED RELATED TO ORACLE.                    
      *----------------------------------------------------------               
       9900-ORACLE-ABEND.                                                       
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = SQLERROR'.                           
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** SQLCODE        ** = ' PV-SQL-CODE.                       
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                           
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                          
           DISPLAY '** SQLERRD(5)     ** = ' SQLERRD(5).                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** TABLE          ** = ' PV-TABLE-NAME.                     
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
      *9900-EXIT.                                                               
       EJECT.                                                                   
                                                                                
      *----------------------------------------------------------               
      * 9990-APPLICATION-ABEND.                                                 
      *     PERFORM AN ABEND IF AN ERROR IS DETECTED.                           
      *----------------------------------------------------------               
       9990-APPLICATION-ABEND.                                                  
                                                                                
           DISPLAY '                       '.                                   
           DISPLAY '** ABEND **       ** = '.                                   
           DISPLAY '** PROGRAM        ** = ' PC-PROGRAM-NAME.                   
           DISPLAY '** ABEND CODE     ** = ' ABEND-CODE.                        
           DISPLAY '** PARAGRAPH      ** = ' PV-PARAGRAPH-NAME.                 
           DISPLAY '** OPERATION      ** = ' PV-OPERATION-MSG.                  
           DISPLAY '                       '.                                   
                                                                                
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
                                                                                
      *----------------------------------------------------------               
      * 9999-WRAPUP.                                                            
      *     END OF PROGRAM ROUTINE.  CLOSES ALL FILES AND DISPLAYS              
      *     ANY DATA FROM THE PROGRAM.                                          
      *----------------------------------------------------------               
       9999-WRAPUP.                                                             
                                                                                
           CLOSE PROCESS-INSTANCE-FILE                                          
                 DATE-RANGE-FILE                                                
                 SAS-GEO-PROD-FILE.                                             
                                                                                
           DISPLAY 'TOTAL DEPARTMENTS PROCESSED: ' PA-CNT-DEPTS                 
           DISPLAY 'NUMBER OF RECS WRITTEN:  ' PA-RECS-WRITTEN.                 
           DISPLAY '*********************************************'.             
                                                                                
      *9999-EXIT.                                                               
       EJECT.                                                                   
                                                                                
