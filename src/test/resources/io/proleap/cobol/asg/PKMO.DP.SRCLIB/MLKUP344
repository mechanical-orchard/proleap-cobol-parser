       IDENTIFICATION DIVISION.                                         00010011
       PROGRAM-ID.    MLKUP344.                                         00020011
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00030011
       DATE-WRITTEN.  09-01-2000.                                       00040011
       DATE-COMPILED.                                                   00050011
                                                                        00060011
      ******************************************************************00070011
      * MLKUP344 - WEEKLY MAJOR CLASS CHANNEL MERCHANDISE TABLE UPDATE  00080011
      *            - TWKMCLC                                            00090011
      *                                                                 00100011
      *     THIS PROGRAM WILL MAINTAIN THE WEEKLY MAJOR CLASS/CHNL DB2  00110011
      * TABLE WITH THE CURRENT WEEK'S DATA.  THE TABLE IS READ USING    00120011
      * THE TABLE KEY FROM EACH INPUT FILE RECORD.  IF THE ROW IS       00130011
      * FOUND, THE ROW IS UPDATED WITH DATA FROM THE INPUT FILE RECORD. 00140011
      * IF THE ROW IS NOT FOUND, A NEW ROW IS INSERTED ON THE DB2       00150011
      * TABLE (TWKMCLC). COMMITS ARE TAKEN EVERY TEN SECONDS, AND       00160011
      * CHECKPOINT RESTART LOGIC IS INCORPORATED.                       00170011
      *                                                                 00180011
      *CHANGE HISTORY:                                                  00190011
      * WR/PROJ DATE       PROGRAMMER   DESCRIPTION                     00200011
      * ------- ---------- ------------ --------------------------      00210011
23565 * 23565   09-10-2003 ROD JANSSEN  DETAILED PURCH ADJUSTMENTS ENH. 00220011
25554 * 25554   12-24-2003 PAUL KEBER   MARKDOWN REFINEMENT FOR PLANNING00230011
W26603******************************************************************00240011
W26603* CHG ID   DATE        DESCRIPTION                               *00250011
W26603* ------   ----------  ----------------------------------------- *00260011
W26603* W26603   04-14-2004  STORE EXPANSION.  EXPANDED SOME PROGRAM   *00270011
W26603*                      VARIABLES TO ACCOMODATE INCREASED LENGTH  *00280011
W26603*                      OF DOLLAR AMOUNTS.  ADDED 'WITH UR' TO    *00290011
W26603*                      A DB2 SELECT.                             *00300011
W26603*                                                                *00310011
W26603*                      - RONNA MCDONALD                          *00320011
W27511* W27511  12-28-2004 BROADCAST OPTIMIZATION SPLIT PLANNING        00330011
W27511*                    - J LINDEMANN                                00340011
W28630* W28630  04-15-2005 LICENSE FEE SPLIT PLANNING                   00350011
W28630*                    - S ASEN                                     00360011
      ******************************************************************00370011
                                                                        00380011
       ENVIRONMENT DIVISION.                                            00390011
       CONFIGURATION SECTION.                                           00400011
       SOURCE-COMPUTER.        IBM-3090.                                00410011
       OBJECT-COMPUTER.        IBM-3090.                                00420011
       INPUT-OUTPUT SECTION.                                            00430011
       FILE-CONTROL.                                                    00440011
                                                                        00450011
           SELECT UPDATE-FILE                                           00460011
               ASSIGN TO INP01.                                         00470011
                                                                        00480011
       DATA DIVISION.                                                   00490011
       FILE SECTION.                                                    00500011
                                                                        00510011
       FD  UPDATE-FILE                                                  00520011
           LABEL RECORDS ARE STANDARD                                   00530011
           RECORDING MODE IS F                                          00540011
           BLOCK CONTAINS 0 RECORDS.                                    00550011
30578  01  UPDATE-RECORD               PIC X(257).                      00560011
                                                                        00570011
      ******************************************************************00580011
       WORKING-STORAGE SECTION.                                         00590011
      ******************************************************************00600011
                                                                        00610011
       01  FILLER                      PIC X(28)   VALUE                00620011
                'BEGINNING OF WORKING STORAGE'.                         00630011
                                                                        00640011
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00650011
                                                                        00660011
       01  PC-PROGRAM-CONSTANTS.                                        00670011
           05  PC-JOB-NAME             PIC  X(06)    VALUE 'MLK289'.    00680011
           05  PC-PROGRAM-NAME         PIC  X(08)    VALUE 'MLKUP344'.  00690011
           05  PC-MAX-DEADLOCKS        PIC S9(03) VALUE +3.             00700011
                                                                        00710011
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00720011
           05  PE-MSG-01                       PIC X(50) VALUE          00730011
                'ATTEMPT TO SELECT ROW FOR UPDATE/INSERT FAILED  '.     00740011
           05  PE-MSG-02                       PIC X(50) VALUE          00750011
                'ATTEMPT TO UPDATE ROW ON TABLE TWKMCLC FAILED   '.     00760011
           05  PE-MSG-03                       PIC X(50) VALUE          00770011
                'ATTEMPT TO INSERT ROW ON TABLE TWKMCLC FAILED   '.     00780011
           05  PE-MSG-04                       PIC X(50) VALUE          00790011
                'ERROR IN SELECT FROM TCKRSTCNTL                 '.     00800011
           05  PE-MSG-05                       PIC X(50) VALUE          00810011
                'UPDATE TO TCKRSTINF FAILED                      '.     00820011
           05  PE-MSG-06                       PIC X(50) VALUE          00830011
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT'.     00840011
           05  PE-MSG-07                       PIC X(50) VALUE          00850011
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00860011
      *                                                                 00870011
       01  PS-PROGRAM-SWITCHES.                                         00880011
           05  PS-EOF-UPDATE-FILE-SW        PIC X(01)     VALUE 'N'.    00890011
               88  PS-EOF-UPDATE-FILE                     VALUE 'Y'.    00900011
           05  PS-WKMCLC-CURSOR-EOF-SW      PIC X(01)     VALUE 'N'.    00910011
               88  PS-END-OF-TABLE-RECS                   VALUE 'Y'.    00920011
           05  PS-FIRST-COMMIT-SW           PIC X(01)     VALUE 'N'.    00930011
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00940011
           05  PS-DEADLOCK-RESTART-SW       PIC X(01)     VALUE 'N'.    00950011
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00960011
                                                                        00970011
       01  PI-PROGRAM-INDICATORS.                                       00980011
           05  PI-PROCESSING-OPTIONS-IND    PIC X(01)     VALUE 'P'.    00990011
               88  PI-UPDATE-WKMCLC-ROW                   VALUE 'U'.    01000011
               88  PI-INSERT-WKMCLC-ROW                   VALUE 'I'.    01010011
      *                                                                 01020011
       01  PROGRAM-VARIABLES.                                           01030011
           05  PV-RETRY-COUNT                  PIC S9(04) VALUE +0.     01040011
           05  PV-DEADLOCK-COUNT               PIC S9(04) VALUE +0.     01050011
      *                                                                 01060011
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. 01070011
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 01080011
           05  PV-OPERATION-ATTEMPTED          PIC  X(50) VALUE SPACES. 01090011
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 01100011
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01110011
      *                                                                 01120011
       01  PA-PROGRAM-ACCUMULATORS.                                     01130011
           05  PA-RECORD-COUNTS.                                        01140011
               10  PA-INPUT-COUNT        PIC  9(09)  VALUE ZEROES.      01150011
               10  PA-UPDATE-COUNT       PIC  9(09)  VALUE ZEROES.      01160011
               10  PA-INSERT-COUNT       PIC  9(09)  VALUE ZEROES.      01170011
               10  PA-MAJ-CL-PROCESSED   PIC  9(09)  VALUE ZEROES.      01180011
               10  PA-COMMIT-COUNT       PIC  9(09)  VALUE ZEROES.      01190011
                                                                        01200011
       01  PD-PROGRAM-DISPLAYS.                                         01210011
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01220011
                                                                        01230011
       01  SAVE-AREA.                                                   01240011
           05  SV-PRIMARY-KEY.                                          01250011
               10  SV-FSCL-WK-NBR        PIC X(02)  VALUE SPACES.       01260011
               10  SV-FSCL-WK-END-DTE    PIC X(10)  VALUE SPACES.       01270011
               10  SV-DEPT-NBR           PIC X(03)  VALUE SPACES.       01280011
               10  SV-MAJ-CL-NBR         PIC X(02)  VALUE SPACES.       01290011
               10  SV-CHNL-CDE           PIC X(01)  VALUE SPACE.        01300011
           05  SV-UPDATE-FIELDS.                                        01310011
W26603         10  SV-SLS-RTL-AMT        PIC S9(11)V99  COMP-3 VALUE +0.01320011
W26603         10  SV-MKDN-PLU-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01330011
W26603         10  SV-MKDN-RGST-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01340011
W26603         10  SV-MKDN-UMTCH-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01350011
W26603         10  SV-MKDN-BYR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01360011
W26603         10  SV-MKDN-STR-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01370011
W26603         10  SV-MKUP-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01380011
W26603         10  SV-XFER-IN-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01390011
W26603         10  SV-XFER-OUT-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01400011
W26603         10  SV-RCPT-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01410011
W26603         10  SV-RCPT-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01420011
W26603         10  SV-PADJ-RTL-AMT       PIC S9(11)V99  COMP-3 VALUE +0.01430011
W26603         10  SV-PADJ-NET-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01440011
W26603         10  SV-PADJ-FRGT-COST-AMT PIC S9(11)V99  COMP-3 VALUE +0.01450011
W26603         10  SV-INV-ADJ-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01460011
W26603         10  SV-PADJ-RTV-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01470011
W26603         10  SV-PADJ-RTV-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01480011
W26603         10  SV-PADJ-DA-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01490011
W26603         10  SV-PADJ-DA-RTL-AMT    PIC S9(11)V99  COMP-3 VALUE +0.01500011
W26603         10  SV-PADJ-OTH-COST-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01510011
W26603         10  SV-PADJ-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01520011
W26603         10  SV-MKDN-PROMO-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01530011
W26603         10  SV-MKDN-PRPT-RTL-AMT  PIC S9(11)V99  COMP-3 VALUE +0.01540011
W26603         10  SV-MKDN-GLDST-RTL-AMT PIC S9(11)V99  COMP-3 VALUE +0.01550011
W26603         10  SV-MKDN-OTH-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01560011
W27511         10  SV-PADJ-BROP-COST-AMT                                01570011
W27511                                   PIC S9(11)V99  COMP-3 VALUE +0.01580011
W28630         10  SV-LIC-FEE-COST-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01590011
W30578         10  SV-MKDN-EMP-RTL-AMT   PIC S9(11)V99  COMP-3 VALUE +0.01600012
                                                                        01610011
      ******************************************************************01620011
      * MLRD150 - M/L WEEKLY MAJOR CLASS CHANNEL CODE UPDATE REC LAYOUT 01630011
      ******************************************************************01640011
23565      COPY MLRD150.                                                01650011
                                                                        01660011
      ******************************************************************01670011
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01680011
      ******************************************************************01690011
           COPY DPWS004.                                                01700011
                                                                        01710011
      ******************************************************************01720011
      *  USED FOR DB2 ERRORS                                            01730011
      ******************************************************************01740011
           EXEC SQL                                                     01750011
               INCLUDE SQLCA                                            01760011
           END-EXEC.                                                    01770011
                                                                        01780011
      ******************************************************************01790011
      *  M/L WEEK-TO-DATE MAJOR CLASS TABLE                             01800011
      ******************************************************************01810011
           EXEC SQL                                                     01820011
               INCLUDE TWKMCLC                                          01830011
           END-EXEC.                                                    01840011
                                                                        01850011
      ***************************************************************** 01860011
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01870011
      ***************************************************************** 01880011
           EXEC SQL                                                     01890011
               INCLUDE TCKRSTCN                                         01900011
           END-EXEC.                                                    01910011
      *                                                                 01920011
           EXEC SQL                                                     01930011
               INCLUDE TCKRSTIN                                         01940011
           END-EXEC.                                                    01950011
      *                                                                 01960011
      ******************************************************************01970011
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01980011
      ******************************************************************01990011
       01  CR-CHECKPOINT-RESTART-AREA.                                  02000011
           05  CR-AREA-LEN                  PIC S9(04) VALUE +28  COMP. 02010011
           05  CR-CHECKPOINT-RESTART-VAR.                               02020011
               10  CR-PREV-DTL-KEY          PIC  X(18) VALUE SPACES.    02030011
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    02040011
                   15  CR-FISCAL-NBR        PIC  X(02).                 02050011
                   15  CR-FISCAL-WK-END-DTE PIC  X(10).                 02060011
                   15  CR-DEPT-NBR          PIC  X(03).                 02070011
                   15  CR-MAJ-CL-NBR        PIC  X(02).                 02080011
                   15  CR-CHNL-CDE          PIC  X(01).                 02090011
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    02100011
      *                                                                 02110011
       01  CK-CHECKPOINT-SAVE-AREA.                                     02120011
           05  CK-SAVE-PREV-KEY         PIC  X(18) VALUE SPACES.        02130011
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       02140011
               10  CK-FISCAL-NBR        PIC  X(02).                     02150011
               10  CK-FISCAL-WK-END-DTE PIC  X(10).                     02160011
               10  CK-DEPT-NBR          PIC  X(03).                     02170011
               10  CK-MAJ-CL-NBR        PIC  X(02).                     02180011
               10  CK-CHNL-CDE          PIC  X(01).                     02190011
                                                                        02200011
      ******************************************************************02210011
      ******************************************************************02220011
       PROCEDURE DIVISION.                                              02230011
      ******************************************************************02240011
                                                                        02250011
       0000-MLKUP344.                                                   02260011
                                                                        02270011
           PERFORM 1000-INITIALIZATION.                                 02280011
                                                                        02290011
           PERFORM 2000-PROCESS-INPUT                                   02300011
                UNTIL PS-EOF-UPDATE-FILE.                               02310011
                                                                        02320011
           PERFORM 8000-EOJ-ROUTINE.                                    02330011
                                                                        02340011
           STOP RUN.                                                    02350011
                                                                        02360011
      ******************************************************************02370011
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 02380011
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02390011
      ******************************************************************02400011
      *                                                                 02410011
       1000-INITIALIZATION.                                             02420011
      *                                                                 02430011
           OPEN INPUT UPDATE-FILE.                                      02440011
                                                                        02450011
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02460011
                                                                        02470011
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02480011
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02490011
               PERFORM 7500-SELECT-RESTART-INFO                         02500011
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02510011
                                            CR-CHECKPOINT-RESTART-VAR   02520011
               MOVE CR-PREV-DTL-KEY          TO CK-SAVE-PREV-KEY        02530011
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                02540011
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 02550011
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          02560011
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   02570011
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 02580011
               DISPLAY 'CHANNEL CODE    ' CR-CHNL-CDE                   02590011
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           02600011
           ELSE                                                         02610011
               SET PS-FIRST-COMMIT TO TRUE                              02620011
               INITIALIZE CR-CHECKPOINT-RESTART-VAR                     02630011
           END-IF.                                                      02640011
      *                                                                 02650011
           PERFORM 4000-READ-UPDATE-FILE UNTIL                          02660011
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02670011
            OR PS-EOF-UPDATE-FILE.                                      02680011
      *                                                                 02690011
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02700011
               DISPLAY 'INPUT FILE RESTARTED ON RECORD '                02710011
                        PA-INPUT-COUNT                                  02720011
               DISPLAY 'FISCAL WEEK NBR       ' ML150-FSCL-WK-NBR       02730011
               DISPLAY 'FISCAL WEEK END DATE  ' ML150-FSCL-WK-END-DTE   02740011
               DISPLAY 'DEPARTMENT NBR        ' ML150-DEPT-NBR          02750011
               DISPLAY 'MAJOR CLASS NBR       ' ML150-MAJ-CL-NBR        02760011
               DISPLAY 'CHANNEL CODE          ' ML150-CHNL-CDE          02770011
           END-IF.                                                      02780011
                                                                        02790011
           IF PS-EOF-UPDATE-FILE                                        02800011
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02810011
                   CONTINUE                                             02820011
               ELSE                                                     02830011
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02840011
           ELSE                                                         02850011
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02860011
               DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                  02870011
                    TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'             02880011
           END-IF.                                                      02890011
                                                                        02900011
      ******************************************************************02910011
      * PROCESS THE SORTED/CONDENSED FILE - UPDATE OR INSERT AS NEEDED  02920011
      ******************************************************************02930011
       2000-PROCESS-INPUT.                                              02940011
      *                                                                 02950011
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02960011
      *                                                                 02970011
           PERFORM 7000-SELECT-DB-ROW.                                  02980011
      *                                                                 02990011
           IF PS-PROGRAM-DEADLOCKED                                     03000011
               CONTINUE                                                 03010011
           ELSE                                                         03020011
               EVALUATE TRUE                                            03030011
                   WHEN PI-UPDATE-WKMCLC-ROW                            03040011
                       PERFORM 7100-UPDATE-WKMCLC-ROW                   03050011
                   WHEN PI-INSERT-WKMCLC-ROW                            03060011
                       PERFORM 7200-INSERT-WKMCLC-ROW                   03070011
                   WHEN OTHER                                           03080011
                       MOVE '7000-SELECT-DB-ROW' TO PV-PARAGRAPH-NAME   03090011
                       MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED         03100011
                       PERFORM 9999-SQL-ABEND                           03110011
               END-EVALUATE                                             03120011
           END-IF.                                                      03130011
      *                                                                 03140011
           IF PS-PROGRAM-DEADLOCKED                                     03150011
               CONTINUE                                                 03160011
           ELSE                                                         03170011
               PERFORM 7700-COMMIT-PROCESSING                           03180011
               PERFORM 4000-READ-UPDATE-FILE                            03190011
           END-IF.                                                      03200011
      *                                                                 03210011
      *                                                                 03220011
      ******************************************************************03230011
      * CALCULATE FIELDS FOR UPDATE OF DATABASE                         03240011
      ******************************************************************03250011
       2100-CALCULATE-UPDATES.                                          03260011
      * ADD DB2 TABLE ROW VALUES TO CURRENT INPUT RECORD ROW VALUES     03270011
      *                                                                 03280011
           COMPUTE SV-SLS-RTL-AMT        = WKMCLC-SLS-RTL-AMT           03290011
                                         + ML150-SLS-RTL-AMT.           03300011
           COMPUTE SV-MKDN-PLU-RTL-AMT   = WKMCLC-MKDN-PLU-RTL-AMT      03310011
                                         + ML150-MKDN-PLU-RTL-AMT.      03320011
           COMPUTE SV-MKDN-RGST-RTL-AMT  = WKMCLC-MKDN-RGST-RTL-AMT     03330011
                                         + ML150-MKDN-RGST-RTL-AMT.     03340011
           COMPUTE SV-MKDN-UMTCH-RTL-AMT = WKMCLC-MKDN-UMTCH-RTL-AMT    03350011
                                         + ML150-MKDN-UNMTCH-RTL-AMT.   03360011
           COMPUTE SV-MKDN-BYR-RTL-AMT   = WKMCLC-MKDN-BYR-RTL-AMT      03370011
                                         + ML150-MKDN-BYR-RTL-AMT.      03380011
           COMPUTE SV-MKDN-STR-RTL-AMT   = WKMCLC-MKDN-STR-RTL-AMT      03390011
                                         + ML150-MKDN-STR-RTL-AMT.      03400011
           COMPUTE SV-MKUP-RTL-AMT       = WKMCLC-MKUP-RTL-AMT          03410011
                                         + ML150-MKUP-RTL-AMT.          03420011
           COMPUTE SV-XFER-IN-RTL-AMT    = WKMCLC-XFER-IN-RTL-AMT       03430011
                                         + ML150-XFER-IN-RTL-AMT.       03440011
           COMPUTE SV-XFER-OUT-RTL-AMT   = WKMCLC-XFER-OUT-RTL-AMT      03450011
                                         + ML150-XFER-OUT-RTL-AMT.      03460011
           COMPUTE SV-RCPT-RTL-AMT       = WKMCLC-RCPT-RTL-AMT          03470011
                                         + ML150-RCPT-RTL-AMT.          03480011
           COMPUTE SV-RCPT-NET-COST-AMT  = WKMCLC-RCPT-NET-COST-AMT     03490011
                                         + ML150-RCPT-NET-COST-AMT.     03500011
           COMPUTE SV-PADJ-RTL-AMT       = WKMCLC-PADJ-RTL-AMT          03510011
                                         + ML150-PADJ-RTL-AMT.          03520011
           COMPUTE SV-PADJ-NET-COST-AMT  = WKMCLC-PADJ-NET-COST-AMT     03530011
                                         + ML150-PADJ-NET-COST-AMT.     03540011
           COMPUTE SV-PADJ-FRGT-COST-AMT = WKMCLC-PADJ-FRGT-COST-AMT    03550011
                                         + ML150-PADJ-FRGT-COST-AMT.    03560011
           COMPUTE SV-INV-ADJ-RTL-AMT    = WKMCLC-INV-ADJ-RTL-AMT       03570011
                                         + ML150-INV-ADJ-RTL-AMT.       03580011
23565      COMPUTE SV-PADJ-RTV-COST-AMT  = WKMCLC-PADJ-RTV-COST-AMT     03590011
23565                                    + ML150-PADJ-RTV-COST-AMT.     03600011
23565      COMPUTE SV-PADJ-RTV-RTL-AMT   = WKMCLC-PADJ-RTV-RTL-AMT      03610011
23565                                    + ML150-PADJ-RTV-RTL-AMT.      03620011
23565      COMPUTE SV-PADJ-DA-COST-AMT   = WKMCLC-PADJ-DA-COST-AMT      03630011
23565                                    + ML150-PADJ-DA-COST-AMT.      03640011
23565      COMPUTE SV-PADJ-DA-RTL-AMT    = WKMCLC-PADJ-DA-RTL-AMT       03650011
23565                                    + ML150-PADJ-DA-RTL-AMT.       03660011
23565      COMPUTE SV-PADJ-OTH-COST-AMT  = WKMCLC-PADJ-OTH-COST-AMT     03670011
23565                                    + ML150-PADJ-OTH-COST-AMT.     03680011
23565      COMPUTE SV-PADJ-OTH-RTL-AMT   = WKMCLC-PADJ-OTH-RTL-AMT      03690011
23565                                    + ML150-PADJ-OTH-RTL-AMT.      03700011
25554      COMPUTE SV-MKDN-PROMO-RTL-AMT = WKMCLC-MKDN-PROMO-RTL-AMT    03710011
  |                                      + ML150-MKDN-PROMO-RTL-AMT.    03720011
  |        COMPUTE SV-MKDN-PRPT-RTL-AMT  = WKMCLC-MKDN-PRPT-RTL-AMT     03730011
  |                                      + ML150-MKDN-PRPT-RTL-AMT.     03740011
  |        COMPUTE SV-MKDN-GLDST-RTL-AMT = WKMCLC-MKDN-GLDST-RTL-AMT    03750011
  |                                      + ML150-MKDN-GLDST-RTL-AMT.    03760011
  |        COMPUTE SV-MKDN-OTH-RTL-AMT   = WKMCLC-MKDN-OTH-RTL-AMT      03770011
25554                                    + ML150-MKDN-OTH-RTL-AMT.      03780011
W27511     COMPUTE SV-PADJ-BROP-COST-AMT                                03790011
  |                                   = WKMCLC-PADJ-BROP-COST-AMT       03800011
W27511                                + ML150-PADJ-BROP-COST-AMT.       03810011
W28630     COMPUTE SV-LIC-FEE-COST-AMT   = WKMCLC-LIC-FEE-COST-AMT      03820011
W28630                                   + ML150-LIC-FEE-COST-AMT.      03830011
W30578     COMPUTE SV-MKDN-EMP-RTL-AMT   = WKMCLC-MKDN-EMP-RTL-AMT      03840012
W30578                                   + ML150-MKDN-EMP-RTL-AMT.      03850012
      *                                                                 03860011
      ******************************************************************03870011
      * READS THE CONDENSED FILE INTO THE TWKMCLC LAYOUT                03880011
      ******************************************************************03890011
       4000-READ-UPDATE-FILE.                                           03900011
           READ UPDATE-FILE INTO ML150-REC                              03910011
              AT END                                                    03920011
                MOVE 'Y' TO PS-EOF-UPDATE-FILE-SW.                      03930011
     *                                                                  03940011
           IF PS-EOF-UPDATE-FILE                                        03950011
               MOVE ML150-FSCL-WK-NBR         TO CR-FISCAL-NBR          03960011
               MOVE ML150-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   03970011
               MOVE ML150-DEPT-NBR            TO CR-DEPT-NBR            03980011
               MOVE ML150-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          03990011
               MOVE ML150-CHNL-CDE            TO CR-CHNL-CDE            04000011
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    04010011
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  04020011
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       04030011
                                             TCKRSTINF-WORK-STRG-DESC   04040011
               EXEC SQL                                                 04050011
                 UPDATE TCKRSTINF                                       04060011
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      04070011
                  WHERE JOB_NAME   = :PC-JOB-NAME                       04080011
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   04090011
               END-EXEC                                                 04100011
               IF SQLCODE = 0                                           04110011
                   CONTINUE                                             04120011
               ELSE                                                     04130011
                   MOVE PE-MSG-05          TO PV-OPERATION-ATTEMPTED    04140011
                   MOVE '* 4000-READ-CONDENSED *' TO PV-PARAGRAPH-NAME  04150011
                   PERFORM 9999-SQL-ABEND                               04160011
               END-IF                                                   04170011
            ELSE                                                        04180011
                ADD 1                        TO PA-INPUT-COUNT          04190011
            END-IF.                                                     04200011
      *                                                                 04210011
      ******************************************************************04220011
      * SELECT A ROW FROM THE MTHLY MCL CHNL TAB THAT MATCHES INPUT KEY 04230011
      *   SQLCODE FROM 'SELECT' IS EVALUATED IN 2000-PROCESS-INPUT      04240011
      ******************************************************************04250011
       7000-SELECT-DB-ROW.                                              04260011
      *                                                                 04270011
           EXEC SQL                                                     04280011
               SELECT SLS_RTL_AMT                                       04290011
                     ,MKDN_PLU_RTL_AMT                                  04300011
                     ,MKDN_RGST_RTL_AMT                                 04310011
                     ,MKDN_UMTCH_RTL_AMT                                04320011
                     ,MKDN_BYR_RTL_AMT                                  04330011
                     ,MKDN_STR_RTL_AMT                                  04340011
                     ,MKUP_RTL_AMT                                      04350011
                     ,XFER_IN_RTL_AMT                                   04360011
                     ,XFER_OUT_RTL_AMT                                  04370011
                     ,RCPT_RTL_AMT                                      04380011
                     ,RCPT_NET_COST_AMT                                 04390011
                     ,PADJ_RTL_AMT                                      04400011
                     ,PADJ_NET_COST_AMT                                 04410011
                     ,PADJ_FRGT_COST_AMT                                04420011
                     ,INV_ADJ_RTL_AMT                                   04430011
23565                ,PADJ_RTV_COST_AMT                                 04440011
23565                ,PADJ_RTV_RTL_AMT                                  04450011
23565                ,PADJ_DA_COST_AMT                                  04460011
23565                ,PADJ_DA_RTL_AMT                                   04470011
23565                ,PADJ_OTH_COST_AMT                                 04480011
23565                ,PADJ_OTH_RTL_AMT                                  04490011
25554                ,MKDN_PROMO_RTL_AMT                                04500011
25554                ,MKDN_PRPT_RTL_AMT                                 04510011
25554                ,MKDN_GLDST_RTL_AMT                                04520011
25554                ,MKDN_OTH_RTL_AMT                                  04530011
W27511               ,PADJ_BROP_COST_AMT                                04540011
W28630               ,LIC_FEE_COST_AMT                                  04550011
W30578               ,MKDN_EMP_RTL_AMT                                  04560012
                 INTO :WKMCLC-SLS-RTL-AMT                               04570011
                     ,:WKMCLC-MKDN-PLU-RTL-AMT                          04580011
                     ,:WKMCLC-MKDN-RGST-RTL-AMT                         04590011
                     ,:WKMCLC-MKDN-UMTCH-RTL-AMT                        04600011
                     ,:WKMCLC-MKDN-BYR-RTL-AMT                          04610011
                     ,:WKMCLC-MKDN-STR-RTL-AMT                          04620011
                     ,:WKMCLC-MKUP-RTL-AMT                              04630011
                     ,:WKMCLC-XFER-IN-RTL-AMT                           04640011
                     ,:WKMCLC-XFER-OUT-RTL-AMT                          04650011
                     ,:WKMCLC-RCPT-RTL-AMT                              04660011
                     ,:WKMCLC-RCPT-NET-COST-AMT                         04670011
                     ,:WKMCLC-PADJ-RTL-AMT                              04680011
                     ,:WKMCLC-PADJ-NET-COST-AMT                         04690011
                     ,:WKMCLC-PADJ-FRGT-COST-AMT                        04700011
                     ,:WKMCLC-INV-ADJ-RTL-AMT                           04710011
23565                ,:WKMCLC-PADJ-RTV-COST-AMT                         04720011
23565                ,:WKMCLC-PADJ-RTV-RTL-AMT                          04730011
23565                ,:WKMCLC-PADJ-DA-COST-AMT                          04740011
23565                ,:WKMCLC-PADJ-DA-RTL-AMT                           04750011
23565                ,:WKMCLC-PADJ-OTH-COST-AMT                         04760011
23565                ,:WKMCLC-PADJ-OTH-RTL-AMT                          04770011
25554                ,:WKMCLC-MKDN-PROMO-RTL-AMT                        04780011
25554                ,:WKMCLC-MKDN-PRPT-RTL-AMT                         04790011
25554                ,:WKMCLC-MKDN-GLDST-RTL-AMT                        04800011
25554                ,:WKMCLC-MKDN-OTH-RTL-AMT                          04810011
W27511               ,:WKMCLC-PADJ-BROP-COST-AMT                        04820011
W28630               ,:WKMCLC-LIC-FEE-COST-AMT                          04830011
W30578               ,:WKMCLC-MKDN-EMP-RTL-AMT                          04840012
               FROM TWKMCLC                                             04850011
              WHERE   FSCL_WK_NBR         = :ML150-FSCL-WK-NBR          04860011
                AND   FSCL_WK_END_DTE     = :ML150-FSCL-WK-END-DTE      04870011
                AND   DEPT_NBR            = :ML150-DEPT-NBR             04880011
                AND   MAJ_CL_NBR          = :ML150-MAJ-CL-NBR           04890011
                AND   CHNL_CDE            = :ML150-CHNL-CDE             04900011
W26603        WITH UR                                                   04910011
           END-EXEC.                                                    04920011
      *                                                                 04930011
           EVALUATE TRUE                                                04940011
               WHEN SQLCODE IS EQUAL TO ZERO                            04950011
                   MOVE 'U' TO PI-PROCESSING-OPTIONS-IND                04960011
               WHEN SQLCODE IS EQUAL TO +100                            04970011
                   MOVE 'I' TO PI-PROCESSING-OPTIONS-IND                04980011
               WHEN SQLCODE IS EQUAL TO -911                            04990011
                   DISPLAY '*** -911 OCCURRED ON SELECT ***'            05000011
                   ADD +1             TO PV-DEADLOCK-COUNT              05010011
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05020011
                       MOVE '7000-SELECT-DB-ROW' TO                     05030011
                                                    PV-PARAGRAPH-NAME   05040011
                       PERFORM 9000-DEADLOCK-ERROR                      05050011
                   ELSE                                                 05060011
                       PERFORM 7999-RESTART-PROCESS                     05070011
                   END-IF                                               05080011
               WHEN SQLWARN IS NOT EQUAL TO SPACES                      05090011
               WHEN SQLCODE IS NOT EQUAL TO ZERO                        05100011
                  DISPLAY ML150-FSCL-WK-NBR          ' '                05110011
                          ML150-FSCL-WK-END-DTE      ' '                05120011
                          ML150-DEPT-NBR             ' '                05130011
                          ML150-MAJ-CL-NBR           ' '                05140011
                          ML150-CHNL-CDE             ' '                05150011
                   MOVE '7000-SELECT-DB-ROW'   TO PV-PARAGRAPH-NAME     05160011
                   MOVE PE-MSG-01 TO PV-OPERATION-ATTEMPTED             05170011
                   PERFORM 9999-SQL-ABEND                               05180011
           END-EVALUATE.                                                05190011
      *                                                                 05200011
      *                                                                 05210011
      ***************************************************************** 05220011
      * THE INPUT RECORD FOUND A MATCHING ROW BASED ON FSCL INFO, DEPT  05230011
      * AND MAJOR CLASS. CURRENT TABLE VALUES UPDATED TO INCLUDE VALUES 05240011
      * FROM INPUT REC MLRD150                                          05250011
      ***************************************************************** 05260011
       7100-UPDATE-WKMCLC-ROW.                                          05270011
      *                                                                 05280011
           PERFORM 2100-CALCULATE-UPDATES.                              05290011
      *                                                                 05300011
           EXEC SQL                                                     05310011
               UPDATE TWKMCLC                                           05320011
                  SET SLS_RTL_AMT          = :SV-SLS-RTL-AMT            05330011
                     ,MKDN_PLU_RTL_AMT     = :SV-MKDN-PLU-RTL-AMT       05340011
                     ,MKDN_RGST_RTL_AMT    = :SV-MKDN-RGST-RTL-AMT      05350011
                     ,MKDN_UMTCH_RTL_AMT   = :SV-MKDN-UMTCH-RTL-AMT     05360011
                     ,MKDN_BYR_RTL_AMT     = :SV-MKDN-BYR-RTL-AMT       05370011
                     ,MKDN_STR_RTL_AMT     = :SV-MKDN-STR-RTL-AMT       05380011
                     ,MKUP_RTL_AMT         = :SV-MKUP-RTL-AMT           05390011
                     ,XFER_IN_RTL_AMT      = :SV-XFER-IN-RTL-AMT        05400011
                     ,XFER_OUT_RTL_AMT     = :SV-XFER-OUT-RTL-AMT       05410011
                     ,RCPT_RTL_AMT         = :SV-RCPT-RTL-AMT           05420011
                     ,RCPT_NET_COST_AMT    = :SV-RCPT-NET-COST-AMT      05430011
                     ,PADJ_RTL_AMT         = :SV-PADJ-RTL-AMT           05440011
                     ,PADJ_NET_COST_AMT    = :SV-PADJ-NET-COST-AMT      05450011
                     ,PADJ_FRGT_COST_AMT   = :SV-PADJ-FRGT-COST-AMT     05460011
                     ,INV_ADJ_RTL_AMT      = :SV-INV-ADJ-RTL-AMT        05470011
23565                ,PADJ_RTV_COST_AMT    = :SV-PADJ-RTV-COST-AMT      05480011
23565                ,PADJ_RTV_RTL_AMT     = :SV-PADJ-RTV-RTL-AMT       05490011
23565                ,PADJ_DA_COST_AMT     = :SV-PADJ-DA-COST-AMT       05500011
23565                ,PADJ_DA_RTL_AMT      = :SV-PADJ-DA-RTL-AMT        05510011
23565                ,PADJ_OTH_COST_AMT    = :SV-PADJ-OTH-COST-AMT      05520011
23565                ,PADJ_OTH_RTL_AMT     = :SV-PADJ-OTH-RTL-AMT       05530011
25554                ,MKDN_PROMO_RTL_AMT   = :SV-MKDN-PROMO-RTL-AMT     05540011
25554                ,MKDN_PRPT_RTL_AMT    = :SV-MKDN-PRPT-RTL-AMT      05550011
25554                ,MKDN_GLDST_RTL_AMT   = :SV-MKDN-GLDST-RTL-AMT     05560011
25554                ,MKDN_OTH_RTL_AMT     = :SV-MKDN-OTH-RTL-AMT       05570011
W27511               ,PADJ_BROP_COST_AMT   = :SV-PADJ-BROP-COST-AMT     05580011
W28630               ,LIC_FEE_COST_AMT     = :SV-LIC-FEE-COST-AMT       05590011
W30578               ,MKDN_EMP_RTL_AMT     = :SV-MKDN-EMP-RTL-AMT       05600013
              WHERE   FSCL_WK_NBR          = :ML150-FSCL-WK-NBR         05610011
                AND   FSCL_WK_END_DTE      = :ML150-FSCL-WK-END-DTE     05620011
                AND   DEPT_NBR             = :ML150-DEPT-NBR            05630011
                AND   MAJ_CL_NBR           = :ML150-MAJ-CL-NBR          05640011
                AND   CHNL_CDE             = :ML150-CHNL-CDE            05650011
           END-EXEC                                                     05660011
      *                                                                 05670011
           EVALUATE SQLCODE                                             05680011
               WHEN 0                                                   05690011
                   CONTINUE                                             05700011
               WHEN -911                                                05710011
                   ADD +1             TO PV-DEADLOCK-COUNT              05720011
                   DISPLAY '*** -911 OCCURRED ON UPDATE ***'            05730011
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              05740011
                       MOVE '7100-UPDATE-WKMCLC-ROW' TO                 05750011
                                                    PV-PARAGRAPH-NAME   05760011
                       PERFORM 9000-DEADLOCK-ERROR                      05770011
                   ELSE                                                 05780011
                       PERFORM 7999-RESTART-PROCESS                     05790011
                   END-IF                                               05800011
               WHEN OTHER                                               05810011
                   MOVE '7100-UPDATE-WKMCLC-ROW' TO PV-PARAGRAPH-NAME   05820011
                   MOVE PE-MSG-02              TO PV-OPERATION-ATTEMPTED05830011
                   PERFORM 9999-SQL-ABEND                               05840011
           END-EVALUATE.                                                05850011
      *                                                                 05860011
           ADD 1 TO PA-UPDATE-COUNT.                                    05870011
      *                                                                 05880011
      ***************************************************************** 05890011
      * THERE WAS NO MATCHING ROW ON THE WEEKLY MCLS TABLE. INITIAL   * 05900011
      * - ENTRY FOR THIS ENTITY IS INSERTED AS A 'NEW' ROW            * 05910011
      * - CALCULATED FIELDS ARE INITIALLY SET TO 0 AND UPDATED IN     * 05920011
      *   RECALC                                                      * 05930011
      ***************************************************************** 05940011
       7200-INSERT-WKMCLC-ROW.                                          05950011
      *                                                                 05960011
           EXEC SQL                                                     05970011
               INSERT INTO TWKMCLC                                      05980011
                     (FSCL_WK_NBR                                       05990011
                     ,FSCL_WK_END_DTE                                   06000011
                     ,DEPT_NBR                                          06010011
                     ,MAJ_CL_NBR                                        06020011
                     ,CHNL_CDE                                          06030011
                     ,SLS_RTL_AMT                                       06040011
                     ,MKDN_PLU_RTL_AMT                                  06050011
                     ,MKDN_RGST_RTL_AMT                                 06060011
                     ,MKDN_UMTCH_RTL_AMT                                06070011
                     ,MKDN_BYR_RTL_AMT                                  06080011
                     ,MKDN_STR_RTL_AMT                                  06090011
                     ,MKUP_RTL_AMT                                      06100011
                     ,XFER_IN_RTL_AMT                                   06110011
                     ,XFER_OUT_RTL_AMT                                  06120011
                     ,RCPT_RTL_AMT                                      06130011
                     ,RCPT_NET_COST_AMT                                 06140011
                     ,PADJ_RTL_AMT                                      06150011
                     ,PADJ_NET_COST_AMT                                 06160011
                     ,PADJ_FRGT_COST_AMT                                06170011
                     ,INV_ADJ_RTL_AMT                                   06180011
                     ,SHNK_RTL_AMT                                      06190011
                     ,END_INV_RTL_AMT                                   06200011
                     ,CUM_MKUP_PCT                                      06210011
                     ,END_INV_COST_AMT                                  06220011
                     ,SLS_COST_AMT                                      06230011
23565                ,GRO_MRGN_AMT                                      06240011
23565                ,PADJ_RTV_COST_AMT                                 06250011
23565                ,PADJ_RTV_RTL_AMT                                  06260011
23565                ,PADJ_DA_COST_AMT                                  06270011
23565                ,PADJ_DA_RTL_AMT                                   06280011
23565                ,PADJ_OTH_COST_AMT                                 06290011
23565                ,PADJ_OTH_RTL_AMT                                  06300011
25554                ,MKDN_PROMO_RTL_AMT                                06310011
25554                ,MKDN_PRPT_RTL_AMT                                 06320011
25554                ,MKDN_GLDST_RTL_AMT                                06330011
25554                ,MKDN_OTH_RTL_AMT                                  06340011
W27511               ,PADJ_BROP_COST_AMT                                06350011
W28630               ,LIC_FEE_COST_AMT                                  06360011
W30578               ,MKDN_EMP_RTL_AMT)                                 06370012
               VALUES                                                   06380011
                     (:ML150-FSCL-WK-NBR                                06390011
                     ,:ML150-FSCL-WK-END-DTE                            06400011
                     ,:ML150-DEPT-NBR                                   06410011
                     ,:ML150-MAJ-CL-NBR                                 06420011
                     ,:ML150-CHNL-CDE                                   06430011
                     ,:ML150-SLS-RTL-AMT                                06440011
                     ,:ML150-MKDN-PLU-RTL-AMT                           06450011
                     ,:ML150-MKDN-RGST-RTL-AMT                          06460011
                     ,:ML150-MKDN-UNMTCH-RTL-AMT                        06470011
                     ,:ML150-MKDN-BYR-RTL-AMT                           06480011
                     ,:ML150-MKDN-STR-RTL-AMT                           06490011
                     ,:ML150-MKUP-RTL-AMT                               06500011
                     ,:ML150-XFER-IN-RTL-AMT                            06510011
                     ,:ML150-XFER-OUT-RTL-AMT                           06520011
                     ,:ML150-RCPT-RTL-AMT                               06530011
                     ,:ML150-RCPT-NET-COST-AMT                          06540011
                     ,:ML150-PADJ-RTL-AMT                               06550011
                     ,:ML150-PADJ-NET-COST-AMT                          06560011
                     ,:ML150-PADJ-FRGT-COST-AMT                         06570011
                     ,:ML150-INV-ADJ-RTL-AMT                            06580011
                     ,0                                                 06590011
                     ,0                                                 06600011
                     ,0                                                 06610011
                     ,0                                                 06620011
                     ,0                                                 06630011
                     ,0                                                 06640011
23565                ,:ML150-PADJ-RTV-COST-AMT                          06650011
23565                ,:ML150-PADJ-RTV-RTL-AMT                           06660011
23565                ,:ML150-PADJ-DA-COST-AMT                           06670011
23565                ,:ML150-PADJ-DA-RTL-AMT                            06680011
23565                ,:ML150-PADJ-OTH-COST-AMT                          06690011
23565                ,:ML150-PADJ-OTH-RTL-AMT                           06700011
25554                ,:ML150-MKDN-PROMO-RTL-AMT                         06710011
25554                ,:ML150-MKDN-PRPT-RTL-AMT                          06720011
25554                ,:ML150-MKDN-GLDST-RTL-AMT                         06730011
25554                ,:ML150-MKDN-OTH-RTL-AMT                           06740011
W27511               ,:ML150-PADJ-BROP-COST-AMT                         06750011
W28630               ,:ML150-LIC-FEE-COST-AMT                           06760011
W30578               ,:ML150-MKDN-EMP-RTL-AMT)                          06770012
           END-EXEC                                                     06780011
      *                                                                 06790011
           EVALUATE SQLCODE                                             06800011
               WHEN 0                                                   06810011
                   CONTINUE                                             06820011
               WHEN -911                                                06830011
                   ADD +1             TO PV-DEADLOCK-COUNT              06840011
                   DISPLAY '*** -911 OCCURRED ON INSERT ***'            06850011
                   IF PV-DEADLOCK-COUNT > PC-MAX-DEADLOCKS              06860011
                       MOVE '7200-INSERT-WKMCLC-ROW' TO                 06870011
                                                    PV-PARAGRAPH-NAME   06880011
                       PERFORM 9000-DEADLOCK-ERROR                      06890011
                   ELSE                                                 06900011
                       PERFORM 7999-RESTART-PROCESS                     06910011
                   END-IF                                               06920011
               WHEN OTHER                                               06930011
                  DISPLAY ML150-FSCL-WK-NBR          ' '                06940011
                          ML150-FSCL-WK-END-DTE      ' '                06950011
                          ML150-DEPT-NBR             ' '                06960011
                          ML150-MAJ-CL-NBR           ' '                06970011
                          ML150-CHNL-CDE             ' '                06980011
                   MOVE '7200-INSERT-WKMCLC-ROW' TO PV-PARAGRAPH-NAME   06990011
                   MOVE PE-MSG-03              TO PV-OPERATION-ATTEMPTED07000011
                   PERFORM 9999-SQL-ABEND                               07010011
           END-EVALUATE.                                                07020011
                                                                        07030011
           ADD 1 TO PA-INSERT-COUNT.                                    07040011
                                                                        07050011
      ******************************************************************07060011
      * 7300-GET-COMMIT-TIMESTAMP.                                     *07070011
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *07080011
      *            CONTROL TABLE                                       *07090011
      ******************************************************************07100011
       7300-GET-COMMIT-TIMESTAMP.                                       07110011
                                                                        07120011
           EXEC SQL                                                     07130011
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        07140011
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       07150011
               INTO :PV-COMMIT-TIMESTAMP                                07160011
               FROM TCKRSTCNTL                                          07170011
              WHERE JOB_NAME  = :PC-JOB-NAME                            07180011
                AND PLAN_NAME = :PC-PROGRAM-NAME                        07190011
           END-EXEC.                                                    07200011
                                                                        07210011
           EVALUATE TRUE                                                07220011
               WHEN SQLCODE = 0                                         07230011
      **     DISPLAY 'NEXT COMMIT AT TIMESTAMP: ', PV-COMMIT-TIMESTAMP  07240011
                   CONTINUE                                             07250011
               WHEN SQLCODE NOT EQUAL ZERO                              07260011
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME07270011
                   PERFORM 9999-SQL-ABEND                               07280011
           END-EVALUATE.                                                07290011
      *                                                                 07300011
      *                                                                 07310011
      ******************************************************************07320011
      * 7400-INIT-CHECKPOINT-SELECT                                    *07330011
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *07340011
      *            IF WE ARE IN A RESTART CONDITION.                   *07350011
      ******************************************************************07360011
       7400-INIT-CHECKPOINT-SELECT.                                     07370011
                                                                        07380011
           EXEC SQL                                                     07390011
             SELECT  CKPT_STAT_CODE                                     07400011
                    ,CKPT_FRQNCY_QTY                                    07410011
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         07420011
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        07430011
               FROM  TCKRSTCNTL                                         07440011
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07450011
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07460011
           END-EXEC.                                                    07470011
                                                                        07480011
           IF SQLCODE = 0                                               07490011
               CONTINUE                                                 07500011
           ELSE                                                         07510011
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  07520011
               MOVE PE-MSG-04             TO PV-OPERATION-ATTEMPTED     07530011
               PERFORM 9999-SQL-ABEND                                   07540011
           END-IF.                                                      07550011
      *                                                                 07560011
      *                                                                 07570011
      ******************************************************************07580011
      * 7500-SELECT-RESTART-INFO                                       *07590011
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *07600011
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *07610011
      ******************************************************************07620011
       7500-SELECT-RESTART-INFO.                                        07630011
                                                                        07640011
           EXEC SQL                                                     07650011
             SELECT  WORK_STRG_DESC                                     07660011
               INTO  :TCKRSTINF-WORK-STRG-DESC                          07670011
               FROM  TCKRSTINF                                          07680011
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07690011
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07700011
           END-EXEC.                                                    07710011
                                                                        07720011
           IF SQLCODE = 0                                               07730011
               CONTINUE                                                 07740011
           ELSE                                                         07750011
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   07760011
               PERFORM 9999-SQL-ABEND                                   07770011
           END-IF.                                                      07780011
                                                                        07790011
      ******************************************************************07800011
      * 7600-SET-CURRENT-TIMESTAMP.                                    *07810011
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *07820011
      ******************************************************************07830011
       7600-SET-CURRENT-TIMESTAMP.                                      07840011
                                                                        07850011
           EXEC SQL                                                     07860011
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           07870011
           END-EXEC.                                                    07880011
                                                                        07890011
           IF SQLCODE = 0                                               07900011
               CONTINUE                                                 07910011
           ELSE                                                         07920011
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 07930011
               PERFORM 9999-SQL-ABEND                                   07940011
           END-IF.                                                      07950011
      *                                                                 07960011
      *                                                                 07970011
      ******************************************************************07980011
      * 7700-COMMIT-PROCESSING.                                        *07990011
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *08000011
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*08010011
      ******************************************************************08020011
       7700-COMMIT-PROCESSING.                                          08030011
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     08040011
                                                                        08050011
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          08060011
                                                                        08070011
      * PREPARE COMMIT RECORD FOR UPDATE                                08080011
           IF PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP                08090011
               MOVE ML150-FSCL-WK-NBR         TO CR-FISCAL-NBR          08100011
               MOVE ML150-FSCL-WK-END-DTE     TO CR-FISCAL-WK-END-DTE   08110011
               MOVE ML150-DEPT-NBR            TO CR-DEPT-NBR            08120011
               MOVE ML150-MAJ-CL-NBR          TO CR-MAJ-CL-NBR          08130011
               MOVE ML150-CHNL-CDE            TO CR-CHNL-CDE            08140011
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    08150011
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  08160011
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       08170011
                                             TCKRSTINF-WORK-STRG-DESC   08180011
               IF PS-FIRST-COMMIT                                       08190011
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                08200011
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                08210011
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       08220011
               END-IF                                                   08230011
               PERFORM 7800-COMMIT-CHANGES                              08240011
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        08250011
           END-IF.                                                      08260011
      *                                                                 08270011
      *                                                                 08280011
      ******************************************************************08290011
      * 7800-COMMIT-CHANGES.                                            08300011
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      08310011
      *            TAKE A COMMIT.                                       08320011
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    08330011
      ******************************************************************08340011
       7800-COMMIT-CHANGES.                                             08350011
                                                                        08360011
           EXEC SQL                                                     08370011
             UPDATE TCKRSTINF                                           08380011
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          08390011
              WHERE JOB_NAME       = :PC-JOB-NAME                       08400011
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   08410011
           END-EXEC.                                                    08420011
                                                                        08430011
           IF SQLCODE = 0                                               08440011
               CONTINUE                                                 08450011
           ELSE                                                         08460011
               MOVE PE-MSG-05                  TO PV-OPERATION-ATTEMPTED08470011
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08480011
               PERFORM 9999-SQL-ABEND                                   08490011
           END-IF.                                                      08500011
                                                                        08510011
           EXEC SQL                                                     08520011
               COMMIT                                                   08530011
           END-EXEC.                                                    08540011
                                                                        08550011
           IF SQLCODE = 0                                               08560011
               CONTINUE                                                 08570011
           ELSE                                                         08580011
               MOVE PE-MSG-06                  TO PV-OPERATION-ATTEMPTED08590011
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     08600011
               PERFORM 9999-SQL-ABEND                                   08610011
           END-IF.                                                      08620011
      *                                                                 08630011
      ******************************************************************08640011
      * 7900-UPDATE-CHECKPOINT-STATUS                                   08650011
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   08660011
      ******************************************************************08670011
       7900-UPDATE-CHECKPOINT-STATUS.                                   08680011
                                                                        08690011
           EXEC SQL                                                     08700011
             UPDATE  TCKRSTCNTL                                         08710011
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        08720011
              WHERE  JOB_NAME  = :PC-JOB-NAME                           08730011
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       08740011
           END-EXEC.                                                    08750011
                                                                        08760011
           IF SQLCODE = 0                                               08770011
               CONTINUE                                                 08780011
           ELSE                                                         08790011
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME08800011
               PERFORM 9999-SQL-ABEND                                   08810011
           END-IF.                                                      08820011
                                                                        08830011
           EJECT                                                        08840011
      *                                                                 08850011
      *                                                                 08860011
      ******************************************************************08870011
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TWKMCLC ROW FOR UPDATE08880011
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  08890011
      ******************************************************************08900011
       7999-RESTART-PROCESS.                                            08910011
      *                                                                 08920011
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           08930011
      *                                                                 08940011
           CLOSE UPDATE-FILE.                                           08950011
      *                                                                 08960011
           PERFORM 7500-SELECT-RESTART-INFO.                            08970011
      *                                                                 08980011
           DISPLAY '**** RESTART **** '.                                08990011
           MOVE PV-DEADLOCK-COUNT TO PD-DEADLOCK-COUNT.                 09000011
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   09010011
                                                                        09020011
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      09030011
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        09040011
      *-- RESTART INFORMATION IS NOT CLEARED OUT.                       09050011
           IF PS-FIRST-COMMIT                                           09060011
               INITIALIZE TCKRSTINF-WORK-STRG-DESC (1:30)               09070011
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     09080011
                      ' BEGINNING'                                      09090011
           ELSE                                                         09100011
               DISPLAY 'TCKRSTINF-LAST CKPT '                           09110011
                        TCKRSTINF-WORK-STRG-DESC (1:28)                 09120011
      * INFO ON LAST CHECKPOINT TAKEN IS IN CR-CHECKPOINT-RESTART-AREA. 09130011
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         09140011
                    CR-CHECKPOINT-RESTART-AREA                          09150011
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                09160011
               DISPLAY 'WEEK NBR        ' CR-FISCAL-NBR                 09170011
               DISPLAY 'WEEK END DATE   ' CR-FISCAL-WK-END-DTE          09180011
               DISPLAY 'DEPARTMENT NBR  ' CR-DEPT-NBR                   09190011
               DISPLAY 'MAJOR CLASS NBR ' CR-MAJ-CL-NBR                 09200011
               DISPLAY 'CHANNEL CODE    ' CR-CHNL-CDE                   09210011
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           09220011
           END-IF.                                                      09230011
                                                                        09240011
           OPEN INPUT UPDATE-FILE.                                      09250011
           MOVE ZEROES                          TO PA-INPUT-COUNT.      09260011
      *                                                                 09270011
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          09280011
           PERFORM 4000-READ-UPDATE-FILE                                09290011
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               09300011
                 OR PS-EOF-UPDATE-FILE.                                 09310011
           IF PS-EOF-UPDATE-FILE                                        09320011
               DISPLAY 'END OF INPUT FILE ON RESTART'                   09330011
           ELSE                                                         09340011
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              09350011
                        PA-INPUT-COUNT                                  09360011
               DISPLAY 'FISCAL WEEK NBR       ' ML150-FSCL-WK-NBR       09370011
               DISPLAY 'FISCAL WEEK END DATE  ' ML150-FSCL-WK-END-DTE   09380011
               DISPLAY 'DEPARTMENT NBR        ' ML150-DEPT-NBR          09390011
               DISPLAY 'MAJOR CLASS NBR       ' ML150-MAJ-CL-NBR        09400011
               DISPLAY 'CHANNEL CODE          ' ML150-CHNL-CDE          09410011
           END-IF.                                                      09420011
      *                                                                 09430011
      ******************************************************************09440011
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *09450011
      ******************************************************************09460011
       8000-EOJ-ROUTINE.                                                09470011
      *                                                                 09480011
           MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE.                       09490011
           PERFORM 7900-UPDATE-CHECKPOINT-STATUS.                       09500011
                                                                        09510011
      *                                                                 09520011
           DISPLAY SPACES.                                              09530011
           DISPLAY 'INPUT RECORDS PROCESSED  ', PA-INPUT-COUNT.         09540011
           DISPLAY 'ROWS UPDATED             ', PA-UPDATE-COUNT.        09550011
           DISPLAY 'ROWS INSERTED            ', PA-INSERT-COUNT.        09560011
           DISPLAY SPACES.                                              09570011
      *                                                                 09580011
           CLOSE UPDATE-FILE.                                           09590011
      *                                                                 09600011
      *                                                                 09610011
      ******************************************************************09620011
      *  PERFROMED BY 7100-UPDATE AND 7200-INSERT                       09630011
      *  THIS ROUTINE FORCES AN ABEND DUE TO A SQLCODE -911 (3 TIMES)   09640011
      ******************************************************************09650011
       9000-DEADLOCK-ERROR.                                             09660011
      *                                                                 09670011
               MOVE PE-MSG-07             TO PV-OPERATION-ATTEMPTED     09680011
               PERFORM 9999-SQL-ABEND.                                  09690011
      *                                                                 09700011
      ******************************************************************09710011
      *  STANDARD DB2 ERROR ABEND ROUTINE                               09720011
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             09730011
      ******************************************************************09740011
       9999-SQL-ABEND.                                                  09750011
                                                                        09760011
           MOVE +4013                 TO ABEND-CODE.                    09770011
           DISPLAY '**  ABEND     **'.                                  09780011
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              09790011
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            09800011
           DISPLAY '**  OPERATION **  = ' PV-OPERATION-ATTEMPTED.       09810011
                                                                        09820011
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         09830011
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        09840011
                                                                        09850011
           COPY DPPD004.                                                09860011
           CALL 'ILBOABN0'  USING ABEND-CODE.                           09870011
