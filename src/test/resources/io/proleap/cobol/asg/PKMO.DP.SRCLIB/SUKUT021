000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.    SUKUT021.                                                 
000600 AUTHOR.        STEPHANIE HERON.                                          
000700 INSTALLATION.  KOHLS.                                                    
000800 DATE-WRITTEN   DECEMBER, 1997.                                           
000900 DATE-COMPILED.                                                           
001000                                                                          
001100******************************************************************        
001200*  THIS PROGRAM CREATES AN EXTRACT FILE FROM TSUEXCP AND TCTEXCP          
001300*  FOR THE AUDITS THAT WERE SELECTED TO BE DELETED.                       
001400******************************************************************        
001500                                                                          
001600******************************************************************        
001700 ENVIRONMENT DIVISION.                                                    
001800******************************************************************        
001900                                                                          
002000 CONFIGURATION SECTION.                                                   
002100                                                                          
002200 SOURCE-COMPUTER.        IBM-3090.                                        
002300 OBJECT-COMPUTER.        IBM-3090.                                        
002400                                                                          
002500 INPUT-OUTPUT SECTION.                                                    
002600                                                                          
002700 FILE-CONTROL.                                                            
002800                                                                          
003420     SELECT SUATRN-AUDIT-FILE                                             
003430         ASSIGN TO IN01.                                                  
003431                                                                          
003440     SELECT SUEXCP-AUDIT-FILE                                             
003450         ASSIGN TO OUT01.                                                 
003460                                                                          
003470     SELECT CTEXCP-AUDIT-FILE                                             
003480         ASSIGN TO OUT02.                                                 
003490                                                                          
003500******************************************************************        
003600 DATA DIVISION.                                                           
003700******************************************************************        
003800                                                                          
003900 FILE SECTION.                                                            
004000                                                                          
005410 FD  SUATRN-AUDIT-FILE                                                    
005420     RECORDING MODE IS F                                                  
005430     LABEL RECORDS ARE STANDARD                                           
005440     BLOCK CONTAINS 0 RECORDS.                                            
005450                                                                          
005460 01  SUATRN-AUDIT-RECORD        PIC  X(103).                              
005470*                                                                         
005480 FD  SUEXCP-AUDIT-FILE                                                    
005490     RECORDING MODE IS F                                                  
005491     LABEL RECORDS ARE STANDARD                                           
005492     BLOCK CONTAINS 0 RECORDS.                                            
005493                                                                          
005494 01  SUEXCP-AUDIT-RECORD        PIC  X(073).                              
005495*                                                                         
005496 FD  CTEXCP-AUDIT-FILE                                                    
005497     RECORDING MODE IS F                                                  
005498     LABEL RECORDS ARE STANDARD                                           
005499     BLOCK CONTAINS 0 RECORDS.                                            
005500                                                                          
005501 01  CTEXCP-AUDIT-RECORD        PIC  X(039).                              
005502*                                                                         
005510*                                                                         
005600******************************************************************        
005700 WORKING-STORAGE SECTION.                                                 
007400******************************************************************        
007500*  DB2 FORMATTED MESSAGE AREA                                             
007600******************************************************************        
007700     COPY DPWS004.                                                        
007800*                                                                         
008100******************************************************************        
008200*  COMMUNICATIONS AREA FOR DB2                                            
008300******************************************************************        
008400     EXEC SQL                                                             
008500          INCLUDE SQLCA                                                   
008600     END-EXEC.                                                            
008800*                                                                         
011180******************************************************************        
011190*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011191*  CARTON AUDIT TABLE                                                     
011192******************************************************************        
011193     EXEC SQL                                                             
011194          INCLUDE TSUATRN                                                 
011195     END-EXEC.                                                            
011196                                                                          
011197******************************************************************        
011198*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011199*  EXCEPTION TABLE                                                        
011200******************************************************************        
011201     EXEC SQL                                                             
011202          INCLUDE TSUEXCP                                                 
011203     END-EXEC.                                                            
011204                                                                          
011205******************************************************************        
011206*  COBOL DECLARATION AND DCLGEN MEMBER FOR THE SHIPMENT UNIT              
011207*  CARTON EXCEPTION TABLE                                                 
011208******************************************************************        
011209     EXEC SQL                                                             
011210          INCLUDE TCTEXCP                                                 
011211     END-EXEC.                                                            
011212                                                                          
011220 01  ABEND-CODE                      PIC S9(04)    VALUE +0               
011300                                                   COMP SYNC.             
011400                                                                          
011500 01  PS-PROGRAM-SWITCHES.                                                 
011600     05  PS-EOF-AUDIT-FILE-SW        PIC  X(01)    VALUE 'N'.             
011700         88  PS-EOF-AUDIT-FILE                     VALUE 'Y'.             
011800         88  PS-MORE-AUDIT-FILE                    VALUE 'N'.             
011804     05  PS-EOF-SUEXCP-CSR-SW        PIC  X(01)    VALUE 'N'.             
011805         88  PS-EOF-SUEXCP-CSR                     VALUE 'Y'.             
011806         88  PS-MORE-SUEXCP-CSR                    VALUE 'N'.             
011807     05  PS-EOF-CTEXCP-CSR-SW        PIC  X(01)    VALUE 'N'.             
011808         88  PS-EOF-CTEXCP-CSR                     VALUE 'Y'.             
011809         88  PS-MORE-CTEXCP-CSR                    VALUE 'N'.             
012000                                                                          
012100 01  PC-PROGRAM-CONSTANTS.                                                
012200     05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100             
012300                                                   COMP SYNC.             
012400     05  PC-CURRENT-PROGRAM-NAME     PIC  X(08)    VALUE                  
012500         'SUKUT021'.                                                      
012600     05  PC-CURRENT-OPERATING-COMPANY                                     
012700                                     PIC  X(02)    VALUE '03'.            
012800     05  PC-MAX-RETRIES              PIC S9(04)    VALUE +3               
012900                                                   COMP SYNC.             
013000     05  PC-CURRENT-TIMESTAMP        PIC  X(26)    VALUE SPACES.          
013010     05  PC-COMPLETE-TIMESTAMP       PIC  X(26)    VALUE SPACES.          
013020     05  PC-INPROCESS-TIMESTAMP      PIC  X(26)    VALUE SPACES.          
013100                                                                          
013200 01  PV-PROGRAM-VARIABLES.                                                
013300     05  PV-ITEM-NBR                 PIC  9(15)    VALUE ZERO.            
013400     05  PV-PARAGRAPH-NAME           PIC  X(30)    VALUE SPACES.          
013500     05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
013600     05  PV-OPEN-COUNTER             PIC S9(04)    VALUE ZERO             
013700                                                   COMP SYNC.             
014200     05  PV-WRITE-COUNTER            PIC S9(09)    VALUE ZERO             
014300                                                   COMP SYNC.             
014301     05  PV-WRITE2-COUNTER           PIC S9(09)    VALUE ZERO             
014302                                                   COMP SYNC.             
014310     05  PV-READ-COUNTER             PIC S9(09)    VALUE ZERO             
014320                                                   COMP SYNC.             
014400     05  PV-DISPLAY-COUNT            PIC ZZZ,ZZZ,ZZ9.                     
014500                                                                          
014600     05  PV-DB2-COUNT                PIC S9(09)    VALUE ZERO             
014700                                                   COMP-3.                
014800                                                                          
014900                                                                          
015000 01  SD-SYSTEM-DATE.                                                      
015100     05  SD-SYSTEM-DATE-YY           PIC  9(02)      VALUE ZERO.          
015200     05  SD-SYSTEM-DATE-MM           PIC  9(02)      VALUE ZERO.          
015300     05  SD-SYSTEM-DATE-DD           PIC  9(02)      VALUE ZERO.          
015400                                                                          
015500*                                                                         
015600*  CURSOR DECLARATION FOR ALL THE AUDITS THAT HAVE BEEN COMPLETED         
015700*  FOR THREE WEEKS OR MORE.                                               
015800*                                                                         
015900                                                                          
018399                                                                          
018400     EXEC SQL                                                             
018401          DECLARE SUEXCP_CURSOR CURSOR FOR                                
018402           SELECT  AUD_CNTL_NBR                                           
018404                  ,SHIPT_UNT_ID                                           
018405                  ,UPC_NBR                                                
018406                  ,SCN_TMST                                               
018407                  ,VILN_GP_CDE                                            
018408                  ,VILN_GP_SEQ_NBR                                        
018409                  ,OVRRID_SYS_DAT_TXT                                     
018417             FROM TSUEXCP                                                 
018418            WHERE AUD_CNTL_NBR  = :SUATRN-AUD-CNTL-NBR                    
018419              AND SHIPT_UNT_ID  = :SUATRN-SHIPT-UNT-ID                    
018420     END-EXEC.                                                            
018421                                                                          
018422     EXEC SQL                                                             
018423          DECLARE CTEXCP_CURSOR CURSOR FOR                                
018424           SELECT  AUD_CNTL_NBR                                           
018426                  ,SHIPT_UNT_ID                                           
018427                  ,VILN_GP_CDE                                            
018428                  ,VILN_GP_SEQ_NBR                                        
018429                  ,OVRRID_SYS_DAT_TXT                                     
018431             FROM TCTEXCP                                                 
018432            WHERE AUD_CNTL_NBR  = :SUATRN-AUD-CNTL-NBR                    
018433              AND SHIPT_UNT_ID  = :SUATRN-SHIPT-UNT-ID                    
018434     END-EXEC.                                                            
018435                                                                          
018440                                                                          
018500******************************************************************        
018600 PROCEDURE DIVISION.                                                      
018700******************************************************************        
018800                                                                          
018900 0000-PROGRAM-MAINLINE.                                                   
019000******************************************************************        
019100* 0000-PROGRAM-MAINLINE - CONTROLS THE MAIN PROCESSING OF THE             
019200*      PROGRAM.                                                           
019300******************************************************************        
019400                                                                          
019500     PERFORM 1000-INITIALIZE.                                             
019510                                                                          
019520     IF PS-MORE-AUDIT-FILE                                                
019700         PERFORM 2000-PROCESS-AUDITS                                      
019800            UNTIL PS-EOF-AUDIT-FILE                                       
019900     END-IF.                                                              
019901                                                                          
020000     PERFORM 9000-EOJ-ROUTINE.                                            
020100                                                                          
020200     STOP RUN.                                                            
020300                                                                          
020400 1000-INITIALIZE.                                                         
020500******************************************************************        
020600*  DO FIRST TIME PROGRAM INITIALIZATION TASKS.                            
020700*    -- OPEN OUTPUT FILE                                                  
020800*    -- OPEN CURSOR                                                       
020900*    -- FETCH CURSOR                                                      
021000******************************************************************        
021100                                                                          
021200     ACCEPT SD-SYSTEM-DATE FROM DATE.                                     
021300                                                                          
021310     OPEN INPUT  SUATRN-AUDIT-FILE.                                       
021400     OPEN OUTPUT SUEXCP-AUDIT-FILE                                        
021401                 CTEXCP-AUDIT-FILE.                                       
021600                                                                          
021700     PERFORM 7000-READ-AUDIT-FILE.                                        
021800                                                                          
021801     IF PS-EOF-AUDIT-FILE                                                 
021802        CONTINUE                                                          
021803     ELSE                                                                 
021810        PERFORM 7100-OPEN-SUEXCP-CURSOR                                   
021900        PERFORM 7200-FETCH-SUEXCP-CURSOR                                  
021901        PERFORM 7400-OPEN-CTEXCP-CURSOR                                   
021902        PERFORM 7500-FETCH-CTEXCP-CURSOR                                  
021910     END-IF.                                                              
022000                                                                          
025410 2000-PROCESS-AUDITS.                                                     
025500******************************************************************        
025600*  PROCESS THE AUDITS THAT FALL INTO THE REQUIRED TIME FRAME TO           
025700*  BE DELETED.  A CURSOR WILL BE OPENED FOR EACH AUDIT TO BRING           
025800*  BACK ALL THE CARTONS THAT ARE ASSOCIATED TO IT, AND A FILE             
025810*  CONTAINING THESE CARTONS WILL BE WRITTEN.                              
025900******************************************************************        
026000                                                                          
026102                                                                          
026103     PERFORM UNTIL PS-EOF-SUEXCP-CSR                                      
026104        PERFORM 8000-WRITE-SUEXCP-FILE                                    
026120        PERFORM 7200-FETCH-SUEXCP-CURSOR                                  
026130     END-PERFORM.                                                         
026141     SET PS-MORE-SUEXCP-CSR TO TRUE.                                      
026150     PERFORM 7300-CLOSE-SUEXCP-CURSOR.                                    
026160                                                                          
026161     PERFORM UNTIL PS-EOF-CTEXCP-CSR                                      
026162        PERFORM 8100-WRITE-CTEXCP-FILE                                    
026163        PERFORM 7500-FETCH-CTEXCP-CURSOR                                  
026164     END-PERFORM.                                                         
026166     SET PS-MORE-CTEXCP-CSR TO TRUE.                                      
026167     PERFORM 7600-CLOSE-CTEXCP-CURSOR.                                    
026168                                                                          
026170     PERFORM 7000-READ-AUDIT-FILE.                                        
026300                                                                          
026310     IF PS-EOF-AUDIT-FILE                                                 
026320        CONTINUE                                                          
026330     ELSE                                                                 
026400        PERFORM 7100-OPEN-SUEXCP-CURSOR                                   
026401        PERFORM 7400-OPEN-CTEXCP-CURSOR                                   
026402     END-IF.                                                              
026403                                                                          
026410 7000-READ-AUDIT-FILE.                                                    
026500******************************************************************        
026600*  READ THE INPUT FILE                                                    
026700******************************************************************        
026710                                                                          
026720      READ SUATRN-AUDIT-FILE INTO                                         
026730           DCLTSUATRN AT END SET                                          
026740           PS-EOF-AUDIT-FILE TO TRUE.                                     
026750                                                                          
026760      IF PS-EOF-AUDIT-FILE                                                
026770         CONTINUE                                                         
026780      ELSE                                                                
026790         ADD +1 TO PV-READ-COUNTER                                        
026791      END-IF.                                                             
026792                                                                          
026800 7100-OPEN-SUEXCP-CURSOR.                                                 
026900******************************************************************        
027000*  OPEN THE CURSOR.                                                       
027100******************************************************************        
027400     EXEC SQL                                                             
027500          OPEN SUEXCP_CURSOR                                              
027600     END-EXEC.                                                            
027700                                                                          
027800     EVALUATE TRUE                                                        
027810         WHEN SQLCODE = ZERO                                              
027820             CONTINUE                                                     
028200         WHEN SQLWARN0 NOT = SPACES                                       
028300         WHEN SQLCODE < ZERO                                              
028400         WHEN SQLCODE > ZERO                                              
028500             MOVE '7100-OPEN-SUEXCP-CURSOR       '                        
028600                             TO PV-PARAGRAPH-NAME                         
028700             MOVE 'OPEN THE UPC_CURSOR      '                             
028800                             TO PV-DB2-OPERATION-ATTEMPTED                
028810             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
028820                     SUATRN-AUD-CNTL-NBR                                  
028830             DISPLAY 'LAST CART ID PROCESSED WAS '                        
028840                     SUATRN-SHIPT-UNT-ID                                  
028900             PERFORM 9100-SQL-ABEND                                       
029200     END-EVALUATE.                                                        
030200                                                                          
030300 7200-FETCH-SUEXCP-CURSOR.                                                
030400******************************************************************        
030500*  FETCH THE RESULTS FROM THE COMPLETE CURSOR.                            
030600******************************************************************        
030700     EXEC SQL                                                             
030800          FETCH SUEXCP_CURSOR                                             
030801               INTO :SUEXCP-AUD-CNTL-NBR                                  
030802                   ,:SUEXCP-SHIPT-UNT-ID                                  
030803                   ,:SUEXCP-UPC-NBR                                       
030804                   ,:SUEXCP-SCN-TMST                                      
030805                   ,:SUEXCP-VILN-GP-CDE                                   
030810                   ,:SUEXCP-VILN-GP-SEQ-NBR                               
030900                   ,:SUEXCP-OVRRID-SYS-DAT-TXT                            
032300     END-EXEC.                                                            
032400                                                                          
032500     EVALUATE TRUE                                                        
032600         WHEN SQLCODE = ZERO                                              
032700             CONTINUE                                                     
032800         WHEN SQLCODE = +100                                              
032900             SET PS-EOF-SUEXCP-CSR                                        
033000                                 TO TRUE                                  
033100         WHEN SQLWARN0 NOT = SPACES                                       
033200         WHEN SQLCODE < ZERO                                              
033300         WHEN SQLCODE > ZERO                                              
033400             MOVE '7200-FETCH-SUEXCP-CURSOR  '                            
033500                                 TO PV-PARAGRAPH-NAME                     
033600             MOVE 'FETCH THE COMPLETE-CURSOR   '                          
033700                                 TO PV-DB2-OPERATION-ATTEMPTED            
033710             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
033720                     SUATRN-AUD-CNTL-NBR                                  
033730             DISPLAY 'LAST CART ID PROCESSED WAS '                        
033740                     SUATRN-SHIPT-UNT-ID                                  
033800             PERFORM 9100-SQL-ABEND                                       
033900     END-EVALUATE.                                                        
034000                                                                          
034204 7300-CLOSE-SUEXCP-CURSOR.                                                
034205******************************************************************        
034206*  CLOSE THE UPC CURSOR.                                                  
034207******************************************************************        
034208     EXEC SQL                                                             
034209          CLOSE SUEXCP_CURSOR                                             
034211     END-EXEC.                                                            
034212                                                                          
034213     EVALUATE TRUE                                                        
034214         WHEN SQLCODE = ZERO                                              
034215             CONTINUE                                                     
034219         WHEN SQLWARN0 NOT = SPACES                                       
034220         WHEN SQLCODE < ZERO                                              
034221         WHEN SQLCODE > ZERO                                              
034222             MOVE '7300-CLOSE-SUEXCP-CURSOR '                             
034223                                 TO PV-PARAGRAPH-NAME                     
034224             MOVE 'CLOSE THE UPC CURSOR        '                          
034225                                 TO PV-DB2-OPERATION-ATTEMPTED            
034226             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
034227                     SUATRN-AUD-CNTL-NBR                                  
034228             DISPLAY 'LAST CART ID PROCESSED WAS '                        
034229                     SUATRN-SHIPT-UNT-ID                                  
034230             PERFORM 9100-SQL-ABEND                                       
034231     END-EVALUATE.                                                        
034232                                                                          
034233 7400-OPEN-CTEXCP-CURSOR.                                                 
034234******************************************************************        
034235*  OPEN THE CURSOR.                                                       
034236******************************************************************        
034237     EXEC SQL                                                             
034238          OPEN CTEXCP_CURSOR                                              
034239     END-EXEC.                                                            
034240                                                                          
034241     EVALUATE TRUE                                                        
034242         WHEN SQLCODE = ZERO                                              
034243             CONTINUE                                                     
034244         WHEN SQLWARN0 NOT = SPACES                                       
034245         WHEN SQLCODE < ZERO                                              
034246         WHEN SQLCODE > ZERO                                              
034247             MOVE '7400-OPEN-CTEXCP-CURSOR       '                        
034248                             TO PV-PARAGRAPH-NAME                         
034249             MOVE 'OPEN THE UPC_CURSOR      '                             
034250                             TO PV-DB2-OPERATION-ATTEMPTED                
034251             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
034252                     SUATRN-AUD-CNTL-NBR                                  
034253             DISPLAY 'LAST CART ID PROCESSED WAS '                        
034254                     SUATRN-SHIPT-UNT-ID                                  
034255             PERFORM 9100-SQL-ABEND                                       
034256     END-EVALUATE.                                                        
034257                                                                          
034258 7500-FETCH-CTEXCP-CURSOR.                                                
034259******************************************************************        
034260*  FETCH THE RESULTS FROM THE COMPLETE CURSOR.                            
034261******************************************************************        
034262     EXEC SQL                                                             
034263          FETCH CTEXCP_CURSOR                                             
034264              INTO  :CTEXCP-AUD-CNTL-NBR                                  
034265                   ,:CTEXCP-SHIPT-UNT-ID                                  
034266                   ,:CTEXCP-VILN-GP-CDE                                   
034267                   ,:CTEXCP-VILN-GP-SEQ-NBR                               
034268                   ,:CTEXCP-OVRRID-SYS-DAT-TXT                            
034271     END-EXEC.                                                            
034272                                                                          
034273     EVALUATE TRUE                                                        
034274         WHEN SQLCODE = ZERO                                              
034275             CONTINUE                                                     
034276         WHEN SQLCODE = +100                                              
034277             SET PS-EOF-CTEXCP-CSR                                        
034278                                 TO TRUE                                  
034279         WHEN SQLWARN0 NOT = SPACES                                       
034280         WHEN SQLCODE < ZERO                                              
034281         WHEN SQLCODE > ZERO                                              
034282             MOVE '7500-FETCH-CTEXCP-CURSOR  '                            
034283                                 TO PV-PARAGRAPH-NAME                     
034284             MOVE 'FETCH THE COMPLETE-CURSOR   '                          
034285                                 TO PV-DB2-OPERATION-ATTEMPTED            
034286             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
034287                     SUATRN-AUD-CNTL-NBR                                  
034288             DISPLAY 'LAST CART ID PROCESSED WAS '                        
034289                     SUATRN-SHIPT-UNT-ID                                  
034290             PERFORM 9100-SQL-ABEND                                       
034291     END-EVALUATE.                                                        
034292                                                                          
034293 7600-CLOSE-CTEXCP-CURSOR.                                                
034294******************************************************************        
034295*  CLOSE THE UPC CURSOR.                                                  
034296******************************************************************        
034297     EXEC SQL                                                             
034298          CLOSE CTEXCP_CURSOR                                             
034299     END-EXEC.                                                            
034300                                                                          
034301     EVALUATE TRUE                                                        
034302         WHEN SQLCODE = ZERO                                              
034303             CONTINUE                                                     
034304         WHEN SQLWARN0 NOT = SPACES                                       
034305         WHEN SQLCODE < ZERO                                              
034306         WHEN SQLCODE > ZERO                                              
034307             MOVE '7600-CLOSE-CTEXCP-CURSOR '                             
034308                                 TO PV-PARAGRAPH-NAME                     
034309             MOVE 'CLOSE THE UPC CURSOR        '                          
034310                                 TO PV-DB2-OPERATION-ATTEMPTED            
034311             DISPLAY 'AUDIT CONTROL NUMBER IS '                           
034312                     SUATRN-AUD-CNTL-NBR                                  
034313             DISPLAY 'LAST CART ID PROCESSED WAS '                        
034314                     SUATRN-SHIPT-UNT-ID                                  
034315             PERFORM 9100-SQL-ABEND                                       
034316     END-EVALUATE.                                                        
034317                                                                          
034318 8000-WRITE-SUEXCP-FILE.                                                  
034319******************************************************************        
034320*  WRITE THE SUEXCP-AUDIT-RECORD                                          
034400******************************************************************        
034500     INITIALIZE SUEXCP-AUDIT-RECORD.                                      
034600     WRITE SUEXCP-AUDIT-RECORD                                            
034700         FROM DCLTSUEXCP.                                                 
034800     ADD +1 TO PV-WRITE-COUNTER.                                          
034900                                                                          
035000 8100-WRITE-CTEXCP-FILE.                                                  
035100******************************************************************        
035200*  WRITE THE SUEXCP-AUDIT-RECORD                                          
035300******************************************************************        
035400     INITIALIZE CTEXCP-AUDIT-RECORD.                                      
035500     WRITE CTEXCP-AUDIT-RECORD                                            
035600         FROM DCLTCTEXCP.                                                 
035700     ADD +1 TO PV-WRITE2-COUNTER.                                         
035710                                                                          
035800 9000-EOJ-ROUTINE.                                                        
035900******************************************************************        
036000*  CLOSE OUTPUT FILE.  CLOSE THE CURSOR.                                  
036100******************************************************************        
036200     CLOSE SUEXCP-AUDIT-FILE                                              
036300           SUATRN-AUDIT-FILE.                                             
036400                                                                          
036500     MOVE PV-READ-COUNTER TO PV-DISPLAY-COUNT.                            
036600     DISPLAY 'TOTAL INPUT RECORDS READ '                                  
036601     PV-DISPLAY-COUNT.                                                    
036610     MOVE PV-WRITE-COUNTER TO PV-DISPLAY-COUNT.                           
036620     DISPLAY 'TOTAL SUEXCP RECORDS WRITEEN ' PV-DISPLAY-COUNT.            
036622     MOVE PV-WRITE2-COUNTER TO PV-DISPLAY-COUNT.                          
036623     DISPLAY 'TOTAL CTEXCP RECORDS WRITTEN '  PV-DISPLAY-COUNT.           
036700                                                                          
040292                                                                          
040300 9100-SQL-ABEND.                                                          
040400******************************************************************        
040500*  PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT THAT AN SQL            
040600*  ERROR OR WARNING CODE OCCURS.                                          
040700******************************************************************        
040800     DISPLAY '9100-SQL-ABEND'.                                            
040900     DISPLAY '** ABEND     **'.                                           
041000     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
041100     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
041200     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
041300                                                                          
041400******************************************************************        
041500* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
041600* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
041700* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
041800* FORMAT.                                                                 
041900******************************************************************        
042000     COPY DPPD004.                                                        
042100                                                                          
042200     MOVE +4013                  TO ABEND-CODE.                           
042300     CALL 'ILBOABN0' USING ABEND-CODE.                                    
