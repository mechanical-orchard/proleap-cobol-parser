000100*----------------------------------------------------------------         
000200 IDENTIFICATION DIVISION.                                                 
000300*----------------------------------------------------------------         
000400 PROGRAM-ID.    EPKUT205.                                                 
000500 AUTHOR.        LARRY WEISS.                                              
000600 DATE-WRITTEN.  DECEMBER 17, 2007.                                        
000700 DATE-COMPILED.                                                           
000800                                                                          
000900*----------------------------------------------------------------         
001000*     **   CREATE OFFERS CORRECTIONS EXTRACT FILES   **                   
001100*               **   NEXT GENERATION STORE   **                           
001200*                                                                         
001300* THIS PROGRAM CONSISTS OF TWO MAIN SECTIONS.                             
001400*                                                                         
001500*     A. EXTRACT ALL OFFERS WHEREIN AN UPDATE TIMESTAMP IN ANY            
001600*        OF THE FOLLOWING ORACLE TABLES IS BETWEEN THE LIMITS             
001700*        SPECIFIED IN THE EP251 CONTROL CARD.                             
001800*                                                                         
001900*            OMT_OFR ORACLE TABLE,                                        
002000*            OMT_OFR_SECT ORACLE TABLE,                                   
002100*            OMT_OFR_SECT_BWP TABLE,                                      
002200*            OMT_MDSE_SET                                                 
002300*                                                                         
002400*        THESE OFFERS AFTER APPROPRIATE PROCESSING WILL BE                
002500*        WRITTEN TO THE:                                                  
002600*                                                                         
002700*            OFFERS-CORR-FILE                                             
002800*            CORR-STORE-LIST-FILE                                         
002900*                                                                         
003000*     B. EXTRACT ALL OFFERS WHEREIN THE UPDATE TIMESTAMP IN               
003100*        THE FOLLOWING ORACLE TABLE IS BETWEEN THE LIMITS                 
003200*        SPECIFIED IN THE EP251 CONTROL CARD                              
003300*                                                                         
003400*            OMT_OSR_LOC_GRP                                              
003500*                                                                         
003600*        THESE OFFERS AFTER APPROPRIATE PROCESSING WILL BE                
003700*        WRITTEN TO THE                                                   
003800*                                                                         
003900*            OFFERS-LOC-GRP-CORR-FILE                                     
004000*            CORR-STR-GRP-LIST-FILE                                       
004100*                                                                         
004200*----------------------------------------------------------------         
004300*                                                                         
004400*--------------------------------------------------------*                
004500*  ORACLE TABLES:                        DCLGEN:         *                
004600*  --------------                        -------         *                
004700*   K_OM01.OMT_OFR                       OMT00001        *                
004800*   K_OM01.OMT_OFR_SECT                  OMT00002        *                
004900*   K_OM01.OMT_OFR_SECT_BWP              OMT00003        *                
005000*   K_OM01.OMT_OFR_LOC_GP                OMT00004        *                
005100*   K_OM01.OMT_MDSE_SET                  OMT00005        *                
005200*   K_OM01.OMT_POS_BTN                   OMT00006        *                
005300*   K_OM01.OMT_EVNT_CTG                  OMT00007        *                
005400*   K_OM01.OMT_OFR_CUSTFACG_DESC         OMT00008        *                
005500*   K_OM09.OMT_EVNT                      OMT00009        *                
005600*                                                        *                
005700*--------------------------------------------------------*                
005800* HISTORY LOG:                                                            
005900*---------------------------------------------------------------*         
006000* DATE:        17 DECEMBER 2007                                           
006100* WR/IR#:      WR#                                                        
006200* PROGRAMMER:  LARRY WEISS                                                
006300* DESCRIPTION: INITIAL PROGRAM CREATION                                   
006400*----------------------------------------------------------------*        
006500* DATE:        08/22/2008                                                 
006600* WR/IR#:      WR34938                                                    
006700* PROGRAMMER:  THOMAS HANSON                                              
006800* DESCRIPTION: ADDED BWP_DLT_IND FIELD TO CURSORS, MUST BE 'N' TO         
006900*              BE PULLED FOR THE EXTRACT. CHANGES MARKED '080822'.        
007000*              ONE OF THE CSRS CHECKS FOR 'Y' AND DELETES THE OFR.        
007100*----------------------------------------------------------------*        
007200* 02/22/12 - COGNIZANT       -    CRQ000000021295                         
007300*    TRIGGER TYPE CODE REINITIALIZED TO AVOID PREVIOUS RECORD             
007400*    OVERRIDES                                                            
007500*                                                                         
007600*---------------------------------------------------------------*         
007700 ENVIRONMENT DIVISION.                                                    
007800*---------------------------------------------------------------*         
007900 CONFIGURATION SECTION.                                                   
008000 SOURCE-COMPUTER. IBM-3090.                                               
008100 OBJECT-COMPUTER. IBM-3090.                                               
008200                                                                          
008300 INPUT-OUTPUT SECTION.                                                    
008400                                                                          
008500 FILE-CONTROL.                                                            
008600                                                                          
008700     SELECT CORR-CARD-FILE                    ASSIGN TO INP01.            
008800                                                                          
008900     SELECT CONTROL-CARD-NXT-FUT-DAY          ASSIGN TO INP02.            
009000                                                                          
009100     SELECT ORACLE-LOGIN-FILE                 ASSIGN TO ORALOGON.         
009200                                                                          
009300     SELECT OFFERS-CORR-FILE                  ASSIGN TO OUT01.            
009400                                                                          
009500     SELECT CORR-STORE-LIST-FILE              ASSIGN TO OUT02.            
009600                                                                          
009700     SELECT OFFERS-LOC-GRP-CORR-FILE          ASSIGN TO OUT03.            
009800                                                                          
009900     SELECT CORR-STR-GRP-LIST-FILE            ASSIGN TO OUT04.            
010000                                                                          
010100     SELECT TEMP-CORR-CARD-FILE               ASSIGN TO OUT05.            
010200                                                                          
010300*---------------------------------------------------------------*         
010400 DATA DIVISION.                                                           
010500*---------------------------------------------------------------*         
010600 FILE SECTION.                                                            
010700 FD  CORR-CARD-FILE                                                       
010800     RECORDING MODE IS F                                                  
010900     BLOCK CONTAINS 0 RECORDS                                             
011000     LABEL RECORDS ARE STANDARD.                                          
011100                                                                          
011200 01  CORRECTION-CARD-RECORD          PIC X(080).                          
011300                                                                          
011400 FD  CONTROL-CARD-NXT-FUT-DAY                                             
011500     RECORDING MODE IS F                                                  
011600     BLOCK CONTAINS 0 RECORDS                                             
011700     LABEL RECORDS ARE STANDARD.                                          
011800                                                                          
011900 01  CONTROL-NXT-FUT-DAY-RECORD      PIC X(080).                          
012000                                                                          
012100 FD  ORACLE-LOGIN-FILE                                                    
012200     RECORDING MODE IS F                                                  
012300     BLOCK CONTAINS 0 RECORDS                                             
012400     LABEL RECORDS ARE STANDARD                                           
012500     DATA RECORD IS ORACLE-LOGIN-RECORD.                                  
012600                                                                          
012700 01  ORACLE-LOGIN-RECORD       PIC X(80).                                 
012800                                                                          
012900 FD  OFFERS-CORR-FILE                                                     
013000     RECORDING MODE IS F                                                  
013100     LABEL RECORDS ARE STANDARD                                           
013200     RECORD CONTAINS 176 CHARACTERS                                       
013300     DATA RECORD IS OFFERS-CORRECTION-RECORD.                             
013400                                                                          
013500 01  OFFERS-CORRECTION-RECORD        PIC X(176).                          
013600                                                                          
013700 FD  OFFERS-LOC-GRP-CORR-FILE                                             
013800     RECORDING MODE IS F                                                  
013900     LABEL RECORDS ARE STANDARD                                           
014000     RECORD CONTAINS 176 CHARACTERS                                       
014100     DATA RECORD IS OFFERS-CORRECTION-RECORD.                             
014200                                                                          
014300 01  OFFERS-LOC-GRP-CORR-REC         PIC X(176).                          
014400                                                                          
014500 FD  CORR-STORE-LIST-FILE                                                 
014600     RECORDING MODE IS F                                                  
014700     LABEL RECORDS ARE STANDARD                                           
014800     BLOCK CONTAINS 0 RECORDS.                                            
014900                                                                          
015000 01  CORRECTION-STORE-LIST-RECORD    PIC X(050).                          
015100                                                                          
015200 FD  CORR-STR-GRP-LIST-FILE                                               
015300     RECORDING MODE IS F                                                  
015400     LABEL RECORDS ARE STANDARD                                           
015500     BLOCK CONTAINS 0 RECORDS.                                            
015600                                                                          
015700 01  CORR-STR-GRP-LIST-REC    PIC X(050).                                 
015800                                                                          
015900 FD  TEMP-CORR-CARD-FILE                                                  
016000     RECORDING MODE IS F                                                  
016100     BLOCK CONTAINS 0 RECORDS                                             
016200     LABEL RECORDS ARE STANDARD.                                          
016300                                                                          
016400 01  TEMP-CORRECTION-CARD-RECORD          PIC X(080).                     
016500                                                                          
016600*---------------------------------------------------------------*         
016700                                                                          
016800 WORKING-STORAGE SECTION.                                                 
016900                                                                          
017000*---------------------------------------------------------------*         
017100                                                                          
017200*---------------------------------------------------------------*         
017300* PC- PROGRAM CONSTANTS                                         *         
017400*---------------------------------------------------------------*         
017500 01  PC-PROGRAM-CONSTANTS.                                                
017600     05  FILLER                      PIC X(27)  VALUE                     
017700         'WORKING STORAGE STARTS HERE'.                                   
017800     05  PC-PROGRAM-NAME             PIC X(08) VALUE 'EPKUT205'.          
017900     05  PC-MAX-RETRIES              PIC 9(01) VALUE 5.                   
018000     05  PC-FIELD-LENGTH             PIC S9(03) COMP-3 VALUE +10.         
018100     05  PC-NULL-VALUE               PIC X(01) VALUE '~'.                 
018200     05  PC-PROMO-SCHM-CDE-1008      PIC X(04) VALUE '1008'.              
018300     05  PC-PROMO-SCHM-CDE-1009      PIC X(04) VALUE '1009'.              
018400     05  PC-PROMO-SCHM-CDE-1010      PIC X(04) VALUE '1010'.              
018500     05  PC-DOT-VALUE                PIC X(01) VALUE '.'.                 
018600     05  PC-DECR-DAYS-365            PIC 9(03) VALUE 365.                 
018700     05  PC-ADD-TRIGGER              PIC X(01) VALUE 'A'.                 
018800     05  PC-DEL-TRIGGER              PIC X(01) VALUE 'D'.                 
018900     05  PC-BTN-TBL-THRSHOLD         PIC S9(04) VALUE +10 COMP.           
019000     05  PC-OMT-STR-TBL-MAX          PIC S9(04) VALUE +2000 COMP.         
019100     05  PC-OMT-OFR-TBL-MAX          PIC S9(04) VALUE +2000 COMP.         
019110     05  PC-AUTO-LOGON               PIC X(01)       VALUE '/'.           
019200                                                                          
019300*---------------------------------------------------------------*         
019400* PS- PROGRAM SWITCHES                                          *         
019500*---------------------------------------------------------------*         
019600 01  PS-PROGRAM-SWITCHES.                                                 
019700                                                                          
019800     05  PS-CORR-FILE-EOF-STATUS     PIC X(01) VALUE 'N'.                 
019900         88  PS-EOF-CC-CORR-FILE               VALUE 'Y'.                 
020000         88  PS-NOT-EOF-CC-CORR-FILE           VALUE 'N'.                 
020100                                                                          
020200     05  PS-NXT-FUT-CC-EOF-STATUS    PIC X(01) VALUE 'N'.                 
020300         88  PS-EOF-CC-NXT-FUT-DAY             VALUE 'Y'.                 
020400         88  PS-NOT-EOF-CC-NXT-FUT-DAY         VALUE 'N'.                 
020500                                                                          
020600     05  PS-TIME-ROUTINE-SW          PIC X(01) VALUE 'N'.                 
020700         88  PS-TIME-VALID                     VALUE 'Y'.                 
020800         88  PS-TIME-NOT-VALID                 VALUE 'N'.                 
020900                                                                          
021000     05  PS-END-OF-FETCH-OFR-SW      PIC X(01) VALUE 'N'.                 
021100         88  PS-END-OF-FETCH-OFR               VALUE 'Y'.                 
021200         88  PS-NOT-END-OF-FETCH-OFR           VALUE 'N'.                 
021300                                                                          
021400     05  PS-END-OF-FETCH-BTN-SW      PIC X(01) VALUE 'N'.                 
021500         88  PS-END-OF-FETCH-BTN               VALUE 'Y'.                 
021600         88  PS-NOT-END-OF-FETCH-BTN           VALUE 'N'.                 
021700                                                                          
021800     05  PS-END-OF-FETCH-MDSE-SW     PIC X(01) VALUE 'N'.                 
021900         88  PS-END-OF-FETCH-MDSE              VALUE 'Y'.                 
022000         88  PS-NOT-END-OF-FETCH-MDSE          VALUE 'N'.                 
022100                                                                          
022200     05  PS-CURSOR-ONE-EOF-SW        PIC X(01) VALUE 'N'.                 
022300         88  PS-END-OF-STORE-CURSOR-ONE        VALUE 'Y'.                 
022400         88  PS-CURSOR-ONE-NOT-EOF             VALUE 'N'.                 
022500                                                                          
022600     05  PS-TIMESTAMP-SW.                                                 
022700         10  PS-TIMESTAMP-OFR-SW     PIC X(01) VALUE 'N'.                 
022800             88  PS-OFR-IN-RANGE               VALUE 'Y'.                 
022900             88  PS-OFR-NOT-IN-RANGE           VALUE 'N'.                 
023000         10  PS-TIMESTAMP-SECT-SW    PIC X(01) VALUE 'N'.                 
023100             88  PS-SECT-IN-RANGE              VALUE 'Y'.                 
023200             88  PS-SECT-NOT-IN-RANGE          VALUE 'N'.                 
023300         10  PS-TIMESTAMP-BWP-SW     PIC X(01) VALUE 'N'.                 
023400             88  PS-BWP-IN-RANGE               VALUE 'Y'.                 
023500             88  PS-BWP-NOT-IN-RANGE           VALUE 'N'.                 
023600         10  PS-TIMESTAMP-MDSE-SW    PIC X(01) VALUE 'N'.                 
023700             88  PS-MDSE-IN-RANGE              VALUE 'Y'.                 
023800             88  PS-MDSE-NOT-IN-RANGE          VALUE 'N'.                 
023900                                                                          
024000     05  PS-TBL-ENTRY-SW             PIC X(01) VALUE 'N'.                 
024100         88  PS-ENTRY-NOT-FOUND                VALUE 'N'.                 
024200         88  PS-ENTRY-FOUND                    VALUE 'Y'.                 
024300                                                                          
024400     05  PS-TABLE-SEARCH-SW          PIC X(01) VALUE 'N'.                 
024500         88  PS-NOT-END-OF-TABLE               VALUE 'N'.                 
024600         88  PS-END-OF-TABLE                   VALUE 'Y'.                 
024700                                                                          
024800     05  PS-STR-TBL-SRCH-SW          PIC X(01) VALUE 'N'.                 
024900         88  PS-STR-TBL-NF                     VALUE 'N'.                 
025000         88  PS-STR-TBL-FND                    VALUE 'Y'.                 
025100                                                                          
025200     05  PS-GRP-LOC-SW               PIC X(01) VALUE 'N'.                 
025300         88  PS-OFFER-PASS                     VALUE 'N'.                 
025400         88  PS-GRP-LOC-PASS                   VALUE 'Y'.                 
025500                                                                          
025501     05  PS-ORACLE-LOGIN-EOF-SW      PIC X(01)  VALUE 'N'.                
025502         88  ORACLE-LOGIN-EOF                   VALUE 'Y'.                
025503*---------------------------------------------------------------*         
025510 01  CC-ORACLE-CONNECTION.                                                
025520     05  CC-ORACLE-USERID        PIC X(40).                               
025530         88 CC-AUTO-LOGON                   VALUE '/'.                    
025540     05  CC-ORACLE-PASSWORD      PIC X(40).                               
025550                                                                          
025600*---------------------------------------------------------------*         
025700* PV- PROGRAM VARIABLES                                         *         
025800*---------------------------------------------------------------*         
025900 01  PV-LOGON-SYSIN.                                                      
026210     05  PV-ORACLE-USERID-LEN        PIC S9(04)      VALUE ZEROS.         
026220     05  PV-ORACLE-PASSWORD-LEN      PIC S9(04)      VALUE ZEROS.         
026300                                                                          
026400 01  PV-PROGRAM-VARIABLES.                                                
026500     05  PV-TALLY                    PIC S9(03) COMP-3 VALUE +0.          
026600     05  PV-SQL-SUB                  PIC S9(04) VALUE 0  COMP.            
026700     05  PV-OFR-SUB                  PIC S9(04) VALUE 0  COMP.            
026800     05  PV-OFR-SUB-SAVE             PIC S9(04) VALUE 0  COMP.            
026900     05  PV-STR-SUB                  PIC S9(04) VALUE 0  COMP.            
027000     05  PV-EVNT-ID-A                PIC 9(10)  VALUE 0.                  
027100     05  PV-OFR-ID-A                 PIC 9(10)  VALUE 0.                  
027200     05  PV-OFR-SECT-TYP-CDE-A       PIC X(01)  VALUE SPACES.             
027300     05  PV-OFR-SECT-BWP-ID-A        PIC 9(10)  VALUE 0.                  
027400     05  PV-MDSE-SET-ID-A            PIC 9(10)  VALUE 0.                  
027500     05  PV-OFR-TBL-CNT              PIC 9(2)   VALUE 0.                  
027600     05  PV-SHADOW-VAR               PIC 9(2)   VALUE 0.                  
027700                                                                          
027800     05  PV-DB2-TIMESTAMP.                                                
027900         10  PV-DB2-YYYY             PIC X(04) VALUE SPACES.              
028000         10  PV-DB2-FILLER-1         PIC X(01) VALUE '-'.                 
028100         10  PV-DB2-MO               PIC X(02) VALUE SPACES.              
028200         10  PV-DB2-FILLER-2         PIC X(01) VALUE '-'.                 
028300         10  PV-DB2-DD               PIC X(02) VALUE SPACES.              
028400         10  PV-DB2-FILLER-3         PIC X(01) VALUE '-'.                 
028500         10  PV-DB2-HH               PIC X(02) VALUE SPACES.              
028600         10  PV-DB2-FILLER-4         PIC X(01) VALUE ':'.                 
028700         10  PV-DB2-MM               PIC X(02) VALUE SPACES.              
028800         10  PV-DB2-FILLER-5         PIC X(01) VALUE ':'.                 
028900         10  PV-DB2-SS               PIC X(02) VALUE SPACES.              
029000         10  PV-DB2-FILLER-6         PIC X(01) VALUE ':'.                 
029100         10  PV-DB2-MS               PIC X(06) VALUE SPACES.              
029200                                                                          
029300     05  PV-WORK-DATE.                                                    
029400         10  PV-WORK-YYYY            PIC X(04) VALUE SPACES.              
029500         10  PV-WORK-FILLER-1        PIC X(01) VALUE '-'.                 
029600         10  PV-WORK-MO              PIC X(02) VALUE SPACES.              
029700         10  PV-WORK-FILLER-2        PIC X(01) VALUE '-'.                 
029800         10  PV-WORK-DY              PIC X(02) VALUE SPACES.              
029900                                                                          
030000     05  PV-RANGE-BEGIN.                                                  
030100         10  PV-RANGE-BEG-YYYY       PIC X(04) VALUE SPACES.              
030200         10  PV-RANGE-BEG-FILLER-1   PIC X(01) VALUE '-'.                 
030300         10  PV-RANGE-BEG-MO         PIC X(02) VALUE SPACES.              
030400         10  PV-RANGE-BEG-FILLER-2   PIC X(01) VALUE '-'.                 
030500         10  PV-RANGE-BEG-DY         PIC X(02) VALUE SPACES.              
030600                                                                          
030700     05  PV-RANGE-END.                                                    
030800         10  PV-RANGE-END-YYYY       PIC X(04) VALUE SPACES.              
030900         10  PV-RANGE-END-FILLER-1   PIC X(01) VALUE '-'.                 
031000         10  PV-RANGE-END-MO         PIC X(02) VALUE SPACES.              
031100         10  PV-RANGE-END-FILLER-2   PIC X(01) VALUE '-'.                 
031200         10  PV-RANGE-END-DY         PIC X(02) VALUE SPACES.              
031300                                                                          
031400     05  PV-OFFER-DATE.                                                   
031500         10  PV-OFFER-DATE-YYYY      PIC X(04) VALUE SPACES.              
031600         10  PV-OFFER-DATE-FILLER-1  PIC X(01) VALUE '-'.                 
031700         10  PV-OFFER-DATE-MO        PIC X(02) VALUE SPACES.              
031800         10  PV-OFFER-DATE-FILLER-2  PIC X(01) VALUE '-'.                 
031900         10  PV-OFFER-DATE-DY        PIC X(02) VALUE SPACES.              
032000                                                                          
032100     05  PV-WORK-TIME.                                                    
032200         10  PV-WORK-HH              PIC X(02) VALUE SPACES.              
032300         10  PV-WORK-MM              PIC X(02) VALUE SPACES.              
032400         10  PV-WORK-SS              PIC X(02) VALUE SPACES.              
032500                                                                          
032600     05  PV-WORK-ORA-FORMAT.                                              
032700       10  PV-WORK-ORA-FORMAT-DATE.                                       
032800         15  PV-WORK-ORA-YYYY        PIC X(04) VALUE SPACES.              
032900         15  PV-WORK-ORA-FILLER-1    PIC X(01) VALUE '-'.                 
033000         15  PV-WORK-ORA-MO          PIC X(02) VALUE SPACES.              
033100         15  PV-WORK-ORA-FILLER-2    PIC X(01) VALUE '-'.                 
033200         15  PV-WORK-ORA-DD          PIC X(02) VALUE SPACES.              
033300       10  PV-WORK-ORA-FORMAT-TIME.                                       
033400         15  PV-WORK-ORA-FILLER-3    PIC X(01) VALUE ' '.                 
033500         15  PV-WORK-ORA-HH          PIC X(02) VALUE SPACES.              
033600         15  PV-WORK-ORA-FILLER-4    PIC X(01) VALUE ':'.                 
033700         15  PV-WORK-ORA-MM          PIC X(02) VALUE SPACES.              
033800         15  PV-WORK-ORA-FILLER-5    PIC X(01) VALUE ':'.                 
033900         15  PV-WORK-ORA-SS          PIC X(02) VALUE SPACES.              
034000                                                                          
034100     05  PV-WORK-DELTA               PIC 9(05) VALUE ZERO.                
034200                                                                          
034300     05  PV-WORK-DB2-FORMAT.                                              
034400         10  PV-WORK-DB2-YYYY        PIC X(04) VALUE SPACES.              
034500         10  PV-WORK-DB2-FILLER-1    PIC X(01) VALUE '-'.                 
034600         10  PV-WORK-DB2-MO          PIC X(02) VALUE SPACES.              
034700         10  PV-WORK-DB2-FILLER-2    PIC X(01) VALUE '-'.                 
034800         10  PV-WORK-DB2-DD          PIC X(02) VALUE SPACES.              
034900         10  PV-WORK-DB2-FILLER-3    PIC X(01) VALUE ' '.                 
035000         10  PV-WORK-DB2-HH          PIC X(02) VALUE SPACES.              
035100         10  PV-WORK-DB2-FILLER-4    PIC X(01) VALUE ':'.                 
035200         10  PV-WORK-DB2-MM          PIC X(02) VALUE SPACES.              
035300         10  PV-WORK-DB2-FILLER-5    PIC X(01) VALUE ':'.                 
035400         10  PV-WORK-DB2-SS          PIC X(02) VALUE SPACES.              
035500                                                                          
035600     05  PV-DATE-BEGIN.                                                   
035700         10  PV-DATE-BGN-YYYY        PIC X(04) VALUE SPACES.              
035800         10  PV-DATE-BGN-FILLER-1    PIC X(01) VALUE '-'.                 
035900         10  PV-DATE-BGN-MM          PIC X(02) VALUE SPACES.              
036000         10  PV-DATE-BGN-FILLER-2    PIC X(01) VALUE '-'.                 
036100         10  PV-DATE-BGN-DD          PIC X(02) VALUE SPACES.              
036200         10  PV-DATE-BGN-FILLER-3    PIC X(01) VALUE ' '.                 
036300         10  PV-DATE-BGN-HH          PIC X(02) VALUE SPACES.              
036400         10  PV-DATE-BGN-FILLER-4    PIC X(01) VALUE ':'.                 
036500         10  PV-DATE-BGN-MI          PIC X(02) VALUE SPACES.              
036600         10  PV-DATE-BGN-FILLER-5    PIC X(01) VALUE ':'.                 
036700         10  PV-DATE-BGN-SS          PIC X(02) VALUE SPACES.              
036800                                                                          
036900     05  PV-DATE-BEGIN-X             PIC X(19) VALUE SPACES.              
037000                                                                          
037100     05  PV-DATE-END.                                                     
037200         10  PV-DATE-END-YYYY        PIC X(04) VALUE SPACES.              
037300         10  PV-DATE-END-FILLER-1    PIC X(01) VALUE '-'.                 
037400         10  PV-DATE-END-MM          PIC X(02) VALUE SPACES.              
037500         10  PV-DATE-END-FILLER-2    PIC X(01) VALUE '-'.                 
037600         10  PV-DATE-END-DD          PIC X(02) VALUE SPACES.              
037700         10  PV-DATE-END-FILLER-3    PIC X(01) VALUE ' '.                 
037800         10  PV-DATE-END-HH          PIC X(02) VALUE SPACES.              
037900         10  PV-DATE-END-FILLER-4    PIC X(01) VALUE ':'.                 
038000         10  PV-DATE-END-MI          PIC X(02) VALUE SPACES.              
038100         10  PV-DATE-END-FILLER-5    PIC X(01) VALUE ':'.                 
038200         10  PV-DATE-END-SS          PIC X(02) VALUE SPACES.              
038300                                                                          
038400     05  PV-DATE-END-X               PIC X(19) VALUE SPACES.              
038500                                                                          
038600     05  PV-OMT00003-LAST-UPD-TMST   PIC X(19) VALUE SPACES.              
038700     05  PV-OMT00005-LAST-UPD-TMST   PIC X(19) VALUE SPACES.              
038800     05  PV-EP251-CC-PREV-TIMESTAMP.                                      
038900         10  PV-EP251-CC-PREV-YYYY   PIC X(04) VALUE SPACES.              
039000         10  PV-EP251-CC-PREV-F-1    PIC X(01) VALUE '-'.                 
039100         10  PV-EP251-CC-PREV-MM     PIC X(02) VALUE SPACES.              
039200         10  PV-EP251-CC-PREV-F-2    PIC X(01) VALUE '-'.                 
039300         10  PV-EP251-CC-PREV-DD     PIC X(02) VALUE SPACES.              
039400         10  PV-EP251-CC-PREV-F-3    PIC X(01) VALUE '-'.                 
039500         10  PV-EP251-CC-PREV-HH     PIC X(02) VALUE SPACES.              
039600         10  PV-EP251-CC-PREV-F-4    PIC X(01) VALUE '.'.                 
039700         10  PV-EP251-CC-PREV-MI     PIC X(02) VALUE SPACES.              
039800         10  PV-EP251-CC-PREV-F-5    PIC X(01) VALUE '.'.                 
039900         10  PV-EP251-CC-PREV-SS     PIC X(02) VALUE SPACES.              
040000     05  PV-EP251-CC-CRNT-TIMESTAMP.                                      
040100         10  PV-EP251-CC-CRNT-YYYY   PIC X(04) VALUE SPACES.              
040200         10  PV-EP251-CC-CRNT-F-1    PIC X(01) VALUE '-'.                 
040300         10  PV-EP251-CC-CRNT-MM     PIC X(02) VALUE SPACES.              
040400         10  PV-EP251-CC-CRNT-F-2    PIC X(01) VALUE '-'.                 
040500         10  PV-EP251-CC-CRNT-DD     PIC X(02) VALUE SPACES.              
040600         10  PV-EP251-CC-CRNT-F-3    PIC X(01) VALUE '-'.                 
040700         10  PV-EP251-CC-CRNT-HH     PIC X(02) VALUE SPACES.              
040800         10  PV-EP251-CC-CRNT-F-4    PIC X(01) VALUE '.'.                 
040900         10  PV-EP251-CC-CRNT-MI     PIC X(02) VALUE SPACES.              
041000         10  PV-EP251-CC-CRNT-F-5    PIC X(01) VALUE '.'.                 
041100         10  PV-EP251-CC-CRNT-SS     PIC X(02) VALUE SPACES.              
041200                                                                          
041300     05  PV-ORA-STAGING              PIC X(19) VALUE SPACES.              
041400                                                                          
041500     05  PV-TODAY-DATE.                                                   
041600         10  PV-TODAY-DATE-YYYY      PIC X(04).                           
041700         10  PV-TODAY-DATE-MO        PIC X(02).                           
041800         10  PV-TODAY-DATE-DY        PIC X(02).                           
041900                                                                          
042000     05  PV-EFF-BEG-TMST.                                                 
042100         10  PV-EFF-BEG-DATE         PIC X(10).                           
042200         10  PV-EFF-BEG-TIME         PIC X(09).                           
042300                                                                          
042400     05  PV-EFF-END-TMST.                                                 
042500         10  PV-EFF-END-DATE         PIC X(10).                           
042600         10  PV-EFF-END-TIME         PIC X(09).                           
042700                                                                          
042800     05  PV-EFF-DATE.                                                     
042900         10  PV-DATE-CCYY            PIC X(04).                           
043000         10  PV-DATE-MM              PIC X(02).                           
043100         10  PV-DATE-DD              PIC X(02).                           
043200                                                                          
043300     05  PV-FORMAT-DATE.                                                  
043400         10  PV-FORMAT-DATE-CCYY.                                         
043500             15  PV-FORMAT-CC        PIC X(02).                           
043600             15  PV-FORMAT-YY        PIC X(02).                           
043700         10  PV-DASH1                PIC X(01).                           
043800         10  PV-FORMAT-DATE-MM       PIC X(02).                           
043900         10  PV-DASH2                PIC X(01).                           
044000         10  PV-FORMAT-DATE-DD       PIC X(02).                           
044100                                                                          
044200     05  PV-VAR-CSR1.                                                     
044300         10  PV-EVNT-ID                  PIC 9(10)  VALUE 0.              
044400         10  PV-OFR-ID                   PIC 9(10)  VALUE 0.              
044500         10  PV-OFR-SECT-TYP-CDE         PIC X(01)  VALUE SPACES.         
044600         10  PV-OFR-SECT-BWP-ID          PIC 9(10)  VALUE 0.              
044700         10  PV-MDSE-SET-ID              PIC 9(10)  VALUE 0.              
044800         10  PV-OFR-TRGR-TYP-CDE-C1      PIC X(01)  VALUE SPACES.         
044900         10  PV-OFR-TBL-CHG-TYPE-C1      PIC X(01) VALUE 'A'.             
045000             88 PV-MDSE-REC-ONLY-C1                VALUE 'M'.             
045100             88 PV-ALL-RECORDS-C1                  VALUE 'A'.             
045200                                                                          
045300     05  PV-VAR-MDSE.                                                     
045400         10  PV-MDS-EVNT-ID             PIC 9(10)  VALUE 0.               
045500         10  PV-MDS-OFR-ID              PIC 9(10)  VALUE 0.               
045600         10  PV-MDS-OFR-SECT-TYP-CDE    PIC X(01)  VALUE SPACES.          
045700                                                                          
045800     05  PV-VAR-STR-CSR1.                                                 
045900         10  PV-STR-GP-TYP-CDE           PIC 9(05) VALUE 0.               
046000         10  PV-STR-GP-ID                PIC 9(05) VALUE 0.               
046100                                                                          
046200*--------------------------------------------------------------*          
046300* DF- DATE AND TIME FIELDS USED IN 9700-GET-CURRENT             *         
046400*--------------------------------------------------------------*          
046500 01  DF-DATE-TIME-FIELDS.                                                 
046600     05  DF-CURRENT-DATE-TIME.                                            
046700         10  DF-CURRENT-YR           PIC 9(04) VALUE ZEROES.              
046800         10  DF-CURRENT-MO           PIC 9(02) VALUE ZEROES.              
046900         10  DF-CURRENT-DY           PIC 9(02) VALUE ZEROES.              
047000         10  DF-CURRENT-HR           PIC 9(02) VALUE ZEROES.              
047100         10  DF-CURRENT-MIN          PIC 9(02) VALUE ZEROES.              
047200         10  DF-CURRENT-SEC          PIC 9(02) VALUE ZEROES.              
047300     05  DF-DISPLAY-DATE.                                                 
047400         10  DF-DD-YR                PIC X(04) VALUE SPACES.              
047500         10  FILLER                  PIC X(01) VALUE '-'.                 
047600         10  DF-DD-MO                PIC X(02) VALUE SPACES.              
047700         10  FILLER                  PIC X(01) VALUE '-'.                 
047800         10  DF-DD-DY                PIC X(02) VALUE SPACES.              
047900     05  DF-DISPLAY-TIME.                                                 
048000         10  DF-DD-HR                PIC X(02) VALUE SPACES.              
048100         10  FILLER                  PIC X(01) VALUE ':'.                 
048200         10  DF-DD-MIN               PIC X(02) VALUE SPACES.              
048300         10  FILLER                  PIC X(01) VALUE ':'.                 
048400         10  DF-DD-SEC               PIC X(02) VALUE SPACES.              
048500                                                                          
048600*****************************************************************         
048700* NULL INDICATORS                                               *         
048800*****************************************************************         
048900 01  PV-NULL-INDICATORS.                                                  
049000     05  PV-POS-RLTV-NUM-NULL-IND     PIC S9(4) COMP VALUE +0.            
049100     05  PV-ITM-DISC-APL-NULL-IND     PIC S9(4) COMP VALUE +0.            
049200     05  PV-POS-BTN-CDE-NULL-IND      PIC S9(4) COMP VALUE +0.            
049300     05  PV-BWP-BUY-QTY-NULL-IND      PIC S9(4) COMP VALUE +0.            
049400     05  PV-BWP-BUY-AMT-NULL-IND      PIC S9(4) COMP VALUE +0.            
049500     05  PV-BWP-BUY-TOL-AMT-NULL-IND  PIC S9(4) COMP VALUE +0.            
049600     05  PV-BWP-GET-QTY-NULL-IND      PIC S9(4) COMP VALUE +0.            
049700     05  PV-BWP-GET-PCT-NULL-IND      PIC S9(4) COMP VALUE +0.            
049800     05  PV-BWP-GET-DOL-NULL-IND      PIC S9(4) COMP VALUE +0.            
049900     05  PV-BWP-GET-PRPT-NULL-IND     PIC S9(4) COMP VALUE +0.            
050000     05  PV-MDSE-DEPT-NBR-NULL-IND    PIC S9(4) COMP VALUE +0.            
050100     05  PV-MDSE-MAJ-CLS-NULL-IND     PIC S9(4) COMP VALUE +0.            
050200     05  PV-MDSE-SUB-CLS-NULL-IND     PIC S9(4) COMP VALUE +0.            
050300     05  PV-MDSE-STL-ID-NULL-IND      PIC S9(4) COMP VALUE +0.            
050400     05  PV-MDSE-SKU-NB-NULL-IND      PIC S9(4) COMP VALUE +0.            
050500     05  PV-MDSE-EMP-DS-NULL-IND      PIC S9(4) COMP VALUE +0.            
050600     05  PV-MDSE-DSC-EL-NULL-IND      PIC S9(4) COMP VALUE +0.            
050700     05  PV-MDSE-TKT-CL-NULL-IND      PIC S9(4) COMP VALUE +0.            
050800                                                                          
050900*---------------------------------------------------------------*         
051000* PV- PROGRAM VARIABLES                                         *         
051100*     (THE BELOW TABLE WILL HOLD ALL OF THE ROWS OF             *         
051200*                       K_OM01.OMT_POS_BTN TABLE                *         
051300*---------------------------------------------------------------*         
051400 01  PV-OMT-POS-BTN.                                                      
051500     05  PV-BTN-RECORD               OCCURS 10 TIMES                      
051600                                     INDEXED BY PV-BTN-INDEX.             
051700         10  PV-POS-BTN-CDE          PIC X(03).                           
051800         10  PV-POS-BTN-TEXT         PIC X(16).                           
051900         10  PV-POS-BTN-RLTV-SEQ-NBR PIC 9(10).                           
052000         10  PV-POS-BTN-RLTV-SEQ-NBR-X REDEFINES                          
052100             PV-POS-BTN-RLTV-SEQ-NBR    PIC X(10).                        
052200                                                                          
052300 01  PV-OMT-OFR-TBL.                                                      
052400     05  PV-OFR-RECORD               OCCURS 2000 TIMES                    
052500                                     INDEXED BY PV-OFR-INDEX              
052600                                         PV-OFR-INDEX-SAVE.               
052700         10  PV-OFR-EVENT-ID         PIC 9(10).                           
052800         10  PV-OFR-OFFER-ID         PIC 9(10).                           
052900         10  PV-OFR-OFR-SECT-TYP-CDE PIC X(01).                           
053000         10  PV-OFR-BWP-ID           PIC 9(10).                           
053100         10  PV-OFR-MDSE-ID          PIC 9(10).                           
053200         10  PV-OFR-TBL-TRGR-TYPE    PIC X(01).                           
053300         10  PV-OFR-TBL-CHG-TYPE     PIC X(01) VALUE 'A'.                 
053400             88 PV-MDSE-REC-ONLY               VALUE 'M'.                 
053500             88 PV-ALL-RECORDS                 VALUE 'A'.                 
053600                                                                          
053700 01  PV-OMT-STR-TBL.                                                      
053800     05  PV-STR-RECORDS              OCCURS 2000 TIMES                    
053900                                     INDEXED BY PV-STR-INDEX              
054000                                         PV-STR-INDEX-SAVE.               
054100         10  PV-STR-TRIGGER-TYPE     PIC X(01)  VALUE SPACES.             
054200         10  PV-STR-EVENT-ID         PIC 9(10)  VALUE ZERO.               
054300         10  PV-STR-OFFER-ID         PIC 9(10)  VALUE ZERO.               
054400                                                                          
054500*---------------------------------------------------------------*         
054600* PV- PROGRAM COUNT VARIABLES                                   *         
054700*---------------------------------------------------------------*         
054800 01  PV-PROGRAM-COUNTERS.                                                 
054900     05  PV-OFFER-SELECT-READ        PIC 9(09)  VALUE 0.                  
055000     05  PV-OFFER-CURSOR-READ        PIC 9(09)  VALUE 0.                  
055100     05  PV-MDSE-CURSOR-READ         PIC 9(09)  VALUE 0.                  
055200     05  PV-STRCRSR1-READ            PIC 9(09)  VALUE 0.                  
055300     05  PV-STRCRSR2-READ            PIC 9(09)  VALUE 0.                  
055400     05  PV-OFFER-RECS-OUT           PIC 9(09)  VALUE 0.                  
055500     05  PV-OFRSECT-RECS-OUT         PIC 9(09)  VALUE 0.                  
055600     05  PV-OFRBWP-RECS-OUT          PIC 9(09)  VALUE 0.                  
055700     05  PV-MDSESET-RECS-OUT         PIC 9(09)  VALUE 0.                  
055800     05  PV-OFFER-STR-RECS-OUT       PIC 9(09)  VALUE 0.                  
055900     05  PV-OFRSECT-STR-RECS-OUT     PIC 9(09)  VALUE 0.                  
056000     05  PV-OFRBWP-STR-RECS-OUT      PIC 9(09)  VALUE 0.                  
056100     05  PV-MDSESET-STR-RECS-OUT     PIC 9(09)  VALUE 0.                  
056200     05  PV-STR-LIST-RECS-OUT        PIC 9(09)  VALUE 0.                  
056300     05  PV-ERR-RECS-OUT             PIC 9(09)  VALUE 0.                  
056400                                                                          
056500*****************************************************************         
056600* ERROR HANDLING                                                *         
056700*****************************************************************         
056800 01  PV-MISC-ABEND-FIELDS.                                                
056900     05  PV-PARAGRAPH-NAME           PIC X(30) VALUE SPACES.              
057000     05  PV-ORA-OPERATION            PIC X(50) VALUE SPACES.              
057100     05  PV-OPERATION-MSG-1          PIC X(40) VALUE SPACES.              
057200     05  PV-OPERATION-MSG-2          PIC X(40) VALUE SPACES.              
057300                                                                          
057400 01  AC-ABEND-CODE                   PIC S9(04) VALUE +0                  
057500                                                COMP SYNC.                
057600     88  AC-4003-CONTROL-CARD-ERROR             VALUE +4003.              
057700     88  AC-4004-TABLE-OVERFLOW-ERROR           VALUE +4004.              
057800     88  AC-4008-INVALID-DATA                   VALUE +4008.              
057900     88  AC-4013-ORACLE-ERROR                   VALUE +4013.              
058000     88  AC-4019-CAL-SUB-ERROR                  VALUE +4019.              
058100                                                                          
058200*---------------------------------------------------------------*         
058300* VARIABLES REQUIRED FOR THE KOHL'S DATE ROUTINE DPKUT500       *         
058400*---------------------------------------------------------------*         
058500     COPY DPWS500.                                                        
058600                                                                          
058700*---------------------------------------------------------------*         
058800* COPYBOOK FOR INPUT TIMESTAMP CONTROL CARD EPCC251             *         
058900*---------------------------------------------------------------*         
059000     COPY EPRD251.                                                        
059100                                                                          
059200*---------------------------------------------------------------*         
059300* COPYBOOK FOR INPUT NEXT FUTURE DAY CONTROL CARD EPCC252       *         
059400*---------------------------------------------------------------*         
059500     COPY EPRD252.                                                        
059600                                                                          
059700*---------------------------------------------------------------*         
059800* COPYBOOK FOR OUTPUT OFFERS CORRECTION FILE - INTERMEDIATE     *         
059900*---------------------------------------------------------------*         
060000     COPY EPRD205.                                                        
060100                                                                          
060200*---------------------------------------------------------------*         
060300* COPYBOOK FOR OUTPUT CORRECTION STORE LIST FILE                *         
060400*---------------------------------------------------------------*         
060500     COPY EPRD206.                                                        
060600                                                                          
060700*---------------------------------------------------------------*         
060800* COPYBOOK FOR DATA VALIDATION ROUTINE EPDD208                            
060900*---------------------------------------------------------------*         
061000     COPY EPWS208.                                                        
061100                                                                          
061200*---------------------------------------------------------------*         
061300* NETCOOL ERROR MESSAGE COPYBOOK                                          
061400*---------------------------------------------------------------*         
061500     COPY ISWS003.                                                        
061600                                                                          
061700*---------------------------------------------------------------*         
061800* ORACLE DECLARE SECTION START                                  *         
061900*---------------------------------------------------------------*         
062000     EXEC SQL                                                             
062100        BEGIN DECLARE SECTION                                             
062200     END-EXEC.                                                            
062300                                                                          
062400*---------------------------------------------------------------*         
062500* PD- ORA VARIABLES (NON DCLGEN)                                *         
062600*---------------------------------------------------------------*         
062700 01  USERNAME                        PIC X(10)  VARYING.                  
062800 01  PASSWD                          PIC X(10)  VARYING.                  
062900 01  PV-ORACLE-ID                    PIC X(01)  VALUE '/'.                
063000 01  PV-MDSE-DLT-IND                 PIC X(01)  VALUE SPACE.              
063100                                                                          
063200*---------------------------------------------------------------*         
063300* OMT00001 - OMT_OFR  COPYBOOK                                  *         
063400* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR             *         
063500*---------------------------------------------------------------*         
063600     EXEC SQL                                                             
063700        INCLUDE OMT00001                                                  
063800     END-EXEC.                                                            
063900                                                                          
064000*---------------------------------------------------------------*         
064100* OMT00002 - OMT_OFR_SECT COPYBOOK                              *         
064200* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR_SECT        *         
064300*---------------------------------------------------------------*         
064400     EXEC SQL                                                             
064500         INCLUDE OMT00002                                                 
064600     END-EXEC.                                                            
064700                                                                          
064800*---------------------------------------------------------------*         
064900* OMT00003 - OMT_OFR_SECT_BWP COPYBOOK                          *         
065000* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR_SECT_BWP    *         
065100*---------------------------------------------------------------*         
065200     EXEC SQL                                                             
065300         INCLUDE OMT00003                                                 
065400     END-EXEC.                                                            
065500                                                                          
065600*---------------------------------------------------------------*         
065700* OMT00004 - OMT_OFR_LOC_GP COPYBOOK                            *         
065800* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR_LOC_GP      *         
065900*---------------------------------------------------------------*         
066000     EXEC SQL                                                             
066100         INCLUDE OMT00004                                                 
066200     END-EXEC.                                                            
066300                                                                          
066400*---------------------------------------------------------------*         
066500* OMT00005 - OMT_MDSE_SET COPYBOOK                              *         
066600* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_MDSE_SET        *         
066700*---------------------------------------------------------------*         
066800     EXEC SQL                                                             
066900         INCLUDE OMT00005                                                 
067000     END-EXEC.                                                            
067100                                                                          
067200*---------------------------------------------------------------*         
067300* OMT00006 - OMT_POS_BTN COPYBOOK                               *         
067400* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_POS_BTN         *         
067500*---------------------------------------------------------------*         
067600     EXEC SQL                                                             
067700         INCLUDE OMT00006                                                 
067800     END-EXEC.                                                            
067900                                                                          
068000*---------------------------------------------------------------*         
068100* OMT00007 - OMT_EVNT_CTG COPYBOOK                              *         
068200* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_EVNT_CTG        *         
068300*---------------------------------------------------------------*         
068400     EXEC SQL                                                             
068500         INCLUDE OMT00007                                                 
068600     END-EXEC.                                                            
068700                                                                          
068800*---------------------------------------------------------------*         
068900* OMT00008 - OMT_OFR_CUSTFACG_DESC COPYBOOK                     *         
069000* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR_CUSTFACG_DES*         
069100*---------------------------------------------------------------*         
069200     EXEC SQL                                                             
069300         INCLUDE OMT00008                                                 
069400     END-EXEC.                                                            
069500                                                                          
069600*---------------------------------------------------------------*         
069700* OMT00009 - OMT_EVNT COPYBOOK                                  *         
069800* COBOL DECLARATION FOR ORACLE TABLE K_OM01.OMT_OFR_EVNT        *         
069900*---------------------------------------------------------------*         
070000     EXEC SQL                                                             
070100         INCLUDE OMT00009                                                 
070200     END-EXEC.                                                            
070300                                                                          
070400*---------------------------------------------------------------*         
070500* ORACLE END OF DECLARATIVE SECTION                             *         
070600*---------------------------------------------------------------*         
070700     EXEC SQL                                                             
070800        END DECLARE SECTION                                               
070900     END-EXEC.                                                            
071000                                                                          
071100     EXEC SQL                                                             
071200        INCLUDE SQLCACOB                                                  
071300     END-EXEC.                                                            
071400                                                                          
071500*---------------------------------------------------------------*         
071600* VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004       *         
071700*---------------------------------------------------------------*         
071800     COPY DPWS004.                                                        
071900                                                                          
072000*---------------------------------------------------------------*         
072100* ORA ABEND COPYBOOK                                            *         
072200*---------------------------------------------------------------*         
072300     COPY DPWS900.                                                        
072400                                                                          
072500*****************************************************************         
072600 PROCEDURE DIVISION.                                                      
072700*****************************************************************         
072800 0000-MAINLINE.                                                           
072900                                                                          
073000     MOVE '0000-MAINLINE                 '                                
073100       TO PV-PARAGRAPH-NAME.                                              
073200                                                                          
073300     PERFORM 1000-INITIALIZATION.                                         
073400                                                                          
073500     PERFORM 1100-LOOK-FOR-CORRECTIONS.                                   
073600                                                                          
073700     PERFORM 1200-LOOK-FOR-STORE-CHANGES.                                 
073800                                                                          
073900     PERFORM 9500-WRAPUP.                                                 
074000                                                                          
074100     GOBACK.                                                              
074200*0000-EXIT.                                                               
074300 EJECT.                                                                   
074400                                                                          
074500*****************************************************************         
074600* 1000-INITIALIZATION                                           *         
074700*     INITIALIZES VARIABLES, OPENS FILES, READS CONTROL CARDS,  *         
074800*     AND PERFORMS SETUP ROUTINES.                              *         
074900*                                                               *         
075000*   1.  OBTAINS THE CURRENT DATE AND TIME VIA THE COBOL         *         
075100*       FUNCTION GET-DATE AND PLACES THE RETURNED VALUE IN      *         
075200*       DF-DISPLAY-DATE AND DF-DISPLAY-TIME.                    *         
075300*                                                               *         
075400*   2.  DISPLAYS THE PROGRAM START BANNER.                      *         
075500*                                                               *         
075600*   3.  OPENS THE PROGRAM'S INPUT AND OUTPUT SEQUENTIAL FILES   *         
075700*                                                               *         
075800*   4.  INITIALIZES THE INPUT AREA FOR THE TWO CONTROL CARDS.   *         
075900*                                                               *         
076000*   5.  READS THE EP251 DATE RANGE CONTROL CARD.                *         
076100*                                                               *         
076200*   6.  READS THE EP252 FUTURE DATE CONTROL CARD.               *         
076300*                                                               *         
076400*   7.  VALIDATES THE VALUE IN EP251-CC-PREV-TIMESTAMP.         *         
076500*                                                               *         
076600*   8.  VALIDATES THE VALUE IN EP251-CC-CRNT-TIMESTAMP.         *         
076700*                                                               *         
076800*   9.  VALIDATES THE VALUE IN EP252-CC-FUTURE-DATE.            *         
076900*                                                               *         
077000*  10.  CONNECTS TO THE ORACLE DATA BASE.                       *         
077100*                                                               *         
077200*  11.  POPULATES THE BTN TABLE.                                *         
077300*                                                               *         
077400*****************************************************************         
077500 1000-INITIALIZATION.                                                     
077600                                                                          
077700     MOVE '1000-INITIALIZATION            '                               
077800       TO PV-PARAGRAPH-NAME.                                              
077900                                                                          
078000     PERFORM 9700-GET-CURRENT.                                            
078100                                                                          
078200     DISPLAY '*************************************'.                     
078300     DISPLAY '**        EPKUT205 STARTED         **'.                     
078400     DISPLAY '*************************************'.                     
078500     DISPLAY '** CURRENT DATE ' DF-DISPLAY-DATE                           
078600                      '   **'.                                            
078700     DISPLAY '** CURRENT TIME ' DF-DISPLAY-TIME                           
078800                      '   **'.                                            
078900     DISPLAY '*************************************'.                     
079000                                                                          
079100     OPEN INPUT  CONTROL-CARD-NXT-FUT-DAY                                 
079200                 CORR-CARD-FILE.                                          
079300                                                                          
079400     OPEN OUTPUT                                                          
079500         OFFERS-CORR-FILE                                                 
079600         CORR-STORE-LIST-FILE                                             
079700         OFFERS-LOC-GRP-CORR-FILE                                         
079800         CORR-STR-GRP-LIST-FILE                                           
079900         TEMP-CORR-CARD-FILE.                                             
080000                                                                          
080100     INITIALIZE EP251-CC-CONTROL-CARD.                                    
080200     INITIALIZE EP252-CC-CONTROL-CARD.                                    
080300                                                                          
080400     PERFORM 8000-READ-CORRECTION-CARD.                                   
080500                                                                          
080600     PERFORM 8050-READ-CC-NXT-FUT-DAY.                                    
080700                                                                          
080800     PERFORM 2000-DATE-ROUTINES.                                          
080900                                                                          
081000     OPEN INPUT ORACLE-LOGIN-FILE.                                        
081100     PERFORM 2100-SETUP-ORACLE-CONNECTION.                                
081200                                                                          
081300     PERFORM 2200-POPULATE-BTN-TBL.                                       
081400                                                                          
081500*1000-EXIT.                                                               
081600 EJECT.                                                                   
081700                                                                          
081800*****************************************************************         
081900* 1100-LOOK-FOR-CORRECTIONS.                                    *         
082000*                                                               *         
082100*     THIS IS THE MAIN DRIVING PARAGRAPH FOR THE 1ST PHASE      *         
082200*     OF THIS PROGRAM WHICH EXTRACTS OFFERS BASED ON TIMESTAMPS *         
082300*     IN THE ORACLE TABLES OMT_OFR, OMT_OFR_SECT,               *         
082400*     OMT_OFR_SECT_BWP, AND OMT_MDSE_SET.                       *         
082500*                                                               *         
082600*     THIS IS ACCOMPLISHED BY:                                  *         
082700*                                                               *         
082800*         A.  BUILDING A TABLE OF OFFERS.                       *         
082900*             2300-FIRST-PASS-FOR-CHANGES                       *         
083000*                                                               *         
083100*         B.  USING THE TABLE TO DRIVE THE CREATION OF THE      *         
083200*             OUTPUT FILES.                                     *         
083300*             2400-PROCESS-OFFERS                               *         
083400*                                                               *         
083500*****************************************************************         
083600 1100-LOOK-FOR-CORRECTIONS.                                               
083700                                                                          
083800     MOVE '1100-LOOK-FOR-CORRECTIONS     '                                
083900       TO PV-PARAGRAPH-NAME.                                              
084000                                                                          
084100     SET PS-OFFER-PASS                                                    
084200       TO TRUE.                                                           
084300                                                                          
084400     SET PV-STR-INDEX-SAVE  TO +1.                                        
084500                                                                          
084600     PERFORM 2300-FIRST-PASS-FOR-CHANGES.                                 
084700                                                                          
084800     IF PV-OFR-TBL-CNT EQUAL ZERO                                         
084900         DISPLAY ' *********************************************'         
085000         DISPLAY ' *    THERE WERE NO CHANGES TO OFFER TABLES   '         
085100         DISPLAY ' *********************************************'         
085200     ELSE                                                                 
085300         PERFORM 1150-EXTRACT-OFFER-CHANGES                               
085400     END-IF.                                                              
085500                                                                          
085600*1100-EXIT.                                                               
085700 EJECT.                                                                   
085800                                                                          
085900******************************************************************        
086000* 1150-EXTRACT-OFFER-CHANGES.                                    *        
086100*  EXTRACT THE MODIFIED OFFERS, WHEN THE CONTROL HAS REACHED     *        
086200* THIS PARAGRAPH, IT MEANS THAT THE TIMESTAMP IN ONE OF THE OFFER*        
086300* TABLES HAS BEEN MODIFIED, AND OFFER DATA NEEDS TO BE EXTRACTED *        
086400******************************************************************        
086500 1150-EXTRACT-OFFER-CHANGES.                                              
086600                                                                          
086700     MOVE '1150-EXTRACT-OFFER-CHANGES    '                                
086800       TO PV-PARAGRAPH-NAME.                                              
086900                                                                          
087000     INITIALIZE PV-VAR-MDSE.                                              
087100                                                                          
087200     MOVE 1    TO PV-OFR-SUB.                                             
087300                                                                          
087400     PERFORM 2400-PROCESS-OFFERS                                          
087500       UNTIL PV-OFR-SUB > PV-OFR-SUB-SAVE.                                
087600                                                                          
087700*1150-EXIT.                                                               
087800 EJECT.                                                                   
087900                                                                          
088000*****************************************************************         
088100* 1200-LOOK-FOR-STORE-CHANGES.                                  *         
088200*  LOOK FOR CHANGES IN OFR_LOC_GP TABLE.                        *         
088300*     THIS IS THE MAIN DRIVING PARAGRAPH FOR THE 2ND PHASE      *         
088400*     OF THIS PROGRAM WHICH EXTRACTS OFFERS BASED ON THE        *         
088500*     TIMESTAMP IN THE ORACLE TABLE OFR_LOC_GP.                 *         
088600*                                                               *         
088700*     THIS IS ACCOMPLISHED BY:                                  *         
088800*                                                               *         
088900*         A.  EVALUATING THE CONTENTS OF THE LOC_DLT_IND        *         
089000*             AND THE IE-IND IN ENTRIES FALLING WITHIN THE      *         
089100*             DATE RANGE.                                                 
089200*                                                               *         
089300*         B.  AN OFFER WILL BE WRITTEN WITH A TRIGGER TYPE      *         
089400*             AS FOLLOWS:                                       *         
089500*                                                               *         
089600*        LOC_DLT_IND         IE_IND         TRIGGER-TYPE        *         
089700*                                                               *         
089800*             Y                N                  A             *         
089900*             Y                Y                  D             *         
090000*             N                N                  D             *         
090100*             N                Y                  A             *         
090200*                                                               *         
090300*****************************************************************         
090400 1200-LOOK-FOR-STORE-CHANGES.                                             
090500                                                                          
090600     MOVE '1200-LOOK-FOR-STORE-CHANGES   '                                
090700       TO PV-PARAGRAPH-NAME.                                              
090800                                                                          
090900     SET PS-NOT-END-OF-FETCH-OFR TO TRUE.                                 
091000     SET PS-GRP-LOC-PASS  TO TRUE.                                        
091100                                                                          
091200     MOVE 0  TO PV-OFR-ID-A                                               
091300                PV-EVNT-ID-A                                              
091400                PV-OFR-SECT-BWP-ID-A                                      
091500                PV-MDSE-SET-ID-A.                                         
091600                                                                          
091700     MOVE 0 TO PV-STR-GP-ID                                               
091800               PV-STR-GP-TYP-CDE.                                         
091900                                                                          
092000     MOVE SPACE TO PV-OFR-SECT-TYP-CDE-A.                                 
092100                                                                          
092200     PERFORM 7850-DEC-OPEN-STORE-CURSOR-2.                                
092300                                                                          
092400     PERFORM 2700-PROCESS-STORE-CURSOR-2                                  
092500       UNTIL PS-END-OF-FETCH-OFR.                                         
092600                                                                          
092700     PERFORM 7950-CLOSE-STORE-CURSOR-2.                                   
092800                                                                          
092900*1200-EXIT.                                                               
093000 EJECT.                                                                   
093100*****************************************************************         
093200* 2000-DATE-ROUTINES.                                           *         
093300*  VALIDATE FOLLOWING INPUT DATES.   *                                    
093400*  EP251-CC-PREV-TIMESTAMP AND EP251-CC-CRNT-TIMESTAMP ARE      *         
093500*    26 CHARACTER DB2 TIMESTAMPS OF FORMAT                      *         
093600*  EP252-CC-FUTURE-DATE IS A TEN CHARACTER DATE FIELD           *         
093700*    FORMATTED AS 'YYYY-MO-DD'.                                 *         
093800*****************************************************************         
093900 2000-DATE-ROUTINES.                                                      
094000     MOVE '2000-DATE-ROUTINES            '                                
094100       TO PV-PARAGRAPH-NAME.                                              
094200                                                                          
094300*     *** ABEND IF EITHER ARE EMPTY ***                                   
094400                                                                          
094500     IF PS-EOF-CC-CORR-FILE                                               
094600       OR EP251-CC-PREV-TIMESTAMP = SPACES                                
094700         OR EP251-CC-CRNT-TIMESTAMP = SPACES                              
094800           MOVE '2000-DATE-ROUTINES       '                               
094900             TO PV-PARAGRAPH-NAME                                         
095000           MOVE 'CORR DATE CARD RANGE MISSING'                            
095100             TO PV-OPERATION-MSG-1                                        
095200           SET AC-4003-CONTROL-CARD-ERROR                                 
095300             TO TRUE                                                      
095400           PERFORM 9100-ABEND                                             
095500     END-IF.                                                              
095600                                                                          
095700*    **** ABEND IF IT IS EMPTY ****                             *         
095800                                                                          
095900     IF PS-EOF-CC-NXT-FUT-DAY                                             
096000       OR EP252-CC-FUTURE-DATE = SPACES                                   
096100         MOVE '2000-DATE-ROUTINES      '  TO PV-PARAGRAPH-NAME            
096200         MOVE 'FUTURE DATE CARD IS EMPTY' TO PV-OPERATION-MSG-1           
096300         SET AC-4003-CONTROL-CARD-ERROR   TO TRUE                         
096400         PERFORM 9100-ABEND                                               
096500     END-IF.                                                              
096600                                                                          
096700     MOVE EP251-CC-PREV-TIMESTAMP  TO PV-DB2-TIMESTAMP.                   
096800                                                                          
096900                                                                          
097000***  VALIDATE THE DATE PORTION FROM  EP251-CC-PREV-TIMESTAMP              
097100                                                                          
097200     MOVE PV-DB2-YYYY  TO PV-WORK-YYYY                                    
097300                          PV-DATE-BGN-YYYY.                               
097400     MOVE PV-DB2-MO    TO PV-WORK-MO                                      
097500                          PV-DATE-BGN-MM.                                 
097600     MOVE PV-DB2-DD    TO PV-WORK-DY                                      
097700                          PV-DATE-BGN-DD.                                 
097800                                                                          
097900     PERFORM 3000-VALIDATE-DATE-INPUT.                                    
098000                                                                          
098100     MOVE EP251-CC-CRNT-TIMESTAMP TO PV-DB2-TIMESTAMP.                    
098200                                                                          
098300*  VALIDATE THE DATE PORTION FROM  EP251-CC-CRNT-TIMESTAMP   *****        
098400                                                                          
098500     MOVE PV-DB2-YYYY TO PV-WORK-YYYY                                     
098600                         PV-DATE-END-YYYY.                                
098700     MOVE PV-DB2-MO   TO PV-WORK-MO                                       
098800                         PV-DATE-END-MM.                                  
098900     MOVE PV-DB2-DD   TO PV-WORK-DY                                       
099000                         PV-DATE-END-DD.                                  
099100                                                                          
099200     PERFORM 3000-VALIDATE-DATE-INPUT.                                    
099300                                                                          
099400*  VALIDATE THE NEXT/FUTURE DATE                             *****        
099500     MOVE EP252-CC-FUTURE-DATE  TO PV-WORK-DATE.                          
099600                                                                          
099700     PERFORM 3000-VALIDATE-DATE-INPUT.                                    
099800                                                                          
099900     MOVE EP251-CC-PREV-TIMESTAMP  TO PV-DB2-TIMESTAMP.                   
100000                                                                          
100100*  EXTRACT THE TIME PART OF EP252-CC-PREV-TIMESTAMP FOR VALIDATION        
100200                                                                          
100300     MOVE PV-DB2-HH TO PV-WORK-HH                                         
100400                       PV-DATE-BGN-HH.                                    
100500     MOVE PV-DB2-MM TO PV-WORK-MM                                         
100600                       PV-DATE-BGN-MI.                                    
100700     MOVE PV-DB2-SS TO PV-WORK-SS                                         
100800                       PV-DATE-BGN-SS.                                    
100900                                                                          
101000     PERFORM 3100-VALIDATE-TIME-INPUT.                                    
101100                                                                          
101200     IF PS-TIME-NOT-VALID                                                 
101300       MOVE '3100-VALIDATE-TIME-INPUT  '  TO PV-PARAGRAPH-NAME            
101400       MOVE 'PREV TIMESTAMP TIME NOT VALID' TO PV-OPERATION-MSG-1         
101500       SET AC-4003-CONTROL-CARD-ERROR   TO TRUE                           
101600       PERFORM 9100-ABEND                                                 
101700     END-IF.                                                              
101800                                                                          
101900     MOVE EP251-CC-CRNT-TIMESTAMP  TO PV-DB2-TIMESTAMP.                   
102000                                                                          
102100*  EXTRACT THE TIME PART OF EP251-CC-CRNT-TIMESTAMP FOR VALIDATION        
102200                                                                          
102300     MOVE PV-DB2-HH  TO PV-WORK-HH                                        
102400                        PV-DATE-END-HH.                                   
102500     MOVE PV-DB2-MM  TO PV-WORK-MM                                        
102600                        PV-DATE-END-MI.                                   
102700     MOVE PV-DB2-SS  TO PV-WORK-SS                                        
102800                        PV-DATE-END-SS.                                   
102900                                                                          
103000     PERFORM 3100-VALIDATE-TIME-INPUT.                                    
103100                                                                          
103200     IF PS-TIME-NOT-VALID                                                 
103300       MOVE '3100-VALIDATE-TIME-INPUT  '   TO PV-PARAGRAPH-NAME           
103400       MOVE 'CURR TIMESTAMP TIME NOT VALID' TO PV-OPERATION-MSG-1         
103500       SET AC-4003-CONTROL-CARD-ERROR   TO TRUE                           
103600       PERFORM 9100-ABEND                                                 
103700     END-IF.                                                              
103800                                                                          
103900*  FORMATTED TIMESTAMP TO BE USED IN CURSOR                               
104000                                                                          
104100     MOVE PV-DATE-BEGIN  TO PV-DATE-BEGIN-X.                              
104200     MOVE PV-DATE-END    TO PV-DATE-END-X.                                
104300                                                                          
104400*--- CHECK IF THE TIMESTAMP RANGE IS SPECIFIED CORRECTLY                  
104500                                                                          
104600     IF EP251-CC-PREV-TIMESTAMP > EP251-CC-CRNT-TIMESTAMP                 
104700       MOVE '2000-DATE-ROUTINES      '   TO PV-PARAGRAPH-NAME             
104800       MOVE 'TIMESTAMP RANGE INCORRECT ' TO PV-OPERATION-MSG-1            
104900       SET AC-4003-CONTROL-CARD-ERROR   TO TRUE                           
105000       PERFORM 9100-ABEND                                                 
105100     END-IF                                                               
105200                                                                          
105300*--- THESE DATES WILL BE USED TO DETERMINE THE TRIGGER TYPE               
105400*--- LATER IN THE PROGRAM.                                                
105500*--- FORMAT EP251-CC-PREV-TMST                                            
105600                                                                          
105700     MOVE EP251-CC-PREV-TIMESTAMP  TO PV-DB2-TIMESTAMP.                   
105800     MOVE PV-DB2-YYYY              TO PV-EP251-CC-PREV-YYYY.              
105900     MOVE PV-DB2-FILLER-1          TO PV-EP251-CC-PREV-F-1.               
106000     MOVE PV-DB2-MO                TO PV-EP251-CC-PREV-MM.                
106100     MOVE PV-DB2-FILLER-2          TO PV-EP251-CC-PREV-F-2.               
106200     MOVE PV-DB2-DD                TO PV-EP251-CC-PREV-DD.                
106300     MOVE PV-DB2-FILLER-3          TO PV-EP251-CC-PREV-F-3.               
106400     MOVE PV-DB2-HH                TO PV-EP251-CC-PREV-HH.                
106500     MOVE PC-DOT-VALUE             TO PV-EP251-CC-PREV-F-4.               
106600     MOVE PV-DB2-MM                TO PV-EP251-CC-PREV-MI.                
106700     MOVE PC-DOT-VALUE             TO PV-EP251-CC-PREV-F-5.               
106800     MOVE PV-DB2-SS                TO PV-EP251-CC-PREV-SS.                
106900                                                                          
107000*--- FORMAT EP251-CC-CRNT-TMST                                            
107100                                                                          
107200     MOVE EP251-CC-CRNT-TIMESTAMP  TO PV-DB2-TIMESTAMP.                   
107300     MOVE PV-DB2-YYYY              TO PV-EP251-CC-CRNT-YYYY.              
107400     MOVE PV-DB2-FILLER-1          TO PV-EP251-CC-CRNT-F-1.               
107500     MOVE PV-DB2-MO                TO PV-EP251-CC-CRNT-MM.                
107600     MOVE PV-DB2-FILLER-2          TO PV-EP251-CC-CRNT-F-2.               
107700     MOVE PV-DB2-DD                TO PV-EP251-CC-CRNT-DD.                
107800     MOVE PV-DB2-FILLER-3          TO PV-EP251-CC-CRNT-F-3.               
107900     MOVE PV-DB2-HH                TO PV-EP251-CC-CRNT-HH.                
108000     MOVE PC-DOT-VALUE             TO PV-EP251-CC-CRNT-F-4.               
108100     MOVE PV-DB2-MM                TO PV-EP251-CC-CRNT-MI.                
108200     MOVE PC-DOT-VALUE             TO PV-EP251-CC-CRNT-F-5.               
108300     MOVE PV-DB2-SS                TO PV-EP251-CC-CRNT-SS.                
108400                                                                          
108500*2000-EXIT.                                                               
108600 EJECT.                                                                   
108700                                                                          
108800*****************************************************************         
108900* 2100-SETUP-ORACLE-CONNECTION                                  *         
109000*    SET UP TO CONNECT TO ORACLE DATABASE                       *         
109100*                                                               *         
109200* FOR LOGGING INTO ORACLE DATABASE CURRENTLY THE LOGIN PARAMETER*         
109300* ARE INPUT VIA THE SYSIN CONTROL CARD                          *         
109400* IF USERNAME-LEN IS ZERO, THE LOGIN ID IS MISSING FROM SYSIN   *         
109500* THEREFORE IT IS A PRODUCTION RUN WHERE THE LOGIN INFO COMES   *         
109600* FROM THE JCL.                                                 *         
109700*****************************************************************         
109800 2100-SETUP-ORACLE-CONNECTION.                                            
109900     MOVE '2100-SETUP-ORACLE-CONNECTION  '                                
110000       TO PV-PARAGRAPH-NAME.                                              
110100                                                                          
110200     READ ORACLE-LOGIN-FILE INTO CC-ORACLE-CONNECTION                     
110210        AT END                                                            
110220           SET ORACLE-LOGIN-EOF TO TRUE.                                  
110300     CLOSE ORACLE-LOGIN-FILE.                                             
110301                                                                          
110310     IF  ((ORACLE-LOGIN-EOF)                                              
110320           OR ((CC-ORACLE-CONNECTION = SPACES)))                          
110330           DISPLAY '** ABEND **'                                          
110340           DISPLAY ' ORACLE LOGON NOT PROVIDED'                           
110350           MOVE    '2100-SET-UP-FOR-ORACLE'  TO PV-PARAGRAPH-NAME         
110360           PERFORM 9200-ORA-ERROR                                         
110370     END-IF.                                                              
110400                                                                          
113100     PERFORM 3200-CONNECT-TO-ORACLE.                                      
113200                                                                          
113300*2100-EXIT.                                                               
113400 EJECT.                                                                   
113500                                                                          
113600*****************************************************************         
113700* 2200-POPULATE-BTN-TBL.                                        *         
113800*    POPULATE THE INTERNAL BUTTON INFO TABLE                    *         
113900*****************************************************************         
114000 2200-POPULATE-BTN-TBL.                                                   
114100     MOVE '2200-POPULATE-BTN-TBL         '                                
114200       TO PV-PARAGRAPH-NAME.                                              
114300                                                                          
114400     PERFORM 7100-DEC-OPEN-BTN-CURSOR.                                    
114500                                                                          
114600     SET PS-NOT-END-OF-FETCH-BTN                                          
114700       TO TRUE.                                                           
114800                                                                          
114900     SET PV-BTN-INDEX                                                     
115000       TO 1.                                                              
115100                                                                          
115200     PERFORM 3300-PROCESS-BTN-CSR                                         
115300       UNTIL PS-END-OF-FETCH-BTN.                                         
115400                                                                          
115500     PERFORM 7200-CLOSE-BTN-CURSOR.                                       
115600                                                                          
115700*2200-EXIT.                                                               
115800 EJECT.                                                                   
115900                                                                          
116000*****************************************************************         
116100* 2300-FIRST-PASS-FOR-CHANGES.                                  *         
116200*  THIS PARAGRAPH DEFINES, FETCHES, AND CLOSES CURSOR OFRCSR1   *         
116300*    THIS PROCESSING OF THIS CURSOR WILL BUILD A TABLE          *         
116400*    OF UNIQUE EVENT-ID, OFFER-ID, AND OFFER-SECT-TYP-IDS.      *         
116500*    WHERE ONE OR MORE OF THE OFFER COMPONENT FILES HAVE A      *         
116600*    TIMESTAMP WITHIN THE SELECTION RANGE.                      *         
116700*                                                               *         
116800*****************************************************************         
116900 2300-FIRST-PASS-FOR-CHANGES.                                             
117000     MOVE '2300-FIRST-PASS-FOR-CHANGES   '                                
117100       TO PV-PARAGRAPH-NAME.                                              
117200                                                                          
117300     PERFORM 7300-DEC-OPEN-OFRCSR1.                                       
117400                                                                          
117500     SET PS-NOT-END-OF-FETCH-OFR TO TRUE.                                 
117600                                                                          
117700     PERFORM 3500-PROCESS-OFRCSR1                                         
117800       UNTIL PS-END-OF-FETCH-OFR.                                         
117900                                                                          
118000     PERFORM 7400-CLOSE-OFRCSR1.                                          
118100                                                                          
118200*2300-EXIT.                                                               
118300 EJECT.                                                                   
118400                                                                          
118500*****************************************************************         
118600* 2400-PROCESS-OFFERS.                                          *         
118700*  THIS PARAGRAPH                                                         
118800*****************************************************************         
118900 2400-PROCESS-OFFERS.                                                     
119000                                                                          
119100     MOVE '2400-PROCESS-OFFERS           '                                
119200       TO PV-PARAGRAPH-NAME.                                              
119300     MOVE SPACES TO PV-VAR-CSR1.                                          
119400     MOVE  PV-OFR-RECORD(PV-OFR-SUB)  TO PV-VAR-CSR1.                     
119500                                                                          
119600*---  IF THE CHANGE IS ONLY IN MERCHANDISE RECORD THEN FORMAT THE         
119700*---  DATA FROM THE VALUES IN THE TABLE.                                  
119800*---                                                                      
119900     IF PV-MDSE-REC-ONLY-C1                                               
120000         PERFORM 5300-BUILD-EPRD205-MDSE-ONLY                             
120100     ELSE                                                                 
120200         PERFORM 2600-FETCH-FRMT-OFFER                                    
120300     END-IF.                                                              
120400                                                                          
120500     ADD 1  TO PV-OFR-SUB.                                                
120600                                                                          
120700*2400-EXIT.                                                               
120800 EJECT.                                                                   
120900                                                                          
121000*****************************************************************         
121100* 2600-FETCH-FRMT-OFFER                                                   
121200* EXTRACT THE COMLPETE SET OF OFFER FROM THE DATABASE FOR THE             
121300* OFR_ID SAVED IN THE TABLE                                               
121400*****************************************************************         
121500 2600-FETCH-FRMT-OFFER.                                                   
121600                                                                          
121700     MOVE '2600-FETCH-FRMT-OFFER         '                                
121800       TO PV-PARAGRAPH-NAME.                                              
121900                                                                          
122000     PERFORM 7500-DEC-OPEN-OFFER-CURSOR.                                  
122100     SET PS-NOT-END-OF-FETCH-OFR TO TRUE.                                 
122200                                                                          
122300     PERFORM 3600-CORR-OFFER-CURSOR                                       
122400       UNTIL PS-END-OF-FETCH-OFR.                                         
122500                                                                          
122600     PERFORM 7600-CLOSE-OFFER-CURSOR.                                     
122700                                                                          
122800*2600-EXIT.                                                               
122900 EJECT.                                                                   
123000                                                                          
123100*****************************************************************         
123200* 2700-PROCESS-STORE-CURSOR-2.                                  *         
123300* THIS PARAGRAPH EXTRACT THE MODIFICATIONS DONE TO THE                    
123400* OMT_OFR_LOC_GP TABLE                                                    
123500*****************************************************************         
123600 2700-PROCESS-STORE-CURSOR-2.                                             
123700     MOVE '2700-PROCESS-STORE-CURSOR-2   '                                
123800       TO PV-PARAGRAPH-NAME.                                              
123900                                                                          
124000     MOVE SPACES      TO PV-OFR-TRGR-TYP-CDE-C1.                          
124100     SET EPWS208-OFFER-DATA-IN-RANGE TO TRUE.                             
124200     PERFORM 7900-FETCH-STORE-CURSOR-2.                                   
124300                                                                          
124400     EVALUATE TRUE                                                        
124500         WHEN SQLCODE IS EQUAL TO ZERO                                    
124600             ADD 1                                                        
124700               TO PV-STRCRSR2-READ                                        
124800             PERFORM EP208-1000-DETERMNE-FLD-OVRFLW                       
124900             IF EPWS208-OFFER-DATA-IN-RANGE                               
125000                 PERFORM 3700-ANALYZE-STORE-DATA                          
125100             END-IF                                                       
125200         WHEN SQLCODE IS EQUAL TO 1403                                    
125300             SET PS-END-OF-FETCH-OFR                                      
125400               TO TRUE                                                    
125500         WHEN OTHER                                                       
125600             MOVE 'FETCH DATA FROM OFRCRSR '                              
125700               TO PV-ORA-OPERATION                                        
125800             PERFORM 9200-ORA-ERROR                                       
125900     END-EVALUATE.                                                        
126000                                                                          
126100*2700-EXIT.                                                               
126200 EJECT.                                                                   
126300                                                                          
126400*****************************************************************         
126500* 3000-VALIDATE-DATE-INPUT.                                     *         
126600*     VALIDATE THE DATE INPUT BY CALLING THE KOHLS CALENDAR     *         
126700*     ROUTINE AND CHECKING TO SEE WHETHER IT THROWS AN ERROR.   *         
126800*****************************************************************         
126900 3000-VALIDATE-DATE-INPUT.                                                
127000     MOVE '3000-VALIDATE-DATE-INPUT      '                                
127100       TO PV-PARAGRAPH-NAME.                                              
127200                                                                          
127300     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
127400     SET DPG51-ACTUAL-CALENDAR-ONLY  TO TRUE.                             
127500     SET DPG52-LK-DTE-ISO-FMT  TO TRUE.                                   
127600                                                                          
127700     MOVE PV-WORK-DATE TO DPG52-LK-DATE-INPUT.                            
127800                                                                          
127900     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
128000       USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
128100                                                                          
128200     EVALUATE TRUE                                                        
128300       WHEN DPG54-NO-ERROR                                                
128400         CONTINUE                                                         
128500       WHEN OTHER                                                         
128600         DISPLAY ' '                                                      
128700         DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'                
128800         DISPLAY '** CALLED BY 3000-VALIDATE-DATE-INPUT'                  
128900         DISPLAY '** ERROR MESSAGE = '                                    
129000                           DPG54-ERROR-MESSAGE                            
129100         DISPLAY '** INPUT DATE VALIDATED WAS:           '                
129200         DISPLAY '** DPG52-LK-DATE-INPUT : ' DPG52-LK-DATE-INPUT          
129300         MOVE '3000-VALIDATE-DATE-INPUT '                                 
129400           TO PV-PARAGRAPH-NAME                                           
129500         MOVE 'INVALID DATE PASSED IN CC'                                 
129600           TO PV-OPERATION-MSG-1                                          
129700         MOVE DPG54-ERROR-MESSAGE                                         
129800           TO PV-OPERATION-MSG-2                                          
129900         SET AC-4019-CAL-SUB-ERROR                                        
130000            TO TRUE                                                       
130100         PERFORM 9100-ABEND                                               
130200     END-EVALUATE.                                                        
130300                                                                          
130400*3000-EXIT.                                                               
130500 EJECT.                                                                   
130600                                                                          
130700*****************************************************************         
130800* 3100-VALIDATE-TIME-INPUT.                                     *         
130900*****************************************************************         
131000 3100-VALIDATE-TIME-INPUT.                                                
131100                                                                          
131200     SET PS-TIME-NOT-VALID  TO TRUE.                                      
131300                                                                          
131400     IF PV-WORK-HH >= ZERO                                                
131500       AND PV-WORK-HH < 24                                                
131600         AND PV-WORK-MM >= 0                                              
131700           AND PV-WORK-MM < 60                                            
131800             AND PV-WORK-SS >= 0                                          
131900               AND PV-WORK-SS < 60                                        
132000                 SET PS-TIME-VALID  TO TRUE                               
132100     ELSE                                                                 
132200       SET PS-TIME-NOT-VALID  TO TRUE                                     
132300     END-IF.                                                              
132400                                                                          
132500*3100-EXIT.                                                               
132600 EJECT.                                                                   
132700                                                                          
132800*****************************************************************         
132900*     CONNECT TO THE ORACLE DATABASE.                           *         
133000*****************************************************************         
133100 3200-CONNECT-TO-ORACLE.                                                  
133200     MOVE '3200-CONNECT-TO-ORACLE        '                                
133300       TO PV-PARAGRAPH-NAME.                                              
133400                                                                          
133500     IF CC-AUTO-LOGON                                                     
133600        DISPLAY PC-PROGRAM-NAME ' - AUTO LOGON TO ORACLE'                 
133700        EXEC SQL                                                          
133800             CONNECT :PC-AUTO-LOGON                                       
133900        END-EXEC                                                          
134000     ELSE                                                                 
134100        INITIALIZE PV-ORACLE-USERID-LEN                                   
134200                   PV-ORACLE-PASSWORD-LEN                                 
134300        INSPECT CC-ORACLE-USERID                                          
134400                TALLYING PV-ORACLE-USERID-LEN                             
134500                FOR CHARACTERS BEFORE INITIAL SPACE                       
134600        INSPECT CC-ORACLE-PASSWORD                                        
134700                TALLYING PV-ORACLE-PASSWORD-LEN                           
134800                FOR CHARACTERS BEFORE INITIAL SPACE                       
134900        MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                 
135000                USERNAME-ARR                                              
135100        MOVE PV-ORACLE-USERID-LEN TO USERNAME-LEN                         
135200        MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
135300                PASSWD-ARR                                                
135400        MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                         
135500        MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
135600             PASSWD-ARR                                                   
135610        MOVE PV-ORACLE-PASSWORD-LEN TO PASSWD-LEN                         
135620        MOVE CC-ORACLE-USERID (1:PV-ORACLE-USERID-LEN) TO                 
135630             USERNAME-ARR                                                 
135640        MOVE PV-ORACLE-USERID-LEN  TO USERNAME-LEN                        
135650        MOVE CC-ORACLE-PASSWORD (1:PV-ORACLE-PASSWORD-LEN) TO             
135660             PASSWD-ARR                                                   
135670        MOVE PV-ORACLE-PASSWORD-LEN  TO PASSWD-LEN                        
135680        DISPLAY PC-PROGRAM-NAME  ' - LOGON TO ORACLE AS USER '            
135690             USERNAME-ARR                                                 
135691        EXEC SQL                                                          
135692             CONNECT :USERNAME IDENTIFIED BY :PASSWD                      
135693        END-EXEC                                                          
135694        INITIALIZE CC-ORACLE-PASSWORD                                     
135695                   PASSWD                                                 
135696     END-IF                                                               
135697                                                                          
135698     IF (SQLCODE NOT = 0)                                                 
135699         DISPLAY PV-PARAGRAPH-NAME                                        
135700         PERFORM 9200-ORA-ERROR                                           
135701     END-IF.                                                              
135702                                                                          
135710                                                                          
135800*3200-EXIT.                                                               
135900 EJECT.                                                                   
136000                                                                          
136100*****************************************************************         
136200* 3300-PROCESS-BTN-CSR.                                         *         
136300*   PROCESS THE BTNCRSR CURSOR AND POPULATE THE PROGRAM TABLE   *         
136400*   (PV-OMT-POS-BTN)                                            *         
136500*****************************************************************         
136600 3300-PROCESS-BTN-CSR.                                                    
136700     MOVE '3300-PROCESS-BTN-CSR          '                                
136800       TO PV-PARAGRAPH-NAME.                                              
136900                                                                          
137000     SET EPWS208-OFFER-DATA-IN-RANGE  TO TRUE.                            
137100                                                                          
137200     PERFORM 7150-FETCH-BTN-CSR.                                          
137300                                                                          
137400     EVALUATE TRUE                                                        
137500        WHEN SQLCODE IS EQUAL TO ZEROS                                    
137600           IF PV-POS-RLTV-NUM-NULL-IND = -1                               
137700               SET EPWS208-OFFER-DATA-IN-RANGE  TO TRUE                   
137800           ELSE                                                           
137900               PERFORM EP208-1100-DETERMNE-POS-OVRFLW                     
138000           END-IF                                                         
138100           IF EPWS208-OFFER-DATA-IN-RANGE                                 
138200               MOVE POS-BTN-CDE IN OMT00006                               
138300                   TO PV-POS-BTN-CDE(PV-BTN-INDEX)                        
138400               MOVE POS-BTN-TXT IN OMT00006                               
138500                   TO PV-POS-BTN-TEXT(PV-BTN-INDEX)                       
138600               IF PV-POS-RLTV-NUM-NULL-IND = -1                           
138700                   MOVE PC-NULL-VALUE                                     
138800                     TO PV-POS-BTN-RLTV-SEQ-NBR-X(PV-BTN-INDEX)           
138900               ELSE                                                       
139000                   MOVE POS-BTN-RLTV-SEQ-NBR IN OMT00006                  
139100                       TO PV-POS-BTN-RLTV-SEQ-NBR(PV-BTN-INDEX)           
139200               END-IF                                                     
139300               SET PV-BTN-INDEX  UP BY 1                                  
139400           END-IF                                                         
139500        WHEN SQLCODE IS EQUAL TO 1403                                     
139600           SET PS-END-OF-FETCH-BTN TO TRUE                                
139700        WHEN OTHER                                                        
139800           MOVE 'FETCH DATA FROM BTNCRSR       '                          
139900             TO PV-ORA-OPERATION                                          
140000           PERFORM 9200-ORA-ERROR                                         
140100     END-EVALUATE.                                                        
140200                                                                          
140300     IF PV-BTN-INDEX > PC-BTN-TBL-THRSHOLD                                
140400        DISPLAY 'LIMIT OF POST-BTN-CURSOR REACHED'                        
140500        MOVE '3300-PROCESS-BTN-CSR ' TO                                   
140600                               PV-PARAGRAPH-NAME                          
140700        MOVE 'TABLE LIMIT REACHED FOR POS-BTN-ARRAY' TO                   
140800                             PV-OPERATION-MSG-1                           
140900        SET AC-4004-TABLE-OVERFLOW-ERROR   TO TRUE                        
141000        PERFORM 9100-ABEND                                                
141100     END-IF.                                                              
141200                                                                          
141300                                                                          
141400*3300-EXIT.                                                               
141500 EJECT.                                                                   
141600                                                                          
141700*****************************************************************         
141800* 3500-PROCESS-OFRCSR1.                                         *         
141900*  THIS PARAGRAPH FETCHES THE OFRCSR1 AND IF SQLCODE IS 0, IT             
142000*  CALLS PARAGRAPH 3550 TO DETERMINE IF THE OFFER NEEDS TO BE             
142100*  EXTRACTED FURTHER AND SENT TO POS STORE SERVER DATABASE.               
142200*****************************************************************         
142300 3500-PROCESS-OFRCSR1.                                                    
142400     MOVE '3500-PROCESS-OFRCSR1          '                                
142500       TO PV-PARAGRAPH-NAME.                                              
142600                                                                          
142700     MOVE SPACES      TO PV-OFR-TRGR-TYP-CDE-C1.                          
142800     PERFORM 7350-FETCH-OFRCSR1.                                          
142900                                                                          
143000     EVALUATE TRUE                                                        
143100         WHEN SQLCODE IS EQUAL TO ZERO                                    
143200             ADD 1 TO PV-OFFER-CURSOR-READ                                
143300             PERFORM 3550-DETERMINE-REC-INRANGE                           
143400         WHEN SQLCODE IS EQUAL TO 1403                                    
143500             SET PS-END-OF-FETCH-OFR TO TRUE                              
143600         WHEN OTHER                                                       
143700             MOVE 'FETCH DATA FROM OFRCRSR' TO PV-ORA-OPERATION           
143800             PERFORM 9200-ORA-ERROR                                       
143900     END-EVALUATE.                                                        
144000                                                                          
144100*3500-EXIT.                                                               
144200 EJECT                                                                    
144300                                                                          
144400*****************************************************************         
144500* 3550-DETERMINE-REC-INRANGE                                   *          
144600* THIS PARAGARPH DETERMINES WHICH TABLE AMONG THE FOLLOWING               
144700* OMT_OFR, OMT_OFR_SECT, OMT_OFR_SECT_BWP, OMT_MDSE_SET , OMT_EVNT        
144800* WAS MODIFIED, AND THEN DETERMIES THE TRIGGER TYPE AS A RESULTANT        
144900* OF THAT CHANGE.                                                         
145000*****************************************************************         
145100 3550-DETERMINE-REC-INRANGE.                                              
145200                                                                          
145300     MOVE '3550-DETERMINE-REC-INRANGE    '                                
145400       TO PV-PARAGRAPH-NAME.                                              
145500                                                                          
145600     MOVE EFF-BEG-TMST IN OMT00002 TO PV-EFF-BEG-TMST.                    
145700                                                                          
145800     SET PV-ALL-RECORDS-C1  TO TRUE.                                      
145900                                                                          
146000     EVALUATE TRUE                                                        
146100                                                                          
146200          WHEN (LAST-UPD-TMST IN OMT00001 >=                              
146300                                     PV-EP251-CC-PREV-TIMESTAMP           
146400                AND  LAST-UPD-TMST IN OMT00001 <=                         
146500                                     PV-EP251-CC-CRNT-TIMESTAMP)          
146600                 SET PS-OFR-IN-RANGE TO TRUE                              
146700                                                                          
146800                 PERFORM 3800-OFR-TRIGGER-TYPE                            
146900                                                                          
147000          WHEN (LAST-UPD-TMST IN OMT00009 >=                              
147100                                     PV-EP251-CC-PREV-TIMESTAMP           
147200                AND  LAST-UPD-TMST IN OMT00009 <=                         
147300                                     PV-EP251-CC-CRNT-TIMESTAMP)          
147400                 SET PS-OFR-IN-RANGE TO TRUE                              
147500                                                                          
147600                 PERFORM 3750-EVNT-TRIGGER-TYPE                           
147700                                                                          
147800          WHEN  (LAST-UPD-TMST IN OMT00002 >=                             
147900                                     PV-EP251-CC-PREV-TIMESTAMP           
148000                AND LAST-UPD-TMST IN OMT00002 <=                          
148100                                     PV-EP251-CC-CRNT-TIMESTAMP)          
148200                 SET PS-SECT-IN-RANGE  TO TRUE                            
148300                                                                          
148400                 PERFORM 3850-SECT-TRIGGER-TYPE                           
148500                                                                          
148600          WHEN (LAST-UPD-TMST IN OMT00003 >=                              
148700                                     PV-EP251-CC-PREV-TIMESTAMP           
148800                AND  LAST-UPD-TMST IN OMT00003 <=                         
148900                                     PV-EP251-CC-CRNT-TIMESTAMP)          
149000                 SET PS-BWP-IN-RANGE TO TRUE                              
149100                                                                          
149200                 PERFORM 3900-BWP-TRIGGER-TYPE                            
149300                                                                          
149400          WHEN (LAST-UPD-TMST IN OMT00005 >=                              
149500                                     PV-EP251-CC-PREV-TIMESTAMP           
149600                AND  LAST-UPD-TMST IN OMT00005 <=                         
149700                                     PV-EP251-CC-CRNT-TIMESTAMP)          
149800                 SET PS-MDSE-IN-RANGE TO TRUE                             
149900                                                                          
150000                 PERFORM 3950-MDSE-TRIGGER-TYPE                           
150100                                                                          
150200     END-EVALUATE.                                                        
150300                                                                          
150400*---  MOVE THE VARIABLES FROM DATABASE FIELDS TO TEMPORARY ---*           
150500*---  VARIABLES BEFORE CALLING 5000 TO LOAD THE TABLES     ---*           
150600                                                                          
150700     MOVE EVNT-ID IN OMT00001          TO PV-EVNT-ID.                     
150800     MOVE OFR-ID  IN OMT00001          TO PV-OFR-ID.                      
150900     MOVE OFR-SECT-TYP-CDE IN OMT00002 TO PV-OFR-SECT-TYP-CDE.            
151000     MOVE OFR-SECT-BWP-ID  IN OMT00003 TO PV-OFR-SECT-BWP-ID.             
151100     MOVE MDSE-SET-ID  IN OMT00005     TO PV-MDSE-SET-ID.                 
151200                                                                          
151300     EVALUATE TRUE                                                        
151400         WHEN ((PV-OFR-TBL-CHG-TYPE-C1 = 'A') AND                         
151500              (PV-OFR-TRGR-TYP-CDE-C1 NOT = ' '))                         
151600              PERFORM 5000-TABLE-THIS-OFR                                 
151700         WHEN ((PV-OFR-TBL-CHG-TYPE-C1 = 'M')  AND                        
151800              (PV-OFR-TRGR-TYP-CDE-C1 NOT = ' '))                         
151900              PERFORM 5100-TABLE-THIS-MDSE                                
152000         WHEN OTHER                                                       
152100              CONTINUE                                                    
152200     END-EVALUATE.                                                        
152300                                                                          
152400*3550-EXIT.                                                               
152500 EJECT                                                                    
152600                                                                          
152700*****************************************************************         
152800* 3600-CORR-OFFER-CURSOR.                                       *         
152900* THIS PARAGRAPH FETCHES THE RECORDS FROM THE OFFER TABLE FOR             
153000* A GIVEN OFFER ID. THE OFFER HAS BEEN MODIFIED AND NEEDS TO BE           
153100* EXTRACTED AND SENT OVER TO POS STORE SERVER DATABASE.                   
153200*****************************************************************         
153300 3600-CORR-OFFER-CURSOR.                                                  
153400     MOVE '3600-CORR-OFFER-CURSOR        '                                
153500       TO PV-PARAGRAPH-NAME.                                              
153600                                                                          
153700     SET EPWS208-OFFER-DATA-IN-RANGE  TO TRUE.                            
153800                                                                          
153900     PERFORM 7550-FETCH-OFFER-CORR-CURSOR.                                
154000                                                                          
154100     EVALUATE TRUE                                                        
154200         WHEN SQLCODE IS EQUAL TO ZERO                                    
154300             ADD 1 TO PV-OFFER-CURSOR-READ                                
154400             PERFORM EP208-1000-DETERMNE-FLD-OVRFLW                       
154500             IF EPWS208-OFFER-DATA-IN-RANGE                               
154600                PERFORM 5400-BUILD-EPRD205-RECORDS                        
154700             END-IF                                                       
154800         WHEN SQLCODE IS EQUAL TO 1403                                    
154900             SET PS-END-OF-FETCH-OFR TO TRUE                              
155000         WHEN OTHER                                                       
155100             MOVE 'FETCH DATA FROM OFRCRSR' TO PV-ORA-OPERATION           
155200             PERFORM 9200-ORA-ERROR                                       
155300     END-EVALUATE.                                                        
155400                                                                          
155500*3600-EXIT.                                                               
155600 EJECT.                                                                   
155700                                                                          
155800*****************************************************************         
155900* 3700-ANALYZE-STORE-DATA.                                      *         
156000*****************************************************************         
156100 3700-ANALYZE-STORE-DATA.                                                 
156200     MOVE '3700-ANALYZE-STORE-DATA       '                                
156300       TO PV-PARAGRAPH-NAME.                                              
156400                                                                          
156500     IF STR-GP-TYP-CDE IN OMT00004  = PV-STR-GP-TYP-CDE AND               
156600        STR-GP-ID      IN OMT00004  = PV-STR-GP-ID                        
156700        CONTINUE                                                          
156800     ELSE                                                                 
156900        MOVE 0  TO PV-OFR-ID-A                                            
157000                   PV-EVNT-ID-A                                           
157100                   PV-OFR-SECT-BWP-ID-A                                   
157200                   PV-MDSE-SET-ID-A                                       
157300     END-IF.                                                              
157400                                                                          
157500     EVALUATE TRUE                                                        
157600         WHEN LOC-DLT-IND IN OMT00004 = 'Y'                               
157700           AND IE-IND IN OMT00004 = 'N'                                   
157800             PERFORM 4100-Y-N-PROCESS                                     
157900         WHEN LOC-DLT-IND IN OMT00004 = 'Y'                               
158000           AND IE-IND IN OMT00004 = 'Y'                                   
158100             PERFORM 4200-Y-Y-PROCESS                                     
158200         WHEN LOC-DLT-IND IN OMT00004 = 'N'                               
158300           AND IE-IND IN OMT00004 = 'N'                                   
158400             PERFORM 4300-N-N-PROCESS                                     
158500         WHEN LOC-DLT-IND IN OMT00004 = 'N'                               
158600           AND IE-IND IN OMT00004 = 'Y'                                   
158700             PERFORM 4400-N-Y-PROCESS                                     
158800         WHEN OTHER                                                       
158900             CONTINUE                                                     
159000     END-EVALUATE.                                                        
159100                                                                          
159200     MOVE STR-GP-TYP-CDE IN OMT00004 TO PV-STR-GP-TYP-CDE.                
159300     MOVE STR-GP-ID      IN OMT00004 TO PV-STR-GP-ID.                     
159400                                                                          
159500*3700-EXIT.                                                               
159600 EJECT.                                                                   
159700                                                                          
159800*****************************************************************         
159900* 3750-EVNT-TRIGGER-TYPE                                                  
160000* THIS PARAGARPH DETERMINES THE TRIGGER TYPE FOR THE OFFER.               
160100*****************************************************************         
160200 3750-EVNT-TRIGGER-TYPE.                                                  
160300                                                                          
160400     MOVE '3750-EVNT-TRIGGER-TYPE        '                                
160500       TO PV-PARAGRAPH-NAME.                                              
160600                                                                          
160700     IF PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                           
160800          AND OFR-STAT-CDE IN OMT00001 = 'A'                              
160900            MOVE PC-ADD-TRIGGER    TO PV-OFR-TRGR-TYP-CDE-C1              
161000     END-IF.                                                              
161100                                                                          
161200*3750-EXIT.                                                               
161300 EJECT.                                                                   
161400                                                                          
161500*****************************************************************         
161600* 3800-OFR-TRIGGER-TYPE                                                   
161700* THIS PARAGARPH DETERMINES THE TRIGGER TYPE FOR THE OFFER.               
161800*****************************************************************         
161900 3800-OFR-TRIGGER-TYPE.                                                   
162000                                                                          
162100     MOVE '3800-OFR-TRIGGER-TYPE         '                                
162200       TO PV-PARAGRAPH-NAME.                                              
162300                                                                          
162400     IF LAST-UPD-TMST IN OMT00001 = CRE-TMST IN OMT00001                  
162500       IF  PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                        
162600          AND OFR-STAT-CDE IN OMT00001 = 'A'                              
162700             MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                
162800       END-IF                                                             
162900     END-IF.                                                              
163000                                                                          
163100     IF LAST-UPD-TMST IN OMT00001 > CRE-TMST IN OMT00001                  
163200       IF PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                         
163300          AND OFR-STAT-CDE IN OMT00001 = 'A'                              
163400            MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                 
163500       ELSE                                                               
163600         IF PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                       
163700            AND OFR-STAT-CDE IN OMT00001 = 'C'                            
163800               MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1              
163900         END-IF                                                           
164000       END-IF                                                             
164100     END-IF.                                                              
164200                                                                          
164300     IF (LAST-UPD-TMST IN OMT00002 >= PV-EP251-CC-PREV-TIMESTAMP          
164400     AND LAST-UPD-TMST IN OMT00002 <= PV-EP251-CC-CRNT-TIMESTAMP)         
164500        IF PV-EFF-BEG-DATE > EP252-CC-FUTURE-DATE                         
164600            MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                 
164700        END-IF                                                            
164800     END-IF.                                                              
164900                                                                          
165000*3800-EXIT.                                                               
165100 EJECT.                                                                   
165200                                                                          
165300*****************************************************************         
165400* 3850-SECT-TRIGGER-TYPE                                                  
165500* THIS PARAGARPH DETERMINES THE TRIGGER TYPE FOR THE OFFER.               
165600* DUE T0 CHANGES TO THE OMT_OFR_SECT TABLE                                
165700*****************************************************************         
165800 3850-SECT-TRIGGER-TYPE.                                                  
165900                                                                          
166000     MOVE '3850-SECT-TRIGGER-TYPE        '                                
166100       TO PV-PARAGRAPH-NAME.                                              
166200                                                                          
166300     IF PV-EFF-BEG-DATE > EP252-CC-FUTURE-DATE                            
166400        AND OFR-STAT-CDE IN OMT00001 = 'A'                                
166500        MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                     
166600     ELSE                                                                 
166700       IF PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                         
166800        AND OFR-STAT-CDE IN OMT00001 = 'A'                                
166900         MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                    
167000       END-IF                                                             
167100     END-IF.                                                              
167200                                                                          
167300*3850-EXIT.                                                               
167400 EJECT.                                                                   
167500                                                                          
167600*****************************************************************         
167700* 3900-BWP-TRIGGER-TYPE                                                   
167800* THIS PARAGARPH DETERMINES THE TRIGGER TYPE FOR THE OFFER.               
167900* DUE T0 CHANGES TO THE OMT_OFR_SECT_BWP TABLE                            
168000*****************************************************************         
168100 3900-BWP-TRIGGER-TYPE.                                                   
168200                                                                          
168300     MOVE '3900-BWP-TRIGGER-TYPE         '                                
168400       TO PV-PARAGRAPH-NAME.                                              
168500                                                                          
168600*--> THE OLD WAY OF DOING THIS PART.... COMMENTING OUT...                 
168700*    IF PV-EFF-BEG-DATE > EP252-CC-FUTURE-DATE                            
168800*       CONTINUE                                                          
168900*    ELSE                                                                 
169000*      IF PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE                         
169100*       AND OFR-STAT-CDE IN OMT00001 = 'A'                                
169200*        MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                    
169300*      END-IF                                                             
169400*    END-IF.                                                              
169500     EVALUATE TRUE                                                        
169600        WHEN (PV-EFF-BEG-DATE > EP252-CC-FUTURE-DATE)                     
169700             CONTINUE                                                     
169800        WHEN ((PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE)                   
169900         AND (BWP-DLT-IND IN OMT00003 = 'Y')                              
170000         AND (OFR-STAT-CDE IN OMT00001 = 'A'))                            
170100             MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                
170200             SET PV-MDSE-REC-ONLY-C1 TO TRUE                              
170300        WHEN ((PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE)                   
170400         AND (BWP-DLT-IND IN OMT00003 = 'N')                              
170500         AND (OFR-STAT-CDE IN OMT00001 = 'A'))                            
170600             MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                
170700     END-EVALUATE.                                                        
170800                                                                          
170900*3900-EXIT.                                                               
171000 EJECT.                                                                   
171100                                                                          
171200*****************************************************************         
171300* 3950-MDSE-TRIGGER-TYPE                                                  
171400* THIS PARAGARPH DETERMINES THE TRIGGER TYPE FOR THE OFFER.               
171500* DUE T0 CHANGES TO THE OMT_MDSE_SET TABLE                                
171600*****************************************************************         
171700 3950-MDSE-TRIGGER-TYPE.                                                  
171800                                                                          
171900     MOVE '3950-MDSE-TRIGGER-TYPE        '                                
172000       TO PV-PARAGRAPH-NAME.                                              
172100                                                                          
172200     EVALUATE TRUE                                                        
172300        WHEN (PV-EFF-BEG-DATE > EP252-CC-FUTURE-DATE)                     
172400             CONTINUE                                                     
172500        WHEN ((PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE)                   
172600         AND (MDSE-DLT-IND IN OMT00005 = 'Y')                             
172700         AND (OFR-STAT-CDE IN OMT00001 = 'A'))                            
172800             MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                
172900             SET PV-MDSE-REC-ONLY-C1 TO TRUE                              
173000        WHEN ((PV-EFF-BEG-DATE <= EP252-CC-FUTURE-DATE)                   
173100         AND (MDSE-DLT-IND IN OMT00005 = 'N')                             
173200         AND (OFR-STAT-CDE IN OMT00001 = 'A'))                            
173300             MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1                
173400     END-EVALUATE.                                                        
173500                                                                          
173600*3950-EXIT.                                                               
173700 EJECT.                                                                   
173800                                                                          
173900*****************************************************************         
174000* 4100-Y-N-PROCESS.                                             *         
174100*****************************************************************         
174200 4100-Y-N-PROCESS.                                                        
174300     MOVE '4100-Y-N-PROCESS              '                                
174400       TO PV-PARAGRAPH-NAME.                                              
174500                                                                          
174600     PERFORM 4500-CALC-DATES-FOR-STRCRS.                                  
174700                                                                          
174800     IF PV-OFFER-DATE <= EP252-CC-FUTURE-DATE                             
174900       AND PV-OFFER-DATE > PV-RANGE-BEGIN                                 
175000          PERFORM 5500-CHECK-FOR-PRIOR-SEND                               
175100          IF PS-STR-TBL-NF                                                
175200              PERFORM 5600-SET-A-IN-TRIGGER                               
175300              PERFORM 5700-BUILD-AND-SEND-OFFER                           
175400          END-IF                                                          
175500     END-IF.                                                              
175600                                                                          
175700*4100-EXIT.                                                               
175800 EJECT.                                                                   
175900                                                                          
176000*****************************************************************         
176100* 4200-Y-Y-PROCESS.                                             *         
176200*****************************************************************         
176300 4200-Y-Y-PROCESS.                                                        
176400     MOVE '4200-Y-Y-PROCESS              '                                
176500       TO PV-PARAGRAPH-NAME.                                              
176600                                                                          
176700     PERFORM 5800-SET-D-IN-TRIGGER.                                       
176800                                                                          
176900     PERFORM 5700-BUILD-AND-SEND-OFFER.                                   
177000                                                                          
177100*4200-EXIT.                                                               
177200 EJECT.                                                                   
177300                                                                          
177400*****************************************************************         
177500* 4300-N-N-PROCESS.                                            *          
177600*****************************************************************         
177700 4300-N-N-PROCESS.                                                        
177800     MOVE '4300-N-N-PROCESS              '                                
177900       TO PV-PARAGRAPH-NAME.                                              
178000                                                                          
178100     PERFORM 5800-SET-D-IN-TRIGGER.                                       
178200                                                                          
178300     PERFORM 5700-BUILD-AND-SEND-OFFER.                                   
178400                                                                          
178500*4300-EXIT.                                                               
178600 EJECT.                                                                   
178700                                                                          
178800*****************************************************************         
178900* 4400-N-Y-PROCESS.                                             *         
179000*****************************************************************         
179100 4400-N-Y-PROCESS.                                                        
179200     MOVE '4400-N-Y-PROCESS              '                                
179300       TO PV-PARAGRAPH-NAME.                                              
179400                                                                          
179500     PERFORM 4500-CALC-DATES-FOR-STRCRS.                                  
179600                                                                          
179700     IF PV-OFFER-DATE <= EP252-CC-FUTURE-DATE                             
179800       AND PV-OFFER-DATE > PV-RANGE-BEGIN                                 
179900          PERFORM 5500-CHECK-FOR-PRIOR-SEND                               
180000          IF PS-STR-TBL-NF                                                
180100              PERFORM 5600-SET-A-IN-TRIGGER                               
180200              PERFORM 5700-BUILD-AND-SEND-OFFER                           
180300          END-IF                                                          
180400     END-IF.                                                              
180500                                                                          
180600*4400-EXIT.                                                               
180700 EJECT.                                                                   
180800                                                                          
180900*****************************************************************         
181000* 4500-CALC-DATES-FOR-STRCRS.                              *              
181100*****************************************************************         
181200 4500-CALC-DATES-FOR-STRCRS.                                              
181300     MOVE '4500-CALC-DATES-FOR-STRCRS    '                                
181400       TO PV-PARAGRAPH-NAME.                                              
181500                                                                          
181600     MOVE EFF-BEG-TMST IN OMT00002  TO PV-WORK-ORA-FORMAT.                
181700                                                                          
181800     MOVE TOL-STRT-NMOFDA-QTY IN OMT00002 TO PV-WORK-DELTA.               
181900                                                                          
182000     PERFORM 5200-DECREMENT-DATE.                                         
182100                                                                          
182200     MOVE PV-WORK-DATE TO PV-OFFER-DATE.                                  
182300                                                                          
182400     MOVE FUNCTION CURRENT-DATE  TO PV-TODAY-DATE.                        
182500                                                                          
182600     MOVE PV-TODAY-DATE-YYYY  TO PV-WORK-YYYY.                            
182700     MOVE PV-TODAY-DATE-MO    TO PV-WORK-MO.                              
182800     MOVE PV-TODAY-DATE-DY    TO PV-WORK-DY.                              
182900     MOVE PC-DECR-DAYS-365    TO PV-WORK-DELTA.                           
183000                                                                          
183100     PERFORM 5200-DECREMENT-DATE.                                         
183200     MOVE PV-WORK-DATE  TO PV-RANGE-BEGIN.                                
183300                                                                          
183400*4500-EXIT.                                                               
183500 EJECT.                                                                   
183600*****************************************************************         
183700* 5000-TABLE-THIS-OFR                                           *         
183800* THIS PARAGRAPH WILL TABLE THE DATA IF THERE WAS A CHANGE IN             
183900* OMT_OFR, OMT_OFR_SECT OR OMT_OFR_SECT_BWP TABLES                        
184000*****************************************************************         
184100 5000-TABLE-THIS-OFR.                                                     
184200                                                                          
184300     SET PS-ENTRY-NOT-FOUND                                               
184400         PS-NOT-END-OF-TABLE                                              
184500       TO TRUE.                                                           
184600     PERFORM 6000-FIND-ENTRY                                              
184700       VARYING PV-OFR-SUB                                                 
184800         FROM 1 BY 1                                                      
184900            UNTIL PS-ENTRY-FOUND                                          
185000              OR PS-END-OF-TABLE.                                         
185100                                                                          
185200     IF PS-ENTRY-NOT-FOUND                                                
185300         SUBTRACT 1  FROM PV-OFR-SUB                                      
185400         MOVE PV-EVNT-ID  TO PV-OFR-EVENT-ID(PV-OFR-SUB)                  
185500         MOVE PV-OFR-ID   TO PV-OFR-OFFER-ID(PV-OFR-SUB)                  
185600         MOVE PV-OFR-SECT-TYP-CDE TO                                      
185700                          PV-OFR-OFR-SECT-TYP-CDE(PV-OFR-SUB)             
185800         MOVE PV-OFR-SECT-BWP-ID TO PV-OFR-BWP-ID(PV-OFR-SUB)             
185900         MOVE PV-MDSE-SET-ID  TO PV-OFR-MDSE-ID(PV-OFR-SUB)               
186000         MOVE PV-OFR-TRGR-TYP-CDE-C1 TO                                   
186100                          PV-OFR-TBL-TRGR-TYPE(PV-OFR-SUB)                
186200         MOVE PV-OFR-TBL-CHG-TYPE-C1 TO                                   
186300                          PV-OFR-TBL-CHG-TYPE(PV-OFR-SUB)                 
186400         ADD 1  TO PV-OFR-TBL-CNT                                         
186500         MOVE PV-OFR-SUB  TO PV-OFR-SUB-SAVE                              
186600     END-IF.                                                              
186700                                                                          
186800     IF PV-OFR-TBL-CNT > PC-OMT-OFR-TBL-MAX                               
186900        MOVE '5000-TABLE-THIS-OFR           '                             
187000                    TO PV-PARAGRAPH-NAME                                  
187100        MOVE 'TABLE LIMIT REACHED FOR PV-OMT-OFR-TBL' TO                  
187200                             PV-OPERATION-MSG-1                           
187300        DISPLAY ' CURRENT COUNT OF RECORD IN TABLE '                      
187400                    PV-OFR-TBL-CNT                                        
187500        DISPLAY ' MAXIMUM NUMBER ALLOWED IS 2000   '                      
187600        SET AC-4004-TABLE-OVERFLOW-ERROR   TO TRUE                        
187700        PERFORM 9100-ABEND                                                
187800     END-IF.                                                              
187900                                                                          
188000*5000-EXIT.                                                               
188100 EJECT.                                                                   
188200                                                                          
188300*****************************************************************         
188400* 5100-TABLE-THIS-MDSE                                          *         
188500* THIS PARAGRAPH WILL TABLE THE DATA IF THERE WAS A CHANGE ONLY           
188600* IN MERCHANDISE SET TABLE.                                               
188700*****************************************************************         
188800 5100-TABLE-THIS-MDSE.                                                    
188900     MOVE '5100-TABLE-THIS-MDSE          '                                
189000       TO PV-PARAGRAPH-NAME.                                              
189100                                                                          
189200     ADD 1 TO PV-OFR-SUB.                                                 
189300     MOVE PV-EVNT-ID  TO PV-OFR-EVENT-ID(PV-OFR-SUB).                     
189400     MOVE PV-OFR-ID    TO PV-OFR-OFFER-ID(PV-OFR-SUB).                    
189500     MOVE PV-OFR-SECT-TYP-CDE                                             
189600                          TO PV-OFR-OFR-SECT-TYP-CDE(PV-OFR-SUB).         
189700     MOVE PV-OFR-SECT-BWP-ID TO PV-OFR-BWP-ID(PV-OFR-SUB).                
189800     MOVE PV-MDSE-SET-ID  TO PV-OFR-MDSE-ID(PV-OFR-SUB).                  
189900     MOVE PV-OFR-TRGR-TYP-CDE-C1 TO                                       
190000                                PV-OFR-TBL-TRGR-TYPE(PV-OFR-SUB).         
190100     MOVE PV-OFR-TBL-CHG-TYPE-C1 TO                                       
190200                                PV-OFR-TBL-CHG-TYPE(PV-OFR-SUB).          
190300     ADD 1     TO PV-OFR-TBL-CNT.                                         
190400     MOVE PV-OFR-SUB        TO PV-OFR-SUB-SAVE.                           
190500                                                                          
190600*5100-EXIT.                                                               
190700 EJECT.                                                                   
190800                                                                          
190900*****************************************************************         
191000*  5200-DECREMENT-DATE                                          *         
191100*    THIS ROUTINE USES THE KOHL'S CALENDAR ROUTINE TO DECREMENT *         
191200*      THE INPUT DATE BY A REQUESTED AMOUNT.                    *         
191300*****************************************************************         
191400 5200-DECREMENT-DATE.                                                     
191500     MOVE '5200-DECREMENT-DATE           '                                
191600       TO PV-PARAGRAPH-NAME.                                              
191700                                                                          
191800     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
191900     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
192000       TO TRUE.                                                           
192100     SET DPG51-DECREMENT-DATE                                             
192200       TO TRUE.                                                           
192300     MOVE PV-WORK-DELTA                                                   
192400       TO DPG51-INCR-DECR-DAYS-9.                                         
192500     SET DPG52-LK-DTE-ISO-FMT                                             
192600       TO TRUE.                                                           
192700     MOVE PV-WORK-ORA-FORMAT-DATE                                         
192800       TO DPG52-LK-DATE-INPUT.                                            
192900                                                                          
193000     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
193100       USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
193200                                                                          
193300     EVALUATE TRUE                                                        
193400         WHEN DPG54-NO-ERROR                                              
193500             CONTINUE                                                     
193600         WHEN OTHER                                                       
193700             DISPLAY ' '                                                  
193800             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
193900             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
194000             DISPLAY '** DATE  INPUT TO CALENDAR ROUTINE  :  '            
194100             DISPLAY '** DPG52-LK-DATE-INPUT ' DPG52-LK-DATE-INPUT        
194200             DISPLAY '** DATE  OUTPUT FROM THE ROUTINE    :  '            
194300             DISPLAY '** DPG55-DB2-ISO-DATE-FMT '                         
194400                            DPG55-DB2-ISO-DATE-FMT                        
194500             MOVE '5200-DECREMENT-DATE'                                   
194600               TO PV-PARAGRAPH-NAME                                       
194700             MOVE 'ERROR CALCULATING ADJUSTED DATE'                       
194800               TO PV-OPERATION-MSG-1                                      
194900             SET AC-4019-CAL-SUB-ERROR                                    
195000               TO TRUE                                                    
195100             MOVE DPG54-ERROR-MESSAGE                                     
195200               TO PV-OPERATION-MSG-2                                      
195300             PERFORM 9100-ABEND                                           
195400     END-EVALUATE.                                                        
195500                                                                          
195600     MOVE DPG55-DB2-ISO-DATE-FMT  TO PV-WORK-DATE.                        
195700                                                                          
195800*5200-EXIT.                                                               
195900 EJECT.                                                                   
196000                                                                          
196100*****************************************************************         
196200* 5300-BUILD-EPRD205-MDSE-ONLY.                                 *         
196300*   CREATE THE APPROPRIATE EPRD205 MDSE RECORDS AND WRITE THEM  *         
196400*****************************************************************         
196500 5300-BUILD-EPRD205-MDSE-ONLY.                                            
196600                                                                          
196700     MOVE '5300-BUILD-EPRD205-MDSE-ONLY  '                                
196800       TO PV-PARAGRAPH-NAME.                                              
196900                                                                          
197000     SET EP205-FILE-TYPE-CHG TO TRUE.                                     
197100     MOVE PV-OFR-TRGR-TYP-CDE-C1 TO EP205-TRIGGER-TYPE.                   
197200     MOVE PV-OFR-ID  TO EP205-OFFER-ID.                                   
197300     MOVE PV-EVNT-ID TO EP205-EVENT-ID.                                   
197400     MOVE PV-OFR-SECT-TYP-CDE TO EP205-OFR-SECT-TYP-CDE.                  
197500     INITIALIZE EP205-VARIABLE-DATA.                                      
197600     SET  EP205-XST-OFR-MDSE TO TRUE.                                     
197700     MOVE PV-OFR-SECT-BWP-ID    TO EP205-OFR-SECT-BWP-ID.                 
197800     MOVE PV-MDSE-SET-ID       TO EP205-MDSE-SET-ID.                      
197900     MOVE '   '  TO EP205-MDSE-HIER-CDE.                                  
198000     MOVE PC-NULL-VALUE TO EP205-DEPT-NBR-X.                              
198100     MOVE PC-NULL-VALUE  TO EP205-MAJ-CL-NBR-X.                           
198200     MOVE PC-NULL-VALUE  TO EP205-SUB-CL-NBR-X.                           
198300     MOVE PC-NULL-VALUE  TO EP205-STYL-ID-X.                              
198400     MOVE PC-NULL-VALUE  TO EP205-SKU-NBR-X.                              
198500     MOVE PC-NULL-VALUE  TO EP205-EMP-DISC-CDE.                           
198600     MOVE PC-NULL-VALUE  TO EP205-DISC-ELIG-IND.                          
198700     MOVE PC-NULL-VALUE  TO EP205-TKT-CLOR-CDE.                           
198800     PERFORM 8100-WRITE-XST-OFR-DATA.                                     
198900     ADD 1 TO PV-MDSESET-RECS-OUT.                                        
199000     MOVE PV-MDSE-SET-ID   TO PV-MDSE-SET-ID-A.                           
199100                                                                          
199200     IF PV-MDS-EVNT-ID NOT = PV-EVNT-ID                                   
199300        AND PV-MDS-OFR-ID  NOT = PV-OFR-ID                                
199400        AND PV-MDS-OFR-SECT-TYP-CDE NOT = PV-OFR-SECT-TYP-CDE             
199500          PERFORM 6700-UPDATE-STORE-LIST                                  
199600          MOVE PV-EVNT-ID   TO PV-MDS-EVNT-ID                             
199700          MOVE PV-OFR-ID    TO PV-MDS-OFR-ID                              
199800          MOVE PV-OFR-SECT-TYP-CDE TO PV-MDS-OFR-SECT-TYP-CDE             
199900     END-IF.                                                              
200000                                                                          
200100*5300-EXIT.                                                               
200200 EJECT.                                                                   
200300                                                                          
200400*****************************************************************         
200500* 5400-BUILD-EPRD205-RECORDS.                                   *         
200600*  CREATE ALL EPRD205 RECORDS                                   *         
200700*****************************************************************         
200800 5400-BUILD-EPRD205-RECORDS.                                              
200900     MOVE '5400-BUILD-EPRD205-RECORDS    '                                
201000       TO PV-PARAGRAPH-NAME.                                              
201100                                                                          
201200     PERFORM 6100-MOVE-COMMON-VALUES.                                     
201300                                                                          
201400     IF OFR-ID IN OMT00001 NOT = PV-OFR-ID-A                              
201500         PERFORM 6300-BUILD-XST-OFR-DATA                                  
201600         PERFORM 6400-BLD-STR-OFFER-TABLE                                 
201700         PERFORM 6500-BUILD-XST-SEC-DATA                                  
201800         PERFORM 6600-BUILD-XST-BWP-DATA                                  
201900         PERFORM 6200-BUILD-XST-MDSE-DATA                                 
202000         PERFORM 6700-UPDATE-STORE-LIST                                   
202100     ELSE                                                                 
202200       IF OFR-ID IN OMT00001 = PV-OFR-ID-A                                
202300         AND OFR-SECT-TYP-CDE IN OMT00002 NOT EQUAL                       
202400               PV-OFR-SECT-TYP-CDE-A                                      
202500           PERFORM 6500-BUILD-XST-SEC-DATA                                
202600           PERFORM 6600-BUILD-XST-BWP-DATA                                
202700           PERFORM 6200-BUILD-XST-MDSE-DATA                               
202800       ELSE                                                               
202900         IF OFR-ID IN OMT00001 = PV-OFR-ID-A                              
203000           AND OFR-SECT-TYP-CDE IN OMT00002 EQUAL                         
203100                 PV-OFR-SECT-TYP-CDE-A                                    
203200             AND OFR-SECT-BWP-ID IN OMT00003 NOT EQUAL                    
203300                   PV-OFR-SECT-BWP-ID-A                                   
203400               PERFORM 6600-BUILD-XST-BWP-DATA                            
203500               PERFORM 6200-BUILD-XST-MDSE-DATA                           
203600         ELSE                                                             
203700           IF OFR-ID IN OMT00001 = PV-OFR-ID-A                            
203800             AND OFR-SECT-TYP-CDE IN OMT00002 EQUAL                       
203900                   PV-OFR-SECT-TYP-CDE-A                                  
204000              AND OFR-SECT-BWP-ID IN OMT00003 EQUAL                       
204100                    PV-OFR-SECT-BWP-ID-A                                  
204200                AND MDSE-SET-ID IN OMT00005 NOT EQUAL                     
204300                      PV-MDSE-SET-ID-A                                    
204400                  PERFORM 6200-BUILD-XST-MDSE-DATA                        
204500           END-IF                                                         
204600         END-IF                                                           
204700       END-IF                                                             
204800     END-IF.                                                              
204900                                                                          
205000*5400-EXIT.                                                               
205100 EJECT.                                                                   
205200                                                                          
205300*****************************************************************         
205400* 5500-CHECK-FOR-PRIOR-SEND.                                    *         
205500*****************************************************************         
205600 5500-CHECK-FOR-PRIOR-SEND.                                               
205700     MOVE '5500-CHECK-FOR-PRIOR-SEND     '                                
205800       TO PV-PARAGRAPH-NAME.                                              
205900                                                                          
206000     SET PV-STR-INDEX   TO 1.                                             
206100     SET PS-STR-TBL-NF  TO TRUE.                                          
206200                                                                          
206300     SEARCH PV-STR-RECORDS                                                
206400         AT END                                                           
206500           SET PS-STR-TBL-NF                                              
206600             TO TRUE                                                      
206700         WHEN EVNT-ID IN OMT00004 = PV-STR-EVENT-ID(PV-STR-INDEX)         
206800          AND OFR-ID IN OMT00004 =  PV-STR-OFFER-ID(PV-STR-INDEX)         
206900              SET PS-STR-TBL-FND TO TRUE                                  
207000     END-SEARCH.                                                          
207100                                                                          
207200*5500-EXIT.                                                               
207300 EJECT.                                                                   
207400                                                                          
207500****************************************************************          
207600* 5600-SET-A-IN-TRIGGER.                                       *          
207700****************************************************************          
207800 5600-SET-A-IN-TRIGGER.                                                   
207900     MOVE '5600-SET-A-IN-TRIGGER         '                                
208000       TO PV-PARAGRAPH-NAME.                                              
208100                                                                          
208200     INITIALIZE EP205-OFFER-EXTRACT.                                      
208300                                                                          
208400     MOVE PC-ADD-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1.                       
208500                                                                          
208600*5600-EXIT.                                                               
208700 EJECT                                                                    
208800                                                                          
208900*****************************************************************         
209000* 5700-BUILD-AND-SEND-OFFER.                                    *         
209100*****************************************************************         
209200 5700-BUILD-AND-SEND-OFFER.                                               
209300     MOVE '5700-BUILD-AND-SEND-OFFER     '                                
209400       TO PV-PARAGRAPH-NAME.                                              
209500                                                                          
209600     PERFORM 6100-MOVE-COMMON-VALUES.                                     
209700                                                                          
209800     IF OFR-ID IN OMT00001 NOT = PV-OFR-ID-A                              
209900         PERFORM 6300-BUILD-XST-OFR-DATA                                  
210000         PERFORM 6500-BUILD-XST-SEC-DATA                                  
210100         PERFORM 6600-BUILD-XST-BWP-DATA                                  
210200         PERFORM 6200-BUILD-XST-MDSE-DATA                                 
210300         PERFORM 6800-UPDATE-STR-LST-FOR-GRP                              
210400     ELSE                                                                 
210500       IF OFR-ID IN OMT00001 = PV-OFR-ID-A                                
210600         AND OFR-SECT-TYP-CDE IN OMT00002 NOT EQUAL                       
210700               PV-OFR-SECT-TYP-CDE-A                                      
210800           PERFORM 6500-BUILD-XST-SEC-DATA                                
210900           PERFORM 6600-BUILD-XST-BWP-DATA                                
211000           PERFORM 6200-BUILD-XST-MDSE-DATA                               
211100       ELSE                                                               
211200         IF OFR-ID IN OMT00001 = PV-OFR-ID-A                              
211300           AND OFR-SECT-TYP-CDE IN OMT00002 EQUAL                         
211400                 PV-OFR-SECT-TYP-CDE-A                                    
211500             AND OFR-SECT-BWP-ID IN OMT00003 NOT EQUAL                    
211600                   PV-OFR-SECT-BWP-ID-A                                   
211700               PERFORM 6600-BUILD-XST-BWP-DATA                            
211800               PERFORM 6200-BUILD-XST-MDSE-DATA                           
211900         ELSE                                                             
212000           IF OFR-ID IN OMT00001 = PV-OFR-ID-A                            
212100             AND OFR-SECT-TYP-CDE IN OMT00002 EQUAL                       
212200                   PV-OFR-SECT-TYP-CDE-A                                  
212300              AND OFR-SECT-BWP-ID IN OMT00003 EQUAL                       
212400                    PV-OFR-SECT-BWP-ID-A                                  
212500                AND MDSE-SET-ID IN OMT00005 NOT EQUAL                     
212600                      PV-MDSE-SET-ID-A                                    
212700                  PERFORM 6200-BUILD-XST-MDSE-DATA                        
212800           END-IF                                                         
212900         END-IF                                                           
213000       END-IF                                                             
213100     END-IF.                                                              
213200                                                                          
213300*5700-EXIT.                                                               
213400 EJECT.                                                                   
213500                                                                          
213600****************************************************************          
213700* 5800-SET-D-IN-TRIGGER.                                       *          
213800****************************************************************          
213900 5800-SET-D-IN-TRIGGER.                                                   
214000     MOVE '5800-SET-D-IN-TRIGGER         '                                
214100       TO PV-PARAGRAPH-NAME.                                              
214200                                                                          
214300     MOVE PC-DEL-TRIGGER TO PV-OFR-TRGR-TYP-CDE-C1.                       
214400                                                                          
214500*5800-EXIT.                                                               
214600 EJECT                                                                    
214700                                                                          
214800*****************************************************************         
214900* 6000-FIND-ENTRY.                                              *         
215000*****************************************************************         
215100 6000-FIND-ENTRY.                                                         
215200     MOVE '6000-FIND-ENTRY               '                                
215300       TO PV-PARAGRAPH-NAME.                                              
215400                                                                          
215500     IF PV-OFR-TBL-CNT = 0                                                
215600       OR PV-OFR-SUB > PV-OFR-TBL-CNT                                     
215700         SET PS-END-OF-TABLE                                              
215800           TO TRUE                                                        
215900     ELSE                                                                 
216000         IF PV-OFR-ID = PV-OFR-OFFER-ID(PV-OFR-SUB)                       
216100             SET PS-ENTRY-FOUND                                           
216200               TO TRUE                                                    
216300         END-IF                                                           
216400     END-IF.                                                              
216500                                                                          
216600*6000-EXIT.                                                               
216700 EJECT.                                                                   
216800                                                                          
216900*****************************************************************         
217000* 6100-MOVE-COMMON-VALUES.                                      *         
217100*   THIS ROUTINE LOADS THE VALUES COMMON TO ALL EPRD205 FORMATS *         
217200*****************************************************************         
217300 6100-MOVE-COMMON-VALUES.                                                 
217400     MOVE '6100-MOVE-COMMON-VALUES       '                                
217500       TO PV-PARAGRAPH-NAME.                                              
217600                                                                          
217700     SET EP205-FILE-TYPE-CHG TO TRUE.                                     
217800                                                                          
217900     MOVE ZEROES   TO EP205-STR-NBR.                                      
218000                                                                          
218100     MOVE PV-OFR-TRGR-TYP-CDE-C1 TO EP205-TRIGGER-TYPE.                   
218200                                                                          
218300     MOVE OFR-ID  IN OMT00001   TO EP205-OFFER-ID.                        
218400     MOVE EVNT-ID IN OMT00001   TO EP205-EVENT-ID.                        
218500     MOVE OFR-SECT-TYP-CDE IN OMT00002 TO EP205-OFR-SECT-TYP-CDE.         
218600                                                                          
218700*6100-EXIT.                                                               
218800 EJECT.                                                                   
218900                                                                          
219000*****************************************************************         
219100* 6200-BUILD-XST-MDSE-DATA.                                     *         
219200*   BUILD OFFER MDSE RECORD FOR OUTPUT AND CALL WRITE PARAGRAPH *         
219300*****************************************************************         
219400 6200-BUILD-XST-MDSE-DATA.                                                
219500     MOVE '6200-BUILD-XST-MDSE-DATA      '                                
219600       TO PV-PARAGRAPH-NAME.                                              
219700                                                                          
219800     INITIALIZE EP205-VARIABLE-DATA.                                      
219900     SET  EP205-XST-OFR-MDSE TO TRUE.                                     
220000                                                                          
220100     MOVE OFR-SECT-BWP-ID IN OMT00003 TO EP205-OFR-SECT-BWP-ID.           
220200     MOVE MDSE-SET-ID IN OMT00005 TO EP205-MDSE-SET-ID.                   
220300     MOVE MDSE-HIER-CDE  IN OMT00005  TO EP205-MDSE-HIER-CDE.             
220400                                                                          
220500     IF PV-MDSE-DEPT-NBR-NULL-IND = -1                                    
220600         MOVE PC-NULL-VALUE  TO EP205-DEPT-NBR-X                          
220700     ELSE                                                                 
220800         MOVE DEPT-NBR  IN OMT00005  TO EP205-DEPT-NBR                    
220900     END-IF.                                                              
221000                                                                          
221100     IF PV-MDSE-MAJ-CLS-NULL-IND = -1                                     
221200         MOVE PC-NULL-VALUE  TO EP205-MAJ-CL-NBR-X                        
221300     ELSE                                                                 
221400         MOVE MAJ-CL-NBR IN OMT00005 TO EP205-MAJ-CL-NBR                  
221500     END-IF.                                                              
221600                                                                          
221700     IF PV-MDSE-SUB-CLS-NULL-IND = -1                                     
221800         MOVE PC-NULL-VALUE   TO EP205-SUB-CL-NBR-X                       
221900     ELSE                                                                 
222000         MOVE SUB-CL-NBR IN OMT00005  TO EP205-SUB-CL-NBR                 
222100     END-IF.                                                              
222200                                                                          
222300     IF PV-MDSE-STL-ID-NULL-IND = -1                                      
222400         MOVE PC-NULL-VALUE  TO EP205-STYL-ID-X                           
222500     ELSE                                                                 
222600         MOVE STYL-ID  IN OMT00005   TO EP205-STYL-ID                     
222700     END-IF.                                                              
222800                                                                          
222900     IF PV-MDSE-SKU-NB-NULL-IND = -1                                      
223000         MOVE PC-NULL-VALUE  TO EP205-SKU-NBR-X                           
223100     ELSE                                                                 
223200         MOVE SKU-NBR  IN OMT00005   TO EP205-SKU-NBR                     
223300     END-IF.                                                              
223400                                                                          
223500     MOVE IE-IND IN OMT00005  TO EP205-IE-IND.                            
223600                                                                          
223700     IF PV-MDSE-EMP-DS-NULL-IND = -1                                      
223800         MOVE PC-NULL-VALUE TO EP205-EMP-DISC-CDE                         
223900     ELSE                                                                 
224000         MOVE EMP-DISC-CDE IN OMT00005  TO EP205-EMP-DISC-CDE             
224100     END-IF.                                                              
224200                                                                          
224300     IF PV-MDSE-DSC-EL-NULL-IND = -1                                      
224400        MOVE PC-NULL-VALUE   TO EP205-DISC-ELIG-IND                       
224500     ELSE                                                                 
224600        MOVE DISC-ELIG-IND IN OMT00005 TO EP205-DISC-ELIG-IND             
224700     END-IF.                                                              
224800                                                                          
224900     IF PV-MDSE-TKT-CL-NULL-IND = -1                                      
225000         MOVE PC-NULL-VALUE  TO EP205-TKT-CLOR-CDE                        
225100     ELSE                                                                 
225200         MOVE TKT-CLOR-CDE IN OMT00005  TO EP205-TKT-CLOR-CDE             
225300     END-IF.                                                              
225400                                                                          
225500     IF PS-OFFER-PASS                                                     
225600         PERFORM 8100-WRITE-XST-OFR-DATA                                  
225700         ADD 1   TO PV-MDSESET-RECS-OUT                                   
225800     ELSE                                                                 
225900         PERFORM 8200-WRITE-XST-GRP-DATA                                  
226000         ADD 1  TO PV-MDSESET-STR-RECS-OUT                                
226100     END-IF.                                                              
226200                                                                          
226300     MOVE MDSE-SET-ID IN OMT00005  TO PV-MDSE-SET-ID-A.                   
226400                                                                          
226500*6200-EXIT.                                                               
226600 EJECT.                                                                   
226700                                                                          
226800*****************************************************************         
226900* 6300-BUILD-XST-OFR-DATA.                                      *         
227000*   BUILD OFFER RECORD FOR OUTPUT AND CALL WRITE PARAGRAH       *         
227100*****************************************************************         
227200 6300-BUILD-XST-OFR-DATA.                                                 
227300     MOVE '6300-BUILD-XST-OFR-DATA       '                                
227400       TO PV-PARAGRAPH-NAME.                                              
227500                                                                          
227600     INITIALIZE EP205-VARIABLE-DATA.                                      
227700                                                                          
227800     SET EP205-XST-OFFER TO TRUE.                                         
227900                                                                          
228000*    MOVE PC-PROMO-SCHM-CDE    TO EP205-PROMO-SCHM-CDE.                   
228100                                                                          
228200     EVALUATE TRUE                                                        
228300         WHEN EVNT-TYP-CDE   IN OMT00009 = 'SP'                           
228400            MOVE PC-PROMO-SCHM-CDE-1008  TO                               
228500                                      EP205-PROMO-SCHM-CDE                
228600         WHEN EVNT-TYP-CDE   IN OMT00009 = 'TR'                           
228700            MOVE PC-PROMO-SCHM-CDE-1009       TO                          
228800                                      EP205-PROMO-SCHM-CDE                
228900         WHEN EVNT-TYP-CDE   IN OMT00009 = 'BW'                           
229000            MOVE PC-PROMO-SCHM-CDE-1010       TO                          
229100                                      EP205-PROMO-SCHM-CDE                
229200     END-EVALUATE.                                                        
229300                                                                          
229400     MOVE EVNT-TYP-CDE IN OMT00009  TO EP205-EVENT-TYP-CODE.              
229500     MOVE EVNT-CTG-NM IN OMT00007  TO EP205-EVENT-NAME.                   
229600     MOVE OFR-NM  IN OMT00001   TO EP205-OFFER-NAME.                      
229700     MOVE DISC-LVL-CDE  IN OMT00001 TO EP205-DISC-LEVEL-CODE.             
229800     MOVE OFR-USG-TYP-CDE IN OMT00001                                     
229900         TO EP205-OFFER-USAGE-TYPE-CODE.                                  
230000     MOVE BRCDE-TYP-CDE  IN OMT00001 TO EP205-BARCODE-TYPE-CODE.          
230100     MOVE UNEARN-IND IN OMT00001  TO EP205-UNEARN-IND.                    
230200     MOVE OFR-TRGR-TYP-CDE IN OMT00001  TO EP205-OFR-TRGR-TYP-CDE.        
230300     IF PV-ITM-DISC-APL-NULL-IND = -1                                     
230400       MOVE PC-NULL-VALUE   TO EP205-BWP-ITM-APPL-CDE                     
230500     ELSE                                                                 
230600       MOVE ITM-DISC-APPL-CDE IN OMT00001                                 
230700             TO EP205-BWP-ITM-APPL-CDE                                    
230800     END-IF.                                                              
230900                                                                          
231000     MOVE SPACES  TO EP205-OFFER-FILLER.                                  
231100                                                                          
231200     IF PS-OFFER-PASS                                                     
231300         PERFORM 8100-WRITE-XST-OFR-DATA                                  
231400         ADD 1   TO PV-OFFER-RECS-OUT                                     
231500     ELSE                                                                 
231600         PERFORM 8200-WRITE-XST-GRP-DATA                                  
231700         ADD 1  TO PV-OFFER-STR-RECS-OUT                                  
231800     END-IF.                                                              
231900                                                                          
232000     MOVE OFR-ID IN OMT00001   TO PV-OFR-ID-A.                            
232100     MOVE EVNT-ID IN OMT00001  TO PV-EVNT-ID-A.                           
232200                                                                          
232300*6300-EXIT.                                                               
232400 EJECT.                                                                   
232500                                                                          
232600*****************************************************************         
232700* 6400-BLD-STR-OFFER-TABLE.                                     *         
232800*****************************************************************         
232900 6400-BLD-STR-OFFER-TABLE.                                                
233000     MOVE '6400-BLD-STR-OFFER-TABLE      '                                
233100       TO PV-PARAGRAPH-NAME.                                              
233200                                                                          
233300     MOVE EP205-TRIGGER-TYPE TO                                           
233400                           PV-STR-TRIGGER-TYPE(PV-STR-INDEX-SAVE).        
233500                                                                          
233600     MOVE EP205-EVENT-ID    TO PV-STR-EVENT-ID(PV-STR-INDEX-SAVE).        
233700                                                                          
233800     MOVE EP205-OFFER-ID    TO PV-STR-OFFER-ID(PV-STR-INDEX-SAVE).        
233900                                                                          
234000     SET PV-STR-INDEX-SAVE UP BY +1.                                      
234100                                                                          
234200     IF PV-STR-INDEX-SAVE > PC-OMT-STR-TBL-MAX                            
234300        MOVE 'TABLE LIMIT REACHED FOR PV-OMT-STR-TBL' TO                  
234400                             PV-OPERATION-MSG-1                           
234500        SET AC-4004-TABLE-OVERFLOW-ERROR   TO TRUE                        
234600        PERFORM 9100-ABEND                                                
234700     END-IF.                                                              
234800                                                                          
234900*6400-EXIT.                                                               
235000 EJECT.                                                                   
235100                                                                          
235200*****************************************************************         
235300* 6500-BUILD-XST-SEC-DATA.                                      *         
235400*   BUILD OFFER-SECT RECORD FOR OUTPUT AND CALL WRITE PARAGRAPH *         
235500*****************************************************************         
235600 6500-BUILD-XST-SEC-DATA.                                                 
235700     MOVE '6500-BUILD-XST-SEC-DATA       '                                
235800       TO PV-PARAGRAPH-NAME.                                              
235900                                                                          
236000     INITIALIZE EP205-VARIABLE-DATA.                                      
236100     SET  EP205-XST-OFR-SECT TO TRUE.                                     
236200                                                                          
236300     PERFORM 8300-CALCULATE-DATES.                                        
236400     MOVE BWP-GET-CNCTR-CDE IN OMT00002                                   
236500         TO EP205-BWP-GET-CNCTR-CDE.                                      
236600     MOVE BWP-BUY-CNCTR-CDE IN OMT00002                                   
236700         TO EP205-BWP-BUY-CNCTR-CDE.                                      
236800     MOVE VLD-POS-TNDR-COMB-TYP-CDE  IN OMT00002                          
236900         TO EP205-VLD-POS-TNDR-CMP-TYP-CDE.                               
237000     MOVE LMT-PER-TRN-QTY   IN OMT00002                                   
237100         TO EP205-LMT-PER-TRN-QTY.                                        
237200     MOVE CUSTFACG-SHRT-DESC  IN OMT00008                                 
237300         TO EP205-CUSTFACG-SHRT-DESC.                                     
237400     IF PV-POS-BTN-CDE-NULL-IND = -1                                      
237500         MOVE PC-NULL-VALUE TO EP205-POS-BTN-TXT                          
237600              EP205-POS-BTN-RLTV-SEQ-NBR-X                                
237700     ELSE                                                                 
237800         PERFORM 8400-POPULATE-BTN-VALUE                                  
237900     END-IF.                                                              
238000                                                                          
238100     IF PS-OFFER-PASS                                                     
238200         PERFORM 8100-WRITE-XST-OFR-DATA                                  
238300         ADD 1 TO PV-OFRSECT-RECS-OUT                                     
238400     ELSE                                                                 
238500         PERFORM 8200-WRITE-XST-GRP-DATA                                  
238600         ADD 1  TO PV-OFRSECT-STR-RECS-OUT                                
238700     END-IF.                                                              
238800                                                                          
238900     MOVE OFR-SECT-TYP-CDE IN OMT00002                                    
239000       TO PV-OFR-SECT-TYP-CDE-A.                                          
239100                                                                          
239200*6500-EXIT.                                                               
239300 EJECT.                                                                   
239400                                                                          
239500*****************************************************************         
239600* 6600-BUILD-XST-BWP-DATA.                                      *         
239700*   BUILD OFFER BWP RECORD FOR OUTPUT AND CALL WRITE PARAGRAPH  *         
239800*****************************************************************         
239900 6600-BUILD-XST-BWP-DATA.                                                 
240000     MOVE '6600-BUILD-XST-BWP-DATA        '                               
240100       TO PV-PARAGRAPH-NAME.                                              
240200                                                                          
240300     INITIALIZE EP205-VARIABLE-DATA.                                      
240400     SET  EP205-XST-OFR-BWP  TO TRUE.                                     
240500                                                                          
240600     MOVE OFR-SECT-BWP-ID IN OMT00003 TO EP205-OFR-SECT-BWP-ID.           
240700     MOVE TIER-LVL-NBR IN OMT00003  TO EP205-TIER-LVL-NBR.                
240800     MOVE BWP-SELN-TYP-CDE IN OMT00003 TO EP205-BWP-SELN-TYP-CDE.         
240900                                                                          
241000     IF PV-BWP-BUY-QTY-NULL-IND = -1                                      
241100         MOVE PC-NULL-VALUE TO EP205-BWP-BUY-QTY-X                        
241200     ELSE                                                                 
241300         MOVE BWP-BUY-QTY IN OMT00003 TO EP205-BWP-BUY-QTY                
241400     END-IF.                                                              
241500                                                                          
241600     IF PV-BWP-BUY-AMT-NULL-IND = -1                                      
241700        MOVE PC-NULL-VALUE TO EP205-BWP-BUY-AMT-X                         
241800     ELSE                                                                 
241900        MOVE BWP-BUY-AMT IN OMT00003 TO EP205-BWP-BUY-AMT                 
242000     END-IF.                                                              
242100                                                                          
242200     IF PV-BWP-BUY-TOL-AMT-NULL-IND = -1                                  
242300        MOVE PC-NULL-VALUE   TO EP205-BWP-BUY-TOL-AMT-X                   
242400     ELSE                                                                 
242500        MOVE BWP-BUY-TOL-AMT IN OMT00003 TO EP205-BWP-BUY-TOL-AMT         
242600     END-IF.                                                              
242700                                                                          
242800     IF PV-BWP-GET-QTY-NULL-IND = -1                                      
242900         MOVE PC-NULL-VALUE  TO EP205-BWP-GET-QTY-X                       
243000     ELSE                                                                 
243100         MOVE BWP-GET-QTY IN OMT00003 TO EP205-BWP-GET-QTY                
243200     END-IF.                                                              
243300                                                                          
243400     IF PV-BWP-GET-PCT-NULL-IND = -1                                      
243500         MOVE PC-NULL-VALUE TO EP205-BWP-GET-PCT-OFF-PCT-X                
243600     ELSE                                                                 
243700         MOVE BWP-GET-PCT-OFF-PCT IN OMT00003                             
243800             TO EP205-BWP-GET-PCT-OFF-PCT                                 
243900     END-IF.                                                              
244000                                                                          
244100     IF PV-BWP-GET-DOL-NULL-IND = -1                                      
244200         MOVE PC-NULL-VALUE  TO EP205-BWP-GET-DOL-OFF-AMT-X               
244300     ELSE                                                                 
244400         MOVE BWP-GET-DOL-OFF-AMT  IN OMT00003                            
244500             TO EP205-BWP-GET-DOL-OFF-AMT                                 
244600     END-IF.                                                              
244700                                                                          
244800     IF PV-BWP-GET-PRPT-NULL-IND = -1                                     
244900         MOVE PC-NULL-VALUE  TO EP205-BWP-GET-PRPT-AMT-X                  
245000     ELSE                                                                 
245100         MOVE BWP-GET-PRPT-AMT  IN OMT00003                               
245200             TO EP205-BWP-GET-PRPT-AMT                                    
245300     END-IF.                                                              
245400                                                                          
245500     IF PS-OFFER-PASS                                                     
245600         PERFORM 8100-WRITE-XST-OFR-DATA                                  
245700         ADD 1  TO PV-OFRBWP-RECS-OUT                                     
245800     ELSE                                                                 
245900         PERFORM 8200-WRITE-XST-GRP-DATA                                  
246000         ADD 1  TO PV-OFRBWP-STR-RECS-OUT                                 
246100     END-IF.                                                              
246200                                                                          
246300     MOVE OFR-SECT-BWP-ID IN OMT00003                                     
246400       TO PV-OFR-SECT-BWP-ID-A.                                           
246500                                                                          
246600*6600-EXIT.                                                               
246700 EJECT.                                                                   
246800                                                                          
246900*****************************************************************         
247000* 6700-UPDATE-STORE-LIST.                                       *         
247100*  DETERMINE AND WRITE THE STORE LIST FOR CORRECTED OFFERS      *         
247200*****************************************************************         
247300 6700-UPDATE-STORE-LIST.                                                  
247400     MOVE '6700-UPDATE-STORE-LIST        '                                
247500       TO PV-PARAGRAPH-NAME.                                              
247600                                                                          
247700     PERFORM 7700-DEC-OPEN-STORE-CURSOR-ONE.                              
247800     SET PS-CURSOR-ONE-NOT-EOF   TO TRUE.                                 
247900                                                                          
248000     PERFORM 7750-FETCH-STORE-CURSOR-ONE                                  
248100       UNTIL PS-END-OF-STORE-CURSOR-ONE.                                  
248200                                                                          
248300*6700-EXIT.                                                               
248400 EJECT.                                                                   
248500                                                                          
248600*****************************************************************         
248700* 6800-UPDATE-STR-LST-FOR-GRP.                                  *         
248800*****************************************************************         
248900 6800-UPDATE-STR-LST-FOR-GRP.                                             
249000     MOVE '6800-UPDATE-STR-LST-FOR-GRP   '                                
249100       TO PV-PARAGRAPH-NAME.                                              
249200                                                                          
249300     INITIALIZE EP206-STORE-LIST-RCD.                                     
249400                                                                          
249500     MOVE EP205-TRIGGER-TYPE TO EP206-ADD-DLT-FLG.                        
249600     MOVE EVNT-ID IN OMT00004  TO EP206-EVENT-ID.                         
249700     MOVE OFR-ID IN OMT00004  TO EP206-OFFER-ID.                          
249800     MOVE OFR-SECT-TYP-CDE IN OMT00004 TO EP206-OFR-SECT-TYP-CDE.         
249900     MOVE STR-GP-TYP-CDE IN OMT00004 TO EP206-STR-GRP-TYP-CDE.            
250000     MOVE STR-GP-ID IN OMT00004  TO EP206-STR-GRP-ID.                     
250100     MOVE 'Y'    TO EP206-IE-IND.                                         
250200                                                                          
250300     WRITE CORR-STR-GRP-LIST-REC                                          
250400       FROM EP206-STORE-LIST-RCD.                                         
250500                                                                          
250600*6800-EXIT.                                                               
250700 EJECT.                                                                   
250800                                                                          
250900*****************************************************************         
251000* 7100-DEC-OPEN-BTN-CURSOR                                      *         
251100* THIS PARAGRAPH WILL DECLARE AND OPEN BTNCRSR                  *         
251200*****************************************************************         
251300 7100-DEC-OPEN-BTN-CURSOR.                                                
251400     MOVE '7100-DEC-OPEN-BTN-CURSOR     '                                 
251500       TO PV-PARAGRAPH-NAME.                                              
251600                                                                          
251700     EXEC SQL  DECLARE BTNCRSR CURSOR FOR                                 
251800        SELECT                                                            
251900            POS_BTN_CDE                                                   
252000           ,POS_BTN_TXT                                                   
252100           ,POS_BTN_RLTV_SEQ_NBR                                          
252200       FROM OMT_POS_BTN                                                   
252300       WHERE POS_BTN_CDE IS NOT NULL                                      
252400     END-EXEC.                                                            
252500                                                                          
252600     PERFORM                                                              
252700         WITH TEST AFTER                                                  
252800         VARYING PV-SQL-SUB                                               
252900         FROM 1 BY 1                                                      
253000         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
253100                   OR SQLCODE = 0)                                        
253200                                                                          
253300             EXEC SQL                                                     
253400                 OPEN BTNCRSR                                             
253500             END-EXEC                                                     
253600                                                                          
253700             EVALUATE TRUE                                                
253800                 WHEN SQLCODE = 0                                         
253900                     CONTINUE                                             
254000                 WHEN OTHER                                               
254100                     MOVE 'OPEN CURSOR FOR BTNCRSR       '                
254200                       TO PV-ORA-OPERATION                                
254300                     PERFORM 9200-ORA-ERROR                               
254400             END-EVALUATE                                                 
254500                                                                          
254600     END-PERFORM.                                                         
254700                                                                          
254800     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
254900         MOVE 'OPEN BTNCRSR RETRIES EXCEEDED'                             
255000           TO PV-ORA-OPERATION                                            
255100         PERFORM 9200-ORA-ERROR                                           
255200     END-IF.                                                              
255300                                                                          
255400*7100-EXIT.                                                               
255500 EJECT.                                                                   
255600                                                                          
255700*****************************************************************         
255800* 7150-FETCH-BTN-CSR.                                           *         
255900* FETCH DATA FROM THE BTNCRSR CURSOR TO POPULATE THE            *         
256000*   PROGRAM TABLE (PV-OMT-POS-BTN). THE VALUES FROM THIS TABLE *          
256100*   WILL THEN BE USED TO POPULATE POS-BTN-TXT AND POS-BTN-RLTV-SEQ        
256200*  -NBR IN EPRD205                                                        
256300*****************************************************************         
256400 7150-FETCH-BTN-CSR.                                                      
256500     MOVE '7150-FETCH-BTN-CSR            '                                
256600       TO PV-PARAGRAPH-NAME.                                              
256700                                                                          
256800     EXEC SQL                                                             
256900         FETCH BTNCRSR                                                    
257000           INTO                                                           
257100                :OMT00006.POS-BTN-CDE                                     
257200               ,:OMT00006.POS-BTN-TXT                                     
257300               ,:OMT00006.POS-BTN-RLTV-SEQ-NBR                            
257400                :PV-POS-RLTV-NUM-NULL-IND                                 
257500     END-EXEC.                                                            
257600                                                                          
257700*7150-EXIT.                                                               
257800 EJECT.                                                                   
257900                                                                          
258000*****************************************************************         
258100* 7200-CLOSE-BTN-CURSOR.                                        *         
258200*  THIS PARAGRAPH CLOSES THE BTNCRSR                            *         
258300*****************************************************************         
258400 7200-CLOSE-BTN-CURSOR.                                                   
258500     MOVE '7200-CLOSE-BTN-CURSOR         '                                
258600       TO PV-PARAGRAPH-NAME.                                              
258700                                                                          
258800     PERFORM                                                              
258900         WITH TEST AFTER                                                  
259000         VARYING PV-SQL-SUB                                               
259100         FROM 1 BY 1                                                      
259200         UNTIL (PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                    
259300                   OR SQLCODE = 0)                                        
259400                                                                          
259500             EXEC SQL                                                     
259600                 CLOSE BTNCRSR                                            
259700             END-EXEC                                                     
259800                                                                          
259900             EVALUATE TRUE                                                
260000                 WHEN SQLCODE = 0                                         
260100                     CONTINUE                                             
260200                 WHEN OTHER                                               
260300                     MOVE 'CLOSE CURSOR FOR OMT_POS_BTN'                  
260400                       TO PV-ORA-OPERATION                                
260500                     PERFORM 9200-ORA-ERROR                               
260600             END-EVALUATE                                                 
260700     END-PERFORM.                                                         
260800                                                                          
260900     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
261000         MOVE 'CLOSE BTNCRSR RETRIES EXCEEDED'                            
261100           TO PV-ORA-OPERATION                                            
261200         PERFORM 9200-ORA-ERROR                                           
261300     END-IF.                                                              
261400                                                                          
261500*7200-EXIT.                                                               
261600 EJECT.                                                                   
261700                                                                          
261800*****************************************************************         
261900* 7300-DEC-OPEN-OFRCSR1.                                        *         
262000* THIS PARAGRAPH DECLARES AND OPENS OFRCSR1. THE CURSOR WILL              
262100* FETCH DATA FROM THE OFFER TABLES WHEN THE LAST-UPD-TMST IN ONE          
262200* OF THESE TABLES (OMT_OFR, OMT_EVNT, OMT_OFR_SECT, OMT_MDSE_SET          
262300* OMT_OFR_SECT_BWP ) IS WITHIN THE RANGE SPECIFIED IN THE                 
262400* INPUT CONTROL CARD EPCC251                                              
262500*****************************************************************         
262600 7300-DEC-OPEN-OFRCSR1.                                                   
262700     MOVE '7300-DEC-OPEN-OFRCSR1         '                                
262800       TO PV-PARAGRAPH-NAME.                                              
262900                                                                          
263000     EXEC SQL DECLARE OFRCSR1 CURSOR FOR                                  
263100       SELECT C.EVNT_ID                                                   
263200             ,C.OFR_ID                                                    
263300             ,C.OFR_STAT_CDE                                              
263400             ,D.OFR_SECT_TYP_CDE                                          
263500             ,D.EFF_BEG_TMST                                              
263600             ,D.TOL_STRT_NMOFDA_QTY                                       
263700             ,G.OFR_SECT_BWP_ID                                           
263800             ,G.BWP_DLT_IND                                               
263900             ,H.MDSE_SET_ID                                               
264000             ,H.MDSE_DLT_IND                                              
264100             ,C.CRE_TMST                                                  
264200             ,C.LAST_UPD_TMST                                             
264300             ,B.LAST_UPD_TMST                                             
264400             ,D.LAST_UPD_TMST                                             
264500             ,G.LAST_UPD_TMST                                             
264600             ,H.LAST_UPD_TMST                                             
264700         FROM OMT_EVNT_CTG A                                              
264800             ,OMT_EVNT B                                                  
264900             ,OMT_OFR C                                                   
265000             ,OMT_OFR_SECT D                                              
265100             ,OMT_OFR_CUSTFACG_DESC E                                     
265200             ,OMT_OFR_SECT_BWP G                                          
265300             ,OMT_MDSE_SET H                                              
265400      WHERE   C.EVNT_ID          = B.EVNT_ID                              
265500          AND B.EVNT_CTG_ID      = A.EVNT_CTG_ID                          
265600          AND C.EVNT_ID          = D.EVNT_ID                              
265700          AND C.OFR_ID           = D.OFR_ID                               
265800          AND D.CUSTFACG_DESC_ID = E.CUSTFACG_DESC_ID                     
265900          AND D.OFR_ID           = G.OFR_ID                               
266000          AND D.OFR_SECT_TYP_CDE = G.OFR_SECT_TYP_CDE                     
266100          AND D.OFR_ID           = H.OFR_ID                               
266200          AND G.OFR_SECT_BWP_ID  = H.OFR_SECT_BWP_ID                      
266300          AND D.EVNT_ID          = G.EVNT_ID                              
266400          AND G.OFR_SECT_TYP_CDE = H.OFR_SECT_TYP_CDE                     
266500          AND G.OFR_ID           = H.OFR_ID                               
266600          AND G.EVNT_ID          = H.EVNT_ID                              
266700          AND ((C.LAST_UPD_TMST BETWEEN                                   
266800                TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')         
266900                 AND                                                      
267000                  TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS'))        
267100           OR (B.LAST_UPD_TMST BETWEEN                                    
267200             TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')            
267300               AND                                                        
267400                 TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS'))         
267500           OR (D.LAST_UPD_TMST BETWEEN                                    
267600             TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')            
267700               AND                                                        
267800                 TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS'))         
267900           OR (G.LAST_UPD_TMST BETWEEN                                    
268000             TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')            
268100               AND                                                        
268200                 TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS'))         
268300           OR (H.LAST_UPD_TMST BETWEEN                                    
268400             TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')            
268500               AND                                                        
268600                 TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS')))        
268700      ORDER BY C.EVNT_ID                                                  
268800              ,C.OFR_ID                                                   
268900              ,D.OFR_SECT_TYP_CDE                                         
269000              ,H.MDSE_DLT_IND                                             
269100     END-EXEC.                                                            
269200                                                                          
269300     PERFORM                                                              
269400         WITH TEST AFTER                                                  
269500         VARYING PV-SQL-SUB                                               
269600         FROM 1 BY 1                                                      
269700         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
269800                  OR SQLCODE = 0)                                         
269900                                                                          
270000             EXEC SQL                                                     
270100                 OPEN OFRCSR1                                             
270200             END-EXEC                                                     
270300                                                                          
270400             EVALUATE TRUE                                                
270500                 WHEN SQLCODE = 0                                         
270600                     CONTINUE                                             
270700                 WHEN OTHER                                               
270800                     MOVE 'OPEN CURSOR FOR OFRCRSR     '                  
270900                       TO PV-ORA-OPERATION                                
271000                     PERFORM 9200-ORA-ERROR                               
271100             END-EVALUATE                                                 
271200                                                                          
271300     END-PERFORM.                                                         
271400                                                                          
271500     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
271600         MOVE 'OPEN OFRCSR1 RETRIES EXCEEDED'                             
271700           TO PV-ORA-OPERATION                                            
271800         PERFORM 9200-ORA-ERROR                                           
271900     END-IF.                                                              
272000                                                                          
272100*7300-EXIT.                                                               
272200 EJECT.                                                                   
272300*****************************************************************         
272400* 7350-FETCH-OFRCSR1.                                           *         
272500* THE FOLLOWING PARAGRAPH FETCHES DATA FROM ORFCSR1                       
272600*****************************************************************         
272700 7350-FETCH-OFRCSR1.                                                      
272800     MOVE '7350-FETCH-OFRCSR1            '                                
272900       TO PV-PARAGRAPH-NAME.                                              
273000                                                                          
273100     INITIALIZE PV-NULL-INDICATORS.                                       
273200                                                                          
273300     EXEC SQL                                                             
273400         FETCH OFRCSR1                                                    
273500           INTO                                                           
273600                :OMT00001.EVNT-ID                                         
273700               ,:OMT00001.OFR-ID                                          
273800               ,:OMT00001.OFR-STAT-CDE                                    
273900               ,:OMT00002.OFR-SECT-TYP-CDE                                
274000               ,:OMT00002.EFF-BEG-TMST                                    
274100               ,:OMT00002.TOL-STRT-NMOFDA-QTY                             
274200               ,:OMT00003.OFR-SECT-BWP-ID                                 
274300               ,:OMT00003.BWP-DLT-IND                                     
274400               ,:OMT00005.MDSE-SET-ID                                     
274500               ,:OMT00005.MDSE-DLT-IND                                    
274600               ,:OMT00001.CRE-TMST                                        
274700               ,:OMT00001.LAST-UPD-TMST                                   
274800               ,:OMT00009.LAST-UPD-TMST                                   
274900               ,:OMT00002.LAST-UPD-TMST                                   
275000               ,:OMT00003.LAST-UPD-TMST                                   
275100               ,:OMT00005.LAST-UPD-TMST                                   
275200     END-EXEC.                                                            
275300                                                                          
275400*7350-EXIT.                                                               
275500 EJECT.                                                                   
275600                                                                          
275700*****************************************************************         
275800* 7400-CLOSE-OFRCSR1.                                           *         
275900* THE FOLLOWING PARAGRAPH CLOSES CURSOR ORFCSR1                           
276000*****************************************************************         
276100 7400-CLOSE-OFRCSR1.                                                      
276200     MOVE '7400-CLOSE-OFRCSR1            '                                
276300       TO PV-PARAGRAPH-NAME.                                              
276400                                                                          
276500     PERFORM                                                              
276600         WITH TEST AFTER                                                  
276700         VARYING PV-SQL-SUB                                               
276800         FROM 1 BY 1                                                      
276900         UNTIL (PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                    
277000                   OR SQLCODE = 0)                                        
277100                                                                          
277200             EXEC SQL                                                     
277300                 CLOSE OFRCSR1                                            
277400             END-EXEC                                                     
277500                                                                          
277600             EVALUATE TRUE                                                
277700                 WHEN SQLCODE = 0                                         
277800                     CONTINUE                                             
277900                 WHEN OTHER                                               
278000                     MOVE 'CLOSE CURSOR FOR OMT_POS_BTN'                  
278100                       TO PV-ORA-OPERATION                                
278200                     PERFORM 9200-ORA-ERROR                               
278300             END-EVALUATE                                                 
278400     END-PERFORM.                                                         
278500                                                                          
278600     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
278700         MOVE 'CLOSE OFRCSR1 RETRIES EXCEEDED'                            
278800           TO PV-ORA-OPERATION                                            
278900         PERFORM 9200-ORA-ERROR                                           
279000     END-IF.                                                              
279100                                                                          
279200*7400-EXIT.                                                               
279300 EJECT.                                                                   
279400*****************************************************************         
279500* 7500-DEC-OPEN-OFFER-CURSOR.                                   *         
279600*****************************************************************         
279700 7500-DEC-OPEN-OFFER-CURSOR.                                              
279800     MOVE '7500-DEC-OPEN-OFFER-CURSOR    '                                
279900       TO PV-PARAGRAPH-NAME.                                              
280000                                                                          
280100     EXEC SQL DECLARE OFRCRSR CURSOR FOR                                  
280200       SELECT C.EVNT_ID                                                   
280300             ,C.OFR_ID                                                    
280400             ,C.OFR_NM                                                    
280500             ,C.DISC_LVL_CDE                                              
280600             ,C.OFR_USG_TYP_CDE                                           
280700             ,C.BRCDE_TYP_CDE                                             
280800             ,C.UNEARN_IND                                                
280900             ,C.OFR_TRGR_TYP_CDE                                          
281000             ,C.ITM_DISC_APPL_CDE                                         
281100             ,C.OFR_STAT_CDE                                              
281200             ,B.EVNT_TYP_CDE                                              
281300             ,A.EVNT_CTG_NM                                               
281400             ,D.OFR_SECT_TYP_CDE                                          
281500             ,D.EFF_BEG_TMST                                              
281600             ,D.EFF_END_TMST                                              
281700             ,D.TOL_STRT_NMOFDA_QTY                                       
281800             ,D.TOL_END_NMOFDA_QTY                                        
281900             ,D.REDMN_SUPVR_OVRRID_NMOFDA_QTY                             
282000             ,D.BWP_GET_CNCTR_CDE                                         
282100             ,D.BWP_BUY_CNCTR_CDE                                         
282200             ,D.LMT_PER_TRN_QTY                                           
282300             ,D.VLD_POS_TNDR_COMB_TYP_CDE                                 
282400             ,D.POS_BTN_CDE                                               
282500             ,E.CUSTFACG_SHRT_DESC                                        
282600             ,G.OFR_SECT_BWP_ID                                           
282700             ,G.TIER_LVL_NBR                                              
282800             ,G.BWP_SELN_TYP_CDE                                          
282900             ,G.BWP_BUY_QTY                                               
283000             ,G.BWP_BUY_AMT                                               
283100             ,G.BWP_BUY_TOL_AMT                                           
283200             ,G.BWP_GET_QTY                                               
283300             ,G.BWP_GET_PCT_OFF_PCT                                       
283400             ,G.BWP_GET_DOL_OFF_AMT                                       
283500             ,G.BWP_GET_PRPT_AMT                                          
283600             ,H.MDSE_SET_ID                                               
283700             ,H.MDSE_HIER_CDE                                             
283800             ,H.DEPT_NBR                                                  
283900             ,H.MAJ_CL_NBR                                                
284000             ,H.SUB_CL_NBR                                                
284100             ,H.STYL_ID                                                   
284200             ,H.SKU_NBR                                                   
284300             ,H.IE_IND                                                    
284400             ,H.EMP_DISC_CDE                                              
284500             ,H.DISC_ELIG_IND                                             
284600             ,H.TKT_CLOR_CDE                                              
284700             ,H.MDSE_DLT_IND                                              
284800             ,C.LAST_UPD_TMST                                             
284900             ,D.LAST_UPD_TMST                                             
285000             ,G.LAST_UPD_TMST                                             
285100             ,H.LAST_UPD_TMST                                             
285200             ,C.CRE_TMST                                                  
285300             ,D.CRE_TMST                                                  
285400             ,G.CRE_TMST                                                  
285500             ,H.CRE_TMST                                                  
285600         FROM OMT_EVNT_CTG A                                              
285700             ,OMT_EVNT B                                                  
285800             ,OMT_OFR C                                                   
285900             ,OMT_OFR_SECT D                                              
286000             ,OMT_OFR_CUSTFACG_DESC E                                     
286100             ,OMT_OFR_SECT_BWP G                                          
286200             ,OMT_MDSE_SET H                                              
286300      WHERE   C.EVNT_ID          = B.EVNT_ID                              
286400          AND B.EVNT_CTG_ID      = A.EVNT_CTG_ID                          
286500          AND C.EVNT_ID          = D.EVNT_ID                              
286600          AND C.OFR_ID           = D.OFR_ID                               
286700          AND D.CUSTFACG_DESC_ID = E.CUSTFACG_DESC_ID                     
286800          AND D.OFR_ID           = G.OFR_ID                               
286900          AND D.OFR_SECT_TYP_CDE = G.OFR_SECT_TYP_CDE                     
287000          AND D.OFR_ID           = H.OFR_ID                               
287100          AND G.OFR_SECT_BWP_ID  = H.OFR_SECT_BWP_ID                      
287200          AND D.EVNT_ID          = G.EVNT_ID                              
287300          AND G.OFR_SECT_TYP_CDE = H.OFR_SECT_TYP_CDE                     
287400          AND G.OFR_ID           = H.OFR_ID                               
287500          AND G.EVNT_ID          = H.EVNT_ID                              
287600          AND C.EVNT_ID          = :PV-EVNT-ID                            
287700          AND C.OFR_ID           = :PV-OFR-ID                             
287800          AND D.OFR_SECT_TYP_CDE = :PV-OFR-SECT-TYP-CDE                   
287900          AND G.BWP_DLT_IND      = 'N'                                    
288000          AND H.MDSE_DLT_IND     = 'N'                                    
288100      ORDER BY C.EVNT_ID                                                  
288200              ,C.OFR_ID                                                   
288300              ,D.OFR_SECT_TYP_CDE                                         
288400     END-EXEC.                                                            
288500                                                                          
288600     PERFORM                                                              
288700         WITH TEST AFTER                                                  
288800         VARYING PV-SQL-SUB                                               
288900         FROM 1 BY 1                                                      
289000         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
289100                  OR SQLCODE = 0)                                         
289200                                                                          
289300             EXEC SQL                                                     
289400                 OPEN OFRCRSR                                             
289500             END-EXEC                                                     
289600                                                                          
289700             EVALUATE TRUE                                                
289800                 WHEN SQLCODE = 0                                         
289900                     CONTINUE                                             
290000                 WHEN OTHER                                               
290100                     MOVE 'OPEN CURSOR FOR OFRCRSR     '                  
290200                       TO PV-ORA-OPERATION                                
290300                     PERFORM 9200-ORA-ERROR                               
290400             END-EVALUATE                                                 
290500                                                                          
290600     END-PERFORM.                                                         
290700                                                                          
290800     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
290900         MOVE 'OPEN OFRCRSR RETRIES EXCEEDED'                             
291000           TO PV-ORA-OPERATION                                            
291100         PERFORM 9200-ORA-ERROR                                           
291200     END-IF.                                                              
291300                                                                          
291400*7500-EXIT.                                                               
291500 EJECT.                                                                   
291600                                                                          
291700*****************************************************************         
291800* 7550-FETCH-OFFER-CORR-CURSOR.                                 *         
291900*****************************************************************         
292000 7550-FETCH-OFFER-CORR-CURSOR.                                            
292100     MOVE '7550-FETCH-OFFER-CORR-CURSOR  '                                
292200       TO PV-PARAGRAPH-NAME.                                              
292300                                                                          
292400     INITIALIZE PV-NULL-INDICATORS.                                       
292500                                                                          
292600     EXEC SQL                                                             
292700         FETCH OFRCRSR                                                    
292800           INTO                                                           
292900                :OMT00001.EVNT-ID                                         
293000               ,:OMT00001.OFR-ID                                          
293100               ,:OMT00001.OFR-NM                                          
293200               ,:OMT00001.DISC-LVL-CDE                                    
293300               ,:OMT00001.OFR-USG-TYP-CDE                                 
293400               ,:OMT00001.BRCDE-TYP-CDE                                   
293500               ,:OMT00001.UNEARN-IND                                      
293600               ,:OMT00001.OFR-TRGR-TYP-CDE                                
293700               ,:OMT00001.ITM-DISC-APPL-CDE                               
293800                :PV-ITM-DISC-APL-NULL-IND                                 
293900               ,:OMT00001.OFR-STAT-CDE                                    
294000               ,:OMT00009.EVNT-TYP-CDE                                    
294100               ,:OMT00007.EVNT-CTG-NM                                     
294200               ,:OMT00002.OFR-SECT-TYP-CDE                                
294300               ,:OMT00002.EFF-BEG-TMST                                    
294400               ,:OMT00002.EFF-END-TMST                                    
294500               ,:OMT00002.TOL-STRT-NMOFDA-QTY                             
294600               ,:OMT00002.TOL-END-NMOFDA-QTY                              
294700               ,:OMT00002.REDMN-SUPVR-OVRRID-NMOFDA-QTY                   
294800               ,:OMT00002.BWP-GET-CNCTR-CDE                               
294900               ,:OMT00002.BWP-BUY-CNCTR-CDE                               
295000               ,:OMT00002.LMT-PER-TRN-QTY                                 
295100               ,:OMT00002.VLD-POS-TNDR-COMB-TYP-CDE                       
295200               ,:OMT00002.POS-BTN-CDE                                     
295300                :PV-POS-BTN-CDE-NULL-IND                                  
295400               ,:OMT00008.CUSTFACG-SHRT-DESC                              
295500               ,:OMT00003.OFR-SECT-BWP-ID                                 
295600               ,:OMT00003.TIER-LVL-NBR                                    
295700               ,:OMT00003.BWP-SELN-TYP-CDE                                
295800               ,:OMT00003.BWP-BUY-QTY                                     
295900                :PV-BWP-BUY-QTY-NULL-IND                                  
296000               ,:OMT00003.BWP-BUY-AMT                                     
296100                :PV-BWP-BUY-AMT-NULL-IND                                  
296200               ,:OMT00003.BWP-BUY-TOL-AMT                                 
296300                :PV-BWP-BUY-TOL-AMT-NULL-IND                              
296400               ,:OMT00003.BWP-GET-QTY                                     
296500                :PV-BWP-GET-QTY-NULL-IND                                  
296600               ,:OMT00003.BWP-GET-PCT-OFF-PCT                             
296700                :PV-BWP-GET-PCT-NULL-IND                                  
296800               ,:OMT00003.BWP-GET-DOL-OFF-AMT                             
296900                :PV-BWP-GET-DOL-NULL-IND                                  
297000               ,:OMT00003.BWP-GET-PRPT-AMT                                
297100                :PV-BWP-GET-PRPT-NULL-IND                                 
297200               ,:OMT00005.MDSE-SET-ID                                     
297300               ,:OMT00005.MDSE-HIER-CDE                                   
297400               ,:OMT00005.DEPT-NBR                                        
297500                :PV-MDSE-DEPT-NBR-NULL-IND                                
297600               ,:OMT00005.MAJ-CL-NBR                                      
297700                :PV-MDSE-MAJ-CLS-NULL-IND                                 
297800               ,:OMT00005.SUB-CL-NBR                                      
297900                :PV-MDSE-SUB-CLS-NULL-IND                                 
298000               ,:OMT00005.STYL-ID                                         
298100                :PV-MDSE-STL-ID-NULL-IND                                  
298200               ,:OMT00005.SKU-NBR                                         
298300                :PV-MDSE-SKU-NB-NULL-IND                                  
298400               ,:OMT00005.IE-IND                                          
298500               ,:OMT00005.EMP-DISC-CDE                                    
298600                :PV-MDSE-EMP-DS-NULL-IND                                  
298700               ,:OMT00005.DISC-ELIG-IND                                   
298800                :PV-MDSE-DSC-EL-NULL-IND                                  
298900               ,:OMT00005.TKT-CLOR-CDE                                    
299000                :PV-MDSE-TKT-CL-NULL-IND                                  
299100               ,:OMT00005.MDSE-DLT-IND                                    
299200               ,:OMT00001.LAST-UPD-TMST                                   
299300               ,:OMT00002.LAST-UPD-TMST                                   
299400               ,:OMT00003.LAST-UPD-TMST                                   
299500               ,:OMT00005.LAST-UPD-TMST                                   
299600               ,:OMT00001.CRE-TMST                                        
299700               ,:OMT00002.CRE-TMST                                        
299800               ,:OMT00003.CRE-TMST                                        
299900               ,:OMT00005.CRE-TMST                                        
300000     END-EXEC.                                                            
300100                                                                          
300200*7550-EXIT.                                                               
300300 EJECT.                                                                   
300400*****************************************************************         
300500* 7600-CLOSE-OFFER-CURSOR.                                      *         
300600*****************************************************************         
300700 7600-CLOSE-OFFER-CURSOR.                                                 
300800     MOVE '7600-CLOSE-OFFER-CURSOR       '                                
300900       TO PV-PARAGRAPH-NAME.                                              
301000                                                                          
301100     PERFORM                                                              
301200         WITH TEST AFTER                                                  
301300         VARYING PV-SQL-SUB                                               
301400         FROM 1 BY 1                                                      
301500         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
301600                   OR SQLCODE = 0)                                        
301700                                                                          
301800             EXEC SQL                                                     
301900                 CLOSE OFRCRSR                                            
302000             END-EXEC                                                     
302100                                                                          
302200             EVALUATE TRUE                                                
302300                 WHEN SQLCODE = 0                                         
302400                     CONTINUE                                             
302500                 WHEN OTHER                                               
302600                     MOVE 'CLOSE CURSOR FOR OFRCRSR'                      
302700                       TO PV-ORA-OPERATION                                
302800                     PERFORM 9200-ORA-ERROR                               
302900             END-EVALUATE                                                 
303000     END-PERFORM.                                                         
303100                                                                          
303200     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
303300         MOVE 'CLOSE OFRCRSR CURSOR RETRIES EXCEEDED'                     
303400           TO PV-ORA-OPERATION                                            
303500         PERFORM 9200-ORA-ERROR                                           
303600     END-IF.                                                              
303700                                                                          
303800*7600-EXIT.                                                               
303900 EJECT.                                                                   
304000                                                                          
304100*****************************************************************         
304200* 7700-DEC-OPEN-STORE-CURSOR-ONE.                               *         
304300*  THIS CURSOR WILL READ THE OFR_LOC_GP TABLE TO CREATE THE     *         
304400*  STORE LIST FILE ENTRIES FOR CORRECTED OFFERS.                *         
304500*****************************************************************         
304600 7700-DEC-OPEN-STORE-CURSOR-ONE.                                          
304700     MOVE '7700-DEC-OPEN-STORE-CURSOR-ONE'                                
304800       TO PV-PARAGRAPH-NAME.                                              
304900                                                                          
305000     EXEC SQL  DECLARE STRCRSR1 CURSOR FOR                                
305100        SELECT                                                            
305200            EVNT_ID                                                       
305300           ,OFR_ID                                                        
305400           ,OFR_SECT_TYP_CDE                                              
305500           ,STR_GP_TYP_CDE                                                
305600           ,STR_GP_ID                                                     
305700           ,IE_IND                                                        
305800           ,LOC_DLT_IND                                                   
305900       FROM OMT_OFR_LOC_GP                                                
306000      WHERE EVNT_ID = :PV-EVNT-ID                                         
306100        AND OFR_ID  = :PV-OFR-ID                                          
306200        AND OFR_SECT_TYP_CDE = :PV-OFR-SECT-TYP-CDE                       
306300        AND LOC_DLT_IND = 'N'                                             
306400      ORDER BY IE_IND DESC                                                
306500              ,STR_GP_TYP_CDE                                             
306600     END-EXEC.                                                            
306700                                                                          
306800     PERFORM                                                              
306900         WITH TEST AFTER                                                  
307000         VARYING PV-SQL-SUB                                               
307100         FROM 1 BY 1                                                      
307200         UNTIL (PV-SQL-SUB GREATER THAN PC-MAX-RETRIES                    
307300                   OR SQLCODE = 0)                                        
307400             EXEC SQL                                                     
307500                 OPEN STRCRSR1                                            
307600             END-EXEC                                                     
307700                                                                          
307800             EVALUATE TRUE                                                
307900                 WHEN SQLCODE = 0                                         
308000                     CONTINUE                                             
308100                 WHEN OTHER                                               
308200                     MOVE 'OPEN CURSOR FOR STRCRSR1'                      
308300                       TO PV-ORA-OPERATION                                
308400                     PERFORM 9200-ORA-ERROR                               
308500             END-EVALUATE                                                 
308600                                                                          
308700     END-PERFORM.                                                         
308800                                                                          
308900     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
309000         MOVE 'OPEN STRCRSR1 RETRIES EXCEEDED'                            
309100           TO PV-ORA-OPERATION                                            
309200         PERFORM 9200-ORA-ERROR                                           
309300     END-IF.                                                              
309400                                                                          
309500*7700-EXIT.                                                               
309600 EJECT.                                                                   
309700                                                                          
309800*****************************************************************         
309900* 7750-FETCH-STORE-CURSOR-ONE.                                  *         
310000*****************************************************************         
310100 7750-FETCH-STORE-CURSOR-ONE.                                             
310200     MOVE '7750-FETCH-STORE-CURSOR-ONE   '                                
310300       TO PV-PARAGRAPH-NAME.                                              
310400                                                                          
310500     EXEC SQL                                                             
310600         FETCH STRCRSR1                                                   
310700           INTO                                                           
310800                :OMT00004.EVNT-ID                                         
310900               ,:OMT00004.OFR-ID                                          
311000               ,:OMT00004.OFR-SECT-TYP-CDE                                
311100               ,:OMT00004.STR-GP-TYP-CDE                                  
311200               ,:OMT00004.STR-GP-ID                                       
311300               ,:OMT00004.IE-IND                                          
311400               ,:OMT00004.LOC-DLT-IND                                     
311500     END-EXEC.                                                            
311600                                                                          
311700     EVALUATE TRUE                                                        
311800         WHEN SQLCODE IS EQUAL TO ZERO                                    
311900             ADD 1                                                        
312000               TO PV-STRCRSR1-READ                                        
312100             IF LOC-DLT-IND IN OMT00004 = 'N'                             
312200                 PERFORM 8800-BUILD-STORE-RECORD                          
312300             END-IF                                                       
312400         WHEN SQLCODE IS EQUAL TO 1403                                    
312500             SET PS-END-OF-STORE-CURSOR-ONE                               
312600               TO TRUE                                                    
312700             PERFORM 7800-CLOSE-STORE-CURSOR-ONE                          
312800         WHEN OTHER                                                       
312900             MOVE 'FETCH DATA FROM STRCRSR1'                              
313000               TO PV-ORA-OPERATION                                        
313100             PERFORM 9200-ORA-ERROR                                       
313200     END-EVALUATE.                                                        
313300                                                                          
313400*7750-EXIT.                                                               
313500 EJECT.                                                                   
313600                                                                          
313700*****************************************************************         
313800* 7800-CLOSE-STORE-CURSOR-ONE.                                  *         
313900*****************************************************************         
314000 7800-CLOSE-STORE-CURSOR-ONE.                                             
314100     MOVE '7800-CLOSE-STORE-CURSOR-ONE   '                                
314200       TO PV-PARAGRAPH-NAME.                                              
314300                                                                          
314400     PERFORM                                                              
314500         WITH TEST AFTER                                                  
314600         VARYING PV-SQL-SUB                                               
314700         FROM 1 BY 1                                                      
314800         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
314900                  OR SQLCODE = 0)                                         
315000                                                                          
315100         EXEC SQL                                                         
315200             CLOSE STRCRSR1                                               
315300         END-EXEC                                                         
315400                                                                          
315500         EVALUATE TRUE                                                    
315600             WHEN SQLCODE = 0                                             
315700                 CONTINUE                                                 
315800             WHEN OTHER                                                   
315900                 MOVE 'CLOSE CURSOR FOR STRCRSR1'                         
316000                   TO PV-ORA-OPERATION                                    
316100                 PERFORM 9200-ORA-ERROR                                   
316200         END-EVALUATE                                                     
316300     END-PERFORM.                                                         
316400                                                                          
316500     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
316600         MOVE 'CLOSE STRCRSR1 CURSOR RETRIEWS EXCEEDED'                   
316700           TO PV-ORA-OPERATION                                            
316800         PERFORM 9200-ORA-ERROR                                           
316900     END-IF.                                                              
317000                                                                          
317100*7800-EXIT.                                                               
317200 EJECT.                                                                   
317300                                                                          
317400*****************************************************************         
317500* 7850-DEC-OPEN-STORE-CURSOR-2.                                 *         
317600*  OPEN STORE CURSOR THAT LOOKS FOR STORE CHANGES TO OFFERS     *         
317700*****************************************************************         
317800 7850-DEC-OPEN-STORE-CURSOR-2.                                            
317900     MOVE '7850-DEC-OPEN-STORE-CURSOR-2  '                                
318000       TO PV-PARAGRAPH-NAME.                                              
318100                                                                          
318200     EXEC SQL DECLARE STRCRSR2 CURSOR FOR                                 
318300       SELECT C.EVNT_ID                                                   
318400             ,C.OFR_ID                                                    
318500             ,C.OFR_NM                                                    
318600             ,C.DISC_LVL_CDE                                              
318700             ,C.OFR_USG_TYP_CDE                                           
318800             ,C.BRCDE_TYP_CDE                                             
318900             ,C.UNEARN_IND                                                
319000             ,C.OFR_TRGR_TYP_CDE                                          
319100             ,C.ITM_DISC_APPL_CDE                                         
319200             ,C.OFR_STAT_CDE                                              
319300             ,B.EVNT_TYP_CDE                                              
319400             ,A.EVNT_CTG_NM                                               
319500             ,D.OFR_SECT_TYP_CDE                                          
319600             ,D.EFF_BEG_TMST                                              
319700             ,D.EFF_END_TMST                                              
319800             ,D.TOL_STRT_NMOFDA_QTY                                       
319900             ,D.TOL_END_NMOFDA_QTY                                        
320000             ,D.REDMN_SUPVR_OVRRID_NMOFDA_QTY                             
320100             ,D.BWP_GET_CNCTR_CDE                                         
320200             ,D.BWP_BUY_CNCTR_CDE                                         
320300             ,D.LMT_PER_TRN_QTY                                           
320400             ,D.VLD_POS_TNDR_COMB_TYP_CDE                                 
320500             ,D.POS_BTN_CDE                                               
320600             ,E.CUSTFACG_SHRT_DESC                                        
320700             ,G.OFR_SECT_BWP_ID                                           
320800             ,G.TIER_LVL_NBR                                              
320900             ,G.BWP_SELN_TYP_CDE                                          
321000             ,G.BWP_BUY_QTY                                               
321100             ,G.BWP_BUY_AMT                                               
321200             ,G.BWP_BUY_TOL_AMT                                           
321300             ,G.BWP_GET_QTY                                               
321400             ,G.BWP_GET_PCT_OFF_PCT                                       
321500             ,G.BWP_GET_DOL_OFF_AMT                                       
321600             ,G.BWP_GET_PRPT_AMT                                          
321700             ,H.MDSE_SET_ID                                               
321800             ,H.MDSE_HIER_CDE                                             
321900             ,H.DEPT_NBR                                                  
322000             ,H.MAJ_CL_NBR                                                
322100             ,H.SUB_CL_NBR                                                
322200             ,H.STYL_ID                                                   
322300             ,H.SKU_NBR                                                   
322400             ,H.IE_IND                                                    
322500             ,H.EMP_DISC_CDE                                              
322600             ,H.DISC_ELIG_IND                                             
322700             ,H.TKT_CLOR_CDE                                              
322800             ,H.MDSE_DLT_IND                                              
322900             ,I.EVNT_ID                                                   
323000             ,I.OFR_ID                                                    
323100             ,I.OFR_SECT_TYP_CDE                                          
323200             ,I.STR_GP_TYP_CDE                                            
323300             ,I.STR_GP_ID                                                 
323400             ,I.IE_IND                                                    
323500             ,I.LOC_DLT_IND                                               
323600             ,C.LAST_UPD_TMST                                             
323700             ,C.CRE_TMST                                                  
323800             ,D.LAST_UPD_TMST                                             
323900             ,D.CRE_TMST                                                  
324000             ,G.LAST_UPD_TMST                                             
324100             ,H.LAST_UPD_TMST                                             
324200             ,I.LAST_UPD_TMST                                             
324300         FROM OMT_EVNT_CTG A                                              
324400             ,OMT_EVNT B                                                  
324500             ,OMT_OFR C                                                   
324600             ,OMT_OFR_SECT D                                              
324700             ,OMT_OFR_CUSTFACG_DESC E                                     
324800             ,OMT_OFR_SECT_BWP G                                          
324900             ,OMT_MDSE_SET H                                              
325000             ,OMT_OFR_LOC_GP I                                            
325100      WHERE   C.EVNT_ID          = B.EVNT_ID                              
325200          AND B.EVNT_CTG_ID      = A.EVNT_CTG_ID                          
325300          AND C.EVNT_ID          = D.EVNT_ID                              
325400          AND C.OFR_ID           = D.OFR_ID                               
325500          AND D.CUSTFACG_DESC_ID = E.CUSTFACG_DESC_ID                     
325600          AND D.OFR_ID           = G.OFR_ID                               
325700          AND D.OFR_SECT_TYP_CDE = G.OFR_SECT_TYP_CDE                     
325800          AND D.OFR_ID           = H.OFR_ID                               
325900          AND G.OFR_SECT_BWP_ID  = H.OFR_SECT_BWP_ID                      
326000          AND D.EVNT_ID          = G.EVNT_ID                              
326100          AND G.OFR_SECT_TYP_CDE = H.OFR_SECT_TYP_CDE                     
326200          AND G.OFR_ID           = H.OFR_ID                               
326300          AND G.EVNT_ID          = H.EVNT_ID                              
326400          AND I.OFR_SECT_TYP_CDE = H.OFR_SECT_TYP_CDE                     
326500          AND I.OFR_ID           = H.OFR_ID                               
326600          AND I.EVNT_ID          = H.EVNT_ID                              
326700          AND G.BWP_DLT_IND      = 'N'                                    
326800          AND H.MDSE_DLT_IND     = 'N'                                    
326900          AND C.OFR_STAT_CDE     = 'A'                                    
327000          AND (I.LAST_UPD_TMST BETWEEN                                    
327100                TO_DATE(:PV-DATE-BEGIN-X,'YYYY-MM-DD HH24:MI:SS')         
327200                 AND                                                      
327300                  TO_DATE(:PV-DATE-END-X,'YYYY-MM-DD HH24:MI:SS'))        
327400      ORDER BY C.OFR_ID                                                   
327500              ,I.STR_GP_TYP_CDE                                           
327600              ,I.STR_GP_ID                                                
327700     END-EXEC.                                                            
327800                                                                          
327900     PERFORM                                                              
328000         WITH TEST AFTER                                                  
328100         VARYING PV-SQL-SUB                                               
328200         FROM 1 BY 1                                                      
328300         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
328400                  OR SQLCODE = 0)                                         
328500                                                                          
328600             EXEC SQL                                                     
328700                 OPEN STRCRSR2                                            
328800             END-EXEC                                                     
328900                                                                          
329000             EVALUATE TRUE                                                
329100                 WHEN SQLCODE = 0                                         
329200                     CONTINUE                                             
329300                 WHEN OTHER                                               
329400                     MOVE 'OPEN CURSOR FOR STRCRSR2    '                  
329500                       TO PV-ORA-OPERATION                                
329600                     PERFORM 9200-ORA-ERROR                               
329700             END-EVALUATE                                                 
329800                                                                          
329900     END-PERFORM.                                                         
330000                                                                          
330100     IF PV-SQL-SUB > PC-MAX-RETRIES                                       
330200         MOVE 'OPEN STRCRSR2 RETRIES EXCEEDED'                            
330300           TO PV-ORA-OPERATION                                            
330400         PERFORM 9200-ORA-ERROR                                           
330500     END-IF.                                                              
330600                                                                          
330700*7850-EXIT.                                                               
330800 EJECT.                                                                   
330900                                                                          
331000*****************************************************************         
331100* 7900-FETCH-STORE-CURSOR-2.                                    *         
331200*   GET DATA FOR CHANGES TO STORE TABLE.                        *         
331300*****************************************************************         
331400 7900-FETCH-STORE-CURSOR-2.                                               
331500     MOVE '7900-FETCH-STORE-CURSOR-2     '                                
331600       TO PV-PARAGRAPH-NAME.                                              
331700                                                                          
331800     INITIALIZE PV-NULL-INDICATORS.                                       
331900                                                                          
332000     EXEC SQL                                                             
332100         FETCH STRCRSR2                                                   
332200           INTO                                                           
332300                :OMT00001.EVNT-ID                                         
332400               ,:OMT00001.OFR-ID                                          
332500               ,:OMT00001.OFR-NM                                          
332600               ,:OMT00001.DISC-LVL-CDE                                    
332700               ,:OMT00001.OFR-USG-TYP-CDE                                 
332800               ,:OMT00001.BRCDE-TYP-CDE                                   
332900               ,:OMT00001.UNEARN-IND                                      
333000               ,:OMT00001.OFR-TRGR-TYP-CDE                                
333100               ,:OMT00001.ITM-DISC-APPL-CDE                               
333200                :PV-ITM-DISC-APL-NULL-IND                                 
333300               ,:OMT00001.OFR-STAT-CDE                                    
333400               ,:OMT00009.EVNT-TYP-CDE                                    
333500               ,:OMT00007.EVNT-CTG-NM                                     
333600               ,:OMT00002.OFR-SECT-TYP-CDE                                
333700               ,:OMT00002.EFF-BEG-TMST                                    
333800               ,:OMT00002.EFF-END-TMST                                    
333900               ,:OMT00002.TOL-STRT-NMOFDA-QTY                             
334000               ,:OMT00002.TOL-END-NMOFDA-QTY                              
334100               ,:OMT00002.REDMN-SUPVR-OVRRID-NMOFDA-QTY                   
334200               ,:OMT00002.BWP-GET-CNCTR-CDE                               
334300               ,:OMT00002.BWP-BUY-CNCTR-CDE                               
334400               ,:OMT00002.LMT-PER-TRN-QTY                                 
334500               ,:OMT00002.VLD-POS-TNDR-COMB-TYP-CDE                       
334600               ,:OMT00002.POS-BTN-CDE                                     
334700                :PV-POS-BTN-CDE-NULL-IND                                  
334800               ,:OMT00008.CUSTFACG-SHRT-DESC                              
334900               ,:OMT00003.OFR-SECT-BWP-ID                                 
335000               ,:OMT00003.TIER-LVL-NBR                                    
335100               ,:OMT00003.BWP-SELN-TYP-CDE                                
335200               ,:OMT00003.BWP-BUY-QTY                                     
335300                :PV-BWP-BUY-QTY-NULL-IND                                  
335400               ,:OMT00003.BWP-BUY-AMT                                     
335500                :PV-BWP-BUY-AMT-NULL-IND                                  
335600               ,:OMT00003.BWP-BUY-TOL-AMT                                 
335700                :PV-BWP-BUY-TOL-AMT-NULL-IND                              
335800               ,:OMT00003.BWP-GET-QTY                                     
335900                :PV-BWP-GET-QTY-NULL-IND                                  
336000               ,:OMT00003.BWP-GET-PCT-OFF-PCT                             
336100                :PV-BWP-GET-PCT-NULL-IND                                  
336200               ,:OMT00003.BWP-GET-DOL-OFF-AMT                             
336300                :PV-BWP-GET-DOL-NULL-IND                                  
336400               ,:OMT00003.BWP-GET-PRPT-AMT                                
336500                :PV-BWP-GET-PRPT-NULL-IND                                 
336600               ,:OMT00005.MDSE-SET-ID                                     
336700               ,:OMT00005.MDSE-HIER-CDE                                   
336800               ,:OMT00005.DEPT-NBR                                        
336900                :PV-MDSE-DEPT-NBR-NULL-IND                                
337000               ,:OMT00005.MAJ-CL-NBR                                      
337100                :PV-MDSE-MAJ-CLS-NULL-IND                                 
337200               ,:OMT00005.SUB-CL-NBR                                      
337300                :PV-MDSE-SUB-CLS-NULL-IND                                 
337400               ,:OMT00005.STYL-ID                                         
337500                :PV-MDSE-STL-ID-NULL-IND                                  
337600               ,:OMT00005.SKU-NBR                                         
337700                :PV-MDSE-SKU-NB-NULL-IND                                  
337800               ,:OMT00005.IE-IND                                          
337900               ,:OMT00005.EMP-DISC-CDE                                    
338000                :PV-MDSE-EMP-DS-NULL-IND                                  
338100               ,:OMT00005.DISC-ELIG-IND                                   
338200                :PV-MDSE-DSC-EL-NULL-IND                                  
338300               ,:OMT00005.TKT-CLOR-CDE                                    
338400                :PV-MDSE-TKT-CL-NULL-IND                                  
338500               ,:OMT00005.MDSE-DLT-IND                                    
338600               ,:OMT00004.EVNT-ID                                         
338700               ,:OMT00004.OFR-ID                                          
338800               ,:OMT00004.OFR-SECT-TYP-CDE                                
338900               ,:OMT00004.STR-GP-TYP-CDE                                  
339000               ,:OMT00004.STR-GP-ID                                       
339100               ,:OMT00004.IE-IND                                          
339200               ,:OMT00004.LOC-DLT-IND                                     
339300               ,:OMT00001.LAST-UPD-TMST                                   
339400               ,:OMT00001.CRE-TMST                                        
339500               ,:OMT00002.LAST-UPD-TMST                                   
339600               ,:OMT00002.CRE-TMST                                        
339700               ,:OMT00003.LAST-UPD-TMST                                   
339800               ,:OMT00005.LAST-UPD-TMST                                   
339900               ,:OMT00004.LAST-UPD-TMST                                   
340000     END-EXEC.                                                            
340100                                                                          
340200*7900-EXIT.                                                               
340300 EJECT.                                                                   
340400                                                                          
340500*****************************************************************         
340600* 7950-CLOSE-STORE-CURSOR-2.                                    *         
340700*****************************************************************         
340800 7950-CLOSE-STORE-CURSOR-2.                                               
340900     MOVE '7950-CLOSE-STORE-CURSOR-2     '                                
341000       TO PV-PARAGRAPH-NAME.                                              
341100                                                                          
341200     PERFORM                                                              
341300         WITH TEST AFTER                                                  
341400         VARYING PV-SQL-SUB                                               
341500         FROM 1 BY 1                                                      
341600         UNTIL (PV-SQL-SUB GREATER PC-MAX-RETRIES                         
341700                   OR SQLCODE = 0)                                        
341800                                                                          
341900             EXEC SQL                                                     
342000                 CLOSE STRCRSR2                                           
342100             END-EXEC                                                     
342200                                                                          
342300             EVALUATE TRUE                                                
342400                 WHEN SQLCODE = 0                                         
342500                     CONTINUE                                             
342600                 WHEN OTHER                                               
342700                     MOVE 'CLOSE CURSOR FOR STRCRSR2'                     
342800                       TO PV-ORA-OPERATION                                
342900                     PERFORM 9200-ORA-ERROR                               
343000             END-EVALUATE                                                 
343100     END-PERFORM.                                                         
343200                                                                          
343300     IF PV-SQL-SUB GREATER PC-MAX-RETRIES                                 
343400         MOVE 'CLOSE STRCRSR2 CURSOR RETRIES EXCEEDED'                    
343500           TO PV-ORA-OPERATION                                            
343600         PERFORM 9200-ORA-ERROR                                           
343700     END-IF.                                                              
343800                                                                          
343900*7950-EXIT.                                                               
344000 EJECT.                                                                   
344100                                                                          
344200*****************************************************************         
344300* 8000-READ-CORRECTION-CARD.                                    *         
344400*     READ CORRECTION EXTRACT DATE RANGE FROM CONTROL CARD      *         
344500*****************************************************************         
344600 8000-READ-CORRECTION-CARD.                                               
344700     MOVE '8000-READ-CORRECTION-CARD     '                                
344800       TO PV-PARAGRAPH-NAME.                                              
344900                                                                          
345000     READ CORR-CARD-FILE                                                  
345100       INTO EP251-CC-CONTROL-CARD                                         
345200         AT END                                                           
345300             SET PS-EOF-CC-CORR-FILE                                      
345400               TO TRUE                                                    
345500     END-READ.                                                            
345600                                                                          
345700     DISPLAY '--------------------------------------------------'.        
345800     DISPLAY '        TIMESTAMP RANGE CONTROL CARD       '.               
345900     DISPLAY EP251-CC-CONTROL-CARD.                                       
346000     DISPLAY '--------------------------------------------------'.        
346100                                                                          
346200*8000-EXIT.                                                               
346300                                                                          
346400*****************************************************************         
346500* 8050-READ-CC-NXT-FUT-DAY.                                     *         
346600*     READ PROCESSING DATE FROM INPUT CONTROL CARD              *         
346700*****************************************************************         
346800 8050-READ-CC-NXT-FUT-DAY.                                                
346900     MOVE '8050-READ-CC-NXT-FUT-DAY      '                                
347000       TO PV-PARAGRAPH-NAME.                                              
347100                                                                          
347200     READ CONTROL-CARD-NXT-FUT-DAY                                        
347300       INTO EP252-CC-CONTROL-CARD                                         
347400         AT END                                                           
347500             SET PS-EOF-CC-NXT-FUT-DAY                                    
347600               TO TRUE                                                    
347700     END-READ.                                                            
347800                                                                          
347900     DISPLAY '                                                  '.        
348000     DISPLAY '--------------------------------------------------'.        
348100     DISPLAY '      NEXT/FUTURE DATE  CONTROL CARD       '.               
348200     DISPLAY EP252-CC-CONTROL-CARD.                                       
348300     DISPLAY '--------------------------------------------------'.        
348400                                                                          
348500*8050-EXIT.                                                               
348600 EJECT.                                                                   
348700                                                                          
348800*****************************************************************         
348900* 8100-WRITE-XST-OFR-DATA.                                      *         
349000*  WRITE OFFER DATA RECORD                                      *         
349100*    ALL RECORD TYPES WILL USE THIS WRITE STATEMENT             *         
349200*****************************************************************         
349300 8100-WRITE-XST-OFR-DATA.                                                 
349400     MOVE '8100-WRITE-XST-OFR-DATA       '                                
349500       TO PV-PARAGRAPH-NAME.                                              
349600                                                                          
349700     MOVE SPACES                                                          
349800       TO OFFERS-CORRECTION-RECORD.                                       
349900     MOVE EP205-OFFER-EXTRACT                                             
350000       TO OFFERS-CORRECTION-RECORD.                                       
350100     WRITE OFFERS-CORRECTION-RECORD.                                      
350200                                                                          
350300*8100-EXIT.                                                               
350400 EJECT.                                                                   
350500                                                                          
350600*****************************************************************         
350700* 8200-WRITE-XST-GRP-DATA.                                      *         
350800*  WRITE GROUP DATA RECORD                                      *         
350900*    ALL RECORD TYPES WILL USE THIS WRITE STATEMENT             *         
351000*****************************************************************         
351100 8200-WRITE-XST-GRP-DATA.                                                 
351200     MOVE '8200-WRITE-XST-GRP-DATA       '                                
351300       TO PV-PARAGRAPH-NAME.                                              
351400                                                                          
351500     MOVE SPACES                                                          
351600       TO OFFERS-CORRECTION-RECORD.                                       
351700     MOVE EP205-OFFER-EXTRACT                                             
351800       TO OFFERS-LOC-GRP-CORR-REC.                                        
351900     WRITE OFFERS-LOC-GRP-CORR-REC.                                       
352000                                                                          
352100*8200-EXIT.                                                               
352200 EJECT.                                                                   
352300                                                                          
352400*****************************************************************         
352500* 8300-CALCULATE-DATES.                                         *         
352600* CALCULATE DATES, BASED ON TOL-STRT-NMOFDA-QTY                 *         
352700*****************************************************************         
352800 8300-CALCULATE-DATES.                                                    
352900     MOVE '8300-CALCULATE-DATES          '                                
353000       TO PV-PARAGRAPH-NAME.                                              
353100                                                                          
353200     IF TOL-STRT-NMOFDA-QTY IN OMT00002 = 0                               
353300         MOVE EFF-BEG-TMST                                                
353400           IN OMT00002                                                    
353500             TO PV-EFF-BEG-TMST                                           
353600         MOVE PV-EFF-BEG-DATE                                             
353700           TO PV-FORMAT-DATE                                              
353800         MOVE PV-FORMAT-DATE-CCYY                                         
353900           TO PV-DATE-CCYY                                                
354000         MOVE PV-FORMAT-DATE-MM                                           
354100           TO PV-DATE-MM                                                  
354200         MOVE PV-FORMAT-DATE-DD                                           
354300           TO PV-DATE-DD                                                  
354400         MOVE PV-EFF-DATE                                                 
354500           TO EP205-EFF-BEG-DTE                                           
354600     ELSE                                                                 
354700         PERFORM 8500-CALC-EFF-BEG-DATE                                   
354800     END-IF.                                                              
354900                                                                          
355000     IF TOL-END-NMOFDA-QTY IN OMT00002 = 0                                
355100         MOVE EFF-END-TMST                                                
355200           IN OMT00002                                                    
355300             TO PV-EFF-END-TMST                                           
355400         MOVE PV-EFF-END-DATE                                             
355500           TO PV-FORMAT-DATE                                              
355600         MOVE PV-FORMAT-DATE-CCYY                                         
355700           TO PV-DATE-CCYY                                                
355800         MOVE PV-FORMAT-DATE-MM                                           
355900           TO PV-DATE-MM                                                  
356000         MOVE PV-FORMAT-DATE-DD                                           
356100           TO PV-DATE-DD                                                  
356200         MOVE PV-EFF-DATE                                                 
356300           TO EP205-EFF-END-DTE                                           
356400     ELSE                                                                 
356500         PERFORM 8600-CALC-EFF-END-DATE                                   
356600     END-IF.                                                              
356700                                                                          
356800     IF REDMN-SUPVR-OVRRID-NMOFDA-QTY IN OMT00002 = 0                     
356900         MOVE EP205-EFF-END-DTE                                           
357000           TO EP205-REDMN-SUPVR-OVRRID-DTE                                
357100     ELSE                                                                 
357200         PERFORM 8700-CALC-SUP-END-DATE                                   
357300     END-IF.                                                              
357400                                                                          
357500*8300-EXIT.                                                               
357600 EJECT.                                                                   
357700                                                                          
357800*****************************************************************         
357900* 8400-POPULATE-BTN-VALUE.                                      *         
358000*****************************************************************         
358100 8400-POPULATE-BTN-VALUE.                                                 
358200     MOVE '8400-POPULATE-BTN-VALUE       '                                
358300       TO PV-PARAGRAPH-NAME.                                              
358400                                                                          
358500     SET PV-BTN-INDEX                                                     
358600       TO 1.                                                              
358700     SEARCH PV-BTN-RECORD                                                 
358800       AT END                                                             
358900          DISPLAY '*** POS-BTN-CDE NOT FOUND IN ARRAY ***'                
359000          MOVE 'SEARCH FOR POS-BTN-CDE FAILED'                            
359100            TO PV-OPERATION-MSG-1                                         
359200          STRING 'POS-BTN-CDE > '                                         
359300            DELIMITED BY SIZE                                             
359400              POS-BTN-CDE                                                 
359500                IN OMT00002                                               
359600                  DELIMITED BY SIZE                                       
359700                    INTO PV-OPERATION-MSG-2                               
359800          SET AC-4008-INVALID-DATA                                        
359900            TO TRUE                                                       
360000          PERFORM 9100-ABEND                                              
360100       WHEN POS-BTN-CDE                                                   
360200              IN OMT00002 > PV-POS-BTN-CDE(PV-BTN-INDEX)                  
360300           MOVE PV-POS-BTN-TEXT(PV-BTN-INDEX)                             
360400             TO EP205-POS-BTN-TXT                                         
360500           MOVE PV-POS-BTN-RLTV-SEQ-NBR(PV-BTN-INDEX)                     
360600             TO EP205-POS-BTN-RLTV-SEQ-NBR-X                              
360700     END-SEARCH.                                                          
360800                                                                          
360900*8400-EXIT.                                                               
361000 EJECT.                                                                   
361100                                                                          
361200*****************************************************************         
361300* 8500-CALC-EFF-BEG-DATE.                                       *         
361400* THIS PARAGRAPH WILL USE THE KOHLS DATE ROUTINE TO CALCULATE   *         
361500* THE EFFECTIVE BEGIN DATE FOR THE POS STORE SERVER DB.         *         
361600* WHICH WILL BE THE (EFF-BEG-TMST - TOL-START-QTY               *         
361700*****************************************************************         
361800 8500-CALC-EFF-BEG-DATE.                                                  
361900     MOVE '8500-CALC-EFF-BEG-DATE        '                                
362000       TO PV-PARAGRAPH-NAME.                                              
362100                                                                          
362200     MOVE EFF-BEG-TMST                                                    
362300       IN OMT00002                                                        
362400         TO PV-EFF-BEG-TMST.                                              
362500     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
362600     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
362700       TO TRUE.                                                           
362800     SET DPG51-DECREMENT-DATE                                             
362900       TO TRUE.                                                           
363000     MOVE TOL-STRT-NMOFDA-QTY                                             
363100       IN OMT00002                                                        
363200         TO DPG51-INCR-DECR-DAYS-9.                                       
363300     SET DPG52-LK-DTE-ISO-FMT                                             
363400       TO TRUE.                                                           
363500     MOVE PV-EFF-BEG-DATE                                                 
363600       TO DPG52-LK-DATE-INPUT.                                            
363700                                                                          
363800     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
363900       USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
364000                                                                          
364100     EVALUATE TRUE                                                        
364200         WHEN DPG54-NO-ERROR                                              
364300             CONTINUE                                                     
364400         WHEN OTHER                                                       
364500             DISPLAY ' '                                                  
364600             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
364700             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
364800             DISPLAY '** DATE  INPUT TO CALENDAR ROUTINE  :  '            
364900             DISPLAY '** DPG52-LK-DATE-INPUT ' DPG52-LK-DATE-INPUT        
365000             DISPLAY '** DATE  OUTPUT FROM THE ROUTINE    :  '            
365100             DISPLAY '** DPG55-DB2-ISO-DATE ' DPG55-DB2-ISO-DATE          
365200             MOVE '8500-CALC-EFF-BEG-DATE'                                
365300               TO PV-PARAGRAPH-NAME                                       
365400             MOVE 'ERROR CALCULATING EFF BEGIN DATE'                      
365500               TO PV-OPERATION-MSG-1                                      
365600             SET AC-4019-CAL-SUB-ERROR                                    
365700               TO TRUE                                                    
365800             MOVE DPG54-ERROR-MESSAGE                                     
365900               TO PV-OPERATION-MSG-2                                      
366000             PERFORM 9100-ABEND                                           
366100         END-EVALUATE.                                                    
366200                                                                          
366300     MOVE DPG55-DB2-ISO-DATE                                              
366400       TO EP205-EFF-BEG-DTE.                                              
366500                                                                          
366600*8500-EXIT.                                                               
366700 EJECT.                                                                   
366800                                                                          
366900*****************************************************************         
367000* 8600-CALC-EFF-END-DATE.                                       *         
367100* THIS PARAGRAPH WILL USE THE KOHLS DATE ROUTINE TO CALCULATE   *         
367200* THE EFFECTIVE END DATE FOR THE POS STORE SERVER DB.           *         
367300*****************************************************************         
367400 8600-CALC-EFF-END-DATE.                                                  
367500     MOVE '8600-CALC-EFF-END-DATE        '                                
367600       TO PV-PARAGRAPH-NAME.                                              
367700                                                                          
367800     MOVE EFF-END-TMST                                                    
367900       IN OMT00002                                                        
368000         TO PV-EFF-END-TMST.                                              
368100     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
368200     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
368300       TO TRUE.                                                           
368400     SET DPG51-INCREMENT-DATE                                             
368500       TO TRUE.                                                           
368600     MOVE TOL-END-NMOFDA-QTY                                              
368700       IN OMT00002                                                        
368800         TO DPG51-INCR-DECR-DAYS-9.                                       
368900     SET DPG52-LK-DTE-ISO-FMT                                             
369000       TO TRUE.                                                           
369100     MOVE PV-EFF-END-DATE                                                 
369200       TO DPG52-LK-DATE-INPUT.                                            
369300                                                                          
369400     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
369500       USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
369600                                                                          
369700     EVALUATE TRUE                                                        
369800         WHEN DPG54-NO-ERROR                                              
369900             CONTINUE                                                     
370000         WHEN OTHER                                                       
370100             DISPLAY ' '                                                  
370200             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
370300             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
370400             DISPLAY '** DATE  INPUT TO CALENDAR ROUTINE  :  '            
370500             DISPLAY '** DPG52-LK-DATE-INPUT ' DPG52-LK-DATE-INPUT        
370600             DISPLAY '** DATE  OUTPUT FROM THE ROUTINE    :  '            
370700             DISPLAY '** DPG55-DB2-ISO-DATE ' DPG55-DB2-ISO-DATE          
370800             MOVE '8600-CALC-EFF-END-DATE'                                
370900               TO PV-PARAGRAPH-NAME                                       
371000             MOVE 'ERROR CALCULATING EFF END DATE'                        
371100               TO PV-OPERATION-MSG-1                                      
371200             SET AC-4019-CAL-SUB-ERROR                                    
371300               TO TRUE                                                    
371400             MOVE DPG54-ERROR-MESSAGE                                     
371500               TO PV-OPERATION-MSG-2                                      
371600             PERFORM 9100-ABEND                                           
371700         END-EVALUATE.                                                    
371800                                                                          
371900     MOVE DPG55-DB2-ISO-DATE                                              
372000       TO EP205-EFF-END-DTE.                                              
372100                                                                          
372200*8600-EXIT.                                                               
372300 EJECT.                                                                   
372400                                                                          
372500*****************************************************************         
372600* 8700-CALC-SUP-END-DATE.                                       *         
372700*  THIS PARAGRAPH WILL USE THE KOHLS DATE ROUTINE TO CALCULATE  *         
372800*  THE SUPERVISOR END DATE FOR THE POS STORE SERVER DB.         *         
372900*****************************************************************         
373000 8700-CALC-SUP-END-DATE.                                                  
373100     MOVE '8700-CALC-SUP-END-DATE        '                                
373200       TO PV-PARAGRAPH-NAME.                                              
373300                                                                          
373400     MOVE EFF-END-TMST                                                    
373500       IN OMT00002                                                        
373600         TO PV-EFF-END-TMST.                                              
373700     INITIALIZE DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                      
373800     SET DPG51-ACTUAL-CALENDAR-ONLY                                       
373900       TO TRUE.                                                           
374000     SET DPG51-INCREMENT-DATE                                             
374100       TO TRUE.                                                           
374200     ADD TOL-END-NMOFDA-QTY                                               
374300       IN OMT00002                                                        
374400         TO REDMN-SUPVR-OVRRID-NMOFDA-QTY                                 
374500           IN OMT00002.                                                   
374600     MOVE REDMN-SUPVR-OVRRID-NMOFDA-QTY                                   
374700       IN OMT00002                                                        
374800         TO DPG51-INCR-DECR-DAYS-9.                                       
374900     SET DPG52-LK-DTE-ISO-FMT                                             
375000       TO TRUE.                                                           
375100     MOVE PV-EFF-END-DATE                                                 
375200       TO DPG52-LK-DATE-INPUT.                                            
375300                                                                          
375400     CALL DP500-KOHLS-CALENDAR-ROUTINE                                    
375500       USING DPG51 DPG52 DPG53 DPG54 DPG55 DPG56.                         
375600                                                                          
375700     EVALUATE TRUE                                                        
375800         WHEN DPG54-NO-ERROR                                              
375900             CONTINUE                                                     
376000         WHEN OTHER                                                       
376100             DISPLAY ' '                                                  
376200             DISPLAY '** INVALID RETURN FROM CALENDAR ROUTINE'            
376300             DISPLAY '** ERROR MESSAGE = ' DPG54-ERROR-MESSAGE            
376400             DISPLAY '** DATE  INPUT TO CALENDAR ROUTINE  :  '            
376500             DISPLAY '** DPG52-LK-DATE-INPUT ' DPG52-LK-DATE-INPUT        
376600             DISPLAY '** DATE  OUTPUT FROM THE ROUTINE    :  '            
376700             DISPLAY '** DPG55-DB2-ISO-DATE ' DPG55-DB2-ISO-DATE          
376800             MOVE '8700-CALC-SUP-END-DATE'                                
376900               TO PV-PARAGRAPH-NAME                                       
377000             MOVE 'ERROR CALCULATING SUP OVERRIDE END DATE'               
377100               TO PV-OPERATION-MSG-1                                      
377200             SET AC-4019-CAL-SUB-ERROR                                    
377300               TO TRUE                                                    
377400             MOVE DPG54-ERROR-MESSAGE                                     
377500               TO PV-OPERATION-MSG-2                                      
377600             PERFORM 9100-ABEND                                           
377700         END-EVALUATE.                                                    
377800                                                                          
377900     MOVE DPG55-DB2-ISO-DATE                                              
378000       TO EP205-REDMN-SUPVR-OVRRID-DTE.                                   
378100                                                                          
378200*8700-EXIT.                                                               
378300 EJECT.                                                                   
378400                                                                          
378500                                                                          
378600*****************************************************************         
378700* 8800-BUILD-STORE-RECORD.                                       *        
378800*   BUILD THE OUTPUT RECORD FOR THE STORE FILE                  *         
378900*****************************************************************         
379000 8800-BUILD-STORE-RECORD.                                                 
379100     MOVE '8800-BUILD-STORE-RECORD       '                                
379200       TO PV-PARAGRAPH-NAME.                                              
379300                                                                          
379400     INITIALIZE EP206-STORE-LIST-RCD.                                     
379500                                                                          
379600     MOVE EP205-TRIGGER-TYPE                                              
379700       TO EP206-ADD-DLT-FLG.                                              
379800     MOVE EVNT-ID                                                         
379900       IN OMT00004                                                        
380000         TO EP206-EVENT-ID.                                               
380100     MOVE OFR-ID                                                          
380200       IN OMT00004                                                        
380300         TO EP206-OFFER-ID.                                               
380400     MOVE OFR-SECT-TYP-CDE                                                
380500       IN OMT00004                                                        
380600         TO EP206-OFR-SECT-TYP-CDE.                                       
380700     MOVE STR-GP-TYP-CDE                                                  
380800       IN OMT00004                                                        
380900         TO EP206-STR-GRP-TYP-CDE.                                        
381000     MOVE STR-GP-ID                                                       
381100       IN OMT00004                                                        
381200         TO EP206-STR-GRP-ID.                                             
381300     MOVE IE-IND                                                          
381400       IN OMT00004                                                        
381500         TO EP206-IE-IND.                                                 
381600                                                                          
381700     PERFORM 8900-WRITE-STORE-RECORD.                                     
381800                                                                          
381900*8800-EXIT.                                                               
382000 EJECT.                                                                   
382100                                                                          
382200*****************************************************************         
382300* 8900-WRITE-STORE-RECORD.                                      *         
382400*****************************************************************         
382500 8900-WRITE-STORE-RECORD.                                                 
382600     MOVE '8900-WRITE-STORE-RECORD      '                                 
382700       TO PV-PARAGRAPH-NAME.                                              
382800                                                                          
382900     WRITE CORRECTION-STORE-LIST-RECORD                                   
383000       FROM EP206-STORE-LIST-RCD.                                         
383100                                                                          
383200     ADD 1                                                                
383300       TO PV-STR-LIST-RECS-OUT.                                           
383400                                                                          
383500*8900-EXIT.                                                               
383600 EJECT.                                                                   
383700                                                                          
383800*****************************************************************         
383900*    DATA RANGE VALIDATION ROUTINE AND NETCOOL ALERT CALL       *         
384000*****************************************************************         
384100     COPY EPPD208.                                                        
384200                                                                          
384300*****************************************************************         
384400* 9100-ABEND.                                                   *         
384500*     ABEND PROGRAM WHEN A NON-ORACLE ERROR OCCURS              *         
384600*****************************************************************         
384700 9100-ABEND.                                                              
384800                                                                          
384900     PERFORM 9600-DISPLAY-TOTALS.                                         
385000     PERFORM 9700-GET-CURRENT.                                            
385100                                                                          
385200     DISPLAY '** ABEND            **'.                                    
385300     DISPLAY '** PROGRAM          **' PC-PROGRAM-NAME.                    
385400     DISPLAY '** PARAGRAPH        **' PV-PARAGRAPH-NAME.                  
385500     DISPLAY '** OPERATION        **' PV-OPERATION-MSG-1.                 
385600     DISPLAY '**                  **' PV-OPERATION-MSG-2.                 
385700     DISPLAY '** TIME             **' DF-DISPLAY-TIME.                    
385800                                                                          
385900     CALL 'ILBOABN0'                                                      
386000       USING AC-ABEND-CODE.                                               
386100                                                                          
386200*9100-EXIT.                                                               
386300                                                                          
386400*****************************************************************         
386500* 9200-ORA-ERROR.                                               *         
386600*     ORACLE ABEND ROUTINE                                      *         
386700*****************************************************************         
386800 9200-ORA-ERROR.                                                          
386900                                                                          
387000     EXEC SQL                                                             
387100        ROLLBACK                                                          
387200     END-EXEC.                                                            
387300                                                                          
387400     PERFORM 9600-DISPLAY-TOTALS.                                         
387500     PERFORM 9700-GET-CURRENT.                                            
387600                                                                          
387700     SET AC-4013-ORACLE-ERROR                                             
387800       TO TRUE.                                                           
387900                                                                          
388000     DISPLAY '** ABEND            **'.                                    
388100     DISPLAY '** PROGRAM          **' PC-PROGRAM-NAME.                    
388200     DISPLAY '** PARAGRAPH        **' PV-PARAGRAPH-NAME.                  
388300     DISPLAY '** OPERATION        **' PV-ORA-OPERATION.                   
388400     DISPLAY '** TIME             **' DF-DISPLAY-TIME.                    
388500                                                                          
388600     COPY DPPD004.                                                        
388700     CALL 'ILBOABN0'                                                      
388800       USING AC-ABEND-CODE.                                               
388900                                                                          
389000*9200-EXIT.                                                               
389100                                                                          
389200*****************************************************************         
389300* 9500-WRAPUP.                                                  *         
389400*****************************************************************         
389500 9500-WRAPUP.                                                             
389600                                                                          
389700     MOVE '9500-WRAPUP                   '                                
389800       TO PV-PARAGRAPH-NAME.                                              
389900                                                                          
390000     PERFORM EP208-1200-DETERMINE-IF-ALERT.                               
390100     PERFORM 9600-DISPLAY-TOTALS.                                         
390200                                                                          
390300     PERFORM 9700-GET-CURRENT.                                            
390400                                                                          
390500     DISPLAY '*******************************************'.               
390600     DISPLAY '**             EPKUT205 END              **'.               
390700     DISPLAY '*******************************************'.               
390800     DISPLAY '**         END DATE ' DF-DISPLAY-DATE                       
390900             '           **'.                                             
391000     DISPLAY '**         END TIME ' DF-DISPLAY-TIME                       
391100             '             **'.                                           
391200     DISPLAY '***********************************************'.           
391300                                                                          
391400     MOVE 'Y' TO EP251-CC-LAST-EXEC-IND.                                  
391500                                                                          
391600     WRITE TEMP-CORRECTION-CARD-RECORD                                    
391700       FROM EP251-CC-CONTROL-CARD.                                        
391800                                                                          
391900     CLOSE                                                                
392000         CORR-CARD-FILE                                                   
392100         TEMP-CORR-CARD-FILE                                              
392200         CONTROL-CARD-NXT-FUT-DAY                                         
392300         OFFERS-CORR-FILE                                                 
392400         CORR-STORE-LIST-FILE                                             
392500         OFFERS-LOC-GRP-CORR-FILE                                         
392600         CORR-STR-GRP-LIST-FILE.                                          
392700                                                                          
392800                                                                          
392900*9500-WRAPUP.                                                             
393000 EJECT.                                                                   
393100                                                                          
393200*****************************************************************         
393300*     DISPLAY END OF JOB TOTALS                                 *         
393400*****************************************************************         
393500 9600-DISPLAY-TOTALS.                                                     
393600                                                                          
393700      DISPLAY '*******************************************'.              
393800      DISPLAY '**     EOJ STATISTICS FOR EPKUT205       **'.              
393900      DISPLAY '*******************************************'               
394000                                                                          
394100      DISPLAY '** OFFER CURSOR RECORDS READ =>'                           
394200              PV-OFFER-CURSOR-READ                                        
394300              ' **'.                                                      
394400                                                                          
394500      DISPLAY '** MDSE  CURSOR RECORDS READ =>'                           
394600              PV-MDSE-CURSOR-READ                                         
394700              ' **'.                                                      
394800                                                                          
394900      DISPLAY '** OFFER STORE1 RECORDS READ =>'                           
395000              PV-STRCRSR1-READ                                            
395100              ' **'.                                                      
395200                                                                          
395300      DISPLAY '** OFFER STORE2 RECORDS READ =>'                           
395400              PV-STRCRSR2-READ                                            
395500              ' **'.                                                      
395600                                                                          
395700      DISPLAY '** OFFER RECORDS WRITTEN =====>'                           
395800              PV-OFFER-RECS-OUT                                           
395900              ' **'.                                                      
396000                                                                          
396100      DISPLAY '** OFFER SECT RCDS WRITTEN ===>'                           
396200              PV-OFRSECT-RECS-OUT                                         
396300              ' **'.                                                      
396400                                                                          
396500      DISPLAY '** OFFER BWP RCDS WRITTEN ====>'                           
396600              PV-OFRBWP-RECS-OUT                                          
396700              ' **'.                                                      
396800                                                                          
396900      DISPLAY '** MDSE SET RCDS WRITTEN =====>'                           
397000              PV-MDSESET-RECS-OUT                                         
397100              ' **'.                                                      
397200                                                                          
397300      DISPLAY '** GROUP OFR REC WRITTEN =====>'                           
397400              PV-OFFER-STR-RECS-OUT                                       
397500              ' **'.                                                      
397600                                                                          
397700      DISPLAY '** GROUP SEC RECS WRITTEN ====>'                           
397800              PV-OFRSECT-STR-RECS-OUT                                     
397900              ' **'.                                                      
398000                                                                          
398100      DISPLAY '** GROUP BWP RCDS WRITTEN ====>'                           
398200              PV-OFRBWP-STR-RECS-OUT                                      
398300              ' **'.                                                      
398400                                                                          
398500      DISPLAY '** GROUP MDSE RCDS WRITTEN ===>'                           
398600              PV-MDSESET-STR-RECS-OUT                                     
398700              ' **'.                                                      
398800                                                                          
398900      DISPLAY '** STR LIST RCDS WRITTEN =====>'                           
399000              PV-STR-LIST-RECS-OUT                                        
399100              ' **'.                                                      
399200                                                                          
399300      DISPLAY '** ERROR RECORDS =============>'                           
399400             EP208-ERROR-RCD-COUNT                                        
399500              ' **'.                                                      
399600                                                                          
399700      DISPLAY '** ALERT RECORDS =============>'                           
399800             EP208-ALERT-RECS-OUT                                         
399900              ' **'.                                                      
400000                                                                          
400100*9600-EXIT.                                                               
400200 EJECT.                                                                   
400300                                                                          
400400*****************************************************************         
400500*     GET CURRENT DATE AND TIME                                 *         
400600*****************************************************************         
400700 9700-GET-CURRENT.                                                        
400800                                                                          
400900     MOVE FUNCTION CURRENT-DATE(1:16)                                     
401000       TO DF-CURRENT-DATE-TIME.                                           
401100                                                                          
401200     DISPLAY '  DF-CURRENT-DATE-TIME  ' DF-CURRENT-DATE-TIME              
401300                                                                          
401400     MOVE DF-CURRENT-YR TO DF-DD-YR.                                      
401500     MOVE DF-CURRENT-MO TO DF-DD-MO.                                      
401600     MOVE DF-CURRENT-DY TO DF-DD-DY.                                      
401700     MOVE DF-CURRENT-HR TO DF-DD-HR.                                      
401800     MOVE DF-CURRENT-MIN TO DF-DD-MIN.                                    
401900     MOVE DF-CURRENT-SEC TO DF-DD-SEC.                                    
402000                                                                          
402100*9700-EXIT.                                                               
402200                                                                          
