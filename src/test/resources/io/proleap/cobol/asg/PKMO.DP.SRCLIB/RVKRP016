000100 IDENTIFICATION DIVISION.                                                 
000200 PROGRAM-ID.    RVKRP016.                                                 
000300 AUTHOR.        VIRGENE LENNEMAN.                                         
000400 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000500 DATE-WRITTEN.  MAY 2007.                                                 
000600 DATE-COMPILED.                                                           
000700******************************************************************        
000800*  RVKRP016 WILL PRINT A PHYSICAL INVENTORY REPORT BY DEPARTMENT *        
000900*  AND CLASS WITH TOTAL BREAKS FOR DEPARTMENT CHANGES AND A      *        
001000*  GRAND TOTAL AT THE END OF THE REPORT.                         *        
001100******************************************************************        
W40134*--------------------------------------------------------------*          
W40134* HISTORY LOG:                                                            
W40134*--------------------------------------------------------------*          
W40134* DATE:        2011/10/30                                                 
W40134* PROJ#:       3219  GENCO/RTV UPGRADE PROJECT                            
W40134* WR/IR#:      WR40134                                                    
W40134* PROGRAMMER:  STEVE ANDERSON - CODEWORKS                                 
W40134* DESCRIPTION: USING NEW RV022-RTV-LOC LOCATION, SELECT FROM              
W40134*              XLT_LOC TABLE.  FORMAT REPORT FOR RTV LOCATION             
W40134*              AND NAME.                                                  
W40134*--------------------------------------------------------------*          
001200*                                                                         
001300 ENVIRONMENT DIVISION.                                                    
001400*                                                                         
001500 CONFIGURATION SECTION.                                                   
001600*                                                                         
001700 SOURCE-COMPUTER.        IBM-3090.                                        
001800 OBJECT-COMPUTER.        IBM-3090.                                        
001900*                                                                         
002000                                                                          
002100 INPUT-OUTPUT SECTION.                                                    
002200                                                                          
002300 FILE-CONTROL.                                                            
002400                                                                          
002500     SELECT INVENTORY-CNT-FILE ASSIGN TO INP01.                           
002700                                                                          
002800     SELECT RTV-INV-CNT-REPORT ASSIGN TO RPT01.                           
003000                                                                          
003100******************************************************************        
003200 DATA DIVISION.                                                           
003300******************************************************************        
003400                                                                          
003500 FILE SECTION.                                                            
003600                                                                          
003700 FD  INVENTORY-CNT-FILE                                                   
003800     RECORDING MODE IS F                                                  
003900     LABEL RECORDS ARE STANDARD                                           
004000     BLOCK CONTAINS 0 RECORDS.                                            
004100                                                                          
004200 01  INVENTORY-CNT-RECORD                PIC  X(175).                     
004300                                                                          
004400 FD  RTV-INV-CNT-REPORT                                                   
004500     RECORDING MODE IS F                                                  
004600     LABEL RECORDS ARE STANDARD                                           
004700     BLOCK CONTAINS 0 RECORDS.                                            
004800                                                                          
004900 01  RTV-INV-CNT-LINE                    PIC  X(132).                     
005000                                                                          
005100******************************************************************        
005200 WORKING-STORAGE SECTION.                                                 
005300******************************************************************        
005400                                                                          
026400 01  ABEND-CODE                          PIC S9(04) VALUE +0              
026500                                                     COMP SYNC.           
025800****************************************************************          
025900*  LAYOUT FOR THE STANDARD 132 WIDE REPORT HEADER                         
026000****************************************************************          
026100     COPY DPWS132.                                                        
026310                                                                          
026600****************************************************************          
026700*  RTV SKU INFO                                                           
026800****************************************************************          
026900     COPY RVRD024.                                                        
027000                                                                          
027690****************************************************************          
027700*  WORK AREA FOR MBS INFORMATION ROUTINE.                                 
027800****************************************************************          
027920     COPY DPWS003.                                                        
028030                                                                          
027100*****************************************************************         
027200** WORK AREA FOR SQL ERROR ROUTINE 'DSNTIAR'                              
027300*****************************************************************         
027400     COPY DPWS004.                                                        
027500                                                                          
W40134******************************************************************        
W40134*  DB2 TABLE DECLARATION                                                  
W40134*  (XLT_LOC    - LOGISTICS LOCATION TABLE)                                
W40134******************************************************************        
W40134     EXEC SQL                                                             
W40134         INCLUDE XLT00010                                                 
W40134     END-EXEC.                                                            
W40134                                                                          
028110****************************************************************          
028200*   DB2 COMMUNICATIONS AREA                                               
028310****************************************************************          
028400      EXEC SQL                                                            
028500          INCLUDE SQLCA                                                   
028600      END-EXEC.                                                           
028700                                                                          
013200******************************************************************        
013300* PC PROGRAM CONSTANTS                                           *        
013400******************************************************************        
013500 01  PC-PROGRAM-CONSTANTS.                                                
014000     05  PC-MAX-DETAIL-LINES          PIC S9(03) VALUE +56 COMP.          
014200     05  PC-CURRENT-PROGRAM-NAME      PIC  X(08) VALUE 'RVKRP016'.        
014490     05  PC-MBS-PGM                   PIC  X(08) VALUE 'DPKUT039'.        
014600     05  PC-REPORT-NBR                PIC  9(01)  VALUE 1.                
014800                                                                          
005500******************************************************************        
005600* PS PROGRAM SWITCHES                                            *        
005700******************************************************************        
005800 01  PS-PROGRAM-SWITCHES.                                                 
005900     05  PS-END-OF-INV-CNT-FILE-IND      PIC  X(01)  VALUE 'N'.           
006000         88  PS-END-OF-INV-CNT-FILE                  VALUE 'Y'.           
006100         88  PS-NOT-END-OF-INV-CNT-FILE              VALUE 'N'.           
006200                                                                          
006300     05  PS-NEW-DEPT-SW                  PIC X(01)   VALUE 'N'.           
006400         88  PS-NEW-DEPT                             VALUE 'Y'.           
006500         88  PS-NO-NEW-DEPT                          VALUE 'N'.           
006600                                                                          
006700******************************************************************        
006800* PH PROGRAM HOLD AREA                                           *        
006900******************************************************************        
007000 01  PH-PROGRAM-HOLD-AREA.                                                
017100     05  PH-DEPT                         PIC  X(03)  VALUE SPACE.         
017200     05  PH-CLASS                        PIC  X(02)  VALUE SPACE.         
017300     05  PH-AVAILABLE                    PIC  9(09)  VALUE 0.             
017400     05  PH-UNIT-RTL-AMT                 PIC S9(09)V99  VALUE +0          
017500                                                     COMP-3.              
017600     05  PH-DEPT-CNT                     PIC  9(09)  VALUE 0.             
017700     05  PH-DEPT-RTL-AMT                 PIC S9(09)V99                    
017800                                                     VALUE +0.            
017900     05  PH-FINAL-CNT                    PIC  9(09)  VALUE 0.             
018000     05  PH-FINAL-RTL-AMT                PIC S9(09)V99                    
018100                                                     VALUE +0.            
W40134     05  PH-LOC-NBR                      PIC S9(04) VALUE +0 COMP.        
008200                                                                          
008300******************************************************************        
008400* PV PROGRAM VARIABLES                                           *        
008500******************************************************************        
008600 01  PV-PROGRAM-VARIABLES.                                                
008700     05  PV-PARAGRAPH-NAME               PIC  X(30)  VALUE SPACE.         
008800     05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(30)  VALUE SPACE.         
008900     05  PV-DB2-DATE                     PIC  X(10)  VALUE SPACES.        
009000     05  PV-DB2-DATE-EXT REDEFINES PV-DB2-DATE.                           
009100         10  PV-DB2-DATE-CCYY.                                            
009100             15  PV-DB2-DATE-CC              PIC  X(02).                  
009200             15  PV-DB2-DATE-YY              PIC  X(02).                  
009300         10  FILLER                      PIC  X(01).                      
009400         10  PV-DB2-DATE-MM              PIC  X(02).                      
009500         10  FILLER                      PIC  X(01).                      
009600         10  PV-DB2-DATE-DD              PIC  X(02).                      
010300     05  PV-DB2-TIME                     PIC  X(08)  VALUE SPACES.        
010400     05  PV-DB2-TIME-EXT REDEFINES PV-DB2-TIME.                           
010500         10  PV-DB2-TIME-HH              PIC  X(02).                      
010600         10  FILLER                      PIC  X(01).                      
010700         10  PV-DB2-TIME-MM              PIC  X(02).                      
010800         10  FILLER                      PIC  X(01).                      
010900         10  PV-DB2-TIME-SS              PIC  X(02).                      
010100     05  PV-WORK-DATE.                                                    
009800         10  PV-WORK-DATE-YY             PIC  9(02)  VALUE ZERO.          
009900         10  PV-WORK-DATE-MM             PIC  9(02)  VALUE ZERO.          
010000         10  PV-WORK-DATE-DD             PIC  9(02)  VALUE ZERO.          
011500     05  PV-LINE-COUNT                   PIC  9(02)  VALUE ZERO.          
011700     05  PV-PAGE-COUNT                   PIC  9(04)  VALUE ZERO.          
011700     05  PV-RECS-READ                    PIC  9(09)  VALUE ZERO.          
012000     05  PV-MBS-DISPLAY.                                                  
012100         10 FILLER                       PIC X(29)  VALUE                 
012200            '** ERROR CALLING MBS ROUTINE '.                              
012300         10 PV-MBS-CALL                  PIC X(20)  VALUE SPACE.          
012400                                                                          
012500     05  PV-MBS-MCL.                                                      
012600         10 PV-MBS-MCL-1                 PIC 9(01)  VALUE 0.              
012700         10 PV-MBS-MCL-0                 PIC 9(01)  VALUE 0.              
012800                                                                          
012900     05  PV-MBS-SCL.                                                      
013000         10 PV-MBS-SCL-0                 PIC 9(01)  VALUE 0.              
013100         10 PV-MBS-SCL-2                 PIC 9(02)  VALUE 00.             
011900                                                                          
014900******************************************************************        
015000* HL  HEADER LINES FOR DOCUMENT                                  *        
015100******************************************************************        
015200 01  HL-BLANK-LINE.                                                       
015300     05  FILLER                    PIC  X(80)    VALUE SPACES.            
015400                                                                          
015500 01  HL01-HEADER-LINE-01.                                                 
015600     05  FILLER                    PIC  X(28)     VALUE SPACES.           
015700     05  FILLER                    PIC  X(42)     VALUE                   
015800         'DEPARTMENT/CLASS SUMMARY REPORT OF RETURN-'.                    
015900     05  FILLER                    PIC  X(42)     VALUE                   
016000         'TO-VENDOR PHYSICAL INVENTORY COUNTS       '.                    
016100     05  FILLER                    PIC  X(20)     VALUE SPACES.           
016200                                                                          
W40134 01  HL02-HEADER-LINE-02.                                                 
W40134     05  FILLER                    PIC  X(17)  VALUE                      
W40134         '  RTV LOCATION:  '.                                             
W40134     05  HL02-RTV-LOC              PIC  X(06).                            
W40134     05  FILLER                    PIC  X(02)  VALUE SPACE.               
W40134     05  HL02-RTV-NAME             PIC  X(100).                           
016200                                                                          
016300 01  DH01-DETAIL-HEADING-01.                                              
016400     05  FILLER                    PIC  X(14)     VALUE SPACES.           
016500     05  FILLER                    PIC  X(15)     VALUE                   
016600         'DPT  DEPARTMENT'.                                               
016700     05  FILLER                    PIC  X(17)     VALUE SPACES.           
016800     05  FILLER                    PIC  X(09)     VALUE                   
016900         'CLS CLASS'.                                                     
017000     05  FILLER                    PIC  X(24)     VALUE SPACES.           
017100     05  FILLER                    PIC  X(09)     VALUE                   
017200         'INVENTORY'.                                                     
017300     05  FILLER                    PIC  X(07)     VALUE SPACES.           
017400     05  FILLER                    PIC  X(09)     VALUE                   
017500         'INVENTORY'.                                                     
017600     05  FILLER                    PIC  X(28)     VALUE SPACES.           
017700                                                                          
017800 01  DH02-DETAIL-HEADING-02.                                              
017900     05  FILLER                    PIC  X(14)     VALUE SPACES.           
018000     05  FILLER                    PIC  X(16)     VALUE                   
018100         'NBR  DESCRIPTION'.                                              
018200     05  FILLER                    PIC  X(16)     VALUE SPACES.           
018300     05  FILLER                    PIC  X(15)     VALUE                   
018400         'NBR DESCRIPTION'.                                               
018500     05  FILLER                    PIC  X(20)     VALUE SPACES.           
018600     05  FILLER                    PIC  X(05)     VALUE                   
018700         'COUNT'.                                                         
018800     05  FILLER                    PIC  X(11)     VALUE SPACES.           
018900     05  FILLER                    PIC  X(06)     VALUE                   
019000         'RETAIL'.                                                        
019100     05  FILLER                    PIC  X(29)     VALUE SPACES.           
019200                                                                          
019300 01  DH03-DETAIL-HEADING-03.                                              
019400     05  FILLER                    PIC  X(14)     VALUE SPACES.           
019500     05  FILLER                    PIC  X(03)     VALUE '---'.            
019600     05  FILLER                    PIC  X(01)     VALUE SPACES.           
019700     05  FILLER                    PIC  X(25)     VALUE                   
019800         '-------------------------'.                                     
019900     05  FILLER                    PIC  X(03)     VALUE SPACES.           
020000     05  FILLER                    PIC  X(03)     VALUE '---'.            
020100     05  FILLER                    PIC  X(01)     VALUE SPACES.           
020200     05  FILLER                    PIC  X(25)     VALUE                   
020300         '-------------------------'.                                     
020400     05  FILLER                    PIC  X(03)     VALUE SPACES.           
020500     05  FILLER                    PIC  X(11)     VALUE                   
020600         '-----------'.                                                   
020700     05  FILLER                    PIC  X(03)     VALUE SPACES.           
020800     05  FILLER                    PIC  X(14)     VALUE                   
020900         '--------------'.                                                
021000     05  FILLER                    PIC  X(26)     VALUE SPACES.           
021100                                                                          
021200 01  DTL-DEPT-TOTAL-LINE.                                                 
021300     05  FILLER                    PIC  X(59)     VALUE SPACES.           
021400     05  FILLER                    PIC  X(19)     VALUE                   
021500         'DEPARTMENT TOTAL   '.                                           
021600     05  DTL-INV-CNT               PIC  ZZZ,ZZZ,ZZ9.                      
021700     05  FILLER                    PIC  X(03)     VALUE SPACES.           
021800     05  DTL-INV-RTL-AMT           PIC  ZZZ,ZZZ,ZZ9.99.                   
021900     05  FILLER                    PIC  X(26)     VALUE SPACES.           
022000                                                                          
022100 01  FTL-FINAL-TOTAL-LINE.                                                
022200     05  FILLER                    PIC  X(64)     VALUE SPACES.           
022300     05  FILLER                    PIC  X(14)     VALUE                   
022400         'FINAL TOTAL   '.                                                
022500     05  FTL-INV-CNT               PIC  ZZZ,ZZZ,ZZ9.                      
022600     05  FILLER                    PIC  X(03)     VALUE SPACES.           
022700     05  FTL-INV-RTL-AMT           PIC  ZZZ,ZZZ,ZZ9.99.                   
022800     05  FILLER                    PIC  X(26)     VALUE SPACES.           
022900                                                                          
023000******************************************************************        
023100* DL  DETAIL LINES FOR DOCUMENT                                  *        
023200******************************************************************        
023300 01  DL01-DETAIL-LINE-01.                                                 
023400     05  FILLER                    PIC  X(14)     VALUE SPACES.           
023500     05  DL01-DPT-NBR              PIC  X(03)     VALUE SPACES.           
023600     05  FILLER                    PIC  X(01)     VALUE SPACES.           
023700     05  DL01-DPT-DESC             PIC  X(25)     VALUE SPACES.           
023800     05  FILLER                    PIC  X(04)     VALUE SPACES.           
023900     05  DL01-CLS-NBR              PIC  X(02)     VALUE SPACES.           
024000     05  FILLER                    PIC  X(01)     VALUE SPACES.           
024100     05  DL01-CLS-DESC             PIC  X(25)     VALUE SPACES.           
024200     05  FILLER                    PIC  X(03)     VALUE SPACES.           
024300     05  DL01-INV-CNT              PIC  ZZZ,ZZZ,ZZ9.                      
024400     05  FILLER                    PIC  X(03)     VALUE SPACES.           
024500     05  DL01-INV-RTL-AMT          PIC  ZZZ,ZZZ,ZZ9.99.                   
024600     05  FILLER                    PIC  X(29)     VALUE SPACES.           
024700                                                                          
024800****************************************************************          
024900* MISCELLANEOUS END OF REPORT LINE                                        
025000****************************************************************          
025100 01  MISCELLANEOUS-LINES.                                                 
025200     05  END-OF-REPORT-LINE.                                              
025300         10  FILLER                  PIC  X(44)    VALUE SPACES.          
025400         10  FILLER                  PIC  X(45)    VALUE                  
025500             '*******   E N D   O F   R E P O R T   *******'.             
025600         10  FILLER                  PIC  X(43)    VALUE SPACES.          
025700                                                                          
028700                                                                          
029000******************************************************************        
029100 PROCEDURE DIVISION.                                                      
029200******************************************************************        
029400******************************************************************        
029500* 0000-MAIN-MODULE  - HIGHEST LEVEL CONTROL ROUTINE.             *        
029600******************************************************************        
029700 0000-MAIN-MODULE.                                                        
029800     PERFORM 1000-INITIALIZE.                                             
029900                                                                          
030000     PERFORM 2000-PROCESS-DETAIL                                          
030100         UNTIL PS-END-OF-INV-CNT-FILE.                                    
030200                                                                          
030300     PERFORM 9000-CLOSE-FILES.                                            
030400                                                                          
W40134     GOBACK.                                                              
030600                                                                          
030700******************************************************************        
030800* 1000-INITIALIZE.                                                        
031100******************************************************************        
031200 1000-INITIALIZE.                                                         
031300     EXEC SQL                                                             
031400         SET :PV-DB2-DATE = CURRENT DATE                                  
031500     END-EXEC.                                                            
031600                                                                          
031700     EVALUATE TRUE                                                        
031800         WHEN SQLCODE = ZERO                                              
031900             CONTINUE                                                     
032200         WHEN SQLWARN0 NOT EQUAL SPACE                                    
032300         WHEN SQLCODE NOT EQUAL ZERO                                      
032400             MOVE '1000-INITIALIZE' TO PV-PARAGRAPH-NAME                  
032500             MOVE 'GET CURRENT DATE' TO PV-DB2-OPERATION-ATTEMPTED        
032600             PERFORM 9100-SQL-ABEND                                       
032700     END-EVALUATE.                                                        
032800                                                                          
032900     EXEC SQL                                                             
033000         SET :PV-DB2-TIME = CURRENT TIME                                  
033100     END-EXEC.                                                            
033200                                                                          
033300     EVALUATE TRUE                                                        
033400         WHEN SQLCODE = ZERO                                              
033500             CONTINUE                                                     
033800         WHEN SQLWARN0 NOT EQUAL SPACE                                    
033900         WHEN SQLCODE NOT EQUAL ZERO                                      
034000             MOVE '1000-INITIALIZE' TO PV-PARAGRAPH-NAME                  
034100             MOVE 'GET CURRENT TIME' TO PV-DB2-OPERATION-ATTEMPTED        
034200             PERFORM 9100-SQL-ABEND                                       
034300     END-EVALUATE.                                                        
113300                                                                          
113400     MOVE PV-DB2-DATE-CCYY        TO DP132-RUN-YEAR.                      
113500     MOVE PV-DB2-DATE-MM          TO DP132-RUN-MONTH.                     
113600     MOVE PV-DB2-DATE-DD          TO DP132-RUN-DAY.                       
113300                                                                          
113900     MOVE PV-DB2-TIME-HH          TO DP132-RUN-HOUR.                      
114000     MOVE PV-DB2-TIME-MM          TO DP132-RUN-MINUTE.                    
114100                                                                          
034500     MOVE PC-REPORT-NBR           TO DP132-REPORT-NUMBER.                 
035200     MOVE PC-CURRENT-PROGRAM-NAME TO DP132-PROGRAM-NAME.                  
035300                                                                          
035400     MOVE PC-MAX-DETAIL-LINES     TO PV-LINE-COUNT.                       
035500     MOVE ZERO                    TO PV-PAGE-COUNT.                       
035600                                                                          
113400     MOVE PV-DB2-DATE-YY          TO PV-WORK-DATE-YY.                     
113500     MOVE PV-DB2-DATE-MM          TO PV-WORK-DATE-MM.                     
113600     MOVE PV-DB2-DATE-DD          TO PV-WORK-DATE-DD.                     
113700                                                                          
035700     OPEN INPUT INVENTORY-CNT-FILE                                        
035800          OUTPUT RTV-INV-CNT-REPORT.                                      
035900                                                                          
036000     SET PS-NOT-END-OF-INV-CNT-FILE                                       
036100         PS-NEW-DEPT              TO TRUE.                                
036200                                                                          
036300     PERFORM 7000-READ-INV-CNT-FILE.                                      
036400                                                                          
W40134     INITIALIZE DCLXLT00010.                                              
W40134     MOVE RV024-CNTRL-STK-ID TO PH-LOC-NBR.                               
W40134                                                                          
W40134     EXEC SQL                                                             
W40134         SELECT  LOC_NBR                                                  
W40134                ,LOC_10_ABBR_NM                                           
W40134           INTO  :XLT00010-LOC-NBR                                        
W40134                ,:XLT00010-LOC-10-ABBR-NM                                 
W40134           FROM XLT_LOC                                                   
W40134         WHERE LOC_NBR = :PH-LOC-NBR                                      
W40134     END-EXEC.                                                            
W40134                                                                          
W40134     EVALUATE TRUE                                                        
W40134         WHEN SQLCODE = ZERO                                              
W40134             MOVE XLT00010-LOC-NBR        TO HL02-RTV-LOC                 
W40134             MOVE XLT00010-LOC-10-ABBR-NM TO HL02-RTV-NAME                
W40134             CONTINUE                                                     
W40134         WHEN SQLWARN0 NOT EQUAL SPACE                                    
W40134         WHEN SQLCODE NOT EQUAL ZERO                                      
W40134             MOVE '1000-INITIALIZE' TO PV-PARAGRAPH-NAME                  
W40134             MOVE 'SELECT XLT_LOC  ' TO PV-DB2-OPERATION-ATTEMPTED        
W40134             PERFORM 9100-SQL-ABEND                                       
W40134     END-EVALUATE.                                                        
W40134                                                                          
113300                                                                          
036500     INITIALIZE PH-PROGRAM-HOLD-AREA.                                     
036600     MOVE RV024-DEPT              TO PH-DEPT.                             
036700     MOVE RV024-CLASS             TO PH-CLASS.                            
036800                                                                          
036900******************************************************************        
037000* 2000-PROCESS-DETAIL.                                                    
037200*   - READ DETAIL INFORMATION AND CREATE THE REPORT UNTIL ALL             
037300*     RECORDS ARE READ FROM THE INPUT FILE.                               
037600******************************************************************        
037700 2000-PROCESS-DETAIL.                                                     
037800     EVALUATE TRUE                                                        
037900     WHEN RV024-DEPT      = PH-DEPT AND                                   
038000          RV024-CLASS     = PH-CLASS                                      
038100         PERFORM 3000-ACCUM-AMOUNTS                                       
038200     WHEN RV024-DEPT      = PH-DEPT AND                                   
038300          RV024-CLASS NOT = PH-CLASS                                      
038400         PERFORM 4000-CLASS-BREAK                                         
038500         PERFORM 3000-ACCUM-AMOUNTS                                       
038600     WHEN RV024-DEPT  NOT = PH-DEPT                                       
038700         PERFORM 4000-CLASS-BREAK                                         
038800         PERFORM 5000-DEPT-BREAK                                          
038900         PERFORM 3000-ACCUM-AMOUNTS                                       
039000     END-EVALUATE.                                                        
039100                                                                          
039200     PERFORM 7000-READ-INV-CNT-FILE.                                      
039300                                                                          
039400     IF PS-END-OF-INV-CNT-FILE                                            
039500         PERFORM 4000-CLASS-BREAK                                         
039600         PERFORM 5000-DEPT-BREAK                                          
039700         PERFORM 6000-FINAL-BREAK                                         
039800     END-IF.                                                              
039900                                                                          
040000******************************************************************        
040100* 3000-ACCUM-AMOUNTS.                                                     
040300*   - ACCUMULATE THE INVENTORY COUNT AND THE INVENTORY RETAIL             
040400*     AMOUNTS BY DEPT/CLASS.                                              
040700******************************************************************        
040800 3000-ACCUM-AMOUNTS.                                                      
040900     COMPUTE PH-AVAILABLE                                                 
041000            = PH-AVAILABLE + RV024-AVAILABLE.                             
041100                                                                          
041200     COMPUTE PH-UNIT-RTL-AMT                                              
041300            = PH-UNIT-RTL-AMT                                             
041400            + (RV024-CUR-UNIT-RTL-AMT * RV024-AVAILABLE).                 
041500                                                                          
041600******************************************************************        
041700* 4000-CLASS-BREAK.                                                       
041900*   - WRITES A DETAIL LINE FOR A CLASS                                    
042200******************************************************************        
042300 4000-CLASS-BREAK.                                                        
042400     COMPUTE PH-DEPT-CNT                                                  
042500            = PH-AVAILABLE + PH-DEPT-CNT.                                 
042600                                                                          
042700     COMPUTE PH-DEPT-RTL-AMT                                              
042800            = PH-UNIT-RTL-AMT + PH-DEPT-RTL-AMT.                          
042900                                                                          
043000     IF PS-NEW-DEPT                                                       
043100         SET PS-NO-NEW-DEPT TO TRUE                                       
043200         MOVE PH-DEPT       TO DL01-DPT-NBR                               
043300         PERFORM 4100-GET-DEPT-MBS-INFO                                   
043400     ELSE                                                                 
043500         MOVE SPACES        TO DL01-DPT-NBR                               
043600                               DL01-DPT-DESC                              
043700     END-IF.                                                              
043800                                                                          
043900     MOVE PH-CLASS          TO DL01-CLS-NBR                               
044000                               PV-MBS-MCL                                 
044100                               PV-MBS-SCL-2.                              
044200     MOVE 0                 TO PV-MBS-MCL-0                               
044300                               PV-MBS-SCL-0.                              
044400     PERFORM 4200-GET-CLASS-MBS-INFO.                                     
044500     MOVE PH-AVAILABLE      TO DL01-INV-CNT.                              
044600     MOVE PH-UNIT-RTL-AMT   TO DL01-INV-RTL-AMT.                          
044700                                                                          
044800     PERFORM 8100-WRITE-DETAIL-LINE.                                      
044900                                                                          
045000     MOVE RV024-CLASS       TO PH-CLASS.                                  
045100     INITIALIZE PH-AVAILABLE                                              
045200                PH-UNIT-RTL-AMT.                                          
045300                                                                          
045400******************************************************************        
045500* 4100-GET-DEPT-MBS-INFO                                                  
045600*           THIS PARAGRAPH WILL GET THE DEPARTMENT DESCRIPTION            
045700******************************************************************        
045800 4100-GET-DEPT-MBS-INFO.                                                  
045900     INITIALIZE DP003-MBS-SUBROUTINE-FIELDS.                              
046000     SET DP003-DPT-LOOKUP TO TRUE.                                        
046100     PERFORM 4300-SETUP-COMMON-MBS-FIELDS.                                
046200                                                                          
046300     CALL PC-MBS-PGM USING DP003-MBS-SUBROUTINE-FIELDS.                   
046400                                                                          
046500     EVALUATE TRUE                                                        
046600         WHEN DP003-SUCCESSFUL                                            
046700             MOVE DP003-DPT-DESCRIPTION TO DL01-DPT-DESC                  
046900         WHEN DP003-DPT-NOT-FOUND                                         
047000             MOVE '** DEPARTMENT NOT FOUND *' TO DL01-DPT-DESC            
047200         WHEN OTHER                                                       
047300             MOVE 'DEPT LOOKUP'  TO PV-MBS-CALL                           
049800                                     PV-DB2-OPERATION-ATTEMPTED           
052900             MOVE '4100-GET-DEPT-MBS-INFO' TO PV-PARAGRAPH-NAME           
052900             DISPLAY 'DEPARTMENT:  ' PH-DEPT                              
052900             DISPLAY 'DP003-RET-CD:' DP003-RETURN-CODE                    
047400             PERFORM 4400-MBS-ABEND                                       
047500     END-EVALUATE.                                                        
047600                                                                          
047700******************************************************************        
047800* 4200-GET-CLASS-MBS-INFO                                                 
047900*           THIS PARAGRAPH WILL GET THE SUB-CLASS DESCRIPTION             
048000******************************************************************        
048100 4200-GET-CLASS-MBS-INFO.                                                 
048200     INITIALIZE DP003-MBS-SUBROUTINE-FIELDS.                              
048300     SET DP003-SCL-LOOKUP TO TRUE.                                        
048400     MOVE PV-MBS-MCL      TO DP003-MCL-INPUT.                             
048500     MOVE PV-MBS-SCL      TO DP003-SCL-INPUT.                             
048600     PERFORM 4300-SETUP-COMMON-MBS-FIELDS.                                
048700                                                                          
048800     CALL PC-MBS-PGM USING DP003-MBS-SUBROUTINE-FIELDS.                   
048900                                                                          
049000     EVALUATE TRUE                                                        
049100         WHEN DP003-SUCCESSFUL                                            
049200             MOVE DP003-SCL-DESCRIPTION TO DL01-CLS-DESC                  
049400         WHEN DP003-SCL-NOT-FOUND                                         
049500             MOVE '** SUB-CLASS NOT FOUND **' TO DL01-CLS-DESC            
049700         WHEN OTHER                                                       
049800             MOVE 'SUB-CLASS LOOKUP' TO PV-MBS-CALL                       
049800                                        PV-DB2-OPERATION-ATTEMPTED        
052900             MOVE '4200-GET-CLASS-MBS-INFO' TO PV-PARAGRAPH-NAME          
052900             DISPLAY 'CLASS:  ' PH-CLASS                                  
052900             DISPLAY 'DP003-RET-CD:' DP003-RETURN-CODE                    
049900             PERFORM 4400-MBS-ABEND                                       
050000     END-EVALUATE.                                                        
050100                                                                          
050200******************************************************************        
050300* 4300-SETUP-COMMON-MBS-FIELDS.                                  *        
050400******************************************************************        
050500 4300-SETUP-COMMON-MBS-FIELDS.                                            
050600     MOVE 'RV'                   TO DP003-SYSTEM-CODE.                    
050700     MOVE '03'                   TO DP003-OP-CO-NMBR.                     
050800                                                                          
050900     MOVE PV-WORK-DATE           TO DP003-CURRENT-DATE                    
051000                                    DP003-EFF-DATE.                       
051100                                                                          
051200     MOVE PH-DEPT                TO DP003-DPT-INPUT.                      
051300                                                                          
051400******************************************************************        
051500* 4400-MBS-ABEND.                                                *        
051600******************************************************************        
051700 4400-MBS-ABEND.                                                          
051800     DISPLAY '** ABEND **'.                                               
051900     DISPLAY '** PROGRAM = RVKRP016 **'.                                  
052000     DISPLAY '** PARAGRAPH = GET-MBS-INFORMATION**'.                      
052100     DISPLAY PV-MBS-DISPLAY.                                              
052200     DISPLAY '** DP003-RETURN-DATA = ' DP003-RETURN-DATA.                 
052300     DISPLAY '**     DP003-SQLCODE = ' DP003-SQLCODE.                     
052500     MOVE +4014 TO ABEND-CODE.                                            
052600     CALL 'ILBOABN0' USING ABEND-CODE.                                    
053200                                                                          
053300******************************************************************        
053400* 5000-DEPT-BREAK.                                                        
053600*   - WRITES A DEPARTMENT TOTAL LINE                                      
053900******************************************************************        
054000 5000-DEPT-BREAK.                                                         
054100     COMPUTE PH-FINAL-CNT                                                 
054200            = PH-FINAL-CNT + PH-DEPT-CNT.                                 
054300                                                                          
054400     COMPUTE PH-FINAL-RTL-AMT                                             
054500            = PH-FINAL-RTL-AMT + PH-DEPT-RTL-AMT.                         
054600                                                                          
054700     MOVE PH-DEPT-CNT       TO DTL-INV-CNT.                               
054800     MOVE PH-DEPT-RTL-AMT   TO DTL-INV-RTL-AMT.                           
054900                                                                          
055000     PERFORM 8200-WRITE-DEPT-LINE.                                        
055100                                                                          
055200     SET PS-NEW-DEPT        TO TRUE.                                      
055300     MOVE RV024-DEPT        TO PH-DEPT.                                   
055400                                                                          
055500     INITIALIZE PH-DEPT-CNT                                               
055600                PH-DEPT-RTL-AMT.                                          
055700                                                                          
055800******************************************************************        
055900* 6000-FINAL-BREAK.                                                       
056100*   - WRITES A FINAL TOTAL LINE                                           
056400******************************************************************        
056500 6000-FINAL-BREAK.                                                        
056600     MOVE PH-FINAL-CNT     TO FTL-INV-CNT                                 
056700     MOVE PH-FINAL-RTL-AMT TO FTL-INV-RTL-AMT                             
056800                                                                          
056900     PERFORM 8300-WRITE-FINAL-LINE.                                       
057000                                                                          
057100     PERFORM 8400-WRITE-CONFIDENTIAL-LINE.                                
057200                                                                          
057300     PERFORM 8500-WRITE-END-OF-REPORT-LINE.                               
057400                                                                          
057500******************************************************************        
057600* 7000-READ-INV-CNT-FILE.                                        *        
057700*  THIS PROCEDURE WILL READ ONE RECORD FROM THE INVENTORY COUNT  *        
057800*  FILE.                                                         *        
057900******************************************************************        
058000 7000-READ-INV-CNT-FILE.                                                  
058100     READ INVENTORY-CNT-FILE INTO RV024-SKU-RECORD                        
058300         AT END SET PS-END-OF-INV-CNT-FILE TO TRUE.                       
058400                                                                          
058400     IF PS-NOT-END-OF-INV-CNT-FILE                                        
058400         ADD 1 TO PV-RECS-READ                                            
058400     END-IF.                                                              
058400                                                                          
058400                                                                          
058500******************************************************************        
058600*     WRITES HEADER LINES WHEN MORE THAN ONE PAGE IS NEEDED      *        
058700******************************************************************        
058800 8000-PRINT-PAGE-HEADER.                                                  
058900     ADD 1                        TO PV-PAGE-COUNT.                       
059100     MOVE PV-PAGE-COUNT           TO DP132-PAGE-NUMBER.                   
059200                                                                          
059300     WRITE RTV-INV-CNT-LINE FROM DP132-STANDRD-HEADER-132-COLS            
059500         AFTER ADVANCING PAGE.                                            
059600                                                                          
059700     WRITE RTV-INV-CNT-LINE FROM HL01-HEADER-LINE-01                      
059900         AFTER ADVANCING 2 LINES.                                         
060000                                                                          
W40134     WRITE RTV-INV-CNT-LINE FROM HL02-HEADER-LINE-02                      
W40134         AFTER ADVANCING 1 LINES.                                         
060000                                                                          
060100     WRITE RTV-INV-CNT-LINE FROM DH01-DETAIL-HEADING-01                   
060300         AFTER ADVANCING 3 LINES.                                         
060400                                                                          
060500     WRITE RTV-INV-CNT-LINE FROM DH02-DETAIL-HEADING-02                   
060700         AFTER ADVANCING 1 LINES.                                         
060800                                                                          
060900     WRITE RTV-INV-CNT-LINE FROM DH03-DETAIL-HEADING-03                   
061100         AFTER ADVANCING 1 LINES.                                         
061200                                                                          
061300******************************************************************        
061400*     WRITES THE DETAIL LINE                                     *        
061500******************************************************************        
061600 8100-WRITE-DETAIL-LINE.                                                  
061700     ADD 1          TO  PV-LINE-COUNT.                                    
061800                                                                          
061900     IF PV-LINE-COUNT > PC-MAX-DETAIL-LINES                               
062000         PERFORM 8000-PRINT-PAGE-HEADER                                   
062100         MOVE 1     TO PV-LINE-COUNT                                      
062200     END-IF.                                                              
062300                                                                          
062400     WRITE RTV-INV-CNT-LINE FROM DL01-DETAIL-LINE-01                      
062600         AFTER ADVANCING 1 LINE.                                          
062700                                                                          
062900******************************************************************        
063000*     WRITES THE DEPARTMENT TOTAL LINE                           *        
063100******************************************************************        
062800 8200-WRITE-DEPT-LINE.                                                    
063200     ADD 2          TO  PV-LINE-COUNT.                                    
063300                                                                          
063400     IF PV-LINE-COUNT > PC-MAX-DETAIL-LINES                               
063500         PERFORM 8000-PRINT-PAGE-HEADER                                   
063600         MOVE 2     TO PV-LINE-COUNT                                      
063700     END-IF.                                                              
063800                                                                          
064300     WRITE RTV-INV-CNT-LINE FROM DTL-DEPT-TOTAL-LINE                      
064500         AFTER ADVANCING 2 LINE.                                          
064600                                                                          
064700     ADD 1          TO  PV-LINE-COUNT.                                    
064800                                                                          
064900     WRITE RTV-INV-CNT-LINE FROM HL-BLANK-LINE                            
065100         AFTER ADVANCING 1 LINE.                                          
065200                                                                          
065400******************************************************************        
065500*     WRITES THE FINAL TOTAL LINE                                *        
065600******************************************************************        
065300 8300-WRITE-FINAL-LINE.                                                   
065700     ADD 2          TO  PV-LINE-COUNT.                                    
065800                                                                          
065900     IF PV-LINE-COUNT > PC-MAX-DETAIL-LINES                               
066000         PERFORM 8000-PRINT-PAGE-HEADER                                   
066100         MOVE 2     TO PV-LINE-COUNT                                      
066200     END-IF.                                                              
066300                                                                          
066800     WRITE RTV-INV-CNT-LINE FROM FTL-FINAL-TOTAL-LINE                     
067000         AFTER ADVANCING 2 LINE.                                          
067100                                                                          
067200     ADD 1          TO  PV-LINE-COUNT.                                    
067300                                                                          
067400     WRITE RTV-INV-CNT-LINE FROM HL-BLANK-LINE                            
067600         AFTER ADVANCING 1 LINE.                                          
067700                                                                          
067900******************************************************************        
068000*     WRITES THE CONFIDENTIAL INFORMATION LINE                   *        
068100******************************************************************        
067800 8400-WRITE-CONFIDENTIAL-LINE.                                            
068200     ADD 3          TO  PV-LINE-COUNT.                                    
068300                                                                          
068400     IF PV-LINE-COUNT > PC-MAX-DETAIL-LINES                               
068500         PERFORM 8000-PRINT-PAGE-HEADER                                   
068600         MOVE 3     TO PV-LINE-COUNT                                      
068700     END-IF.                                                              
068800                                                                          
068900     WRITE RTV-INV-CNT-LINE FROM HL-BLANK-LINE                            
069100         AFTER ADVANCING 2 LINE.                                          
069200                                                                          
069700     WRITE RTV-INV-CNT-LINE FROM DP132-CNFDL-HDR-132-COLS                 
069900         AFTER ADVANCING 1 LINE.                                          
070000                                                                          
070100     ADD 1          TO  PV-LINE-COUNT.                                    
070200                                                                          
070300     WRITE RTV-INV-CNT-LINE FROM HL-BLANK-LINE                            
070500         AFTER ADVANCING 1 LINE.                                          
070600                                                                          
070700******************************************************************        
070800*     WRITES THE END-OF-REPORT LINE                              *        
070900******************************************************************        
071100 8500-WRITE-END-OF-REPORT-LINE.                                           
071200     ADD 3          TO  PV-LINE-COUNT.                                    
071300                                                                          
071400     IF PV-LINE-COUNT > PC-MAX-DETAIL-LINES                               
071500         PERFORM 8000-PRINT-PAGE-HEADER                                   
071600         MOVE 3     TO PV-LINE-COUNT                                      
071700     END-IF.                                                              
071800                                                                          
071900     WRITE RTV-INV-CNT-LINE FROM HL-BLANK-LINE                            
072100         AFTER ADVANCING 2 LINE.                                          
072200                                                                          
072600     WRITE RTV-INV-CNT-LINE FROM END-OF-REPORT-LINE                       
072800         AFTER ADVANCING 1 LINE.                                          
072900                                                                          
073100******************************************************************        
073200*9000-CLOSE-FILES.                                                        
073400*   - CLOSE THE OPEN INPUT AND REPORT FILE.                               
073600******************************************************************        
073700 9000-CLOSE-FILES.                                                        
073800     CLOSE INVENTORY-CNT-FILE                                             
073900           RTV-INV-CNT-REPORT.                                            
074000                                                                          
074000     DISPLAY '***   RVKRP016  ************************'.                  
074000     DISPLAY 'RECORDS READ: ' PV-RECS-READ.                               
074000     DISPLAY '****************************************'.                  
074000                                                                          
074100******************************************************************        
074200* 9100-SQL-ABEND - PERFORMS THE ABEND FOR THE PROGRAM IN THE              
074300* EVENT THAT AN SQL ERROR OR WARNING CODE OCCURS.                         
074400******************************************************************        
074500 9100-SQL-ABEND.                                                          
073800     CLOSE INVENTORY-CNT-FILE                                             
073900           RTV-INV-CNT-REPORT.                                            
074000                                                                          
074600     DISPLAY '** ABEND     **'.                                           
074700     DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.                
074800     DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.                      
074900     DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION-ATTEMPTED.             
075000                                                                          
075100******************************************************************        
075200* THIS COPY BOOK MEMBER CONTAINS THE PROCEDURE DIVISION STATE-            
075300* MENTS THAT ARE REQUIRED TO UTILIZE THE DB2 ABEND MODULE                 
075400* 'DSNTIAR' AND CONVERT THE SQLCA INFORMATION INTO A READABLE             
075500* FORMAT.                                                                 
075600******************************************************************        
075700     COPY DPPD004.                                                        
075800*                                                                         
075900***********  END OF RVKRP016 ************************************         
