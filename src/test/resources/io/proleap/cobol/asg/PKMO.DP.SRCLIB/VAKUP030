       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    VAKUP030.                                                 
       AUTHOR.        NIJESH/INFOSYS.                                           
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN.  10-24-08.                                                 
       DATE-COMPILED.                                                           
                                                                                
      ******************************************************************        
      *           VAKUP030 - FORMAT FOR LEDGER UPDATE                  *        
      *                                                                *        
      * THIS PROGRAM READS FOUR TRANSACTION EXTRACT FILES (PURCHASE,   *        
      * SALE, PRICE CHANGE, AND ADJUSTMENT), FORMATS THE TRANSACTIONS  *        
      * INTO THE LAYOUTS USED BY THE MERCHANDISE LEDGER UPDATE, AND    *        
      * WRITES THEM TO A COMMON OUTPUT FILE.                           *        
      ******************************************************************        
      *                                                                *        
      ******************** HISTORY OF CHANGES **************************        
      * CHG ID/                                                        *        
      * WR/PROJ  DATE        DESCRIPTION OF CHANGES                    *        
      * -------  ----------  ----------------------------------------  *        
      *                                                                *        
      * WR#33304 10/24/2008  NIJESH/INFOSYS                            *        
      *                      NEW PROGRAM WRIITEN AS PART OF HOLDING    *        
      *                      INVENTORY LEDGER OPEN (HILO).             *        
      *                      THIS PROGRAM IS COPIED FROM MLKUP030      *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.        IBM-3090.                                        
       OBJECT-COMPUTER.        IBM-3090.                                        
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT PURCHASE-EXTRACT-FILE                                         
               ASSIGN                       TO INP01.                           
                                                                                
           SELECT SALE-EXTRACT-FILE                                             
               ASSIGN                       TO INP02.                           
                                                                                
           SELECT PRICE-CHANGE-EXTRACT-FILE                                     
               ASSIGN                       TO INP03.                           
                                                                                
           SELECT ADJUSTMENT-EXTRACT-FILE                                       
               ASSIGN                       TO INP04.                           
                                                                                
           SELECT ML-DATE-CONTROL-FILE                                          
               ASSIGN                       TO INP05.                           
                                                                                
           SELECT ML-FORMATTED-TRANS-FILE                                       
               ASSIGN                       TO OUT01.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  PURCHASE-EXTRACT-FILE                                                
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PURCHASE-EXTRACT-RECORD          PIC  X(200).                        
                                                                                
       FD  SALE-EXTRACT-FILE                                                    
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SALE-EXTRACT-RECORD              PIC  X(80).                         
                                                                                
       FD  PRICE-CHANGE-EXTRACT-FILE                                            
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  PRICE-CHANGE-EXTRACT-RECORD      PIC  X(200).                        
                                                                                
       FD  ADJUSTMENT-EXTRACT-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ADJUSTMENT-EXTRACT-RECORD        PIC  X(90).                         
                                                                                
       FD  ML-DATE-CONTROL-FILE                                                 
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ML-DATE-CONTROL-RECORD           PIC  X(80).                         
                                                                                
       FD  ML-FORMATTED-TRANS-FILE                                              
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  ML-FORMATTED-TRANS-RECORD        PIC  X(150).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-PURCHASE-EXTRACT-SW   PIC  X(01)    VALUE 'N'.            
               88  PS-EOF-PURCHASE-EXTRACT                VALUE 'Y'.            
           05  PS-EOF-SALE-EXTRACT-SW       PIC  X(01)    VALUE 'N'.            
               88  PS-EOF-SALE-EXTRACT                    VALUE 'Y'.            
           05  PS-EOF-PRICE-CHANGE-EXTRACT-SW                                   
                                            PIC  X(01)    VALUE 'N'.            
               88  PS-EOF-PRICE-CHANGE-EXTRACT            VALUE 'Y'.            
           05  PS-EOF-ADJUSTMENT-EXTRACT-SW PIC  X(01)    VALUE 'N'.            
               88  PS-EOF-ADJUSTMENT-EXTRACT              VALUE 'Y'.            
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-ACCT-NO.                                                      
               10  PV-ACCT-NO-STORE-NBR     PIC  X(05)    VALUE SPACE.          
           05  PV-ISO-FMT-DATE.                                                 
               10  PV-ISO-FMT-CC            PIC  9(02)    VALUE ZERO.           
               10  PV-ISO-FMT-YY            PIC  9(02)    VALUE ZERO.           
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-ISO-FMT-MM            PIC  9(02)    VALUE ZERO.           
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-ISO-FMT-DD            PIC  9(02)    VALUE ZERO.           
           05  PV-FORMATTED-DATE.                                               
               10  PV-FMT-DTE-CCYY          PIC  X(04)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-FMT-DTE-MM            PIC  X(02)    VALUE SPACE.          
               10  FILLER                   PIC  X(01)    VALUE SPACE.          
               10  PV-FMT-DTE-DD            PIC  X(02)    VALUE SPACE.          
           05  PV-UNFMT-DATE.                                                   
               10  PV-UNFMT-DTE-CCYY        PIC  X(04)    VALUE SPACE.          
               10  PV-UNFMT-DTE-MM          PIC  X(02)    VALUE SPACE.          
               10  PV-UNFMT-DTE-DD          PIC  X(02)    VALUE SPACE.          
           05  PV-PARAGRAPH-NAME            PIC  X(30)    VALUE SPACE.          
           05  PV-DB2-OPER-ATTEMPTED        PIC  X(30)    VALUE SPACE.          
           05  PV-ABEND-CODE                PIC S9(04)    COMP                  
                                                          VALUE ZERO.           
           05  PV-RETRY-COUNT               PIC S9(04)    COMP SYNC             
                                                          VALUE ZERO.           
           05  PV-STORE-NBR                 PIC  9(05)    VALUE ZERO.           
           05  PV-STORE-NBR-COMP            PIC S9(04)    COMP                  
                                                          VALUE ZERO.           
           05  PV-MAX-RETRIES               PIC S9(03)    VALUE +3.             
                                                                                
       01  RC-RECORD-COUNTS.                                                    
           05  RC-XT-EXTRACT-COUNTS.                                            
               10  RC-XT-PURCHASE-EXTRACT-CNT                                   
                                            PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-XT-SALE-EXTRACT-CNT   PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-XT-PRICE-CHANGE-EXTRACT-CNT                               
                                            PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-XT-ADJUSTMENT-EXTRACT-CNT                                 
                                            PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
           05  RC-ML-FORMATTED-TRANS-COUNTS.                                    
               10  RC-ML-PURCHASE-TRANS-CNT PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-TRANSFER-FROM-TRANS-CNT                                
                                            PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-TRANSFER-TO-TRANS-CNT                                  
                                            PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-SALE-CNT           PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-MARKDOWN-CNT       PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-MARKUP-CNT         PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
               10  RC-ML-ADJUSTMENT-CNT     PIC S9(07)    COMP-3                
                                                          VALUE ZERO.           
                                                                                
       01  DF-DISPLAY-FIELDS.                                                   
           05  DF-XT-COUNTS-TITLE-1         PIC  X(32)    VALUE                 
                                       'NUMBER OF EXTRACT RECORDS READ'.        
           05  DF-XT-COUNTS-TITLE-2         PIC  X(32)    VALUE ALL '-'.        
           05  DF-XT-COUNT-DISPLAY.                                             
               10  FILLER                   PIC  X(04)    VALUE SPACE.          
               10  DF-XT-TRANS-TYPE         PIC  X(20).                         
               10  DF-XT-RECORD-CNT         PIC  Z(06)9.                        
           05  DF-ML-COUNTS-TITLE-1         PIC  X(43)    VALUE                 
                          'NUMBER OF FORMATTED ML TRANSACTIONS WRITTEN'.        
           05  DF-ML-COUNTS-TITLE-2         PIC  X(43)    VALUE ALL '-'.        
           05  DF-ML-TRANS-HEADING.                                             
               10  FILLER                   PIC  X(04)    VALUE SPACE.          
               10  DF-ML-TRANS-TYPE-HEAD    PIC  X(18).                         
           05  DF-ML-COUNT-DISPLAY.                                             
               10  FILLER                   PIC  X(06)    VALUE SPACE.          
               10  DF-ML-TRANS-TYPE         PIC  X(18).                         
               10  DF-ML-RECORD-CNT         PIC  Z(06)9.                        
                                                                                
       01  ABEND-CODE                       PIC S9(04)    VALUE ZERO            
                                                          COMP SYNC.            
                                                                                
      *                                                                         
      ******************************************************************        
      *  VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004       *        
      ******************************************************************        
                                                                                
           COPY DPWS004.                                                        
                                                                                
      ******************************************************************        
      *  COMMUNICATIONS AREA FOR DB2                                   *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE SQLCA                                                    
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DCLGEN FOR THE TDTATTR DB2 TABLE                              *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TDTATTR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DCLGEN FOR THE INVENTORY CONTROL DB2 TABLE                    *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TINCNTL                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      *  DCLGEN FOR THE INVENTORY PARAMETER DB2 TABLE.                 *        
      ******************************************************************        
                                                                                
           EXEC SQL                                                             
               INCLUDE TINVPAR                                                  
           END-EXEC.                                                            
                                                                                
      ******************************************************************        
      * INPUT CONTROL CARD LAYOUT.                                     *        
      ******************************************************************        
       01  CC-CONTROL-CARD.                                                     
           05  FILLER                  PIC  X(03)    VALUE SPACE.               
           05  CC-INPUT-DATE.                                                   
               10  CC-ISO-FMT-CC       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-YY       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-DASH1    PIC  X(01)    VALUE SPACE.               
               10  CC-ISO-FMT-MM       PIC  X(02)    VALUE SPACE.               
               10  CC-ISO-FMT-DASH2    PIC  X(01)    VALUE SPACE.               
               10  CC-ISO-FMT-DD       PIC  X(02)    VALUE SPACE.               
           05  FILLER                  PIC  X(67)    VALUE SPACE.               
                                                                                
       01  CC-CONTROL-DATE             PIC  X(10)    VALUE SPACE.               
                                                                                
      ******************************************************************        
      * OUTPUT CONTROL RECORD LAYOUT. THE CONTROL RECORD IS THE FIRST  *        
      * RECORD WRITTEN TO THE FORMATTED TRANSACTION FILE.              *        
      ******************************************************************        
       01  OC-OUTPUT-CONTROL-RECORD.                                            
           05  FILLER                  PIC  X(02)    VALUE SPACE.               
           05  FILLER                  PIC  X(18)    VALUE SPACE.               
           05  FILLER                  PIC  X(08)    VALUE 'DSAIC40U'.          
           05  FILLER                  PIC  X(01)    VALUE SPACE.               
           05  OC-WEEK-END-DATE.                                                
               10  OC-WEEK-END-MM      PIC  X(02)    VALUE SPACE.               
               10  OC-WEEK-END-DD      PIC  X(02)    VALUE SPACE.               
               10  OC-WEEK-END-YY      PIC  X(02)    VALUE SPACE.               
           05  FILLER                  PIC  X(01)    VALUE SPACE.               
           05  OC-NEW-MONTH            PIC  X(01)    VALUE SPACE.               
           05  FILLER                  PIC  X(01)    VALUE SPACE.               
           05  FILLER                  PIC  X(01)    VALUE SPACE.               
           05  FILLER                  PIC  X(82)    VALUE SPACE.               
                                                                                
      ******************************************************************        
      * PURCHASE EXTRACT RECORD LAYOUT                                 *        
      ******************************************************************        
                                                                                
           COPY MLRD030.                                                        
                                                                                
      ******************************************************************        
      * SALE EXTRACT RECORD LAYOUT                                     *        
      ******************************************************************        
                                                                                
           COPY MLRD022.                                                        
                                                                                
      ******************************************************************        
      * PRICE CHANGE EXTRACT RECORD LAYOUT                             *        
      ******************************************************************        
                                                                                
           COPY MLRD029.                                                        
                                                                                
      ******************************************************************        
      * ADJUSTMENT EXTRACT RECORD LAYOUT                               *        
      ******************************************************************        
                                                                                
           COPY MLRD024.                                                        
                                                                                
      ******************************************************************        
      * MERCHANDISE LEDGER FORMATTED TRANSACTION LAYOUT                *        
      ******************************************************************        
                                                                                
           COPY INRD053.                                                        
                                                                                
      ******************************************************************        
      * PROCEDURE DIVISION OF THE PROGRAM STARTS HERE.                 *        
      ******************************************************************        
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       A100-MAIN-MODULE.                                                        
                                                                                
           OPEN INPUT  ML-DATE-CONTROL-FILE.                                    
           OPEN OUTPUT ML-FORMATTED-TRANS-FILE.                                 
                                                                                
           PERFORM B050-PROCESS-CONTROL-CARD.                                   
                                                                                
           DISPLAY DF-XT-COUNTS-TITLE-1.                                        
           DISPLAY DF-XT-COUNTS-TITLE-2.                                        
                                                                                
      *    *------------------------------------------------------------        
      *    * PROCESS THE PURCHASE EXTRACT FILE AND DISPLAY THE         *        
      *    * NUMBER OF PURCHASE EXTRACT RECORDS READ.                  *        
      *    *------------------------------------------------------------        
           OPEN INPUT PURCHASE-EXTRACT-FILE.                                    
           PERFORM Z100-READ-PURCHASE-EXTRACT.                                  
           PERFORM B100-FORMAT-PURCHASE-TRANS                                   
               UNTIL PS-EOF-PURCHASE-EXTRACT.                                   
           CLOSE PURCHASE-EXTRACT-FILE.                                         
           MOVE 'PURCHASE:'                 TO DF-XT-TRANS-TYPE.                
           MOVE RC-XT-PURCHASE-EXTRACT-CNT  TO DF-XT-RECORD-CNT.                
           DISPLAY DF-XT-COUNT-DISPLAY.                                         
                                                                                
      *    *------------------------------------------------------------        
      *    * PROCESS THE SALE EXTRACT FILE AND DISPLAY THE             *        
      *    * NUMBER OF SALE EXTRACT RECORDS READ.                      *        
      *    *------------------------------------------------------------        
           OPEN INPUT SALE-EXTRACT-FILE.                                        
           PERFORM Z200-READ-SALE-EXTRACT.                                      
           PERFORM B200-FORMAT-SALE-TRANS                                       
               UNTIL PS-EOF-SALE-EXTRACT.                                       
           CLOSE SALE-EXTRACT-FILE.                                             
           MOVE 'SALE:'                     TO DF-XT-TRANS-TYPE.                
           MOVE RC-XT-SALE-EXTRACT-CNT      TO DF-XT-RECORD-CNT.                
           DISPLAY DF-XT-COUNT-DISPLAY.                                         
                                                                                
      *    *------------------------------------------------------------        
      *    * PROCESS THE PRICE CHANGE EXTRACT FILE AND DISPLAY THE     *        
      *    * NUMBER OF PRICE CHANGE EXTRACT RECORDS READ.              *        
      *    *------------------------------------------------------------        
           OPEN INPUT PRICE-CHANGE-EXTRACT-FILE.                                
           PERFORM Z300-READ-PRICE-CHANGE-EXTRACT.                              
           PERFORM B300-FORMAT-PRICE-CHANGE-TRANS                               
               UNTIL PS-EOF-PRICE-CHANGE-EXTRACT.                               
           CLOSE PRICE-CHANGE-EXTRACT-FILE.                                     
           MOVE 'PRICE CHANGE:'             TO DF-XT-TRANS-TYPE.                
           MOVE RC-XT-PRICE-CHANGE-EXTRACT-CNT                                  
                                            TO DF-XT-RECORD-CNT.                
           DISPLAY DF-XT-COUNT-DISPLAY.                                         
                                                                                
      *    *------------------------------------------------------------        
      *    * PROCESS THE ADJUSTMENT EXTRACT FILE AND DISPLAY THE       *        
      *    * NUMBER OF ADJUSTMENT EXTRACT RECORDS READ.                *        
      *    *------------------------------------------------------------        
           OPEN INPUT ADJUSTMENT-EXTRACT-FILE.                                  
           PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                                
           PERFORM B400-FORMAT-ADJUSTMENT-TRANS                                 
               UNTIL PS-EOF-ADJUSTMENT-EXTRACT.                                 
           CLOSE ADJUSTMENT-EXTRACT-FILE.                                       
           MOVE 'ADJUSTMENT:'               TO DF-XT-TRANS-TYPE.                
           MOVE RC-XT-ADJUSTMENT-EXTRACT-CNT                                    
                                            TO DF-XT-RECORD-CNT.                
           DISPLAY DF-XT-COUNT-DISPLAY.                                         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE ML-FORMATTED-TRANS-FILE.                                       
                                                                                
           DISPLAY DF-ML-COUNTS-TITLE-1.                                        
           DISPLAY DF-ML-COUNTS-TITLE-2.                                        
      *    *------------------------------------------------------------        
      *    * DISPLAY THE NUMBER OF PURCHASE TRANSACTIONS WRITTEN.      *        
      *    *------------------------------------------------------------        
           MOVE 'PURCHASE'                  TO DF-ML-TRANS-TYPE-HEAD.           
           DISPLAY DF-ML-TRANS-HEADING.                                         
           MOVE 'PURCHASE:'                 TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-PURCHASE-TRANS-CNT    TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           MOVE 'TRANSFER FROM:'            TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-TRANSFER-FROM-TRANS-CNT                                   
                                            TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           MOVE 'TRANSFER TO:'              TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-TRANSFER-TO-TRANS-CNT TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           DISPLAY ' '.                                                         
      *    *------------------------------------------------------------        
      *    * DISPLAY THE NUMBER OF SALE TRANSACTIONS WRITTEN.          *        
      *    *------------------------------------------------------------        
           MOVE 'SALE'                      TO DF-ML-TRANS-TYPE-HEAD.           
           DISPLAY DF-ML-TRANS-HEADING.                                         
           MOVE 'SALE:'                     TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-SALE-CNT              TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           DISPLAY ' '.                                                         
      *    *------------------------------------------------------------        
      *    * DISPLAY THE NUMBER OF PRICE CHANGE TRANSACTIONS WRITTEN.  *        
      *    *------------------------------------------------------------        
           MOVE 'PRICE CHANGE'              TO DF-ML-TRANS-TYPE-HEAD.           
           DISPLAY DF-ML-TRANS-HEADING.                                         
           MOVE 'MARKDOWN:'                 TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-MARKDOWN-CNT          TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           MOVE 'MARKUP:'                   TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-MARKUP-CNT            TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
           DISPLAY ' '.                                                         
      *    *------------------------------------------------------------        
      *    * DISPLAY THE NUMBER OF ADJUSTMENT TRANSACTIONS WRITTEN.    *        
      *    *------------------------------------------------------------        
           MOVE 'ADJUSTMENT'                TO DF-ML-TRANS-TYPE-HEAD.           
           DISPLAY DF-ML-TRANS-HEADING.                                         
           MOVE 'ADJUSTMENT:'               TO DF-ML-TRANS-TYPE.                
           MOVE RC-ML-ADJUSTMENT-CNT        TO DF-ML-RECORD-CNT.                
           DISPLAY DF-ML-COUNT-DISPLAY.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      * ACCEPT THE INPUT CONTROL CARD, VALIDATE ITS DATE, AND USE IT TO*        
      * BUILD AND WRITE THE CONTROL RECORD FOR THE ML FORMATTED        *        
      * TRANSACTION FILE.                                              *        
      ******************************************************************        
       B050-PROCESS-CONTROL-CARD.                                               
                                                                                
           READ ML-DATE-CONTROL-FILE                                            
                INTO CC-CONTROL-CARD.                                           
                                                                                
           IF  CC-CONTROL-CARD = SPACE                                          
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = VAKUP030'                                      
               DISPLAY '** INPUT CONTROL CARD MISSING OR BLANK'                 
               MOVE +4003                   TO ABEND-CODE                       
               CALL 'ILBOABN0'              USING ABEND-CODE                    
           ELSE                                                                 
               DISPLAY '** VAKUP030 INPUT CONTROL CARD ='                       
                                                        CC-CONTROL-CARD         
               MOVE CC-INPUT-DATE           TO CC-CONTROL-DATE                  
               PERFORM B050-SELECT-TDTATTR                                      
           END-IF.                                                              
                                                                                
           IF  DTATTR-DAY-NM = 'SATURDAY'                                       
               CONTINUE                                                         
           ELSE                                                                 
               DISPLAY '** ABEND **'                                            
               DISPLAY '** PGM = VAKUP030'                                      
               DISPLAY '** CONTROL CARD DATE NOT A SATURDAY DATE.'              
               MOVE +4003                   TO ABEND-CODE                       
               CALL 'ILBOABN0'              USING ABEND-CODE                    
           END-IF.                                                              
                                                                                
           MOVE DTATTR-FSCL-WK-END-DTE(6:2) TO OC-WEEK-END-MM.                  
           MOVE DTATTR-FSCL-WK-END-DTE(9:2) TO OC-WEEK-END-DD.                  
           MOVE DTATTR-FSCL-WK-END-DTE(3:2) TO OC-WEEK-END-YY.                  
                                                                                
           IF  DTATTR-WK-IN-MN-NBR = '1'                                        
               MOVE 'Y'                     TO OC-NEW-MONTH                     
           END-IF.                                                              
                                                                                
           MOVE OC-OUTPUT-CONTROL-RECORD    TO                                  
                                             ML-FORMATTED-TRANS-RECORD.         
           PERFORM Z500-WRITE-ML-FORMATTED-TRANS.                               
                                                                                
      ******************************************************************        
      *  DATE TABLE CURSOR                                             *        
      ******************************************************************        
       B050-SELECT-TDTATTR.                                                     
                                                                                
           EXEC SQL                                                             
                 SELECT DAY_NM                                                  
                       ,FSCL_WK_BEG_DTE                                         
                       ,FSCL_WK_END_DTE                                         
                       ,FSCL_MN_END_DTE                                         
                       ,FSCL_MN_NBR                                             
                       ,WK_IN_MN_NBR                                            
                   INTO :DTATTR-DAY-NM                                          
                       ,:DTATTR-FSCL-WK-BEG-DTE                                 
                       ,:DTATTR-FSCL-WK-END-DTE                                 
                       ,:DTATTR-FSCL-MN-END-DTE                                 
                       ,:DTATTR-FSCL-MN-NBR                                     
                       ,:DTATTR-WK-IN-MN-NBR                                    
                   FROM TDTATTR                                                 
                  WHERE KY_DTE = :CC-CONTROL-DATE                               
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                   DISPLAY 'DTATTR-KEY-DATE:       ' CC-CONTROL-DATE            
                   DISPLAY 'DTATTR-DAY-NAME:       ' DTATTR-DAY-NM              
                   DISPLAY 'DTATTR-FSCL WK RANGE : '                            
                            DTATTR-FSCL-WK-BEG-DTE, ' THRU ',                   
                            DTATTR-FSCL-WK-END-DTE                              
                   DISPLAY 'DTATTR-FSCL-MN-END-DT: '                            
                                               DTATTR-FSCL-MN-END-DTE           
               WHEN OTHER                                                       
                   DISPLAY 'DTATTR-KY-DTE: ' CC-CONTROL-DATE                    
                   MOVE    'B050-SELECT-TDTATTR'                                
                                            TO PV-PARAGRAPH-NAME                
                   MOVE    'SELECT CONTROL DATES'                               
                                            TO PV-DB2-OPER-ATTEMPTED            
                   PERFORM 9998-SQL-ABEND                                       
           END-EVALUATE.                                                        
                                                                                
      ******************************************************************        
      * FORMAT AND WRITE THE PURCHASE TRANSACTIONS.                    *        
      ******************************************************************        
       B100-FORMAT-PURCHASE-TRANS.                                              
                                                                                
           ADD +1                           TO                                  
                                             RC-XT-PURCHASE-EXTRACT-CNT.        
                                                                                
           IF  ML030-PURCH-STORE-NBR  NOT = PV-STORE-NBR                        
               MOVE ML030-PURCH-STORE-NBR   TO PV-STORE-NBR                     
               PERFORM B500-GET-STORE-DETAILS                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML030-PURCH-PURCHASE                                        
                   MOVE SPACE               TO D115-PURCH-JOURNAL-REC           
                   MOVE ML030-PURCH-TRANS-CDE                                   
                                            TO D115-CODE-10                     
                   MOVE ML030-PURCH-BATCH-NBR                                   
                                            TO D115-BATCH-NO                    
                   MOVE ML030-PURCH-STORE-NBR                                   
                                            TO D115-STORE-10                    
                   MOVE ML030-PURCH-DEPT-NBR                                    
                                            TO D115-DEPT-10X                    
                   MOVE ML030-PURCH-CLASS-NBR                                   
                                            TO D115-CLASS-10X                   
                   MOVE ML030-PURCH-DOC-5-NBR                                   
                                            TO D115-DOC-NO-10                   
                   MOVE ML030-PURCH-DOC-DTE TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-DOC-MM                      
                   MOVE PV-ISO-FMT-DD       TO D115-DOC-DD                      
                   MOVE ML030-PURCH-VEND-NBR                                    
                                            TO D115-VEND-NO-10                  
                   MOVE ML030-PURCH-PO-NBR  TO D115-PO-NO                       
                   MOVE ML030-PURCH-STORE-NBR                                   
                                            TO D115-ACCT-NO                     
                   MOVE ML030-PURCH-KOHLS-MM                                    
                                            TO D115-KOHLS-MM                    
                   MOVE ML030-PURCH-EXTD-GRO-COST-AMT                           
                                            TO D115-MERCH-AMT                   
                   MOVE ML030-PURCH-DISC-AMT                                    
                                            TO D115-DISCT-AMT                   
                   MOVE ML030-PURCH-FRGT-AMT                                    
                                            TO D115-FRGHT-AMT                   
                   MOVE ML030-PURCH-EXTD-NET-COST-AMT                           
                                            TO D115-NET-COST                    
                   MOVE ML030-PURCH-EXTD-RTL-AMT                                
                                            TO D115-RETAIL-AMT                  
                   MOVE ML030-PURCH-DR-CR-CDE                                   
                                            TO D115-TRANS-CD                    
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-PJ-FISCAL-PERIOD            
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-PJ-FISCAL-WK                
                   MOVE ML030-PURCH-FIN-LIAB-DTE                                
                                            TO PV-FORMATTED-DATE                
                   MOVE PV-FMT-DTE-CCYY     TO PV-UNFMT-DTE-CCYY                
                   MOVE PV-FMT-DTE-MM       TO PV-UNFMT-DTE-MM                  
                   MOVE PV-FMT-DTE-DD       TO PV-UNFMT-DTE-DD                  
                   MOVE PV-UNFMT-DATE       TO D115-FIN-LIAB-DTE                
                   MOVE ML030-PURCH-AP-TRANS-CDE                                
                                            TO D115-AP-TRANS-CDE                
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-10          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-10                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-10          
                   MOVE D115-PURCH-JOURNAL-REC                                  
                                            TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO RC-ML-PURCHASE-TRANS-CNT         
               WHEN ML030-PURCH-TRANSFER-FRM-STORE                              
                   MOVE SPACE               TO D115-TRANSFER-FROM-REC           
                   MOVE ML030-PURCH-TRANS-CDE                                   
                                            TO D115-CODE-30                     
                   MOVE ML030-PURCH-STORE-NBR                                   
                                            TO D115-TRANSFER-FROM               
                   MOVE ML030-PURCH-DEPT-NBR                                    
                                            TO D115-DEPT-30X                    
                   MOVE ML030-PURCH-CLASS-NBR                                   
                                            TO D115-CLASS-30X                   
                   MOVE ML030-PURCH-SKU     TO D115-SKU-30                      
                   MOVE ML030-PURCH-DOC-5-NBR                                   
                                            TO D115-DOC-NO-30                   
                   MOVE ML030-PURCH-DOC-DTE TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-TRANS-MM                    
                   MOVE PV-ISO-FMT-DD       TO D115-TRANS-DD                    
                   MOVE ML030-PURCH-REL-STORE-NBR                               
                                            TO D115-TRANSFER-TO                 
                   MOVE ML030-PURCH-KOHLS-MM                                    
                                            TO D115-KOHLS-MM-30                 
                   MOVE ML030-PURCH-QTY     TO D115-TRANS-UNITS                 
                   MOVE ML030-PURCH-PER-UNT-RTL-AMT                             
                                            TO D115-RETAIL-PER-UN               
                   MOVE ML030-PURCH-BATCH-NBR                                   
                                            TO D115-TRANS-BATCHX                
                   MOVE ZERO                TO D115-TRANS-SCAN-NO               
                   MOVE ML030-PURCH-EXTD-NET-COST-AMT                           
                                            TO D115-TOTAL-TRNSFR-FM-COST        
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-TFR-FISCAL-PERIOD           
                                                                                
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-TFR-FISCAL-WK               
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-30          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-30                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-30          
                   MOVE D115-TRANSFER-FROM-REC                                  
                                            TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO                                  
                                           RC-ML-TRANSFER-FROM-TRANS-CNT        
               WHEN ML030-PURCH-TRANSFER-TO-STORE                               
                   MOVE SPACE               TO D115-TRANSFER-TO-REC             
                   MOVE ML030-PURCH-TRANS-CDE                                   
                                            TO D115-CODE-31                     
                   MOVE ML030-PURCH-STORE-NBR                                   
                                            TO D115-TRANSFER-TO31               
                   MOVE ML030-PURCH-DEPT-NBR                                    
                                            TO D115-DEPT-31                     
                   MOVE ML030-PURCH-CLASS-NBR                                   
                                            TO D115-CLASS-31                    
                   MOVE ML030-PURCH-SKU     TO D115-SKU-31                      
                   MOVE ML030-PURCH-DOC-5-NBR                                   
                                            TO D115-DOC-NO-31                   
                   MOVE ML030-PURCH-DOC-DTE TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-MM-31                       
                   MOVE PV-ISO-FMT-DD       TO D115-DD-31                       
                   MOVE ML030-PURCH-REL-STORE-NBR                               
                                            TO D115-TRANSFER-FROM-31            
                   MOVE ML030-PURCH-KOHLS-MM                                    
                                            TO D115-KOHLS-MM-31                 
                   MOVE ML030-PURCH-QTY     TO D115-UNITS-TRANSFER              
                   MOVE ML030-PURCH-PER-UNT-RTL-AMT                             
                                            TO D115-RETAIL-PER                  
                   MOVE ML030-PURCH-BATCH-NBR                                   
                                            TO D115-TRANS-BATCHX-31             
                   MOVE ML030-PURCH-EXTD-NET-COST-AMT                           
                                            TO D115-TOTAL-TRNSFR-TO-COST        
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-TTR-FISCAL-PERIOD           
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-TTR-FISCAL-WK               
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-31          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-31                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-31          
                   MOVE D115-TRANSFER-TO-REC                                    
                                            TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO                                  
                                             RC-ML-TRANSFER-TO-TRANS-CNT        
               WHEN OTHER                                                       
                   MOVE +4008               TO ABEND-CODE                       
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM = VAKUP030'                              
                   DISPLAY                                                      
                         '** PARAGRAPH = B100-FORMAT-PURCHASE-TRANS'            
                   DISPLAY '** TRANS-CDE ' ML030-PURCH-TRANS-CDE                
                             ' ON PURCHASE-EXTRACT-RECORD IS NOT VALID.'        
                   DISPLAY '** PURCHASE-EXTRACT-RECORD: '                       
                           PURCHASE-EXTRACT-RECORD                              
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM Z100-READ-PURCHASE-EXTRACT.                                  
                                                                                
      ******************************************************************        
      * FORMAT AND WRITE THE SALE TRANSACTIONS.                        *        
      ******************************************************************        
       B200-FORMAT-SALE-TRANS.                                                  
                                                                                
           ADD +1                           TO RC-XT-SALE-EXTRACT-CNT.          
                                                                                
           IF  ML022-SALE-STORE-NBR  NOT = PV-STORE-NBR                         
               MOVE ML022-SALE-STORE-NBR    TO PV-STORE-NBR                     
               PERFORM B500-GET-STORE-DETAILS                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML022-SALE                                                  
                   MOVE SPACE               TO D115-SALES-REC                   
                   MOVE ML022-SALE-TRANS-CDE                                    
                                            TO D115-CODE-20                     
                   MOVE ML022-SALE-STORE-NBR                                    
                                            TO D115-STORE-20                    
                   MOVE ML022-SALE-DEPT-NBR TO D115-DEPT-20X                    
                   MOVE ML022-SALE-CLASS-NBR                                    
                                            TO D115-CLASS-20X                   
                   MOVE ML022-SALE-FIN-LIAB-DTE                                 
                                            TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-SALES-MM                    
                   MOVE PV-ISO-FMT-DD       TO D115-SALES-DD                    
                   MOVE PV-ISO-FMT-YY       TO D115-SALES-YY                    
                   MOVE ML022-SALE-EXTD-RTL-AMT                                 
                                            TO D115-SALE-AMT                    
                   MOVE ML022-SALE-FSCL-WK-NBR                                  
                                            TO D115-SALE-WEEK                   
                   MOVE ML022-SALE-FSCL-DAY TO D115-SALE-DAY                    
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-SR-FISCAL-PERIOD            
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-SR-FISCAL-WK                
                   MOVE ML022-SALE-TYPE-2   TO D115-SALE-TYP-CDE                
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-20          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-20                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-20          
                   MOVE D115-SALES-REC      TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO RC-ML-SALE-CNT                   
               WHEN OTHER                                                       
                   MOVE +4008               TO ABEND-CODE                       
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM = VAKUP030'                              
                   DISPLAY '** PARAGRAPH = B200-FORMAT-SALE-TRANS'              
                   DISPLAY '** TRANS-CDE ' ML022-SALE-TRANS-CDE                 
                         ' ON SALE-EXTRACT-RECORD IS NOT VALID.'                
                   DISPLAY '** SALE-EXTRACT-RECORD: '                           
                           SALE-EXTRACT-RECORD                                  
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM Z200-READ-SALE-EXTRACT.                                      
                                                                                
      ******************************************************************        
      * FORMAT AND WRITE THE PRICE CHANGE TRANSACTIONS.                *        
      ******************************************************************        
       B300-FORMAT-PRICE-CHANGE-TRANS.                                          
                                                                                
           ADD +1                           TO                                  
                                         RC-XT-PRICE-CHANGE-EXTRACT-CNT.        
                                                                                
           IF  ML029-PC-STORE-NBR  NOT = PV-STORE-NBR                           
               MOVE ML029-PC-STORE-NBR      TO PV-STORE-NBR                     
               PERFORM B500-GET-STORE-DETAILS                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML029-PC-MARKDOWN                                           
                   MOVE SPACE               TO D115-MKDWN-REC                   
                   MOVE ML029-PC-TRANS-CDE  TO D115-CODE-40                     
                   MOVE ML029-PC-BATCH-NBR  TO D115-BATCH-NO-40                 
                   MOVE ML029-PC-STORE-NBR  TO D115-STORE-40                    
                   MOVE ML029-PC-DEPT-NBR   TO D115-DEPT-40X                    
                   MOVE ML029-PC-CLASS-NBR  TO D115-CLASS-40X                   
                   MOVE ML029-PC-DOC-5-NBR  TO D115-DOC-NO-40                   
                   MOVE ML029-PC-FIN-LIAB-DTE                                   
                                            TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-MKDWN-MM                    
                   MOVE PV-ISO-FMT-DD       TO D115-MKDWN-DD                    
                   MOVE ML029-PC-KOHLS-MM   TO D115-KOHLS-MM-40                 
                   MOVE ML029-PC-OLD-RSN-CDE                                    
                                            TO D115-MKDWN-REASON                
                   MOVE ML029-PC-OLD-PR-AMT TO D115-MKDWN-OPRICE                
                   MOVE ML029-PC-NEW-PR-AMT TO D115-MKDWN-NPRICE                
                   MOVE ML029-PC-QTY        TO D115-MKDWN-QTY                   
                   MOVE ML029-PC-CATEGORY   TO D115-CATEGORY-40                 
                   MOVE ML029-PC-DOC-7-NBR  TO                                  
                                           D115-7-DIGIT-DOCUMENT-NMBR-40        
                   MOVE ML029-PC-MKDN-MKUP-RSN-CDE                              
                                            TO                                  
                                            D115-MKDWN-MKDN-MKUP-RSN-CDE        
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-MKDWN-FISCAL-PERIOD         
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-MKDWN-FISCAL-WK             
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-40          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-40                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-40          
                   MOVE D115-MKDWN-REC      TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO RC-ML-MARKDOWN-CNT               
               WHEN ML029-PC-MARKUP                                             
                   MOVE SPACE               TO D115-MARKON-REC                  
                   MOVE ML029-PC-TRANS-CDE  TO D115-CODE-50                     
                   MOVE ML029-PC-BATCH-NBR  TO D115-BATCH-NO-50                 
                   MOVE ML029-PC-STORE-NBR  TO D115-STORE-50                    
                   MOVE ML029-PC-DEPT-NBR   TO D115-DEPT-50X                    
                   MOVE ML029-PC-CLASS-NBR  TO D115-CLASS-50X                   
                   MOVE ML029-PC-DOC-5-NBR  TO D115-DOC-NO-50                   
                   MOVE ML029-PC-FIN-LIAB-DTE                                   
                                            TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-MARKON-MM                   
                   MOVE PV-ISO-FMT-DD       TO D115-MARKON-DD                   
                   MOVE ML029-PC-KOHLS-MM   TO D115-KOHLS-MM-50                 
                   MOVE ML029-PC-OLD-RSN-CDE                                    
                                            TO D115-MARKON-REASON               
                   MOVE ML029-PC-OLD-PR-AMT TO D115-MARKON-OPRICE               
                   MOVE ML029-PC-NEW-PR-AMT TO D115-MARKON-NPRICE               
                   MOVE ML029-PC-QTY        TO D115-MARKON-QTY                  
                   MOVE ML029-PC-CATEGORY   TO D115-CATEGORY-50                 
                   MOVE ML029-PC-DOC-7-NBR  TO                                  
                                           D115-7-DIGIT-DOCUMENT-NMBR-50        
                   MOVE ML029-PC-MKDN-MKUP-RSN-CDE                              
                                            TO                                  
                                           D115-MARKON-MKDN-MKUP-RSN-CDE        
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-MARKON-FISCAL-PERIOD        
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-MARKON-FISCAL-WK            
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-50          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-50                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-50          
                   MOVE D115-MARKON-REC     TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO RC-ML-MARKUP-CNT                 
               WHEN OTHER                                                       
                   MOVE +4008               TO ABEND-CODE                       
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM = VAKUP030'                              
                   DISPLAY                                                      
                         '** PARAGRAPH = B300-FORMAT-PRICE-CHANGE-TRANS'        
                   DISPLAY '** TRANS-CDE ' ML029-PC-TRANS-CDE                   
                         ' ON PRICE-CHANGE-EXTRACT-RECORD IS NOT VALID.'        
                   DISPLAY '** PRICE-CHANGE-EXTRACT-RECORD: '                   
                           PRICE-CHANGE-EXTRACT-RECORD                          
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM Z300-READ-PRICE-CHANGE-EXTRACT.                              
                                                                                
      ******************************************************************        
      * FORMAT AND WRITE THE ADJUSTMENT TRANSACTIONS.                  *        
      ******************************************************************        
       B400-FORMAT-ADJUSTMENT-TRANS.                                            
                                                                                
           ADD +1 TO RC-XT-ADJUSTMENT-EXTRACT-CNT.                              
                                                                                
           IF  ML024-ADJ-STORE-NBR  NOT = PV-STORE-NBR                          
               MOVE ML024-ADJ-STORE-NBR     TO PV-STORE-NBR                     
               PERFORM B500-GET-STORE-DETAILS                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
               WHEN ML024-ADJ-ADJUSTMENT                                        
                   MOVE SPACE               TO D115-ADJ-REC                     
                   MOVE ML024-ADJ-TRANS-CDE TO D115-CODE-80                     
                   MOVE ML024-ADJ-STORE-NBR TO D115-STORE-80                    
                   MOVE ML024-ADJ-DEPT-NBR  TO D115-DEPT-80X                    
                   MOVE ML024-ADJ-CLASS-NBR TO D115-CLASS-80X                   
                   MOVE ML024-ADJ-FIN-LIAB-DTE                                  
                                            TO PV-ISO-FMT-DATE                  
                   MOVE PV-ISO-FMT-MM       TO D115-ADJ-MM                      
                   MOVE PV-ISO-FMT-DD       TO D115-ADJ-DD                      
                   MOVE PV-ISO-FMT-YY       TO D115-ADJ-YY                      
                   MOVE ML024-ADJ-KOHLS-MM  TO D115-KOHLS-MM-80                 
                   MOVE ML024-ADJ-AT-RTL-AMT                                    
                                            TO D115-AT-RETAIL                   
                   MOVE ML024-ADJ-AT-COST-AMT                                   
                                            TO D115-AT-COST                     
                   MOVE DTATTR-FSCL-MN-NBR  TO D115-AR-FISCAL-PERIOD            
                   MOVE DTATTR-FSCL-WK-NBR  TO D115-AR-FISCAL-WK                
                   MOVE INCNTL-PLND-FIN-BKG-DTE                                 
                                            TO D115-PLND-FIN-BK-DTE-80          
                   MOVE INCNTL-INV-ID       TO D115-INV-ID-80                   
                   MOVE INVPAR-FINCL-INV-STAT-CDE                               
                                            TO                                  
                                            D115-FINCL-INV-STAT-CDE-80          
                   MOVE D115-ADJ-REC        TO ML-FORMATTED-TRANS-RECORD        
                   PERFORM Z500-WRITE-ML-FORMATTED-TRANS                        
                   ADD +1                   TO RC-ML-ADJUSTMENT-CNT             
               WHEN OTHER                                                       
                   MOVE +4008               TO ABEND-CODE                       
                   DISPLAY '** ABEND     **'                                    
                   DISPLAY '** PROGRAM = VAKUP030'                              
                   DISPLAY                                                      
                         '** PARAGRAPH = B400-FORMAT-ADJUSTMENT-TRANS'          
                   DISPLAY '** TRANS-CDE ' ML024-ADJ-TRANS-CDE                  
                         ' ON ADJUSTMENT-EXTRACT-RECORD IS NOT VALID.'          
                   DISPLAY '** ADJUSTMENT-EXTRACT-RECORD: '                     
                           ADJUSTMENT-EXTRACT-RECORD                            
                   CALL 'ILBOABN0'          USING ABEND-CODE                    
           END-EVALUATE.                                                        
                                                                                
           PERFORM Z400-READ-ADJUSTMENT-EXTRACT.                                
                                                                                
      ******************************************************************        
      * PARA TO FETCH THE INVENTORY ID, INV STAT CDE, FIN BKG DATE.    *        
      ******************************************************************        
       B500-GET-STORE-DETAILS.                                                  
                                                                                
           MOVE PV-STORE-NBR                TO PV-STORE-NBR-COMP.               
                                                                                
           PERFORM                                                              
               WITH TEST AFTER                                                  
               VARYING PV-RETRY-COUNT FROM 1 BY 1                               
                 UNTIL (PV-RETRY-COUNT > PV-MAX-RETRIES)                        
                    OR (SQLCODE         = +000)                                 
                                                                                
               EXEC SQL                                                         
                    SELECT B.PLND_FIN_BKG_DTE,                                  
                           B.INV_ID,                                            
                           A.FINCL_INV_STAT_CDE                                 
                      INTO :INCNTL-PLND-FIN-BKG-DTE,                            
                           :INCNTL-INV-ID,                                      
                           :INVPAR-FINCL-INV-STAT-CDE                           
                      FROM TINVPAR A                                            
                          ,TINCNTL B                                            
                     WHERE B.ACTV_IND = 'Y'                                     
                       AND A.INV_ID = B.INV_ID                                  
                       AND A.LOC_INV_STAT_CDE = 'IN'                            
                       AND A.LOC_NBR = :PV-STORE-NBR-COMP                       
                       AND A.ACTL_FIN_BK_DTE = '9999-09-09'                     
               END-EXEC                                                         
                                                                                
               EVALUATE TRUE                                                    
                   WHEN SQLCODE        = +000                                   
                        CONTINUE                                                
                   WHEN SQLCODE        = -904                                   
                   WHEN SQLCODE        = -911                                   
                       CONTINUE                                                 
                   WHEN OTHER                                                   
                       MOVE 'B500-GET-STORE-DETAILS'                            
                                            TO PV-PARAGRAPH-NAME                
                       MOVE 'SELECT ROW FROM TINVPAR'                           
                                            TO PV-DB2-OPER-ATTEMPTED            
                       PERFORM 9998-SQL-ABEND                                   
               END-EVALUATE                                                     
           END-PERFORM.                                                         
                                                                                
           IF  PV-RETRY-COUNT > PV-MAX-RETRIES                                  
               DISPLAY '** MAX RETRIES EXCEEDED ON TINVPAR **'                  
               MOVE 'B500-GET-STORE-DETAILS'                                    
                                            TO PV-PARAGRAPH-NAME                
               MOVE 'SELECT ROW FROM TINVPAR'                                   
                                            TO PV-DB2-OPER-ATTEMPTED            
               PERFORM 9998-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * FILE READ AND WRITE PARAS.                                     *        
      ******************************************************************        
       Z100-READ-PURCHASE-EXTRACT.                                              
                                                                                
           READ PURCHASE-EXTRACT-FILE       INTO ML030-PURCH-RECORD             
           AT END                                                               
           SET PS-EOF-PURCHASE-EXTRACT      TO TRUE.                            
                                                                                
       Z200-READ-SALE-EXTRACT.                                                  
                                                                                
           READ SALE-EXTRACT-FILE           INTO ML022-SALE-RECORD              
           AT END                                                               
           SET PS-EOF-SALE-EXTRACT          TO TRUE.                            
                                                                                
       Z300-READ-PRICE-CHANGE-EXTRACT.                                          
                                                                                
           READ PRICE-CHANGE-EXTRACT-FILE   INTO ML029-PC-RECORD                
           AT END                                                               
           SET PS-EOF-PRICE-CHANGE-EXTRACT  TO TRUE.                            
                                                                                
       Z400-READ-ADJUSTMENT-EXTRACT.                                            
                                                                                
           READ ADJUSTMENT-EXTRACT-FILE     INTO ML024-ADJ-RECORD               
           AT END                                                               
           SET PS-EOF-ADJUSTMENT-EXTRACT    TO TRUE.                            
                                                                                
       Z500-WRITE-ML-FORMATTED-TRANS.                                           
                                                                                
           WRITE ML-FORMATTED-TRANS-RECORD.                                     
                                                                                
      ******************************************************************        
      * THIS PARA DISPLAYS THE MESSAGE IN CASE OF ABEND.               *        
      ******************************************************************        
       9998-SQL-ABEND.                                                          
                                                                                
           MOVE +4013                       TO PV-ABEND-CODE.                   
           DISPLAY '** ABEND **         ** = SQLERROR'.                         
           DISPLAY '** PROGRAM          ** = VAKUP030'.                         
           DISPLAY '** PARAGRAPH        ** = ' PV-PARAGRAPH-NAME.               
           DISPLAY '** OPERATION        ** = ' PV-DB2-OPER-ATTEMPTED.           
                                                                                
      *    *------------------------------------------------------------        
      *    *THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE       *        
      *    *DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.      *        
      *    *------------------------------------------------------------        
           COPY DPPD004.                                                        
                                                                                
           PERFORM 9999-ABEND.                                                  
                                                                                
      ******************************************************************        
      * THIS PARA CALLS THE ABEND PROGRAM.                             *        
      ******************************************************************        
       9999-ABEND.                                                              
                                                                                
           CALL 'ILBOABN0'                  USING PV-ABEND-CODE.                
