      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
      *                                                                         
       PROGRAM-ID.    IMKUT088.                                                 
       AUTHOR.        PAUL KEBER.                                               
       INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
       DATE-WRITTEN   11/17/2006.                                               
       DATE-COMPILED.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *   IMKUT088 - ONHAND AND PERM VALIDATION FOR SKU COMPRESSION    *        
      *              OF A SKU ON A PO LESS THAN 2 YEARS                *        
      *----------------------------------------------------------------*        
      * THIS PROGRAM READS DB2 TABLES TO CHECK FOR THE EXISTENCE OF ANY*        
      * ONHAND OR FOR AN RCP PERM PRICE CHANGE WITHIN THE PAST 6 MONTHS*        
      * OR THE FUTURE FOR AN INPUT COMPRESSION CANDIDATE SKU. IF EITHER*        
      * ONE OF THESE CHECKS IS TRUE, THE SKU IS NOT ELIGIBLE FOR       *        
      * COMPRESSION.  THE RCP PERM PRICE CHANGE CHECK GOES BACK 6      *        
      * MONTHS IN ORDER TO MAKE SURE NO FURTHER MARKDOWNS COULD TAKE   *        
      * PLACE.  THE PROGRAM WILL WRITE OUT ALL VALID COMPRESSION SKUS  *        
      * THAT PASS THE TWO CHECKS TO A MOS OUTPUT FILE.                 *        
      *----------------------------------------------------------------*        
      *  CHANGED:                                                      *        
      *                                                                *        
      *    WR/PITS#     DATE        DESCRIPTION                        *        
      *    --------   ----------   ---------------------------------   *        
      *    WR29925    11-17-2006    INITIAL DEVELOPMENT.               *        
      *----------------------------------------------------------------*        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *                                                                         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT SKU-COMPRESS-CAND-IN                                          
               ASSIGN TO INP01.                                                 
           SELECT SKU-COMPRESS-ELIG-OUT                                         
               ASSIGN TO OUT01.                                                 
      *                                                                         
      *                                                                         
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *                                                                         
       FILE SECTION.                                                            
      *                                                                         
      *                                                                         
       FD  SKU-COMPRESS-CAND-IN                                                 
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-COMPRESS-CAND-IN-REC        PIC X(20).                           
      *                                                                         
       FD  SKU-COMPRESS-ELIG-OUT                                                
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS.                                            
       01  SKU-COMPRESS-ELIG-OUT-REC       PIC X(12).                           
      *                                                                         
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  ABEND-CODE                      PIC S9(04)  VALUE ZERO               
                                                       COMP SYNC.               
      *                                                                         
       01  PA-PROGRAM-ACCUMULATORS.                                             
           05  PA-RECORDS-READ             PIC 9(09)   VALUE ZERO.              
           05  PA-VALID-ONHAND-RECS        PIC 9(09)   VALUE ZERO.              
           05  PA-VALID-RECS-WRITTEN       PIC 9(09)   VALUE ZERO.              
           05  PA-COUNT                    PIC S9(7)   VALUE ZERO               
                                                        COMP-3.                 
           05  PA-RETRY-COUNT              PIC S9(04)   VALUE 0                 
                                                        COMP SYNC.              
      *                                                                         
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME             PIC X(08)   VALUE 'IMKUT088'.        
           05  PC-MAX-RETRY-COUNT          PIC S9(04)  VALUE +2 COMP.           
           05  PC-MORE-THAN-MAX-RETRY      PIC S9(04)  VALUE +3 COMP.           
      *                                                                         
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-END-OF-FILE-SW           PIC X(01)   VALUE 'N'.               
               88  PS-END-OF-FILE                      VALUE 'Y'.               
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-PARA-NAME                PIC X(35).                           
           05  PV-DB2-OPERATION-ATTEMPTED  PIC  X(30)    VALUE SPACES.          
           05  PV-SQLCODE-DISPLAY          PIC +(8)9     VALUE ZEROES.  00830000
      *                                                                         
       01  DL-DISPLAY-LINE1.                                                    
           05  FILLER                      PIC X(35)     VALUE                  
               '*** ITEM RECORDS READ:             '.                           
           05  DL-RECORDS-READ             PIC ZZZ,ZZZ,ZZ9 VALUE ZERO.          
       01  DL-DISPLAY-LINE2.                                                    
           05  FILLER                      PIC X(35)     VALUE                  
               '*** ITEM RECORDS WITH NO ON HANDS: '.                           
           05  DL-NO-ONHANDS               PIC ZZZ,ZZZ,ZZ9 VALUE ZERO.          
       01  DL-DISPLAY-LINE3.                                                    
           05  FILLER                      PIC X(35)     VALUE                  
               '*** MOS OUTPUT RECORDS WRITTEN:    '.                           
           05  DL-VALID-COMPRESS           PIC ZZZ,ZZZ,ZZ9 VALUE ZERO.          
                                                                                
      ******************************************************************        
      *  SKU COMPRESSION CANDIDATES RECORD LAYOUT                               
      ******************************************************************        
           COPY IMRD036.                                                FSKRP071
      *                                                                         
      ******************************************************************        
      *  SKU MOS OUTPUT RECORD LAYOUT                                           
      ******************************************************************        
           COPY IMRD030.                                                FSKRP071
      *                                                                         
      *                                                                         
           COPY SQLCA2.                                                         
      *                                                                         
           EXEC SQL                                                             
               INCLUDE SYSDUMMY                                                 
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *  TSKST - SKU STORE TABLE DCLGEN                                         
      *****************************************************************         
           EXEC SQL                                                             
                INCLUDE TSKST                                                   
           END-EXEC.                                                            
      *                                                                         
      *****************************************************************         
      *  XMT_PRCHG DCLGEN                                                       
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00009                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  XMT_STR_GP_PRCHG DCLGEN                                                
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00010                                                 
           END-EXEC.                                                            
                                                                                
      *****************************************************************         
      *  XMT_PRCHG_MDSE DCLGEN                                                  
      *****************************************************************         
           EXEC SQL                                                             
               INCLUDE XMT00011                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
      ******************************************************************02730094
      *  DB2 ERROR MESSAGES FORMAT AREA.                                02740094
      ******************************************************************02750094
           COPY DPWS004.                                                02760094
                                                                        02770094
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *                                                                         
           EJECT                                                                
       0000-IMKUT088.                                                           
      ******************************************************************        
      * MAIN PROCESSING ROUTINE                                        *        
      ******************************************************************        
                                                                                
           OPEN INPUT  SKU-COMPRESS-CAND-IN.                                    
           OPEN OUTPUT SKU-COMPRESS-ELIG-OUT.                                   
                                                                                
           PERFORM 4000-READ-SKU-COMPRESS-CAND-IN.                              
                                                                                
           PERFORM 2000-PROCESS-COMPRESS-CANDS                                  
             UNTIL PS-END-OF-FILE.                                              
                                                                                
           PERFORM 3000-END-OF-JOB.                                             
           GOBACK.                                                              
                                                                                
      ******************************************************************        
      * DETERMINE IF EACH INPUT SKU IS ELIGIBLE FOR TUPCPLS COMPRESSION*        
      * BE CHECKING ONHAND AND FOR ANY PERM PRICE CHANGES.             *        
      ******************************************************************        
       2000-PROCESS-COMPRESS-CANDS.                                             
                                                                                
           MOVE '*2000-COMPRESS-CANDS. *' TO PV-PARA-NAME                       
      *                                                                         
           PERFORM 6000-CHECK-FOR-ONHANDS.                                      
           IF PA-COUNT = 0                                                      
               ADD 1 TO PA-VALID-ONHAND-RECS                                    
               PERFORM 6100-CHECK-PERM-SYSTEM                                   
               IF PA-COUNT = 0                                                  
                   PERFORM 5000-WRITE-VALID-COMPRESS                            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-SKU-COMPRESS-CAND-IN.                              
                                                                                
      ******************************************************************        
      * DISPLAY EOJ STATS                                              *        
      ******************************************************************        
       3000-END-OF-JOB.                                                         
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY '*** IMKUT088 RUN STATISTICS ***'.                           
           DISPLAY '*******************************'.                           
           MOVE PA-RECORDS-READ TO DL-RECORDS-READ.                             
           DISPLAY DL-DISPLAY-LINE1.                                            
           MOVE PA-VALID-ONHAND-RECS TO DL-NO-ONHANDS.                          
           DISPLAY DL-DISPLAY-LINE2.                                            
           MOVE PA-VALID-RECS-WRITTEN TO DL-VALID-COMPRESS.                     
           DISPLAY DL-DISPLAY-LINE3.                                            
           DISPLAY '*******************************'.                           
      *                                                                         
           CLOSE SKU-COMPRESS-CAND-IN                                           
                 SKU-COMPRESS-ELIG-OUT.                                         
      *                                                                         
      *                                                                         
       4000-READ-SKU-COMPRESS-CAND-IN.                                          
                                                                                
           READ SKU-COMPRESS-CAND-IN INTO IM036-SKU-COMPRESS-CAND-REC           
              AT END                                                            
                 SET PS-END-OF-FILE TO TRUE.                                    
                                                                                
           IF PS-END-OF-FILE                                                    
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1  TO PA-RECORDS-READ                                         
           END-IF.                                                              
                                                                                
       5000-WRITE-VALID-COMPRESS.                                               
                                                                                
           MOVE IM036-ITM-NBR     TO IM030-ITM-NBR.                             
           MOVE IM036-INTR-UPC-ID TO IM030-UPC-ID.                              
                                                                                
           WRITE SKU-COMPRESS-ELIG-OUT-REC                              REC.    
                 FROM IM030-ITEM-MOS-REC.                                       
                                                                                
           ADD 1 TO PA-VALID-RECS-WRITTEN.                                      
                                                                                
      ******************************************************************        
      * 6000-CHECK-FOR-ONHANDS - CHECK TSKST FOR ONHANDS               *        
      ******************************************************************        
       6000-CHECK-FOR-ONHANDS.                                                  
           MOVE '*6000-CHECK-FOR-ONHANDS           *' TO PV-PARA-NAME.          
                                                                                
           MOVE 0 TO PA-COUNT.                                                  
                                                                                
           PERFORM WITH TEST AFTER                                              
             VARYING PA-RETRY-COUNT                                             
             FROM 1 BY 1                                                        
             UNTIL SQLCODE = 0                                                  
               OR  SQLCODE = +100                                               
               OR  PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                          
             EXEC SQL                                                           
                 SELECT 1                                                       
                   INTO :PA-COUNT                                               
                   FROM SYSIBM.SYSDUMMY1 X                                      
                  WHERE EXISTS                                                  
                       (SELECT 1                                                
                           FROM TSKST                                           
                          WHERE ITEM_NMBR  = :IM036-ITM-NBR                     
                            AND ONHAND_QTY <> 0                                 
                            AND LOC_NBR    <> SMALLINT(0)                       
                            AND X.IBMREQD > ' ')                                
                 WITH UR                                                        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                 WHEN SQLCODE = +100                                            
                   CONTINUE                                                     
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                   CONTINUE                                                     
                 WHEN OTHER                                                     
                   MOVE 'SELECT TSKST' TO PV-DB2-OPERATION-ATTEMPTED            
                   PERFORM 9998-SQL-ERROR                                       
             END-EVALUATE                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                               
               MOVE 'SELECT TSKST' TO PV-DB2-OPERATION-ATTEMPTED                
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * CHECK PERM SYSTEM FOR ANY RCP PRICE CHANGE IN THE PAST 6 MONTHS*        
      * OR THE FUTURE FOR THIS ITEM.                                   *        
      ******************************************************************        
       6100-CHECK-PERM-SYSTEM.                                                  
           MOVE '*6100-CHECK-PERM-SYSTEM.           *' TO PV-PARA-NAME.         
                                                                                
           MOVE 0 TO PA-COUNT.                                                  
                                                                                
           PERFORM WITH TEST AFTER                                              
             VARYING PA-RETRY-COUNT                                             
             FROM 1 BY 1                                                        
             UNTIL SQLCODE = 0                                                  
               OR  SQLCODE = +100                                               
               OR  PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                          
             EXEC SQL                                                           
                SELECT 1                                                        
                  INTO :PA-COUNT                                                
                  FROM XMT_PRCHG A                                              
                     , XMT_STR_GP_PRCHG B                                       
                     , XMT_PRCHG_MDSE C                                         
                 WHERE A.PRCHG_ID = B.PRCHG_ID                                  
                   AND A.PRCHG_ID = C.PRCHG_ID                                  
                   AND A.PRCHG_TYP_CDE = 'RCP'                                  
                   AND B.STR_GP_TYP_CDE = C.STR_GP_TYP_CDE                      
                   AND B.STR_GP_ID = C.STR_GP_ID                                
                   AND B.PRCHG_EFF_DTE > CURRENT DATE - 6 MONTHS                
                   AND C.MDSE_HIER_CDE = 'SKU'                                  
                   AND C.SKU_NBR = :IM036-SKU                                   
                 FETCH FIRST 1 ROWS ONLY                                        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
                 WHEN SQLCODE = 0                                               
                 WHEN SQLCODE = +100                                            
                   CONTINUE                                                     
                 WHEN SQLCODE = -911                                            
                 WHEN SQLCODE = -913                                            
                   CONTINUE                                                     
                 WHEN OTHER                                                     
                   MOVE 'SELECT XMT_PRCHG' TO PV-DB2-OPERATION-ATTEMPTED        
                   PERFORM 9998-SQL-ERROR                                       
             END-EVALUATE                                                       
                                                                                
           END-PERFORM.                                                         
                                                                                
           IF PA-RETRY-COUNT > PC-MAX-RETRY-COUNT                               
               MOVE 'SELECT XMT_PRCHG' TO PV-DB2-OPERATION-ATTEMPTED            
               PERFORM 9998-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ******************************************************************        
      * ABEND ROUTINE FOR BAD SQL CALLS                                *        
      ******************************************************************        
       9998-SQL-ERROR.                                                          
           MOVE SQLCODE TO PV-SQLCODE-DISPLAY.                                  
                                                                                
           DISPLAY '** ABEND **'.                                               
           DISPLAY 'IMKUP062 - ABEND'.                                          
           DISPLAY 'PARAGRAPH = ' PV-PARA-NAME.                                 
           DISPLAY 'OPERATION = ' PV-DB2-OPERATION-ATTEMPTED.                   
           DISPLAY 'SQL CODE  = ' PV-SQLCODE-DISPLAY.                           
           DISPLAY 'INPUT SKU = ' IM036-SKU.                                    
           DISPLAY 'INPUT ITEM= ' IM036-ITM-NBR.                                
                                                                                
           COPY DPPD004.                                                        
                                                                                
           MOVE +4013 TO ABEND-CODE.                                            
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
