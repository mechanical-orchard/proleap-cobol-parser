      ***************************************************************** 09/20/95
       IDENTIFICATION DIVISION.                                         MRKUP077
      *****************************************************************    LV006
                                                                           CL**2
       PROGRAM-ID.             MRKUP085.                                   CL**2
       AUTHOR.                 JEFFREY A OGUTU.                            CL**2
       INSTALLATION.           KOHLS DEPARTMENT STORES.                    CL**2
       DATE-WRITTEN            01-07-98.                                   CL**2
       DATE-COMPILED.                                                      CL**2
                                                                           CL**2
      *****************************************************************    CL**2
      * THIS PROGRAM WILL UPDATE THE TMRITST TABLE WITH DATA FROM     *    CL**2
      * OLRITPR FILE. THE PTAG WILL BE UPDATED.                       *    CL**2
      *****************************************************************    CL**2
      *****************************************************************    CL**2
      *  CHANGES:                                                     *    CL**2
      *  MADE BY: CHRIS FOLEY - CAF                                   *    CL**2
      *     DATE: 10/13/98                                            *    CL**2
      *     DESC: REPLACED HARD CODING OF FILE LAYOUT WS-ITPR-RECORD  *    CL**2
      *           WITH MDS COPY BOOK OLRITPR.                         *    CL**2
      *****************************************************************    CL**2
      *  CHANGES:                                                     *    CL**2
      *  MADE BY: BOB FORD                                            *    CL**2
      *     DATE: 07/30/04                                            *    CL**2
      *     DESC: UPDATED PROGRAM FOR STORE NUMBER EXPANSION.         *    CL**2
      *****************************************************************    CL**2
      *  CHANGES:                                                     *    CL**2
      *  MADE BY: JEAN KURK                      CHG0271937           *    CL**2
      *     DATE: 07/19/22                                            *    CL**2
      *     DESC: RECOMPILED FOR COBOL UPGRADE - COPYBOOK OLRITPR     *    CL**2
      *           DID NOT EXIST, SO WAS CREATED                       *    CL**2
      *****************************************************************    CL**2
       ENVIRONMENT DIVISION.                                               CL**2
      *****************************************************************    CL**2
                                                                           CL**2
       CONFIGURATION SECTION.                                              CL**2
                                                                           CL**2
       SOURCE-COMPUTER.        IBM-3090.                                   CL**2
       OBJECT-COMPUTER.        IBM-3090.                                   CL**2
                                                                           CL**2
       INPUT-OUTPUT SECTION.                                               CL**2
                                                                           CL**2
       FILE-CONTROL.                                                       CL**2
                                                                           CL**2
      *                                                                    CL**2
           SELECT ITEM-PROFILE-FILE      ASSIGN TO INP01.                  CL**2
      *                                                                    CL**2
                                                                           CL**2
      *****************************************************************    CL**2
       DATA DIVISION.                                                      CL**2
      *****************************************************************    CL**2
      *                                                                    CL**2
       FILE SECTION.                                                       CL**2
      *                                                                    CL**2
                                                                           CL**2
       FD  ITEM-PROFILE-FILE                                               CL**2
           RECORDING MODE F                                                CL**2
           RECORD CONTAINS 80 CHARACTERS                                   CL**2
           LABEL RECORDS ARE STANDARD                                      CL**2
           BLOCK CONTAINS 0  RECORDS.                                           
      *    DATA RECORD IS ITEM-PROF-RECORD.                                CL**2
                                                                           CL**2
       01  ITEM-PROF-RECORD   PIC X(80).                                   CL**2
                                                                           CL**2
      ******************************************                           CL**2
       WORKING-STORAGE SECTION.                                            CL**2
      ******************************************                           CL**2
                                                                           CL**2
                                                                           CL**2
       01  FILLER.                                                         CL**2
           05  FILLER                      PIC  X(36)    VALUE             CL**2
               ' ***** WORKING STORAGE PGM=MRKUP085 '.                     CL**2
           05  FILLER                      PIC  X(10)    VALUE             CL**2
               ' LOCATION='.                                               CL**2
           05  PGM-LOCATION                PIC  X(04)    VALUE SPACE.      CL**2
           05  FILLER                      PIC  X(06)    VALUE             CL**2
               ' *****'.                                                   CL**2
                                                                           CL**2
       01  PROGRAM-SWITCHES.                                               CL**2
           05  PS-END-OF-ITPR-FILE-IND     PIC  X(01)    VALUE 'N'.             
               88  PS-END-OF-ITPR-FILE                   VALUE 'Y'.             
               88  PS-NOT-END-OF-ITPR-FILE               VALUE 'N'.             
                                                                           CL**4
       01  PC-CONSTANTS.                                                   CL**2
           05  PC-PLUS-1                   PIC S9(04)    VALUE +1          CL**2
                                                         COMP SYNC.        CL**2
           05  PC-RT-ZERO                  PIC S9(03)V   VALUE ZERO        CL**2
                                                         COMP-3.           CL**2
           05  PC-RT-DAY-ZERO              PIC S9(03)V   VALUE ZERO        CL**2
                                                         COMP-3.           CL**2
           05  PC-ROW-NOT-FOUND            PIC S9(04)    VALUE +100.       CL**2
                                                                                
                                                                           CL**2
       01  CD-CPU-DATE.                                                    CL**2
           05  CD-CPU-YR                    PIC 9(2)     VALUE ZEROS.      CL**2
           05  CD-CPU-MO                    PIC 9(2)     VALUE ZEROS.      CL**2
           05  CD-CPU-DY                    PIC 9(2)     VALUE ZEROS.      CL**2
                                                                           CL**2
       01  CT-CPU-TIME.                                                    CL**2
           05  CT-CPU-HR                    PIC 9(2)     VALUE ZEROS.      CL**2
           05  CT-CPU-MIN                   PIC 9(2)      VALUE ZEROS.     CL**2
           05  CT-CPU-SEC                   PIC 9(2)      VALUE ZEROS.     CL**2
           05  CT-CPU-HSEC                  PIC 9(2)      VALUE ZEROS.     CL**2
                                                                           CL**2
      *01  WS-ITPR-RECORD.                                                      
      *    05  WS-ITPR-SKU      PIC X(15).                                      
      *    05  FILLER           PIC X.                                          
      *    05  WS-ITPR-STORE    PIC X(3).                                       
      *    05  FILLER           PIC X.                                          
      *    05  WS-ITPR-ROS      PIC 9999999.99.                                 
      *    05  FILLER           PIC X.                                          
      *    05  WS-ITPR-PROFILE  PIC X(07).                                      
      *    05  FILLER           PIC X.                                          
      *    05  WS-ITPR-SESS-ID  PIC 9(7).                                       
      *    05  FILLER           PIC X(34).                                      
      *                                                                         
      * CAF 10/13/98                                                            
           COPY OLRITPR.                                                        
      * CAF 10/13/98 END                                                        
                                                                           CL**2
       01  PV-MISC-FIELDS.                                                 CL**2
           05  PV-CURR-TMST                PIC  X(26)    VALUE SPACES.     CL**2
           05  PV-COMMIT-TMST              PIC  X(26)    VALUE SPACES.     CL**2
           05  PV-ABEND-CODE               PIC  S9(04)   VALUE ZERO        CL**2
                                                         COMP SYNC.        CL**2
           05  PV-ABEND-PARAGRAPH          PIC  X(30)    VALUE SPACES.     CL**2
           05  PV-ITPR-PROFILE             PIC  X(07)    VALUE SPACES.     CL**2
           05  PV-ITPR-SKU                 PIC  S9(15)V  VALUE ZERO        CL**2
                                                         COMP-3.           CL**2
           05  PV-ITPR-STORE-X             PIC  X(5).                      CL**2
           05  PV-ITPR-STORE-9             PIC  9(5).                      CL**2
           05  PV-ITPR-STORE               PIC S9(4) COMP  VALUE ZERO.     CL**2
           05  PV-PTAG-CNTR                PIC  S9(05)   VALUE ZERO        CL**2
                                                         COMP.             CL**2
           05  PV-CORP-PTAG-CNTR           PIC  S9(05)   VALUE ZERO        CL**2
                                                         COMP.             CL**2
           05  PV-NO-PTAG-CNTR             PIC S9(05)    VALUE ZERO        CL**2
                                                         COMP.             CL**2
           05  PV-TOTAL-PTAG-CNTR          PIC S9(05)    VALUE ZERO        CL**2
                                                         COMP.             CL**2
           05  PV-PTAG-DISP                PIC ZZZZ9-.                     CL**2
           05  PV-NO-PTAG-DISP             PIC ZZZZ9-.                     CL**2
           05  PV-TOTAL-PTAG-DISP          PIC ZZZZ9-.                     CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               INCLUDE TMRITST                                             CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               INCLUDE TCKRSTCN                                            CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
                                                                           CL**2
      * DB2 COMMUNICATION AREA                                             CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               INCLUDE SQLCA                                               CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
      * DB2 COMMUNICATION AREA2                                            CL**2
                                                                           CL**2
           COPY SQLCA2.                                                    CL**2
                                                                           CL**2
      *                                                                    CL**4
      *****************************************************************    CL**2
       PROCEDURE DIVISION.                                                 CL**2
      *****************************************************************    CL**2
      *                                                                    CL**4
       0000-MAINLINE-PROCESSING.                                           CL**2
                                                                           CL**2
           PERFORM 1000-INITIALIZATION.                                    CL**2
                                                                                
           PERFORM 9000-READ-ITEM-PROFILE-FILE.                            CL**2
                                                                           CL**2
           PERFORM 2000-UPDATE-PTAG                                        CL**2
                   UNTIL PS-END-OF-ITPR-FILE.                                 CL
                                                                           CL**2
           PERFORM 4000-DISPLAY-UPDATES.                                   CL**2
                                                                           CL**2
           PERFORM 9600-COMMIT.                                            CL**2
                                                                           CL**2
           CLOSE  ITEM-PROFILE-FILE.                                       CL**2
                                                                           CL**2
           GOBACK.                                                         CL**2
      *                                                                    CL**2
      ******************************************************************   CL**2
      ** OPEN THE SYSTEM FILE AND ACCEPT CONTROL CARDS.                    CL**2
      ** CALL CALENDAR ROUTINES TO RETRIEVE VALUES NEEDED IN PROGRAM       CL**2
      ******************************************************************   CL**2
      *                                                                    CL**2
                                                                           CL**2
       1000-INITIALIZATION.                                                CL**2
                                                                           CL**2
           OPEN INPUT  ITEM-PROFILE-FILE.                                  CL**2
                                                                           CL**2
      ***************************************                              CL**2
      *  SET UP FIRST SCHEDULED COMMIT TIME *                              CL**2
      ***************************************                              CL**2
           EXEC SQL                                                        CL**2
               SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       CL**2
                 INTO  :PV-COMMIT-TMST                                     CL**2
                 FROM  TCKRSTCNTL                                          CL**2
                WHERE  PLAN_NAME = 'MRKUP085'                              CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
           EVALUATE TRUE                                                   CL**6
             WHEN SQLCODE         = ZERO                                   CL**6
               CONTINUE                                                    CL**2
             WHEN OTHER                                                    CL**6
               DISPLAY 'PARAGRAPH 1000, BAD SELECT FOR TIMESTAMP'          CL**2
               DISPLAY 'SQLCODE ' SQLCODE                                  CL**2
               DISPLAY ' '                                                 CL**2
               DISPLAY PV-ABEND-PARAGRAPH                                  CL**6
               PERFORM 9999-SQL-ERROR                                      CL**6
           END-EVALUATE.                                                   CL**6
                                                                                
                                                                                
      *  GET THE CURRENT TIMESTAMP                                         CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               SET :PV-CURR-TMST = CURRENT TIMESTAMP                       CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
                                                                           CL**2
           EVALUATE TRUE                                                   CL**2
             WHEN SQLCODE         = ZERO                                   CL**2
               CONTINUE                                                    CL**2
             WHEN OTHER                                                    CL**2
               DISPLAY 'PARAGRAPH 1000  BAD SET FOR CURRENT TIMESTAMP'     CL**2
               DISPLAY ' '                                                 CL**2
               DISPLAY PV-ABEND-PARAGRAPH                                  CL**2
               PERFORM 9999-SQL-ERROR                                      CL**2
           END-EVALUATE.                                                   CL**2
                                                                           CL**2
                                                                                
                                                                           CL**2
      ******************************************************************   CL**2
      *  PROCESS ITEM PROFILE RECORD                                   *   CL**2
      ******************************************************************   CL**2
                                                                           CL**2
      *                                                                    CL**2
       2000-UPDATE-PTAG.                                                   CL**2
      *                                                                    CL**2
           MOVE '2000'                TO PGM-LOCATION.                     CL**2
           MOVE '2000-UPDATE-PTAG'    TO PV-ABEND-PARAGRAPH.               CL**2
                                                                           CL**2
      * *******************************************************                 
      * CHANGE MADE TO UTILIZE STORE 91 AS TEMPLATE FOR CORP-STORE              
      ******************** ** 7/98 * S.MEYER ** ***************                 
                                                                                
           MOVE ITPR-SKU         TO PV-ITPR-SKU.                           CL**2
           MOVE ITPR-PROFILE     TO PV-ITPR-PROFILE.                       CL**2
                                                                                
           IF ITPR-STORE  EQUAL '91' OR '091' OR '00091'                        
                                                                                
              ADD +1                   TO PV-CORP-PTAG-CNTR                     
              MOVE ZERO                TO PV-ITPR-STORE                         
                                          PV-ITPR-STORE-9                  CL**2
              MOVE SPACES              TO PV-ITPR-STORE-X                       
              PERFORM 2100-UPDATE-RECORD                                        
                                                                                
              PERFORM 9500-COMMIT-TEST                                        CL
           END-IF                                                               
                                                                           CL**2
           MOVE ITPR-STORE       TO PV-ITPR-STORE-X.                       CL**2
           MOVE PV-ITPR-STORE-X  TO PV-ITPR-STORE-9.                       CL**2
           MOVE PV-ITPR-STORE-9  TO PV-ITPR-STORE.                         CL**2
                                                                           CL**2
           PERFORM 2100-UPDATE-RECORD.                                          
                                                                                
           PERFORM 9500-COMMIT-TEST.                                          CL
                                                                                
           PERFORM 9000-READ-ITEM-PROFILE-FILE.                            CL**2
                                                                              CL
       2100-UPDATE-RECORD.                                                 CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
              UPDATE TMRITST                                               CL**2
                 SET PTAG_ID           = :PV-ITPR-PROFILE,                 CL**2
                     PTAG_LAST_UPD_DTE = CURRENT DATE                      CL**2
              WHERE ITEM_NBR = :PV-ITPR-SKU                                CL**2
              AND   STR_NBR  = :PV-ITPR-STORE                              CL**2
           END-EXEC.                                                       CL**2
                                                                                
                                                                                
           EVALUATE TRUE                                                        
            WHEN SQLCODE         = ZERO                                         
              ADD +1              TO PV-PTAG-CNTR                               
      *       CONTINUE                                                          
            WHEN SQLCODE         = PC-ROW-NOT-FOUND                             
               PERFORM 2200-ERROR-DISPLAY                                  CL**6
            WHEN OTHER                                                          
               DISPLAY PV-ABEND-PARAGRAPH                                       
               PERFORM 9999-SQL-ERROR                                           
           END-EVALUATE.                                                        
                                                                                
       2200-ERROR-DISPLAY.                                                 CL**2
                                                                           CL**2
           DISPLAY 'BAD UPDATE  ITEM#=: ' ITPR-SKU.                        CL**2
           DISPLAY 'STORE NOT FOUND:    ' ITPR-STORE.                      CL**2
           DISPLAY 'PTAG NOT UPDATED:   ' ITPR-PROFILE.                    CL**2
                                                                           CL**2
           ADD +1              TO PV-NO-PTAG-CNTR.                              
                                                                                
                                                                           CL**2
      ******************************************************************   CL**2
      * THIS PARAGRAPH DISPLAYS IN SYSOUT A COUNT OF ALL UPDATES TO THE    CL**2
      * DB2 TABLES.                                                        CL**2
      ******************************************************************   CL**2
                                                                           CL**2
       4000-DISPLAY-UPDATES.                                               CL**2
           COMPUTE PV-TOTAL-PTAG-CNTR = PV-PTAG-CNTR +                     CL**2
                                                 PV-NO-PTAG-CNTR.          CL**2
                                                                           CL**2
           MOVE    PV-PTAG-CNTR       TO PV-PTAG-DISP.                     CL**2
           MOVE    PV-NO-PTAG-CNTR    TO PV-NO-PTAG-DISP.                  CL**2
           MOVE    PV-TOTAL-PTAG-CNTR TO PV-TOTAL-PTAG-DISP.               CL**2
           DISPLAY '                              '.                       CL**2
           DISPLAY 'TMRITST PTAGS UPDATED:     '  PV-PTAG-DISP.            CL**2
           DISPLAY 'TMRITST PTAGS NOT UPDATED: '  PV-NO-PTAG-DISP.         CL**2
           DISPLAY '                             --------- '.              CL**2
           DISPLAY 'TOTAL NUMBER OF PTAGS:     '  PV-TOTAL-PTAG-DISP.      CL**2
           DISPLAY '                             --------- '.              CL**2
           DISPLAY '                              '.                       CL**2
           DISPLAY 'TMRITST CORP-STORE UPDATES:'  PV-CORP-PTAG-CNTR.            
                                                                                
      ******************************************************************        
      *  9000-READ-ITEM-PROFILE-FILE.                                  *        
      *  THIS PROCEDURE WILL READ ONE ITPR RECORD FROM THE ITPR        *        
      *  FLAT FILE.                                                    *        
      ******************************************************************        
                                                                                
       9000-READ-ITEM-PROFILE-FILE.                                             
                                                                                
           READ ITEM-PROFILE-FILE INTO ITPR-RECORD                              
               AT END SET PS-END-OF-ITPR-FILE TO TRUE.                          
                                                                                
                                                                           CL**2
      ******************************************************************   CL**2
      *     DB2 COMMIT TEST                                            *   CL**2
      *                                                                *   CL**2
      ******************************************************************   CL**2
                                                                           CL**2
       9500-COMMIT-TEST.                                                   CL**2
                                                                           CL**2
           MOVE '9500'                   TO PGM-LOCATION.                  CL**2
           MOVE '9500-COMMIT-TEST     '  TO PV-ABEND-PARAGRAPH.            CL**2
                                                                           CL**2
      *  GET THE CURRENT TIMESTAMP                                         CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               SET :PV-CURR-TMST = CURRENT TIMESTAMP                       CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
           EVALUATE TRUE                                                   CL**2
             WHEN SQLCODE         = ZERO                                   CL**2
               CONTINUE                                                    CL**2
             WHEN OTHER                                                    CL**2
               DISPLAY 'PARAGRAPH 9500  BAD SET FOR CURRENT TIMESTAMP'     CL**2
               DISPLAY ' '                                                 CL**2
               DISPLAY PV-ABEND-PARAGRAPH                                  CL**2
               PERFORM 9999-SQL-ERROR                                      CL**2
           END-EVALUATE.                                                   CL**2
                                                                           CL**2
      *  COMPARE TO THE NEXT SCHEDULED COMMIT TIME                         CL**2
                                                                           CL**2
           IF PV-CURR-TMST > PV-COMMIT-TMST                                CL**2
               PERFORM 9600-COMMIT                                         CL**2
           END-IF.                                                         CL**2
                                                                           CL**2
       EJECT                                                               CL**2
      ******************************************************************   CL**2
      *     DB2 COMMIT PROCESSING                                      *   CL**2
      *                                                                *   CL**2
      ******************************************************************   CL**2
                                                                           CL**2
       9600-COMMIT.                                                        CL**2
                                                                           CL**2
      *  TAKE COMMIT                                                       CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               COMMIT                                                      CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
                                                                           CL**2
           EVALUATE TRUE                                                   CL**2
             WHEN SQLCODE         = ZERO                                   CL**2
               CONTINUE                                                    CL**2
             WHEN OTHER                                                    CL**2
               DISPLAY 'PARAGRAPH 9600  BAD COMMIT'                        CL**2
               DISPLAY ' '                                                 CL**2
               DISPLAY PV-ABEND-PARAGRAPH                                  CL**2
               PERFORM 9999-SQL-ERROR                                      CL**2
           END-EVALUATE.                                                   CL**2
                                                                           CL**2
      *  GET THE NEXT SCHEDULED COMMIT TIME                                CL**2
                                                                           CL**2
           EXEC SQL                                                        CL**2
               SELECT  (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       CL**2
                 INTO  :PV-COMMIT-TMST                                     CL**2
                 FROM  TCKRSTCNTL                                          CL**2
                WHERE  PLAN_NAME = 'MRKUP085'                              CL**2
           END-EXEC.                                                       CL**2
                                                                           CL**2
           EVALUATE TRUE                                                   CL**6
             WHEN SQLCODE         = ZERO                                   CL**6
               CONTINUE                                                    CL**2
             WHEN OTHER                                                    CL**6
               DISPLAY 'PARAGRAPH 9600  SELECT FOR NEXT TIMESTAMP'         CL**2
               DISPLAY ' '                                                 CL**2
               DISPLAY PV-ABEND-PARAGRAPH                                  CL**6
               PERFORM 9999-SQL-ERROR                                      CL**6
           END-EVALUATE.                                                   CL**6
      *                                                                    CL**2
      ******************************************************************   CL**2
      *   ERROR MESSAGE ROUTINE                                            CL**2
      ******************************************************************   CL**2
      *                                                                    CL**2
       9999-SQL-ERROR.                                                     CL**2
                                                                           CL**2
           MOVE SQLCODE    TO SQLCODE2.                                    CL**2
           MOVE SQLERRD(3) TO SQLERRD3.                                    CL**2
           DISPLAY '** ABEND **       ** = SQLERROR'.                      CL**2
           DISPLAY '** PROGRAM        ** = MRKUP085'.                      CL**2
           DISPLAY '** SQLCODE        ** = ' SQLCODE2.                     CL**2
           DISPLAY '** ROWS PROCESSED ** = ' SQLERRD3.                     CL**2
           DISPLAY '** SQLERRM        ** = ' SQLERRM.                      CL**2
           DISPLAY '** SQLERRMC       ** = ' SQLERRMC.                     CL**2
           MOVE +4013 TO PV-ABEND-CODE.                                    CL**2
           CALL 'ILBOABN0' USING PV-ABEND-CODE.                            CL**2
      ******************************************************************   CL**2
