       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.     APKUT403.                                                
       AUTHOR.         JILL JUEL.                                               
       DATE-WRITTEN.   05/24/2000.                                              
       DATE-COMPILED.                                                           
      *************************************************************             
      *    PO SPECIAL INSTRUCTIONS FOR PRG AUDIT                  *             
      *************************************************************             
      *    COBOL 2 WITH SQL                                       *             
      *************************************************************             
      *                                                           *             
      * DB2 TABLES:                                               *             
      *    TPOINST - PURCHASE ORDER INSTRUCTION TABLE             *             
      *    TINSTCD - INSTRUCTION NUMBER TABLE                     *             
      *    TENTNME - ENTITY NAME TABLE                            *             
      *    TEXTENT - EXTERNAL ENTERPRISE                          *             
      *                                                           *             
      * INPUT:                                                    *             
      * 1.  PO NUMBER FILE                                        *             
      *                                                           *             
      * OUTPUT:                                                   *             
      * 1.  FLAT FILE OF SPECIAL INSTRUCTIONS FOR PRG AUDIT       *             
      *                                                           *             
      *************************************************************             
      * CHANGE LOG:                                               *             
      *                                                           *             
      * 05/24/2000  INITIAL VERSION.                              *             
      *                                                           *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT PO-NBR-FILE                ASSIGN TO UT-S-INP01.              
           SELECT SPECIAL-INSTRUCTIONS-FILE  ASSIGN TO UT-S-OUT01.              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  PO-NBR-FILE                                                          
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           DATA RECORD IS PO-NBR-REC.                                           
                                                                                
       01  PO-NBR-REC                       PIC  X(800).                        
                                                                                
       FD  SPECIAL-INSTRUCTIONS-FILE                                            
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           DATA RECORD IS  SPECIAL-INSTRUCTIONS-RECORD.                         
                                                                                
       01  SPECIAL-INSTRUCTIONS-RECORD      PIC X(200).                         
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  PROGRAM-VARIABLES.                                                   
           05  PV-PO-NBR                    PIC  S9(9) COMP VALUE ZERO.         
                                                                                
       01  PROGRAM-SWITCHES.                                                    
           05  PS-END-OF-POS-SW             PIC X(01) VALUE 'N'.                
               88  NOT-PO-NBR-EOF                     VALUE 'N'.                
               88  PO-NBR-EOF                         VALUE 'Y'.                
           05  PS-INSTRUC-EOC-SW            PIC X(01) VALUE 'N'.                
               88  NOT-INSTRUC-EOC                    VALUE 'N'.                
               88  INSTRUC-EOC                        VALUE 'Y'.                
                                                                                
       01  PROGRAM-COUNTERS.                                                    
           05  PO-RECORDS-IN                PIC  9(09) VALUE ZERO.              
           05  SPECIAL-INSTRUC-OUT          PIC  9(09) VALUE ZERO.              
           05  INSTRUC-CSR-COUNT            PIC  9(09) VALUE ZERO.              
                                                                                
       01  ABEND-CODE                       PIC S9(4)  COMP SYNC                
                                                       VALUE ZEROS.             
           88 AP-DB2-ERROR                             VALUE +4013.             
                                                                                
       01  PV-RETRY-COUNTER                 PIC S9(4)  COMP VALUE ZERO.         
       01  PV-ABEND-FIELDS.                                                     
           05  PV-ABEND-PROGRAM             PIC X(8)  VALUE 'APKUT403'.         
           05  AA-PARAGRAPH-NAME            PIC X(50) VALUE SPACES.             
           05  AA-DB2-OPERATION             PIC X(50) VALUE SPACES.             
           05  AA-DB2-TABLE-1               PIC X(8)  VALUE SPACES.             
           05  AA-DB2-TABLE-2               PIC X(8)  VALUE SPACES.             
           05  AA-DB2-TABLE-3               PIC X(8)  VALUE SPACES.             
           05  PV-ABEND-STATUS              PIC XX    VALUE SPACES.             
                                                                                
                                                                                
      ****************************************************************          
      *  COPYBOOK FOR PO NUMBER FILE                                 *          
      ****************************************************************          
           COPY APRD400.                                                        
                                                                                
      ****************************************************************          
      *  COPYBOOK FOR PRG AUDIT SPECIAL INSTRUCTIONS FILE            *          
      ****************************************************************          
           COPY APRD403.                                                        
                                                                                
      ****************************************************************          
      * DPWS004 CONTAINS THE FIELDS NECESSARY TO CALL THE SQLCA      *          
      * MESSAGE MODULE 'DSNTIAR' AND DISPLAY THE FORMATTED RESULTS.  *          
      ****************************************************************          
           COPY DPWS004.                                                        
                                                                                
      ****************************************************************          
      *  COMMUNICATIONS AREA2 FOR DB2                                *          
      ****************************************************************          
           COPY SQLCA2.                                                         
       EJECT                                                                    
      ****************************************************************          
      *  SQL COMMUNICATIONS AREA DCLGEN                              *          
      ****************************************************************          
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
       EJECT                                                                    
      ****************************************************************          
      *  DCLGEN FOR TPOINST                                          *          
      ****************************************************************          
           EXEC SQL                                                             
                INCLUDE TPOINST                                                 
           END-EXEC.                                                            
       EJECT                                                                    
      ****************************************************************          
      *  DCLGEN FOR TINSTCD                                          *          
      ****************************************************************          
           EXEC SQL                                                             
                INCLUDE TINSTCD                                                 
           END-EXEC.                                                            
       EJECT                                                                    
      ****************************************************************          
      *  DCLGEN FOR TENTNME                                          *          
      ****************************************************************          
           EXEC SQL                                                             
                INCLUDE TENTNME                                                 
           END-EXEC.                                                            
       EJECT                                                                    
      ****************************************************************          
      *  DCLGEN FOR TEXTENT                                          *          
      ****************************************************************          
           EXEC SQL                                                             
                INCLUDE TEXTENT                                                 
           END-EXEC.                                                            
       EJECT                                                                    
      ****************************************************************          
      * DECLARE SPECIAL INSTRUCTION CURSOR                           *          
      ****************************************************************          
           EXEC SQL                                                             
               DECLARE INSTRUC_CSR CURSOR FOR                                   
                SELECT IC.INSTR_DESC                                            
                      ,SI.INSTR_NBR                                             
                      ,SI.RECIP_CDE                                             
                FROM   TPOINST SI                                               
                      ,TINSTCD IC                                               
                WHERE SI.PO_NBR    = :PV-PO-NBR                                 
                  AND IC.INSTR_NBR = SI.INSTR_NBR                               
                ORDER BY SI.RECIP_CDE, SI.INSTR_NBR                             
               END-EXEC.                                                        
       EJECT                                                                    
                                                                                
      ***********************                                                   
       PROCEDURE DIVISION.                                                      
      ***********************                                                   
                                                                                
       A100-MAINLINE.                                                           
                                                                                
           PERFORM B100-HOUSEKEEPING.                                           
                                                                                
           PERFORM B200-PROCESSING                                              
               UNTIL PO-NBR-EOF.                                                
                                                                                
           PERFORM B300-TERMINATION.                                            
                                                                                
           GOBACK.                                                              
              EJECT                                                             
                                                                                
      *****************************************************************         
      *    GET DATE, TIME, OPEN FILES, SET UP CONTROLS, ETC                     
      *****************************************************************         
       B100-HOUSEKEEPING.                                                       
           DISPLAY ' '.                                                         
           DISPLAY '**--------------- APKUT403 --------------------**'.         
           DISPLAY '  RUN DATE = ' FUNCTION CURRENT-DATE(1:8)                   
                   '  RUN TIME = ' FUNCTION CURRENT-DATE(9:6)                   
                                                                                
           OPEN  INPUT   PO-NBR-FILE                                            
                 OUTPUT  SPECIAL-INSTRUCTIONS-FILE.                             
                                                                                
           INITIALIZE SPECIAL-INSTRUCTIONS-RECORD.                              
                                                                                
           PERFORM R100-READ-PO-NUMBER.                                         
                                                                                
      **************************************************************            
      *  THIS ROUTINE PROCESSES THE PO FILE, CREATING AN OUTPUT    *            
      *  RECORD FOR EACH SPECIAL INSTRUCTION.                      *            
      **************************************************************            
       B200-PROCESSING.                                                         
                                                                                
           MOVE AP400-PO-NBR TO PV-PO-NBR.                                      
           PERFORM Y100-OPEN-INSTRUC-CSR.                                       
           PERFORM R200-FETCH-INSTRUC-CSR.                                      
           IF NOT-INSTRUC-EOC                                                   
              PERFORM R300-SELECT-VENDOR-NAME                                   
              MOVE ENTNME-ENT-NAME-DESC TO AP403-VND-NAME                       
           END-IF.                                                              
           PERFORM UNTIL INSTRUC-EOC                                            
               MOVE AP400-PO-NBR       TO AP403-PO-NBR                          
               MOVE AP400-DUN-NBR      TO AP403-DUN-NBR                         
               MOVE POINST-RECIP-CDE   TO AP403-RECIP-CDE                       
               MOVE INSTCD-INSTR-DESC  TO AP403-SPECIAL-INSTR                   
               PERFORM W100-WRITE-SPECIAL-INSTRUC                               
               MOVE AP400-PO-NBR TO PV-PO-NBR                                   
               PERFORM R200-FETCH-INSTRUC-CSR                                   
           END-PERFORM.                                                         
           PERFORM Y200-CLOSE-INSTRUC-CSR.                                      
                                                                                
           PERFORM R100-READ-PO-NUMBER.                                         
                                                                                
          EJECT                                                                 
                                                                                
      ***************************************************************           
      *  PROGRAM TERMINATION: DISPLAYS TOTALS AND CLOSES FILES      *           
      ***************************************************************           
       B300-TERMINATION.                                                        
           DISPLAY '************************************************'.          
           DISPLAY '****         TOTALS FOR APKUT403            ****'.          
           DISPLAY '************************************************'.          
           DISPLAY 'PO NUMBER RECORDS READ  = ' PO-RECORDS-IN.                  
           DISPLAY 'SPECIAL INSTRUC CURSOR  = ' SPECIAL-INSTRUC-OUT.            
           DISPLAY 'SPECIAL INSTRUC WRITTEN = ' INSTRUC-CSR-COUNT.              
           DISPLAY '************************************************'.          
           DISPLAY '****           END OF APKUT403              ****'.          
           DISPLAY '************************************************'.          
                                                                                
           CLOSE PO-NBR-FILE                                                    
                 SPECIAL-INSTRUCTIONS-FILE.                                     
       EJECT                                                                    
                                                                                
      ***************************************************************           
      *  READ PO NUMBER FILE                                        *           
      ***************************************************************           
       R100-READ-PO-NUMBER.                                                     
           READ PO-NBR-FILE INTO AP400-PO-EXTRACT-RECORD                        
               AT END                                                           
                   SET PO-NBR-EOF TO TRUE                                       
               NOT AT END                                                       
                   ADD 1 TO PO-RECORDS-IN                                       
           END-READ.                                                            
                                                                                
      ***************************************************************           
      *  FETCH PURCHASE ITEM DATA                                   *           
      ***************************************************************           
       R200-FETCH-INSTRUC-CSR.                                                  
           PERFORM WITH TEST AFTER                                              
                   VARYING PV-RETRY-COUNTER FROM 1 BY 1                         
                   UNTIL   PV-RETRY-COUNTER > 3  OR                             
                           SQLCODE = ZERO OR                                    
                           SQLCODE = +100                                       
                                                                                
             EXEC SQL                                                           
                 FETCH INSTRUC_CSR                                              
                  INTO :INSTCD-INSTR-DESC                                       
                      ,:POINST-INSTR-NBR                                        
                      ,:POINST-RECIP-CDE                                        
             END-EXEC                                                           
                                                                                
             EVALUATE TRUE                                                      
               WHEN SQLCODE = ZERO                                              
                   ADD 1 TO INSTRUC-CSR-COUNT                                   
                   SET NOT-INSTRUC-EOC TO TRUE                                  
               WHEN SQLCODE = +100                                              
                   SET INSTRUC-EOC TO TRUE                                      
               WHEN SQLCODE = -904                                              
               WHEN SQLCODE = -913                                              
                   CONTINUE                                                     
               WHEN OTHER                                                       
                   DISPLAY 'PO-NBR ' AP400-PO-NBR                               
                   MOVE 'R100-FETCH-INSTRUC-CSR' TO AA-PARAGRAPH-NAME           
                   MOVE 'UNSUCCESSFUL FETCH'     TO AA-DB2-OPERATION            
                   MOVE 'TPOINST '               TO AA-DB2-TABLE-1              
                   MOVE 'TINSTCD '               TO AA-DB2-TABLE-2              
                   MOVE SPACES                   TO AA-DB2-TABLE-3              
                   PERFORM Z999-SQL-ERROR                                       
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > 3                                              
               MOVE 'R200-FETCH-INSTRUC-CSR'                                    
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED ON FETCH  INSTRUC_CSR'                
                                       TO AA-DB2-OPERATION                      
               PERFORM Z999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
                                                                                
      ***************************************************************           
      *  SELECT VENDOR NAME                                         *           
      ***************************************************************           
       R300-SELECT-VENDOR-NAME.                                                 
           EXEC SQL                                                             
               SELECT DISTINCT                                                  
                      B.ENT_NAME_DESC                                           
                 INTO :ENTNME-ENT-NAME-DESC                                     
                 FROM TEXTENT A                                                 
                    , TENTNME B                                                 
                WHERE A.DUN_NBR  = :AP400-DUN-NBR                               
                  AND A.ENT_ID   = B.ENT_ID                                     
                  AND B.TYPE_CDE = '01'                                         
           END-EXEC.                                                            
           EVALUATE TRUE                                                        
              WHEN SQLCODE = ZERO                                               
                  CONTINUE                                                      
              WHEN SQLCODE = -904                                               
              WHEN SQLCODE = -911                                               
              WHEN SQLCODE = -913                                               
              WHEN SQLCODE = +100                                               
                  MOVE SPACES TO ENTNME-ENT-NAME-DESC                           
              WHEN OTHER                                                        
                  DISPLAY 'PO-NBR ' AP400-PO-NBR                                
                  DISPLAY 'DUNS # ' AP400-DUN-NBR                               
                  MOVE 'R300-SELECT-VENDOR-NAME' TO AA-PARAGRAPH-NAME           
                  MOVE 'UNSUCCESSFUL SELECT'     TO AA-DB2-OPERATION            
                  MOVE 'TEXTENT'                 TO AA-DB2-TABLE-1              
                  MOVE 'TENTNME'                 TO AA-DB2-TABLE-2              
                  MOVE SPACES                    TO AA-DB2-TABLE-3              
                  PERFORM Z999-SQL-ERROR                                        
           END-EVALUATE.                                                        
                                                                                
       EJECT                                                                    
                                                                                
      ****************************************************************          
      *  WRITE SPECIAL INSTRUCTIONS FILE                             *          
      ****************************************************************          
       W100-WRITE-SPECIAL-INSTRUC.                                              
                                                                                
           WRITE SPECIAL-INSTRUCTIONS-RECORD                                    
                 FROM AP403-SPECIAL-INSTRUCTIONS.                               
                                                                                
           ADD 1 TO SPECIAL-INSTRUC-OUT.                                        
           INITIALIZE AP403-SPECIAL-INSTR.                                      
                                                                                
       EJECT                                                                    
      ****************************************************************          
      *  OPEN SPECIAL INSTRUCTIONS CURSOR                            *          
      ****************************************************************          
       Y100-OPEN-INSTRUC-CSR.                                                   
           PERFORM WITH TEST AFTER                                              
                   VARYING PV-RETRY-COUNTER FROM 1 BY 1                         
                   UNTIL   PV-RETRY-COUNTER > 3  OR                             
                           SQLCODE = ZERO                                       
                                                                                
              EXEC SQL                                                          
                  OPEN INSTRUC_CSR                                              
              END-EXEC                                                          
                                                                                
              EVALUATE TRUE                                                     
                WHEN SQLCODE = ZERO                                             
                WHEN SQLCODE = -904                                             
                WHEN SQLCODE = -913                                             
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     MOVE 'Y100-OPEN-INSTRUC-CSR' TO AA-PARAGRAPH-NAME          
                     MOVE 'UNSUCCESSFUL OPEN'  TO AA-DB2-OPERATION              
                     MOVE 'TPOINST '           TO AA-DB2-TABLE-1                
                     MOVE 'TINSTCD '           TO AA-DB2-TABLE-2                
                     MOVE SPACES               TO AA-DB2-TABLE-3                
                     PERFORM Z999-SQL-ERROR                                     
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > 3                                              
               MOVE 'Y100-OPEN-INSTRUC-CSR'  TO AA-PARAGRAPH-NAME               
               MOVE 'MAX RETRIES EXCEEDED ON OPEN   INSTRUC_CSR'                
                                             TO AA-DB2-OPERATION                
               PERFORM Z999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
      ***************************************************************           
      *  CLOSE SPECIAL INSTRUCTIONS CURSOR                          *           
      ***************************************************************           
       Y200-CLOSE-INSTRUC-CSR.                                                  
           PERFORM WITH TEST AFTER                                              
                   VARYING PV-RETRY-COUNTER FROM 1 BY 1                         
                   UNTIL   PV-RETRY-COUNTER > 3  OR                             
                           SQLCODE = ZERO                                       
                                                                                
              EXEC SQL                                                          
                 CLOSE INSTRUC_CSR                                              
              END-EXEC                                                          
                                                                                
              EVALUATE TRUE                                                     
                  WHEN SQLCODE = ZERO                                           
                  WHEN SQLCODE = -904                                           
                  WHEN SQLCODE = -913                                           
                       CONTINUE                                                 
                  WHEN OTHER                                                    
                       MOVE 'Y200-CLOSE-INSTRUC-CSR'                            
                                       TO AA-PARAGRAPH-NAME                     
                       MOVE 'UNSUCCESSFUL CLOSE'                                
                                       TO AA-DB2-OPERATION                      
                       MOVE 'TPOINST ' TO AA-DB2-TABLE-1                        
                       MOVE 'TINSTCD ' TO AA-DB2-TABLE-2                        
                       MOVE SPACES     TO AA-DB2-TABLE-3                        
                       PERFORM Z999-SQL-ERROR                                   
              END-EVALUATE                                                      
           END-PERFORM.                                                         
                                                                                
           IF PV-RETRY-COUNTER > 3                                              
               MOVE 'Y200-CLOSE-INSTRUC-CSR'                                    
                                       TO AA-PARAGRAPH-NAME                     
               MOVE 'MAX RETRIES EXCEEDED ON CLOSE  INSTRUC_CSR'                
                                       TO AA-DB2-OPERATION                      
               PERFORM Z999-SQL-ERROR                                           
           END-IF.                                                              
                                                                                
       EJECT                                                                    
      *****************************************************************         
      *     PROCESS DB2 ABENDS                                                  
      *****************************************************************         
       Z999-SQL-ERROR.                                                          
           DISPLAY '*** DB2 ERROR '.                                            
           DISPLAY '*** PROGRAM   = ' PV-ABEND-PROGRAM.                         
           DISPLAY '*** PARAGRAPH = ' AA-PARAGRAPH-NAME.                        
           DISPLAY '*** OPERATION = ' AA-DB2-OPERATION.                         
           DISPLAY '*** TABLE 1   = ' AA-DB2-TABLE-1.                           
           IF AA-DB2-TABLE-2 > SPACES                                           
               DISPLAY '*** TABLE 2   = ' AA-DB2-TABLE-2.                       
           IF AA-DB2-TABLE-3 > SPACES                                           
               DISPLAY '*** TABLE 3   = ' AA-DB2-TABLE-3.                       
                                                                                
           MOVE +4013                    TO ABEND-CODE.                         
                                                                                
           COPY DPPD004.                                                        
           CALL 'ILBOABN0' USING ABEND-CODE.                                    
                                                                                
