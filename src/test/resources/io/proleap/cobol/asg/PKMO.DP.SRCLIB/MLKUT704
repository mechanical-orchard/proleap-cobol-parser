000010 IDENTIFICATION DIVISION.                                                 
000030 PROGRAM-ID.    MLKUT704.                                                 
000040 AUTHOR.        TESS BESTE.                                               
000050 INSTALLATION.  KOHLS DEPARTMENT STORES.                                  
000060 DATE-WRITTEN.  01-17-95.                                                 
000070 DATE-COMPILED.                                                           
000080                                                                          
000090******************************************************************        
000100*           MLKUT704 - REVERSE PURCHASES                                  
000110*                                                                         
000120* THIS PROGRAM REVERSES PURCHASE TRANSACTIONS IN THE MLRD015              
000130* FORMAT, WHICH IS INPUT TO MLKUP020                                      
000140* IN JOB MLK140, ON DD'S INP01, INP02, INP03, AND INP04. THE              
000150* TRANSACTION COUNT, TOTAL COST, AND TOTAL RETAIL ARE DISPLAYED.          
000160*                                                                         
000170* THE REVERSAL IS DONE BY CHANGING THE ML015-PURCH-TRANS-CDE              
000180* AS FOLLOWS:                                                             
000190*             01 --> 02                                                   
000200*             02 --> 01                                                   
000210*             07 --> 08                                                   
000220*             08 --> 07                                                   
000233*                                                                         
000234*CHANGE HISTORY:                                                          
000235* WR/PROJ DATE       PGMR        DESCRIPTION OF CHANGES                   
000236* ------- ---------- ----------- --------------------------------         
000237* W26603  03-08-2004 ROD JANSSEN MERCH LEDGER STORE EXPANSION             
000240******************************************************************        
000241                                                                 *        
000260 ENVIRONMENT DIVISION.                                                    
000280 CONFIGURATION SECTION.                                                   
000300 SOURCE-COMPUTER.    IBM-3090.                                            
000310 OBJECT-COMPUTER.    IBM-3090.                                            
000340 INPUT-OUTPUT SECTION.                                                    
000350                                                                          
000360 FILE-CONTROL.                                                            
000370     SELECT INPUT-FILE                                                    
000380         ASSIGN TO INP01.                                                 
000390                                                                          
000400     SELECT OUTPUT-FILE                                                   
000410         ASSIGN TO OUT01.                                                 
000420                                                                          
000450 DATA DIVISION.                                                           
000470 FILE SECTION.                                                            
000480                                                                          
000490 FD  INPUT-FILE                                                           
000500     LABEL RECORDS ARE STANDARD                                           
000510     BLOCK CONTAINS 0 RECORDS                                             
000520     RECORDING  MODE IS F.                                                
000530 01  INPUT-RECORD                PIC X(150).                              
000540                                                                          
000550 FD  OUTPUT-FILE                                                          
000560     LABEL RECORDS ARE STANDARD                                           
000570     BLOCK CONTAINS 0 RECORDS                                             
000580     RECORDING  MODE IS F.                                                
000590 01  OUTPUT-RECORD               PIC X(150).                              
000600                                                                          
000610 WORKING-STORAGE SECTION.                                                 
000620                                                                          
000630******************************************************************        
000640* INPUT/OUTPUT RECORD LAYOUT                                              
000650******************************************************************        
000660                                                                          
000670       COPY MLRD015.                                                      
000680                                                                          
000690 01  PS-PROGRAM-SWITCHES.                                                 
000700     05  PS-END-OF-INPUT-FILE-SW      PIC X         VALUE 'N'.            
000710         88  PS-END-OF-INPUT-FILE                   VALUE 'Y'.            
000720         88  PS-NOT-END-OF-INPUT-FILE               VALUE 'N'.            
000730                                                                          
000740 01  PV-PROGRAM-VARIABLES.                                                
000750     05  PV-RECORD-COUNT              PIC S9(13)    VALUE +0              
000760                                                    COMP-3.               
000770     05  PV-ACCUM-COST                PIC S9(13)V99 VALUE +0              
000780                                                    COMP-3.               
000790     05  PV-ACCUM-RETAIL              PIC S9(13)V99 VALUE +0              
000800                                                    COMP-3.               
000810     05  PV-DISPLAY-COUNT             PIC ZZZ,ZZZ,ZZZ,ZZZ-.               
000820     05  PV-DISPLAY-AMT               PIC ZZZ,ZZZ,ZZZ,ZZZ.99-.            
000830 EJECT                                                                    
000840 PROCEDURE DIVISION.                                                      
000850                                                                          
000860                                                                          
000870 A100-PROGRAM-MAINLINE.                                                   
000880                                                                          
000890     OPEN INPUT INPUT-FILE                                                
000900          OUTPUT OUTPUT-FILE.                                             
000910                                                                          
000920     READ INPUT-FILE INTO ML015-PURCHASE-RECORD                           
000930         AT END SET PS-END-OF-INPUT-FILE TO TRUE.                         
000940                                                                          
000950     PERFORM UNTIL PS-END-OF-INPUT-FILE                                   
000960         ADD +1 TO PV-RECORD-COUNT                                        
000970         IF ML015-PURCH-TRANS-CDE = '01' OR '07'                          
000980             COMPUTE PV-ACCUM-COST                                        
000990                   = PV-ACCUM-COST                                        
001000                   + ML015-PURCH-NET                                      
001010             COMPUTE PV-ACCUM-RETAIL                                      
001020                   = PV-ACCUM-RETAIL                                      
001030                   + ML015-PURCH-RETAIL                                   
001040         ELSE                                                             
001050             COMPUTE PV-ACCUM-COST                                        
001060                   = PV-ACCUM-COST                                        
001070                   - ML015-PURCH-NET                                      
001080             COMPUTE PV-ACCUM-RETAIL                                      
001090                   = PV-ACCUM-RETAIL                                      
001100                   - ML015-PURCH-RETAIL                                   
001110         END-IF                                                           
001120         IF ML015-PURCH-TRANS-CDE = '01'                                  
001130             MOVE '02' TO ML015-PURCH-TRANS-CDE                           
001140         ELSE                                                             
001150             IF ML015-PURCH-TRANS-CDE = '02'                              
001160                 MOVE '01' TO ML015-PURCH-TRANS-CDE                       
001170             ELSE                                                         
001180                 IF ML015-PURCH-TRANS-CDE = '07'                          
001190                     MOVE '08' TO ML015-PURCH-TRANS-CDE                   
001200                 ELSE                                                     
001210                     IF ML015-PURCH-TRANS-CDE = '08'                      
001220                         MOVE '07' TO ML015-PURCH-TRANS-CDE               
001230                     END-IF                                               
001240                 END-IF                                                   
001250             END-IF                                                       
001260         END-IF                                                           
001270         WRITE OUTPUT-RECORD FROM ML015-PURCHASE-RECORD                   
001280         READ INPUT-FILE INTO ML015-PURCHASE-RECORD                       
001290             AT END SET PS-END-OF-INPUT-FILE TO TRUE                      
001300         END-READ                                                         
001310     END-PERFORM.                                                         
001320                                                                          
001330     MOVE PV-RECORD-COUNT TO PV-DISPLAY-COUNT.                            
001340     DISPLAY 'TRANSACTION COUNT: ' PV-DISPLAY-COUNT.                      
001350     DISPLAY ' '.                                                         
001360     MOVE PV-ACCUM-COST   TO PV-DISPLAY-AMT.                              
001370     DISPLAY 'TOTAL COST:        ' PV-DISPLAY-AMT.                        
001380     MOVE PV-ACCUM-RETAIL TO PV-DISPLAY-AMT.                              
001390     DISPLAY 'TOTAL RETAIL:      ' PV-DISPLAY-AMT.                        
001400                                                                          
001410     CLOSE INPUT-FILE                                                     
001420           OUTPUT-FILE.                                                   
001430                                                                          
001440     STOP RUN.                                                            
