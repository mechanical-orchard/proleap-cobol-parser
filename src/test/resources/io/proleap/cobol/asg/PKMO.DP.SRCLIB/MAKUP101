      *****************************************************************         
       IDENTIFICATION DIVISION.                                                 
      *****************************************************************         
       PROGRAM-ID.    MAKUP101.                                                 
       AUTHOR.        KAREN WALL                                                
       INSTALLATION.  KOHLS.                                                    
       DATE-WRITTEN   APRIL 2005.                                               
       DATE-COMPILED.                                                           
      *****************************************************************         
      *  THIS BATCH DB2 PROGRAM READS THE FILE CREATED BY IMKUT024    *         
      *  (PKMO.IM.ITEM.SKU.DOMAIN.EXTRACT.G**V00)                     *         
      *  CONTAINING SKU INFORMATION.  THIS DATA USED TO UPDATE        *         
      *  ASSOCIATED SKU INFORMATION IN THE ORACLE TABLE TMDSEIT FOR   *         
      *  SIZE CODE AND COLOR CODE.                                    *         
      *  THIS IS USED AS A CONVERSION PROGRAM OR AS A FIX ALL PROGRAM.*         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT SKU-UPDATE-IN             ASSIGN TO INP01.                    
           SELECT SKU-UPD-ERR-OUT           ASSIGN TO OUT01.                    
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
       FILE SECTION.                                                            
                                                                                
       FD  SKU-UPDATE-IN                                                        
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  SKU-UPDATE-IN-RECORD         PIC X(500).                             
                                                                                
       FD  SKU-UPD-ERR-OUT                                                      
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE OMITTED.                                           
                                                                                
       01  SKU-UPD-ERROR-RECORD         PIC X(550).                             
                                                                                
      *****************************************************************         
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-EOF-INPUT-SW              PIC X(01)  VALUE 'N'.               
               88  PS-EOF-INPUT-YES                    VALUE 'Y'.               
               88  PS-EOF-INPUT-NO                     VALUE 'N'.               
                                                                                
       01  PC-PROGRAM-CONSTANTS.                                                
           05  PC-PROGRAM-NAME              PIC  X(08) VALUE 'MAKUP101'.        
                                                                                
       01  PV-PROGRAM-VARIABLES.                                                
           05  PV-RECORDS-READ              PIC  9(09)  VALUE  0.               
           05  PV-RECORDS-UPDATED           PIC  9(09)  VALUE  0.               
           05  PV-TOTAL-ERR-REC             PIC  9(09)  VALUE  0.               
           05  PV-DISP-SKU                  PIC  9(08)  VALUE  0.               
           05  PV-SQLCODE                   PIC S9(09)  VALUE +0.               
           05  PV-DISP-SQLCODE              PIC ZZZZZZ9-.                       
           05  PV-CALLING-PARA              PIC X(10)   VALUE SPACES.           
           05  PV-SKU-PROCESS-CODE          PIC X(01)   VALUE SPACES.           
           05  PV-ERRMSGBUF.                                                    
               10 PV-EXTRACT-MSG            PIC  X(500) VALUE SPACES.           
               10 PV-EXTRACT-SPACES         PIC  X(5)   VALUE SPACES.           
               10 PV-EXTRACT-ERR-PARA       PIC  X(20)  VALUE SPACES.           
               10 PV-EXTRACT-SPACES2        PIC  X(5)   VALUE SPACES.           
               10 PV-EXTRACT-ERR            PIC  X(10)  VALUE SPACES.           
                                                                                
                                                                                
       01  PS-PROGRAM-SWITCHES.                                                 
           05  PS-FOUND-SKU-SW                PIC  X(01)  VALUE 'N'.            
               88  PS-FOUND-SKU                           VALUE 'Y'.            
               88  PS-NOT-FOUND-SKU                       VALUE 'N'.            
                                                                                
           EXEC SQL BEGIN DECLARE SECTION END-EXEC.                             
                                                                                
       01  PV-ORACLE-ID                     PIC X(1)    VALUE '/'.              
       01  USERNAME                         PIC X(10)   VARYING.                
       01  PASSWD                           PIC X(10)   VARYING.                
       01  PV-CURRENT-TMST                  PIC X(19)   VALUE SPACES.           
       01  PV-CURRENT-TMST-NEW              PIC X(19)   VALUE SPACES.           
       01  PV-ABEND-FIELDS.                                                     
           05  PV-ABEND-CODE                PIC  S9(4) VALUE +0                 
                                                       COMP SYNC.               
           05  PV-ABEND-PROGRAM             PIC  X(08) VALUE 'MAKUP101'.        
           05  PV-ABEND-PARAGRAPH           PIC  X(50) VALUE SPACES.            
           05  PV-ABEND-OPERATION           PIC  X(50) VALUE SPACES.            
           05  PV-ABEND-TABLE               PIC  X(12) VALUE SPACES.            
           05  PV-DB2-OPERATION-MESSAGE-1   PIC  X(79) VALUE SPACES.            
           05  PV-DB2-OPERATION-MESSAGE-2   PIC  X(79) VALUE SPACES.            
           05  PV-DB2-OPERATION-MESSAGE-3   PIC  X(79) VALUE SPACES.            
                                                                                
                                                                                
      ***********************************************************               
      *  COPYBOOK USED FOR THE SKU ADDS INPUT FILE              *               
      ***********************************************************               
           COPY IMRD053.                                                        
                                                                                
      ***********************************************************               
      *  USED FOR DB2 ERROR MESSAGE BUILDING                    *               
      ***********************************************************               
           COPY DPWS004.                                                        
                                                                                
      ***********************************************************               
      *  ORACLE SQL COMMUNICATION AREA                          *               
      ***********************************************************               
                                                                                
                                                                                
           EXEC SQL                                                             
                INCLUDE TITMCLT                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
                INCLUDE TMDSEIT                                                 
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
                INCLUDE SQLCA                                                   
           END-EXEC.                                                            
                                                                                
           EXEC SQL END DECLARE SECTION END-EXEC.                               
                                                                                
           COPY SQLCA2.                                                         
       EJECT                                                                    
      ***********************************************************               
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
           DISPLAY '**** BEGINNING MAKUP101 ****'.                              
           DISPLAY ' '.                                                         
                                                                                
           OPEN INPUT  SKU-UPDATE-IN.                                           
           OPEN OUTPUT SKU-UPD-ERR-OUT.                                         
                                                                                
           PERFORM 0500-CONNECT-TO-ORACLE.                                      
           PERFORM 1000-READ-INPUT-FILE.                                        
                                                                                
           PERFORM 2000-PROCESS-INPUT-FILE                                      
             UNTIL PS-EOF-INPUT-YES.                                            
                                                                                
           CLOSE SKU-UPDATE-IN.                                                 
           CLOSE SKU-UPD-ERR-OUT.                                               
                                                                                
           PERFORM 3000-TABLE-COMMIT.                                           
                                                                                
           DISPLAY '  INPUT SKU RECS READ    = ' PV-RECORDS-READ.               
           DISPLAY '  TMDSEIT ROWS UPDATED   = ' PV-RECORDS-UPDATED.            
           DISPLAY '  TMDSEIT ROWS ERRORED   = ' PV-TOTAL-ERR-REC.              
           DISPLAY ' '.                                                         
           DISPLAY '****  END OF  MAKUP101  ****'.                              
                                                                                
           GOBACK.                                                              
                                                                                
                                                                                
      ****************************************************************          
      *   CONNECT TO THE APPROPRIATE ORACLE SERVER.                  *          
      ****************************************************************          
       0500-CONNECT-TO-ORACLE.                                                  
           MOVE 'A100-INITIAL' TO PV-ABEND-PARAGRAPH.                           
           MOVE SPACES         TO PV-CALLING-PARA.                              
           MOVE SPACES         TO PV-SKU-PROCESS-CODE.                          
                                                                                
                                                                                
      ****************************************************************          
      * LOGON                                                        *          
      ****************************************************************          
      *                                                                         
      *                                                                         
           DISPLAY '**********************************'.                        
CMNT  *    MOVE 'MATEAM' TO USERNAME-ARR.                                       
OUT   *    MOVE 7  TO USERNAME-LEN.                                             
FOR   *    MOVE 'TEAMMA' TO PASSWD-ARR.                                         
PROD  *    MOVE 7  TO PASSWD-LEN.                                               
                                                                                
SCTEMP*    MOVE 'MATEAM' TO USERNAME-ARR.                                       
SCTEMP*    MOVE 6  TO USERNAME-LEN.                                             
SCTEMP*    MOVE 'TEAMMA' TO PASSWD-ARR.                                         
SCTEMP*    MOVE 6  TO PASSWD-LEN.                                               
SCTEMP*                                                                         
           DISPLAY 'ATTEMPTING LOGON AS ' USERNAME-ARR.                         
                                                                                
CMNT  *    EXEC SQL                                                             
FOR   *     CONNECT :USERNAME IDENTIFIED BY :PASSWD                             
PROD  *    END-EXEC.                                                            
                                                                                
CMNT       EXEC SQL                                                             
FOR         CONNECT :PV-ORACLE-ID                                               
TEST       END-EXEC.                                                            
                                                                                
           IF SQLCODE = 0                                                       
             DISPLAY 'CONNECTED TO ORACLE AS USER: ' PV-ORACLE-ID               
           ELSE                                                                 
               PERFORM Z999-SQL-ABEND                                           
           END-IF.                                                              
                                                                                
           EXEC SQL                                                             
            ALTER SESSION                                                       
                  SET NLS_DATE_FORMAT = 'YYYY-MM-DD.HH24.MI.SS'                 
           END-EXEC.                                                            
                                                                                
                                                                                
           EXEC SQL                                                             
            SELECT SYSDATE                                                      
            INTO :PV-CURRENT-TMST                                               
            FROM DUAL                                                           
           END-EXEC.                                                            
                                                                                
           DISPLAY 'SYSDATE = ' PV-CURRENT-TMST.                                
           MOVE PV-CURRENT-TMST TO PV-CURRENT-TMST-NEW.                         
                                                                                
                                                                                
      *****************************************************************         
      *  READ A SKU ADD EXTRACT RECORD THAT WILL BE USED TO UPDATE    *         
      *  A ROW OF DATA INTO THE ORACLE TMDSEIT TABLE.                 *         
      *****************************************************************         
       1000-READ-INPUT-FILE.                                                    
                                                                                
           MOVE '1000-READ-INPUT-FILE    ' TO PV-ABEND-PARAGRAPH.               
                                                                                
           INITIALIZE IM053-ITEM-SKU-DOMAIN-REC.                                
                                                                                
           READ SKU-UPDATE-IN                                                   
                INTO IM053-ITEM-SKU-DOMAIN-REC                                  
                  AT END                                                        
                     SET PS-EOF-INPUT-YES TO TRUE                               
                                                                                
                  NOT AT END                                                    
                     SET PS-EOF-INPUT-NO  TO TRUE                               
                     ADD +1 TO PV-RECORDS-READ                                  
           END-READ.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  LOAD THE INFORMATION FROM THE SKU ADD EXTRACT FILE INTO THE  *         
      *  HOST VARIABLES AND PERFORM A PARAGRAPH TO UPDATE THE NEW ROW *         
      *  INTO THE ORACLE TMDSEIT TABLE.                               *         
      *****************************************************************         
       2000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE '2000-PROCESS-INPUT-FILE ' TO PV-ABEND-PARAGRAPH.               
                                                                                
           SET  PS-NOT-FOUND-SKU           TO TRUE.                             
                                                                                
           PERFORM 2100-SELECT-TITMCLT.                                         
                                                                                
           IF PS-FOUND-SKU                                                      
              PERFORM 2200-UPDATE-TMDSEIT.                                      
                                                                                
           PERFORM 1000-READ-INPUT-FILE.                                        
                                                                                
                                                                                
      *****************************************************************         
      *  FIRST SELECT ORACLE TABLE TITMCLT TO GET THE APPROPRIATE     *         
      *  ITEM-CLUSTR-ID BASED OFF OF SKU NUMBER RETRIEVED FROM THE    *         
      *  VSM COMMON EXTRACT TO GET THE ITEM ATTRIBUTE ROW ON ORACLE   *         
      *  TABLE TMDSEIT TO UPDATE.                                               
      *****************************************************************         
       2100-SELECT-TITMCLT.                                                     
                                                                                
               INITIALIZE ITMCLT-ITEM-CLUSTR-ID.                                
                                                                                
               EXEC SQL                                                         
                   SELECT ITEM_CLUSTR_ID                                        
                   INTO  :ITMCLT-ITEM-CLUSTR-ID                                 
                   FROM   TITMCLT                                               
                   WHERE                                                        
                       SKU               = :IM053-SKU                           
                   AND ITEM_CLUSTR_CDE   = 'I'                                  
                   AND (EFF_END_TMST IS NULL                                    
                    OR  EFF_END_TMST > :PV-CURRENT-TMST)                        
               END-EXEC.                                                        
                                                                                
               EVALUATE TRUE                                                    
                 WHEN SQLCODE = ZEROS                                           
                      SET PS-FOUND-SKU     TO TRUE                              
                 WHEN OTHER                                                     
                   MOVE '2100-SELECT-TITMCLT ' TO PV-ABEND-PARAGRAPH            
                   PERFORM Z999-SQL-ABEND                                       
               END-EVALUATE.                                                    
                                                                                
                                                                                
                                                                                
      *****************************************************************         
      *  UPDATE THE SKU INTO THE ORACLE TMDSEIT TABLE.                *         
      *****************************************************************         
       2200-UPDATE-TMDSEIT.                                                     
                                                                                
           EXEC SQL                                                             
                UPDATE   TMDSEIT                                                
                SET  STAT_CHNG_DTE    = :PV-CURRENT-TMST                        
                    ,KOHLS_SIZE_CDE   = :IM053-KOHLS-SIZE-CDE                   
                    ,NRF_CLOR_CDE     = :IM053-NRF-CLOR-CDE                     
                WHERE                                                           
                     ITEM_CLUSTR_ID  = :ITMCLT-ITEM-CLUSTR-ID                   
           END-EXEC.                                                            
                                                                                
           EVALUATE TRUE                                                        
               WHEN SQLCODE = ZERO                                              
                    ADD +1                 TO PV-RECORDS-UPDATED                
               WHEN OTHER                                                       
                    MOVE '2100-UPDATE-TMDSEIT'                                  
                                           TO PV-ABEND-PARAGRAPH                
                    MOVE 'UPDATE TMDSEIT'  TO PV-ABEND-OPERATION                
                    MOVE IM053-SKU         TO PV-DISP-SKU                       
                    STRING '  SKU NBR: '            DELIMITED BY SIZE           
                           PV-DISP-SKU              DELIMITED BY SIZE           
                                         INTO PV-DB2-OPERATION-MESSAGE-1        
                    MOVE 'TMDSEIT'         TO PV-ABEND-TABLE                    
                    PERFORM Z999-SQL-ABEND                                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
      *****************************************************************         
      * COMMIT THE PENDING TABLE CHANGES.                             *         
      *****************************************************************         
       3000-TABLE-COMMIT.                                                       
                                                                                
           EXEC SQL                                                             
                COMMIT                                                          
           END-EXEC.                                                            
                                                                                
                                                                                
      *****************************************************************         
      *  DISPLAY DB2 ERROR INFORMATION, DO NOT END PROGRAM OR ABEND,  *         
      *  JUST WRITE OUT ERROR INTO A FLAT FILE AND PROCEED WITH       *         
      *  THE PROCESS OF READING THE NEXT ROW.                         *         
      *****************************************************************         
       Z999-SQL-ABEND.                                                          
                                                                                
           ADD +1                  TO PV-TOTAL-ERR-REC.                         
                                                                                
           MOVE SQLCODE            TO PV-SQLCODE.                               
           MOVE SKU-UPDATE-IN-RECORD                                            
                                   TO PV-EXTRACT-MSG.                           
           MOVE PV-SQLCODE         TO PV-EXTRACT-ERR.                           
           MOVE PV-ABEND-PARAGRAPH TO PV-EXTRACT-ERR-PARA.                      
                                                                                
           WRITE SKU-UPD-ERROR-RECORD FROM  PV-ERRMSGBUF.                       
                                                                                
