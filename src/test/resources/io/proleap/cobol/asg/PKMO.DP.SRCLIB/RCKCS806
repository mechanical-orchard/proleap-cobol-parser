000100******************************************************************        
000200 IDENTIFICATION DIVISION.                                                 
000300******************************************************************        
000400                                                                          
000500 PROGRAM-ID.                RCKCS806.                                     
000600 AUTHOR.                    PAUL JORGENSEN.                               
000700 INSTALLATION.              KOHLS DEPARTMENT STORES.                      
000800 DATE-WRITTEN.              06-99.                                        
000900 DATE-COMPILED.                                                           
001000******************************************************************        
001100*  CICS PROGRAM                                                  *        
001200*                                                                *        
001300*      THIS PROGRAM IS THE PREPACK PACK ID EDIT/VALIDATION       *        
001400*      ROUTINE.  IT'S BASIC FUNCTIONS ARE:                       *        
001500*      1. VALIDATE LINE RATIO-TO-ORDER QUANTITY.                 *        
001600*      2. DETERMINE THE 'DRIVER' LINE.                           *        
001700*      3. CALCULATE THE 'BEST CASE RECEIPT' OF EACH LINE.        *        
001800******************************************************************        
001900 ENVIRONMENT DIVISION.                                                    
002000******************************************************************        
002100                                                                          
002200******************************************************************        
002300 DATA DIVISION.                                                           
002400******************************************************************        
002500 WORKING-STORAGE SECTION.                                                 
002600 01  FILLER                           PIC X(28) VALUE                     
002700                                        'RCKCS806 START OF W/S'.          
002800 01  PV-PROGRAM-VARIABLES.                                                
002900     05  PV-TOT-RATIO                 PIC S9(7)  VALUE +0                 
003000                                                 COMP-3.                  
003100     05  PV-TOT-PACK-QTY              PIC S9(7)  VALUE +0                 
003200                                                 COMP-3.                  
003300     05  PV-WRK-RATIO                 PIC S9(5)  VALUE +0                 
003400                                                 COMP-3.                  
003500     05  PV-WRK-QTY                   PIC S9(7)  VALUE +0                 
003600                                                 COMP-3.                  
003700     05  PV-WRK-REMAINDER             PIC S9(5)  VALUE +0                 
003800                                                 COMP-3.                  
003900     05  PV-HOLD-WRK-QTY              PIC S9(7)  VALUE +0                 
004000                                                 COMP-3.                  
004100     05  PV-WRK-PACKS                 PIC S9(7)V9(4) VALUE +0             
004200                                                 COMP-3.                  
004100     05  PV-HOLD-WRK-PACKS            PIC S9(7)V9(4)                      
004200                             VALUE +9999999.9999 COMP-3.                  
004500     05  PV-VARIANCE                  PIC S9(7)V9(4)                      
004600                                                 COMP-3.                  
004700     05  PV-HOLD-LINE-NBR             PIC S9(5)  VALUE +1                 
004800                                                 COMP-3.                  
005100 01  PS-SUBSRIPTS.                                                        
005200     05  PS-LINE-NBR-SUB              PIC S9(4)  VALUE +1                 
005300                                                 COMP.                    
005400                                                                          
005500 01  PS-SWITCHES.                                                         
005600     05  PS-VARY-OVER-SW              PIC X      VALUE SPACE.             
005700         88  PS-VARIANCE-OVER                    VALUE 'Y'.               
005800     05  PS-VARY-UNDER-SW             PIC X      VALUE SPACE.             
005900         88  PS-VARIANCE-UNDER                   VALUE 'Y'.               
006000                                                                          
006100 01  PC-CONSTANTS.                                                        
006200     05  PC-1                         PIC  X(1)  VALUE '1'.               
006300     05  PC-Y                         PIC  X(1)  VALUE 'Y'.               
006400     05  PC-N                         PIC  X(1)  VALUE 'N'.               
006500     05  PC-MAX-LINES                 PIC S9(3)  VALUE +100.              
006600*                                                                         
006700*  LAYOUT FOR THE ABEND MODULE COMMUNICATIONS AREA.                       
006800*                                                                         
006900     COPY DPWS013.                                                        
007000                                                                          
007100     COPY RCWS806.                                                        
007200                                                                          
007300******************************************************************        
007400 LINKAGE SECTION.                                                         
007500******************************************************************        
007600 01  DFHCOMMAREA.                                                         
007700     05  FILLER                       OCCURS 1 TO 4000 TIMES              
007800                                      DEPENDING ON EIBCALEN.              
007900         10  FILLER                   PIC X.                              
008000                                                                          
008100******************************************************************        
008200 PROCEDURE DIVISION.                                                      
008300 0000-MAINLINE.                                                           
008400                                                                          
008500     MOVE DFHCOMMAREA           TO RC806-PRODUCT-PACK-VALIDATION.         
008600                                                                          
009100* * *  INITIALIZE ALL FIELDS THAT RETURN TO THE HOST PROGRAM              
009200* * *  AND CHECK INCOMING RATIOS AND LINE ORDER QUANTITIES                
009300* * *  FOR VALID DATA.                                                    
009400     INITIALIZE RC806-PACK-STATUS                                         
009500                RC806-BEST-CASE-PACKS                                     
009600                RC806-INNER-PACK-RATIO-TOT                                
009700                RC806-OUTER-PACK-RATIO-TOT                                
009800                RC806-UNITS-PER-CARTON                                    
009900                RC806-NBR-OF-PACKS                                        
010000                RC806-TOTAL-NBR-OF-CARTONS                                
010000                RC806-DRIVER-LNE-SUB                                      
010100                RC806-ERROR-FLAGS                                         
                                                                                
008700     IF RC806-NBR-OF-LINES NUMERIC                                        
008700         IF RC806-NBR-OF-LINES > ZERO                                     
                   CONTINUE                                                     
               ELSE                                                             
008800             SET RC806-INV-NBR-OF-LINES                                   
008900                                TO TRUE                                   
               END-IF                                                           
           ELSE                                                                 
008800         SET RC806-INV-NBR-OF-LINES                                       
008900                                TO TRUE                                   
               MOVE ZERO              TO RC806-NBR-OF-LINES                     
           END-IF.                                                              
010200     PERFORM UNTIL PS-LINE-NBR-SUB > PC-MAX-LINES                         
010300         INITIALIZE RC806-BEST-CASE-UNITS (PS-LINE-NBR-SUB)               
010400                    RC806-ODD-UNITS (PS-LINE-NBR-SUB)                     
010500                    RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)                 
010600                    RC806-LINE-STAT (PS-LINE-NBR-SUB)                     
010700                    RC806-VARIANCE-TYPE (PS-LINE-NBR-SUB)                 
010800         IF PS-LINE-NBR-SUB > RC806-NBR-OF-LINES                          
010900             CONTINUE                                                     
011000         ELSE                                                             
011100             IF RC806-PO-RATIO (PS-LINE-NBR-SUB) NOT NUMERIC              
011200               OR RC806-RC-RATIO (PS-LINE-NBR-SUB) NOT NUMERIC            
011300               OR RC806-UNIT-QTY (PS-LINE-NBR-SUB) NOT NUMERIC            
011400               OR RC806-RCVG-QTY (PS-LINE-NBR-SUB) NOT NUMERIC            
011500                 SET RC806-LINE-NOT-NUMERIC (PS-LINE-NBR-SUB)             
011600                                         TO TRUE                          
011700                 SET RC806-INV-LINE-DATA TO TRUE                          
011800             END-IF                                                       
011900         END-IF                                                           
012000         ADD +1                         TO PS-LINE-NBR-SUB                
012100     END-PERFORM.                                                         
012200                                                                          
012300     IF RC806-INV-LINE-DATA                                               
012400       OR RC806-INV-NBR-OF-LINES                                          
012500         PERFORM 8000-END                                                 
012600     END-IF.                                                              
012700     IF NOT RC806-VALID-FUNCTION                                          
012800         SET RC806-INV-FUNCTION-CD TO TRUE                                
012900         PERFORM 8000-END                                                 
013000     END-IF.                                                              
013100                                                                          
013200     MOVE +1                       TO PS-LINE-NBR-SUB                     
013300     PERFORM 1000-PROCESS-LINES                                           
013400         UNTIL PS-LINE-NBR-SUB > RC806-NBR-OF-LINES                       
013500            OR PS-LINE-NBR-SUB > PC-MAX-LINES                             
           PERFORM 1100-FINISH-PROCESS                                          
                                                                                
017300     PERFORM 8000-END.                                                    
017400                                                                          
017500 1000-PROCESS-LINES.                                                      
017600     EVALUATE TRUE                                                        
017700         WHEN RC806-VALIDATE-RATIO                                        
017800             PERFORM 2000-VALIDATE-RATIO                                  
017900         WHEN RC806-CALC-BEST-CASE                                        
018000             PERFORM 3000-VARY-DRIVER-CALCS                               
018100         WHEN RC806-VALIDATE-AND-CALC                                     
018200             PERFORM 2000-VALIDATE-RATIO                                  
018300             PERFORM 3000-VARY-DRIVER-CALCS                               
018400     END-EVALUATE.                                                        
018500* * *  IF JUST VALIDATING RATIO, ACCUMULATE THE P.O. QUANTITIES           
018600* * *  OTHERWISE ACCUMULATE THE RECEIVING QUANTITIES.                     
018700     IF RC806-VALIDATE-RATIO                                              
018800         IF RC806-PO-RATIO (PS-LINE-NBR-SUB) NUMERIC                      
018900             ADD RC806-PO-RATIO (PS-LINE-NBR-SUB)                         
019000                               TO PV-TOT-RATIO                            
019100         END-IF                                                           
019200         IF RC806-UNIT-QTY (PS-LINE-NBR-SUB) NUMERIC                      
019300             ADD RC806-UNIT-QTY (PS-LINE-NBR-SUB)                         
019400                               TO PV-TOT-PACK-QTY                         
019500         END-IF                                                           
019600     ELSE                                                                 
019700         IF RC806-RC-RATIO (PS-LINE-NBR-SUB) NUMERIC                      
019800             ADD RC806-RC-RATIO (PS-LINE-NBR-SUB)                         
019900                               TO PV-TOT-RATIO                            
020000         END-IF                                                           
020100         IF RC806-RCVG-QTY (PS-LINE-NBR-SUB) NUMERIC                      
020200             ADD RC806-RCVG-QTY (PS-LINE-NBR-SUB)                         
020300                               TO PV-TOT-PACK-QTY                         
020400         END-IF                                                           
020500     END-IF.                                                              
020600                                                                          
020700     ADD +1 TO PS-LINE-NBR-SUB.                                           
                                                                                
       1100-FINISH-PROCESS.                                                     
013600     PERFORM 5000-BEST-CASE                                               
013700     MOVE +1                       TO PS-LINE-NBR-SUB                     
013800     PERFORM 4000-PROCESS-LINES-AGAIN                                     
013900         UNTIL PS-LINE-NBR-SUB > RC806-NBR-OF-LINES                       
014000            OR PS-LINE-NBR-SUB > PC-MAX-LINES                             
014100                                                                          
014200* * *  FOR 'INNER PREPACK' ORDERS:                                        
014300* * *  IF COMING FROM P.O. SYSTEM, INNER PACK QUANTITY IS                 
014400* * *  TOTAL UNITS PER CARTON, SO NUMBER OF PACKS PER CARTON              
014500* * *  WILL BE CALCULATED AND BOTH NUMBERS WILL BE SENT BACK.             
014600* * *  IF COMING FROM RECEIVING SYSTEM, INNER PACK QUANTITY IS            
014700* * *  NUMBER OF PACKS PER CARTON, SO TOTAL UNITS PER CARTON              
014800* * *  WILL BE CALCULATED AND BOTH NUMBERS WILL BE SENT BACK.             
015100     MOVE PV-TOT-RATIO             TO RC806-INNER-PACK-RATIO-TOT          
014900     IF RC806-INNER-PACK-QTY > ZERO                                       
015200         IF RC806-VALIDATE-RATIO                                          
015300             DIVIDE RC806-INNER-PACK-QTY BY PV-TOT-RATIO                  
015400                               GIVING RC806-NBR-OF-PACKS                  
015500             MOVE RC806-INNER-PACK-QTY                                    
015600                                   TO RC806-UNITS-PER-CARTON              
015700         ELSE                                                             
015800             MULTIPLY RC806-INNER-PACK-QTY                                
015900               BY PV-TOT-RATIO GIVING RC806-UNITS-PER-CARTON              
016000             MOVE RC806-INNER-PACK-QTY                                    
016100                                   TO RC806-NBR-OF-PACKS                  
016200         END-IF                                                           
016300     ELSE                                                                 
016400         MOVE PV-TOT-RATIO         TO RC806-UNITS-PER-CARTON              
016500         MOVE +1                   TO RC806-NBR-OF-PACKS                  
016700     END-IF.                                                              
           IF RC806-UNITS-PER-CARTON NOT = ZERO                                 
016800         DIVIDE PV-TOT-PACK-QTY BY RC806-UNITS-PER-CARTON                 
016900                           GIVING  RC806-TOTAL-NBR-OF-CARTONS.            
017000     MOVE PV-TOT-RATIO              TO RC806-OUTER-PACK-RATIO-TOT         
017100     MOVE PV-TOT-PACK-QTY           TO RC806-OUTER-PACK-QTY-TOT.          
017200                                                                          
020900 2000-VALIDATE-RATIO.                                                     
021000* * *  IF JUST VALIDATING RATIO, USE THE P.O. QUANTITIES                  
021100* * *  OTHERWISE USE THE RECEIVING QUANTITIES.                            
021200     IF RC806-VALIDATE-RATIO                                              
021300         MOVE RC806-UNIT-QTY (PS-LINE-NBR-SUB) TO PV-WRK-QTY              
021400         MOVE RC806-PO-RATIO (PS-LINE-NBR-SUB) TO PV-WRK-RATIO            
021500     ELSE                                                                 
021600         MOVE RC806-RCVG-QTY (PS-LINE-NBR-SUB) TO PV-WRK-QTY              
021700         MOVE RC806-RC-RATIO (PS-LINE-NBR-SUB) TO PV-WRK-RATIO            
021800         IF RC806-RC-RATIO (PS-LINE-NBR-SUB) NOT =                        
021900            RC806-PO-RATIO (PS-LINE-NBR-SUB)                              
022000             SET RC806-BAD-PACK                TO TRUE                    
022100         END-IF                                                           
022200     END-IF.                                                              
022300                                                                          
022400     INITIALIZE PV-WRK-REMAINDER                                          
022500                                                                          
022600     IF PV-WRK-RATIO NOT = ZERO                                           
022700         DIVIDE PV-WRK-QTY BY PV-WRK-RATIO GIVING PV-WRK-QTY              
022800                           REMAINDER PV-WRK-REMAINDER                     
022900     ELSE                                                                 
023000         IF PV-WRK-QTY > ZERO                                             
023100             SET RC806-BAD-RATIO (PS-LINE-NBR-SUB)                        
023200                                               TO TRUE                    
023300         END-IF                                                           
023400     END-IF.                                                              
023500     IF PV-WRK-REMAINDER NOT = ZERO                                       
023600         SET RC806-BAD-RATIO (PS-LINE-NBR-SUB) TO TRUE                    
023700     ELSE                                                                 
023800         IF PV-HOLD-WRK-QTY = ZERO                                        
023900             MOVE PV-WRK-QTY                   TO PV-HOLD-WRK-QTY         
024000         END-IF                                                           
024100     END-IF.                                                              
024200     IF (PS-LINE-NBR-SUB > +1                                             
024300       AND PV-HOLD-WRK-QTY NOT = ZERO)                                    
024400         IF PV-WRK-QTY NOT = PV-HOLD-WRK-QTY                              
024500             SET RC806-BAD-PACK                TO TRUE                    
024600             SET RC806-BAD-RATIO (PS-LINE-NBR-SUB)                        
024700                                               TO TRUE                    
               END-IF                                                           
           END-IF.                                                              
024800                                                                          
024900 3000-VARY-DRIVER-CALCS.                                                  
           IF RC806-UNIT-QTY (PS-LINE-NBR-SUB) > ZERO                           
               IF RC806-RC-RATIO (PS-LINE-NBR-SUB) > ZERO                       
                   SUBTRACT RC806-UNIT-QTY (PS-LINE-NBR-SUB)                    
                       FROM RC806-RCVG-QTY (PS-LINE-NBR-SUB)                    
                       GIVING PV-WRK-QTY                                        
                   DIVIDE PV-WRK-QTY                                            
                                    BY RC806-UNIT-QTY (PS-LINE-NBR-SUB)         
                       GIVING PV-VARIANCE ROUNDED                               
                   MULTIPLY PV-VARIANCE BY +100                                 
                       GIVING PV-VARIANCE                                       
                   MOVE PV-VARIANCE TO PV-WRK-QTY                               
                   IF PV-VARIANCE NOT = PV-WRK-QTY                              
                       IF PV-VARIANCE > ZERO                                    
                           ADD +1 PV-WRK-QTY                                    
                           GIVING RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)         
                       ELSE                                                     
                           SUBTRACT +1 FROM PV-WRK-QTY                          
                           GIVING RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)         
                       END-IF                                                   
                   ELSE                                                         
                       MOVE PV-VARIANCE                                         
                               TO RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)         
                   END-IF                                                       
               ELSE                                                             
                   MOVE ZERO   TO RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)         
                                  PV-VARIANCE                                   
               END-IF                                                           
           ELSE                                                                 
               MOVE +9999.9    TO RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB)         
                                  PV-VARIANCE                                   
           END-IF.                                                              
           EVALUATE TRUE                                                        
               WHEN RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB) < ZERO                
027000             SET PS-VARIANCE-UNDER                      TO TRUE           
027300             SET RC806-VARIANCE-UNDER (PS-LINE-NBR-SUB) TO TRUE           
               WHEN RC806-PCNT-VARIANCE (PS-LINE-NBR-SUB) > ZERO                
027000             SET PS-VARIANCE-OVER                       TO TRUE           
026900             SET RC806-VARIANCE-OVER (PS-LINE-NBR-SUB)  TO TRUE           
           END-EVALUATE                                                         
026100                                                                          
030200     IF RC806-RC-RATIO (PS-LINE-NBR-SUB) NOT = ZERO                       
030200         DIVIDE RC806-RCVG-QTY (PS-LINE-NBR-SUB)                          
030200             BY RC806-RC-RATIO (PS-LINE-NBR-SUB)                          
                   GIVING PV-WRK-PACKS ROUNDED                                  
               IF PV-WRK-PACKS < PV-HOLD-WRK-PACKS                              
                   MOVE PV-WRK-PACKS               TO PV-HOLD-WRK-PACKS         
028800             MOVE PS-LINE-NBR-SUB            TO PV-HOLD-LINE-NBR          
028900         END-IF                                                           
           END-IF.                                                              
029600                                                                          
029700 4000-PROCESS-LINES-AGAIN.                                                
029800* *  GO THRU THE TABLE AGAIN:                                             
029900* *  1. CALCULATE BEST UNITS AND ODD-UNITS.                               
030200     IF RC806-RC-RATIO (PS-LINE-NBR-SUB) NOT = ZERO                       
030300         MULTIPLY         RC806-BEST-CASE-PACKS                           
030400                       BY RC806-RC-RATIO (PS-LINE-NBR-SUB)                
030500                   GIVING RC806-BEST-CASE-UNITS (PS-LINE-NBR-SUB)         
030500         IF RC806-BEST-CASE-UNITS (PS-LINE-NBR-SUB) >                     
030700                          RC806-RCVG-QTY (PS-LINE-NBR-SUB)                
                   MOVE ZERO TO RC806-BEST-CASE-UNITS (PS-LINE-NBR-SUB)         
030700             MOVE         RC806-RCVG-QTY (PS-LINE-NBR-SUB)                
030800                       TO RC806-ODD-UNITS (PS-LINE-NBR-SUB)               
               ELSE                                                             
030600             SUBTRACT RC806-BEST-CASE-UNITS (PS-LINE-NBR-SUB)             
030700                 FROM RC806-RCVG-QTY (PS-LINE-NBR-SUB)                    
030800               GIVING RC806-ODD-UNITS (PS-LINE-NBR-SUB)                   
               END-IF                                                           
           ELSE                                                                 
               IF RC806-RCVG-QTY (PS-LINE-NBR-SUB) > +1                         
                   MOVE RC806-RCVG-QTY (PS-LINE-NBR-SUB)                        
                             TO RC806-ODD-UNITS (PS-LINE-NBR-SUB)               
               END-IF                                                           
030900     END-IF.                                                              
                                                                                
031400     ADD +1 TO PS-LINE-NBR-SUB.                                           
031500                                                                          
031600 5000-BEST-CASE.                                                          
031700     IF PS-VARIANCE-OVER                                                  
031800       AND PS-VARIANCE-UNDER                                              
031900         SET RC806-BAD-PACK       TO TRUE                                 
032500     END-IF                                                               
032600     IF (NOT PS-VARIANCE-OVER)                                            
032700       AND PS-VARIANCE-UNDER                                              
032800         SET RC806-PACK-UNDER     TO TRUE                                 
033400     END-IF                                                               
033500     IF PS-VARIANCE-OVER                                                  
033600       AND (NOT PS-VARIANCE-UNDER)                                        
033700         SET RC806-PACK-OVER      TO TRUE                                 
034300     END-IF.                                                              
033800     MOVE PV-HOLD-LINE-NBR        TO PS-LINE-NBR-SUB                      
032100                                     RC806-DRIVER-LNE-SUB                 
034000     IF RC806-RCVG-QTY (PS-LINE-NBR-SUB)                                  
034100                               < RC806-RC-RATIO (PS-LINE-NBR-SUB)         
             OR RC806-RC-RATIO (PS-LINE-NBR-SUB) = ZERO                         
11/23 *        MOVE RC806-RCVG-QTY (PS-LINE-NBR-SUB)                            
11/23 *                                 TO RC806-BEST-CASE-PACKS                
11/23          MOVE ZERO                TO RC806-BEST-CASE-PACKS                
           ELSE                                                                 
034000         DIVIDE RC806-RCVG-QTY (PS-LINE-NBR-SUB)                          
034100             BY RC806-RC-RATIO (PS-LINE-NBR-SUB)                          
034200         GIVING RC806-BEST-CASE-PACKS                                     
035200     END-IF.                                                              
035300                                                                          
035400 8000-END.                                                                
035500     MOVE RC806-PRODUCT-PACK-VALIDATION    TO DFHCOMMAREA.                
035600     EXEC CICS RETURN                                                     
035700     END-EXEC.                                                            
035800     GOBACK.                                                              
035900                                                                          
036000                                                                          
036100 9999-ABEND.                                                              
036200                                                                          
036300     MOVE 'RCKCS806'             TO DP013-PROGRAM.                        
036400                                                                          
036500     SET DP013-LINK-RETURN       TO TRUE.                                 
036600     SET DP013-NO-ROLLBACK       TO TRUE.                                 
036700                                                                          
036800     INITIALIZE                     DP013-SRC-TASK-NUMBER                 
036900                                    DP013-STACK.                          
037000                                                                          
037100     MOVE DFHEIBLK               TO DP013-DFHEIBLK.                       
037200                                                                          
037300     EXEC CICS                                                            
037400         LINK PROGRAM  (DP013-NAME)                                       
037500              COMMAREA (DP013-PARAMETERS)                                 
037600              LENGTH   (DP013-LENGTH)                                     
037700     END-EXEC.                                                            
037800                                                                          
037900     EXEC CICS                                                            
038000         RETURN                                                           
038100     END-EXEC.                                                            
038200                                                                          
