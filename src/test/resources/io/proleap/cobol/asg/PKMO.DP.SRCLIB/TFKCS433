      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    TFKCS433.                                                 
       AUTHOR.        JACK KRAMER.                                      00050000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00060000
       DATE-WRITTEN.  SEPTEMBER 1996.                                   00070000
       DATE-COMPILED.                                                   00080000
      *----------------------------------------------------------------*00090000
      *    P433 - TRANSFER INQUIRY CRITERIA DISPLAY.                   *00100000
      *                                                                *00110000
      *    THIS PROGRAM ALLOWS THE USER TO DISPLAY THE CRITERIA        *00120000
      *    WHICH WAS SPECIFIED ON THE TRANSFER CRITERIA SPECIFICATION  *00130000
      *    SCREEN.                                                     *00140000
      *----------------------------------------------------------------*00170000
      *  1/20/2003 - RAY GRAF                                                   
      *   PROCESS NONINTELLIGENT SKU.                                           
      ******************************************************************        
      **DATE:08/02/2004 CHANGED BY: RAY GRAF                                    
      **     RECOMPILE DUE TO PRINTER EXPANSION IN TFWS001                      
      *****************************************************************         
100404**DATE:10/04/2004 CHANGED BY: RAY GRAF                                    
100404**     EXPAND STORE NUMBER. MAKE DOMAIN COMPLIANT.                        
      *****************************************************************         
CICS  * CHANGED 11/17/2022    BY JIM HUNTER      WR: CHG0278345                 
CICS  * ADD COPYBOOK DPWS930 AND MAKE THE CICS ARCHITECTURE CALL                
CICS  * DYNAMIC INSTEAD OF STATIC BY CALLING DP930-CICS-ARCH-API.               
CICS  *-----------------------------------------------------------------        
                                                                        00180000
       ENVIRONMENT DIVISION.                                            00210000
                                                                        00240000
       DATA DIVISION.                                                   00250000
                                                                        00270000
       WORKING-STORAGE SECTION.                                         00280000
      *                                                                 00330000
       01  PC-PROGRAM-CONSTANTS.                                        00340000
           05  PC-CURRENT-MAP-NAME      PIC  X(08)  VALUE  'TF433A  '.  00350000
           05  PC-CURRENT-MAPSET-NAME   PIC  X(08)  VALUE  'TFKM433 '.  00360000
           05  PC-CURRENT-PROGRAM-NAME  PIC  X(08)  VALUE  'TFKCS433'.  00370000
           05  PC-MAX-RETRIES           PIC S9(04)  VALUE  +3           00380000
                                                    COMP.               00390000
      *                                                                 00530000
       01  PS-PROGRAM-SWITCHES.                                         00540000
           05  PS-ERROR-SW              PIC  X(01)  VALUE  'N'.         00550000
               88  PS-ERROR                         VALUE  'Y'.         00560000
               88  PS-NO-ERROR                      VALUE  'N'.         00570000
      *                                                                 00610000
       01  PV-PROGRAM-VARIABLES.                                        00620000
           05  PV-DATE                  PIC 99/99/9999..                00650000
           05  PV-RETRY-COUNTER         PIC S9(04)  VALUE +0            00650000
                                                    COMP SYNC.          00660000
       EJECT                                                            00720000
      *----------------------------------------------------------------*00790000
      *    MAP LAYOUT                                                  *00800000
      *----------------------------------------------------------------*00810000
                                                                        00820000
           COPY TFKM433.                                                00830004
                                                                        00850000
           EJECT                                                        00860000
      *----------------------------------------------------------------*00870000
      *    ATTRIBUTE SETTINGS COPYBOOK.                                *00880000
      *----------------------------------------------------------------*00890000
                                                                        00900000
           COPY DPWS015.                                                00910000
                                                                        00920000
      *----------------------------------------------------------------*00930000
      *    FUNCTION KEYS COPYBOOK                                      *00940000
      *----------------------------------------------------------------*00950000
                                                                        00960000
           COPY DPWS016.                                                00970000
       EJECT                                                            00980000
      ***************************************************************** 00990000
      * PARAMETERS FOR CALLING THE CICS ARCHITECTURE API.               01000000
      ***************************************************************** 01010000
           COPY DPWS030.                                                01020000
CICS       COPY DPWS930.                                                01020000
       EJECT                                                            01030000
      ***************************************************************** 01040000
      *    STANDARD COMMAREA.                                           01050000
      ***************************************************************** 01060000
                                                                        01070000
           COPY DPWS020.                                                01080000
           05  FILLER REDEFINES DP020-VARIABLE-COMMAREA.                01090000
                                                                        01100000
      ***************************************************************** 01110000
      *    INTER-APPLICATION COMMAREA.                                  01120000
      ***************************************************************** 01130000
                                                                        01140000
               10  INTER-APPL-COMM-AREA PIC X(200).                     01150000
           COPY TFWS001.                                                01160000
      ***************************************************************** 01170000
      *    SPECIFIC COMMMAREA FOR TFKCS433.                             01180000
      ***************************************************************** 01190000
               10  ASC-SPECIFIC-COMMAREA.                               01200000
                   15  ASC-INT-APPL-FORMAT-IND                          01201600
                                                 PIC X.                 01201700
                   15  ASC-XFER-NBR.                                    01201802
100404*                20  ASC-XFER-FROM-STORE   PIC 9(03).             01201902
100404                 20  ASC-XFER-FROM-STORE   PIC 9(04).             01201902
                       20  ASC-XFER-SEQ-NBR      PIC 9(06).             01202002
                   15  ASC-XFER-TYPE             PIC X(02).             01202100
                   15  ASC-XFER-STAT             PIC X(02).             01202200
100404*            15  ASC-XFER-FROM             PIC X(03).             01202302
100404             15  ASC-XFER-FROM             PIC X(04).             01202302
100404*            15  ASC-XFER-TO               PIC X(03).             01202402
100404             15  ASC-XFER-TO               PIC X(04).             01202402
                   15  ASC-COMP-FROM-DATE.                              01202500
                       20  ASC-COMP-FROM-MM      PIC X(02).             01202600
                       20  ASC-COMP-FROM-DD      PIC X(02).             01202700
                       20  ASC-COMP-FROM-YY      PIC X(04).             01202800
                   15  ASC-COMP-FROM-DATE-9                             01202902
                       REDEFINES ASC-COMP-FROM-DATE                     01203002
                                                 PIC 9(08).             01203102
                   15  ASC-COMP-TO-DATE.                                01203204
                       20  ASC-COMP-TO-MM        PIC X(02).             01203300
                       20  ASC-COMP-TO-DD        PIC X(02).             01203400
                       20  ASC-COMP-TO-YY        PIC X(04).             01203500
                   15  ASC-COMP-TO-DATE-9                               01203602
                       REDEFINES ASC-COMP-TO-DATE                       01203702
                                                 PIC 9(08).             01203802
                   15  ASC-RECV-FROM-DATE.                              01203902
                       20  ASC-RECV-FROM-MM      PIC X(02).             01204000
                       20  ASC-RECV-FROM-DD      PIC X(02).             01204100
                       20  ASC-RECV-FROM-YY      PIC X(04).             01204200
                   15  ASC-RECV-FROM-DATE-9                             01204302
                       REDEFINES ASC-RECV-FROM-DATE                     01204402
                                                 PIC 9(08).             01204502
                   15  ASC-RECV-TO-DATE.                                01204604
                       20  ASC-RECV-TO-MM        PIC X(02).             01204700
                       20  ASC-RECV-TO-DD        PIC X(02).             01204800
                       20  ASC-RECV-TO-YY        PIC X(04).             01204900
                   15  ASC-RECV-TO-DATE-9                               01205002
                       REDEFINES ASC-RECV-TO-DATE                       01205102
                                                 PIC 9(08).             01205202
                   15  ASC-DEPT                  PIC X(03).             01205300
                   15  ASC-CLASS                 PIC X(03).             01205400
                   15  ASC-UPC                   PIC X(13).             01205400
                   15  ASC-CALLBACK-NBR.                                01205502
                       20  ASC-CALLBACK-NBR-9    PIC 9(04).             01205602
                   15  ASC-APPROVAL-NBR.                                01205704
                       20  ASC-APPROVAL-NBR-9    PIC 9(09).             01205802
                   15  ASC-COMP-BY-INIT          PIC X.                 01205900
                   15  ASC-COMP-BY-NM            PIC X(20).             01206000
                   15  ASC-RECV-BY-INIT          PIC X.                 01206100
                   15  ASC-RECV-BY-NM            PIC X(20).             01206200
                   15  ASC-DISPLAY-TOT           PIC X.                 01207000
                   15  ASC-SORT-ORDER-FIELDS.                           01210100
                       20  ASC-XFER-TYPE-SORT-NR   PIC  X(01).          01210200
                       20  ASC-XFER-STATUS-SORT-NR PIC  X(01).          01210300
                       20  ASC-FROM-STORE-SORT-NR  PIC  X(01).          01210400
                       20  ASC-TO-STORE-SORT-NR    PIC  X(01).          01210500
                       20  ASC-COMP-DTE-SORT-NR    PIC  X(01).          01210600
                       20  ASC-RECV-DTE-SORT-NR    PIC  X(01).          01210700
                       20  ASC-CALLBCK-NR-SORT-NR  PIC  X(01).          01210900
                       20  ASC-APPRV-NR-SORT-NR    PIC  X(01).          01211000
                       20  ASC-SUBCL               PIC  X(03).          01210800
           EJECT                                                        01290000
      *----------------------------------------------------------------*01300000
      *    ABEND PROCESSING COPYBOOK.                                  *01310000
      *----------------------------------------------------------------*01320000
                                                                        01330000
           COPY DPWS013.                                                01340000
                                                                        01350000
      *    DB2 AREA FOR TTFTYCD  (TRANSFER TYPE TABLE)                  01350203
                                                                        01350301
           EXEC SQL                                                     01350401
                INCLUDE TTFTYCD                                         01350503
           END-EXEC.                                                    01350601
                                                                        01350701
      *    DB2 AREA FOR TTFSTCD  (TRANSFER STATUS CODE TABLE)           01350803
                                                                        01350901
           EXEC SQL                                                     01351001
                INCLUDE TTFSTCD                                         01351103
           END-EXEC.                                                    01351201
                                                                        01351301
      *    DB2 AREA FOR COMMUNICATIONS                                  01351401
                                                                        01351501
           EXEC SQL                                                     01351601
                INCLUDE SQLCA                                           01352001
           END-EXEC.                                                    01360001
                                                                        01530000
       EJECT                                                            01540000
       LINKAGE SECTION.                                                 01550000
                                                                        01560000
       01  DFHCOMMAREA.                                                 01570000
           05  FILLER                         OCCURS  1 TO 4072 TIMES   01580000
                                              DEPENDING ON EIBCALEN.    01590000
               10  FILLER                     PIC  X(01).               01600000
                                                                        01610000
                                                                        01620000
       PROCEDURE DIVISION.                                              01630000
      ***************************************************************** 01640000
      ** THIS MODULE CONTROLS THE OVERALL PROCESSING IN THE PROGRAM. ** 01650000
      ** THE SETUP AND PERFORM OF PARAGRAPH 0001-CALL-CICS-ARCH-API  ** 01660000
      ** MUST BE THE FIRST CODE EXECUTED IN THIS PROGRAM.            ** 01670000
      **                                                             ** 01680000
      ** THE SECOND OR 'EXIT' PERFORM OF THIS PARAGRAPH MUST BE THE  ** 01690000
      ** LAST CODE EXECUTED ON EACH ITERATION OF THIS PROGRAM.       ** 01700000
      ***************************************************************** 01710000
       0000-MAIN-MODULE.                                                01720000
           INITIALIZE DP030-CICS-API-FIELDS.                            01730000
           MOVE +1                       TO DP030-NUMBER-OF-MAPS.       01740000
           MOVE PC-CURRENT-MAPSET-NAME   TO DP030-MAPSET-NAME.          01750000
           MOVE PC-CURRENT-MAP-NAME      TO DP030-MAP-NAME (1).         01760000
           SET DP030-RECEIVE-APPL-MAP    TO TRUE.                       01770000
           MOVE LENGTH OF TF433AI        TO DP030-MAP-LENGTH (1).       01780000
           MOVE 'PROGRAM ENTRY CALL'     TO DP013-MESSAGE-TEXT (1).     01790000
           PERFORM 0001-CALL-CICS-ARCH-API.                             01800000
      *                                                                 01810000
           PERFORM 1000-CONTROL-PROCESSING                              01820000
      *                                                                 01830000
           MOVE 'PROGRAM EXIT CALL'      TO DP013-MESSAGE-TEXT (1).     01840000
           PERFORM 0001-CALL-CICS-ARCH-API.                             01850000
                                                                        01860000
       0001-CALL-CICS-ARCH-API.                                         01870000
CICS       CALL DP930-CICS-ARCH-API USING DFHEIBLK                              
                                          DFHCOMMAREA                           
                                          DP030-CICS-API-FIELDS                 
                                          DP020-STANDARD-COMMAREA               
                                          TF433AI.                              
      *                                                                 01930000
           IF  DP030-RC-CALL-SUCCESSFUL                                 01940000
               CONTINUE                                                 01950000
           ELSE                                                         01960000
               SET DP013-NO-ROLLBACK                                    01970000
                   DP013-XCTL-DISPLAY-RESTART                           01980000
                   DP013-CICS-ABEND      TO TRUE                        01990000
               MOVE 'BEFORE 0000-MAIN-MODULE'                           02000000
                                         TO DP013-PARAGRAPH             02010000
CICS           MOVE 'CALL TO CICS ARCH API NOT SUCCESSFUL, RETURN-CODE O        
CICS  -             'N NEXT LINE'        TO DP013-MESSAGE-TEXT (2)              
               MOVE DP030-RETURN-CODE                                   02040000
                                         TO DP013-MESSAGE-TEXT (3)      02050000
               PERFORM DP013-0000-PROCESS-ABEND                         02060000
           END-IF.                                                      02070000
       EJECT                                                            02080000
      *----------------------------------------------------------------*02090000
      *    PROCESS THE APPROPRIATE PARAGRAPHS BASED ON WHAT THE NEXT   *02100000
      *    COURSE OF ACTION IS FOR THIS TRANSACTION.                   *02110000
      *----------------------------------------------------------------*02120000
                                                                        02130000
       1000-CONTROL-PROCESSING.                                         02140000
                                                                        02150000
           EVALUATE TRUE                                                02160000
               WHEN DP020-NEXT-ACT-INITIAL                              02170000
                   INITIALIZE ASC-SPECIFIC-COMMAREA                     02180000
                   PERFORM 4000-BUILD-INITIAL-PANEL                     02201002
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02210000
                                                                        02300000
               WHEN DP020-NEXT-ACT-RETURN                               02301000
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02302000
                                                                        02303000
               WHEN DP020-NEXT-ACT-READ-MAP                             02310000
                   PERFORM 2000-PROCESS-PANEL                           02330000
                                                                        02340000
               WHEN OTHER                                               02350000
                   SET DP013-LOGIC-ABEND                                02360000
                       DP013-NO-ROLLBACK TO TRUE                        02370000
                   MOVE '1000-CONTROL-PROCESSING'                       02380000
                                         TO DP013-PARAGRAPH             02390000
                   MOVE 'INVALID NEXT ACTIVITY RETURNED TO APPL PGM:'   02400000
                                         TO DP013-MESSAGE-TEXT(1)       02410000
                   MOVE DP020-NEXT-APPL-ACTIVITY                        02420000
                                         TO DP013-MESSAGE-TEXT(2)       02430000
                   PERFORM DP013-0000-PROCESS-ABEND                     02440000
           END-EVALUATE.                                                02450000
      *----------------------------------------------------------------*02460000
      * FURTHER DETERMINE PROCESSING PATH BASED ON FUNCTION KEY ACTIONS*02470000
      * SPECIFIC FUNCTION KEYS LISTED HERE ARE ACTED UPON REGARDLESS OF*02480000
      * EDITS.                                                         *02490000
      *----------------------------------------------------------------*02500000
       2000-PROCESS-PANEL.                                              02510000
                                                                        02520000
           EVALUATE TRUE                                                02530000
                                                                        02540000
               WHEN DP020-SRC-AID = DP016-ENTER                         02541000
               WHEN DP020-SRC-AID = DP016-CLEAR                         02550000
               WHEN DP020-FK-REFRESH(DP020-SRC-AID)                     02551000
                   PERFORM 4400-MOVE-COMMAREA-TO-SCREEN                 02560000
                                                                        02580000
               WHEN OTHER                                               02720000
                   SET DP013-NO-ROLLBACK                                02721000
                       DP013-XCTL-DISPLAY-RESTART                       02722000
                       DP013-CICS-ABEND  TO TRUE                        02723000
                   MOVE '2000-PROCESS-PANEL'                            02724001
                                         TO DP013-PARAGRAPH             02725000
                   MOVE 'INVALID FUNCTION KEY NOT CAPTURED BY API'      02726000
                                         TO DP013-MESSAGE-TEXT (1)      02727000
                   PERFORM DP013-0000-PROCESS-ABEND                     02728000
           END-EVALUATE.                                                02730000
                                                                        02740000
       EJECT                                                            03130000
      *                                                                 04300000
      *----------------------------------------------------------------*04310000
      * GET THE DATA FROM THE INTER-APPLICATION COMMAREA, AND          *04320000
      * SET UP THE SCREEN.                                             *04330000
      *----------------------------------------------------------------*04340000
                                                                        04370000
       4000-BUILD-INITIAL-PANEL.                                        04360000
                                                                        04370000
           SET DP030-ERASE                                              PEKCS025
               DP030-SEND-MAP-AND-DATA (1)                              PEKCS025
               DP030-SET-CURSOR-TRAILER  TO TRUE.                       04380000
           MOVE TF001-INTER-APPL-COMMAREA TO ASC-SPECIFIC-COMMAREA.     04410002
      *                                                                 04630000
       EJECT                                                            05390000
      *----------------------------------------------------------------*05400000
      *    RESTORE THE DATA ON THE SCREEN FROM THE COMM AREA.          *05410000
      *----------------------------------------------------------------*05420000
                                                                        05430000
       4400-MOVE-COMMAREA-TO-SCREEN.                                    05440000
           MOVE ASC-XFER-TYPE            TO AXFRTYPO.                   05450001
           MOVE ASC-XFER-STAT            TO AXFRSTAO.                   05460001
100404     IF ASC-XFER-FROM > '0000'                                            
               MOVE ASC-XFER-FROM        TO AFRSTORO                    05470001
           END-IF.                                                      05473502
100404     IF ASC-XFER-TO > '0000'                                              
               MOVE ASC-XFER-TO          TO ATOSTORO                    05471001
           END-IF.                                                      05473502
           IF ASC-COMP-FROM-DATE > '00000000'                           05471702
               MOVE ASC-COMP-FROM-DATE-9 TO PV-DATE                     05471805
               MOVE PV-DATE              TO ACMPFDTO                    05471902
           END-IF.                                                      05472002
           IF ASC-COMP-TO-DATE > '00000000'                             05472102
               MOVE ASC-COMP-TO-DATE-9   TO PV-DATE                     05472205
               MOVE PV-DATE              TO ACMPTDTO                    05472302
           END-IF.                                                      05472402
           IF ASC-RECV-FROM-DATE > '00000000'                           05472502
               MOVE ASC-RECV-FROM-DATE-9 TO PV-DATE                     05472605
               MOVE PV-DATE              TO ARCVFDTO                    05473002
           END-IF.                                                      05473102
           IF ASC-RECV-TO-DATE > '00000000'                             05473202
               MOVE ASC-RECV-TO-DATE-9   TO PV-DATE                     05473305
               MOVE PV-DATE              TO ARCVTDTO                    05473402
           END-IF.                                                      05473502
           IF ASC-DEPT > '000'                                                  
               MOVE ASC-DEPT             TO ADEPTO                      05478001
           END-IF.                                                      05473502
           IF ASC-CLASS > '000'                                                 
               MOVE ASC-CLASS            TO ACLASSO                     05479001
           END-IF.                                                      05473502
      *    IF ASC-SUBCL > '000'                                                 
               MOVE ASC-SUBCL            TO ASUBCLO                     05478001
      *    END-IF.                                                      05473502
           IF ASC-UPC > '0000000000000'                                 05479102
               MOVE ASC-UPC              TO AUPCNOO                     05479202
           END-IF.                                                      05479302
           IF ASC-CALLBACK-NBR > '0000'                                 05479102
               MOVE ASC-CALLBACK-NBR-9   TO ACALLNOO                    05479202
           END-IF.                                                      05479302
           IF ASC-APPROVAL-NBR > '000000000'                            05479402
               MOVE ASC-APPROVAL-NBR-9   TO AAPPRNOO                    05479502
           END-IF.                                                      05479602
           MOVE ASC-COMP-BY-INIT         TO ACMPBYINO.                  05479701
           MOVE ASC-COMP-BY-NM           TO ACMPBYNMO.                  05479801
           MOVE ASC-RECV-BY-INIT         TO ARCVBYINO.                  05479901
           MOVE ASC-RECV-BY-NM           TO ARCVBYNMO.                  05480001
           MOVE ASC-DISPLAY-TOT          TO ADISPTOTO.                  05480101
           IF ASC-XFER-TYPE-SORT-NR > '0'                               05480201
               MOVE ASC-XFER-TYPE-SORT-NR                               05480301
                                         TO ASXFRTYPO                   05480401
           END-IF.                                                      05480501
           IF ASC-XFER-STATUS-SORT-NR > '0'                             05480601
               MOVE ASC-XFER-STATUS-SORT-NR                             05480701
                                         TO ASXFRSTAO                   05480801
           END-IF.                                                      05480901
           IF ASC-FROM-STORE-SORT-NR > '0'                              05481001
               MOVE ASC-FROM-STORE-SORT-NR                              05481101
                                         TO ASFRSTORO                   05481201
           END-IF.                                                      05481301
           IF ASC-TO-STORE-SORT-NR > '0'                                05481401
               MOVE ASC-TO-STORE-SORT-NR                                05481501
                                         TO ASTOSTORO                   05481601
           END-IF.                                                      05481701
           IF ASC-COMP-DTE-SORT-NR > '0'                                05481801
               MOVE ASC-COMP-DTE-SORT-NR                                05481901
                                         TO ASCMDTERO                   05482001
           END-IF.                                                      05482101
           IF ASC-RECV-DTE-SORT-NR > '0'                                05482201
               MOVE ASC-RECV-DTE-SORT-NR                                05482301
                                         TO ASRCDTERO                   05482401
           END-IF.                                                      05482501
           IF ASC-CALLBCK-NR-SORT-NR > '0'                              05483001
               MOVE ASC-CALLBCK-NR-SORT-NR                              05483101
                                         TO ASCALLNOO                   05483201
           END-IF.                                                      05483301
           IF ASC-APPRV-NR-SORT-NR > '0'                                05483401
               MOVE ASC-APPRV-NR-SORT-NR                                05483501
                                         TO ASAPPRNOO                   05483601
           END-IF.                                                      05483701
           IF ASC-XFER-TYPE > SPACE                                     05484601
               PERFORM 5000-GET-TRANSFER-TYPE-DESC                      05484701
               MOVE TFTYCD-TYP-1ST-LNE-DESC                             05484803
                                         TO AXFRTYPDO                   05484901
           END-IF.                                                      05485001
           IF ASC-XFER-STAT > SPACE                                     05485101
               PERFORM 5100-GET-STATUS-DESC                             05485201
               MOVE TFSTCD-XFER-STAT-DESC                               05485304
                                         TO AXFRSTADO                   05485401
           END-IF.                                                      05485501
                                                                        05485600
      *----------------------------------------------------------------*05485701
      * SELECT DESCRIPTION FROM TRANSFER TYPE TABLE                     05485801
      *----------------------------------------------------------------*05485901
       5000-GET-TRANSFER-TYPE-DESC.                                     05486001
           PERFORM                                                      05486101
               WITH TEST AFTER                                          05486201
               VARYING PV-RETRY-COUNTER                                 05486301
               FROM 1 BY 1                                              05486401
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               05486501
                         OR (SQLCODE = ZERO)                            05486601
                         OR (SQLCODE = +100))                           05486701
      *                                                                 05486801
               EXEC SQL                                                 05486901
                   SELECT TYP_1ST_LNE_DESC                              05488503
                   INTO   :TFTYCD-TYP-1ST-LNE-DESC                      05490903
                   FROM   TTFTYCD                                       05491403
                   WHERE  XFER_TYP_CDE   = :ASC-XFER-TYPE               05492001
               END-EXEC                                                 05492501
                                                                        05492601
               EVALUATE TRUE                                            05492701
                   WHEN SQLCODE = ZERO                                  05492801
                   WHEN SQLCODE = -904                                  05497701
                   WHEN SQLCODE = -913                                  05497801
                       CONTINUE                                         05497901
                   WHEN SQLCODE = +100                                  05498001
                       MOVE 'NOT FOUND'  TO TFTYCD-TYP-1ST-LNE-DESC     05498103
                   WHEN OTHER                                           05498201
                       MOVE '5000-GET-TRANSFER-TYPE-DESC'               05498501
                                         TO DP013-PARAGRAPH             05498601
                       MOVE 'SELECT TRANSFER DATA'                      05498701
                                         TO DP013-MESSAGE-TEXT (1)      05498801
                       MOVE SQLCA        TO DP013-SQLCA                 05498901
                       MOVE 'TTFTYCD'    TO DP013-DB2-TABLE-NAME (1)    05499003
                       SET DP013-DB2-ABEND                              05499101
                           DP013-XCTL-DISPLAY-RESTART                   05499201
                                         TO TRUE                        05499301
                       PERFORM DP013-0000-PROCESS-ABEND                 05499401
               END-EVALUATE                                             05499501
           END-PERFORM.                                                 05499601
      *                                                                 05499701
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        05499801
               MOVE '5000-GET-TRANSFER-TYPE-DESC'                       05499901
                                         TO DP013-PARAGRAPH             05500001
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'     05500101
                                         TO DP013-MESSAGE-TEXT (1)      05500201
               MOVE 'TRANSFER TYPE DESC' TO DP013-MESSAGE-TEXT (2)      05500301
               MOVE SQLCA                TO DP013-SQLCA                 05500401
               MOVE 'TTFTYCD'            TO DP013-DB2-TABLE-NAME (1)    05500703
               SET DP013-DB2-ABEND                                      05500801
                   DP013-XCTL-DISPLAY-RESTART                           05500901
                                         TO TRUE                        05501001
               PERFORM DP013-0000-PROCESS-ABEND                         05501101
           END-IF.                                                      05501201
       EJECT                                                            05502001
      *----------------------------------------------------------------*05503001
      * SELECT DESCRIPTION FROM TRANSFER STATUS TABLE                   05504001
      *----------------------------------------------------------------*05505001
       5100-GET-STATUS-DESC.                                            05506001
           PERFORM                                                      05507001
               WITH TEST AFTER                                          05508001
               VARYING PV-RETRY-COUNTER                                 05509001
               FROM 1 BY 1                                              05509101
               UNTIL ((PV-RETRY-COUNTER > PC-MAX-RETRIES)               05509201
                         OR (SQLCODE = ZERO)                            05509301
                         OR (SQLCODE = +100))                           05509401
      *                                                                 05509501
               EXEC SQL                                                 05509601
                   SELECT XFER_STAT_DESC                                05509701
                   INTO   :TFSTCD-XFER-STAT-DESC                        05509803
                   FROM   TTFSTCD                                       05509903
                   WHERE  XFER_STAT_CDE  = :ASC-XFER-STAT               05510001
               END-EXEC                                                 05511001
                                                                        05512001
               EVALUATE TRUE                                            05513001
                   WHEN SQLCODE = ZERO                                  05514001
                   WHEN SQLCODE = -904                                  05515001
                   WHEN SQLCODE = -913                                  05516001
                       CONTINUE                                         05517001
                   WHEN SQLCODE = +100                                  05518001
                       MOVE 'NOT FOUND'  TO TFSTCD-XFER-STAT-DESC       05519003
                   WHEN OTHER                                           05519101
                       MOVE '5100-GET-STATUS-DESC'                      05519401
                                         TO DP013-PARAGRAPH             05519501
                       MOVE 'SELECT TRANSFER STATUS DESC'               05519601
                                         TO DP013-MESSAGE-TEXT (1)      05519701
                       MOVE SQLCA        TO DP013-SQLCA                 05519801
                       MOVE 'TTFSTCD'    TO DP013-DB2-TABLE-NAME (1)    05519903
                       SET DP013-DB2-ABEND                              05520001
                           DP013-XCTL-DISPLAY-RESTART                   05521001
                                         TO TRUE                        05522001
                       PERFORM DP013-0000-PROCESS-ABEND                 05523001
               END-EVALUATE                                             05524001
           END-PERFORM.                                                 05525001
      *                                                                 05526001
           IF  PV-RETRY-COUNTER > PC-MAX-RETRIES                        05527001
               MOVE '5100-GET-STATUS-DESC'                              05528001
                                         TO DP013-PARAGRAPH             05529001
               MOVE 'MAXIMUM RETRIES EXCEEDED ATTEMPTING TO SELECT'     05529101
                                         TO DP013-MESSAGE-TEXT (1)      05529201
               MOVE 'STATUS DESC'        TO DP013-MESSAGE-TEXT (2)      05529301
               MOVE SQLCA                TO DP013-SQLCA                 05529401
               MOVE 'TTFSTCD'            TO DP013-DB2-TABLE-NAME (1)    05529503
               SET DP013-DB2-ABEND                                      05529601
                   DP013-XCTL-DISPLAY-RESTART                           05529701
                                         TO TRUE                        05529801
               PERFORM DP013-0000-PROCESS-ABEND                         05529901
           END-IF.                                                      05530001
       EJECT                                                            05540000
      *----------------------------------------------------------------*06390000
      *    ABEND PROCESSOR MODULE                                      *06400000
      *----------------------------------------------------------------*06410000
                                                                        06420000
           COPY DPPD013.                                                06430000
                                                                        06440000
