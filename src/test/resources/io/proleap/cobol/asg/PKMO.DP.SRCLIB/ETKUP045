       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.         ETKUP045.                                            
       AUTHOR.             SUE PARI.                                            
       DATE-WRITTEN.       02/15/00.                                            
       DATE-COMPILED.                                                           
                                                                                
************************************************************************        
******* THIS PROGRAM WILL USE THE EXTRACT FILES CREATED FOR     ********        
******* ALL INBOUND DOCUMENTS TO UPDATE THE TFGTRST TABLE WITH  ********        
******* THE RELEASE TIMESTAMP.                                  ********        
************************************************************************        
0228JF* 02-28-2001 JON FIEDLER 0228JF                                           
0228JF* ADDED THE VENDOR AND PROGRAM NAME TO THE SYSOUT ERROR MESSAGE.          
0228JF******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT INPUT-FILE                                                    
               ASSIGN TO IN01.                                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  INPUT-FILE                                                           
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD                                           
           RECORD CONTAINS 150 CHARACTERS                                       
           DATA RECORD IS INPUT-ASN-RECORD.                                     
                                                                                
           COPY ETRD023.                                                        
                                                                        ETKUT004
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                        ETKUT005
       01  PROGRAM-SWITCHES.                                            ETKUT004
           05  END-OF-FILE-SW            PIC X(1)  VALUE 'N'.           ETKUT004
               88  END-OF-FILE                     VALUE 'Y'.           ETKUT004
           05  ERRORS-FOUND-SW           PIC X(1)  VALUE 'N'.                   
               88  ERRORS-FOUND                    VALUE 'Y'.                   
                                                                                
           EXEC SQL                                                             
              INCLUDE SQLCA                                                     
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
              INCLUDE TFGTRST                                                   
           END-EXEC.                                                            
                                                                                
0228JF     EXEC SQL                                                             
0228JF        INCLUDE TENTNME                                                   
0228JF     END-EXEC.                                                            
0228JF                                                                          
0228JF     EXEC SQL                                                             
0228JF        INCLUDE TTRDPRT                                                   
0228JF     END-EXEC.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
           OPEN INPUT INPUT-FILE.                                               
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
           PERFORM 1000-PROCESS-INPUT-FILE                                      
              THRU 1000-PROCESS-INPUT-FILE-EXIT                                 
                 UNTIL END-OF-FILE.                                             
                                                                                
           IF ERRORS-FOUND                                                      
              MOVE 4095 TO RETURN-CODE.                                         
                                                                                
           CLOSE INPUT-FILE.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
       1000-PROCESS-INPUT-FILE.                                                 
                                                                                
           MOVE ET023-TP-DKEY           TO FGTRST-TP-DKEY.                      
           MOVE ET023-TP-DKEY-TMST      TO FGTRST-TP-DKEY-TMST.                 
           MOVE ET023-FUNC-CDE-CNTL-NBR TO FGTRST-FUNC-CDE-CNTL-NBR.            
           MOVE ET023-TRN-SET-CNTL-NBR  TO FGTRST-TRN-SET-CNTL-NBR.             
           MOVE ET023-TRN-DTL-TXT       TO FGTRST-TRN-DTL-TXT.                  
           MOVE ET023-TRN-RLSE-TMST     TO FGTRST-TRN-RLSE-TMST.                
                                                                                
           EXEC SQL                                                             
              UPDATE TFGTRST                                                    
                 SET TRN_RLSE_TMST = :FGTRST-TRN-RLSE-TMST                      
               WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
                 AND TP_DKEY_TMST = :FGTRST-TP-DKEY-TMST                        
                 AND FUNC_CDE_CNTL_NBR = :FGTRST-FUNC-CDE-CNTL-NBR              
                 AND TRN_SET_CNTL_NBR = :FGTRST-TRN-SET-CNTL-NBR                
                 AND TRN_DTL_TXT = :FGTRST-TRN-DTL-TXT                          
           END-EXEC.                                                            
                                                                                
                                                                                
           IF SQLCODE NOT = +000                                                
              DISPLAY SPACES                                                    
              DISPLAY 'ERROR UPDATING TFGTRST TABLE'                            
              DISPLAY 'SQLCODE = ' SQLCODE                                      
0228JF        PERFORM 2000-GET-VENDOR-NAME                                      
0228JF        DISPLAY 'VENDOR = ' ENTNME-ENT-NAME-DESC                          
              DISPLAY 'TP-DKEY = ' ET023-TP-DKEY                                
              DISPLAY 'TP-DKEY-TMST = ' ET023-TP-DKEY-TMST                      
              DISPLAY 'FUNC-CDE-CNTL-NBR = ' ET023-FUNC-CDE-CNTL-NBR            
              DISPLAY 'TRN-SET-CNTL-NBR = ' ET023-TRN-SET-CNTL-NBR              
              DISPLAY 'TRN-DTL-TXT = ' ET023-TRN-DTL-TXT                        
0228JF        DISPLAY 'PGM NAME = ETKUP045'                                     
              MOVE 'Y' TO ERRORS-FOUND-SW.                                      
                                                                                
           PERFORM 1100-READ-INPUT-FILE                                         
              THRU 1100-READ-INPUT-FILE-EXIT.                                   
                                                                                
       1000-PROCESS-INPUT-FILE-EXIT.                                            
           EXIT.                                                                
                                                                                
                                                                                
       1100-READ-INPUT-FILE.                                                    
                                                                        ETKUT004
           READ INPUT-FILE                                              ETKUT004
              AT END                                                    ETKUT004
                 SET END-OF-FILE TO TRUE.                               ETKUT004
                                                                                
       1100-READ-INPUT-FILE-EXIT.                                               
           EXIT.                                                                
                                                                                
0228JF 2000-GET-VENDOR-NAME.                                                    
0228JF                                                                          
0228JF     EXEC SQL                                                             
0228JF        SELECT ENT_NAME_DESC                                              
0228JF          INTO :ENTNME-ENT-NAME-DESC                                      
0228JF          FROM TENTNME E                                                  
0228JF              ,TTRDPRT T                                                  
0228JF         WHERE TP_DKEY = :FGTRST-TP-DKEY                                  
0228JF           AND T.ENT_ID = E.ENT_ID                                        
0228JF           AND TYPE_CDE = '01'                                            
0228JF     END-EXEC.                                                            
0228JF                                                                          
0228JF     IF SQLCODE NOT = +000                                                
0228JF        MOVE 'NOT FOUND' TO ENTNME-ENT-NAME-DESC                          
0228JF     END-IF.                                                              
                                                                                
