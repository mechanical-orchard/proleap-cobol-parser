00001  ID DIVISION.                                                     06/29/93
00002                                                                   SNKUP012
00003  PROGRAM-ID.    SNKUP012.                                            LV003
00004                                                                   SNKUP012
00005  AUTHOR.        ANNE YEKO.                                        SNKUP012
00006  INSTALLATION.  KOHLS DEPARTMENT STORES.                          SNKUP012
00007  DATE-WRITTEN.  NOVEMBER, 1992.                                   SNKUP012
00008  DATE-COMPILED.                                                   SNKUP012
00009                                                                   SNKUP012
00010 ******************************************************************SNKUP012
00011 *   THIS PROGRAM WILL READ AN INPUT FILE CREATED IN EITHER      **SNKUP012
00012 *   SNKUP007 OR SNKUP008.  THE KEY INFORMATION FROM THE INPUT   **SNKUP012
00013 *   FILE IS USED TO DELETE ROWS FROM THE ASOANM TABLE.          **SNKUP012
00014 ******************************************************************SNKUP012
00015 * CHANGED __/__/__ BY ____________                              **SNKUP012
00016 *   P_________  ________________________________________________**SNKUP012
00017 *               ________________________________________________**SNKUP012
00018 *                                                               **SNKUP012
00019 ******************************************************************SNKUP012
00020                                                                   SNKUP012
00021      EJECT                                                        SNKUP012
00022                                                                   SNKUP012
00023  ENVIRONMENT DIVISION.                                            SNKUP012
00024                                                                   SNKUP012
00025  INPUT-OUTPUT SECTION.                                            SNKUP012
00026                                                                   SNKUP012
00027  FILE-CONTROL.                                                    SNKUP012
00028                                                                   SNKUP012
00029      SELECT ASNHDR-DELETE-FILE                                    SNKUP012
00030             ASSIGN TO INP01.                                      SNKUP012
00031                                                                   SNKUP012
00032  DATA DIVISION.                                                   SNKUP012
00033                                                                   SNKUP012
00034  FILE SECTION.                                                    SNKUP012
00035                                                                   SNKUP012
00036  FD  ASNHDR-DELETE-FILE                                           SNKUP012
00037      RECORDING MODE IS F                                          SNKUP012
00038      LABEL RECORDS ARE STANDARD                                   SNKUP012
00039      RECORD CONTAINS 05 CHARACTERS                                   CL**3
00040      BLOCK CONTAINS 0 RECORDS.                                    SNKUP012
00041                                                                   SNKUP012
00042  01  ASNHDR-DELETE-RECORD        PIC X(05).                          CL**3
00043                                                                   SNKUP012
00044      EJECT                                                        SNKUP012
00045  WORKING-STORAGE SECTION.                                         SNKUP012
00046                                                                   SNKUP012
00047  01  FILLER.                                                      SNKUP012
00048      05  FILLER                  PIC  X(36)  VALUE                SNKUP012
00049          ' ***** WORKING STORAGE PGM=SNKUP012 '.                  SNKUP012
00050      05  PGM-COMPILED            PIC  X(20)  VALUE SPACE.         SNKUP012
00051      05  FILLER                  PIC  X(10)  VALUE                SNKUP012
00052          ' LOCATION='.                                            SNKUP012
00053      05  PGM-LOCATION            PIC  X(04)  VALUE SPACE.         SNKUP012
00054      05  FILLER                  PIC  X(06)  VALUE                SNKUP012
00055          ' *****'.                                                SNKUP012
00056                                                                   SNKUP012
00057  01  ABEND-CODE                  PIC S9(04)  VALUE ZERO           SNKUP012
00058                                              COMP SYNC.           SNKUP012
00059                                                                   SNKUP012
00060  01  ABEND-PARAGRAPH             PIC  X(30)  VALUE SPACE.         SNKUP012
00061                                                                   SNKUP012
00062      EJECT                                                        SNKUP012
00063  01  PC-PROGRAM-CONSTANTS.                                        SNKUP012
00064      05  PC-Y                    PIC  X(01)  VALUE 'Y'.           SNKUP012
00065      05  PC-N                    PIC  X(01)  VALUE 'N'.           SNKUP012
00066                                                                   SNKUP012
00067  01  PS-PROGRAM-SWITCHES.                                         SNKUP012
00068      05  PS-END-OF-INPUT-SW      PIC  X(01)  VALUE 'N'.           SNKUP012
00069          88 PS-END-OF-INPUT                  VALUE 'Y'.           SNKUP012
00070                                                                   SNKUP012
00071  01  PS-PROGRAM-VARIABLES.                                        SNKUP012
00072      05  PV-RECS-READ            PIC  S9(7)  VALUE +0             SNKUP012
00073                                              COMP-3.              SNKUP012
00074      05  PV-RECS-DELETED         PIC  S9(7)  VALUE +0             SNKUP012
00075                                              COMP-3.              SNKUP012
00076      05  PV-DELETE-COUNTER       PIC  S9(3)  VALUE +0             SNKUP012
00077                                              COMP-3.              SNKUP012
00078                                                                   SNKUP012
00079 *    ASNHDR DELETE RECORD LAYOUT                                  SNKUP012
00080                                                                   SNKUP012
00081      COPY SNRD002I.                                               SNKUP012
00082      EJECT                                                        SNKUP012
00083 ******************************************************************SNKUP012
00084 *    DB2 DATABASE AREA                                            SNKUP012
00085 ******************************************************************SNKUP012
00086                                                                   SNKUP012
00087 *    ASN ORDER ADDRESS NAME TEXT                                  SNKUP012
00088                                                                   SNKUP012
00089      EXEC SQL                                                     SNKUP012
00090          INCLUDE TASOANM                                          SNKUP012
00091      END-EXEC.                                                    SNKUP012
00092                                                                   SNKUP012
00093      EJECT                                                        SNKUP012
00094 *    SQL COMMUNICATION AREA                                       SNKUP012
00095                                                                   SNKUP012
00096      EXEC SQL                                                     SNKUP012
00097          INCLUDE SQLCA                                            SNKUP012
00098      END-EXEC.                                                    SNKUP012
00099      EJECT                                                        SNKUP012
00100 *    SQL COMMUNICATION AREA 2                                     SNKUP012
00101                                                                   SNKUP012
00102      COPY SQLCA2.                                                 SNKUP012
00103      EJECT                                                        SNKUP012
00104                                                                   SNKUP012
00105 ***************************************************************** SNKUP012
00106  PROCEDURE DIVISION.                                              SNKUP012
00107 ***************************************************************** SNKUP012
00108                                                                   SNKUP012
00109  A000-MAIN-DRIVER.                                                SNKUP012
00110                                                                   SNKUP012
00111      MOVE 'A000'                 TO PGM-LOCATION.                 SNKUP012
00112      MOVE WHEN-COMPILED          TO PGM-COMPILED.                 SNKUP012
00113                                                                   SNKUP012
00114      OPEN INPUT ASNHDR-DELETE-FILE.                               SNKUP012
00115                                                                   SNKUP012
00116      PERFORM B200-READ-INPUT-FILE.                                SNKUP012
00117                                                                   SNKUP012
00118      PERFORM B100-DELETE-ROWS                                     SNKUP012
00119        UNTIL PS-END-OF-INPUT.                                     SNKUP012
00120                                                                   SNKUP012
00121      DISPLAY 'SNKUP012 RECORDS READ:    ' PV-RECS-READ.           SNKUP012
00122      DISPLAY 'SNKUP012 RECORDS DELETED: ' PV-RECS-DELETED.        SNKUP012
00123                                                                   SNKUP012
00124      CLOSE ASNHDR-DELETE-FILE.                                    SNKUP012
00125                                                                   SNKUP012
00126      GOBACK.                                                      SNKUP012
00127                                                                   SNKUP012
00128      EJECT                                                        SNKUP012
00129                                                                   SNKUP012
00130 ***************************************************************** SNKUP012
00131 *    THIS PARAGRAPH CONTROLS THE MAIN PROCESSING OF THE PROGRAM   SNKUP012
00132 ***************************************************************** SNKUP012
00133  B100-DELETE-ROWS.                                                SNKUP012
00134                                                                   SNKUP012
00135      MOVE 'B100'                 TO PGM-LOCATION.                 SNKUP012
00136      MOVE 'B100-DELETE-ROWS          '                            SNKUP012
00137                                  TO ABEND-PARAGRAPH.              SNKUP012
00138                                                                   SNKUP012
00139      PERFORM B300-DELETE-ASOANM.                                  SNKUP012
00140      IF PV-DELETE-COUNTER > +500                                     CL**2
00141          PERFORM B400-COMMIT                                      SNKUP012
00142          MOVE ZERO               TO PV-DELETE-COUNTER             SNKUP012
00143      END-IF.                                                      SNKUP012
00144      PERFORM B200-READ-INPUT-FILE.                                SNKUP012
00145                                                                   SNKUP012
00146      EJECT                                                        SNKUP012
00147                                                                   SNKUP012
00148 ***************************************************************** SNKUP012
00149 *    THIS PARAGRAPH WILL READ THE NEXT ASN TO BE DELETED.         SNKUP012
00150 ***************************************************************** SNKUP012
00151  B200-READ-INPUT-FILE.                                            SNKUP012
00152                                                                   SNKUP012
00153      MOVE 'B200'                 TO PGM-LOCATION.                 SNKUP012
00154      MOVE 'B200-READ-INPUT-FILE      '                            SNKUP012
00155                                  TO ABEND-PARAGRAPH.              SNKUP012
00156                                                                   SNKUP012
00157      READ ASNHDR-DELETE-FILE                                      SNKUP012
00158          INTO SN002I-ASN-DELETE-RECORD                            SNKUP012
00159          AT END                                                   SNKUP012
00160              SET PS-END-OF-INPUT TO TRUE.                         SNKUP012
00161                                                                   SNKUP012
00162      IF NOT PS-END-OF-INPUT                                       SNKUP012
00163          ADD 1                   TO PV-RECS-READ                  SNKUP012
00164      END-IF.                                                      SNKUP012
00165                                                                   SNKUP012
00166      EJECT                                                        SNKUP012
00167                                                                   SNKUP012
00168 ***************************************************************** SNKUP012
00169 *    THIS PARAGRAPH DELETES ALL ASOANM ROWS THAT ARE EQUAL TO     SNKUP012
00170 *    THE CURRENT INPUTED ASN.                                     SNKUP012
00171 ***************************************************************** SNKUP012
00172  B300-DELETE-ASOANM.                                              SNKUP012
00173                                                                   SNKUP012
00174      MOVE 'B300'                 TO PGM-LOCATION.                 SNKUP012
00175      MOVE 'B300-DELETE-ASOANM        '                            SNKUP012
00176                                  TO ABEND-PARAGRAPH.              SNKUP012
00177                                                                   SNKUP012
00178      EXEC SQL                                                     SNKUP012
00179          DELETE                                                   SNKUP012
00180          FROM   TASOANM                                           SNKUP012
00181          WHERE  ASNHDR_ID   = :SN002I-ASNHDR-ID                      CL**3
00182      END-EXEC.                                                    SNKUP012
00183                                                                   SNKUP012
00184      IF SQLCODE = ZERO                                            SNKUP012
00185          ADD SQLERRD (3)         TO PV-RECS-DELETED               SNKUP012
00186          ADD +1                  TO PV-DELETE-COUNTER             SNKUP012
00187      ELSE                                                         SNKUP012
00188          IF SQLCODE = +100                                        SNKUP012
00189              CONTINUE                                             SNKUP012
00190          ELSE                                                     SNKUP012
00191              PERFORM Z998-SQL-ERROR                               SNKUP012
00192          END-IF                                                   SNKUP012
00193      END-IF.                                                      SNKUP012
00194                                                                   SNKUP012
00195      EJECT                                                        SNKUP012
00196                                                                   SNKUP012
00197 ***************************************************************** SNKUP012
00198 *    THIS PARAGRAPH WILL COMMIT THE ROWS JUST DELETED             SNKUP012
00199 ***************************************************************** SNKUP012
00200  B400-COMMIT.                                                     SNKUP012
00201                                                                   SNKUP012
00202      MOVE 'B400'                 TO PGM-LOCATION.                 SNKUP012
00203      MOVE 'B400-COMMIT               '                            SNKUP012
00204                                  TO ABEND-PARAGRAPH.              SNKUP012
00205                                                                   SNKUP012
00206      EXEC SQL                                                     SNKUP012
00207          COMMIT                                                   SNKUP012
00208      END-EXEC.                                                    SNKUP012
00209                                                                   SNKUP012
00210      IF SQLCODE = ZERO                                            SNKUP012
00211          CONTINUE                                                 SNKUP012
00212      ELSE                                                         SNKUP012
00213          PERFORM Z998-SQL-ERROR                                   SNKUP012
00214      END-IF.                                                      SNKUP012
00215                                                                   SNKUP012
00216      EJECT                                                        SNKUP012
00217                                                                   SNKUP012
00218 ***************************************************************** SNKUP012
00219 *    THIS MODULE PERFORMS THE ABEND FOR THE PROGRAM IN THE EVENT  SNKUP012
00220 *    THAT AN SQL ERROR CODE OCCURS.                               SNKUP012
00221 ***************************************************************** SNKUP012
00222  Z998-SQL-ERROR.                                                  SNKUP012
00223                                                                   SNKUP012
00224      MOVE SQLCODE                TO SQLCODE2.                     SNKUP012
00225      MOVE SQLERRD(3)             TO SQLERRD3.                     SNKUP012
00226      DISPLAY '** ABEND **         ** = SQLERROR'.                 SNKUP012
00227      DISPLAY '** PROGRAM          ** = SNKUP012'.                 SNKUP012
00228      DISPLAY '** PARAGRAPH        ** = ' ABEND-PARAGRAPH.         SNKUP012
00229      DISPLAY '** SQLCODE          ** = ' SQLCODE2.                SNKUP012
00230      DISPLAY '** ROWS PROCESSED   ** = ' SQLERRD3.                SNKUP012
00231      DISPLAY '** SQLERRM          ** = ' SQLERRM.                 SNKUP012
00232      DISPLAY '** SQLERRMC         ** = ' SQLERRMC.                SNKUP012
00233      MOVE +4013                  TO ABEND-CODE.                   SNKUP012
00234      CALL 'ILBOABN0'             USING ABEND-CODE.                SNKUP012
