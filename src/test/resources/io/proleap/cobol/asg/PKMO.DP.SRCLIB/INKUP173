       IDENTIFICATION DIVISION.                                         00010000
                                                                        00020000
       PROGRAM-ID.    INKUP173                                          00030000
       AUTHOR.        PAUL KEBER - STRATAGEM.                           00040000
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00050000
       DATE-WRITTEN.  10-13-98.                                         00060000
       DATE-COMPILED.                                                   00070000
                                                                        00080000
      ******************************************************************00090000
      *     INKUP173 - SALES ADJUSTMENT UPDATE PROGRAM                 *00100000
      ******************************************************************00110000
      *     THIS PROGRAM WILL READ SALE ADJUSTMENT RECORDS AND INSERT  *00120000
      * THEM TO THE ADJUSTMENT DETAIL DB2 TABLE (TADJDTL).  COMMITS    *00130000
      * ARE TAKEN EVERY TEN SECONDS, AND CHECKPOINT RESTART LOGIC IS   *00180000
      * INCORPORATED.                                                  *00190000
      ******************************************************************00200000
      * 03/11/1999 MARIA KLAMIK                                        *00190000
      *            ADDED LOGIC TO UPDATE THE SALES-ADJ-COMPLETED-      *00190000
      *            INDICATOR ON THE TINVPAR TABLE FOR EACH STORE       *00190000
      *            PROCESSED.                                          *00190000
      ******************************************************************00200000
      * 06/29/2000 JULIE SEIDLER                                       *00190000
      *            BEFORE DOING UPDATES FOR A STORE,                            
      *            ADDED LOGIC TO CHECK IF SALES-ADJ-COMPLETED-         00190000
      *            INDICATOR HAS ALREADY BE SET TO 'Y'.  PROGRAM MUST   00190000
      *            PREVENT FROM ADDING ADJUSTMENTS TWICE                00190000
      *                                                                 00190000
      *            ADDED LOGIC TO FORCE A CHECKPOINT AFTER PROCESSING   00190000
      *            A STORE                                              00190000
      *                                                                 00190000
      *            STORE TOTAL RECORDS (9999'S) HAVE BEEN ADDED TO THE  00190000
      *            INPUT FILE AND WILL BE IGNORED IN PROGRAM PROCESSING 00190000
      ******************************************************************00200000
W21732* 11/19/2002        CHANGED BY SUKUMAR,IGSI    WR# W21732        *        
W21732* CHANGED THE PROGRAM TO MOVE NON-INTELLIGENT SKU INTO THE       *        
W21732* TABLE AREA.                                                    *        
W21732******************************************************************        
W26600* 04/27/2004        CHANGED BY PAUL KEBER      WR# 26600         *        
W26600* STORE NUMBER EXPANSION PROJECT CHANGES TO POPULATE THE NEW     *        
W26600* TADJDTL LOC_NBR COLUMN WITH THE STORE NUMBER.                  *        
W26600******************************************************************        
W26600* 06/16/2004        CHANGED BY D. THEEL        WR# 26600         *        
W26600* PI STORE EXPANSION CHANGES.                                    *        
W26600******************************************************************        
28545 *                                                                         
28545 * 08/10/2006        B. GRAHAM - STRATAGEM      WR# W28545                 
28545 *                   DEPARTMENT LEVEL INVENTORY PROJECT                    
28545 *                   JOIN TINVPAR TO TINCNTL                               
W33304* 10/16/2008        CHANGED BY THERESE RAMSEYER #WR33304         *        
W33304* HILO - HOLD INVENTORY LEDGER OPEN                                       
W33304*                   --> ADDED TO 8010-CHECK-TINVPAR THE FOLLOWING         
W33304*                       ADDITIONS TO THE WHERE CLAUSE:                    
W33304*                       A.INV_ID = :IN019-INV-ID                          
W33304*                       LOC_INV_STAT_CDE = :PC-INITIAL-STATUS             
W33304*                   --> INCREASE LENGTH OF SALES-ADJUSTMENT-FILE          
W33304*                       FROM 90 TO 100 BYTES TO ACCOMMODATE THE           
W33304*                       EXPANSION OF THE INRD019 COPYBOOK.                
PE6906*03/18/2011          CHANGED BY LALITHA (COGNIZANT) PKE6906               
PE6906*                   AS A PART OF CHANGES TO INCLUDE UNCOUNTED             
PE6906*                   RETURNS IN INK09705 REPORT CHANGED INKUP173           
PE6906*                   TO ADD ADJUSTMENTS TO TADJADD AND TADJDTL             
W33304******************************************************************        
                                                                        00210000
                                                                        00220000
       ENVIRONMENT DIVISION.                                            00230000
                                                                        00240000
       CONFIGURATION SECTION.                                           00250000
       SOURCE-COMPUTER.        IBM-3090.                                00260000
       OBJECT-COMPUTER.        IBM-3090.                                00270000
       INPUT-OUTPUT SECTION.                                            00280000
       FILE-CONTROL.                                                    00290000
           SELECT SALES-ADJUSTMENT-FILE                                 00300000
               ASSIGN TO INP01.                                         00310000
                                                                        00320000
                                                                        00330000
       DATA DIVISION.                                                   00340000
       FILE SECTION.                                                    00350000
                                                                        00360000
       FD  SALES-ADJUSTMENT-FILE                                        00370000
           LABEL RECORDS ARE STANDARD                                   00380000
           RECORDING MODE IS F                                          00390000
           BLOCK CONTAINS 0 RECORDS.                                    00400000
W33304 01  SALES-ADJUSTMENT-RECORD     PIC X(100).                      00410000
      *                                                                 00420000
                                                                        00430000
                                                                        00440000
       WORKING-STORAGE SECTION.                                         00450000
                                                                        00460000
       01  ABEND-CODE                  PIC S9(04)  VALUE ZERO COMP SYNC.00470000
                                                                        00480000
       01  PC-PROGRAM-CONSTANTS.                                        00490000
           05  PC-JOB-NAME                PIC  X(06) VALUE 'INK097'.    00500000
           05  PC-PROGRAM-NAME            PIC  X(08) VALUE 'INKUP173'.  00510000
W33304     05  PC-INITIAL-STATUS          PIC  X(02) VALUE 'IN'.        00510000
           05  PC-MAX-RETRIES             PIC S9(03) VALUE +3.          00520000
           05  PC-MAX-DEADLOCKS           PIC S9(03) VALUE +3.          00530000
           05  PC-SALES-ADJUST-ADJ-RSN-TXT  PIC  X(20) VALUE            INKUP126
                                                     'SALES ADJUSTMENT'.INKUP126
           05  PC-SALES-ADJUST-ADJ-TYPE-CD  PIC  X(02) VALUE 'SA'.      INKUP126
PE6906     05  PC-ONE-BILLION       PIC S9(11) COMP-3 VALUE +1000000000.        
PE6906     05  PC-PRIME1            PIC S9(05) COMP-3 VALUE +38543.             
PE6906     05  PC-PRIME2            PIC S9(05) COMP-3 VALUE +58889.             
                                                                        00540000
       01  PE-PROGRAM-ERROR-MESSAGES.                                   00550000
           05  PE-MSG-01                       PIC X(50) VALUE          00600000
                'ATTEMPT TO INSERT ROW ON TABLE TADJDTL FAILED    '.    00610000
           05  PE-MSG-02                       PIC X(50) VALUE          00620000
                'ERROR IN SELECT ''STATUS CODE'' FROM TCKRSTCNTL  '.    00630000
           05  PE-MSG-03                       PIC X(50) VALUE          00640000
                'UPDATE TO ''WORK_STRG_DESC'' ON TCKRSTINF FAILED '.    00650000
           05  PE-MSG-04                       PIC X(50) VALUE          00660000
                'PROGRAM ABENDED WHILE TRYING TO PERFORM A COMMIT '.    00670000
           05  PE-MSG-05                       PIC X(50) VALUE          00680000
                'PROGRAM MAXIMUM DEADLOCK COUNT HAS BEEN EXCEEDED'.     00690000
           05  PE-MSG-06                       PIC X(50) VALUE          00740000
                'UPDATE OF STATUS CODE ON TCKRSTCNTL UNSUCCESSFUL'.     00750000
           05  PE-MSG-07                       PIC X(50) VALUE          00760000
                'ATTEMPT TO SELECT TIMESTAMP + CKPT_FRQNCY_QTY   '.     00770000
           05  PE-MSG-08                       PIC X(50) VALUE          00600000
                'ATTEMPT TO UPDATE ROW ON TABLE TINVPAR FAILED    '.    00610000
      *                                                                 00780000
       01  PS-PROGRAM-SWITCHES.                                         00790000
           05  PS-INPUT-FILE-EOF-SW         PIC  X        VALUE 'N'.    00800000
               88  PS-MORE-INPUT-RECORDS                  VALUE 'N'.    00810000
               88  PS-NO-MORE-INPUT-RECORDS               VALUE 'Y'.    00820000
JS0599     05  PS-EMPTY-INPUT-SW            PIC  X        VALUE 'N'.    00800000
               88  PS-EMPTY-INPUT                         VALUE 'Y'.    00810000
JS0600         88  PS-NOT-EMPTY-INPUT                     VALUE 'N'.    00820000
           05  PS-FIRST-COMMIT-SW           PIC X         VALUE 'N'.    00850000
               88  PS-FIRST-COMMIT                        VALUE 'Y'.    00860000
           05  PS-DEADLOCK-RESTART-SW       PIC X         VALUE 'N'.    00870000
               88  PS-PROGRAM-DEADLOCKED                  VALUE 'Y'.    00880000
PE6906     05  PS-INSERT-SW             PIC X(01)   VALUE  'N'.                 
PE6906         88  PS-INSERT-DONE                   VALUE  'Y'.                 
PE6906         88  PS-INSERT-NOT-DONE               VALUE  'N'.                 
PE6906     05  PS-FIRST-SW                   PIC X(01)   VALUE  'Y'.            
PE6906         88  PS-FIRST-TIME                    VALUE  'Y'.                 
PE6906         88  PS-NOT-FIRST-TIME                VALUE  'N'.                 
PE6906     05  PS-DUP-DKEY-NBR-SW            PIC X(01) VALUE 'N'.               
PE6906         88  PS-DUP-DKEY-NBR                     VALUE 'Y'.               
PE6906         88  PS-NOT-DUP-DKEY-NBR                 VALUE 'N'.               
      *                                                                 00930000
       01  PROGRAM-VARIABLES.                                           00940000
           05  PV-SQL-SUB                      PIC  9(03) VALUE ZERO.   INKUP126
PE6906     05  PV-RETRY-COUNT                PIC S9(04) COMP   VALUE +0.        
           05  PV-DEADLOCK-COUNTER             PIC  9(03) VALUE ZERO.   INKUP126
           05  PV-PARAGRAPH-NAME               PIC  X(30) VALUE SPACES. INKUP126
           05  PV-DB2-OPERATION-ATTEMPTED      PIC  X(50) VALUE SPACES. INKUP126
           05  PV-ABEND-MESSAGE                PIC  X(75) VALUE SPACES. 00970000
           05  PV-CURRENT-TIMESTAMP            PIC  X(26) VALUE SPACES. 00990000
           05  PV-COMMIT-TIMESTAMP             PIC  X(26) VALUE SPACES. 01000000
W26600*    05  PV-STORE-NBR                    PIC  X(03) VALUE SPACES.         
W26600     05  PV-STORE-NBR                    PIC  S9(4) COMP VALUE +0.        
W26600     05  PV-DISPLAY-STR                  PIC  9(05) VALUE ZEROES.         
W33304     05  PV-DISPLAY-INV-ID               PIC  9(05) VALUE ZEROES.         
W26600*    05  PV-STORE-NBR-9 REDEFINES PV-STORE-NBR                            
W26600*                                        PIC  9(03).                      
W26600*    05  PV-STORE-NBR-PREV               PIC  X(03) VALUE SPACES.         
W26600     05  PV-STORE-NBR-PREV               PIC  S9(4) COMP VALUE +0.        
W26600*    05  PV-STORE-NBR-HOLD               PIC  X(03) VALUE SPACES.         
W26600     05  PV-STORE-NBR-HOLD               PIC  S9(4) COMP VALUE +0.        
PE6906     05  PV-SKU-HOLD                     PIC  X(8) VALUE SPACE.           
PE6906     05  PV-DUP-ADJADD-DKEY-NBR          PIC S9(11) COMP-3                
PE6906                                         VALUE +0.                        
PE6906     05  PV-ADJADD-DKEY-NBR-HOLD         PIC S9(11) COMP-3                
PE6906                                         VALUE +0.                        
PE6906     05  PV-ADJ-EXT-RTL-AMT-HOLD         PIC S9(09)V99                    
PE6906                                         COMP-3 VALUE +0.                 
JS0600     05  PV-STORE-COUNT                  PIC  9(03) VALUE 1.      00530000
PE6906     05  PV-ADD-NUM                      PIC S9(04) COMP VALUE +0.        
PE6906     05  PV-RANDOM-NUMBER     PIC S9(11) COMP-3 VALUE +0.                 
PE6906     05  PV-SEED              PIC S9(11) COMP-3 VALUE ZERO.               
PE6906     05  PV-WORK-FIELD        PIC S9(17) COMP-3 VALUE ZERO.               
PE6906     05  PV-WORK-FIELD2       PIC S9(17) COMP-3 VALUE ZERO.               
PE6906     05  PV-TODAYS-DATE       PIC 9(06)  VALUE ZERO.                      
PE6906     05  PV-TODAYS-TIME       PIC 9(08)  VALUE ZERO.                      
PE6906     05  FILLER REDEFINES PV-TODAYS-TIME.                                 
PE6906         10  PV-HH            PIC 9(02).                                  
PE6906         10  PV-MIN           PIC 9(02).                                  
PE6906         10  PV-SEC           PIC 9(02).                                  
PE6906         10  PV-HUND-SEC      PIC 9(02).                                  
PE6906     05  PV-INITIAL-SEED      PIC 9(08)    VALUE ZERO.                    
PE6906     05  FILLER REDEFINES PV-INITIAL-SEED.                                
PE6906         10  PV-SEED-BYTE-1-2 PIC 9(02).                                  
PE6906         10  PV-SEED-BYTE-3-4 PIC 9(02).                                  
PE6906         10  PV-SEED-BYTE-5-6 PIC 9(02).                                  
PE6906         10  PV-SEED-BYTE-7-8 PIC 9(02).                                  
      *                                                                 01010000
      *                                                                 01020000
       01  PA-PROGRAM-ACCUMULATORS.                                     01030000
           05  PA-RECORD-COUNTS.                                        01040000
               10  PA-INPUT-COUNT              PIC  9(9)  VALUE ZEROES. 01050000
PE6906         10  PA-ADJDTL-INSERT-COUNT      PIC  9(9)  VALUE ZEROES. 01070000
PE6906         10  PA-ADJADD-INSERT-COUNT      PIC  9(9)  VALUE ZEROES. 01070000
               10  PA-COMMIT-COUNT             PIC  9(9)  VALUE ZEROES. 01090000
JS0600         10  PA-TOTAL-REC-COUNT          PIC  9(9)  VALUE ZEROES. 01090000
      *                                                                 01100000
       01  PD-PROGRAM-DISPLAYS.                                         01110000
           05  PD-DEADLOCK-COUNT         PIC  9(04) VALUE 0.            01120000
      *                                                                 01130000
      *                                                                 01140000
      ******************************************************************01150000
      * INRD019 - TIME TIMESTAMP RECORD/ SALE ADJUSTMENT RECORD         01160000
      ******************************************************************01170000
           COPY INRD019.                                                01180000
      *                                                                 01190000
      ******************************************************************01200000
      *  DB2 ERROR MESSAGES FORMAT AREA.                                01210000
      ******************************************************************01220000
           COPY DPWS004.                                                01230000
      *                                                                 01240000
      ******************************************************************01250000
      *  USED FOR DB2 ERRORS                                            01260000
      ******************************************************************01270000
           EXEC SQL                                                     01280000
               INCLUDE SQLCA                                            01290000
           END-EXEC.                                                    01300000
PE6906*                                                                 01310000
PE6906******************************************************************01320000
PE6906*  DCLGEN FOR THE INVENTORY ADJUSTED ADD TABLE.                   01330000
PE6906******************************************************************01340000
PE6906     EXEC SQL                                                     01350000
PE6906         INCLUDE TADJADD                                          01360000
PE6906     END-EXEC.                                                    01370000
      *                                                                 01310000
      ******************************************************************01320000
      *  DCLGEN FOR THE INVENTORY ADJUSTED DETAIL TABLE.                01330000
      ******************************************************************01340000
           EXEC SQL                                                     01350000
               INCLUDE TADJDTL                                          01360000
           END-EXEC.                                                    01370000
      *                                                                 01380000
      ******************************************************************01320000
      *  DCLGEN FOR THE INVENTORY PARAMETERS TABLE.                     01330000
      ******************************************************************01340000
           EXEC SQL                                                     01350000
               INCLUDE TINVPAR                                          01360000
           END-EXEC.                                                    01370000
28545 *                                                                 01380000
28545 ******************************************************************01320000
28545 *  DCLGEN FOR THE INVENTORY CONTROL TABLE.                        01330000
28545 ******************************************************************01340000
28545      EXEC SQL                                                     01350000
28545          INCLUDE TINCNTL                                          01360000
28545      END-EXEC.                                                    01370000
      *                                                                 01380000
      ***************************************************************** 01390000
      * DB2 CHECKPOINT RESTART CONTROL TABLE AND RESTART INFO TABLE     01400000
      ***************************************************************** 01410000
           EXEC SQL                                                     01420000
               INCLUDE TCKRSTCN                                         01430000
           END-EXEC.                                                    01440000
      *                                                                 01450000
           EXEC SQL                                                     01460000
               INCLUDE TCKRSTIN                                         01470000
           END-EXEC.                                                    01480000
      *                                                                 01490000
      ******************************************************************01500000
      * CHECKPOINT RESTART VARIABLES AND PREV COMMIT VARIABLES.        *01510000
      ******************************************************************01520000
       01  CR-CHECKPOINT-RESTART-AREA.                                  01530000
           05  CR-AREA-LEN                  PIC S9(04) VALUE +55  COMP. 01540000
           05  CR-CHECKPOINT-RESTART-VAR.                               01550000
W26600*        10  CR-PREV-DTL-KEY          PIC  X(17) VALUE SPACES.    01560000
W26600         10  CR-PREV-DTL-KEY          PIC  X(16) VALUE SPACES.    01560000
               10  FILLER REDEFINES CR-PREV-DTL-KEY.                    01570000
W26600*            15  CR-STORE-NBR         PIC  X(03).                 01580000
W26600             15  CR-STORE-NBR         PIC  S9(4) COMP.            01580000
                   15  CR-AREA-NBR          PIC  9(06).                 01590000
W21732             15  CR-SKU-NBR           PIC  X(08).                 01620000
               10  CR-INPUT-READ-COUNT      PIC  9(09) VALUE ZEROES.    01630000
               10  CR-TADJDTL-COMMIT-COUNT  PIC  9(09) VALUE ZEROES.    01640000
               10  CR-INSERT-COUNT          PIC  9(09) VALUE ZEROES.    01660000
               10  CR-TTL-REC-COUNT         PIC  9(09) VALUE ZEROES.    01660000
      *                                                                 01680000
       01  CK-CHECKPOINT-SAVE-AREA.                                     01690000
W26600*    05  CK-SAVE-PREV-KEY         PIC  X(17) VALUE SPACES.        01700000
W26600     05  CK-SAVE-PREV-KEY         PIC  X(16) VALUE SPACES.        01700000
           05  FILLER REDEFINES CK-SAVE-PREV-KEY.                       01710000
W26600*        10  CK-STORE-NBR         PIC  X(03).                     01720000
W26600         10  CK-STORE-NBR         PIC  S9(4) COMP.                01720000
               10  CK-AREA-NBR          PIC  9(06).                     01730000
W21732         10  CK-SKU-NBR           PIC  X(08).                     01760000
      *                                                                 01770000
      *                                                                 01780000
      *                                                                 01790000
      *                                                                 01800000
      *-------------------*                                             01810000
       PROCEDURE DIVISION.                                              01820000
      *-------------------*                                             01830000
                                                                        01840000
       0000-MAIN-MODULE.                                                01850000
                                                                        01860000
           PERFORM 1000-INITIALIZATION.                                 01870000
      *                                                                 01880000
           PERFORM 2000-PROCESS-INPUT                                   01890000
                UNTIL PS-NO-MORE-INPUT-RECORDS.                         01900000
      *                                                                 01910000
           PERFORM 8999-EOJ-ROUTINE.                                    01920000
                                                                        01930000
           STOP RUN.                                                    01940000
      *                                                                 01950000
      *                                                                 01960000
      *                                                                 01970000
      ******************************************************************01980000
      * OPEN INPUT FILE , INITIALIZE COUNTERS , READ FIRST INPUT RECORD 01990000
      * PREPARE CHECKPOINT RESTART TABLES FOR PROCESSING / RESTART      02000000
      ******************************************************************02010000
      *                                                                 02020000
       1000-INITIALIZATION.                                             02030000
      *                                                                 02040000
           OPEN INPUT SALES-ADJUSTMENT-FILE.                            02050000
      *                                                                 02060000
           PERFORM 7400-INIT-CHECKPOINT-SELECT.                         02070000
      *                                                                 02080000
      * CKPT-STAT-CODE = '9' INDICATES A RESTART CONDITION              02090000
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02100000
               PERFORM 7500-SELECT-RESTART-INFO                         02110000
               MOVE TCKRSTINF-WORK-STRG-DESC-TEXT TO                    02120000
                                             CR-CHECKPOINT-RESTART-VAR  02130000
               MOVE CR-PREV-DTL-KEY         TO CK-SAVE-PREV-KEY         02140000
      * MOVE RESTART COUNTS TO PA-FIELDS FOR ACCURATE DISPLAY AT EOJ    02150000
               MOVE CR-INSERT-COUNT         TO PA-ADJDTL-INSERT-COUNT   02170000
               MOVE CR-TADJDTL-COMMIT-COUNT TO PA-COMMIT-COUNT          02190000
JS0600         MOVE CR-TTL-REC-COUNT        TO PA-TOTAL-REC-COUNT       01660000
           ELSE                                                         02200000
               SET PS-FIRST-COMMIT TO TRUE                              02210000
           END-IF.                                                      02220000
      *                                                                 02230000
           DISPLAY 'COMMITS WILL BE TAKEN EVERY ',                      02240000
                   TCKRSTCNTL-CKPT-FRQNCY-QTY, ' SECONDS.'.             02250000
      *                                                                 02260000
           PERFORM 4000-READ-SALES-ADJUST-FILE UNTIL                    02270000
               PA-INPUT-COUNT > CR-INPUT-READ-COUNT                     02280000
            OR PS-NO-MORE-INPUT-RECORDS.                                02290000
      *                                                                 02300000
           MOVE IN019-STR-NBR TO PV-STORE-NBR-PREV                              
                                                                                
           IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                           02310000
               MOVE PV-STORE-NBR-HOLD TO PV-STORE-NBR-PREV                      
W26600         MOVE PV-STORE-NBR-HOLD TO PV-DISPLAY-STR                         
W26600         DISPLAY 'STORE NBR            ' PV-DISPLAY-STR           02360000
               DISPLAY 'AREA NBR             ' IN019-SCAN-AREA          02360000
W21732         DISPLAY 'SKU                  ' IN019-SKU                02350000
               DISPLAY 'DKEY                 ' IN019-DKEY-NBR           02350000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD: '             02370000
                        PA-INPUT-COUNT                                  02380000
           END-IF.                                                      02390000
                                                                        02400000
           IF PS-NO-MORE-INPUT-RECORDS                                  02410000
JS0599         SET PS-EMPTY-INPUT TO TRUE                                       
               IF TCKRSTCNTL-CKPT-STAT-CODE = '9'                       02420000
                   CONTINUE                                             02430000
               ELSE                                                     02440000
                   DISPLAY '******************************'             02450000
                   DISPLAY '** NO RECORDS ON INPUT FILE **'             02450000
                   DISPLAY '******************************'             02450000
           ELSE                                                         02460000
JS0600         PERFORM 8010-CHECK-TINVPAR                                       
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        02470000
           END-IF.                                                      02480000
      *                                                                 02490000
      *                                                                 02500000
      ******************************************************************02510000
      * PROCESS THE INPUT FILE BY INSERTING A TADJDTL ROW FOR EACH      02520000
      * RECORD.                                                         02500000
      ******************************************************************02530000
       2000-PROCESS-INPUT.                                              02540000
      *                                                                 02550000
           MOVE 'N' TO PS-DEADLOCK-RESTART-SW.                          02560000
PE6906     MOVE 'N' TO PS-DUP-DKEY-NBR-SW.                                      
      *    *-----------------------------------------                   02570000
      *    * THE STORE TOTAL RECORDS WILL BE IGNORED                    02570000
      *    *-----------------------------------------                   02570000
W21732     IF IN019-SKU       = '99999999' AND                                  
JS0600        IN019-SCAN-AREA = 999999                                          
JS0600        ADD +1 TO PA-TOTAL-REC-COUNT                              01090000
JS0600          CONTINUE                                                        
JS0600     ELSE                                                                 
PE6906*****************************************************************         
PE6906* DKEY NUMBER ZERO INDICATES MISSED/UNCOUNTED RETURNS. FOR A SKU          
PE6906* WITH SAME KEY OF TADJADD TABLE, FIRST ADJUSTMENT WILL BE ADDED          
PE6906* IN TADJADD TABLE AND SECOND ADJUSTMENT WILL BE ADDED IN TADJDTL         
PE6906* TABLE WITH SAME DKEY NUMBER AS THAT OF TADJADD ADJUSTMENT               
PE6906*****************************************************************         
PE6906         IF IN019-DKEY-NBR = 0                                            
PE6906               PERFORM 2099-BUILD-TADJADD-ROW                             
PE6906               PERFORM 7100-INSERT-TADJADD                            0258
PE6906             IF PS-DUP-DKEY-NBR-SW = 'Y'                              0258
PE6906               PERFORM 2100-BUILD-TADJDTL-ROW                             
PE6906               PERFORM 7000-INSERT-TADJDTL-ROW                            
PE6906             END-IF                                                       
PE6906         ELSE                                                             
PE6906               PERFORM 2100-BUILD-TADJDTL-ROW                             
PE6906               PERFORM 7000-INSERT-TADJDTL-ROW                            
PE6906         END-IF                                                       0258
PE6906     MOVE IN019-SKU     TO PV-SKU-HOLD                                    
PE6906     MOVE IN019-SALES-ADJ-EXT-RTL-AMT TO PV-ADJ-EXT-RTL-AMT-HOLD          
           END-IF.                                                              
                                                                                
           IF PV-STORE-NBR = PV-STORE-NBR-PREV                                  
               CONTINUE                                                         
           ELSE                                                                 
               PERFORM 8000-UPDATE-TINVPAR                                      
                                                                                
JS0600         ADD 1 TO PV-STORE-COUNT                                          
JS0600         PERFORM 8010-CHECK-TINVPAR                                       
               MOVE PV-STORE-NBR TO PV-STORE-NBR-PREV                           
           END-IF.                                                              
      *                                                                 02590000
           IF PS-PROGRAM-DEADLOCKED                                     02750000
               CONTINUE                                                 02760000
           ELSE                                                         02770000
               PERFORM 7700-COMMIT-PROCESSING                           02780000
               PERFORM 4000-READ-SALES-ADJUST-FILE.                     02790000
      *                                                                 03170000
      *                                                                 03180000
PE6906******************************************************************03190000
PE6906* BUILDS THE ADJUSTMENT ADD TABLE RECORD FROM THE SALES           03200000
PE6906* ADJUSTMENT RECORD LAYOUT.                                       03200000
PE6906******************************************************************03210000
PE6906 2099-BUILD-TADJADD-ROW.                                          03220000
PE6906                                                                  INKUP126
PE6906     MOVE IN019-STR-NBR               TO ADJADD-LOC-NBR           INKUP126
PE6906                                         PV-STORE-NBR.                    
PE6906     MOVE IN019-SCAN-AREA             TO ADJADD-AREA-NBR.         INKUP126
PE6906     MOVE IN019-SKU                   TO ADJADD-SKU-NBR.          INKUP126
PE6906     MOVE PC-SALES-ADJUST-ADJ-RSN-TXT TO ADJADD-ADJ-RSN-TXT.      INKUP126
PE6906     MOVE IN019-UNIT-PRICE            TO ADJADD-ADJ-UNIT-PR-AMT.  INKUP126
PE6906     MOVE PC-PROGRAM-NAME             TO ADJADD-CHG-ID-NBR.       INKUP126
PE6906     MOVE SPACES                      TO ADJADD-MISSED-MD-IND.    INKUP126
PE6906     MOVE IN019-SALES-ADJ-QTY         TO ADJADD-ADJ-QTY.          INKUP126
PE6906     MOVE IN019-SALES-ADJ-EXT-RTL-AMT TO ADJADD-ADJ-EXTD-AMT.     INKUP126
PE6906     MOVE ZEROES                      TO ADJADD-UPC-NBR.                  
PE6906*** GENERATE A RANDOM NUMBER FOR THE DKEY.....                            
PE6906     ADD 11                   TO PV-ADD-NUM.                              
PE6906                                                                          
PE6906     ACCEPT PV-TODAYS-TIME    FROM TIME.                                  
PE6906     COMPUTE PV-TODAYS-TIME   = (PV-TODAYS-DATE                           
PE6906                              + PV-ADD-NUM)                               
PE6906                              * 11.                                       
PE6906     MOVE PV-HUND-SEC         TO PV-SEED-BYTE-1-2.                        
PE6906     MOVE PV-SEC              TO PV-SEED-BYTE-3-4.                        
PE6906     MOVE PV-MIN              TO PV-SEED-BYTE-5-6.                        
PE6906     MOVE PV-HH               TO PV-SEED-BYTE-7-8.                        
PE6906     MOVE PV-INITIAL-SEED     TO PV-SEED.                                 
PE6906                                                                          
PE6906     COMPUTE PV-WORK-FIELD = ((PV-SEED + PC-PRIME1)                       
PE6906                           * PC-PRIME2).                                  
PE6906                                                                          
PE6906     DIVIDE PV-WORK-FIELD BY  PC-ONE-BILLION                              
PE6906         GIVING PV-WORK-FIELD2                                            
PE6906         REMAINDER PV-RANDOM-NUMBER.                                      
PE6906                                                                          
PE6906     IF (PV-SKU-HOLD = IN019-SKU)                                         
PE6906        IF PV-ADJ-EXT-RTL-AMT-HOLD NOT EQUAL TO                           
PE6906                               IN019-SALES-ADJ-EXT-RTL-AMT                
PE6906        MOVE PV-RANDOM-NUMBER    TO ADJADD-DKEY-NBR                       
PE6906        DISPLAY 'ADJADD-DKEY-NBR: ' ADJADD-DKEY-NBR                       
PE6906        END-IF                                                            
PE6906     ELSE                                                                 
PE6906        MOVE PV-RANDOM-NUMBER    TO ADJADD-DKEY-NBR                       
PE6906        DISPLAY 'ADJADD-DKEY-NBR: ' ADJADD-DKEY-NBR                       
PE6906     END-IF.                                                              
PE6906                                                                          
PE6906     MOVE PC-PROGRAM-NAME             TO ADJDTL-CRE-ID-NBR.       INKUP126
PE6906                                                                  INKUP126
PE6906******************************************************************03190000
      * BUILDS THE ADJUSTMENT DETAIL TABLE RECORD FROM THE SALES        03200000
      * ADJUSTMENT RECORD LAYOUT.                                       03200000
      ******************************************************************03210000
       2100-BUILD-TADJDTL-ROW.                                          03220000
00163                                                                   INKUP126
00164      MOVE IN019-STR-NBR               TO ADJDTL-LOC-NBR           INKUP126
                                               PV-STORE-NBR.                    
00165      MOVE IN019-SCAN-AREA             TO ADJDTL-AREA-NBR.         INKUP126
W21732     MOVE IN019-SKU                   TO ADJDTL-SKU-NBR.          INKUP126
00169      MOVE PC-SALES-ADJUST-ADJ-RSN-TXT TO ADJDTL-ADJ-RSN-TXT.      INKUP126
00170      MOVE PC-SALES-ADJUST-ADJ-TYPE-CD TO ADJDTL-ADJ-TYPE-CDE.     INKUP126
00171      MOVE ZERO                        TO ADJDTL-ADJ-UNIT-PR-AMT.  INKUP126
00172      MOVE IN019-SALES-ADJ-QTY         TO ADJDTL-ADJ-QTY.          INKUP126
00173      MOVE IN019-SALES-ADJ-EXT-RTL-AMT TO ADJDTL-ADJ-EXTD-AMT.     INKUP126
PE6906     IF PS-DUP-DKEY-NBR-SW = 'Y'                                          
PE6906        MOVE PV-DUP-ADJADD-DKEY-NBR      TO ADJDTL-DKEY-NBR       INKUP126
PE6906     ELSE                                                         INKUP126
PE6906        MOVE IN019-DKEY-NBR           TO ADJDTL-DKEY-NBR          INKUP126
PE6906     END-IF                                                       INKUP126
00175      MOVE PC-PROGRAM-NAME             TO ADJDTL-CRE-ID-NBR.       INKUP126
00176                                                                   INKUP126
      *                                                                 03170000
      *                                                                 03180000
      ******************************************************************03190000
      * READS THE SALES ADJUSTMENT FILE INTO THE SALES ADJUSTMENT       03200000
      * RECORD LAYOUT.                                                  03200000
      ******************************************************************03210000
       4000-READ-SALES-ADJUST-FILE.                                     03220000
           MOVE IN019-STR-NBR TO PV-STORE-NBR-HOLD.                             
                                                                                
           READ SALES-ADJUSTMENT-FILE INTO IN019-ITEM-REC               03230000
               AT END MOVE 'Y' TO PS-INPUT-FILE-EOF-SW.                 03240000
      *                                                                 03250000
           IF PS-NO-MORE-INPUT-RECORDS                                  03260000
               ADD 1                          TO PA-COMMIT-COUNT        06410000
               MOVE IN019-STR-NBR             TO CR-STORE-NBR           03310000
               MOVE IN019-SCAN-AREA           TO CR-AREA-NBR            03310000
W21732         MOVE IN019-SKU                 TO CR-SKU-NBR             03290000
               MOVE PA-COMMIT-COUNT           TO CR-TADJDTL-COMMIT-COUNT06470000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06480000
PE6906         MOVE PA-ADJDTL-INSERT-COUNT    TO CR-INSERT-COUNT        06500000
JS0600         MOVE PA-TOTAL-REC-COUNT        TO CR-TTL-REC-COUNT       01660000
      *        MOVE COMMIT INFO TO WORKING STORAGE ROW                  03330000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       03340000
                                             TCKRSTINF-WORK-STRG-DESC   03350000
               EXEC SQL                                                 03360000
                 UPDATE TCKRSTINF                                       03370000
                    SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC      03380000
                  WHERE JOB_NAME   = :PC-JOB-NAME                       03390000
                    AND PLAN_NAME  = :PC-PROGRAM-NAME                   03400000
               END-EXEC                                                 03410000
               IF SQLCODE = 0                                           03420000
                   CONTINUE                                             03430000
               ELSE                                                     03440000
                   MOVE PE-MSG-03        TO PV-DB2-OPERATION-ATTEMPTED  03450000
                   MOVE '* 4000-READ-CONDENSED-FILE *' TO               03460000
                        PV-PARAGRAPH-NAME                               03470000
                   PERFORM 9999-SQL-ABEND                               03480000
               END-IF                                                   03490000
           ELSE                                                         03500000
               ADD 1                     TO PA-INPUT-COUNT              03510000
           END-IF.                                                      03520000
      *                                                                 03530000
00196 ******************************************************************INKUP126
00197 * INSERT A ROW INTO THE TADJDTL TABLE. IF A -911 (DEADLOCK)       INKUP126
00198 * OCCURS, ALL UPDATES SINCE THE LAST COMMIT HAVE BEEN ROLLED BACK,INKUP126
00199 * SO PERFORM THE RESTART PROCESS.  PROCESSING WILL RESUME WITH THEINKUP126
00200 * FIRST RECORD AFTER THE LAST COMMIT.                             INKUP126
00202 ******************************************************************INKUP126
00204  7000-INSERT-TADJDTL-ROW.                                         INKUP126
      *                                                                 04720000
PE6906     SET PS-INSERT-NOT-DONE        TO TRUE.                       04730000
00213      EXEC SQL                                                     INKUP126
00214          INSERT INTO TADJDTL                                      INKUP126
00216                (                                                  INKUP126
W26600*                STORE_NBR                                        INKUP126
W26600                 LOC_NBR                                                  
00217                , AREA_NBR                                         INKUP126
W21732               , SKU_NBR                                          INKUP126
00221                , CRE_TMST                                         INKUP126
00222                , ADJ_RSN_TXT                                      INKUP126
00223                , ADJ_TYPE_CDE                                     INKUP126
00224                , ADJ_UNIT_PR_AMT                                  INKUP126
00225                , ADJ_QTY                                          INKUP126
00226                , ADJ_EXTD_AMT                                     INKUP126
00227                , DKEY_NBR                                         INKUP126
00228                , CRE_ID_NBR                                       INKUP126
00229                )                                                  INKUP126
00230          VALUES                                                   INKUP126
00231                (                                                  INKUP126
W26600                 :ADJDTL-LOC-NBR                                  INKUP126
W26600*                :ADJDTL-STORE-NBR                                INKUP126
00233                , :ADJDTL-AREA-NBR                                 INKUP126
W21732               , :ADJDTL-SKU-NBR                                  INKUP126
00237                ,  CURRENT TIMESTAMP                               INKUP126
00238                , :ADJDTL-ADJ-RSN-TXT                              INKUP126
00239                , :ADJDTL-ADJ-TYPE-CDE                             INKUP126
00240                , :ADJDTL-ADJ-UNIT-PR-AMT                          INKUP126
00241                , :ADJDTL-ADJ-QTY                                  INKUP126
00242                , :ADJDTL-ADJ-EXTD-AMT                             INKUP126
00243                , :ADJDTL-DKEY-NBR                                 INKUP126
00244                , :ADJDTL-CRE-ID-NBR                               INKUP126
00245                )                                                  INKUP126
00246      END-EXEC                                                     INKUP126
      *                                                                 05080000
           EVALUATE SQLCODE                                             05090000
               WHEN 0                                                   05100000
                   CONTINUE                                             05110000
               WHEN -911                                                05120000
                   ADD +1                 TO PV-DEADLOCK-COUNTER        05130000
                   DISPLAY 'DEADLOCK -911 IN 7000-INSERT-TADJDTL-ROW'   05140000
                   IF PV-DEADLOCK-COUNTER > PC-MAX-DEADLOCKS            05150000
                       MOVE '7000-INSERT-TADJDTL-ROW'                   05160000
                                          TO PV-PARAGRAPH-NAME          05170000
                       MOVE PE-MSG-05     TO PV-DB2-OPERATION-ATTEMPTED 08160000
                       PERFORM 9999-SQL-ABEND                           08170000
                   ELSE                                                 05190000
                       PERFORM 7999-RESTART-PROCESS                     05200000
                   END-IF                                               05210000
               WHEN OTHER                                               05220000
                   MOVE '7000-INSERT-TADJDTL-ROW'                       05160000
                                          TO PV-PARAGRAPH-NAME          05170000
                   MOVE PE-MSG-01         TO PV-DB2-OPERATION-ATTEMPTED 05240000
                   PERFORM 9999-SQL-ABEND                               05250000
           END-EVALUATE                                                 05260000
      *                                                                 05270000
           IF PS-PROGRAM-DEADLOCKED                                     05280000
               CONTINUE                                                 05290000
           ELSE                                                         05300000
PE6906         ADD 1 TO PA-ADJDTL-INSERT-COUNT                          05310000
           END-IF.                                                      05320000
PE6906******************************************************************        
PE6906*  INSERT TADJADD                                                         
PE6906******************************************************************        
PE6906  7100-INSERT-TADJADD.                                                    
PE6906                                                                          
PE6906*** INSERT INTO TADJADD....                                               
PE6906     MOVE ZERO                TO PV-RETRY-COUNT.                          
PE6906     SET PS-INSERT-NOT-DONE   TO TRUE.                                    
PE6906                                                                          
PE6906     PERFORM UNTIL PS-INSERT-DONE                                         
PE6906                OR PV-RETRY-COUNT > PC-MAX-RETRIES                        
PE6906         EXEC SQL                                                         
PE6906              INSERT                                                      
PE6906              INTO  TADJADD                                               
PE6906                   (LOC_NBR                                               
PE6906                  , AREA_NBR                                              
PE6906                  , SKU_NBR                                               
PE6906                  , UPC_NBR                                               
PE6906                  , ADJ_UNIT_PR_AMT                                       
PE6906                  , ADJ_QTY                                               
PE6906                  , MISSED_MD_IND                                         
PE6906                  , DKEY_NBR                                              
PE6906                  , CHG_TMST                                              
PE6906                  , CHG_ID_NBR                                            
PE6906                  , ADJ_EXTD_AMT                                          
PE6906                  , ADJ_RSN_TXT)                                          
PE6906         VALUES    (:ADJADD-LOC-NBR                                       
PE6906                  , :ADJADD-AREA-NBR                                      
PE6906                  , :ADJADD-SKU-NBR                                       
PE6906                  , :ADJADD-UPC-NBR                                       
PE6906                  , :ADJADD-ADJ-UNIT-PR-AMT                               
PE6906                  , :ADJADD-ADJ-QTY                                       
PE6906                  , :ADJADD-MISSED-MD-IND                                 
PE6906                  , :ADJADD-DKEY-NBR                                      
PE6906                  ,  CURRENT TIMESTAMP                                    
PE6906                  , :ADJADD-CHG-ID-NBR                                    
PE6906                  , :ADJADD-ADJ-EXTD-AMT                                  
PE6906                  , :ADJADD-ADJ-RSN-TXT)                                  
PE6906         END-EXEC                                                         
PE6906                                                                          
PE6906         EVALUATE TRUE                                                    
PE6906             WHEN SQLCODE = ZERO                                          
PE6906                 SET PS-INSERT-DONE TO TRUE                               
PE6906             WHEN SQLCODE = -803                                          
PE6906                 ADD 1 TO PV-RETRY-COUNT                                  
PE6906                 SET PS-DUP-DKEY-NBR TO TRUE                              
PE6906                 COMPUTE PV-DUP-ADJADD-DKEY-NBR = ADJADD-DKEY-NBR         
PE6906                 SET PS-INSERT-DONE TO TRUE                               
PE6906             WHEN SQLCODE = +100                                          
PE6906                  SET PS-INSERT-DONE TO TRUE                              
PE6906             WHEN SQLCODE = -904                                          
PE6906             WHEN SQLCODE = -911                                          
PE6906             WHEN SQLCODE = -913                                          
PE6906                  ADD 1 TO PV-RETRY-COUNT                                 
PE6906             WHEN SQLWARN0 NOT = SPACES                                   
PE6906             WHEN SQLCODE < ZERO                                          
PE6906             WHEN SQLCODE > ZERO                                          
PE6906                DISPLAY 'SQLCODE -000 ' ADJADD-DKEY-NBR                   
PE6906                DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                         
PE6906                MOVE '7100-INSERT-TADJADD'                                
PE6906                                   TO PV-PARAGRAPH-NAME                   
PE6906                MOVE 'INSERT INTO TADJADD'                                
PE6906                                   TO PV-DB2-OPERATION-ATTEMPTED          
PE6906                PERFORM 9999-SQL-ABEND                                    
PE6906         END-EVALUATE                                                     
PE6906         MOVE ADJADD-DKEY-NBR  TO PV-ADJADD-DKEY-NBR-HOLD                 
PE6906     END-PERFORM.                                                         
PE6906                                                                          
PE6906     IF  PV-RETRY-COUNT > PC-MAX-RETRIES                                  
PE6906          DISPLAY 'SKU  :  ' ADJADD-SKU-NBR                               
PE6906          DISPLAY ' STORE: ' ADJADD-LOC-NBR                               
PE6906                  ' AREA: '  ADJADD-AREA-NBR                              
PE6906                  ' SKU: '   ADJADD-SKU-NBR                               
PE6906                  ' UPC: '   ADJADD-UPC-NBR                               
PE6906                  ' RTL: '   ADJADD-ADJ-UNIT-PR-AMT                       
PE6906                  ' DKEY: '  ADJADD-DKEY-NBR                              
PE6906          MOVE '7100-INSERT-TADJADD' TO PV-PARAGRAPH-NAME                 
PE6906          MOVE 'MAX RETRIES EXCEEDED ON TADJADD TABLE'                    
PE6906                                   TO PV-DB2-OPERATION-ATTEMPTED          
PE6906          PERFORM 9999-SQL-ABEND                                          
PE6906     END-IF.                                                              
PE6906                                                                          
PE6906     IF PS-PROGRAM-DEADLOCKED                                     05280000
PE6906         CONTINUE                                                 05290000
PE6906     ELSE                                                         05300000
PE6906         ADD 1 TO PA-ADJADD-INSERT-COUNT                          05310000
PE6906     END-IF.                                                      05320000
PE6906                                                                          
      *                                                                 05330000
      *                                                                 05340000
      ******************************************************************05350000
      * 7300-GET-COMMIT-TIMESTAMP.                                     *05360000
      * PROCESSES: GET THE COMMIT TIMESTAMP FROM THE CHECKPOINT        *05370000
      *            CONTROL TABLE                                       *05380000
      ******************************************************************05390000
       7300-GET-COMMIT-TIMESTAMP.                                       05400000
                                                                        05410000
           EXEC SQL                                                     05420000
      * CHECKPOINT TAKEN BASED ON CHEKPOINT RESTART CICSP1 PANEL        05430000
             SELECT (CURRENT TIMESTAMP + CKPT_FRQNCY_QTY SECONDS)       05440000
               INTO :PV-COMMIT-TIMESTAMP                                05450000
               FROM TCKRSTCNTL                                          05460000
              WHERE JOB_NAME  = :PC-JOB-NAME                            05470000
                AND PLAN_NAME = :PC-PROGRAM-NAME                        05480000
           END-EXEC.                                                    05490000
                                                                        05500000
           EVALUATE TRUE                                                05510000
               WHEN SQLCODE = 0                                         05520000
                   CONTINUE                                                     
      *            DISPLAY 'NEXT COMMIT AT TIMESTAMP: ',                05530000
      *                  PV-COMMIT-TIMESTAMP                            05540000
               WHEN SQLCODE NOT EQUAL ZERO                              05550000
                   MOVE '7300-GET-COMMIT-TIMESTAMP' TO PV-PARAGRAPH-NAME05560000
                   MOVE PE-MSG-07        TO PV-DB2-OPERATION-ATTEMPTED  05570000
                   PERFORM 9999-SQL-ABEND                               05580000
           END-EVALUATE.                                                05590000
      *                                                                 05600000
      *                                                                 05610000
      ******************************************************************05620000
      * 7400-INIT-CHECKPOINT-SELECT                                    *05630000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART CONTROL TABLE   *05640000
      *            IF WE ARE IN A RESTART CONDITION.                   *05650000
      ******************************************************************05660000
       7400-INIT-CHECKPOINT-SELECT.                                     05670000
                                                                        05680000
           EXEC SQL                                                     05690000
             SELECT  CKPT_STAT_CODE                                     05700000
                    ,CKPT_FRQNCY_QTY                                    05710000
               INTO  :TCKRSTCNTL-CKPT-STAT-CODE                         05720000
                    ,:TCKRSTCNTL-CKPT-FRQNCY-QTY                        05730000
               FROM  TCKRSTCNTL                                         05740000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05750000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       05760000
           END-EXEC.                                                    05770000
                                                                        05780000
           IF SQLCODE = 0                                               05790000
               CONTINUE                                                 05800000
           ELSE                                                         05810000
               MOVE '7400-INIT-CHECKPOINT-SELECT' TO PV-PARAGRAPH-NAME  05820000
               MOVE PE-MSG-02         TO PV-DB2-OPERATION-ATTEMPTED     05830000
               PERFORM 9999-SQL-ABEND                                   05840000
           END-IF.                                                      05850000
      *                                                                 05860000
      *                                                                 05870000
      ******************************************************************05880000
      * 7500-SELECT-RESTART-INFO                                       *05890000
      * PROCESSES: SELECTS FROM THE CHECKPOINT RESTART INFORMATION     *05900000
      *            TABLE TO OBTAIN THE PROGRAMS SAVED VARIABLES.       *05910000
      ******************************************************************05920000
       7500-SELECT-RESTART-INFO.                                        05930000
                                                                        05940000
           EXEC SQL                                                     05950000
             SELECT  WORK_STRG_DESC                                     05960000
               INTO  :TCKRSTINF-WORK-STRG-DESC                          05970000
               FROM  TCKRSTINF                                          05980000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           05990000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       06000000
           END-EXEC.                                                    06010000
                                                                        06020000
           IF SQLCODE = 0                                               06030000
               CONTINUE                                                 06040000
           ELSE                                                         06050000
               MOVE '*7500-SELECT-RESTART-INFO*' TO PV-PARAGRAPH-NAME   06060000
               PERFORM 9999-SQL-ABEND                                   06070000
           END-IF.                                                      06080000
      *                                                                 06090000
      *                                                                 06100000
      ******************************************************************06110000
      * 7600-SET-CURRENT-TIMESTAMP.                                    *06120000
      * PROCESSES: GET THE CURRENT TIMESTAMP FROM DB2.                 *06130000
      ******************************************************************06140000
       7600-SET-CURRENT-TIMESTAMP.                                      06150000
                                                                        06160000
           EXEC SQL                                                     06170000
                SET :PV-CURRENT-TIMESTAMP = CURRENT TIMESTAMP           06180000
           END-EXEC.                                                    06190000
      *                                                                 06200000
           IF SQLCODE = 0                                               06210000
               CONTINUE                                                 06220000
           ELSE                                                         06230000
               MOVE '*7600-SET-CURRENT-TIMESTAMP*' TO PV-PARAGRAPH-NAME 06240000
               PERFORM 9999-SQL-ABEND                                   06250000
           END-IF.                                                      06260000
      *                                                                 06270000
      *                                                                 06280000
      ******************************************************************06290000
      * 7700-COMMIT-PROCESSING.                                        *06300000
      * PROCESSES: CHECK IF WE NEED TO TAKE A COMMIT.                  *06310000
      *          PREPARE CHECKPOINT RECORD FOR UPDATE OF WORK_STRG_DESC*06320000
      ******************************************************************06330000
       7700-COMMIT-PROCESSING.                                          06340000
           MOVE '*7700-COMMIT-PROCESSING*'    TO PV-PARAGRAPH-NAME.     06350000
                                                                        06360000
           PERFORM 7600-SET-CURRENT-TIMESTAMP.                          06370000
      *    *------------------------------------                        06380000
      *        PREPARE COMMIT RECORD FOR UPDATE                         06400000
      *    *------------------------------------                        06380000
           IF (PV-CURRENT-TIMESTAMP > PV-COMMIT-TIMESTAMP)              06390000
               ADD 1                          TO PA-COMMIT-COUNT        06410000
               MOVE IN019-STR-NBR             TO CR-STORE-NBR           03310000
               MOVE IN019-SCAN-AREA           TO CR-AREA-NBR            03310000
W21732         MOVE IN019-SKU                 TO CR-SKU-NBR             03290000
               MOVE PA-COMMIT-COUNT           TO CR-TADJDTL-COMMIT-COUNT06470000
               MOVE PA-INPUT-COUNT            TO CR-INPUT-READ-COUNT    06480000
PE6906         MOVE PA-ADJDTL-INSERT-COUNT    TO CR-INSERT-COUNT        06500000
JS0600         MOVE PA-TOTAL-REC-COUNT        TO CR-TTL-REC-COUNT       01660000
      *    *---MOVE COMMIT INFO TO WORKING STORAGE ROW                  06520000
               MOVE CR-CHECKPOINT-RESTART-AREA TO                       06530000
                                             TCKRSTINF-WORK-STRG-DESC   06540000
               IF PS-FIRST-COMMIT                                       06550000
                   MOVE '9' TO TCKRSTCNTL-CKPT-STAT-CODE                06560000
                   PERFORM 7900-UPDATE-CHECKPOINT-STATUS                06570000
                   MOVE 'N' TO PS-FIRST-COMMIT-SW                       06580000
               END-IF                                                   06590000
TEST           DISPLAY '   .. TIMED COMMIT '                                    
TEST               '   CHECKPT KEY: ' CR-PREV-DTL-KEY                   01700000
TEST               '   LINE NBR: '  CR-INPUT-READ-COUNT                 06480000
               PERFORM 7800-COMMIT-CHANGES                              06600000
               PERFORM 7300-GET-COMMIT-TIMESTAMP                        06610000
           END-IF.                                                      06620000
      *                                                                 06630000
      *                                                                 06640000
      ******************************************************************06650000
      * 7800-COMMIT-CHANGES.                                            06660000
      * PROCESSES: PERFORM UPDATE OF CHECKPOINT INFORMATION TABLE.      06670000
      *            TAKE A COMMIT.                                       06680000
      *  ==> PERFORMED FROM: 7700-COMMIT-PROCESSING.                    06690000
      ******************************************************************06700000
       7800-COMMIT-CHANGES.                                             06710000
                                                                        06720000
           EXEC SQL                                                     06730000
             UPDATE TCKRSTINF                                           06740000
                SET WORK_STRG_DESC = :TCKRSTINF-WORK-STRG-DESC          06750000
              WHERE JOB_NAME       = :PC-JOB-NAME                       06760000
                AND PLAN_NAME      = :PC-PROGRAM-NAME                   06770000
           END-EXEC.                                                    06780000
                                                                        06790000
           IF SQLCODE = 0                                               06800000
               CONTINUE                                                 06810000
           ELSE                                                         06820000
               MOVE PE-MSG-03         TO PV-DB2-OPERATION-ATTEMPTED     06830000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06840000
               PERFORM 9999-SQL-ABEND                                   06850000
           END-IF.                                                      06860000
                                                                        06870000
           EXEC SQL                                                     06880000
               COMMIT                                                   06890000
           END-EXEC.                                                    06900000
                                                                        06910000
           IF SQLCODE = 0                                               06920000
               CONTINUE                                                 06930000
           ELSE                                                         06940000
               MOVE PE-MSG-04         TO PV-DB2-OPERATION-ATTEMPTED     06950000
               MOVE  '* 7800-COMMIT-CHANGES *' TO PV-PARAGRAPH-NAME     06960000
               PERFORM 9999-SQL-ABEND                                   06970000
           END-IF.                                                      06980000
      *                                                                 06990000
      *                                                                 07000000
      ******************************************************************07010000
      * 7900-UPDATE-CHECKPOINT-STATUS                                   07020000
      * PROCESSES: UPDATES THE STATUS ON THE CHECKPOINT CONTROL TABLE   07030000
      *                                                                 07040000
      ******************************************************************07050000
       7900-UPDATE-CHECKPOINT-STATUS.                                   07060000
                                                                        07070000
           EXEC SQL                                                     07080000
             UPDATE  TCKRSTCNTL                                         07090000
                SET  CKPT_STAT_CODE = :TCKRSTCNTL-CKPT-STAT-CODE        07100000
              WHERE  JOB_NAME  = :PC-JOB-NAME                           07110000
                AND  PLAN_NAME = :PC-PROGRAM-NAME                       07120000
           END-EXEC.                                                    07130000
                                                                        07140000
           IF SQLCODE = 0                                               07150000
               CONTINUE                                                 07160000
           ELSE                                                         07170000
               MOVE '7900-UPDATE-CHECKPOINT-STATUS' TO PV-PARAGRAPH-NAME07180000
               MOVE PE-MSG-06        TO PV-DB2-OPERATION-ATTEMPTED      07190000
               PERFORM 9999-SQL-ABEND                                   07200000
           END-IF.                                                      07210000
                                                                        07220000
           EJECT                                                        07230000
      *                                                                 07240000
      *                                                                 07250000
      ******************************************************************07260000
      * PROGRAM RESTART DUE TO A DEADLOCK AGAINST TADJDTL ROW FOR UPDATE07270000
      *  FIND RESTART RECORD AND CKPT RESTART AT POINT OF LAST COMMIT.  07280000
      ******************************************************************07290000
       7999-RESTART-PROCESS.                                            07300000
      *                                                                 07310000
           SET PS-PROGRAM-DEADLOCKED TO TRUE.                           07320000
      *                                                                 07330000
           CLOSE SALES-ADJUSTMENT-FILE.                                 07340000
      *                                                                 07350000
           PERFORM 7500-SELECT-RESTART-INFO.                            07360000
      *                                                                 07370000
           DISPLAY '**** RESTART **** '.                                07380000
           MOVE PV-DEADLOCK-COUNTER TO PD-DEADLOCK-COUNT.               07390000
           DISPLAY '** SQLCODE -911 **  ' PD-DEADLOCK-COUNT ' TIMES'.   07400000
      *                                                                 07410000
      *-- THIS CHECK WILL PREVENT THE FILE FROM BEING RESTARTED IN      07420000
      *-- THE WRONG POSITION IF NO COMMITS HAVE OCCURRED AND THE        07430000
      *-- RESTART INFORMATION IN NOT CLEARED OUT.                       07440000
           IF PS-FIRST-COMMIT                                           07450000
               INITIALIZE TCKRSTINF-WORK-STRG-DESC                      07460000
               DISPLAY 'NO COMMITS HAVE OCCURRED YET; RESTART FROM'     07470000
                      ' BEGINNING'                                      07480000
           ELSE                                                         07490000
               DISPLAY 'TCKRSTINF-LAST CKPT '                           07500000
                        TCKRSTINF-WORK-STRG-DESC (1:54)                 07510000
      ***    INFO ON LAST CHECKPOINT TAKEN IS IN                        07520000
      ***    CR-CHECKPOINT-RESTART-AREA.                                07530000
               MOVE TCKRSTINF-WORK-STRG-DESC TO                         07540000
                     CR-CHECKPOINT-RESTART-AREA                         07550000
W26600         MOVE CR-STORE-NBR    TO PV-DISPLAY-STR                           
               DISPLAY 'CHECKPOINT RESTART INFORMATION:'                07560000
W26600*        DISPLAY 'STORE NBR       ' CR-STORE-NBR                  07570000
W26600         DISPLAY 'STORE NBR       ' PV-DISPLAY-STR                07570000
               DISPLAY 'AREA NBR        ' CR-AREA-NBR                   07580000
W21732         DISPLAY 'SKU             ' CR-SKU-NBR                    07610000
               DISPLAY 'INPUT RECS READ ' CR-INPUT-READ-COUNT           07620000
           END-IF.                                                      07630000
      *                                                                 07640000
           OPEN INPUT SALES-ADJUSTMENT-FILE.                            07650000
           MOVE ZEROES                          TO PA-INPUT-COUNT.      07660000
      *                                                                 07670000
      *POSITION INPUT FILE TO 1ST RECORD AFTER LAST CHECKPOINT          07680000
           PERFORM 4000-READ-SALES-ADJUST-FILE                          07690000
               UNTIL PA-INPUT-COUNT > CR-INPUT-READ-COUNT               07700000
                 OR PS-NO-MORE-INPUT-RECORDS.                           07710000
           IF PS-NO-MORE-INPUT-RECORDS                                  07720000
               DISPLAY 'END OF INPUT FILE ON RESTART'                   07730000
           ELSE                                                         07740000
               DISPLAY 'INPUT RECORD RESTARTED ON RECORD '              07750000
                        PA-INPUT-COUNT                                  07760000
W26600*        DISPLAY 'STORE NBR       ' IN019-STR-NBR                 07570000
W26600         MOVE IN019-STR-NBR   TO PV-DISPLAY-STR                           
W26600         DISPLAY 'STORE NBR       ' PV-DISPLAY-STR                07570000
               DISPLAY 'AREA NBR        ' IN019-SCAN-AREA               07580000
W21732         DISPLAY 'SKU             ' IN019-SKU                     07610000
           END-IF.                                                      07820000
      *                                                                 07830000
      *RE-ESTABLISH COUNTS FOR PROCESSED INPUT (FROM CHECKPOINT RECORD) 07840000
PE6906     MOVE CR-INSERT-COUNT         TO PA-ADJDTL-INSERT-COUNT.      07860000
           MOVE CR-TADJDTL-COMMIT-COUNT TO PA-COMMIT-COUNT.             07880000
      *                                                                 07890000
      *                                                                 07900000
      ******************************************************************07910000
      *  UPDATE TINVPAR TABLE                                          *07920000
      ******************************************************************07930000
       8000-UPDATE-TINVPAR.                                             07940000
      *                                                                 07950000
           EXEC SQL                                                             
                UPDATE TINVPAR                                                  
                SET    SLS_ADJ_CPLD_IND = 'Y'                                   
W26600*         WHERE  STORE_NBR = :PV-STORE-NBR-PREV                           
W26600          WHERE  LOC_NBR   = :PV-STORE-NBR-PREV                           
                  AND  ACTL_UNIT_BK_DTE = '9999-09-09'                          
                  AND  ACTL_FIN_BK_DTE  = '9999-09-09'                          
JS0600            AND  SLS_ADJ_CPLD_IND = 'N'                                   
28545             AND  INV_ID = :INVPAR-INV-ID                                  
           END-EXEC.                                                            
                                                                                
W26600     MOVE PV-STORE-NBR-PREV TO PV-DISPLAY-STR.                            
           IF SQLCODE = +0                                                      
JS0600        DISPLAY '** TINVPAR UPDATED '                                     
JS0600*                  'FOR STORE NUMBER: ' PV-STORE-NBR-PREV                 
W26600                   'FOR STORE NUMBER: ' PV-DISPLAY-STR                    
           ELSE                                                                 
              DISPLAY '*------------------------------------'                   
W26600*       DISPLAY 'STR ' PV-STORE-NBR-PREV                                  
W26600        DISPLAY 'STR ' PV-DISPLAY-STR                                     
                  ' ---  CHECK SLS_ADJ_CPLD_IND ON TINVPAR'                     
              DISPLAY '*------------------------------------'                   
              MOVE '8000-UPDATE-TINVPAR'  TO PV-PARAGRAPH-NAME                  
              MOVE PE-MSG-08              TO PV-DB2-OPERATION-ATTEMPTED         
              PERFORM 9999-SQL-ABEND                                            
           END-IF.                                                              
      ******************************************************************05880000
      * BEFORE UPDATING TADJDTL, CHECK IF SLS-ADJ-IND HAS ALREAD BEEN  *05900000
      * SET TO 'Y'                                                     *05910000
      ******************************************************************05920000
JS0600 8010-CHECK-TINVPAR.                                              05930000
                                                                        05940000
           EXEC SQL                                                     05950000
W26600*      SELECT  STORE_NBR                                          05960000
W26600       SELECT  LOC_NBR                                            05960000
                    ,SLS_ADJ_CPLD_IND                                           
28545               ,A.INV_ID                                                   
W26600*        INTO  :INVPAR-STORE-NBR                                  05970000
W26600         INTO  :INVPAR-LOC-NBR                                    05970000
                    ,:INVPAR-SLS-ADJ-CPLD-IND                           05970000
28545               ,:INVPAR-INV-ID                                     05970000
               FROM  TINVPAR A                                          05980000
28545               ,TINCNTL B                                          05980000
W26600*         WHERE  STORE_NBR = :IN019-STR-NBR                               
W26600          WHERE  LOC_NBR = :IN019-STR-NBR                                 
W33304            AND  A.INV_ID = :IN019-INV-ID                                 
W33304            AND  LOC_INV_STAT_CDE = :PC-INITIAL-STATUS                    
W33304            AND  ACTL_UNIT_BK_DTE = '9999-09-09'                          
                  AND  ACTL_FIN_BK_DTE  = '9999-09-09'                          
28545             AND A.INV_ID = B.INV_ID                                       
28545             AND B.ACTV_IND = 'Y'                                          
           END-EXEC.                                                    06010000
                                                                        06020000
           IF SQLCODE = 0                                               06030000
               IF INVPAR-SLS-ADJ-CPLD-IND = 'N'                         05970000
                   CONTINUE                                             06040000
               ELSE                                                     06050000
                   DISPLAY '*------------------------------------'              
W26600*            DISPLAY 'STR ' IN019-STR-NBR                                 
W26600             MOVE IN019-STR-NBR TO PV-DISPLAY-STR                         
W26600             DISPLAY 'STR ' PV-DISPLAY-STR                                
                   ' SALES ADJUSTMENTS HAVE ALREADY BEEN PROCESSED'             
                   DISPLAY '       '                                            
                    ' HAS ITS SLS-ADJ-COMP-IND ALREADY SET TO  Y'               
                   DISPLAY '*------------------------------------'              
                   MOVE '*8010-CHECK-TINVPAR*' TO PV-PARAGRAPH-NAME         0606
                   PERFORM 9999-SQL-ABEND                                   0607
           ELSE                                                         06050000
               DISPLAY '*------------------------------------'                  
W26600         MOVE IN019-STR-NBR TO PV-DISPLAY-STR                             
W26600*        DISPLAY 'STR ' IN019-STR-NBR                                     
W26600         DISPLAY 'STR ' PV-DISPLAY-STR                                    
W33304         MOVE IN019-INV-ID TO PV-DISPLAY-INV-ID                           
W33304         DISPLAY 'INV_ID ' PV-DISPLAY-INV-ID                              
               DISPLAY '*------------------------------------'                  
               MOVE '*8010-CHECK-TINVPAR*' TO PV-PARAGRAPH-NAME         06060000
               PERFORM 9999-SQL-ABEND                                   06070000
           END-IF.                                                      06080000
      *                                                                 07890000
      *                                                                 07900000
      ******************************************************************07910000
      *  RESET STATUS CODE, DISPLAY FINAL COUNTS , CLOSE INPUT FILE    *07920000
      ******************************************************************07930000
       8999-EOJ-ROUTINE.                                                07940000
      *                                                                 07950000
JS0600     IF PS-NOT-EMPTY-INPUT                                                
JS0600       OR TCKRSTCNTL-CKPT-STAT-CODE = '9'                                 
               PERFORM 8000-UPDATE-TINVPAR                              0797    
                                                                                
               MOVE '0' TO TCKRSTCNTL-CKPT-STAT-CODE                    0796    
               PERFORM 7900-UPDATE-CHECKPOINT-STATUS                    0797    
                                                                                
               DISPLAY '                                      '         0799    
               DISPLAY 'INPUT RECORDS    ', PA-INPUT-COUNT              0800    
               DISPLAY 'TTL 99999999 REC ', PA-TOTAL-REC-COUNT          0800    
PE6906         DISPLAY 'ADJDTL INSERT COUNT    ', PA-ADJDTL-INSERT-COUNT0803    
PE6906         DISPLAY 'ADJADD INSERT COUNT    ', PA-ADJADD-INSERT-COUNT0803    
               DISPLAY '                                         '      0804    
               DISPLAY 'COMMITS TAKEN    ', PA-COMMIT-COUNT             0805    
               CLOSE SALES-ADJUSTMENT-FILE                              0807    
JS0600     ELSE                                                                 
               CLOSE SALES-ADJUSTMENT-FILE                                  0807
           END-IF.                                                              
      *                                                                 08180000
      ******************************************************************08190000
      *  STANDARD DB2 ERROR ABEND ROUTINE                               08200000
      *  THIS ROUTINE FORCES AN ABEND DUE TO A DB2 PROBLEM.             08210000
      ******************************************************************08220000
       9999-SQL-ABEND.                                                  08230000
                                                                        08240000
           MOVE +4013                 TO ABEND-CODE.                    08250000
           DISPLAY '**  ABEND     **'.                                  08260000
           DISPLAY '**  PROGRAM   ** => ' PC-PROGRAM-NAME.              08270000
           DISPLAY '**  PARAGRAPH **  = ' PV-PARAGRAPH-NAME.            08280000
           DISPLAY '**  OPERATION **  = ' PV-DB2-OPERATION-ATTEMPTED.   08290000
                                                                        08300000
      *    THE FOLLOWING COPYBOOK UTILIZES THE DB2 ABEND MODULE         08310000
      *    DSNTIAR TO CONVERT SQLCA INTO UNDERSTANDABLE VERBAGE.        08320000
                                                                        08330000
           COPY DPPD004.                                                08340000
           CALL 'ILBOABN0'  USING ABEND-CODE.                           08350000
