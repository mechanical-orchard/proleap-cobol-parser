       IDENTIFICATION DIVISION.                                         00010012
       PROGRAM-ID.    XMKRP035                                          00020012
       AUTHOR.        BOB MCASEY.                                       00030012
       INSTALLATION.  KOHLS DEPARTMENT STORES.                          00040012
       DATE-WRITTEN.  01/10/2003.                                       00050012
       DATE-COMPILED.                                                   00060012
                                                                        00070012
      ******************************************************************00080012
      *          DOMAIN OUTLOC LOCATION DATA INTEGRITY                  00090012
      *                                                                 00100012
      *     XMKRP035 - OUTLOC DATA INTEGRITY REPORT                     00110012
      *                                                                 00120012
      * THIS PROGRAM READS AN EXTRACT OF RMS OUTLOC DATA AND COMPARES   00130012
      * IT TO AN EXTRACT OF OUTLOC DATA FROM THE XM DOMAIN TABLES.      00140012
      * ANY DIFFERENCES ARE WRITTEN TO THE XM LOCATION DATA INTEGRITY   00150012
      * ERRORS REPORT.                                                  00160012
      *                                                                 00170012
      * THE RMS INPUT FILE ARE FORMATTED TO THE HO014-WH-DATA.          00180012
      *                                                                 00190012
      * THE MESSAGE TEXT FOR THE REPORT ERROR MESSAGES ARE FOUND IN     00200012
      * COPYBOOK XMWS001.                                               00210012
      *                                                                 00220012
      * IF THERE ARE ANY ERRORS TO BE REPORTED, PROGRAM WILL END WITH   00230012
      * RETURN CODE 4095 SO THAT AN E-MAIL CAN BE SENT IN A             00240012
      * SUBSEQUENT JOB STEP.                                            00250012
      *                                                                 00260012
      ******************************************************************00270012
      *---------------------------------------------------------------* 00280012
      *    WR/PITS#     DATE        DESCRIPTION                       * 00290012
      *    --------   ----------   ---------------------------------  * 00300012
      *    WR24442    08/20/2003   INITIAL CREATION.                  * 00310012
      *    WR25285    09/19/2003   USE NEW RMS OUTLOC DATASET (V2)    * 00320012
      *                            MOD CODE TO USE NEW HORD015 CPYBK  * 00330012
      *    WR26600    04/29/2004   ADD THE 10 CHARACTER LOCATION NAME * 00331012
      *                            TO THE XMT_LOC EXTRACT.            * 00332012
0407PK*    WR31384    04/26/2007   USE NEW COPYBOOK XMRD016 FOR THE   * 00333012
0407PK*    XMT_LOC BMC UNLOAD INPUT FILE THAT IS NOW 463 BYTES.       * 00334012
      *---------------------------------------------------------------* 00340012
                                                                        00350012
                                                                        00360012
       ENVIRONMENT DIVISION.                                            00370012
       CONFIGURATION SECTION.                                           00380012
       SOURCE-COMPUTER.        IBM-3090.                                00390012
       OBJECT-COMPUTER.        IBM-3090.                                00400012
       INPUT-OUTPUT SECTION.                                            00410012
       FILE-CONTROL.                                                    00420012
                                                                        00430012
           SELECT RMS-OUTLOC-EXTRACT-DATA-FILE                          00440012
               ASSIGN TO INP01.                                         00450012
                                                                        00460012
           SELECT XM-OUTLOC-EXTRACT-DATA-FILE                           00470012
               ASSIGN TO INP02.                                         00480012
                                                                        00490012
           SELECT XM-OUTLOC-DATA-INTEGRITY-RPT                          00500012
               ASSIGN TO RPT01.                                         00510012
                                                                        00520012
       DATA DIVISION.                                                   00530012
       FILE SECTION.                                                    00540012
                                                                        00550012
       FD  RMS-OUTLOC-EXTRACT-DATA-FILE                                 00560012
           RECORDING MODE IS F                                          00570012
           LABEL RECORDS ARE STANDARD                                   00580012
           BLOCK CONTAINS 0 RECORDS.                                    00590012
                                                                        00600012
       01  RMS-EXTRACT-REC                  PIC  X(194).                00620012
                                                                        00630012
       FD  XM-OUTLOC-EXTRACT-DATA-FILE                                  00640012
           RECORDING MODE IS F                                          00650012
           LABEL RECORDS ARE STANDARD                                   00660012
           BLOCK CONTAINS 0 RECORDS.                                    00670012
                                                                        00680012
      *01  XM-EXTRACT-REC                   PIC  X(456).                00702012
0407PK 01  XM-EXTRACT-REC                   PIC  X(463).                00703012
                                                                        00710012
       FD  XM-OUTLOC-DATA-INTEGRITY-RPT                                 00720012
           RECORDING  MODE IS F                                         00730012
           LABEL RECORDS ARE STANDARD                                   00740012
           BLOCK CONTAINS 0 RECORDS.                                    00750012
                                                                        00760012
       01  XM-OUTLOC-RPT-LINE                PIC X(200).                00770012
                                                                        00780012
                                                                        00790012
       WORKING-STORAGE SECTION.                                         00800012
                                                                        00810012
       01  PC-PROGRAM-CONSTANTS.                                        00820012
           05  PC-PGM-NAME             PIC  X(08)      VALUE 'XMKRP035'.00830012
           05  PC-MAX-DTL-LINES        PIC S9(04) COMP VALUE +70.       00840012
                                                                        00850012
       01  PV-PROGRAM-VARIABLES.                                        00860012
           05  PV-PAGE-CNT             PIC S9(05)        VALUE ZERO.    00870012
           05  PV-LINE-CNT             PIC S9(04) COMP   VALUE +71.     00880012
           05  PV-DISPLAY-CNT          PIC ZZZZ9.                       00890012
           05  PV-ADVANCE              PIC S9(03) COMP-3 VALUE ZERO.    00900012
           05  PV-NBR-ERRORS           PIC S9(04) COMP   VALUE ZERO.    00910012
           05  PV-DATE-YYMMDD.                                          00920012
               10  PV-YYMMDD-YY        PIC  X(02)        VALUE SPACE.   00930012
               10  PV-YYMMDD-MM        PIC  X(02)        VALUE SPACE.   00940012
               10  PV-YYMMDD-DD        PIC  X(02)        VALUE SPACE.   00950012
           05  PV-TIME-HHMMSS.                                          00960012
               10  PV-HHMMSS-HH        PIC  X(02)        VALUE SPACE.   00970012
               10  PV-HHMMSS-MM        PIC  X(02)        VALUE SPACE.   00980012
               10  PV-HHMMSS-SS        PIC  X(02)        VALUE SPACE.   00990012
           05  PV-MSG-NBR              PIC S9(03).                      01000012
           05  PV-XM-VALUE             PIC  X(30).                      01010012
           05  PV-RMS-VALUE            PIC  X(30).                      01020012
           05  PV-XM-EXTRACT-REC-CNT   PIC S9(04)  COMP                 01030012
                                                   VALUE ZERO.          01040012
           05  PV-RMS-EXTR-REC-CNT     PIC S9(04)  COMP                 01050012
                                                   VALUE ZERO.          01060012
           05  PV-REPORT-CNT                 PIC S9(04)  COMP           01070012
                                                         VALUE ZERO.    01080012
           05  ABEND-CODE                    PIC S9(04) VALUE +0.       01090012
           05  PV-RETRY-COUNTER              PIC  9(01)  VALUE ZEROS.   01100012
           05  PV-PARAGRAPH                  PIC  X(50).                01110012
           05  PV-MESSAGE                    PIC  X(50).                01120012
           05  PV-OUTLOC-NBR                 PIC  9(04) VALUE 0.        01130012
           05  PV-REPLT-STR-NBR              PIC  9(04) VALUE 0.        01140012
           05  PV-DIST-NBR                   PIC  9(04) VALUE 0.        01150012
           05  PV-POS-RGST-BNK-QTY           PIC  9(03) VALUE 0.        01160012
           05  PV-FMT-CDE                    PIC  9(04) VALUE 0.        01170012
           05  PV-TOT-SLG-SQ-FTG-QTY         PIC  9(08) VALUE 0.        01180012
           05  PV-TOT-SQ-FTG-QTY             PIC  9(08) VALUE 0.        01190012
           05  PV-STP-ORD-DAYS-QTY           PIC  9(03) VALUE 0.        01200012
           05  PV-STRT-ORD-DAYS-QTY          PIC  9(03) VALUE 0.        01210012
           05  PV-MEDN-AGE-NBR               PIC  9(02)V9(02) VALUE 0.  01220012
           05  PV-EST-HH-INCM-AMT            PIC  9(11) VALUE 0.        01230012
                                                                        01240012
                                                                        01250012
       01  PS-PROGRAM-SWITCHES.                                         01260012
           05  PS-END-OF-RMS-FILE-SW PIC X   VALUE 'N'.                 01270012
               88 PS-END-OF-RMS-FILE         VALUE 'Y'.                 01280012
               88 PS-NOT-END-OF-RMS-FILE     VALUE 'N'.                 01290012
           05  PS-END-OF-XM-FILE-SW PIC X    VALUE 'N'.                 01300012
               88 PS-END-OF-XM-FILE          VALUE 'Y'.                 01310012
               88 PS-NOT-END-OF-XM-FILE      VALUE 'N'.                 01320012
                                                                        01330012
                                                                        01340012
      ******************************************************************01350012
      * STANDARD REPORT HEADING                                         01360012
      ******************************************************************01370012
                                                                        01380012
           COPY DPWS200O.                                               01390012
                                                                        01400012
       01  RPT-REPORT-HEADING.                                          01410012
           05  FILLER            PIC  X(83)    VALUE SPACES.            01420012
           05  FILLER            PIC  X(33)    VALUE                    01430012
             'XM OUTLOC DATA INTEGRITY ERRORS'.                         01440012
                                                                        01450012
       01  RPT-COL-HEADING-1.                                           01460012
           05  FILLER            PIC  X(01) VALUE SPACE.                01470012
           05  FILLER            PIC  X(17) VALUE ALL '-'.              01480012
           05  FILLER            PIC  X(05) VALUE ' KEY '.              01490012
           05  FILLER            PIC  X(18) VALUE ALL '-'.              01500012
                                                                        01510012
       01  RPT-COL-HEADING-2.                                           01520012
           05  FILLER            PIC  X(01) VALUE SPACE.                01530012
           05  FILLER            PIC  X(32) VALUE SPACE.                01540012
           05  FILLER            PIC  X(10) VALUE 'LOCATION'.           01550012
           05  FILLER            PIC  X(07) VALUE 'TYPE'.               01560012
           05  FILLER            PIC  X(38) VALUE 'XM DOMAIN'.          01570012
           05  FILLER            PIC  X(40) VALUE                       01580012
              'RETEK MANAGEMENT SYSTEM - RMS'.                          01590012
           05  FILLER            PIC  X(08) VALUE 'MESSAGE'.            01600012
                                                                        01610012
       01  RPT-COL-HEADING-3.                                           01620012
           05  FILLER            PIC  X(01) VALUE SPACE.                01630012
           05  FILLER            PIC  X(42) VALUE SPACE.                01640012
           05  FILLER            PIC  X(07) VALUE 'CODE'.               01650012
           05  FILLER            PIC  X(38) VALUE 'VALUE'.              01660012
           05  FILLER            PIC  X(10) VALUE 'VALUE'.              01670012
                                                                        01680012
       01  DL-DETAIL-LINE.                                              01690012
           05  FILLER            PIC  X(01) VALUE SPACE.                01700012
           05  FILLER            PIC  X(34) VALUE SPACE.                01710012
           05  DL-LOCATION       PIC  ZZZZZ.                            01720012
           05  FILLER            PIC  X(04) VALUE SPACE.                01730012
           05  DL-TYP-CDE        PIC  X(03).                            01740012
           05  FILLER            PIC  X(03) VALUE SPACE.                01750012
           05  DL-XM-VALUE       PIC  X(35).                            01760012
           05  FILLER            PIC  X(03) VALUE SPACE.                01770012
           05  DL-RMS-VALUE      PIC  X(35).                            01780012
           05  FILLER            PIC  X(03) VALUE SPACE.                01790012
           05  DL-MESSAGE        PIC  X(75).                            01800012
                                                                        01810012
       01  RPT-TOTAL-LINE.                                              01820012
           05  FILLER            PIC  X(23) VALUE                       01830012
                                              'TOTAL NUMBER OF ERRORS '.01840012
           05  RPT-NBR-ERRORS    PIC  ZZZZZZ9.                          01850012
                                                                        01860012
       01  RPT-END-OF-REPORT-LINE.                                      01870012
           05  FILLER            PIC  X(21) VALUE                       01880012
                                                '*** END OF REPORT ***'.01890012
                                                                        01900012
      ***************************************************************** 01910012
      * RECORD LAYOUT FOR RMS LOCATION EXTRACT FILE                     01920012
      ***************************************************************** 01930012
      *    COPY HORD007.                                                01940012
           COPY HORD015.                                                01950012
                                                                        01960012
      ***************************************************************** 01970012
      * RECORD LAYOUT FOR XMT_LOC EXTRACT FILE                          01980012
      * FILLER OF 01 BETWEEN COLUMNS INDICATES THAT THE PRIOR COLUMN    01990012
      * IS NULLABLE.                                                    02000012
      ***************************************************************** 02010012
0407PK     COPY XMRD016.                                                02703012
                                                                        02704012
      ***************************************************************** 02705012
      * COPYBOOK FOR THE LOCATION DATA INTEGRITY ERROR MESSAGE TABLE.   02706012
      ***************************************************************** 02707012
                                                                        02710012
           COPY XMWS001.                                                02720012
                                                                        02730012
                                                                        02740012
      *----------------------------------------------------------------*02750012
       PROCEDURE DIVISION.                                              02760012
      *----------------------------------------------------------------*02770012
                                                                        02780012
           OPEN INPUT  RMS-OUTLOC-EXTRACT-DATA-FILE                     02790012
                        XM-OUTLOC-EXTRACT-DATA-FILE                     02800012
                OUTPUT XM-OUTLOC-DATA-INTEGRITY-RPT.                    02810012
                                                                        02820012
           PERFORM 1000-INITIALIZE.                                     02830012
                                                                        02840012
           PERFORM 7000-READ-RMS-FILE.                                  02850012
           PERFORM 7100-READ-XM-FILE.                                   02860012
                                                                        02870012
                                                                        02880012
           PERFORM 2000-COMPARE-FILES                                   02890012
               UNTIL PS-END-OF-RMS-FILE AND                             02900012
                     PS-END-OF-XM-FILE.                                 02910012
                                                                        02920012
           PERFORM 8400-WRITE-TOTAL-LINE.                               02930012
                                                                        02940012
           CLOSE RMS-OUTLOC-EXTRACT-DATA-FILE                           02950012
                  XM-OUTLOC-EXTRACT-DATA-FILE                           02960012
                 XM-OUTLOC-DATA-INTEGRITY-RPT.                          02970012
                                                                        02980012
           DISPLAY '***** ' PC-PGM-NAME ' STATISTICS *****'.            02990012
           MOVE PV-XM-EXTRACT-REC-CNT TO PV-DISPLAY-CNT.                03000012
           DISPLAY '   ** XM EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.    03010012
           MOVE PV-RMS-EXTR-REC-CNT    TO PV-DISPLAY-CNT.               03020012
           DISPLAY '   ** RMS    EXTRACT RECORDS READ: ' PV-DISPLAY-CNT.03030012
           MOVE PV-NBR-ERRORS  TO PV-DISPLAY-CNT.                       03040012
                                                                        03050012
      *    IF THERE ARE ANY ERRORS TO BE REPORTED, ISSUE A              03060012
      *    RETURN CODE SO THAT AN E-MAIL WILL BE SENT IN A              03070012
      *    SUBSEQUENT JOB STEP.                                         03080012
                                                                        03090012
           IF PV-NBR-ERRORS > 0                                         03100012
               MOVE 4095 TO RETURN-CODE                                 03110012
           END-IF.                                                      03120012
                                                                        03130012
           DISPLAY '   ** ERRORS REPORTED: ' PV-DISPLAY-CNT.            03140012
           GOBACK.                                                      03150012
                                                                        03160012
                                                                        03170012
      ******************************************************************03180012
      * INITIALIZE AND WRITE REPORT HEADINGS                            03190012
      ******************************************************************03200012
       1000-INITIALIZE.                                                 03210012
                                                                        03220012
           ACCEPT PV-DATE-YYMMDD FROM DATE.                             03230012
           MOVE PV-YYMMDD-YY TO DP200O-RUN-YEAR.                        03240012
           MOVE PV-YYMMDD-MM TO DP200O-RUN-MONTH.                       03250012
           MOVE PV-YYMMDD-DD TO DP200O-RUN-DAY.                         03260012
                                                                        03270012
           ACCEPT PV-TIME-HHMMSS FROM TIME.                             03280012
           MOVE PV-HHMMSS-HH TO DP200O-RUN-HOUR.                        03290012
           MOVE PV-HHMMSS-MM TO DP200O-RUN-MINUTE.                      03300012
           MOVE PC-PGM-NAME  TO DP200O-PROGRAM-NAME.                    03310012
           MOVE 1            TO DP200O-REPORT-NUMBER.                   03320012
                                                                        03330012
           PERFORM 8100-WRITE-STD-RPT-HEADING.                          03340012
           PERFORM 8200-WRITE-REPORT-HEADING.                           03350012
           PERFORM 8300-WRITE-COLUMN-HEADING.                           03360012
                                                                        03370012
      ******************************************************************03380012
      * COMPARE RMS DATA TO XM DATA                                     03390012
      ******************************************************************03400012
       2000-COMPARE-FILES.                                              03410012
                                                                        03420012
           MOVE SPACES       TO PV-XM-VALUE                             03430012
                                PV-RMS-VALUE.                           03440012
                                                                        03450012
           IF PV-OUTLOC-NBR = HO015-OUTLC-ID-2-5                        03460012
      *****    LOCATION IS ON BOTH XM AND RMS.  *****                   03470012
               PERFORM 2100-COMPARE-FIELDS                              03480012
               PERFORM 7000-READ-RMS-FILE                               03490012
               PERFORM 7100-READ-XM-FILE                                03500012
           ELSE                                                         03510012
               IF PV-OUTLOC-NBR < HO015-OUTLC-ID-2-5                    03520012
      *****        LOCATION IS ON XM ONLY.      *****                   03530012
                   MOVE 26 TO PV-MSG-NBR                                03540012
                   PERFORM 3200-WRITE-REPORT-REC-XM                     03550012
                   PERFORM 7100-READ-XM-FILE                            03560012
              ELSE                                                      03570012
      *****        LOCATION IS ON RMS ONLY.     *****                   03580012
                   MOVE 27 TO PV-MSG-NBR                                03590012
                   PERFORM 3100-WRITE-REPORT-REC-RMS                    03600012
                   PERFORM 7000-READ-RMS-FILE                           03610012
              END-IF                                                    03620012
           END-IF.                                                      03630012
                                                                        03640012
      ******************************************************************03650012
      * COMPARE RMS DATA TO XM DATA FIELD BY FIELD                      03660012
      ******************************************************************03670012
      * DUE TO NULL DATA, NEED TO CHECK FOR THE POSSIBILITY THAT THE    03680012
0407PK* RMS FILE HAS SPACES WHEN THE XM EXTRACT HAS LOW VALUES. THIS    03690014
      * IS OK AND SHOULD NOT BE REGARDED AS AN ERROR.                   03700012
      * ALSO IT IS POSSIBLE THAT FOR DATE FIELDS, XM EXTRACT WILL HAVE  03710012
0407PK* LOW VALUES WHEN RMS HAS 9999-09-09. THIS IS ALSO OK AND         03720014
      * SHOULD NOT BE REGARDED AS AN ERROR.                             03730012
      ******************************************************************03740012
       2100-COMPARE-FIELDS.                                             03750012
                                                                        03751014
           IF (HO015-OTLC-TYPE   NOT = XM016-LOC-TYP-CDE                03752014
               AND NOT (HO015-OTLC-TYPE       = SPACES                  03753014
                        AND XM016-LOC-TYP-CDE = LOW-VALUES))            03754014
               MOVE HO015-OTLC-TYPE      TO PV-RMS-VALUE                03755014
               MOVE XM016-LOC-TYP-CDE    TO PV-XM-VALUE                 03756014
               MOVE 3                    TO PV-MSG-NBR                  03757014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03758014
           END-IF.                                                      03759014
           IF (HO015-OTLC-DESC   NOT = XM016-LOC-NM                     03760014
               AND NOT (HO015-OTLC-DESC    = SPACES                     03770014
                        AND XM016-LOC-NM   = LOW-VALUES))               03780014
               MOVE HO015-OTLC-DESC       TO PV-RMS-VALUE               03790014
               MOVE XM016-LOC-NM          TO PV-XM-VALUE                03800014
               MOVE 4                     TO PV-MSG-NBR                 03810014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03820014
           END-IF.                                                      03830014
           IF (HO015-OTLC-LOC-5-ABBR-NM NOT = XM016-LOC-5-ABBR-NM       03840014
               AND NOT (HO015-OTLC-LOC-5-ABBR-NM = SPACES               03850014
                        AND XM016-LOC-5-ABBR-NM  = LOW-VALUES))         03860014
               MOVE HO015-OTLC-LOC-5-ABBR-NM TO PV-RMS-VALUE            03870014
               MOVE XM016-LOC-5-ABBR-NM      TO PV-XM-VALUE             03880014
               MOVE 5                        TO PV-MSG-NBR              03890014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03900014
           END-IF.                                                      03901014
           IF (HO015-OTLC-LNE-1-ADDR NOT = XM016-LNE-1-ADDR             03902014
               AND NOT (HO015-OTLC-LNE-1-ADDR = SPACES                  03903014
                        AND XM016-LNE-1-ADDR  = LOW-VALUES))            03904014
               MOVE HO015-OTLC-LNE-1-ADDR   TO PV-RMS-VALUE             03905014
               MOVE XM016-LNE-1-ADDR        TO PV-XM-VALUE              03906014
               MOVE 6                       TO PV-MSG-NBR               03907014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03908014
           END-IF.                                                      03909014
           IF (HO015-OTLC-LNE-2-ADDR NOT = XM016-LNE-2-ADDR             03910014
               AND NOT (HO015-OTLC-LNE-2-ADDR = SPACES                  03920014
                        AND XM016-LNE-2-ADDR  = LOW-VALUES))            03930014
               MOVE HO015-OTLC-LNE-2-ADDR   TO PV-RMS-VALUE             03940014
               MOVE XM016-LNE-2-ADDR        TO PV-XM-VALUE              03950014
               MOVE 7                       TO PV-MSG-NBR               03960014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       03970014
           END-IF.                                                      03980014
           IF (HO015-OTLC-CTY-NM    NOT = XM016-CTY-NM                  03990014
               AND NOT (HO015-OTLC-CTY-NM  = SPACES                     04000014
                        AND XM016-CTY-NM   = LOW-VALUES))               04010014
               MOVE HO015-OTLC-CTY-NM     TO PV-RMS-VALUE               04020014
               MOVE XM016-CTY-NM          TO PV-XM-VALUE                04030014
               MOVE 8                     TO PV-MSG-NBR                 04040014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04050014
           END-IF.                                                      04060014
           IF (HO015-OTLC-ST-CDE    NOT = XM016-ST-CDE                  04070014
               AND NOT (HO015-OTLC-ST-CDE  = SPACES                     04080014
                        AND XM016-ST-CDE   = LOW-VALUES))               04090014
               MOVE HO015-OTLC-ST-CDE     TO PV-RMS-VALUE               04100014
               MOVE XM016-ST-CDE          TO PV-XM-VALUE                04110014
               MOVE 9                     TO PV-MSG-NBR                 04120014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04130014
           END-IF.                                                      04140014
           IF (HO015-OTLC-ZIP-10-CDE    NOT = XM016-ZIP-10-CDE          04150014
               AND NOT (HO015-OTLC-ZIP-10-CDE = SPACES                  04160014
                        AND XM016-ZIP-10-CDE = LOW-VALUES))             04170014
               MOVE HO015-OTLC-ZIP-10-CDE    TO PV-RMS-VALUE            04180014
               MOVE XM016-ZIP-10-CDE         TO PV-XM-VALUE             04190014
               MOVE 10                       TO PV-MSG-NBR              04200014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04210014
           END-IF.                                                      04220014
           IF (HO015-OTLC-OPN-DTE         NOT = XM016-OPN-DTE           04230014
               AND NOT (HO015-OTLC-OPN-DTE    = '9999-09-09'            04240014
                        AND XM016-OPN-DTE     = LOW-VALUES))            04250014
               MOVE HO015-OTLC-OPN-DTE       TO PV-RMS-VALUE            04260014
               MOVE XM016-OPN-DTE            TO PV-XM-VALUE             04270014
               MOVE 11                       TO PV-MSG-NBR              04280014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04290014
           END-IF.                                                      04300014
           IF (HO015-OTLC-CLO-DTE        NOT = XM016-CLO-DTE            04310014
               AND NOT (HO015-OTLC-CLO-DTE   = '9999-09-09'             04320014
                        AND XM016-CLO-DTE    = LOW-VALUES))             04330014
               MOVE HO015-OTLC-CLO-DTE      TO PV-RMS-VALUE             04340014
               MOVE XM016-CLO-DTE           TO PV-XM-VALUE              04350014
               MOVE 12                      TO PV-MSG-NBR               04360014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04370014
           END-IF.                                                      04380014
           IF (HO015-OTLC-PHN-NBR          NOT = XM016-PHN-NBR          04390014
               AND NOT (HO015-OTLC-PHN-NBR = SPACES                     04400014
                        AND XM016-PHN-NBR      = LOW-VALUES))           04410014
               MOVE HO015-OTLC-PHN-NBR        TO PV-RMS-VALUE           04420014
               MOVE XM016-PHN-NBR             TO PV-XM-VALUE            04430014
               MOVE 14                        TO PV-MSG-NBR             04440014
               PERFORM 3000-WRITE-REPORT-REC-BOTH                       04450014
           END-IF.                                                      04460014
                                                                        04580012
                                                                        04590012
      ******************************************************************04600012
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON BOTH RMS AND XM.    04610012
      ******************************************************************04620012
       3000-WRITE-REPORT-REC-BOTH.                                      04630012
                                                                        04640012
           MOVE HO015-OUTLC-ID-2-5   TO DL-LOCATION.                    04650012
           MOVE HO015-OTLC-TYPE      TO DL-TYP-CDE.                     04660012
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    04670012
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   04680012
           SET  XMEM-IDX             TO PV-MSG-NBR.                     04690012
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 04700012
                                                                        04710012
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            04720012
               PERFORM 8100-WRITE-STD-RPT-HEADING                       04730012
               PERFORM 8200-WRITE-REPORT-HEADING                        04740012
               PERFORM 8300-WRITE-COLUMN-HEADING                        04750012
           END-IF.                                                      04760012
                                                                        04770012
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          04780012
                                                                        04790012
      ******************************************************************04800012
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON RMS ONLY.           04810012
      ******************************************************************04820012
       3100-WRITE-REPORT-REC-RMS.                                       04830012
                                                                        04840012
           MOVE HO015-OUTLC-ID-2-5   TO DL-LOCATION.                    04850012
           MOVE HO015-OTLC-TYPE      TO DL-TYP-CDE.                     04860012
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    04870012
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   04880012
           SET  XMEM-IDX             TO PV-MSG-NBR.                     04890012
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 04900012
                                                                        04910012
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            04920012
               PERFORM 8100-WRITE-STD-RPT-HEADING                       04930012
               PERFORM 8200-WRITE-REPORT-HEADING                        04940012
               PERFORM 8300-WRITE-COLUMN-HEADING                        04950012
           END-IF.                                                      04960012
                                                                        04970012
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          04980012
                                                                        04990012
      ******************************************************************05000012
      * CREATE A REPORT RECORD FOR A KEY THAT IS ON XM ONLY.            05010012
      ******************************************************************05020012
       3200-WRITE-REPORT-REC-XM.                                        05030012
                                                                        05040012
           MOVE XM016-LOC-NBR        TO DL-LOCATION.                    05050012
           MOVE HO015-OTLC-TYPE      TO DL-TYP-CDE.                     05060012
           MOVE PV-XM-VALUE          TO DL-XM-VALUE.                    05070012
           MOVE PV-RMS-VALUE         TO DL-RMS-VALUE.                   05080012
           SET  XMEM-IDX             TO PV-MSG-NBR.                     05090012
           MOVE XMEM-MSG-TEXT (XMEM-IDX) TO DL-MESSAGE.                 05100012
                                                                        05110012
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            05120012
               PERFORM 8100-WRITE-STD-RPT-HEADING                       05130012
               PERFORM 8200-WRITE-REPORT-HEADING                        05140012
               PERFORM 8300-WRITE-COLUMN-HEADING                        05150012
           END-IF.                                                      05160012
                                                                        05170012
           PERFORM 8000-WRITE-RPT-DETAIL-LINE.                          05180012
                                                                        05190012
      ******************************************************************05200012
      * READ RMS INPUT FILE                                             05210012
      ******************************************************************05220012
       7000-READ-RMS-FILE.                                              05230012
           IF PS-NOT-END-OF-RMS-FILE                                    05240012
             READ RMS-OUTLOC-EXTRACT-DATA-FILE                          05250012
               INTO HO015-OUTLOC-DATA                                   05260012
                 AT END SET PS-END-OF-RMS-FILE TO TRUE                  05270012
                 MOVE 9999 TO HO015-OUTLC-ID-2-5                        05280012
           END-IF.                                                      05290012
           IF PS-NOT-END-OF-RMS-FILE                                    05300012
              ADD +1 TO PV-RMS-EXTR-REC-CNT                             05310012
           END-IF.                                                      05320012
                                                                        05330012
      ******************************************************************05340012
      * READ XM INPUT FILE                                              05350012
      ******************************************************************05360012
       7100-READ-XM-FILE.                                               05370012
           IF PS-NOT-END-OF-XM-FILE                                     05380012
             READ XM-OUTLOC-EXTRACT-DATA-FILE                           05390012
               INTO XM016-LOC-UNLOAD                                    05400012
                 AT END SET PS-END-OF-XM-FILE TO TRUE                   05410012
                 MOVE 9999 TO PV-OUTLOC-NBR                             05420012
           END-IF.                                                      05430012
           IF PS-NOT-END-OF-XM-FILE                                     05440012
              ADD +1 TO PV-XM-EXTRACT-REC-CNT                           05450012
              MOVE XM016-LOC-NBR TO PV-OUTLOC-NBR                       05460012
           END-IF.                                                      05470012
                                                                        05480012
      ******************************************************************05490012
      * WRITE REPORT DETAIL LINE                                        05500012
      ******************************************************************05510012
       8000-WRITE-RPT-DETAIL-LINE.                                      05520012
                                                                        05530012
           WRITE XM-OUTLOC-RPT-LINE FROM DL-DETAIL-LINE                 05540012
               AFTER ADVANCING PV-ADVANCE.                              05550012
           ADD PV-ADVANCE TO PV-LINE-CNT.                               05560012
           MOVE +1        TO PV-ADVANCE.                                05570012
           ADD +1 TO PV-NBR-ERRORS.                                     05580012
                                                                        05590012
                                                                        05600012
      ******************************************************************05610012
      * PRINT THE KOHLS STANDARD REPORT HEADING.                        05620012
      ******************************************************************05630012
       8100-WRITE-STD-RPT-HEADING.                                      05640012
                                                                        05650012
           ADD 1            TO PV-PAGE-CNT.                             05660012
           MOVE PV-PAGE-CNT TO DP200O-PAGE-NUMBER.                      05670012
           WRITE XM-OUTLOC-RPT-LINE FROM DP200O-STANDRD-HEADER-200-COLS 05680012
               AFTER ADVANCING PAGE.                                    05690012
           MOVE +1          TO PV-LINE-CNT.                             05700012
                                                                        05710012
                                                                        05720012
      ******************************************************************05730012
      * REPORT HEADING                                                  05740012
      ******************************************************************05750012
       8200-WRITE-REPORT-HEADING.                                       05760012
                                                                        05770012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-REPORT-HEADING             05780012
               AFTER ADVANCING 1 LINES.                                 05790012
           ADD +1 TO PV-LINE-CNT.                                       05800012
                                                                        05810012
                                                                        05820012
      ******************************************************************05830012
      * REPORT COLUMN HEADING                                           05840012
      ******************************************************************05850012
       8300-WRITE-COLUMN-HEADING.                                       05860012
                                                                        05870012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-COL-HEADING-1              05880012
               AFTER ADVANCING 2 LINES.                                 05890012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-COL-HEADING-2              05900012
               AFTER ADVANCING 1 LINES.                                 05910012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-COL-HEADING-3              05920012
               AFTER ADVANCING 1 LINES.                                 05930012
           ADD  +4 TO PV-LINE-CNT.                                      05940012
           MOVE +2 TO PV-ADVANCE.                                       05950012
                                                                        05960012
                                                                        05970012
      ******************************************************************05980012
      * REPORT TOTAL LINE                                               05990012
      ******************************************************************06000012
       8400-WRITE-TOTAL-LINE.                                           06010012
                                                                        06020012
           MOVE PV-NBR-ERRORS TO RPT-NBR-ERRORS.                        06030012
                                                                        06040012
           IF PV-LINE-CNT > PC-MAX-DTL-LINES                            06050012
               PERFORM 8100-WRITE-STD-RPT-HEADING                       06060012
               PERFORM 8200-WRITE-REPORT-HEADING                        06070012
               PERFORM 8300-WRITE-COLUMN-HEADING                        06080012
           END-IF.                                                      06090012
                                                                        06100012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-TOTAL-LINE                 06110012
               AFTER ADVANCING 2 LINES.                                 06120012
           ADD +2 TO PV-LINE-CNT.                                       06130012
                                                                        06140012
           WRITE XM-OUTLOC-RPT-LINE FROM RPT-END-OF-REPORT-LINE         06150012
               AFTER ADVANCING 2 LINES.                                 06160012
                                                                        06170012
