       IDENTIFICATION DIVISION.                                         00010004
       PROGRAM-ID.      XSKUT010.                                       00020036
       AUTHOR.          KOHLS DEPARTMENT STORES.                        00030005
       INSTALLATION.    KOHLS DEPARTMENT STORES.                        00040004
       DATE-WRITTEN.    09/16/2002.                                     00050036
       DATE-COMPILED.                                                   00060004
      ******************************************************************00070007
      **XS MQ TABLE LOAD - MESSAGE LOAD FOR TABLES                      00080036
      **                       XST_VND_STYL                             00090021
      **                       XST_SKU                                  00100021
      **                       XST_UPC                                  00110021
      ******************************************************************00120007
      **THIS PROGRAM TAKES MESSAGES FROM A FLAT FILE AND PUTS THEM IN   00130036
      **THE VSM MESSAGE QUEUE FOR PROCESSING.  THIS PROGRAM CAN BE USED 00140036
      **FOR TESTING AND TO RELOAD MESSAGES WRITTEN TO A FLAT FILE.      00150036
      ******************************************************************00160007
      *DATE:09/16/2002   MADE BY:JULIE SMITH INITIAL PROGRAM WRITE      00170036
      *DATE:11/05/2002   MADE BY:JULIE SMITH CHANGES PER LYNN KELLING   00180052
      ******************************************************************00190014
       ENVIRONMENT DIVISION.                                            00200004
       CONFIGURATION SECTION.                                           00210004
       SOURCE-COMPUTER.        IBM-3090.                                00220004
       OBJECT-COMPUTER.        IBM-3090.                                00230004
       INPUT-OUTPUT SECTION.                                            00240004
       FILE-CONTROL.                                                    00250004
                                                                        00260004
           SELECT CONTROL-CARD-1                                        00270021
                  ASSIGN TO INP01.                                      00280007
           SELECT CONTROL-CARD-2                                        00290021
                  ASSIGN TO INP02.                                      00300021
           SELECT MESSAGE-FILE                                          00310028
                  ASSIGN TO INP03.                                      00320049
                                                                        00330004
       DATA DIVISION.                                                   00340004
       FILE SECTION.                                                    00350004
                                                                        00360004
       FD  CONTROL-CARD-1                                               00370021
           RECORDING MODE F                                             00380007
           BLOCK CONTAINS 0 RECORDS                                     00390007
           LABEL RECORDS ARE OMITTED                                    00400007
           DATA RECORD IS CONTROL-CARD-1-RECORD.                        00410021
       01  CONTROL-CARD-1-RECORD           PIC X(080).                  00420022
                                                                        00430004
       FD  CONTROL-CARD-2                                               00440022
           RECORDING MODE F                                             00450022
           BLOCK CONTAINS 0 RECORDS                                     00460022
           LABEL RECORDS ARE OMITTED                                    00470022
           DATA RECORD IS CONTROL-CARD-2-RECORD.                        00480022
       01  CONTROL-CARD-2-RECORD           PIC X(080).                  00490022
                                                                        00500022
       FD  MESSAGE-FILE                                                 00510028
           RECORDING MODE F                                             00520022
           BLOCK CONTAINS 0 RECORDS                                     00530022
           LABEL RECORDS ARE OMITTED                                    00540022
           DATA RECORD IS MESSAGE-FILE-RECORD.                          00550028
       01  MESSAGE-FILE-RECORD             PIC X(450).                  00560048
                                                                        00570022
       WORKING-STORAGE SECTION.                                         00580004
      ******************************************************************00590007
      **PROGRAM SWITCHES                                                00600024
      ******************************************************************00610007
       01  PS-PROGRAM-SWITCHES.                                         00620024
           05  PS-END-OF-MSGS-ON-Q-SW          PIC X(01)   VALUE 'Y'.   00630024
               88  PS-NO-MSGS                              VALUE 'N'.   00640030
               88  PS-MORE-MSGS                            VALUE 'Y'.   00650030
      ******************************************************************00660023
      **CONTROL CARD 1 RECORD LAYOUT - QUEUE MANAGER NAME               00670023
      ******************************************************************00680023
       01  CC-CONTROL-CARD-1-LAYOUT.                                    00690022
           05  CC-QUEUE-MANAGER-NAME      PIC X(48)   VALUE SPACES.     00700022
           05  FILLER                     PIC X(32)   VALUE SPACES.     00710022
      ******************************************************************00720022
      **CONTROL CARD 2 RECORD LAYOUT - ITEM QUEUE NAME                  00730022
      ******************************************************************00740022
       01  CC-CONTROL-CARD-2-LAYOUT.                                    00750022
           05  CC-ITEM-QUEUE-NAME         PIC X(48)   VALUE SPACES.     00760022
           05  FILLER                     PIC X(32)   VALUE SPACES.     00770022
      ******************************************************************00780022
      **PROGRAM CONSTANTS                                               00790022
      ******************************************************************00800022
       01  PC-PROGRAM-CONSTANTS.                                        00810022
           05  PC-CURRENT-PROGRAM-NAME    PIC X(08)   VALUE 'XSKUT010'. 00820036
           05  PC-CALLED-PROGRAM          PIC X(08).                    00830027
               88  PC-CALLED-XSKUP005                 VALUE 'XSKUP005'. 00840029
      ******************************************************************00850036
      **TIMESTAMP VARIABLES                                             00860036
      ******************************************************************00870036
       01  SD-TIMESTAMP                        PIC  X(26)  VALUE SPACES.00880036
       01  FILLER REDEFINES SD-TIMESTAMP.                               00890036
           05  SD-CURR-DTE                     PIC  X(10).              00900036
           05  FILLER                          PIC  X(01).              00910036
           05  SD-HOUR                         PIC  X(02).              00920036
           05  FILLER                          PIC  X(01).              00930036
           05  SD-MINUTE                       PIC  X(02).              00940036
           05  FILLER                          PIC  X(01).              00950036
           05  SD-SECOND                       PIC  X(02).              00960036
           05  FILLER                          PIC  X(01).              00970036
           05  SD-MICROSEC                     PIC  X(06).              00980036
      ******************************************************************00990022
      **MQ WORKING STORAGE                                              01000022
      ******************************************************************01010022
       01  MQV-WORKING-STORAGE.                                         01020040
           05  MQV-MQCONN                 PIC  X(08) VALUE 'CSQBCONN'.  01030040
           05  MQV-MQOPEN                 PIC  X(08) VALUE 'CSQBOPEN'.  01040040
           05  MQV-MQINQ                  PIC  X(08) VALUE 'CSQBINQ'.   01050040
           05  MQV-MQPUT                  PIC  X(08) VALUE 'CSQBPUT'.   01060040
           05  MQV-MQGET                  PIC  X(08) VALUE 'CSQBGET'.   01070040
           05  MQV-MQCMIT                 PIC  X(08) VALUE 'CSQBCOMM'.  01080040
           05  MQV-MQBACK                 PIC  X(08) VALUE 'CSQBBACK'.  01090040
           05  MQV-MQCLOSE                PIC  X(08) VALUE 'CSQBCLOS'.  01100040
           05  MQV-MQDISC                 PIC  X(08) VALUE 'CSQBDISC'.  01110040
           05  MQV-MQCHECK                PIC  X(08) VALUE 'MQR2C'.     01120040
           05  MQV-HCONN                  PIC S9(09) BINARY VALUE 0.    01130040
           05  MQV-COMPLETION-CODE        PIC S9(09) BINARY.            01140040
           05  MQV-REASON                 PIC S9(09) BINARY.            01150040
           05  MQV-OPEN-OPTIONS           PIC S9(09) BINARY.            01160040
           05  MQV-GQ-HANDLE              PIC S9(09) BINARY VALUE 0.    01170040
           05  MQV-PQ-HANDLE              PIC S9(09) BINARY VALUE 0.    01180040
           05  MQV-REASON-TEXT            PIC X(30)  VALUE SPACES.      01190041
           05  MQV-MSGBUFFER              PIC X(450) VALUE SPACES.      01200040
           05  MQV-MSG                    PIC X(450) VALUE SPACES.      01210040
           05  MQV-ERRMSGBUF              PIC X(500) VALUE SPACES.      01220040
           05  MQV-MESSAGE-BUFFER         PIC X(500) VALUE SPACES.      01230040
           05  MQV-MSG-LENGTH             PIC S9(09) BINARY VALUE +450. 01240049
           05  MQV-DATA-LENGTH            PIC S9(09) BINARY.            01250040
      ******************************************************************01260004
      **COPYBOOKS                                                       01270007
      ******************************************************************01280004
      ***INPUT FILE LAYOUT - VSM ITEM MESSAGE COPYBOOK                  01290022
           COPY IMRD008.                                                01300022
      ***VARIABLES REQUIRED FOR THE FORMAT SQLCA ROUTINE DPPD004        01310007
           COPY DPWS004.                                                01320004
      ***DB2 ABEND COPYBOOK                                             01330007
           COPY DPWS900.                                                01340007
      ***DB2 MESSAGE AREA                                               01350007
           COPY SQLCA2.                                                 01360018
      ***OBJECT-DESCRIPTOR.                                             01370022
       01  MQM-OBJECT-DESCRIPTOR.                                       01380022
           COPY CMQODV.                                                 01390022
      ***MESSAGE-DESCRIPTOR.                                            01400022
       01  MQM-MESSAGE-DESCRIPTOR.                                      01410022
           COPY CMQMDV.                                                 01420022
      ***GET-MESSAGE-OPTIONS.                                           01430022
       01  MQM-GET-MESSAGE-OPTIONS.                                     01440022
           COPY CMQGMOV.                                                01450022
      ***PUT-MESSAGE-OPTIONS.                                           01460022
       01  MQM-PUT-MESSAGE-OPTIONS.                                     01470022
           COPY CMQPMOV.                                                01480022
      ***CONSTANTS.                                                     01490022
       01  MQM-CONSTANTS.                                               01500022
           COPY CMQV.                                                   01510022
      ******************************************************************01520028
      **PROGRAM COUNTERS                                                01530036
      ******************************************************************01540028
       01  PC-PROGRAM-COUNTERS.                                         01550028
           05  PC-MESSAGES-PUT-ON-QUEUE     PIC 9(05) VALUE ZEROS.      01560036
           05  PC-MSG-FILE-READ             PIC 9(05) VALUE ZEROS.      01570036
           05  PC-COMMIT-COUNTER            PIC 9(05) VALUE ZEROS.      01580040
      ******************************************************************01590024
      **ERROR HANDLING                                                  01600024
      ******************************************************************01610024
       01  PV-MISC-ABEND-FIELDS.                                        01620007
           05  PV-PARAGRAPH-NAME            PIC X(30) VALUE SPACE.      01630007
           05  PV-OPERATION-MSG-1           PIC X(40) VALUE SPACE.      01640007
           05  PV-OPERATION-MSG-2           PIC X(40) VALUE SPACE.      01650007
           05  PV-DB2-OPERATION             PIC X(30) VALUE SPACE.      01660007
       01  ABEND-CODE                  PIC S9(4) VALUE ZEROS COMP SYNC. 01670007
           88  ABEND-4001-WRITE-ERROR            VALUE +4001.           01680007
           88  ABEND-4002-READ-ERROR             VALUE +4002.           01690007
           88  ABEND-4003-CTRL-CARD-ERROR        VALUE +4003.           01700007
           88  ABEND-4004-TABLE-ERROR            VALUE +4004.           01710007
           88  ABEND-4005-OPEN-ERROR             VALUE +4005.           01720007
           88  ABEND-4006-CLOSE-ERROR            VALUE +4006.           01730007
           88  ABEND-4007-SEQUENCE-ERROR         VALUE +4007.           01740007
           88  ABEND-4008-DATA-ERROR             VALUE +4008.           01750007
           88  ABEND-4009-KEY-DATA-ERROR         VALUE +4009.           01760007
           88  ABEND-4013-DB2-ERROR              VALUE +4013.           01790007
           88  ABEND-4019-CAL-SUB-ERROR          VALUE +4019.           01800007
           88  ABEND-4444-FORCED-ABEND           VALUE +4444.           01810007
      ******************************************************************01820026
      **TABLE INCLUSIONS                                                01830026
      ******************************************************************01840026
      ***DB2 COMMUNICATIONS AREA                                        01850026
           EXEC SQL                                                     01860026
                INCLUDE SQLCA                                           01870026
           END-EXEC.                                                    01880026
                                                                        01890036
       PROCEDURE DIVISION.                                              01900004
      ***************************************************************** 01910004
       0000-MAINLINE.                                                   01920045
                                                                        01930004
           PERFORM 1000-INITIALIZATION.                                 01940007
           PERFORM 2000-PROCESS-RECORDS                                 01950024
                   UNTIL PS-NO-MSGS.                                    01960030
           PERFORM 9999-WRAPUP.                                         01970007
           GOBACK.                                                      01980004
                                                                        01990004
       EJECT.                                                           02000007
      ******************************************************************02010004
      **INITIALIZATION AREA                                             02020007
      ******************************************************************02030004
       1000-INITIALIZATION.                                             02040007
                                                                        02050004
           MOVE '1000-INITIALIZATION' TO PV-PARAGRAPH-NAME.             02060007
                                                                        02070007
           OPEN INPUT  CONTROL-CARD-1                                   02080022
                       CONTROL-CARD-2                                   02090022
                       MESSAGE-FILE.                                    02100036
                                                                        02110007
           PERFORM 9999-READ-CONTROL-CARD-1.                            02120022
           PERFORM 9999-READ-CONTROL-CARD-2.                            02130022
                                                                        02140022
           IF  CC-QUEUE-MANAGER-NAME    =  SPACES                       02150022
            OR CC-ITEM-QUEUE-NAME       =  SPACES                       02160022
               DISPLAY 'QUEUE MANAGER NAME    ' CC-QUEUE-MANAGER-NAME   02170022
               DISPLAY 'ITEM QUEUE NAME       ' CC-ITEM-QUEUE-NAME      02180022
               PERFORM 9999-MQ-ABEND                                    02190040
           END-IF.                                                      02200022
                                                                        02210022
           PERFORM 1100-RETRIEVE-DB2-TIME.                              02220036
           PERFORM 1200-CONNECT-TO-QMGR.                                02230036
           PERFORM 1300-OPEN-ITEM-QUEUE.                                02240036
                                                                        02250024
           PERFORM 9999-READ-MSG-FILE.                                  02260036
                                                                        02270022
       EJECT.                                                           02280007
      ******************************************************************02290036
      **RETRIEVE TIME FROM DB2                                          02300036
      ******************************************************************02310036
       1100-RETRIEVE-DB2-TIME.                                          02320037
                                                                        02330036
           MOVE '1100-SELECT-DB2-TIME' TO PV-PARAGRAPH-NAME.            02340036
                                                                        02350036
           EXEC SQL                                                     02360036
                SELECT CURRENT TIMESTAMP                                02370036
                INTO  :SD-TIMESTAMP                                     02380036
                FROM   SYSIBM.SYSDUMMY1                                 02390036
           END-EXEC.                                                    02400036
                                                                        02410036
           EVALUATE SQLCODE                                             02420037
              WHEN ZERO                                                 02430037
                   CONTINUE                                             02440037
              WHEN OTHER                                                02450037
                   PERFORM 9999-SQL-ABEND-ROUTINE                       02460041
           END-EVALUATE.                                                02470037
                                                                        02480036
       EJECT.                                                           02490036
      ******************************************************************02500007
      **CONNECT TO THE QUEUE MANAGER                                    02510022
      ******************************************************************02520007
       1200-CONNECT-TO-QMGR.                                            02530036
                                                                        02540007
           MOVE '1200-CONNECT-TO-QMGR' TO PV-PARAGRAPH-NAME.            02550036
                                                                        02560022
           CALL MQV-MQCONN USING CC-QUEUE-MANAGER-NAME                  02570040
                                 MQV-HCONN                              02580040
                                 MQV-COMPLETION-CODE                    02590040
                                 MQV-REASON.                            02600040
                                                                        02610022
           EVALUATE TRUE                                                02620022
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        02630040
                   CONTINUE                                             02640041
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                02650041
                   CALL MQV-MQCHECK USING MQV-REASON                    02660040
                                          MQV-REASON-TEXT               02670040
                   DISPLAY 'QUEUE CONNECTING TO ' CC-QUEUE-MANAGER-NAME 02680023
                   MOVE 'UNABLE TO CONNECT' TO PV-OPERATION-MSG-1       02690023
                   PERFORM 9999-MQ-ABEND                                02700040
           END-EVALUATE.                                                02710022
                                                                        02720022
       EJECT.                                                           02730022
      ******************************************************************02740022
      **OPEN THE QUEUE                                                  02750022
      ******************************************************************02760022
       1300-OPEN-ITEM-QUEUE.                                            02770036
                                                                        02780022
           MOVE '1300-OPEN-ITEM-QUEUE' TO PV-PARAGRAPH-NAME.            02790036
                                                                        02800022
           MOVE CC-QUEUE-MANAGER-NAME    TO MQOD-OBJECTQMGRNAME.        02810041
           MOVE CC-ITEM-QUEUE-NAME       TO MQOD-OBJECTNAME.            02820041
                                                                        02830022
           COMPUTE MQV-OPEN-OPTIONS      = MQOO-OUTPUT                  02840041
                                         + MQOO-FAIL-IF-QUIESCING.      02850052
110502*                                  + MQOO-SET-IDENTITY-CONTEXT.   02860052
                                                                        02870022
           CALL MQV-MQOPEN USING MQV-HCONN                              02880040
                                 MQM-OBJECT-DESCRIPTOR                  02890040
                                 MQV-OPEN-OPTIONS                       02900040
                                 MQV-PQ-HANDLE                          02910046
                                 MQV-COMPLETION-CODE                    02920040
                                 MQV-REASON.                            02930040
                                                                        02940022
           EVALUATE TRUE                                                02950022
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        02960040
                   CONTINUE                                             02970041
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                02980041
                   CALL MQV-MQCHECK USING MQV-REASON                    02990040
                                          MQV-REASON-TEXT               03000040
                   DISPLAY 'OPENING QUEUE ' CC-QUEUE-MANAGER-NAME       03010023
                   MOVE 'CANNOT OPEN QUEUE'  TO PV-OPERATION-MSG-1      03020023
                   PERFORM 9999-DISCONNECT-QUEUE                        03030043
                   PERFORM 9999-MQ-ABEND                                03040040
           END-EVALUATE.                                                03050022
                                                                        03060022
       EJECT.                                                           03070022
      ******************************************************************03080022
      **PROCESSES INPUT FILE                                            03090022
      ******************************************************************03100022
       2000-PROCESS-RECORDS.                                            03110022
                                                                        03120022
           MOVE '2000-PROCESS-RECORDS' TO PV-PARAGRAPH-NAME.            03130036
                                                                        03140036
           PERFORM 2100-PUT-MESSAGE-QUEUE.                              03150036
                                                                        03160052
           IF  PC-COMMIT-COUNTER > 100                                  03170052
               PERFORM 2200-MQV-COMMIT                                  03180052
               INITIALIZE PC-COMMIT-COUNTER                             03190052
           END-IF.                                                      03200052
                                                                        03210036
           INITIALIZE IMRD008-RECORD.                                   03220052
                                                                        03230052
           PERFORM 9999-READ-MSG-FILE.                                  03240052
                                                                        03250036
       EJECT.                                                           03260036
      ******************************************************************03270036
      **PUT MESSAGE ON QUEUE FOR PROCESSING                             03280040
      ******************************************************************03290036
       2100-PUT-MESSAGE-QUEUE.                                          03300036
                                                                        03310036
           MOVE '2100-PUT-MESSAGE-QUEUE' TO PV-PARAGRAPH-NAME.          03320036
                                                                        03330036
           MOVE CC-QUEUE-MANAGER-NAME    TO MQOD-OBJECTQMGRNAME.        03340041
           MOVE CC-ITEM-QUEUE-NAME       TO MQOD-OBJECTNAME.            03350041
                                                                        03360041
           MOVE IMRD008-RECORD           TO MQV-MESSAGE-BUFFER.         03370047
           MOVE MQMI-NONE                TO MQMD-MSGID.                 03380043
           MOVE MQCI-NONE                TO MQMD-CORRELID.              03390040
110502*    MOVE MQFMT-STRING             TO MQMD-FORMAT.                03400052
           MOVE MQPER-PERSISTENT         TO MQMD-PERSISTENCE.           03410040
                                                                        03420042
           COMPUTE MQPMO-CONTEXT  =  MQV-PQ-HANDLE.                     03430042
           COMPUTE MQPMO-OPTIONS  =  MQPMO-SYNCPOINT.                   03440052
110502*                           +  MQPMO-SET-IDENTITY-CONTEXT.        03450052
                                                                        03460036
           CALL MQV-MQPUT USING MQV-HCONN                               03470040
                                MQV-PQ-HANDLE                           03480040
                                MQMD                                    03490041
                                MQPMO                                   03500041
                                MQV-MSG-LENGTH                          03510040
                                MQV-MESSAGE-BUFFER                      03520040
                                MQV-COMPLETION-CODE                     03530040
                                MQV-REASON.                             03540040
                                                                        03550036
           EVALUATE TRUE                                                03560036
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        03570040
                   ADD +1           TO PC-MESSAGES-PUT-ON-QUEUE         03580036
              WHEN MQV-COMPLETION-CODE NOT = MQCC-OK                    03590040
                   CALL MQV-MQCHECK USING MQV-REASON                    03600040
                                          MQV-REASON-TEXT               03610040
                   PERFORM 9999-BACK-OUT-MQ                             03620051
                   PERFORM 9999-DISCONNECT-QUEUE                        03630043
                   PERFORM 9999-MQ-ABEND                                03640040
           END-EVALUATE.                                                03650037
                                                                        03660036
       EJECT.                                                           03670036
      ******************************************************************03680040
      **COMMIT MQ PROCESSING EVERY 100 MESSAGES                         03690040
      ******************************************************************03700040
       2200-MQV-COMMIT.                                                 03710040
                                                                        03720040
           MOVE '2200-MQV-COMMIT' TO PV-PARAGRAPH-NAME.                 03730040
                                                                        03740040
           CALL MQV-MQCMIT USING MQV-HCONN                              03750040
                                 MQV-COMPLETION-CODE                    03760040
                                 MQV-REASON.                            03770040
                                                                        03780040
           EVALUATE TRUE                                                03790040
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        03800040
                   CONTINUE                                             03810040
              WHEN MQV-COMPLETION-CODE NOT = MQCC-OK                    03820040
                   CALL MQV-MQCHECK USING MQV-REASON                    03830040
                                          MQV-REASON-TEXT               03840040
                   PERFORM 9999-BACK-OUT-MQ                             03850040
                   PERFORM 9999-MQ-ABEND                                03860040
           END-EVALUATE.                                                03870040
                                                                        03880040
       EJECT.                                                           03890040
      ******************************************************************03900040
      * BACK OUT MQ PROCESSING SINCE THE POINT OF THE LAST COMMIT       03910040
      ******************************************************************03920040
       9999-BACK-OUT-MQ.                                                03930040
                                                                        03940040
           CALL MQV-MQBACK USING MQV-HCONN                              03950040
                                 MQV-COMPLETION-CODE                    03960040
                                 MQV-REASON.                            03970040
                                                                        03980040
           EVALUATE TRUE                                                03990040
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        04000040
                   CONTINUE                                             04010040
              WHEN MQV-COMPLETION-CODE NOT = MQCC-OK                    04020040
                   CALL MQV-MQCHECK USING MQV-REASON                    04030040
                                          MQV-REASON-TEXT               04040040
                   PERFORM 9999-MQ-ABEND                                04050040
           END-EVALUATE.                                                04060040
                                                                        04070040
       EJECT.                                                           04080040
      ******************************************************************04090036
      **READS NEXT MESSAGE FROM THE MESSAGE FILE                        04100036
      ******************************************************************04110036
       9999-READ-MSG-FILE.                                              04120036
                                                                        04130036
           MOVE '9999-READ-MSG-FILE' TO PV-PARAGRAPH-NAME.              04140036
                                                                        04150036
           READ MESSAGE-FILE INTO IMRD008-RECORD                        04160047
                AT END                                                  04170036
                   SET PS-NO-MSGS TO TRUE                               04180036
           END-READ.                                                    04190036
                                                                        04200036
           ADD +1                      TO PC-MSG-FILE-READ.             04210040
           ADD +1                      TO PC-COMMIT-COUNTER.            04220040
                                                                        04230036
       EJECT.                                                           04240036
      ***************************************************************** 04250036
      **READ CONTROL CARD 1                                             04260036
      ***************************************************************** 04270036
       9999-READ-CONTROL-CARD-1.                                        04280036
                                                                        04290036
           READ CONTROL-CARD-1 INTO CC-CONTROL-CARD-1-LAYOUT.           04300036
                                                                        04310036
       EJECT.                                                           04320036
      ***************************************************************** 04330036
      **READ CONTROL CARD 2                                             04340036
      ***************************************************************** 04350036
       9999-READ-CONTROL-CARD-2.                                        04360036
                                                                        04370036
           READ CONTROL-CARD-2 INTO CC-CONTROL-CARD-2-LAYOUT.           04380036
                                                                        04390036
       EJECT.                                                           04400036
      ****************************************************************  04410036
      *END OF PROGRAM ROUTINE                                           04420036
      ****************************************************************  04430036
       9999-WRAPUP.                                                     04440036
                                                                        04450036
           PERFORM 2200-MQV-COMMIT.                                     04460040
                                                                        04470040
           CLOSE CONTROL-CARD-1                                         04480036
                 CONTROL-CARD-2                                         04490036
                 MESSAGE-FILE.                                          04500036
                                                                        04510036
           PERFORM 9999-DISPLAY-STATISTICS.                             04520036
                                                                        04530036
       EJECT.                                                           04540036
      ****************************************************************  04550036
      *DISPLAY PROGRAM STATISTICS FOR THIS RUN                          04560036
      ****************************************************************  04570036
       9999-DISPLAY-STATISTICS.                                         04580036
                                                                        04590036
           DISPLAY '*********************************************'.     04600036
           DISPLAY 'PROGRAM NAME: ' PC-CURRENT-PROGRAM-NAME.            04610036
           DISPLAY '*********************************************'.     04620036
           DISPLAY 'MESSAGES PUT ON QUEUE '   PC-MESSAGES-PUT-ON-QUEUE. 04630036
           DISPLAY 'MESSAGES READ FROM LOG: ' PC-MSG-FILE-READ.         04640036
                                                                        04650036
       EJECT.                                                           04660036
      ****************************************************************  04670043
      *DISCONNECT FROM QUEUE IN THE EVENT OF ERROR OR END OF PROGRAM    04680043
      ****************************************************************  04690043
       9999-DISCONNECT-QUEUE.                                           04700043
                                                                        04710043
           CALL MQV-MQDISC USING MQV-HCONN                              04720043
                                 MQV-COMPLETION-CODE                    04730043
                                 MQV-REASON.                            04740043
                                                                        04750043
           EVALUATE TRUE                                                04760043
              WHEN MQV-COMPLETION-CODE = MQCC-OK                        04770043
                   CONTINUE                                             04780043
              WHEN MQV-COMPLETION-CODE NOT EQUAL MQCC-OK                04790043
                   DISPLAY 'UNABLE TO DISCONNECT FROM QUEUE'            04800043
                   PERFORM 9999-MQ-ABEND                                04810043
           END-EVALUATE.                                                04820043
                                                                        04830043
       EJECT.                                                           04840043
      ******************************************************************04850036
      **MQ ABEND ROUTINE                                                04860036
      ******************************************************************04870036
       9999-MQ-ABEND.                                                   04880040
                                                                        04890036
           DISPLAY '**** ABEND *************************************'.  04900036
           DISPLAY '** MQSERIES ERROR **'.                              04910036
           DISPLAY '** PROGRAM:    ' PC-CURRENT-PROGRAM-NAME.           04920036
           DISPLAY '** ABEND CODE: ' MQV-COMPLETION-CODE.               04930040
           DISPLAY '** PARAGRAPH:  ' PV-PARAGRAPH-NAME.                 04940036
           DISPLAY '** MESSAGE:    ' PV-OPERATION-MSG-1.                04950036
           DISPLAY '** COMP CODE:  ' MQV-COMPLETION-CODE.               04960040
           DISPLAY '** RSN CODE:   ' MQV-REASON.                        04970040
           DISPLAY '** RSN TEXT:   ' MQV-REASON-TEXT.                   04980040
           DISPLAY '************************************************'.  04990036
           MOVE +4013 TO ABEND-CODE.                                    05000036
           CALL 'ILBOABN0' USING ABEND-CODE.                            05010036
                                                                        05020036
       EJECT.                                                           05030036
      ******************************************************************05040036
      **SQL ABEND ROUTINE                                               05050036
      ******************************************************************05060036
       9999-SQL-ABEND-ROUTINE.                                          05070036
                                                                        05080036
           DISPLAY '** ABEND     **'.                                   05090036
           DISPLAY '** PROGRAM   ** = ' PC-CURRENT-PROGRAM-NAME.        05100036
           DISPLAY '** PARAGRAPH ** = ' PV-PARAGRAPH-NAME.              05110036
           DISPLAY '** OPERATION ** = ' PV-DB2-OPERATION.               05120036
      ******************************************************************05130036
      * COPY BOOK CONTAINING PROCEDURE DIVISION STATEMENTS REQUIRED     05140036
      * TO UTILIZE SQLCA MESSAGE DISPLAY MODULE 'DSNTIAR' FOLLOWS.      05150036
      ******************************************************************05160036
           COPY DPPD004.                                                05170055
           SET ABEND-4013-DB2-ERROR TO TRUE.                            05180036
           CALL 'ILBOABN0' USING ABEND-CODE.                            05190036
                                                                        05200036
       EJECT                                                            05210036
